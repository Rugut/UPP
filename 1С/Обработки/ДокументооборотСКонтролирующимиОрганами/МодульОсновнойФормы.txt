Процедура ПередОткрытием(Отказ, СтандартнаяОбработка)
	
	Отказ = Истина;
	
	Если Метаданные.Обработки.Содержит(Метаданные()) Тогда // это обработка из конфигурации
		Предупреждение("Обработка не предназначена для интерактивного открытия!");
	Иначе
		
		// получаем свойства зарегистрированного в ИБ модуля документооборота
		Свойства = ПолучитьСвойстваМодуляДокументооборота();
		Если Свойства = Неопределено ИЛИ НЕ ИзменениеСвойствМодуляДокументооборотаВозможно()Тогда
			Предупреждение("У Вас недостаточно прав для регистрации внешнего модуля в информационной базе!");
		Иначе
			
			ИспользоватьВнешнийМодуль = Свойства.ИспользоватьВнешнийМодуль;
			
			ВнешнийМодуль = Свойства.ВнешнийМодуль;
			ВнешнийМодульДвДанные = ВнешнийМодуль.Получить();
			
			Если ВнешнийМодульДвДанные = Неопределено Тогда
				ДанныйВнешнийМодульУжеЗагружен = Ложь;
			Иначе
				
				// если в ИБ уже загружен модуль, то сравниваем его с текущим
				ВнешнийМодульФайл = ПолучитьИмяВременногоФайла();
				ВнешнийМодульДвДанные.Записать(ВнешнийМодульФайл);
				
				ОбъектСравнение = Новый СравнениеФайлов;
				ОбъектСравнение.ПервыйФайл = ЭтотОбъект.ИспользуемоеИмяФайла;
				ОбъектСравнение.ВторойФайл = ВнешнийМодульФайл;
				ОбъектСравнение.СпособСравнения = СпособСравненияФайлов.Двоичное;
				ДанныйВнешнийМодульУжеЗагружен = ОбъектСравнение.Сравнить();
				
				УдалитьФайлы(ВнешнийМодульФайл);
				
			КонецЕсли;
			
			// если текущий модуль уже зарегистрирован в ИБ и используется, то информируем об этом пользователя
			Если ДанныйВнешнийМодульУжеЗагружен И ИспользоватьВнешнийМодуль Тогда
				Предупреждение("Данный модуль уже зарегистрирован в информационной базе!");
			Иначе // модули различаются
				
				// если текущий модуль не загружен и ИБ или загружен, но не используется, то
				// предлагаем пользователю зарегистрировать/включить его
				Если НЕ ИспользоватьВнешнийМодуль И ДанныйВнешнийМодульУжеЗагружен Тогда
					ТекстВопроса = "Данный модуль уже загружен в информационную базу, но его использование отключено.
									|Хотите включить использование модуля?";
				Иначе //Если ИспользоватьВнешнийМодуль И НЕ ДанныйВнешнийМодульУжеЗагружен Тогда
					ТекстВопроса = "Зарегистрировать модуль в информационной базе?";
				КонецЕсли;
				
				Ответ = Вопрос(ТекстВопроса, РежимДиалогаВопрос.ДаНет, , , "Модуль защищенного документооборота с ФНС");
				Если Ответ = КодВозвратаДиалога.Да Тогда
					ОписаниеОшибки = "";
					Если ДанныйВнешнийМодульУжеЗагружен Тогда
						Результат = ПрименитьСвойстваМодуляДокументооборота(Истина, , ОписаниеОшибки);
					Иначе
						Результат = ПрименитьСвойстваМодуляДокументооборота(Истина, ЭтотОбъект.ИспользуемоеИмяФайла, ОписаниеОшибки);
					КонецЕсли;
					Если Результат Тогда
						Если НЕ ИспользоватьВнешнийМодуль И ДанныйВнешнийМодульУжеЗагружен Тогда
							ТекстВопроса = "Использование модуля включено.
											|
											|Изменения вступят в силу только после повторного открытия программы!
											|Перезапустить программу сейчас?";
						Иначе
							ТекстВопроса = "Модуль успешно зарегистрирован.
											|
											|Изменения вступят в силу только после повторного открытия программы!
											|Перезапустить программу сейчас?";
						КонецЕсли;
						Ответ = Вопрос(ТекстВопроса, РежимДиалогаВопрос.ДаНет);
						Если Ответ = КодВозвратаДиалога.Да Тогда
							ЗавершитьРаботуСистемы(Истина, Истина);
						КонецЕсли;
					Иначе
						Предупреждение("Не удалось зарегистрировать модуль:
										|" + ОписаниеОшибки);
					КонецЕсли;
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры