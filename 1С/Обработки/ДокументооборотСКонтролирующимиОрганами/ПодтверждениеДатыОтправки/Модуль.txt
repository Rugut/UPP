Процедура ОсновныеДействияФормыЗакрыть(Кнопка)
	
	Закрыть();
	
КонецПроцедуры

Процедура ПередОткрытием(Отказ, СтандартнаяОбработка)
	
	Если НЕ ЗначениеЗаполнено(Сообщение) Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	
	// извлекаем файл подтверждения даты отправки из содержимого сообщения
	СтрПодтверждения = КонтекстЭДО.ПолучитьВложенияТранспортногоСообщения(Сообщение, Истина, Перечисления.ТипыСодержимогоТранспортногоКонтейнера.ПодтверждениеДатыОтправки, ИмяФайлаПодтверждения);
	Если СтрПодтверждения.Количество() = 0 Тогда
		Предупреждение("Подтверждение даты отправки не обнаружено среди содержимого сообщения.");
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	СтрПодтверждение = СтрПодтверждения[0];
	
	// записываем вложение во временный файл
	ФайлПодтверждения = ПолучитьИмяВременногоФайла("xml");
	Попытка
		СтрПодтверждение.Данные.Получить().Записать(ФайлПодтверждения);
	Исключение
		Сообщить("Ошибка выгрузки подтверждения даты отправки во временный файл:" + Символы.ПС + ИнформацияОбОшибке().Описание, СтатусСообщения.Важное);
		Отказ = Истина;
		Возврат;
	КонецПопытки;
	
	// считываем подтверждение из файла в дерево XML
	ОписаниеОшибки = "";
	ДеревоXML = КонтекстЭДО.ЗагрузитьXMLВДеревоЗначений(ФайлПодтверждения, , ОписаниеОшибки);
	КонтекстЭДО.УдалитьВременныйФайл(ФайлПодтверждения);
	Если НЕ ЗначениеЗаполнено(ДеревоXML) Тогда
		Сообщить("Ошибка чтения XML из файла подтверждения даты отправки:" + Символы.ПС + ИнформацияОбОшибке().Описание, СтатусСообщения.Важное);
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	
	УзелФайл = ДеревоXML.Строки.Найти("Файл", "Имя");
	Если НЕ ЗначениеЗаполнено(УзелФайл) Тогда
		Сообщить("Некорректная структура XML подтверждения: не обнаружен узел ""Файл"".", СтатусСообщения.Важное);
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	УзелДокумент = УзелФайл.Строки.Найти("Документ", "Имя");
	Если НЕ ЗначениеЗаполнено(УзелДокумент) Тогда
		Сообщить("Некорректная структура XML подтверждения: не обнаружен узел ""Документ"".", СтатусСообщения.Важное);
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	УзелОргПодт = УзелДокумент.Строки.Найти("ОргПодт", "Имя");
	Если НЕ ЗначениеЗаполнено(УзелОргПодт) Тогда
		Сообщить("Некорректная структура XML подтверждения: не обнаружен узел ""ОргПодт"".", СтатусСообщения.Важное);
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	// получаем сведения об организации, подтвердившей дату отправки
	УзелСпецОперат = УзелОргПодт.Строки.Найти("СпецОперат", "Имя");
	УзелНО = УзелОргПодт.Строки.Найти("НО", "Имя");
	Если НЕ ЗначениеЗаполнено(УзелСпецОперат) И НЕ ЗначениеЗаполнено(УзелНО) Тогда
		Сообщить("Некорректная структура XML подтверждения: отсутствуют сведения об организации, подтвердившей дату отправки.", СтатусСообщения.Важное);
		Отказ = Истина;
		Возврат;
	ИначеЕсли ЗначениеЗаполнено(УзелСпецОперат) Тогда
		УзелНаимОрг = УзелСпецОперат.Строки.Найти("НаимОрг", "Имя");
		Если НЕ ЗначениеЗаполнено(УзелНаимОрг) Тогда
			Сообщить("Некорректная структура XML подтверждения: отсутствуют сведения об организации, подтвердившей дату отправки.", СтатусСообщения.Важное);
			Отказ = Истина;
			Возврат;
		КонецЕсли;
		ОрганизацияСтр = СокрЛП(УзелНаимОрг.Значение);
	ИначеЕсли ЗначениеЗаполнено(УзелНО) Тогда
		УзелКодНО = УзелНО.Строки.Найти("КодНО", "Имя");
		УзелНаимНО = УзелНО.Строки.Найти("НаимНО", "Имя");
		Если ЗначениеЗаполнено(УзелКодНО) И ЗначениеЗаполнено(УзелКодНО.Значение) Тогда
			ОрганизацияСтр = СокрЛП(УзелКодНО.Значение);
			Если ЗначениеЗаполнено(УзелНаимНО) И ЗначениеЗаполнено(УзелНаимНО.Значение) Тогда
				ОрганизацияСтр = ОрганизацияСтр + " (" + СокрЛП(УзелНаимНО.Значение) + ")";
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	// получаем сведения подтверждения
	УзелСведПодтв = УзелДокумент.Строки.Найти("СведПодтв", "Имя");
	Если НЕ ЗначениеЗаполнено(УзелСведПодтв) Тогда
		Сообщить("Некорректная структура XML подтверждения: отсутствуют сведения подтверждения.", СтатусСообщения.Важное);
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	// получаем сведения об имени поступившего файла
	УзелИмяПостФайла = УзелСведПодтв.Строки.Найти("ИмяПостФайла", "Имя", Истина);
	Если ЗначениеЗаполнено(УзелИмяПостФайла) Тогда
		ИмяПоступившегоФайла = СокрЛП(УзелИмяПостФайла.Значение);
	КонецЕсли;
	
	// получаем сведения о дате и времени поступившего файла
	УзелДатаОтпр = УзелСведПодтв.Строки.Найти("ДатаОтпр", "Имя");
	УзелВремяОтпр = УзелСведПодтв.Строки.Найти("ВремяОтпр", "Имя");
	Если ЗначениеЗаполнено(УзелДатаОтпр) Тогда
		ДатаОтправки = СокрЛП(УзелДатаОтпр.Значение);
	КонецЕсли;
	Если ЗначениеЗаполнено(УзелВремяОтпр) Тогда
		ДатаОтправки = СокрЛП(ДатаОтправки + " " + СокрЛП(УзелВремяОтпр.Значение));
	КонецЕсли;
	
КонецПроцедуры

Процедура ОрганизацияСтрОчистка(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры