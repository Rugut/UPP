Процедура ОсновныеДействияФормыЗакрыть(Кнопка)
	
	Закрыть();
	
КонецПроцедуры

Процедура ПередОткрытием(Отказ, СтандартнаяОбработка)
	
	Если НЕ ЗначениеЗаполнено(Сообщение) Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	
	// извлекаем файл квитанции из содержимого сообщения
	СтрКвитанции = КонтекстЭДО.ПолучитьВложенияТранспортногоСообщения(Сообщение, Истина, Перечисления.ТипыСодержимогоТранспортногоКонтейнера.КвитанцияОПриеме, ИмяФайлаКвитанции);
	Если СтрКвитанции.Количество() = 0 Тогда
		Предупреждение("Квитанция о приеме не обнаружена среди содержимого сообщения.");
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	СтрКвитанция = СтрКвитанции[0];
	
	// записываем вложение во временный файл
	ФайлКвитанции = ПолучитьИмяВременногоФайла("xml");
	Попытка
		СтрКвитанция.Данные.Получить().Записать(ФайлКвитанции);
	Исключение
		Сообщить("Ошибка выгрузки квитанции о приеме во временный файл:" + Символы.ПС + ИнформацияОбОшибке().Описание, СтатусСообщения.Важное);
		Отказ = Истина;
		Возврат;
	КонецПопытки;
	
	// считываем квитанцию из файла в дерево XML
	ОписаниеОшибки = "";
	ДеревоXML = КонтекстЭДО.ЗагрузитьXMLВДеревоЗначений(ФайлКвитанции, , ОписаниеОшибки);
	КонтекстЭДО.УдалитьВременныйФайл(ФайлКвитанции);
	Если НЕ ЗначениеЗаполнено(ДеревоXML) Тогда
		Сообщить("Ошибка чтения XML из файла квитанции о приеме:" + Символы.ПС + ИнформацияОбОшибке().Описание, СтатусСообщения.Важное);
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	
	// анализируем XML
	УзелФайл = ДеревоXML.Строки.Найти("Файл", "Имя");
	Если НЕ ЗначениеЗаполнено(УзелФайл) Тогда
		Сообщить("Некорректная структура XML квитанции: не обнаружен узел ""Файл"".", СтатусСообщения.Важное);
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	УзелДокумент = УзелФайл.Строки.Найти("Документ", "Имя");
	Если НЕ ЗначениеЗаполнено(УзелДокумент) Тогда
		Сообщить("Некорректная структура XML квитанции: не обнаружен узел ""Документ"".", СтатусСообщения.Важное);
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	УзелСвКвит = УзелДокумент.Строки.Найти("СвКвит", "Имя");
	Если НЕ ЗначениеЗаполнено(УзелСвКвит) Тогда
		Сообщить("Некорректная структура XML квитанции: не обнаружен узел ""СвКвит"".", СтатусСообщения.Важное);
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	// разбираем узел с общими сведениями
	ОбщиеСведения = Новый Структура;
	Для Каждого УзелОбщСвед Из УзелСвКвит.Строки Цикл
		ОбщиеСведения.Вставить(УзелОбщСвед.Имя, СокрЛП(УзелОбщСвед.Значение));
	КонецЦикла;
	
	Если ОбщиеСведения.Свойство("ИмяОбрабФайла") Тогда
		ИмяОбрабФайла = ОбщиеСведения.ИмяОбрабФайла;
	КонецЕсли;
	
	Если ОбщиеСведения.Свойство("ИдФайл") Тогда
		ИдФайл = ОбщиеСведения.ИдФайл;
	КонецЕсли;
	
	Если ОбщиеСведения.Свойство("ИдДок") Тогда
		ИдДок = ОбщиеСведения.ИдДок;
	КонецЕсли;
	
	Если ОбщиеСведения.Свойство("ДатаПредст") Тогда
		ДатаВремяПредст = ОбщиеСведения.ДатаПредст;
	КонецЕсли;
	
	Если ОбщиеСведения.Свойство("ВремПредст") Тогда
		ДатаВремяПредст = СокрЛП(ДатаВремяПредст + " " + ОбщиеСведения.ВремПредст);
	КонецЕсли;
	
	Если ОбщиеСведения.Свойство("КНД") Тогда
		КНД = ОбщиеСведения.КНД;
	КонецЕсли;
	
	Если ОбщиеСведения.Свойство("НаимОтч") Тогда
		НаимОтч = ОбщиеСведения.НаимОтч;
	КонецЕсли;
	
	Если ОбщиеСведения.Свойство("НомКорр") Тогда
		НомКорр = ОбщиеСведения.НомКорр;
	КонецЕсли;
	
	Если ОбщиеСведения.Свойство("Период") Тогда
		ОтчетныйПериод = ОбщиеСведения.Период;
	КонецЕсли;
	
	Если ОбщиеСведения.Свойство("ПериодНаим") Тогда
		ПериодНаим = СокрЛП(ОбщиеСведения.ПериодНаим);
		Если ЗначениеЗаполнено(ОтчетныйПериод) Тогда
			ОтчетныйПериод = ОтчетныйПериод + " (" + ПериодНаим + ")";
		Иначе
			ОтчетныйПериод = ПериодНаим;
		КонецЕсли;
	КонецЕсли;
	
	Если ОбщиеСведения.Свойство("ОтчетГод") Тогда
		ОтчетГод = ОбщиеСведения.ОтчетГод;
	КонецЕсли;
	
	Если ОбщиеСведения.Свойство("ДатаПост") Тогда
		ДатаПост = ОбщиеСведения.ДатаПост;
	КонецЕсли;
	
	Если ОбщиеСведения.Свойство("ДатаПрин") Тогда
		ДатаПрин = ОбщиеСведения.ДатаПрин;
	КонецЕсли;
	
	Если ОбщиеСведения.Свойство("РегНом") Тогда
		РегНом = ОбщиеСведения.РегНом;
	КонецЕсли;
	
	Если ОбщиеСведения.Свойство("ПрогрКомпл") Тогда
		ПрогрКомпл = ОбщиеСведения.ПрогрКомпл;
	КонецЕсли;
	
	// разбираем сведения о налоговом органе
	УзелСвНалОргПост = УзелДокумент.Строки.Найти("СвНалОргПост", "Имя");
	Если НЕ ЗначениеЗаполнено(УзелСвНалОргПост) Тогда
		Сообщить("Некорректная структура XML квитанции: не обнаружен узел ""СвНалОргПост"".", СтатусСообщения.Важное);
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	УзелКодНО = УзелСвНалОргПост.Строки.Найти("КодНО", "Имя");
	Если ЗначениеЗаполнено(УзелКодНО) Тогда
		НалоговыйОрган = СокрЛП(УзелКодНО.Значение);
	КонецЕсли;
	
	УзелНаимНО = УзелСвНалОргПост.Строки.Найти("НаимНО", "Имя");
	Если ЗначениеЗаполнено(УзелНаимНО) Тогда
		Если ЗначениеЗаполнено(НалоговыйОрган) Тогда
			НаименованиеНО = СокрЛП(УзелНаимНО.Значение);
			Если НалоговыйОрган <> НаименованиеНО Тогда
				НалоговыйОрган = НалоговыйОрган + " (" + НаименованиеНО + ")";
			КонецЕсли;
		Иначе
			НалоговыйОрган = СокрЛП(УзелНаимНО.Значение);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПолеДанныхОчистка(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры