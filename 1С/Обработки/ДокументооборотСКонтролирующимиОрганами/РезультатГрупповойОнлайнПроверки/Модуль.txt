Перем ЦветГиперссылки;
Перем ПредставлениеРезультатовПроверки;
Перем КрасныйЦвет;

Функция ЗагрузитьТекст(ИмяФайла, Кодировка = Неопределено)
	
	ОбъектЧтение = Новый ЧтениеТекста(ИмяФайла);
	ТекстРезультат = ОбъектЧтение.Прочитать();
	ОбъектЧтение.Закрыть();
	Возврат ТекстРезультат;
	
КонецФункции

Процедура ТаблицаРезультатовПриПолученииДанных(Элемент, ОформленияСтрок)
	
	Для Каждого ОформлениеСтроки Из ОформленияСтрок Цикл
		
		// прорисовываем колонку Результат
		КодРезультата = ОформлениеСтроки.ДанныеСтроки.РезультатПроверки;
		ПредставлениеРезультата = ПредставлениеРезультатовПроверки[КодРезультата];
		ОформлениеСтроки.Ячейки.РезультатПроверки.УстановитьТекст(ПредставлениеРезультата);
		Если КодРезультата <> "0" Тогда
			ОформлениеСтроки.Ячейки.РезультатПроверки.ЦветТекста = КрасныйЦвет;
		КонецЕсли;
		
		// прорисовываем колонку Дополнительно
		Если ЗначениеЗаполнено(ОформлениеСтроки.ДанныеСтроки.ИмяФайлаПротокола) Тогда
			ОформлениеСтроки.Ячейки.Дополнительно.ЦветТекста = ЦветГиперссылки;
			ОформлениеСтроки.Ячейки.Дополнительно.Гиперссылка = Истина;
			ОформлениеСтроки.Ячейки.Дополнительно.УстановитьТекст("Показать протокол");
		Иначе
			ОформлениеСтроки.Ячейки.Дополнительно.ЦветТекста = Новый Цвет(127, 127, 127);
			ОформлениеСтроки.Ячейки.Дополнительно.УстановитьТекст("Протокол недоступен");
		КонецЕсли;
		
		// прорисовываем регламентированный отчет
		Если ТипЗнч(ОформлениеСтроки.ДанныеСтроки.Документ) = Тип("ДокументСсылка.РегламентированныйОтчет") Тогда
			ОформлениеСтроки.Ячейки.Документ.УстановитьТекст(РегламентированнаяОтчетность.ПредставлениеДокументаРеглОтч(ОформлениеСтроки.ДанныеСтроки.Документ));
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ТаблицаРезультатовВыбор(Элемент, ВыбраннаяСтрока, Колонка, СтандартнаяОбработка)
	
	Если Колонка.Имя = "Документ" И ТипЗнч(ВыбраннаяСтрока.Документ) <> Тип("Строка") Тогда
		ОткрытьЗначение(ВыбраннаяСтрока.Документ);
	ИначеЕсли ЗначениеЗаполнено(ВыбраннаяСтрока.ИмяФайлаПротокола) Тогда
		ПоказатьПротокол(ВыбраннаяСтрока);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПоказатьПротокол(Стр)
	
	КонтекстЭДО.ПоказатьПротоколОнлайнПроверки(Стр.Документ, Стр.ИмяФайлаОтчета, ЗагрузитьТекст(Стр.ПолноеИмяФайлаВыгрузки), ЗагрузитьТекст(Стр.ИмяФайлаПротокола), Стр.ИсходноеИмяФайлаПротокола);
	
КонецПроцедуры

ЦветГиперссылки = Новый Цвет(0, 0, 255);
КрасныйЦвет = Новый Цвет(255, 0, 0);

ПредставлениеРезультатовПроверки = Новый Соответствие;
ПредставлениеРезультатовПроверки.Вставить("-1",	"Неизвестная ошибка");
ПредставлениеРезультатовПроверки.Вставить("0",	"Проверка пройдена");
ПредставлениеРезультатовПроверки.Вставить("1",	"Ошибка расшифровки");
ПредставлениеРезультатовПроверки.Вставить("2",	"Неизвестный формат");
ПредставлениеРезультатовПроверки.Вставить("3",	"Файл отчета поврежден");
ПредставлениеРезультатовПроверки.Вставить("4",	"Обнаружены ошибки");
ПредставлениеРезультатовПроверки.Вставить("5",	"Проверка не выполнена");
ПредставлениеРезультатовПроверки.Вставить("6",	"Обнаружены ошибки");