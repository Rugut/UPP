Перем мЖирныйШрифт;
Перем мНеЖирныйШрифт;

Процедура ПередОткрытием(Отказ, СтандартнаяОбработка)
	
	Сообщение = ВладелецФормы.ЭтотОбъект;
	
	Если Сообщение.Статус <> Перечисления.СтатусыПисем.Отправленное И Сообщение.Статус <> Перечисления.СтатусыПисем.Полученное Тогда
		Предупреждение("Дополнительные свойства доступны только для отправленных и полученных писем.");
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	Если НЕ ПустаяСтрока(Сообщение.ОтКогоАдрес) ИЛИ НЕ ПустаяСтрока(Сообщение.ОтКогоПредставление) Тогда
		НовСтр = Дополнительно.Строки.Добавить();
		НовСтр.Имя = "От кого";
		НовСтр2 = НовСтр.Строки.Добавить();
		НовСтр2.Имя = Сообщение.ОтКогоАдрес;
		НовСтр2.Значение = Сообщение.ОтКогоПредставление;
	КонецЕсли;
	
	Если Сообщение.Кому.Количество() <> 0 Тогда
		НовСтр = Дополнительно.Строки.Добавить();
		НовСтр.Имя = "Кому";
		Для Каждого ЭлКому Из Сообщение.Кому Цикл
			НовСтр2 = НовСтр.Строки.Добавить();
			НовСтр2.Имя = ЭлКому.АдресЭлектроннойПочты;
			НовСтр2.Значение = ЭлКому.Представление;
		КонецЦикла;
	КонецЕсли;
	
	Если Сообщение.Копии.Количество() <> 0 Тогда
		НовСтр = Дополнительно.Строки.Добавить();
		НовСтр.Имя = "Копии";
		Для Каждого ЭлКопии Из Сообщение.Копии Цикл
			НовСтр2 = НовСтр.Строки.Добавить();
			НовСтр2.Имя = ЭлКопии.АдресЭлектроннойПочты;
			НовСтр2.Значение = ЭлКопии.Представление;
		КонецЦикла;
	КонецЕсли;
	
	Если Сообщение.СкрытыеКопии.Количество() <> 0 Тогда
		НовСтр = Дополнительно.Строки.Добавить();
		НовСтр.Имя = "Скрытые копии";
		Для Каждого ЭлСкрытыеКопии Из Сообщение.Копии Цикл
			НовСтр2 = НовСтр.Строки.Добавить();
			НовСтр2.Имя = ЭлСкрытыеКопии.АдресЭлектроннойПочты;
			НовСтр2.Значение = ЭлСкрытыеКопии.Представление;
		КонецЦикла;
	КонецЕсли;
	
	НовСтр = Дополнительно.Строки.Добавить();
	НовСтр.Имя = "Прочее";
	
	НовСтр2 = НовСтр.Строки.Добавить();
	НовСтр2.Имя = "Идентификатор сообщения";
	НовСтр2.Значение = Сообщение.ИдентификаторСообщения;
	
	//НовСтр2 = НовСтр.Строки.Добавить();
	//НовСтр2.Имя = "Важность";
	//НовСтр2.Значение = Сообщение.Важность;
	
	НовСтр2 = НовСтр.Строки.Добавить();
	НовСтр2.Имя = "Кодировка";
	НовСтр2.Значение = Сообщение.Кодировка;
	
	НовСтр2 = НовСтр.Строки.Добавить();
	НовСтр2.Имя = "Тема";
	НовСтр2.Значение = Сообщение.Тема;
	
	НовСтр2 = НовСтр.Строки.Добавить();
	НовСтр2.Имя = "Текст письма";
	НовСтр2.Значение = Сообщение.ТекстПисьма;
	
	Если Сообщение.ДополнительныеРеквизитыЗаголовка.Количество() <> 0 Тогда
		НовСтр = Дополнительно.Строки.Добавить();
		НовСтр.Имя = "Дополнительные реквизиты";
		Для Каждого ЭлДопРеквизиты Из Сообщение.ДополнительныеРеквизитыЗаголовка Цикл
			НовСтр2 = НовСтр.Строки.Добавить();
			НовСтр2.Имя = ЭлДопРеквизиты.Тип;
			НовСтр2.Значение = ЭлДопРеквизиты.Значение;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

Процедура ДополнительноПриПолученииДанных(Элемент, ОформленияСтрок)
	
	Для Каждого ОформлениеСтроки из ОформленияСтрок Цикл
		Если ОформлениеСтроки.ДанныеСтроки.Уровень() = 0 Тогда
			ОформлениеСтроки.Шрифт = мЖирныйШрифт;
		Иначе
			ОформлениеСтроки.Шрифт = мНеЖирныйШрифт;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Процедура ДополнительноПередСворачиванием(Элемент, Строка, Отказ)
	
	Отказ = Истина;
	
КонецПроцедуры

мЖирныйШрифт = Новый Шрифт( , , Истина);
мНеЖирныйШрифт = Новый Шрифт;