Процедура ОсновныеДействияФормыЗакрыть(Кнопка)
	
	Закрыть();
	
КонецПроцедуры

Процедура ПередОткрытием(Отказ, СтандартнаяОбработка)
	
	Если НЕ ЗначениеЗаполнено(Сообщение) Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	
	// извлекаем файл извещения из содержимого сообщения
	СтрИзвещения = КонтекстЭДО.ПолучитьВложенияТранспортногоСообщения(Сообщение, Истина, Перечисления.ТипыСодержимогоТранспортногоКонтейнера.ИзвещениеОВводе, ИмяФайлаИзвещения);
	Если СтрИзвещения.Количество() = 0 Тогда
		Предупреждение("Извещение о вводе не обнаружено среди содержимого сообщения.");
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	СтрИзвещение = СтрИзвещения[0];
	
	// записываем вложение во временный файл
	ФайлИзвещения = ПолучитьИмяВременногоФайла("xml");
	Попытка
		СтрИзвещение.Данные.Получить().Записать(ФайлИзвещения);
	Исключение
		Сообщить("Ошибка выгрузки извещения о вводе во временный файл:" + Символы.ПС + ИнформацияОбОшибке().Описание, СтатусСообщения.Важное);
		Отказ = Истина;
		Возврат;
	КонецПопытки;
	
	// считываем извещение из файла в дерево XML
	ОписаниеОшибки = "";
	ДеревоXML = КонтекстЭДО.ЗагрузитьXMLВДеревоЗначений(ФайлИзвещения, , ОписаниеОшибки);
	КонтекстЭДО.УдалитьВременныйФайл(ФайлИзвещения);
	Если НЕ ЗначениеЗаполнено(ДеревоXML) Тогда
		Сообщить("Ошибка чтения XML из файла извещения о вводе:" + Символы.ПС + ИнформацияОбОшибке().Описание, СтатусСообщения.Важное);
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	
	// анализируем XML
	УзелФайл = ДеревоXML.Строки.Найти("Файл", "Имя");
	Если НЕ ЗначениеЗаполнено(УзелФайл) Тогда
		Сообщить("Некорректная структура XML извещения: не обнаружен узел ""Файл"".", СтатусСообщения.Важное);
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	УзелДокумент = УзелФайл.Строки.Найти("Документ", "Имя");
	Если НЕ ЗначениеЗаполнено(УзелДокумент) Тогда
		Сообщить("Некорректная структура XML извещения: не обнаружен узел ""Документ"".", СтатусСообщения.Важное);
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	УзелСвИзвещВ = УзелДокумент.Строки.Найти("СвИзвещВ", "Имя");
	Если НЕ ЗначениеЗаполнено(УзелСвИзвещВ) Тогда
		Сообщить("Некорректная структура XML извещения: не обнаружен узел ""СвИзвещВ"".", СтатусСообщения.Важное);
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	// разбираем узел с общими сведениями
	ОбщиеСведения = Новый Структура;
	Для Каждого УзелОбщСвед Из УзелСвИзвещВ.Строки Цикл
		ОбщиеСведения.Вставить(УзелОбщСвед.Имя, СокрЛП(УзелОбщСвед.Значение));
	КонецЦикла;
	
	Если ОбщиеСведения.Свойство("ИмяОбрабФайла") Тогда
		ИмяОбрабФайла = ОбщиеСведения.ИмяОбрабФайла;
	КонецЕсли;
	
	Если ОбщиеСведения.Свойство("ИдФайл") Тогда
		ИдФайл = ОбщиеСведения.ИдФайл;
	КонецЕсли;
	
	Если ОбщиеСведения.Свойство("ИдДок") Тогда
		ИдДок = ОбщиеСведения.ИдДок;
	КонецЕсли;
	
	Если ОбщиеСведения.Свойство("КНД") Тогда
		КНД = ОбщиеСведения.КНД;
	КонецЕсли;
	
	Если ОбщиеСведения.Свойство("НаимОтч") Тогда
		НаимОтч = ОбщиеСведения.НаимОтч;
	КонецЕсли;
	
	Если ОбщиеСведения.Свойство("НомКорр") Тогда
		НомКорр = ОбщиеСведения.НомКорр;
	КонецЕсли;
	
	Если ОбщиеСведения.Свойство("Период") Тогда
		ОтчетныйПериод = ОбщиеСведения.Период;
	КонецЕсли;
	
	Если ОбщиеСведения.Свойство("ПериодНаим") Тогда
		ПериодНаим = СокрЛП(ОбщиеСведения.ПериодНаим);
		Если ЗначениеЗаполнено(ОтчетныйПериод) Тогда
			ОтчетныйПериод = ОтчетныйПериод + " (" + ПериодНаим + ")";
		Иначе
			ОтчетныйПериод = ПериодНаим;
		КонецЕсли;
	КонецЕсли;
	
	Если ОбщиеСведения.Свойство("ОтчетГод") Тогда
		ОтчетГод = ОбщиеСведения.ОтчетГод;
	КонецЕсли;
	
	// разбираем сведения о налоговом органе
	УзелКодНоОтпр = УзелДокумент.Строки.Найти("КодНоОтпр", "Имя");
	Если НЕ ЗначениеЗаполнено(УзелКодНоОтпр) Тогда
		Сообщить("Некорректная структура XML извещения: не обнаружен узел ""КодНоОтпр"".", СтатусСообщения.Важное);
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	НалоговыйОрган = СокрЛП(УзелКодНоОтпр.Значение);
	
КонецПроцедуры

Процедура ПолеДанныхОчистка(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры