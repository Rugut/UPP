Процедура ДействияФормыВыгрузитьВФайл(Кнопка)
	
	ТекСтроки = ЭлементыФормы.РегистрСведенийСписок.ВыделенныеСтроки;
	Если ТекСтроки.Количество() <> 0 Тогда
		
		Диалог = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.ВыборКаталога);
		Диалог.Заголовок = "Выберите каталог для сохранения";
		Если Диалог.Выбрать() Тогда
			
			Запрос = Новый Запрос;
			Запрос.Текст = Запрос.Текст + "ВЫБРАТЬ РАЗРЕШЕННЫЕ
			                      |	СодержимоеТранспортныхКонтейнеров.ТранспортноеСообщение,
			                      |	СодержимоеТранспортныхКонтейнеров.ИмяФайла,
			                      |	СодержимоеТранспортныхКонтейнеров.Данные,
			                      |	СодержимоеТранспортныхКонтейнеров.Тип
			                      |ИЗ
			                      |	РегистрСведений.СодержимоеТранспортныхКонтейнеров КАК СодержимоеТранспортныхКонтейнеров
								  |ГДЕ";
			НомерСтроки = 1;
			Для Каждого ТекСтрока Из ТекСтроки Цикл
				ИмяПпараметраСообщение = "Сообщение" + Формат(НомерСтроки, "ЧГ=");
				ИмяПараметраИмяФайла = "ИмяФайла" + Формат(НомерСтроки, "ЧГ=");
				Запрос.Текст = Запрос.Текст + "
								  | " + ?(НомерСтроки = 1, "", " ИЛИ ") + "(СодержимоеТранспортныхКонтейнеров.ТранспортноеСообщение = &" + ИмяПпараметраСообщение
								  +	" И СодержимоеТранспортныхКонтейнеров.ИмяФайла = &" + ИмяПараметраИмяФайла + ")";
				Запрос.УстановитьПараметр(ИмяПпараметраСообщение, ТекСтрока.ТранспортноеСообщение);
				Запрос.УстановитьПараметр(ИмяПараметраИмяФайла, ТекСтрока.ИмяФайла);
				НомерСтроки = НомерСтроки + 1;
			КонецЦикла;
			
			КоличествоОшибок = 0;
			КаталогСохранения = ?(Прав(Диалог.Каталог, 1) = "\", Диалог.Каталог, Диалог.Каталог + "\");
			Выборка = Запрос.Выполнить().Выбрать();
			Пока Выборка.Следующий() Цикл
				КороткоеИмяФайла = Выборка.ИмяФайла;
				ПолноеИмяСохраняемогоФайла = КаталогСохранения + КороткоеИмяФайла;
				ОбъектСохраняемыйФайл = Новый Файл(ПолноеИмяСохраняемогоФайла);
				Если ОбъектСохраняемыйФайл.Существует() Тогда
					Ответ = Вопрос("В выбранном каталоге уже существует файл " + КороткоеИмяФайла + ".
									|Заменить указанный файл?", РежимДиалогаВопрос.ДаНетОтмена);
					Если Ответ = КодВозвратаДиалога.Нет Тогда
						Продолжить;					
					ИначеЕсли Ответ = КодВозвратаДиалога.Отмена Тогда
						Возврат;
					КонецЕсли;
				КонецЕсли;
				Попытка
					Выборка.Данные.Получить().Записать(ПолноеИмяСохраняемогоФайла);
				Исключение
					Сообщить("Во время сохранения файла " + КороткоеИмяФайла + " возникла ошибка:" + Символы.ПС + ОписаниеОшибки(), СтатусСообщения.Важное);
					КоличествоОшибок = КоличествоОшибок + 1;
				КонецПопытки;
			КонецЦикла;
			
			Если ТекСтроки.Количество() = 1 Тогда
				Если КоличествоОшибок = 0 Тогда
					Предупреждение("Файл успешно выгружен.");
				КонецЕсли;
			Иначе
				Если КоличествоОшибок = 0 Тогда
					Предупреждение("Файлы успешно выгружены.");
				Иначе
					Предупреждение("Во время сохранения произошли ошибки. Не удалось сохранить файлов: " + КоличествоОшибок + ".");
				КонецЕсли;
			КонецЕсли;
			
		КонецЕсли;
		
	Иначе
		
		Предупреждение("Выберите файлы, которые следует сохранить на диск, и повторите попытку.
		|Для множественного выбора используйте клавишу Ctrl.");
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередОткрытием(Отказ, СтандартнаяОбработка)
	
	ЭлементыФормы.РегистрСведенийСписок.ТолькоПросмотр = НЕ КонтекстЭДО.ТекущийПользовательЯвляетсяАдминистраторомУчетныхЗаписейДокументооборота();
	
КонецПроцедуры

Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "Опрос форм" И Параметр.Ключ = "РегистрСведений_СодержимоеТранспортныхКонтейнеров_ФормаСписка" Тогда
		Параметр.Форма = ЭтаФорма;
	КонецЕсли;
	
КонецПроцедуры