Перем СоответствиеТипаФайлаКартинке;

Процедура ПередОткрытием(Отказ, СтандартнаяОбработка)
	
	Если Не РежимВыбора Тогда
		ЭлементыФормы.ДействияФормы.Кнопки.Удалить(0);
		ЭлементыФормы.ДействияФормы.Кнопки.Удалить(0);
	КонецЕсли;
	
	Для Каждого ЭлементПорядка Из ЭлементыФормы.ДокументСписок.НастройкаПорядка Цикл
		ЭлементПорядка.Доступность = Истина;
	КонецЦикла;
	
КонецПроцедуры

Процедура ДокументСписокПриПолученииДанных(Элемент, ОформленияСтрок)
	
	СоответствиеСсылкиНаДокументОформлениюСтроки = Новый Соответствие;
	СсылкиНаДокументы = Новый Массив;
	Для Каждого ОформлениеСтроки Из ОформленияСтрок Цикл
		СсылкаНаДокумент = ОформлениеСтроки.ДанныеСтроки.Ссылка;
		СсылкиНаДокументы.Добавить(СсылкаНаДокумент);
		СоответствиеСсылкиНаДокументОформлениюСтроки.Вставить(СсылкаНаДокумент, ОформлениеСтроки);
	КонецЦикла;
	КолонкаТипФайлаВидима = ЭлементыФормы.ДокументСписок.Колонки.ТипФайла.Видимость;
	
	Запрос = Новый Запрос("ВЫБРАТЬ РАЗРЕШЕННЫЕ
	                      |	ВложенияНеформализованныхДокументов.НеформализованныйДокумент,
	                      |	ВложенияНеформализованныхДокументов.ИмяФайла,
	                      |	ВложенияНеформализованныхДокументов.Тип
	                      |ИЗ
	                      |	РегистрСведений.ВложенияНеформализованныхДокументов КАК ВложенияНеформализованныхДокументов
	                      |ГДЕ
	                      |	ВложенияНеформализованныхДокументов.НеформализованныйДокумент В(&НеформализованныйДокумент)");
	Запрос.УстановитьПараметр("НеформализованныйДокумент", СсылкиНаДокументы);
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		ТипДокумента = Выборка.Тип;
		КартинкаТипаДокумента = СоответствиеТипаФайлаКартинке[ТипДокумента];
		СсылкаНаДокумент = Выборка.НеформализованныйДокумент;
		ОформлениеСтроки = СоответствиеСсылкиНаДокументОформлениюСтроки[СсылкаНаДокумент];
		ОформлениеСтроки.Ячейки.ТипФайла.Значение = ТипДокумента;
		ОформлениеСтроки.Ячейки.ИмяФайла.Значение = Выборка.ИмяФайла;
		Если КолонкаТипФайлаВидима Тогда
			ОформлениеСтроки.Ячейки.ТипФайла.ОтображатьКартинку = Истина;
			ОформлениеСтроки.Ячейки.ТипФайла.Картинка = КартинкаТипаДокумента;
		Иначе
			ОформлениеСтроки.Ячейки.ИмяФайла.ОтображатьКартинку = Истина;
			ОформлениеСтроки.Ячейки.ИмяФайла.Картинка = КартинкаТипаДокумента;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Процедура ДействияФормыЦиклыОбмена(Кнопка)
	
	Если ЭлементыФормы.ДокументСписок.ТекущаяСтрока = Неопределено Тогда
		Предупреждение("Выберите документ для просмотра связанных с ним циклов обмена!");
		Возврат;
	КонецЕсли;
	
	КонтекстЭДО.ПолучитьФорму("УправлениеОбменом").Открыть();
	Оповестить("Показать циклы обмена", ЭлементыФормы.ДокументСписок.ТекущаяСтрока.Ссылка);
	
КонецПроцедуры

Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "Опрос форм" И Параметр.Ключ = "Документ_НеформализованныйДокументНалогоплательщика_ФормаСписка" Тогда
		Параметр.Форма = ЭтаФорма;
	КонецЕсли;
	
КонецПроцедуры

СоответствиеТипаФайлаКартинке = Новый Соответствие;
СоответствиеТипаФайлаКартинке.Вставить(Перечисления.ТипыВложенийНеформализованныхДокументовНалогоплательщика.ДокументMicrosoftExcel, БиблиотекаКартинок.ПиктограммаФайла_Excel);
СоответствиеТипаФайлаКартинке.Вставить(Перечисления.ТипыВложенийНеформализованныхДокументовНалогоплательщика.ДокументMicrosoftWord, БиблиотекаКартинок.ПиктограммаФайла_Word);
СоответствиеТипаФайлаКартинке.Вставить(Перечисления.ТипыВложенийНеформализованныхДокументовНалогоплательщика.ТекстовыйДокумент, БиблиотекаКартинок.ПиктограммаФайла_TXT);
СоответствиеТипаФайлаКартинке.Вставить(Перечисления.ТипыВложенийНеформализованныхДокументовНалогоплательщика.ПустаяСсылка(), БиблиотекаКартинок.ПиктограммаФайла_НеОпределен);