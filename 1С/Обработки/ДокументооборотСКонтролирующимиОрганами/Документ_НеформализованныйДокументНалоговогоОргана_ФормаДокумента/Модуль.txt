Процедура НалоговыйОрганНажатие(Элемент)
	
	ОткрытьЗначение(НалоговыйОрган);
	
КонецПроцедуры

Процедура ПередОткрытием(Отказ, СтандартнаяОбработка)
	
	Если ЭтоНовый() Тогда
		Предупреждение("Документ не предназначен для ввода вручную!");
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	ЭлементыФормы.НалоговыйОрган.Заголовок = НалоговыйОрган;
	ЭлементыФормы.Организация.Заголовок = Организация;
	ЭлементыФормы.НадписьСоздан.Заголовок = "Создан " + Дата;
	
	СформироватьСтрокуВложенныеДокументы();
	Если ПустаяСтрока(ПредставлениеВложенныеДокументы) Тогда
		ЭлементыФормы.ПанельАттачмент.Свертка = РежимСверткиЭлементаУправления.Верх;
	КонецЕсли;
	
	КонтекстЭДО.ПометитьПисьмоКакПрочитанное(Ссылка);
	
КонецПроцедуры

Процедура ОрганизацияНажатие(Элемент)
	
	ОткрытьЗначение(Организация);
	
КонецПроцедуры

Процедура ДействияФормыЦиклыОбмена(Кнопка)
	
	Если ЭтоНовый() Тогда
		Предупреждение("Документ еще не сохранялся.
		|Циклы обмена могут быть связаны только с сохраненными документами.");
		Возврат;
	КонецЕсли;
	
	КонтекстЭДО.ПолучитьФорму("УправлениеОбменом").Открыть();
	Оповестить("Показать циклы обмена", Ссылка);
	
КонецПроцедуры

Процедура ДействияФормыОтветить(Кнопка)
	
	НовыйДокумент = Документы.НеформализованныйДокументНалогоплательщика.СоздатьДокумент();
	НовыйДокумент.Заполнить(Ссылка);
	Если НовыйДокумент.ОшибкаЗаполнения <> Истина Тогда
		НовыйДокумент.ПолучитьФорму().Открыть();
	КонецЕсли;
	
КонецПроцедуры

Процедура НадписьСохранитьНажатие(Элемент)
	
	ДиалогСохранение = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.ВыборКаталога);
	ДиалогСохранение.Заголовок = "Укажите каталог для выгрузки";
	Если ДиалогСохранение.Выбрать() Тогда
		
		ВложенияНФД = КонтекстЭДО.ПолучитьВложенияНеформализованногоДокумента(Ссылка);
		Если ВложенияНФД.Количество() = 0 Тогда
			Предупреждение("Вложенные документы не найдены!");
			Возврат;
		КонецЕсли;
		
		СписокВложенийНФД = Новый СписокЗначений;
		Для Каждого ВложениеНФД Из ВложенияНФД Цикл
			СписокВложенийНФД.Добавить(ВложениеНФД.ИмяФайла, , Истина);
		КонецЦикла;
		
		Если СписокВложенийНФД.Количество() > 1 Тогда
			Если НЕ СписокВложенийНФД.ОтметитьЭлементы("Выберите документы для выгрузки") Тогда
				Возврат;
			КонецЕсли;
		КонецЕсли;
		
		МассивВыбранныхВложений = Новый Массив;
		Для Каждого ВложениеНФД Из СписокВложенийНФД Цикл
			Если ВложениеНФД.Пометка Тогда
				МассивВыбранныхВложений.Добавить(ВложениеНФД.Значение);
			КонецЕсли;
		КонецЦикла;
		
		Если МассивВыбранныхВложений.Количество() = 0 Тогда
			Предупреждение("Не выбраны документы для сохранения!");
			Возврат;
		КонецЕсли;
		
		ДокументыДляСохранения = КонтекстЭДО.ПолучитьВложенияНеформализованногоДокумента(Ссылка, МассивВыбранныхВложений, Истина);
		ЧислоОшибок = 0;
		
		Для Каждого ДокументДляСохранения Из ДокументыДляСохранения Цикл
		
			КороткоеИмяФайла = КонтекстЭДО.СформироватьИмяФайла(ДокументДляСохранения.ИмяФайла);
			ПолноеИмяФайла = ДиалогСохранение.Каталог + КороткоеИмяФайла;
			ОбъектФайл = Новый Файл(ПолноеИмяФайла);
			Если ОбъектФайл.Существует() Тогда
				Ответ = Вопрос("В выбранном каталоге уже существует файл """ + КороткоеИмяФайла + """.
								|Вы уверены, что хотите переписать его?", РежимДиалогаВопрос.ДаНетОтмена);
				Если Ответ = КодВозвратаДиалога.Нет Тогда
					Продолжить;
				ИначеЕсли Ответ = КодВозвратаДиалога.Отмена Тогда
					Возврат;
				КонецЕсли;
			КонецЕсли;
			Попытка
				ДокументДляСохранения.Данные.Получить().Записать(ПолноеИмяФайла);
			Исключение
				ЧислоОшибок = ЧислоОшибок + 1;
				Сообщить("Ошибка выгрузки документа """ + КороткоеИмяФайла + """:
						|" + ИнформацияОбОшибке().Описание, СтатусСообщения.Важное);
			КонецПопытки;
			
		КонецЦикла;
		
		Если ЧислоОшибок = 0 Тогда
			Предупреждение("Документы успешно выгружены.");
		Иначе
			Предупреждение("Во время выгрузки документов произошли ошибки.
							|Подробная информация об ошибках приведена в окне сообщений.");
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура СформироватьСтрокуВложенныеДокументы()
	
	ВложенияНФД = КонтекстЭДО.ПолучитьВложенияНеформализованногоДокумента(Ссылка);
	
	СтрокаПредставление = "";
	Для Каждого ВложениеНФД Из ВложенияНФД Цикл
		СтрокаПредставление = СтрокаПредставление + "; " + ВложениеНФД.ИмяФайла;
	КонецЦикла;
	Если НЕ ПустаяСтрока(СтрокаПредставление) Тогда
		СтрокаПредставление = Сред(СтрокаПредставление, 3);
	КонецЕсли;
	
	ПредставлениеВложенныеДокументы = СтрокаПредставление;
	
КонецПроцедуры

Процедура НадписьОткрытьНажатие(Элемент)
	
	ВложенияНФД = КонтекстЭДО.ПолучитьВложенияНеформализованногоДокумента(Ссылка);
	
	ВложенияНФДСписок = Новый СписокЗначений;
	Для Каждого ВложениеНФД Из ВложенияНФД Цикл
		ВложенияНФДСписок.Добавить(ВложениеНФД.ИмяФайла);
	КонецЦикла;
	
	КоличествоВложений = ВложенияНФДСписок.Количество();
	Если КоличествоВложений = 0 Тогда
		Предупреждение("Вложенные документы не найдены!");
		Возврат;
	ИначеЕсли КоличествоВложений = 1 Тогда
		РезультатВыбора = ВложенияНФДСписок[0];
	Иначе
		РезультатВыбора = ВложенияНФДСписок.ВыбратьЭлемент("Выберите документ");
		Если РезультатВыбора = Неопределено Тогда
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	ВложениеДокумент = КонтекстЭДО.ПолучитьВложенияНеформализованногоДокумента(Ссылка, РезультатВыбора.Значение, Истина);
	Если ВложениеДокумент.Количество() = 0 Тогда
		Предупреждение("Документ """ + РезультатВыбора.Значение + """ не найден!");
		Возврат;
	КонецЕсли;
	
	ИмяВремФайлаДокумента = ПолучитьИмяВременногоФайла(РезультатВыбора.Значение);
	Попытка
		ВложениеДокумент[0].Данные.Получить().Записать(ИмяВремФайлаДокумента);
		ЗапуститьПриложение(ИмяВремФайлаДокумента);
	Исключение
		Предупреждение("При попытке выгрузки и открытия документа произошла ошибка:
						|
						|" + ИнформацияОбОшибке().Описание);
		Возврат;
	КонецПопытки;
	
КонецПроцедуры

Процедура СодержаниеОчистка(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "Опрос форм" И Параметр.Ключ = Ссылка Тогда
		Параметр.Форма = ЭтаФорма;
	КонецЕсли;
	
КонецПроцедуры