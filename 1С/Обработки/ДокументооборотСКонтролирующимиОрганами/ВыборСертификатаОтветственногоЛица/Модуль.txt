Перем СвойстваСертификатов Экспорт;
Перем СерыйЦвет;
Перем Организация Экспорт;

Процедура СертификатыПередСворачиванием(Элемент, Строка, Отказ)
	
	Отказ = Истина;
	
КонецПроцедуры

Процедура ПередОткрытием(Отказ, СтандартнаяОбработка)
	
	СертификатыСоответствие = Новый Соответствие;
	
	Запрос = Новый Запрос("ВЫБРАТЬ РАЗРЕШЕННЫЕ
	                      |	Организации.Ссылка КАК Организация,
	                      |	ПользователиУчетныхЗаписейДокументооборота.УчетнаяЗапись.СертификатРуководителя КАК СертификатРуководителя,
	                      |	ПользователиУчетныхЗаписейДокументооборота.УчетнаяЗапись.СертификатГлавногоБухгалтера КАК СертификатГлавногоБухгалтера
	                      |ИЗ
	                      |	РегистрСведений.ПользователиУчетныхЗаписейДокументооборота КАК ПользователиУчетныхЗаписейДокументооборота
	                      |		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	                      |			Организации.Ссылка КАК Ссылка,
	                      |			Организации.УчетнаяЗаписьОбмена КАК УчетнаяЗаписьОбмена,
	                      |			Организации.Наименование КАК Наименование
	                      |		ИЗ
	                      |			Справочник.Организации КАК Организации
	                      |		ГДЕ
	                      |			Организации.ВидОбменаСКонтролирующимиОрганами = &ВидОбменаСКонтролирующимиОрганами
	                      |			И Организации.УчетнаяЗаписьОбмена.ПометкаУдаления = &ПометкаУдаления) КАК Организации
	                      |		ПО ПользователиУчетныхЗаписейДокументооборота.УчетнаяЗапись = Организации.УчетнаяЗаписьОбмена
	                      |ГДЕ
	                      |	ПользователиУчетныхЗаписейДокументооборота.Пользователь = &Пользователь");
						  
	Если ЗначениеЗаполнено(Организация) Тогда
		Запрос.Текст = Запрос.Текст + "
						  |	И ПользователиУчетныхЗаписейДокументооборота.УчетнаяЗапись = &УчетнаяЗаписьОрагнизации";
		Запрос.УстановитьПараметр("УчетнаяЗаписьОрагнизации", Организация.УчетнаяЗаписьОбмена);
	КонецЕсли;						  
	Запрос.Текст = Запрос.Текст + "
	                      |
	                      |УПОРЯДОЧИТЬ ПО
	                      |	Организации.Наименование";
	Запрос.УстановитьПараметр("ВидОбменаСКонтролирующимиОрганами", Перечисления.ВидыОбменаСКонтролирующимиОрганами.ОбменВУниверсальномФормате);
	Запрос.УстановитьПараметр("Пользователь", глЗначениеПеременной("глТекущийПользователь"));
	Запрос.УстановитьПараметр("ПометкаУдаления", Ложь);
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Организация = Выборка.Организация;
		Если НЕ ЗначениеЗаполнено(Организация) Тогда
			Продолжить;
		КонецЕсли;
		СертификатРуководителя = Выборка.СертификатРуководителя;
		СертификатГлавногоБухгалтера = Выборка.СертификатГлавногоБухгалтера;
		Если НЕ ПустаяСтрока(СертификатРуководителя) ИЛИ НЕ ПустаяСтрока(СертификатГлавногоБухгалтера) Тогда
			НовСтр = Сертификаты.Строки.Добавить();
			НовСтр.ОтветственноеЛицо = Организация;
			Если НЕ ПустаяСтрока(СертификатРуководителя) Тогда
				НовСтр2Уровня = НовСтр.Строки.Добавить();
				НовСтр2Уровня.ОтветственноеЛицо = "Сертификат руководителя";
				НовСтр2Уровня.Сертификат = СертификатРуководителя;
				Если СертификатРуководителя = НачальноеЗначениеВыбора Тогда
					ЭлементыФормы.Сертификаты.ТекущаяСтрока = НовСтр2Уровня;
				КонецЕсли;
				СертификатыСоответствие.Вставить(СертификатРуководителя);
			КонецЕсли;
			Если НЕ ПустаяСтрока(СертификатГлавногоБухгалтера) Тогда
				НовСтр2Уровня = НовСтр.Строки.Добавить();
				НовСтр2Уровня.ОтветственноеЛицо = "Сертификат главного бухгалтера";
				НовСтр2Уровня.Сертификат = СертификатГлавногоБухгалтера;
				Если СертификатГлавногоБухгалтера = НачальноеЗначениеВыбора Тогда
					ЭлементыФормы.Сертификаты.ТекущаяСтрока = НовСтр2Уровня;
				КонецЕсли;
				СертификатыСоответствие.Вставить(СертификатГлавногоБухгалтера);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	СвойстваСертификатов = КонтекстЭДО.ПолучитьСвойстваСертификатовПоОтпечаткам(СертификатыСоответствие);
	Если СвойстваСертификатов = Неопределено Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	Если Сертификаты.Строки.Количество() = 0 Тогда
		Предупреждение("Не определено ни одного сертификата ответственого лица
						|в учетных записях налогоплательщика, пользователем которых Вы являетесь.");
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	УправлениеДоступностьюКнопокВыбратьИОткрыть();
	
КонецПроцедуры

Процедура Выбрать()
	
	ТекДанные = ЭлементыФормы.Сертификаты.ТекущиеДанные;
	
	Если ТекДанные = Неопределено ИЛИ ТекДанные.Уровень() <> 1 Тогда
		Возврат;
	Конецесли;
	
	СвойстваСертификата = СвойстваСертификатов[ТекДанные.Сертификат];
	Если СвойстваСертификата = Неопределено Тогда
		Предупреждение("Выбранный сертификат не зарегистрирован в системном хранилище личных сертификатов!");
		Возврат;
	КонецЕсли;
	
	ТекДата = ТекущаяДата();
	СрокИстек = ТекДата > СвойстваСертификата.ДействителенПо;
	СрокНеНачался = ТекДата < СвойстваСертификата.ДействителенС;
	Если СрокИстек ИЛИ СрокНеНачался Тогда
		Ответ = Вопрос("Вы уверены, что хотите выбрать сертификат, срок действия которого " + ?(СрокИстек, "истек", "еще не начался") + "?", РежимДиалогаВопрос.ДаНет);
		Если Ответ <> КодВозвратаДиалога.Да Тогда
			Возврат;
		КонецЕсли;
	КонецЕсли;
	СвойстваСертификата.Вставить("Организация", ТекДанные.Родитель.ОтветственноеЛицо);
	Закрыть(СвойстваСертификата);
	
КонецПроцедуры

Процедура КоманднаяПанельФормыВыбрать(Кнопка)
	
	Выбрать();
	
КонецПроцедуры

Процедура СертификатыПриПолученииДанных(Элемент, ОформленияСтрок)
	
	Для Каждого ОформлениеСтроки Из ОформленияСтрок Цикл
		
		ДанныеСтроки = ОформлениеСтроки.ДанныеСтроки;
		Если ДанныеСтроки.Уровень() <> 1 Тогда
			Продолжить;
		КонецЕсли;
		
		Сертификат = ДанныеСтроки.Сертификат;
		СвойстваСертификата = СвойстваСертификатов.Получить(Сертификат);
		ЯчейкаСертификат = ОформлениеСтроки.Ячейки.Сертификат;
		Если СвойстваСертификата = Неопределено Тогда
			ЯчейкаСертификат.ЦветТекста = Новый Цвет(255, 0, 0);
			ЯчейкаСертификат.УстановитьТекст("Отпечаток: " + Сертификат + "
											|Сертификат недоступен текущему пользователю операционной системы!");
			Продолжить;
		КонецЕсли;
		
		ТекДата = ТекущаяДата();
		ПредставлениеСертификата = СокрЛП(СвойстваСертификата.Наименование) + "
									|Действует с " + СвойстваСертификата.ДействителенС + " по " + СвойстваСертификата.ДействителенПо;
		Если НЕ СвойстваСертификата.ВозможностьПодписи Тогда
			ЯчейкаСертификат.ЦветТекста = Новый Цвет(255, 0, 0);
			ЯчейкаСертификат.УстановитьТекст(ПредставлениеСертификата + "
								  		|Сертификат не предназначен для подписания!");
		ИначеЕсли ТекДата > СвойстваСертификата.ДействителенПо Тогда
			ЯчейкаСертификат.ЦветТекста = Новый Цвет(160, 160, 160);
			ЯчейкаСертификат.УстановитьТекст(ПредставлениеСертификата + "
								  		|Период действия сертификата истек!");
		ИначеЕсли ТекДата < СвойстваСертификата.ДействителенС Тогда
			ЯчейкаСертификат.ЦветТекста = Новый Цвет(160, 160, 160);
			ЯчейкаСертификат.УстановитьТекст(ПредставлениеСертификата + "
								  		|Период действия сертификата еще не наступил!");
		Иначе
			ЯчейкаСертификат.ЦветТекста = Новый Цвет;
			ЯчейкаСертификат.УстановитьТекст(ПредставлениеСертификата);
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура СертификатыВыбор(Элемент, ВыбраннаяСтрока, Колонка, СтандартнаяОбработка)
	
	//Если ЭлементыФормы.Сертификаты.ТекущаяКолонка.Имя = "Сертификат" И ВыбраннаяСтрока.Уровень() = 1 Тогда
	//	КонтекстЭДО.ПоказатьСертификат(ЭлементыФормы.Сертификаты.ТекущиеДанные.Сертификат, "MY");
	//Иначе
		Выбрать();
	//КонецЕсли;
	
КонецПроцедуры

Процедура УправлениеДоступностьюКнопокВыбратьИОткрыть()
	
	ПризнакДоступности = (ЭлементыФормы.Сертификаты.ТекущаяСтрока <> Неопределено И ЭлементыФормы.Сертификаты.ТекущиеДанные.Уровень() = 1);
	ЭлементыФормы.КоманднаяПанельФормы.Кнопки.Выбрать.Доступность = ПризнакДоступности;
	ЭлементыФормы.КоманднаяПанельФормы.Кнопки.Открыть.Доступность = ПризнакДоступности;
	
КонецПроцедуры

Процедура СертификатыПриАктивизацииСтроки(Элемент)
	
	УправлениеДоступностьюКнопокВыбратьИОткрыть();
	
КонецПроцедуры

Процедура КоманднаяПанельФормыОткрыть(Кнопка)
	
	Если ЭлементыФормы.Сертификаты.ТекущиеДанные <> Неопределено Тогда
		КонтекстЭДО.ПоказатьСертификат(ЭлементыФормы.Сертификаты.ТекущиеДанные.Сертификат, "MY");
	КонецЕсли;
	
КонецПроцедуры

СерыйЦвет = Новый Цвет(128, 128, 128);