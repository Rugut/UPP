Процедура ПередОткрытием(Отказ, СтандартнаяОбработка)
	
	ДопустимыеОрганизации = КонтекстЭДО.СписокДопустимыхОрганизацийВОбъектахОбмена();
	Если ДопустимыеОрганизации.Количество() = 0 Тогда
		Предупреждение("Вы не являетесь зарегистрированным пользователем ни одной учетной записи.");
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	Если МножественныйВыбор Тогда
		ЭлементыФормы.Организации.ОтображатьПометку = Истина;
		Заголовок = "Выберите организации";
		ИсходныйСписокОрганизаций = ?(ТипЗнч(НачальноеЗначениеВыбора) = Тип("Массив"), НачальноеЗначениеВыбора, Новый Массив);
		Для Каждого Орг Из ДопустимыеОрганизации Цикл
			Организации.Добавить(Орг, , (ИсходныйСписокОрганизаций.Найти(Орг) <> Неопределено));
		КонецЦикла;
		Если Организации.Количество() <> 0 Тогда
			ЭлементыФормы.Организации.ТекущаяСтрока = Организации.Получить(0);
		КонецЕсли;
	Иначе
		ЭлементыФормы.Организации.ОтображатьПометку = Ложь;
		Заголовок = "Выберите организацию";
		ЭлементыФормы.КоманднаяПанельОрганизации.Кнопки.Удалить(ЭлементыФормы.КоманднаяПанельОрганизации.Кнопки.УстановитьФлажки);
		ЭлементыФормы.КоманднаяПанельОрганизации.Кнопки.Удалить(ЭлементыФормы.КоманднаяПанельОрганизации.Кнопки.СнятьФлажки);
		ЭлементыФормы.КоманднаяПанельОрганизации.Кнопки.Удалить(ЭлементыФормы.КоманднаяПанельОрганизации.Кнопки.Разделитель);
		Организации.ЗагрузитьЗначения(ДопустимыеОрганизации);
		Если ЗначениеЗаполнено(НачальноеЗначениеВыбора) Тогда
			АктивнаяОрганизация = Организации.НайтиПоЗначению(НачальноеЗначениеВыбора);
			ЭлементыФормы.Организации.ТекущаяСтрока = ?(АктивнаяОрганизация = Неопределено, Организации.Получить(0), АктивнаяОрганизация);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Функция ВыбранныеОрганизации()
	
	МассивВыбранныхОрганизаций = Новый Массив;
	Если МножественныйВыбор Тогда
		Для Каждого Эл Из Организации Цикл
			Если Эл.Пометка Тогда
				МассивВыбранныхОрганизаций.Добавить(Эл.Значение);
			КонецЕсли;
		КонецЦикла;
	Иначе
		Если ЭлементыФормы.Организации.ТекущаяСтрока <> Неопределено Тогда
			МассивВыбранныхОрганизаций.Добавить(ЭлементыФормы.Организации.ТекущаяСтрока.Значение);
		КонецЕсли;
	КонецЕсли;
	Возврат МассивВыбранныхОрганизаций;
	
КонецФункции

Процедура КоманднаяПанельОрганизацииУстановитьФлажки(Кнопка)
	
	Организации.ЗаполнитьПометки(Истина);
	
КонецПроцедуры

Процедура КоманднаяПанельОрганизацииСнятьФлажки(Кнопка)
	
	Организации.ЗаполнитьПометки(Ложь);
	
КонецПроцедуры

Процедура ОрганизацииВыбор(Элемент, ЭлементСписка)
	
	Выбрать(ЭлементСписка);
	
КонецПроцедуры

Процедура КоманднаяПанельОрганизацииОткрыть(Кнопка)
	
	Если ЭлементыФормы.Организации.ТекущаяСтрока <> Неопределено Тогда
		ОткрытьЗначение(ЭлементыФормы.Организации.ТекущаяСтрока.Значение);
	КонецЕсли;
	
КонецПроцедуры

Процедура КоманднаяПанельФормыВыбрать(Кнопка)
	
	Выбрать();
	
КонецПроцедуры

Процедура Выбрать(ВыбранныйЭлемент = Неопределено)
	
	Если ВыбранныйЭлемент = Неопределено Тогда
		ВыбранныеОрганизации = ВыбранныеОрганизации();
		Если ВыбранныеОрганизации.Количество() = 0 Тогда
			Ответ = Вопрос("Не выбрана ни одна организация. Продолжить?", РежимДиалогаВопрос.ДаНет);
			Если Ответ <> КодВозвратаДиалога.Да Тогда
				Возврат;
			КонецЕсли;
		КонецЕсли;
	Иначе
		ВыбранныеОрганизации = Новый Массив;
		ВыбранныеОрганизации.Добавить(ВыбранныйЭлемент.Значение);
	КонецЕсли;	
	
	Если МножественныйВыбор Тогда
		Закрыть(ВыбранныеОрганизации);
	Иначе
		Если ВыбранныеОрганизации.Количество() = 0 Тогда
			Закрыть(Справочники.Организации.ПустаяСсылка());
		Иначе
			Закрыть(ВыбранныеОрганизации[0]);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры