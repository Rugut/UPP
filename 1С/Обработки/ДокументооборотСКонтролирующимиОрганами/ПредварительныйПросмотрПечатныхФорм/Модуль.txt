Перем ПоследнийРедактируемыйТабличныйДокумент;
Перем ВидПечати Экспорт;

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

// Процедура выводит на печать все табличные документы из списка. Вызывается при
// попытке открытия формы с параметром ВидПечати = "Печатать сразу".
//
// Параметры:
//	Нет.
//
Процедура ПечататьСразу()
	
	Для Каждого Стр1 Из НачальноеЗначениеВыбора.Строки Цикл
		Для Каждого Стр2 Из Стр1.Строки Цикл
			Стр2.ТабличныйДокумент.КоличествоЭкземпляров = 1;
			Стр2.ТабличныйДокумент.ЭкземпляровНаСтранице = 1;
			Стр2.ТабличныйДокумент.Напечатать(Истина);
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры

// Процедура сохраняет изменения, произведенные в поле табличного документа ИтоговаяТаблица.
//
// Параметры:
//	Нет.
//
Процедура ЗафиксироватьИзмененияТабличногоДокумента()
	
	Если ПоследнийРедактируемыйТабличныйДокумент <> Неопределено Тогда
		ПоследнийРедактируемыйТабличныйДокумент.ТабличныйДокумент.Очистить();
		ПоследнийРедактируемыйТабличныйДокумент.ТабличныйДокумент.Вывести(ЭлементыФормы.ИтоговаяТаблица);
		ЗаполнитьЗначенияСвойств(ПоследнийРедактируемыйТабличныйДокумент.ТабличныйДокумент, ЭлементыФормы.ИтоговаяТаблица, "ПолеСлева, ПолеСверху, ПолеСправа, ПолеСнизу");
	КонецЕсли;
	
КонецПроцедуры

// Процедура показывает в поле табличного документа ИтоговаяТаблица ("выбранный лист") табличный документ,
// соответствующий выбранной строке табличного поля СписокЛистов.
//
// Параметры:
//	Нет.
Процедура ОсвежитьВыбранныйЛист()
	
	ЗафиксироватьИзмененияТабличногоДокумента();
	ЭлементыФормы.ИтоговаяТаблица.Очистить();
	
	Если ЭлементыФормы.СписокЛистов.ТекущиеДанные.Уровень() = 1 Тогда
		
		ВыводимыйТаблДок = ЭлементыФормы.СписокЛистов.ТекущиеДанные.ТабличныйДокумент;
		ЭлементыФормы.ИтоговаяТаблица.Вывести(ВыводимыйТаблДок);
		ЭлементыФормы.ИтоговаяТаблица.ОриентацияСтраницы = ВыводимыйТаблДок.ОриентацияСтраницы;
		ЭлементыФормы.ИтоговаяТаблица.АвтоМасштаб = ВыводимыйТаблДок.АвтоМасштаб;
		Если НЕ ЭлементыФормы.ИтоговаяТаблица.АвтоМасштаб Тогда
			ЭлементыФормы.ИтоговаяТаблица.МасштабПечати = ВыводимыйТаблДок.МасштабПечати;
		КонецЕсли;
		ЭлементыФормы.ИтоговаяТаблица.ТолькоПросмотр = Ложь;
		ЭлементыФормы.ИтоговаяТаблица.ПолеСверху = ВыводимыйТаблДок.ПолеСверху;
		ЭлементыФормы.ИтоговаяТаблица.ПолеСнизу = ВыводимыйТаблДок.ПолеСнизу;
		ЭлементыФормы.ИтоговаяТаблица.ПолеСправа = ВыводимыйТаблДок.ПолеСправа;
		ЭлементыФормы.ИтоговаяТаблица.ПолеСлева = ВыводимыйТаблДок.ПолеСлева;
		
	Иначе
		
		ЭлементыФормы.ИтоговаяТаблица.ТолькоПросмотр = Истина;
		ТД = Новый ТабличныйДокумент;
		ЭлементыФормы.ИтоговаяТаблица.Вывести(ТД);
		КолСтрокВГруппе = ЭлементыФормы.СписокЛистов.ТекущиеДанные.Строки.Количество();
		Отступ			= 10;
		Интервал		= 16;
		ШиринаРисунка	= 24;
		ВысотаРисунка	= 30;
		РисунковВСтроке	= 4;
		ЭлементыФормы.ИтоговаяТаблица.Область().ЦветФона = Новый Цвет(255, 251, 240);
		
		Для Инд = 0 По КолСтрокВГруппе - 1 Цикл
			
			ТекСтр = ЭлементыФормы.СписокЛистов.ТекущиеДанные.Строки.Получить(Инд);
			ТекТаблДок = ТекСтр.ТабличныйДокумент;
			
			НовРис = ЭлементыФормы.ИтоговаяТаблица.Рисунки.Добавить(ТипРисункаТабличногоДокумента.Текст);
			НовРис.Узор = ТипУзораТабличногоДокумента.Сплошной;
			НовРис.ЦветФона = Новый Цвет(255, 255, 205);
			Если ТекТаблДок.ОриентацияСтраницы = ОриентацияСтраницы.Портрет Тогда
				НовРис.Ширина = ШиринаРисунка;
				НовРис.Высота = ВысотаРисунка;
			Иначе
				НовРис.Ширина = ВысотаРисунка;
				НовРис.Высота = ШиринаРисунка;
			КонецЕсли;
			НовРис.Лево = Отступ + ((Инд % РисунковВСтроке) * (ШиринаРисунка + Интервал)) + Цел((ШиринаРисунка + Интервал - НовРис.Ширина) / 2);
			НовРис.Верх = Отступ + ((Цел(Инд / РисунковВСтроке)) * (ВысотаРисунка + Интервал)) + Цел((ВысотаРисунка + Интервал - НовРис.Высота) / 2);
			НовРис.ГиперСсылка = Истина;
			
			КоличествоЛистовВсего = ТекТаблДок.КоличествоСтраниц();
			ТекстЛистовВсего = Формат(КоличествоЛистовВсего, "ЧГ=")
							 + " "
							 + СтрЗаменить(ЧислоПрописью(КоличествоЛистовВсего, "НП=Истина, НД=Ложь", "лист, листа, листов, м, , , , ,0"),
											ЧислоПрописью(КоличествоЛистовВсего, "НП=Ложь, НД=Ложь", " , , , , , , , ,0"),
											"");
			НовРис.Текст = ТекСтр.Объект + Символы.ПС + "(" + ТекстЛистовВсего + ")";
			
			НовРис.ГоризонтальноеПоложение = ГоризонтальноеПоложение.Центр;
			НовРис.ВертикальноеПоложение = ВертикальноеПоложение.Центр;
			НовРис.Имя = "Лист" + Формат(Инд, "ЧН=0; ЧГ=");
			
			НовЛиния = ЭлементыФормы.ИтоговаяТаблица.Рисунки.Добавить(ТипРисункаТабличногоДокумента.Прямая);
			НовЛиния.Линия = Новый Линия(ТипЛинииРисункаТабличногоДокумента.Сплошная, 2);
			НовЛиния.ГиперСсылка = Истина;
			Если ТекТаблДок.ОриентацияСтраницы = ОриентацияСтраницы.Портрет Тогда
				НовЛиния.Ширина = ШиринаРисунка - 0.3;
				НовЛиния.Высота = 0.01;
			Иначе
				НовЛиния.Ширина = ВысотаРисунка - 0.3;
				НовЛиния.Высота = 0.01;
			КонецЕсли;
			НовЛиния.Лево = НовРис.Лево + 0.3;
			НовЛиния.Верх = НовРис.Верх + НовРис.Высота;
			НовЛиния.Имя = "ЛНПН" + Формат(Инд, "ЧН=0; ЧГ=");
			
			НовЛиния = ЭлементыФормы.ИтоговаяТаблица.Рисунки.Добавить(ТипРисункаТабличногоДокумента.Прямая);
			НовЛиния.Линия = Новый Линия(ТипЛинииРисункаТабличногоДокумента.Сплошная, 2);
			НовЛиния.ГиперСсылка = Истина;
			Если ТекТаблДок.ОриентацияСтраницы = ОриентацияСтраницы.Портрет Тогда
				НовЛиния.Ширина = 0.01;
				НовЛиния.Высота = ВысотаРисунка - 0.3;
			Иначе
				НовЛиния.Ширина = 0.01;
				НовЛиния.Высота = ШиринаРисунка - 0.3;
			КонецЕсли;
			НовЛиния.Лево = НовРис.Лево + НовРис.Ширина;
			НовЛиния.Верх = НовРис.Верх + 0.3;
			НовЛиния.Имя = "ПНПВ" + Формат(Инд, "ЧН=0; ЧГ=");
			
		КонецЦикла;
		
		ЭлементыФормы.ИтоговаяТаблица.ТолькоПросмотр = Истина;
		
	КонецЕсли;
	
	Если ЭлементыФормы.СписокЛистов.ТекущиеДанные.Уровень() = 1 Тогда
		ПоследнийРедактируемыйТабличныйДокумент = ЭлементыФормы.СписокЛистов.ТекущаяСтрока;
	Иначе
		ПоследнийРедактируемыйТабличныйДокумент = Неопределено;
	КонецЕсли;
	
КонецПроцедуры

// Функция возвращает общее количество выбранных из списка печатаемых листов.
//
// Параметры:
//	Нет.
//
// Возвращаемое значение: 
//	Число - общее количество выбранных из списка печатаемых листов.
//
Функция КоличествоВыбранныхТабличныхДокументов()
	
	Кол = 0;
	Для Каждого Стр1 Из СписокЛистов.Строки Цикл
		Для Каждого Стр2 Из Стр1.Строки Цикл
			Кол = Кол + ?(Стр2.Пом, 1, 0);
		КонецЦикла;
	КонецЦикла;
	Возврат Кол;
	
КонецФункции

Процедура ВыгрузитьНаДиск(ВФормате)
	
	СоответствиеФорматаРасширению = Новый Соответствие;
	СоответствиеФорматаРасширению.Вставить(ТипФайлаТабличногоДокумента.MXL, "mxl");
	СоответствиеФорматаРасширению.Вставить(ТипФайлаТабличногоДокумента.XLS, "xls");
	
	ЗафиксироватьИзмененияТабличногоДокумента();
	
	Если КоличествоВыбранныхТабличныхДокументов() = 0 Тогда
		Предупреждение("Выберите листы в дереве печатаемых листов!");
		Возврат;
	КонецЕсли;
	
	Диалог = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.ВыборКаталога);
	Диалог.Заголовок = "Укажите каталог для сохранения";
	Если НЕ Диалог.Выбрать() Тогда
		Возврат;
	КонецЕсли;
	ПутьДляВыгрузки = Диалог.Каталог;
	Если Прав(ПутьДляВыгрузки, 1) <> "\" Тогда
		ПутьДляВыгрузки = ПутьДляВыгрузки + "\";
	КонецЕсли;
	
	ПризнакОшибки = Ложь;
	Для Каждого Стр1 Из СписокЛистов.Строки Цикл
		Для Каждого Эл Из Стр1.Строки Цикл
			Если Эл.Пом Тогда
				ИмяФайла = ПутьДляВыгрузки + СтрЗаменить(Стр1.Объект, ":", ".") + ". " + Эл.Объект + "." + СоответствиеФорматаРасширению[ВФормате];
				Попытка
					Состояние("Сохранение " + ИмяФайла);
					Эл.ТабличныйДокумент.Область().ЦветФона = Новый Цвет();
					Эл.ТабличныйДокумент.Записать(ИмяФайла, ВФормате);
				Исключение
					//Сообщить(ОписаниеОшибки(), СтатусСообщения.Важное);
					ПризнакОшибки = Истина;
				Конецпопытки;
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	Состояние();
	
	Если ПризнакОшибки Тогда
		Предупреждение("Один или несколько файлов не удалось записать в указанный каталог!
						|Возможно, недостаточно места на диске или диск защищен от записи.", , "Ошибка записи на диск");
	Иначе
		Если Вопрос("Выбранные файлы успешно записаны на диск. Открыть каталог?", РежимДиалогаВопрос.ДаНет,,, "Файлы успешно записаны") = КодВозвратаДиалога.Да Тогда
			ЗапуститьПриложение("explorer " + ПутьДляВыгрузки);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

// Процедура - обработчик события "ПередОткрытием" формы.
//
Процедура ПередОткрытием(Отказ, СтандартнаяОбработка)
	
	Если НачальноеЗначениеВыбора.Строки.Количество() = 0 Тогда
		Предупреждение("Не выбраны листы для вывода на печать!");
		Отказ = Истина;
	КонецЕсли;
	Если ВидПечати = "ПечататьСразу" Тогда
		ПечататьСразу();
		Отказ = Истина;
	КонецЕсли;
	
КонецПроцедуры

Процедура АктивизироватьПервыйТабличныйДокумент()
	
	Для Инд = 1 По СписокЛистов.Строки.Количество() Цикл
		ТекРодитель = СписокЛистов.Строки[Инд - 1];
		Для Инд2 = 1 По ТекРодитель.Строки.Количество() Цикл
			ЭлементыФормы.СписокЛистов.ТекущаяСтрока = ТекРодитель.Строки[Инд2 - 1];
			Возврат;
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры

// Процедура - обработчик события "ПриОткрытии" формы.
// В процедуре инициализируются начальные установки и заполняется список печатаемых листов.
//
Процедура ПриОткрытии()
	
	// Инициализируем начальные установки
	сохрКоличествоКопий = ВосстановитьЗначение("ПредпросмотрПечатныхЛистовКоличествоКопий");
	Если сохрКоличествоКопий = Неопределено ИЛИ сохрКоличествоКопий = 0 Тогда
		КоличествоКопий = 1;
	Иначе
		КоличествоКопий = сохрКоличествоКопий;
	КонецЕсли;
	ПоследнийРедактируемыйТабличныйДокумент = Неопределено;
	
	СписокЛистов.Строки.Очистить();
	Для Каждого Стр1 Из НачальноеЗначениеВыбора.Строки Цикл
		Стр1Уровня = СписокЛистов.Строки.Добавить();
		Стр1Уровня.Пом = 1;
		ЗаполнитьЗначенияСвойств(Стр1Уровня, Стр1);
		Для Каждого Стр2 Из Стр1.Строки Цикл
			Стр2Уровня = Стр1Уровня.Строки.Добавить();
			Стр2Уровня.Пом = 1;
			ЗаполнитьЗначенияСвойств(Стр2Уровня, Стр2);
		КонецЦикла;
		ЭлементыФормы.СписокЛистов.Развернуть(Стр1Уровня);
	КонецЦикла;
	АктивизироватьПервыйТабличныйДокумент();
	
	//Стр1Уровня = СписокЛистов.Строки.Добавить();
	//Стр1Уровня.Пом = 1;
	//Стр1Уровня.Объект = "Уровень 1";
	//Стр1Уровня.ТабличныйДокумент = Неопределено;
	//Для Каждого Сч Из СписокПечатаемыхЛистов Цикл
	//	НовСтр = Стр1Уровня.Строки.Добавить();
	//	НовСтр.Пом = 1;
	//	НовСтр.Объект = Сч.Представление;
	//	НовСтр.ТабличныйДокумент = Сч.Значение;
	//	ЭлементыФормы.СписокЛистов.Развернуть(Стр1Уровня);
	//КонецЦикла;
	//ЭлементыФормы.СписокЛистов.ТекущаяСтрока = СписокЛистов.Строки[0].Строки[0];
	
КонецПроцедуры

// Процедура - обработчик события "ПриЗакрытии" формы.
// В процедуре сохраняются параметры печати и параметры отображения поля табличного документа ИтоговаяТаблица
// для восстановления при следующем открытии формы.
//
Процедура ПриЗакрытии()
	
	СохранитьЗначение("ПредпросмотрПечатныхЛистовКоличествоКопий", КоличествоКопий);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ДЕЙСТВИЯ КОМАНДНЫХ ПАНЕЛЕЙ ФОРМЫ

Процедура ОсновныеДействияФормыВВидеТабличныхДокументов(Кнопка)
	
	ВыгрузитьНаДиск(ТипФайлаТабличногоДокумента.MXL);
	
КонецПроцедуры

Процедура ОсновныеДействияФормыВФорматеMicrosoftExcel(Кнопка)
	
	ВыгрузитьНаДиск(ТипФайлаТабличногоДокумента.XLS);
	
КонецПроцедуры

// Процедура - обработчик события "Нажатие" командной панели ОсновныеДействияФормы.
// Выводит на принтер выбранные табличные документы в соответствии с выбранными
// параметрами печати.
//
Процедура ОсновныеДействияФормыПечать(Кнопка)
	
	ЗафиксироватьИзмененияТабличногоДокумента();
	Для Каждого Стр1 Из СписокЛистов.Строки Цикл
		Для Каждого Эл из Стр1.Строки Цикл
			Если НЕ Эл.Пом Тогда
				Продолжить;
			КонецЕсли;
			Для Сч = 1 По КоличествоКопий Цикл
				Эл.ТабличныйДокумент.КоличествоЭкземпляров = 1;
				Эл.ТабличныйДокумент.ЭкземпляровНаСтранице = 1;
				Эл.ТабличныйДокумент.Напечатать();
			КонецЦикла;
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ФОРМЫ

// Процедура - обработчик события "Нажатие" кнопки КнопкаРаспометитьВсе.
// Снимает пометки с элементов списка печатаемых листов.
//
Процедура КнопкаРаспометитьВсеНажатие(Элемент)
	
	Для Каждого Эл Из СписокЛистов.Строки Цикл
		Эл.Пом = Ложь;
		Для Каждого Стр Из Эл.Строки Цикл
			Стр.Пом = Ложь;
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры

// Процедура - обработчик события "Нажатие" кнопки КнопкаПометитьВсе.
// Устанавливает пометки у элементов списка печатаемых листов.
//
Процедура КнопкаПометитьВсеНажатие(Элемент)
	
	Для Каждого Эл Из СписокЛистов.Строки Цикл
		Эл.Пом = 1;
		Для Каждого Стр Из Эл.Строки Цикл
			Стр.Пом = 1;
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры

// Процедура - обработчик события "Нажатие" кнопки КнопкаПереместитьВниз.
// Текущая строка таблицы значений СписокЛистов перемещается вниз на одну позицию.
//
Процедура КнопкаПереместитьВнизНажатие(Элемент)
	
	Родитель = ?(ЭлементыФормы.СписокЛистов.ТекущиеДанные.Родитель = Неопределено, СписокЛистов, ЭлементыФормы.СписокЛистов.ТекущиеДанные.Родитель);
	Если Родитель.Строки.Индекс(ЭлементыФормы.СписокЛистов.ТекущиеДанные) <> Родитель.Строки.Количество() - 1 Тогда
		Родитель.Строки.Сдвинуть(ЭлементыФормы.СписокЛистов.ТекущиеДанные, 1);
	КонецЕсли;
	ОсвежитьВыбранныйЛист();
	
КонецПроцедуры

// Процедура - обработчик события "Нажатие" кнопки КнопкаПереместитьВверх.
// Текущая строка таблицы значений СписокЛистов перемещается вверх на одну позицию.
//
Процедура КнопкаПереместитьВверхНажатие(Элемент)
	
	Родитель = ?(ЭлементыФормы.СписокЛистов.ТекущиеДанные.Родитель = Неопределено, СписокЛистов, ЭлементыФормы.СписокЛистов.ТекущиеДанные.Родитель);
	Если Родитель.Строки.Индекс(ЭлементыФормы.СписокЛистов.ТекущиеДанные) <> 0 Тогда
		Родитель.Строки.Сдвинуть(ЭлементыФормы.СписокЛистов.ТекущиеДанные, -1);
	КонецЕсли;
	ОсвежитьВыбранныйЛист();
	
КонецПроцедуры

// Процедура - обработчик события "ПриИзмененииФлажка" табличного поля СписокЛистов.
//
Процедура СписокЛистовПриИзмененииФлажка(Элемент, Колонка)
	
	Если ЭлементыФормы.СписокЛистов.ТекущаяСтрока.Пом = 2 Тогда
		ЭлементыФормы.СписокЛистов.ТекущаяСтрока.Пом = 0;
	КонецЕсли;
	
	Если ЭлементыФормы.СписокЛистов.ТекущаяСтрока.Уровень() = 0 Тогда
		Для Каждого Стр Из ЭлементыФормы.СписокЛистов.ТекущаяСтрока.Строки Цикл
			Стр.Пом = ЭлементыФормы.СписокЛистов.ТекущаяСтрока.Пом;
		КонецЦикла;
	Иначе
		ПометкиОдинаковы = Истина;
		Для Каждого Стр Из ЭлементыФормы.СписокЛистов.ТекущаяСтрока.Родитель.Строки Цикл
			Если Стр.Пом <> ЭлементыФормы.СписокЛистов.ТекущаяСтрока.Родитель.Строки[0].Пом Тогда
				ПометкиОдинаковы = Ложь;
				Прервать;
			КонецЕсли;
		КонецЦикла;
		ЭлементыФормы.СписокЛистов.ТекущаяСтрока.Родитель.Пом = ?(ПометкиОдинаковы, ЭлементыФормы.СписокЛистов.ТекущаяСтрока.Пом, 2);
	КонецЕсли;
	
КонецПроцедуры

// Процедура - обработчик события "ПриАктивизацииСтроки" табличного поля СписокЛистов.
// Выводит в поле табличного документа ИтоговаяТаблица табличный документ, соответствующей текущей
// выбранной строке списка "Печатаемые листы".
//
Процедура СписокЛистовПриАктивизацииСтроки(Элемент)
	
	//ТекСтрока = Элемент.ТекущаяСтрока;
	//Если ТекСтрока = Неопределено Тогда
	//	Возврат;
	//КонецЕсли;
	//
	//Если ТекСтрока <> Неопределено И ТекСтрока.Уровень() = 0 И ТекСтрока.Строки.Количество() > 0 Тогда
	//	Элемент.ТекущаяСтрока = ТекСтрока.Строки[0];
	//КонецЕсли;
	//
	ОсвежитьВыбранныйЛист();
	
КонецПроцедуры

Процедура ПриПовторномОткрытии(СтандартнаяОбработка)
	
	Для Каждого Стр1 Из НачальноеЗначениеВыбора.Строки Цикл
		Стр1Уровня = СписокЛистов.Строки.Добавить();
		Стр1Уровня.Пом = 1;
		ЗаполнитьЗначенияСвойств(Стр1Уровня, Стр1);
		Для Каждого Стр2 Из Стр1.Строки Цикл
			Стр2Уровня = Стр1Уровня.Строки.Добавить();
			Стр2Уровня.Пом = 1;
			ЗаполнитьЗначенияСвойств(Стр2Уровня, Стр2);
		КонецЦикла;
		ЭлементыФормы.СписокЛистов.Развернуть(Стр1Уровня);
	КонецЦикла;
	АктивизироватьПервыйТабличныйДокумент();
	
КонецПроцедуры

Процедура КоманднаяПанельСписокЛистовРаскрыть(Кнопка)
	
	Для Каждого Стр Из СписокЛистов.Строки Цикл
		ЭлементыФормы.СписокЛистов.Развернуть(Стр);
	КонецЦикла;
	
КонецПроцедуры

Процедура КоманднаяПанельСписокЛистовСвернуть(Кнопка)
	
	Для Каждого Стр Из СписокЛистов.Строки Цикл
		ЭлементыФормы.СписокЛистов.Свернуть(Стр);
	КонецЦикла;
	
КонецПроцедуры

Процедура СписокЛистовПриПолученииДанных(Элемент, ОформленияСтрок)
	
	Для Каждого ОформлениеСтроки Из ОформленияСтрок Цикл
		Если ОформлениеСтроки.ДанныеСтроки.Уровень() = 0 Тогда
			ОформлениеСтроки.Ячейки.Объект.УстановитьТекст(КонтекстЭДО.печатьПредставлениеЦиклаОбмена(ОформлениеСтроки.ДанныеСтроки.Объект, Ложь));
			ОформлениеСтроки.ЦветФона = Элемент.ЦветФонаЧередованияСтрок;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Процедура ТаблицаГруппыВыбор(Элемент, Область, СтандартнаяОбработка)
	
	Если ЭлементыФормы.СписокЛистов.ТекущаяСтрока <> Неопределено И ЭлементыФормы.СписокЛистов.ТекущаяСтрока.Уровень() = 0 Тогда
		ИмяБезИндекса = Лев(Область.Имя, 4);
		Если ИмяБезИндекса = "Лист" ИЛИ ИмяБезИндекса = "ЛНПН" ИЛИ ИмяБезИндекса = "ПНПВ" Тогда
			ЭлементыФормы.СписокЛистов.ТекущаяСтрока = ЭлементыФормы.СписокЛистов.ТекущиеДанные.Строки.Получить(Число(Сред(Область.Имя, 5)));
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

ЭлементыФормы.ИтоговаяТаблица.АвтоМасштаб = Истина;