Процедура ДействияФормыВыгрузитьВФайл(Кнопка)
	
	ТекСтроки = ЭлементыФормы.РегистрСведенийСписок.ВыделенныеСтроки;
	Если ТекСтроки.Количество() <> 0 Тогда
		Диалог = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.ВыборКаталога);
		Диалог.Заголовок = "Выберите каталог для сохранения";
		Если Диалог.Выбрать() Тогда
			КоличествоОшибок = 0;
			КаталогСохранения = ?(Прав(Диалог.Каталог, 1) = "\", Диалог.Каталог, Диалог.Каталог + "\");
			Сообщения = Новый Массив;
			Для Каждого ТекСтрока Из ТекСтроки Цикл
				Сообщения.Добавить(ТекСтрока.ТранспортноеСообщение);
			КонецЦикла;
			Запрос = Новый Запрос("ВЫБРАТЬ РАЗРЕШЕННЫЕ
			                      |	ТранспортныеКонтейнеры.ТранспортноеСообщение,
			                      |	ТранспортныеКонтейнеры.Данные,
			                      |	ТранспортныеКонтейнеры.ИмяФайла
			                      |ИЗ
			                      |	РегистрСведений.ТранспортныеКонтейнеры КАК ТранспортныеКонтейнеры
			                      |ГДЕ
			                      |	ТранспортныеКонтейнеры.ТранспортноеСообщение В(&ТранспортноеСообщение)");
			Запрос.УстановитьПараметр("ТранспортноеСообщение", Сообщения);
			Выборка = Запрос.Выполнить().Выбрать();
			Пока Выборка.Следующий() Цикл
				КороткоеИмяФайла = Выборка.ИмяФайла;
				ПолноеИмяСохраняемогоФайла = КаталогСохранения + КороткоеИмяФайла;
				ОбъектСохраняемыйФайл = Новый Файл(ПолноеИмяСохраняемогоФайла);
				Если ОбъектСохраняемыйФайл.Существует() Тогда
					Ответ = Вопрос("В выбранном каталоге уже существует файл " + КороткоеИмяФайла + ".
									|Заменить указанный файл?", РежимДиалогаВопрос.ДаНетОтмена);
					Если Ответ = КодВозвратаДиалога.Нет Тогда
						Продолжить;					
					ИначеЕсли Ответ = КодВозвратаДиалога.Отмена Тогда
						Возврат;
					КонецЕсли;
				КонецЕсли;
				Попытка
					Выборка.Данные.Получить().Записать(ПолноеИмяСохраняемогоФайла);
				Исключение
					Сообщить("Во время сохранения транспортного контейнера " + КороткоеИмяФайла + " возникла ошибка:" + Символы.ПС + ОписаниеОшибки(), СтатусСообщения.Важное);
					КоличествоОшибок = КоличествоОшибок + 1;
				КонецПопытки;
			КонецЦикла;
			Если Сообщения.Количество() = 1 Тогда
				Если КоличествоОшибок = 0 Тогда
					Предупреждение("Транспортный контейнер успешно выгружен.");
				КонецЕсли;
			Иначе
				Если КоличествоОшибок = 0 Тогда
					Предупреждение("Транспортные контейнеры успешно выгружены.");
				Иначе
					Предупреждение("Во время сохранения произошли ошибки. Не удалось сохранить контейнеров: " + КоличествоОшибок + ".");
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	Иначе
		Предупреждение("Выберите контейнеры, которые следует сохранить на диск, и повторите попытку.
		|Для множественного выбора используйте клавишу Ctrl.");
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередОткрытием(Отказ, СтандартнаяОбработка)
	
	ЭлементыФормы.РегистрСведенийСписок.ТолькоПросмотр = НЕ КонтекстЭДО.ТекущийПользовательЯвляетсяАдминистраторомУчетныхЗаписейДокументооборота();
	
КонецПроцедуры

Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "Опрос форм" И Параметр.Ключ = "РегистрСведений_ТранспортныеКонтейнеры_ФормаСписка" Тогда
		Параметр.Форма = ЭтаФорма;
	КонецЕсли;
	
КонецПроцедуры