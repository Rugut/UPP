Перем Сведения Экспорт;
Перем КомандыИзмененияСертификатов;

Процедура ПередОткрытием(Отказ, СтандартнаяОбработка)
	
	Если Сведения = Неопределено Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	Если КомандыИзмененияСертификатов.Найти(Сведения.Настройка) <> Неопределено Тогда
		
		Если ТипЗнч(Сведения.СтароеЗначение) = Тип("Строка") Тогда
			СвойстваСертификата = КонтекстЭДО.ПолучитьСвойстваСертификатаПоОтпечатку(Сведения.СтароеЗначение);
			ЭлементыФормы.СтароеЗначение.Заголовок = КонтекстЭДО.ПредставлениеСертификатаПоСвойствам(СвойстваСертификата);
		Иначе
			ЭлементыФормы.СтароеЗначение.Заголовок = КонтекстЭДО.ПредставлениеСертификатаПоСвойствам(Сведения.СтароеЗначение);
		КонецЕсли;
		
		Если ТипЗнч(Сведения.НовоеЗначение) = Тип("Строка") Тогда
			СвойстваСертификата = КонтекстЭДО.ПолучитьСвойстваСертификатаПоОтпечатку(Сведения.НовоеЗначение);
			ЭлементыФормы.НовоеЗначение.Заголовок = КонтекстЭДО.ПредставлениеСертификатаПоСвойствам(СвойстваСертификата);
		Иначе
			ЭлементыФормы.НовоеЗначение.Заголовок = КонтекстЭДО.ПредставлениеСертификатаПоСвойствам(Сведения.НовоеЗначение);
		КонецЕсли;
		
		ЭлементыФормы.СтароеЗначение.ГиперСсылка = Истина;
		ЭлементыФормы.НовоеЗначение.ГиперСсылка = Истина;
		
	ИначеЕсли Сведения.Настройка = "ИзменитьОсновнуюИФНС" Тогда
		ЭлементыФормы.СтароеЗначение.Заголовок = Сведения.СтароеЗначение;
		ЭлементыФормы.НовоеЗначение.Заголовок = Сведения.НовоеЗначение.Код;
	Иначе
		ЭлементыФормы.СтароеЗначение.Заголовок = Сведения.СтароеЗначение;
		ЭлементыФормы.НовоеЗначение.Заголовок = Сведения.НовоеЗначение;
	КонецЕсли;
	
КонецПроцедуры

Процедура СтароеЗначениеНажатие(Элемент)
	
	Если ТипЗнч(Сведения.СтароеЗначение) = Тип("Строка") Тогда
		КонтекстЭДО.ПоказатьСертификат(нрег(Сведения.СтароеЗначение));
	Иначе
		СведенияОСертификате = Сведения.СтароеЗначение;
		Если СведенияОСертификате <> Неопределено И ТипЗнч(СведенияОСертификате) <> Тип("ТаблицаЗначений") И ТипЗнч(СведенияОСертификате) <> Тип("Массив") И СведенияОСертификате.Свойство("Сертификат") Тогда
			ФайлСертификата = КонтекстЭДО.ЗаписатьСертификат64ВФайл(СведенияОСертификате.Сертификат);
			ЗапуститьПриложение(ФайлСертификата);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура НовоеЗначениеНажатие(Элемент)
	
	Если ТипЗнч(Сведения.НовоеЗначение) = Тип("Строка") Тогда
		КонтекстЭДО.ПоказатьСертификат(нрег(Сведения.НовоеЗначение));
	Иначе
		СведенияОСертификате = Сведения.НовоеЗначение;
		Если СведенияОСертификате <> Неопределено И ТипЗнч(СведенияОСертификате) <> Тип("ТаблицаЗначений") И СведенияОСертификате.Свойство("Сертификат") Тогда
			ФайлСертификата = КонтекстЭДО.ЗаписатьСертификат64ВФайл(СведенияОСертификате.Сертификат);
			ЗапуститьПриложение(ФайлСертификата);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура ОсновныеДействияФормыОК(Кнопка)
	
	Закрыть();
	
КонецПроцедуры

Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "Автонастройка параметров документооборота активна?" Тогда
		Параметр = Истина;
	КонецЕсли;
	
КонецПроцедуры

КомандыИзмененияСертификатов = Новый Массив;
КомандыИзмененияСертификатов.Добавить("ИзменитьСертификатРуководителя");
КомандыИзмененияСертификатов.Добавить("ИзменитьСертификатГлавногоБухгалтера");
КомандыИзмененияСертификатов.Добавить("ИзменитьСертификатДляШифрования");
КомандыИзмененияСертификатов.Добавить("ИзменитьСертификатИФНС");
КомандыИзмененияСертификатов.Добавить("ИзменитьСертификатПочтовогоСервера");
КомандыИзмененияСертификатов.Добавить("ИзменитьСертификатНалоговогоОргана");
КомандыИзмененияСертификатов.Добавить("ИзменитьСертификатОрганаПФР");
КомандыИзмененияСертификатов.Добавить("ИзменитьСертификатОрганаФСГС");