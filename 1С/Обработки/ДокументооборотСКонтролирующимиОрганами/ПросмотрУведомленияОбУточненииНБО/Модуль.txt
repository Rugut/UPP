Перем КрасныйЦвет;

Процедура ПередОткрытием(Отказ, СтандартнаяОбработка)
	
	ЗаполнитьДанные(Отказ);
	Если Отказ Тогда
		ПоказатьТекстУведомления();
		Возврат;
	КонецЕсли;
	
	Заголовок = СокрЛП("Уведомление об уточнении " + СокрЛП(ИмяФайлаУведомления));
	
КонецПроцедуры

Процедура ПоказатьТекстУведомления()
	
	Текст = Новый ТекстовыйДокумент;
	Текст.УстановитьТекст(ТекстУведомления);
	Текст.Показать(СокрЛП("Уведомление об уточнении " + СокрЛП(ИмяФайлаУведомления)), СокрЛП(ИмяФайлаУведомления));
	
КонецПроцедуры

Процедура ЗаполнитьДанные(Отказ)
	
	Если НЕ ЗначениеЗаполнено(ТекстУведомления) Тогда
		Предупреждение("Уведомление пусто!");
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	Дерево = КонтекстЭДО.ЗагрузитьСтрокуXMLВДеревоЗначений(ТекстУведомления);
	
	УзлыСведУвед = Дерево.Строки.НайтиСтроки(Новый Структура("Имя, Тип", "СведУвед", "Э"), Истина);
	Если УзлыСведУвед.Количество() = 0 Тогда
		Предупреждение("Неизвестный формат файла уведомления!");
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	УзелСведУвед = УзлыСведУвед[0];
	
	УзлыОбщСвУвед = УзелСведУвед.Строки.НайтиСтроки(Новый Структура("Имя, Тип", "ОбщСвУвед", "Э"));
	Если УзлыОбщСвУвед.Количество() = 0 Тогда
		Предупреждение("Неизвестный формат файла уведомления!");
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	УзелОбщСвУвед = УзлыОбщСвУвед[0];
	
	// заполняем общие сведения
	АтрибутыОбщСвУвед = КонтекстЭДО.ПолучитьЗначенияАтрибутовИЭлементовУзла(УзелОбщСвУвед);
	ЗаполнитьЗначенияСвойств(ЭтаФорма, АтрибутыОбщСвУвед);
	
	НомКорр = ПредставлениеНомерКорректировки(НомКорр);
	ДатаПрием = ПредставлениеДатыПриема(ДатаПрием);
	ПрУточ = ПредставлениеПризнакаУточнения(ПрУточ);
	
	//Если АтрибутыОбщСвУвед.Свойство("КодОшОб") И АтрибутыОбщСвУвед.КодОшОб = "0000000000" Тогда
	//	ЭлементыФормы.ПанельПодробно.Доступность = Ложь;
	//КонецЕсли;
	
	// заполняем сведения о налогоплательщике
	УзлыСвНП = Дерево.Строки.НайтиСтроки(Новый Структура("Имя, Тип", "СвНП", "Э"), Истина);
	Если УзлыСвНП.Количество() > 0 Тогда
		УзелСвНП = УзлыСвНП[0];
		
		АтрибутыУзелСвНП = КонтекстЭДО.ПолучитьАтрибутыУзла(УзелСвНП);
		Если АтрибутыУзелСвНП.Свойство("Тлф") Тогда
			Тлф = АтрибутыУзелСвНП.Тлф;
		КонецЕсли;
		
		УзелНПЮЛ = УзелСвНП.Строки.Найти("НПЮЛ", "Имя");
		УзелНПФЛ = УзелСвНП.Строки.Найти("НПФЛ", "Имя");
		
		Если УзелНПЮЛ <> Неопределено Тогда
			АтрибутыНПЮЛ = КонтекстЭДО.ПолучитьАтрибутыУзла(УзелНПЮЛ);
			
			Если АтрибутыНПЮЛ.Свойство("НаимОрг") Тогда
				НаимОрг = СокрЛП(АтрибутыНПЮЛ.НаимОрг);
			КонецЕсли;
			
			Если АтрибутыНПЮЛ.Свойство("ИННЮЛ") Тогда
				ИНН = СокрЛП(АтрибутыНПЮЛ.ИННЮЛ);
			КонецЕсли;
			
			Если АтрибутыНПЮЛ.Свойство("КПП") Тогда
				КПП = СокрЛП(АтрибутыНПЮЛ.КПП);
			КонецЕсли;
			
			УзелАдрОрг = УзелНПЮЛ.Строки.Найти("АдрОрг", "Имя");
			Если УзелАдрОрг <> Неопределено Тогда
				Адрес = СформироватьПредставлениеАдреса(УзелАдрОрг);
			КонецЕсли;
			
			ЭлементыФормы.ПанельСведенияОНалогоплательщике.ТекущаяСтраница = ЭлементыФормы.ПанельСведенияОНалогоплательщике.Страницы.ЮрЛицо;
			
		ИначеЕсли УзелНПФЛ <> Неопределено Тогда
			АтрибутыНПФЛ = КонтекстЭДО.ПолучитьАтрибутыУзла(УзелНПФЛ);
			
			Если АтрибутыНПФЛ.Свойство("ИННФЛ") Тогда
				ИНН = СокрЛП(АтрибутыНПФЛ.ИННФЛ);
			КонецЕсли;
			
			УзелФИО = УзелНПФЛ.Найти("ФИО", "Имя");
			Если УзелФИО <> Неопределено Тогда
				Адрес = СформироватьПредставлениеФИО(УзелФИО);
			КонецЕсли;
			
			УзелАдрМЖРФ = УзелНПФЛ.Строки.Найти("АдрМЖРФ", "Имя");
			Если УзелАдрМЖРФ <> Неопределено Тогда
				Адрес = СформироватьПредставлениеАдреса(УзелАдрМЖРФ);
			КонецЕсли;
			
			ЭлементыФормы.ПанельСведенияОНалогоплательщике.ТекущаяСтраница = ЭлементыФормы.ПанельСведенияОНалогоплательщике.Страницы.ФизЛицо;
			
		КонецЕсли;
		
	КонецЕсли;
	
	// заполняем результаты проверки
	УзлыРекНал = УзелСведУвед.Строки.НайтиСтроки(Новый Структура("Имя, Тип", "РекНал", "Э"));
	КоличествоРекомендаций = УзлыРекНал.Количество();
	Для Каждого УзелРекНал Из УзлыРекНал Цикл
		НовСтр = РезультатыПроверки.Добавить();
		НовСтр.ТекстСообщения = СокрЛП(УзелРекНал.Значение);
	КонецЦикла;
	
	СоответствиеКодовОшибокТекстам = СоответствиеКодовОшибокТекстам();
	
	УзлыСвПоОшибке = УзелСведУвед.Строки.НайтиСтроки(Новый Структура("Имя, Тип", "СвПоОшибке", "Э"));
	КоличествоОшибок = УзлыСвПоОшибке.Количество();
	Для Каждого УзелСвПоОшибке Из УзлыСвПоОшибке Цикл
		НовСтр = РезультатыПроверки.Добавить();
		НовСтр.ЭтоОшибка = Истина;
		Для Каждого Эл Из УзелСвПоОшибке.Строки Цикл
			ИмяУзла = Эл.Имя;
			Если ИмяУзла = "ПолОшЭл" Тогда
				НовСтр.Местоположение = СокрЛП(Эл.Значение);
			ИначеЕсли ИмяУзла = "ЗнЭлем" Тогда
				НовСтр.ОшибочноеЗначение = СокрЛП(Эл.Значение);
			ИначеЕсли ИмяУзла = "КодОшибки" Тогда
				НовСтр.КодОшибки = СокрЛП(Эл.Значение);
				Если НЕ ЗначениеЗаполнено(НовСтр.ТекстСообщения) Тогда
					ТекстСообщенияПоКоду = СоответствиеКодовОшибокТекстам[НовСтр.КодОшибки];
					Если ЗначениеЗаполнено(ТекстСообщенияПоКоду) Тогда
						НовСтр.ТекстСообщения = ТекстСообщенияПоКоду;
					КонецЕсли;
				КонецЕсли;
			ИначеЕсли ИмяУзла = "ТекстОш" Тогда
				Если ЗначениеЗаполнено(Эл.Значение) Тогда
					НовСтр.ТекстСообщения = СокрЛП(Эл.Значение);
				КонецЕсли;
			ИначеЕсли ИмяУзла = "ИдОш" Тогда
				НовСтр.Местоположение = СокрЛП(Эл.Значение);
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры

Функция СформироватьПредставлениеФИО(УзелФИО)
	
	АтрибутыФИО = КонтекстЭДО.ПолучитьАтрибутыУзла(УзелФИО);
	
	ИменаАтрибутов = Новый Массив;
	ИменаАтрибутов.Добавить("Фамилия");
	ИменаАтрибутов.Добавить("Имя");
	ИменаАтрибутов.Добавить("Отчество");
	
	СтрРезультат = "";
	Для Каждого ИмяАтрибута Из ИменаАтрибутов Цикл
		Если АтрибутыФИО.Свойство(ИмяАтрибута) Тогда
			ЗначениеАтрибута = СокрЛП(АтрибутыФИО[ИмяАтрибута]);
			Если ЗначениеЗаполнено(ЗначениеАтрибута) Тогда
				СтрРезультат = СтрРезультат + ЗначениеАтрибута + " ";
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Возврат СокрЛП(СтрРезультат);
	
КонецФункции

Функция СформироватьПредставлениеАдреса(УзелАдрОрг)
	
	АтрибутыАдрОрг = КонтекстЭДО.ПолучитьАтрибутыУзла(УзелАдрОрг);
	
	ИменаАтрибутов = Новый Массив;
	ИменаАтрибутов.Добавить("Индекс");
	ИменаАтрибутов.Добавить("КодРегион");
	ИменаАтрибутов.Добавить("Район");
	ИменаАтрибутов.Добавить("Город");
	ИменаАтрибутов.Добавить("НаселПункт");
	ИменаАтрибутов.Добавить("Улица");
	ИменаАтрибутов.Добавить("Дом");
	ИменаАтрибутов.Добавить("Корпус");
	ИменаАтрибутов.Добавить("Кварт");
	
	СтрРезультат = "";
	Для Каждого ИмяАтрибута Из ИменаАтрибутов Цикл
		Если АтрибутыАдрОрг.Свойство(ИмяАтрибута) Тогда
			ЗначениеАтрибута = СокрЛП(АтрибутыАдрОрг[ИмяАтрибута]);
			Если ЗначениеЗаполнено(ЗначениеАтрибута) Тогда
				СтрРезультат = СтрРезультат + ЗначениеАтрибута + ", ";
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Если Прав(СтрРезультат, 2) = ", " Тогда
		СтрРезультат = Лев(СтрРезультат, СтрДлина(СтрРезультат) - 2);
	КонецЕсли;
	
	Возврат СокрЛП(СтрРезультат);
	
КонецФункции

Функция СоответствиеКодовОшибокТекстам()
	
	МакетКодовОшибок = ПолучитьМакет("КодыОшибокУведомленияОбУточнении");
	Результат = Новый Соответствие;
	
	Для Инд = 1 По МакетКодовОшибок.ВысотаТаблицы Цикл
		Результат.Вставить(СокрЛП(МакетКодовОшибок.Область(Инд, 1, Инд, 1).Текст), СокрЛП(МакетКодовОшибок.Область(Инд, 2, Инд, 2).Текст));
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

Функция ПредставлениеПризнакаУточнения(ПрУточ)
	
	Если ПрУточ = "1" Тогда
		Возврат "Нет";
	ИначеЕсли ПрУточ = "2" Тогда
		Возврат "Да";
	Иначе
		Возврат "";
	КонецЕсли;
	
КонецФункции

Функция ПредставлениеНомерКорректировки(НомерКорректировки)
	
	Если НЕ ЗначениеЗаполнено(НомерКорректировки) ИЛИ НомерКорректировки = "0" Тогда
		Возврат "Первичный";
	Иначе
		Возврат "Корректирующий. Номер корректировки: " + НомерКорректировки + ".";
	КонецЕсли;
	
КонецФункции

Функция ПредставлениеДатыПриема(СтрДатаПриема)
	
	Попытка
		Возврат Формат(Дата(СтрЗаменить(СтрДатаПриема, "-", "")), "ДЛФ=D");
	Исключение
		Возврат СтрДатаПриема;
	Конецпопытки
	
КонецФункции

Процедура РезультатыПроверкиПриПолученииДанных(Элемент, ОформленияСтрок)
	
	Для Каждого ОформлениеСтроки Из ОформленияСтрок Цикл
		ДанныеСтроки = ОформлениеСтроки.ДанныеСтроки;
		Если ДанныеСтроки <> Неопределено Тогда
			
			НомерСтроки = ДанныеСтроки.Владелец().Индекс(ДанныеСтроки);
			ОформлениеСтроки.Ячейки.НомерСтроки.УстановитьТекст(НомерСтроки + 1);
			
			Если ДанныеСтроки.ЭтоОшибка Тогда
				ОформлениеСтроки.ЦветТекста = КрасныйЦвет;
			КонецЕсли;
			
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

КрасныйЦвет = Новый Цвет(255, 0, 0);