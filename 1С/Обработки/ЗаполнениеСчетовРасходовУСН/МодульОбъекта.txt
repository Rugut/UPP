Перем мДата;
Перем мОрганизация;
Перем мИтерация;
Перем мИтерацийВсего;

Функция ВыполнитьЗапросПоРасходамУСН()
	
	СтатусыОплатыРасходовУСН = Новый Массив;
	СтатусыОплатыРасходовУСН.Добавить(Перечисления.СтатусыРасходовУСН.НеСписано);
	СтатусыОплатыРасходовУСН.Добавить(Перечисления.СтатусыРасходовУСН.НеСписаноНеОплачено);
	СтатусыОплатыРасходовУСН.Добавить(Перечисления.СтатусыРасходовУСН.НеСписаноНеРаспределено);
	СтатусыОплатыРасходовУСН.Добавить(Перечисления.СтатусыРасходовУСН.НеСписаноПринято);
	
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Дата",							Новый Граница(мДата, ВидГраницы.Включая));
	Запрос.УстановитьПараметр("Организация",					мОрганизация);
	Запрос.УстановитьПараметр("ВидРасхода",						Перечисления.ВидыРасходовУСН.Номенклатура);
	Запрос.УстановитьПараметр("СтатусыПартийУСНКупленные",		Перечисления.СтатусыПартийУСН.Купленные);
	Запрос.УстановитьПараметр("СтатусыПартийУСНВПереработке",	Перечисления.СтатусыПартийУСН.ВПереработке);
	Запрос.УстановитьПараметр("СтатусыОплатыРасходовУСН",		СтатусыОплатыРасходовУСН);
	Запрос.УстановитьПараметр("СчетУчета",						ПланыСчетов.Хозрасчетный.ПустаяСсылка());
	Запрос.Текст =
   	"ВЫБРАТЬ
   	|	РасходыПриУСНОстатки.Организация 									КАК Организация,
   	|	РасходыПриУСНОстатки.ВидРасхода 									КАК ВидРасхода,
   	|	РасходыПриУСНОстатки.ЭлементРасхода 								КАК ЭлементРасхода,
   	|	РасходыПриУСНОстатки.СчетУчета 										КАК СчетУчета,
   	|	РасходыПриУСНОстатки.Валюта 										КАК Валюта,
   	|	РасходыПриУСНОстатки.ДоговорКонтрагента 							КАК ДоговорКонтрагента,
   	|	РасходыПриУСНОстатки.РасчетныйДокумент 								КАК РасчетныйДокумент,
   	|	РасходыПриУСНОстатки.СтатусыПартийУСН 								КАК СтатусыПартийУСН,
   	|	РасходыПриУСНОстатки.Партия 										КАК Партия,
   	|	РасходыПриУСНОстатки.ОтражениеВУСН									КАК ОтражениеВУСН,
   	|	РасходыПриУСНОстатки.СтатусыОплатыРасходовУСН 						КАК СтатусыОплатыРасходовУСН,
   	|	РасходыПриУСНОстатки.НомерСтрокиДокумента 							КАК НомерСтрокиДокумента,
	|	СУММА(РасходыПриУСНОстатки.КоличествоОстаток) 						КАК Количество,
	|	СУММА(РасходыПриУСНОстатки.СуммаОстаток)							КАК Сумма,
	|	СУММА(РасходыПриУСНОстатки.НДСОстаток)								КАК НДС
   	|ИЗ
   	|	РегистрНакопления.РасходыПриУСН.Остатки(&Дата,
   	|			Организация = &Организация
   	|		    И ВидРасхода = &ВидРасхода
   	|		    И СтатусыОплатыРасходовУСН В (&СтатусыОплатыРасходовУСН)) КАК РасходыПриУСНОстатки
	|
	|ГДЕ  РасходыПриУСНОстатки.СчетУчета = &СчетУчета
	|
	|СГРУППИРОВАТЬ ПО
	|	ЭлементРасхода,
	|	СтатусыПартийУСН,
	|	Партия,
   	|	СтатусыОплатыРасходовУСН,
  	|	Организация,
   	|	ВидРасхода,
   	|	СчетУчета,
   	|	Валюта,
   	|	ДоговорКонтрагента,
   	|	РасчетныйДокумент,
   	|	ОтражениеВУСН,
   	|	НомерСтрокиДокумента
	|
	|ИТОГИ ПО
	|	ЭлементРасхода,
	|	СтатусыПартийУСН,
	|	Партия,
	|	СтатусыОплатыРасходовУСН
	|";
	
	Возврат Запрос.Выполнить();
	
КонецФункции

Функция ВыполнитьЗапросПоПартиям()
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Дата",							Новый Граница(мДата, ВидГраницы.Включая));
	Запрос.УстановитьПараметр("Организация",					мОрганизация);
	Запрос.УстановитьПараметр("ПартииТоваровНаСкладахНалоговыйУчет","ПартииТоваровНаСкладахНалоговыйУчет");
	Запрос.УстановитьПараметр("ПартииТоваровПереданныеНалоговыйУчет","ПартииТоваровПереданныеНалоговыйУчет");
	Запрос.Текст = 
	
	"ВЫБРАТЬ
	|	&ПартииТоваровНаСкладахНалоговыйУчет								КАК Регистр,	
	|	ПартииТоваровНаСкладахНалоговыйУчетОстатки.Номенклатура.Ссылка	 	КАК Номенклатура,
	|	ПартииТоваровНаСкладахНалоговыйУчетОстатки.СчетУчета.Ссылка 		КАК СчетУчета,
	|	СУММА(ПартииТоваровНаСкладахНалоговыйУчетОстатки.КоличествоОстаток) КАК Количество,
	|	ПартииТоваровНаСкладахНалоговыйУчетОстатки.ДокументОприходования 	КАК ДокументОприходования
	|ИЗ
	|	РегистрНакопления.ПартииТоваровНаСкладахНалоговыйУчет.Остатки(&Дата, Организация = &Организация) КАК ПартииТоваровНаСкладахНалоговыйУчетОстатки
	|
	|СГРУППИРОВАТЬ ПО
	|	ПартииТоваровНаСкладахНалоговыйУчетОстатки.Номенклатура.Ссылка,
	|	ПартииТоваровНаСкладахНалоговыйУчетОстатки.ДокументОприходования,
	|	ПартииТоваровНаСкладахНалоговыйУчетОстатки.СчетУчета.Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&ПартииТоваровПереданныеНалоговыйУчет									КАК Регистр,	
	|	ПартииТоваровПереданныеНалоговыйУчетОстатки.Номенклатура.Ссылка			КАК Номенклатура,
	|	ПартииТоваровПереданныеНалоговыйУчетОстатки.СчетУчета.Ссылка          	КАК СчетУчета,
	|	СУММА(ПартииТоваровПереданныеНалоговыйУчетОстатки.КоличествоОстаток)  	КАК Количество,
	|	ПартииТоваровПереданныеНалоговыйУчетОстатки.ДокументОприходования       КАК ДокументОприходования
	|ИЗ
	|	РегистрНакопления.ПартииТоваровПереданныеНалоговыйУчет.Остатки(&Дата, Организация = &Организация) КАК ПартииТоваровПереданныеНалоговыйУчетОстатки
	|
	|СГРУППИРОВАТЬ ПО
	|	ПартииТоваровПереданныеНалоговыйУчетОстатки.Номенклатура.Ссылка,
	|	ПартииТоваровПереданныеНалоговыйУчетОстатки.ДокументОприходования,
	|	ПартииТоваровПереданныеНалоговыйУчетОстатки.СчетУчета.Ссылка
	|";
	 
	Возврат Запрос.Выполнить();
	
КонецФункции	

Процедура РаспределитьПоКоличеству(ТаблицаПрихода,НайденныеСтроки,СтрокаДереваРасходовУСН)
	
	ИтогоКоличество = 0;
	Для Каждого Строка Из НайденныеСтроки Цикл
		ИтогоКоличество = ИтогоКоличество + Строка.Количество;
	КонецЦикла;
	
	Для Каждого Строка Из НайденныеСтроки Цикл
		Коэффициент = Строка.Количество/ИтогоКоличество;
		ДобавитьВТаблицуПрихода(Таблицаприхода,СтрокаДереваРасходовУСН,Коэффициент,Строка.СчетУчета);
	КонецЦикла;	
	
Конецпроцедуры	

Процедура ДобавитьВТаблицуПрихода(ТаблицаПрихода,СтрокаДереваРасходовУСН,Коэффициент,СчетУчета)
	
	НоваяСтрока 							= ТаблицаПрихода.Добавить();
	НоваяСтрока.Организация					= СтрокаДереваРасходовУСН.Организация; 
	НоваяСтрока.ВидРасхода 	                = СтрокаДереваРасходовУСН.ВидРасхода;
	НоваяСтрока.ЭлементРасхода              = СтрокаДереваРасходовУСН.ЭлементРасхода;
	НоваяСтрока.СчетУчета                   = СчетУчета;
	НоваяСтрока.Валюта 	                    = СтрокаДереваРасходовУСН.Валюта;
	НоваяСтрока.ДоговорКонтрагента          = СтрокаДереваРасходовУСН.ДоговорКонтрагента;
	НоваяСтрока.РасчетныйДокумент           = СтрокаДереваРасходовУСН.РасчетныйДокумент;
	НоваяСтрока.СтатусыПартийУСН            = СтрокаДереваРасходовУСН.СтатусыПартийУСН;
	НоваяСтрока.Партия                      = СтрокаДереваРасходовУСН.Партия;
	НоваяСтрока.ОтражениеВУСН               = СтрокаДереваРасходовУСН.ОтражениеВУСН;
	НоваяСтрока.СтатусыОплатыРасходовУСН    = СтрокаДереваРасходовУСН.СтатусыОплатыРасходовУСН;
	НоваяСтрока.НомерСтрокиДокумента        = СтрокаДереваРасходовУСН.НомерСтрокиДокумента;
	НоваяСтрока.Количество			        = СтрокаДереваРасходовУСН.Количество*Коэффициент;
	НоваяСтрока.Сумма 		        		= СтрокаДереваРасходовУСН.Сумма*Коэффициент;
	НоваяСтрока.НДС                 		= СтрокаДереваРасходовУСН.НДС*Коэффициент;

	
Конецпроцедуры	

Процедура ДобавитьВТаблицуРасхода(ТаблицаРасхода,СтрокаДереваРасходовУСН)
	
	НоваяСтрока 							= ТаблицаРасхода.Добавить();
	НоваяСтрока.Организация					= СтрокаДереваРасходовУСН.Организация; 
	НоваяСтрока.ВидРасхода 	                = СтрокаДереваРасходовУСН.ВидРасхода;
	НоваяСтрока.ЭлементРасхода              = СтрокаДереваРасходовУСН.ЭлементРасхода;
	НоваяСтрока.Валюта 	                    = СтрокаДереваРасходовУСН.Валюта;
	НоваяСтрока.ДоговорКонтрагента          = СтрокаДереваРасходовУСН.ДоговорКонтрагента;
	НоваяСтрока.РасчетныйДокумент           = СтрокаДереваРасходовУСН.РасчетныйДокумент;
	НоваяСтрока.СтатусыПартийУСН            = СтрокаДереваРасходовУСН.СтатусыПартийУСН;
	НоваяСтрока.Партия                      = СтрокаДереваРасходовУСН.Партия;
	НоваяСтрока.ОтражениеВУСН	            = СтрокаДереваРасходовУСН.ОтражениеВУСН;
	НоваяСтрока.СтатусыОплатыРасходовУСН    = СтрокаДереваРасходовУСН.СтатусыОплатыРасходовУСН;
	НоваяСтрока.НомерСтрокиДокумента        = СтрокаДереваРасходовУСН.НомерСтрокиДокумента;
	НоваяСтрока.Количество			        = СтрокаДереваРасходовУСН.Количество;
	НоваяСтрока.Сумма 		        		= СтрокаДереваРасходовУСН.Сумма;
	НоваяСтрока.НДС                 		= СтрокаДереваРасходовУСН.НДС;
	
КонецПроцедуры	

Процедура СформироватьДокумент(ТаблицаПрихода,ТаблицаРасхода)
	
	Документ = Документы.КорректировкаЗаписейРегистров.СоздатьДокумент();
	
	НоваяСтрока = Документ.ТаблицаРегистровНакопления.Добавить();
	НоваяСтрока.Имя = "РасходыПриУСН";
	НоваяСтрока.Представление = "Расходы при УСН";
	
	Если ТаблицаРасхода.Количество() = 0 Тогда
		Сообщить("Счета учета уже установлены. Документ <Корректировка записей регистров> не создан.");
		Возврат
	КонецЕсли;
	
    РасходыПриУСН 					= Документ.Движения.РасходыПриУСН;
	РасходыПриУСН.мТаблицаДвижений 	= ТаблицаРасхода;
	РасходыПриУСН.мПериод			= мДата;
	РасходыПриУСН.ВыполнитьРасход();
	
	РасходыПриУСН.мТаблицаДвижений = ТаблицаПрихода;
	РасходыПриУСН.ВыполнитьПриход();

	Документ.Дата = мДата;
	#Если Клиент Тогда
	Документ.Ответственный = глЗначениеПеременной("глТекущийПользователь");
	#КонецЕсли
	Документ.Записать();
	#Если Клиент Тогда
	Предупреждение("Счета учета установлены документом
					|"+Документ.Ссылка);
	#КонецЕсли
КонецПроцедуры	

Процедура ОбновитьСостояние()
	
	#Если Клиент Тогда
	
		мИтерация	= мИтерация + 1;
		Процент 	= Окр((мИтерация/мИтерацийВсего*100),2,1);
		Состояние("Осталось "+Процент+"%");	
	
	#КонецЕсли

КонецПроцедуры	

Процедура ЗаполнитьСчетаУчетаРасходовУСН(Дата,Организация) Экспорт
	
	мДата 			= НачалоДня(Дата)-1;
	мОрганизация	= Организация;
	
	
	РезультатРасходыприУСН	= ВыполнитьЗапросПоРасходамУСН();

	Если РезультатРасходыПриУСН.Пустой() Тогда
		Сообщить("Счета учета уже установлены. Документ <Корректировка записей регистров накопления> не создан.");
		Возврат
	КонецЕсли;	

	ТаблицаРасхода			= РезультатРасходыПриУСН.Выгрузить(ОбходРезультатаЗапроса.Прямой);	
	ТаблицаПрихода			= ТаблицаРасхода.Скопировать();
	мИтерация				= 0;
	мИтерацийВсего			= ТаблицаРасхода.Количество();		
	
	ТаблицаПартий	 		= ВыполнитьЗапросПоПартиям().Выгрузить(ОбходРезультатаЗапроса.Прямой);	
	Выборка					= РезультатРасходыприУСН.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
    ТаблицаРасхода.Очистить();
	ТаблицаПрихода.Очистить();
	СтруктураОтбора			= Новый Структура;
	
	Пока Выборка.Следующий() Цикл
				
		ЭлементРасхода 		= Выборка.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		
		ОбновитьСостояние();
		
		Пока ЭлементРасхода.Следующий() Цикл
			
			СтатусыПартийУСН = ЭлементРасхода.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			
			ОбновитьСостояние();
			
			Пока СтатусыПартийУСН.Следующий() Цикл
				
				Партия =  СтатусыПартийУСН.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
				
				ОбновитьСостояние();

				Пока Партия.Следующий() Цикл
				
					СтруктураОтбора.Очистить();
					СтруктураОтбора.Вставить("Номенклатура",ЭлементРасхода.ЭлементРасхода);
					СтруктураОтбора.Вставить("ДокументОприходования",Партия.партия);
					СтруктураОтбора.Вставить("Регистр",?(Партия.СтатусыПартийУСН = Перечисления.СтатусыПартийУСН.Купленные,"ПартииТоваровНаСкладахНалоговыйУчет","ПартииТоваровПереданныеНалоговыйУчет"));
					НайденныеСтроки = ТаблицаПартий.НайтиСтроки(СтруктураОтбора);
			
					СтатусыОплатыРасходовУСН = Партия.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
				
					Пока СтатусыОплатыРасходовУСН.Следующий() Цикл
						
						ОбновитьСостояние();

						Если НайденныеСтроки.Количество() > 1 Тогда	
							РаспределитьПоКоличеству(ТаблицаПрихода,НайденныеСтроки,СтатусыОплатыРасходовУСН);
							ДобавитьВТаблицуРасхода(ТаблицаРасхода,СтатусыОплатыРасходовУСН);
						ИначеЕсли НайденныеСтроки.Количество() = 1 Тогда	
							ДобавитьВТаблицуПрихода(ТаблицаПрихода,СтатусыОплатыРасходовУСН,1,НайденныеСтроки[0].СчетУчета);
							ДобавитьВТаблицуРасхода(ТаблицаРасхода,СтатусыОплатыРасходовУСН);
						Иначе
							Сообщить("Данные по партиям для элемента "+СтатусыОплатыРасходовУСН.ЭлементРасхода+" различны между регистрами <Расходы при УСН> и регистрами партий НУ");
						КонецЕсли;
						
					КонецЦикла;
					
				КонецЦикла;	
					
			КонецЦикла;
			
		КонецЦикла;	
		
	КонецЦикла;	
	
	СформироватьДокумент(ТаблицаПрихода,ТаблицаРасхода);

Конецпроцедуры