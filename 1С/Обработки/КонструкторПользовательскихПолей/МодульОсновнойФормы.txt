Процедура РедактироватьПользовательскоеПоле(Поле)
	
	ИмяФормы = ТиповыеОтчеты.ПолучитьИмяФормыРедактированияПользовательскогоПоля(Поле);
	Если ИмяФормы = Неопределено Тогда
		Возврат;
	КонецЕсли;
	Форма = ПолучитьФорму(ИмяФормы, ЭтаФорма);
	Форма.КомпоновщикНастроек = КомпоновщикНастроек;
	Форма.ПользовательскоеПоле = Поле;
	РезультатОткрытия = Форма.ОткрытьМодально();
 
КонецПроцедуры

Процедура ПользовательскиеПоляВыбор(Элемент, ВыбраннаяСтрока, Колонка, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	РедактироватьПользовательскоеПоле(ВыбраннаяСтрока);
	
КонецПроцедуры

Процедура КоманднаяПанельПользовательскиеПоляДобавить(Кнопка)
	
	Если Кнопка.Имя = "Тренд" Тогда
		Форма = ПолучитьФорму("ФормаИнтервал", ЭтаФорма);
		Форма.Режим = "Тренд";
	ИначеЕсли Кнопка.Имя = "Состояние" Тогда
		Форма = ПолучитьФорму("ФормаИнтервал", ЭтаФорма);
		Форма.Режим = "Состояние";
	ИначеЕсли Кнопка.Имя = "ИнтервальныеГруппировки" Тогда
		Форма = ПолучитьФорму("ФормаИнтервалы", ЭтаФорма);
		Форма.Режим = "";
	ИначеЕсли Кнопка.Имя = "ABCКлассификация" Тогда
		Форма = ПолучитьФорму("ФормаABCКлассификация", ЭтаФорма);
		Форма.Режим = "";
	ИначеЕсли Кнопка.Имя = "Формула" Тогда
		Форма = ПолучитьФорму("ФормаФормула", ЭтаФорма);
	КонецЕсли;
	Форма.КомпоновщикНастроек = КомпоновщикНастроек;
	
	РезультатОткрытия = Форма.ОткрытьМодально();
	Если РезультатОткрытия = Истина Тогда
		Если Кнопка.Имя = "Тренд" ИЛИ Кнопка.Имя = "Состояние" Тогда
			НовоеПоле = КомпоновщикНастроек.Настройки.Выбор.Элементы.Добавить(Тип("ВыбранноеПолеКомпоновкиДанных"));
			НовоеПоле.Поле = Новый ПолеКомпоновкиДанных(Форма.ПользовательскоеПоле.ПутьКДанным);
		ИначеЕсли Кнопка.Имя = "ИнтервальныеГруппировки" Тогда
			// Добавить поле в группировку
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередОткрытием(Отказ, СтандартнаяОбработка)
	
	Если КомпоновщикНастроек.Настройки.Выбор.ДоступныеПоляВыбора.Элементы.Количество() = 0 Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	Действие = Новый Действие("КоманднаяПанельПользовательскиеПоляДобавить");
	Кнопки = ЭлементыФормы.КоманднаяПанельПользовательскиеПоля.Кнопки.Добавить.Кнопки;
	Если ПроцедурыПроизвольныхОтчетов.ЭтоПроизвольныйОтчет(ОтчетОбъект) Тогда
		Кнопки.Добавить("Тренд", ТипКнопкиКоманднойПанели.Действие, "Тренд", Действие);
		Кнопки.Добавить("Состояние", ТипКнопкиКоманднойПанели.Действие, "Состояние", Действие);
		Кнопки.Добавить("ABCКлассификация", ТипКнопкиКоманднойПанели.Действие, "ABC - классификация", Действие);
	Иначе
		Кнопки.Добавить("ИнтервальныеГруппировки", ТипКнопкиКоманднойПанели.Действие, "Интервальные группировки", Действие);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПользовательскиеПоляПередНачаломИзменения(Элемент, Отказ)
	
	Отказ = Истина;
	РедактироватьПользовательскоеПоле(ЭлементыФормы.ПользовательскиеПоля.ТекущиеДанные);
	
КонецПроцедуры