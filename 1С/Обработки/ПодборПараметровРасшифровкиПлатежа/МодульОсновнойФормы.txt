Перем ОбычныйШрифт;
Перем ЖирныйШрифт;


Процедура ПриОткрытии()
	
	ЭтаФорма.Заголовок=?(ТипЗадолженности=">0","Дебиторская","Кредиторская")+" задолженность. Подбор для контрагента "+Контрагент;
	
	ЗапрашиватьСуммуПлатежа       		= ВосстановитьЗначение("ЗапрашиватьСуммуПлатежаПриПодборе");
	ЗапрашиватьСуммуВзаиморасчетов      = ВосстановитьЗначение("ЗапрашиватьСуммуВзаиморасчетовПриПодборе");
	ЗапрашиватьКурсВзаиморасчетов       = ВосстановитьЗначение("ЗапрашиватьКурсВзаиморасчетовПриПодборе");
	ЗапрашиватьДокументПланирования     = ВосстановитьЗначение("ЗапрашиватьПланируемоеДвижениеПриПодборе");
	
	Если НЕ (УчитыватьФактическиеЗадолженности ИЛИ УчитыватьОперативныеЗадолженности) Тогда УчитыватьФактическиеЗадолженности=Истина КонецЕсли;
	
	Если ТипЗадолженности=">0" Тогда
		ЭлементыФормы.ЗапрашиватьДокументПланирования.Заголовок="Планируемое поступление денежных средств";
	Иначе
		ЭлементыФормы.ЗапрашиватьДокументПланирования.Заголовок="Заявка на расходование средств";
	КонецЕсли;
	
	ТекстВидОперации=ВидОперацииДок.Метаданные().Имя;
	
	Если ТекстВидОперации="ВидыОперацийЗаявкиНаРасходование"
		ИЛИ ТекстВидОперации="ВидыОперацийПланируемоеПоступлениеДС" Тогда
		ЭлементыФормы.ЗапрашиватьДокументПланирования.Видимость=Ложь;
		
	Иначе	
		ЭлементыФормы.ЗапрашиватьДокументПланирования.Видимость=Истина;
	КонецЕсли;
	
	СформироватьСписокДолгов();
		
КонецПроцедуры

Процедура ЗадолженностьДляОплатыВыбор(Элемент, ВыбраннаяСтрока, Колонка, СтандартнаяОбработка)
	
	Перем КурсВзаиморасчетов,КратностьВзаиморасчетов,СуммаПлатежа,СуммаВзаиморасчетов;
		
	Если ТипЗнч(ВыбраннаяСтрока)=Тип("Массив") Тогда
		СтрокаДоговор=ВыбраннаяСтрока[0];
	Иначе
		СтрокаДоговор=ВыбраннаяСтрока;
	КонецЕсли;
	
	ДоговорКонтрагента=СтрокаДоговор.ДоговорКонтрагента;
	Сделка=СтрокаДоговор.Сделка;
	ДокументРасчетовСКонтрагентом=СтрокаДоговор.ДокументРасчетовСКонтрагентом;
	КратностьВзаиморасчетов=СтрокаДоговор.КратностьВзаиморасчетов;
	
	СтруктураПодбора=Новый Структура;
	
	Если ЗапрашиватьКурсВзаиморасчетов 
		ИЛИ ЗапрашиватьСуммуПлатежа
		ИЛИ ЗапрашиватьСуммуВзаиморасчетов 
		ИЛИ ЗапрашиватьДокументПланирования Тогда
		
		Если ЗапрашиватьДокументПланирования И ЭлементыФормы.ЗапрашиватьДокументПланирования.Видимость Тогда
			ФормаВводПараметров = ПолучитьФорму("ВводПланируемогоДвиженияСуммыИКурса", ЭтаФорма);
		Иначе
			ФормаВводПараметров = ПолучитьФорму("ВводСуммыИКурса", ЭтаФорма);
		КонецЕсли;
		
		ФормаВводПараметров.КурсВзаиморасчетов=СтрокаДоговор.КурсВзаиморасчетов;
		ФормаВводПараметров.КратностьВзаиморасчетов=КратностьВзаиморасчетов;
		ФормаВводПараметров.СуммаПлатежа=СтрокаДоговор.СуммаПлатежа;
		ФормаВводПараметров.СуммаВзаиморасчетов=СтрокаДоговор.СуммаВзаиморасчетов;
		ФормаВводПараметров.ДатаПлатежа=ДатаДок;
	
		СтруктураПараметров = ФормаВводПараметров.ОткрытьМодально();
		
		Если ТипЗнч(СтруктураПараметров) = Тип("Структура") Тогда
			СуммаПлатежа=СтруктураПараметров.СуммаПлатежа;
			КурсВзаиморасчетов=СтруктураПараметров.КурсВзаиморасчетов;
			СуммаВзаиморасчетов=СтруктураПараметров.СуммаВзаиморасчетов;
			
			Если СтруктураПараметров.Свойство("ДокументПланирования") Тогда
				
				СтруктураПодбора.Вставить("ДокументПланирования",СтруктураПараметров.ДокументПланирования);
				СтруктураПодбора.Вставить("СтатьяДвиженияДенежныхСредств",СтатьяДвиженияДенежныхСредств);
				СтруктураПодбора.Вставить("Проект",Проект);
				СтруктураПодбора.Вставить("СуммаПлатежаПлан",СтруктураПараметров.СуммаПлатежаПлан);
				СтруктураПодбора.Вставить("КурсВзаиморасчетовПлан",СтруктураПараметров.КурсВзаиморасчетовПлан);
				
			КонецЕсли;
			
			СтруктураПодбора.Вставить("ДоговорКонтрагента",ДоговорКонтрагента);
			СтруктураПодбора.Вставить("Сделка",Сделка);
			СтруктураПодбора.Вставить("ДокументРасчетовСКонтрагентом",ДокументРасчетовСКонтрагентом);
			СтруктураПодбора.Вставить("СуммаПлатежа",СуммаПлатежа);
			СтруктураПодбора.Вставить("КурсВзаиморасчетов",КурсВзаиморасчетов);
			СтруктураПодбора.Вставить("КратностьВзаиморасчетов",КратностьВзаиморасчетов);
			СтруктураПодбора.Вставить("СуммаВзаиморасчетов",СуммаВзаиморасчетов);
			
			СтруктураПодбора.Вставить("ЕстьПодбор",ЕстьПодбор);
			
			РасшифровкаПлатежа.Удалить(СтрокаДоговор);
			ОповеститьОВыборе(СтруктураПодбора);
				
		ИначеЕсли СтруктураПараметров="АвтоПодбор" Тогда
			
			РасшифровкаПлатежа.Удалить(СтрокаДоговор);
			ОповеститьОВыборе(СтруктураПараметров);
			
		Иначе
			
			Возврат; // форма ввода параметров расшифровки закрыта не по кнопке "ОК"
			
		КонецЕсли;
		
	Иначе
		
		СуммаПлатежа=СтрокаДоговор.СуммаПлатежа;
		КурсВзаиморасчетов=СтрокаДоговор.КурсВзаиморасчетов;
		СуммаВзаиморасчетов=СтрокаДоговор.СуммаВзаиморасчетов;
		
		СтруктураПодбора.Вставить("ДоговорКонтрагента",ДоговорКонтрагента);
		СтруктураПодбора.Вставить("Сделка",Сделка);
		СтруктураПодбора.Вставить("ДокументРасчетовСКонтрагентом",ДокументРасчетовСКонтрагентом);
		СтруктураПодбора.Вставить("СуммаПлатежа",СуммаПлатежа);
		СтруктураПодбора.Вставить("КурсВзаиморасчетов",КурсВзаиморасчетов);
		СтруктураПодбора.Вставить("КратностьВзаиморасчетов",КратностьВзаиморасчетов);
		СтруктураПодбора.Вставить("СуммаВзаиморасчетов",СуммаВзаиморасчетов);
		
		СтруктураПодбора.Вставить("ЕстьПодбор",ЕстьПодбор);
		
		РасшифровкаПлатежа.Удалить(СтрокаДоговор);	
		ОповеститьОВыборе(СтруктураПодбора);
		
	КонецЕсли;
				
КонецПроцедуры

Процедура ПриЗакрытии()
	
	СохранитьЗначение("ЗапрашиватьСуммуПлатежаПриПодборе", ЗапрашиватьСуммуПлатежа);
	СохранитьЗначение("ЗапрашиватьСуммуВзаиморасчетовПриПодборе", ЗапрашиватьСуммуВзаиморасчетов);
	СохранитьЗначение("ЗапрашиватьПланируемоеДвижениеПриПодборе", ЗапрашиватьДокументПланирования);
	СохранитьЗначение("ЗапрашиватьКурсВзаиморасчетовПриПодборе", ЗапрашиватьКурсВзаиморасчетов);
	
КонецПроцедуры

Процедура УчитыватьФактическиеЗадолженностиПриИзменении(Элемент)
	
	СформироватьСписокДолгов();

КонецПроцедуры

Процедура УчитыватьОперативныеЗадолженностиПриИзменении(Элемент)
	
	СформироватьСписокДолгов();
	
КонецПроцедуры

Процедура ЗадолженностьДляОплатыПриВыводеСтроки(Элемент, ОформлениеСтроки, ДанныеСтроки)
	
	ОформлениеСтроки.Шрифт = ?(ДанныеСтроки.ДвиженияДокумента, ЖирныйШрифт, ОбычныйШрифт);
	
КонецПроцедуры

Процедура ЗадолженностьДляОплатыПриАктивизацииСтроки(Элемент)
	
	СуммаВыделено=0;
	
	Для Каждого Строка Из Элемент.ВыделенныеСтроки Цикл
		
		СуммаВыделено=СуммаВыделено+Строка.СуммаПлатежа;
		
	КонецЦикла;
	
	ЭлементыФормы.НадписьВыбрано.Заголовок="Всего выбрано: "+Формат(СуммаВыделено,"ЧЦ=15; ЧДЦ=2; ЧРГ=")+" "+Строка(ВалютаДокумента);
		
		
КонецПроцедуры

Процедура ПередОткрытием(Отказ, СтандартнаяОбработка)
	// Если не установлено значения объекта редактируемого документа,
	// то форму обработки открывать не нужно.
	Если ДокументСсылка = Неопределено Тогда
		Отказ = Истина;
		Предупреждение("Не задан документ для обработки! " 
		               + "Эта обработка вызывается из формы документа.", , Заголовок);
		Возврат;
	КонецЕсли;

КонецПроцедуры




ОбычныйШрифт = Новый Шрифт;
ЖирныйШрифт  = Новый Шрифт(ОбычныйШрифт,,,Истина);