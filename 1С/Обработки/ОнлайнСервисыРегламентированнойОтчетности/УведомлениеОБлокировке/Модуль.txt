Перем мПрограммноеЗакрытие;

Процедура ПередОткрытием(Отказ, СтандартнаяОбработка)
	
	Если ЗапретНаПродолжение Тогда
		ЭлементыФормы.ПанельПродолжить.Свертка = РежимСверткиЭлементаУправления.Низ;
		ЭлементыФормы.ПанельКнопки.ТекущаяСтраница = ЭлементыФормы.ПанельКнопки.Страницы.ЖесткаяБлокировка;
		ЭлементыФормы.КнопкаПроверитьОбновленияЖесткая.КнопкаПоУмолчанию = Истина;
	Иначе
		ЭлементыФормы.КнопкаПродолжить.Доступность = ПродолжитьДействие;
		ЭлементыФормы.ПанельКнопки.ТекущаяСтраница = ЭлементыФормы.ПанельКнопки.Страницы.МягкаяБлокировка;
		ЭлементыФормы.КнопкаПроверитьОбновления.КнопкаПоУмолчанию = Истина;
	КонецЕсли;
	
	ЗаполнитьHTML();
	
КонецПроцедуры

Процедура ПродолжитьДействиеПриИзменении(Элемент)
	
	ЭлементыФормы.КнопкаПродолжить.Доступность = ПродолжитьДействие;
	
КонецПроцедуры

Процедура ПередЗакрытием(Отказ, СтандартнаяОбработка)
	
	Если НЕ мПрограммноеЗакрытие Тогда
		мПрограммноеЗакрытие = Истина;
		Отказ = Истина;
		Закрыть(Ложь);
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьHTML()
	
	// заполняем
	ТекстЗаголовок = ПредставлениеОбъектаБлокировки(Объект);
	ТекстШапка = СформироватьТекстШапки();
	
	// сохраняем картинку во временный файл
	ИмяФайлаКартинки = ПолучитьИмяВременногоФайла(".bmp");
	Если ЗапретНаПродолжение Тогда
		БиблиотекаКартинок.Стоп48.Записать(ИмяФайлаКартинки);
	Иначе
		БиблиотекаКартинок.Внимание48.Записать(ИмяФайлаКартинки);
	КонецЕсли;
	
	// устанавливаем в HTML тексты и ссылку на картинку
	ТекстHTML = ПолучитьМакет("УведомлениеОБлокировкеОбъекта").ПолучитьТекст();
	ТекстHTML = СтрЗаменить(ТекстHTML, "ПутьККартинке",		ИмяФайлаКартинки);
	ТекстHTML = СтрЗаменить(ТекстHTML, "ТекстЗаголовок",	ТекстЗаголовок);
	ТекстHTML = СтрЗаменить(ТекстHTML, "ТекстШапка",		ТекстШапка);
	
	Если ЗначениеЗаполнено(ТекстПодробнее) Тогда
		ТекстHTML = СтрЗаменить(ТекстHTML, "ЗаголовокПодробнее",	"Дополнительная информация:");
		ТекстHTML = СтрЗаменить(ТекстHTML, "ТекстПодробнее",		ТекстПодробнее);
	Иначе
		ТекстHTML = СтрЗаменить(ТекстHTML, "ЗаголовокПодробнее",	"");
		ТекстHTML = СтрЗаменить(ТекстHTML, "ТекстПодробнее",		"");
	КонецЕсли;
	
	// заменяем
	ЭлементыФормы.ПолеHTMLДокументаСообщение.УстановитьТекст(ТекстHTML);
	
КонецПроцедуры

Функция СформироватьТекстШапки()
	
	Если БлокируемаяФункция = "И" Тогда // блокировка использования
		Если ТипЗнч(Объект) = Тип("Форма") Тогда
			Если ЗапретНаПродолжение Тогда
				ТекстШапка = "Использование данной формы регламентированной отчетности запрещено!";
			Иначе
				ТекстШапка = "Использование данной формы регламентированной отчетности не рекомендуется!";
			КонецЕсли;
		Иначе
			Если ЗапретНаПродолжение Тогда
				ТекстШапка = "Использование данного регламентированного отчета запрещено!";
			Иначе
				ТекстШапка = "Использование данного регламентированного отчета не рекомендуется!";
			КонецЕсли;
		КонецЕсли;
	ИначеЕсли БлокируемаяФункция = "П" Тогда // блокировка печати
		Если ЗапретНаПродолжение Тогда
			ТекстШапка = "Для данной формы регламентированной отчетности заблокирована функция печати.";
		Иначе
			ТекстШапка = "Для данной формы регламентированной отчетности не рекомендуется использовать функцию печати.";
		КонецЕсли;
	ИначеЕсли БлокируемаяФункция = "ПВ" Тогда // блокировка печати МЧБ
		Если ЗапретНаПродолжение Тогда
			ТекстШапка = "Для данной формы регламентированной отчетности заблокирована функция формирования машиночитаемого бланка.";
		Иначе
			ТекстШапка = "Для данной формы регламентированной отчетности не рекомендуется использовать функцию формирования машиночитаемого бланка.";
		КонецЕсли;
	ИначеЕсли БлокируемаяФункция = "В" Тогда // блокировка выгрузки
		Если ЗапретНаПродолжение Тогда
			ТекстШапка = "Для данной формы регламентированной отчетности заблокирована функция выгрузки.";
		Иначе
			ТекстШапка = "Для данной формы регламентированной отчетности не рекомендуется использовать функцию выгрузки.";
		КонецЕсли;
	ИначеЕсли БлокируемаяФункция = "З" Тогда // блокировка заполнения
		Если ЗапретНаПродолжение Тогда
			ТекстШапка = "Для данной формы регламентированной отчетности заблокирована функция автоматического заполнения по данным информационной базы.";
		Иначе
			ТекстШапка = "Для данной формы регламентированной отчетности не рекомендуется использовать функцию автоматического заполнения по данным информационной базы.";
		КонецЕсли;
	Иначе
		Если ЗапретНаПродолжение Тогда
			ТекстШапка = "Функция заблокирована!";
		Иначе
			ТекстШапка = "Использование функции не рекомендуется!";
		КонецЕсли;
	КонецЕсли;
	
	Возврат ТекстШапка;
	
КонецФункции

Процедура КнопкаЗакрытьНажатие(Элемент)
	
	Закрыть();
	
КонецПроцедуры

Процедура КнопкаСправкаНажатие(Элемент)
	
	ОткрытьСправку(Метаданные());
	
КонецПроцедуры

Процедура КнопкаПродолжитьНажатие(Элемент)
	
	мПрограммноеЗакрытие = Истина;
	Закрыть(Истина);
	
КонецПроцедуры

Процедура КнопкаПроверитьОбновленияНажатие(Элемент)
	
	ОткрытьФормуДоступныхОбновленийРО();
	
КонецПроцедуры

мПрограммноеЗакрытие = Ложь;