// Функция возвращает признак того, что клиент поддерживает работу с видом ТО,
// переданным в качестве параметра.
//
// Параметры:
//  Вид      - <ПеречислениеСсылка.ВидыТорговогоОборудования>
//           - Вид торгового оборудования, информация о поддержке
//             которого запрашивается.
//
// Возвращаемое значение:
//  <Булево> - Признак поддержки указанного класса торгового оборудования.
//
Функция ПоддерживаетсяВидТО(Вид) Экспорт

	Результат = Ложь;

	Если Вид = Перечисления.ВидыТорговогоОборудования.ФискальныйРегистратор
	 Или Вид = Перечисления.ВидыТорговогоОборудования.ЭквайринговаяСистема Тогда
		Результат = Истина;
	КонецЕсли;

	Возврат Результат;

КонецФункции // ПоддерживаетсяВидТО()

// Обработчик события "ПриОткрытии" формы.
//
Процедура ПриОткрытии()

	ПолучитьСерверТО().ПодключитьКлиента(ЭтаФорма);

КонецПроцедуры // ПриОткрытии()

// Обработчик события "ПриЗакрытии" формы.
//
Процедура ПриЗакрытии()

	ПолучитьСерверТО().ОтключитьКлиента(ЭтаФорма);

КонецПроцедуры // ПриЗакрытии()

Процедура ВыполнитьСверкуИтоговНажатие(Элемент)

	Перем ФР;

	МассивЭС = ПолучитьСерверТО().ПолучитьСписокУстройств(Перечисления.ВидыТорговогоОборудования.ЭквайринговаяСистема);

	КоличествоЭС = МассивЭС.Количество();
	
	Если КоличествоЭС = 0 Тогда
		Предупреждение("Не подключено ни одной эквайринговой системы!");
		Возврат;
	КонецЕсли;

	Для Тмп = 1 По КоличествоЭС Цикл
		ТекЭС = МассивЭС[КоличествоЭС - Тмп];

		Если Не ПолучитьСерверТО().ПоддерживаетсяСверкаИтогов(ТекЭС) Тогда
			МассивЭС.Удалить(КоличествоЭС - Тмп);
		КонецЕсли;
	КонецЦикла;

	Если МассивЭС.Количество() = 0 Тогда
		Предупреждение("Не подключено ни одной эквайринговой системы, требующей сверки итогов!");
		Возврат;
	КонецЕсли;

	Если Не РаботаСТорговымОборудованием.ПолучитьПроверитьПараметрыДляПробитияЧека(ФР) Тогда
		Возврат;
	КонецЕсли;

	Если МассивЭС.Количество() = 1 Тогда
		ТекМассивЭС = МассивЭС;
	Иначе
		ТекМассивЭС = Новый Массив;

		СписокЭС = РаботаСТорговымОборудованием.ПолучитьСписокУстройствТОДляВыбора(МассивЭС);
		СписокЭС.ЗаполнитьПометки(Истина);

		Если СписокЭС.ОтметитьЭлементы("Укажите эквайринговые системы") Тогда
			Для Каждого ТекЭС Из СписокЭС Цикл
				Если ТекЭС.Пометка Тогда
					ТекМассивЭС.Добавить(ТекЭС.Значение);
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;

	Для Каждого ЭС Из ТекМассивЭС Цикл
		ПолучитьСерверТО().ИтогиДняПоКартам(ЭС, ФР);
	КонецЦикла;

КонецПроцедуры // ВыполнитьСверкуИтоговНажатие()

// Обработчик нажатия кнопки "ОплатитьКартой".
//
Процедура ОплатитьКартойНажатие(Элемент)

	Перем ЭС;
	Перем ФР;

	Если РаботаСТорговымОборудованием.ПолучитьПроверитьПараметрыДляОплатыКартой(ЭС, ФР) Тогда
		КодRRN = Неопределено;

		Результат = ПолучитьСерверТО().ОплатитьПлатежнойКартой(ЭС, 0, КодRRN, ФР);

		Если ЗначениеЗаполнено(Результат) Тогда
			Ошибка = ПолучитьСерверТО().ПолучитьТекстОшибкиТО(Результат);
			Предупреждение(Ошибка);
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры // ОплатитьКартойНажатие()

// Обработчик нажатия кнопки "ОтменитьПлатежПоКарте".
//
Процедура ОтменитьПлатежПоКартеНажатие(Элемент)

	Перем ЭС;
	Перем ФР;

	Если РаботаСТорговымОборудованием.ПолучитьПроверитьПараметрыДляОплатыКартой(ЭС, ФР) Тогда
		КодRRN = Неопределено;

		Результат = ПолучитьСерверТО().ОтменитьПлатежПоПлатежнойКарте(ЭС, 0, КодRRN, ФР);

		Если ЗначениеЗаполнено(Результат) Тогда
			Ошибка = ПолучитьСерверТО().ПолучитьТекстОшибкиТО(Результат);
			Предупреждение(Ошибка);
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры // ОтменитьПлатежПоКартеНажатие()

// Обработчик нажатия кнопки "ВернутьПлатежПоКарте".
//
Процедура ВернутьПлатежПоКартеНажатие(Элемент)

	Перем ЭС;
	Перем ФР;

	Если РаботаСТорговымОборудованием.ПолучитьПроверитьПараметрыДляОплатыКартой(ЭС, ФР) Тогда
		КодRRN = Неопределено;

		Результат = ПолучитьСерверТО().ВернутьПлатежПоПлатежнойКарте(ЭС, 0, КодRRN, ФР);

		Если ЗначениеЗаполнено(Результат) Тогда
			Ошибка = ПолучитьСерверТО().ПолучитьТекстОшибкиТО(Результат);
			Предупреждение(Ошибка);
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры // ВернутьПлатежПоКартеНажатие()