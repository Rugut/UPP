Перем мСтруктураСтарыхЗначений;

Функция ПолучитьКоличествоСсылокНеобходимыхДляРегистрации(МетаданныеОбъекта, РегистрироватьВсе = Ложь)
	
	ЭтоРегистр = Ложь;
	ЭтоДокумент = Ложь;
	ИмяТаблицыДляПоискаЗарегистрированных = ПолучитьИмяОбъектаПодлежащегоРегистрацииПриПервомЗнакомствеПоМетаданным(МетаданныеОбъекта, , ЭтоРегистр, ЭтоДокумент);
	
	Если ПустаяСтрока(ИмяТаблицыДляПоискаЗарегистрированных) тогда
		Возврат 0;
	КонецЕсли;
	
	Запрос = Новый Запрос();
	
	ОграничениеВыборки = "";
				
	Состояние("Анализ количества объектов для сопоставления: " + ИмяТаблицыДляПоискаЗарегистрированных);
	
	ВозможныОрганиченияПоДате = ЭтоДокумент И ЗначениеЗаполнено(ДатаНачалаВыгрузкиДокументов);
		
	Если (НЕ РегистрироватьВсе)
		И ВозможныОрганиченияПоДате Тогда
		
		Если ВозможныОрганиченияПоДате Тогда
			
			ОграничениеВыборки = "";
			
			НужноЗарегистрироватьИзмененияДляВсехОбъектов = Ложь;
									
			
			Если ВозможныОрганиченияПоДате Тогда
				
				Если ПустаяСтрока(ОграничениеВыборки) Тогда
					ОграничениеВыборки = "ГДЕ РегистрацияИзменений.Дата >= &ДатаОграничений";
				Иначе
					ОграничениеВыборки = ОграничениеВыборки + "
					|И РегистрацияИзменений.Дата >= &ДатаОграничений";
				КонецЕсли;
								
			КонецЕсли;
			
			Запрос.УстановитьПараметр("ДатаОграничений", ДатаНачалаВыгрузкиДокументов);
			
		КонецЕсли;
			
	КонецЕсли;	
					   
	Запрос.Текст = "ВЫБРАТЬ Количество(*) КАК Количество
	   |ИЗ
	   |	" + ИмяТаблицыДляПоискаЗарегистрированных + " КАК РегистрацияИзменений
	   |" + ОграничениеВыборки;
		
	ТаблицаДанных = Запрос.Выполнить().Выгрузить();

	Если ТаблицаДанных.Количество() = 0 Тогда
		Возврат 0;
	Иначе
		
		Возврат ТаблицаДанных[0].Количество;
		
	КонецЕсли;
	
КонецФункции

Функция ОпределитьПоПлануОбменаНеобходимоеКоличествоОбъектовДляСопоставления(ИмяПлана, ТекстПравилОбмена)
	
	ИтоговоеКоличествоРегистрации = 0;
	
	Если Не ЗначениеЗаполнено(ИмяПлана) Тогда
		Возврат ИтоговоеКоличествоРегистрации;
	КонецЕсли;
	
	МетаданныеПланаОбмена = Метаданные.ПланыОбмена[ИмяПлана];
	
	// регистрация происходит только для тех объекто для которых есть правила выгрузки данных
	ОбработкаОбменаДаннымиXML = Обработки.ОбменДаннымиXML.Создать();
		
	ОбработкаОбменаДаннымиXML.ЗагрузитьПравилаОбмена(ТекстПравилОбмена, "Строка");
	
	ДеревоИнформацииОВыгрузке = ОбработкаОбменаДаннымиXML.ТаблицаПравилВыгрузки;
	
	
	Для Каждого ЭлементСостава из МетаданныеПланаОбмена.Состав Цикл
		
		МетаданныеДляОбмена = ЭлементСостава.Метаданные;
		
		ЭтоРегистр = Ложь;
		ИмяСсылочногоТипа = ПолучитьИмяОбъектаПодлежащегоРегистрацииПриПервомЗнакомствеПоМетаданным(МетаданныеДляОбмена,,ЭтоРегистр);
	
		Если ПустаяСтрока(ИмяСсылочногоТипа) Тогда
			Продолжить;
		КонецЕсли;
		
		Если НЕ ЭтоРегистр Тогда
			СтрокаДерева = ДеревоИнформацииОВыгрузке.Строки.Найти(ИмяСсылочногоТипа, "ИмяОбъектаДляЗапроса", Истина);
		Иначе
			СтрокаДерева = ДеревоИнформацииОВыгрузке.Строки.Найти(ИмяСсылочногоТипа, "ИмяОбъектаДляЗапросаРегистра", Истина);
		КонецЕсли;
		
		ЕстьПравилоВыгрузкиДанных = СтрокаДерева <> Неопределено;
		
		Если НЕ ЕстьПравилоВыгрузкиДанных Тогда
			Продолжить;
		КонецЕсли;
		
		Если СтрокаДерева.Включить <> 1 Тогда
			Продолжить;
		КонецЕсли;		
		
		РегистрироватьВсе = (ЭлементСостава.АвтоРегистрация = АвтоРегистрацияИзменений.Разрешить);
		
		КоличествоЗарегистрированныхЭлеменетов = ПолучитьКоличествоСсылокНеобходимыхДляРегистрации(МетаданныеДляОбмена, РегистрироватьВсе);
		
		ИтоговоеКоличествоРегистрации = ИтоговоеКоличествоРегистрации + КоличествоЗарегистрированныхЭлеменетов;
				
	КонецЦикла;
	
	Состояние("");
	
	Возврат ИтоговоеКоличествоРегистрации;
	
КонецФункции

Процедура ВыполнитьПодсчетОбъектовДляКоторыхНужноСопоставлениеНажатие(Элемент)
	
	// подсчет объектов для сопоставления
	ОсновныеПравилаОбмена = ОпределитьПравилаОбменаДляВыгрузкиИзТекущейИнформационнойБазыВПриемник(ИнформацияОТипеОбмена);
	мКоличествоОбъектовДляСопоставления = ОпределитьПоПлануОбменаНеобходимоеКоличествоОбъектовДляСопоставления(ТипИнформационнойБазыДляОбмена, ОсновныеПравилаОбмена);
	
	УстановитьНадписьКоличестваОбъектовДляСопоставления();
	
КонецПроцедуры

Процедура УстановитьНадписьКоличестваОбъектовДляСопоставления()
	
	Если мКоличествоОбъектовДляСопоставления = Неопределено Тогда
		
		ЗаголовокКоличествоОбъектовДлясопоставления = "Количество объектов для сопоставления: ???";
		
	Иначе
		
		ЗаголовокКоличествоОбъектовДлясопоставления = "Количество объектов для сопоставления: " + Строка(мКоличествоОбъектовДляСопоставления);
				
	КонецЕсли;
	
	ЭлементыФормы.НадписьКоличествоОбъектовДляСопоставления.Заголовок = ЗаголовокКоличествоОбъектовДлясопоставления;
	ЭлементыФормы.ВыполнитьПодсчетОбъектовДляКоторыхНужноСопоставление.Видимость = мКоличествоОбъектовДляСопоставления = Неопределено;
		
КонецПроцедуры

Процедура УстановитьНадписьКоличествоОбъектовФоновогоОбмена()
	
	Если ДобавлениеОбъектовИзФоновогоОбмена = 1 Тогда
		ЭлементыФормы.НадписьКоличествоДополняемыхОбъектов.Заголовок = "Дополнять пакет фиксированным количеством объектов:";
	Иначе
		ЭлементыФормы.НадписьКоличествоДополняемыхОбъектов.Заголовок = "Дополнять пакет до фиксированного количества объектов:";
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриОткрытии()
		
	Если ДобавлениеОбъектовИзФоновогоОбмена = 0 Тогда
		ДобавлениеОбъектовИзФоновогоОбмена = 1;
	КонецЕсли;
			
	УстановитьНадписьКоличестваОбъектовДляСопоставления();
		
	УстановитьНадписьКоличествоОбъектовФоновогоОбмена();
			
	мСтруктураСтарыхЗначений = Новый Структура("ДобавлениеОбъектовИзФоновогоОбмена,КоличествоОбъектовДляФоновогоОбмена", 
		ДобавлениеОбъектовИзФоновогоОбмена,КоличествоОбъектовДляФоновогоОбмена);
	
КонецПроцедуры

Процедура ДобавлятьФиксированноеКоличествоОбъектовПриФоновомОбменеПриИзменении(Элемент)
	
	УстановитьНадписьКоличествоОбъектовФоновогоОбмена();
	
КонецПроцедуры

Процедура ОсновныеДействияФормыОК(Кнопка)
	
	Отказ = Ложь;
	
	Если Не ЗначениеЗаполнено(КоличествоОбъектовДляФоновогоОбмена) Тогда
			
		ОбщегоНазначения.СообщитьОбОшибке("Не указано количество объектов для обмена данными на этапе сопоставления объектов", Отказ);
		
	КонецЕсли;
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	Закрыть(Истина);
	
КонецПроцедуры

Процедура ОсновныеДействияФормыОтмена(Кнопка)
	
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, мСтруктураСтарыхЗначений);
	Закрыть(Ложь);
	
КонецПроцедуры