Перем МенеджерОбъекта;
Перем ЕстьГруппыЭлементов;

Процедура ИмяФайлаОбменаНачалоВыбора(Элемент, СтандартнаяОбработка)
	
	ДиалогВыбораФайлаОбмена = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Сохранение);
	ДиалогВыбораФайлаОбмена.Фильтр = "Файл обмена данными (*.xml)|*.xml";
	ДиалогВыбораФайлаОбмена.МножественныйВыбор = Ложь;
	ДиалогВыбораФайлаОбмена.Заголовок = "Выберите файл";

	Если НЕ ДиалогВыбораФайлаОбмена.Выбрать() Тогда
		Возврат;
	КонецЕсли;
	
	ИмяФайлаОбмена = ДиалогВыбораФайлаОбмена.ПолноеИмяФайла;
	
КонецПроцедуры

Функция ПроверитьНастройки()
	
	Отказ = Ложь;
	
	Если ПустаяСтрока(ИмяФайлаОбмена) Тогда
		ОбщегоНазначения.СообщитьОбОшибке("Не заполнено поле ""Файл данных""", Отказ);
	КонецЕсли;
	
	Если СписокВыгружаемыхОбъектов.Количество() = 0 Тогда
		ОбщегоНазначения.СообщитьОбОшибке("Не выбраны выгружаемые объекты", Отказ);
	КонецЕсли; 
	
	НомерСтроки = 1;
	Для каждого ЭлКоллекции Из СписокВыгружаемыхОбъектов Цикл
		Если НЕ ЗначениеЗаполнено(ЭлКоллекции.Объект) Тогда
			ОбщегоНазначения.СообщитьОбОшибке("Не заполнено поле ""Объект"" в строке №" + НомерСтроки, Отказ);
		КонецЕсли; 
		НомерСтроки = НомерСтроки + 1;
	КонецЦикла; 
	
	Возврат НЕ Отказ;
	
КонецФункции // 

////////////////////////////////////////////////////////////////////////////////
// СПИСОК ОБЪЕКТОВ

Процедура СписокВыгружаемыхОбъектовОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	ТипВыбранногоЗначения = ТипЗнч(ВыбранноеЗначение);
	
	Если ТипВыбранногоЗначения = Тип("Массив") Тогда
		СписокВыбранныхОбъектов = ВыбранноеЗначение;
	Иначе	
		СписокВыбранныхОбъектов = Новый Массив;
		СписокВыбранныхОбъектов.Добавить(ВыбранноеЗначение);
	КонецЕсли; 
	
	Для каждого ОбъектСсылка Из СписокВыбранныхОбъектов Цикл
		НайденныйОбъект = СписокВыгружаемыхОбъектов.Найти(ОбъектСсылка, "Объект");
		Если НайденныйОбъект = Неопределено Тогда
			НовыйОбъект = СписокВыгружаемыхОбъектов.Добавить();
			НовыйОбъект.Объект = ОбъектСсылка;
			Если ЕстьГруппыЭлементов Тогда
				НовыйОбъект.ЭтоГруппа = ОбъектСсылка.ЭтоГруппа;
			КонецЕсли; 
		КонецЕсли; 
	КонецЦикла; 
	
КонецПроцедуры

Процедура СписокВыгружаемыхОбъектовПриВыводеСтроки(Элемент, ОформлениеСтроки, ДанныеСтроки)
	
	Если ЕстьГруппыЭлементов И ДанныеСтроки.ЭтоГруппа Тогда
		ОформлениеСтроки.Ячейки.КартинкаЭлемента.ИндексКартинки = 0;
	Иначе
		ОформлениеСтроки.Ячейки.КартинкаЭлемента.ИндексКартинки = 1;
	КонецЕсли; 
	ОформлениеСтроки.Ячейки.КартинкаЭлемента.ОтображатьКартинку = Истина;
	
КонецПроцедуры

Процедура СписокВыгружаемыхОбъектовПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	ТекущиеДанные = Элемент.ТекущиеДанные;
	
	ТекущиеДанные.Объект = ТипВыгружаемыхОбъектов.ПривестиЗначение(ТекущиеДанные.Объект);
	
КонецПроцедуры

Процедура СписокВыгружаемыхОбъектовОбъектПриИзменении(Элемент)
	
	Если ЕстьГруппыЭлементов Тогда
		ТекущиеДанные = ЭлементыФормы.СписокВыгружаемыхОбъектов.ТекущиеДанные;
		Если ЗначениеЗаполнено(ТекущиеДанные.Объект) Тогда
			ТекущиеДанные.ЭтоГруппа = ТекущиеДанные.Объект.ЭтоГруппа;
		Иначе	
			ТекущиеДанные.ЭтоГруппа = Ложь;
		КонецЕсли; 
	КонецЕсли; 
	
КонецПроцедуры

Процедура СписокВыгружаемыхОбъектовПередОкончаниемРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования, Отказ)
	
	ТекущиеДанные = Элемент.ТекущиеДанные;
	Если ЗначениеЗаполнено(ТекущиеДанные.Объект) Тогда
		ИндексТекущейСтроки = СписокВыгружаемыхОбъектов.Индекс(ТекущиеДанные);
		СписокОбъектов = СписокВыгружаемыхОбъектов.НайтиСтроки(Новый Структура("Объект", ТекущиеДанные.Объект));
		Для каждого НайденныйОбъект Из СписокОбъектов Цикл
			Если ИндексТекущейСтроки <> СписокВыгружаемыхОбъектов.Индекс(НайденныйОбъект) Тогда
				Предупреждение("Такой объект уже выбран");
				Отказ = Истина;
				Прервать;
			КонецЕсли;		
		КонецЦикла; 
	КонецЕсли; 
	
КонецПроцедуры


////////////////////////////////////////////////////////////////////////////////
// КОМАНДЫ

Процедура КнопкаВыполнитьНажатие(Кнопка)
	
	Если НЕ ПроверитьНастройки() Тогда
		Предупреждение("Выгрузка не выполнена.
						|Не заполнены необходимые данные.");
		Возврат;
	КонецЕсли; 
	
	СписокОбъектов.Очистить();
	Для каждого ЭлКоллекции Из СписокВыгружаемыхОбъектов Цикл
		СписокОбъектов.Добавить(ЭлКоллекции.Объект);
	КонецЦикла; 
	
	ВыполнитьВыгрузку();
	
КонецПроцедуры

Процедура КоманднаяПанельСпискаОбъектовДействиеПодбор(Кнопка)
	
	ФормаВыбора = МенеджерОбъекта.ПолучитьФормуВыбора(, ЭлементыФормы.СписокВыгружаемыхОбъектов);
	ФормаВыбора.ЗакрыватьПриВыборе = Ложь;
	ФормаВыбора.МножественныйВыбор = Истина;
	ФормаВыбора.Открыть();
	
КонецПроцедуры


////////////////////////////////////////////////////////////////////////////////
// ФОРМА

Процедура ПередОткрытием(Отказ, СтандартнаяОбработка)
	
	МетаданныеОбъекта = Метаданные.НайтиПоТипу(ТипВыгружаемыхОбъектов.Типы()[0]);
	
	Заголовок = "Выгрузка объектов: " + МетаданныеОбъекта.Синоним;
	
	ЕстьГруппыЭлементов = МетаданныеОбъекта.Иерархический И МетаданныеОбъекта.ВидИерархии = Метаданные.СвойстваОбъектов.ВидИерархии.ИерархияГруппИЭлементов;
	
	МенеджерОбъекта = Справочники[МетаданныеОбъекта.Имя];
	
	СписокВыгружаемыхОбъектов.Колонки.Добавить("ЭтоГруппа", Новый ОписаниеТипов("Булево"));
	
КонецПроцедуры

Процедура ПередСохранениемЗначений(Отказ)
	
	СохраненныеНастройки.Очистить();
	СохраненныеНастройки.Добавить(ТипВыгружаемыхОбъектов, "ТипВыгружаемыхОбъектов");
	
КонецПроцедуры

Процедура ПослеВосстановленияЗначений()
	
	Для каждого ЭлКоллекции Из СохраненныеНастройки Цикл
		Если ЭлКоллекции.Представление = "ТипВыгружаемыхОбъектов" Тогда
			ТипОбъекта = ТипВыгружаемыхОбъектов.Типы()[0];
			Если ТипОбъекта <> ЭлКоллекции.Значение.Типы()[0] Тогда
				ОбщегоНазначения.Сообщение("Ошибка при загрузке настройки: сохраненная настройка принадлежит другому типу объектов");
				СписокВыгружаемыхОбъектов.Очистить();
			КонецЕсли; 
		КонецЕсли; 
	КонецЦикла; 
	
КонецПроцедуры