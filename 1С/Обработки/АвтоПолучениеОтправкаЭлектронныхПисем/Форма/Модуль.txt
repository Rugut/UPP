////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОБЩЕГО НАЗНАЧЕНИЯ

// Процедура обновляет список учетных записей автоотправки/получения
// и время последнего транспорта данным пользователем.
//
Процедура ОбновитьСписок() Экспорт

	ОбновитьСписокУчетныхЗаписей();
	Если УчетныеЗаписиАвтоматическогоТранспортаПисем.Количество() > 0 Тогда
		ПодключитьОбработчикОжидания("ПроверкаПисем", 1);
	КонецЕсли;

КонецПроцедуры

// Процедура осуществляет проверку на необходимость транспорта писем по учетным записям,
// с заданным интервалом.
//
Процедура ПроверкаПисем()

	ОтключитьОбработчикОжидания("ПроверкаПисем");
	
	МассивОбработанныхСтрок = Новый Массив;
	
	Для каждого СтрокаТЧ Из УчетныеЗаписиАвтоматическогоТранспортаПисем Цикл
		Если СтрокаТЧ.ВремяПоследнегоПолучения + СтрокаТЧ.ИнтервалОбновления*60 > ТекущаяДата() Тогда
			Продолжить;
		КонецЕсли;
		МассивОбработанныхСтрок.Добавить(СтрокаТЧ);
	КонецЦикла;
	
	Если МассивОбработанныхСтрок.Количество() > 0 Тогда
		Состояние("Производится отправка/получение писем ...");
		Для каждого СтрокаТЧ Из МассивОбработанныхСтрок Цикл
			МассивУчетныхЗаписей = Новый Массив;
			МассивУчетныхЗаписей.Добавить(СтрокаТЧ.УчетнаяЗапись);
			Если СтрокаТЧ.Действие = Перечисления.ВидыДействийАвтоПолученияОтправкиЭлектронныхПисем.Получение Тогда
				УправлениеЭлектроннойПочтой.ПолучениеОтправкаПисем(глЗначениеПеременной("глСоответствиеТекстовЭлектронныхПисем"), глЗначениеПеременной("глТекущийПользователь"), МассивУчетныхЗаписей,, Ложь, Истина, Ложь);
			ИначеЕсли СтрокаТЧ.Действие = Перечисления.ВидыДействийАвтоПолученияОтправкиЭлектронныхПисем.Отправка Тогда
				УправлениеЭлектроннойПочтой.ПолучениеОтправкаПисем(глЗначениеПеременной("глСоответствиеТекстовЭлектронныхПисем"), глЗначениеПеременной("глТекущийПользователь"), МассивУчетныхЗаписей,, Истина, Ложь, Ложь);
			Иначе
				УправлениеЭлектроннойПочтой.ПолучениеОтправкаПисем(глЗначениеПеременной("глСоответствиеТекстовЭлектронныхПисем"), глЗначениеПеременной("глТекущийПользователь"), МассивУчетныхЗаписей,, Истина, Истина, Ложь);
			КонецЕсли; 
			СтрокаТЧ.ВремяПоследнегоПолучения = ТекущаяДата();
		КонецЦикла; 
		Оповестить("РоботПолученияПисем");
	КонецЕсли; 
	
	Состояние("");
	
	ПодключитьОбработчикОжидания("ПроверкаПисем", 1);

КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ КОМАНДНЫХ ПАНЕЛЕЙ ФОРМЫ

// Процедура - обработчик события "Нажатие" элемента формы "КоманднаяПанельФормы.Обновить".
//
Процедура КоманднаяПанельФормыОбновить(Кнопка)
	
	ОтключитьОбработчикОжидания("ПроверкаПисем");
	ОбновитьСписок();
	
КонецПроцедуры

ОбновитьСписок();