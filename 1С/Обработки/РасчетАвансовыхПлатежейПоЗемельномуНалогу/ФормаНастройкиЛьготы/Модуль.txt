////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ МОДУЛЯ

Перем мСтруктураСохраняемыхРеквизитов;

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОБЩЕГО НАЗНАЧЕНИЯ

// Записывает данные в строку формы документа.
//
Процедура ЗаписатьДанные(Отказ = Ложь)
	
	Если НалоговаяЛьготаПоНалоговойБазе = Перечисления.ВидНалоговойЛьготыПоНалоговойБазеПоЗемельномуНалогу.ОсвобождениеОтНалогообложенияПоСтатье395 Тогда
		
		Если ПустаяСтрока(КодНалоговойЛьготыОсвобождениеОтНалогообложенияПоСтатье395) Тогда
			ОбщегоНазначения.СообщитьОбОшибке("Не указан код льготы при освобождении от налогообложения по ст. 395 НК РФ!");
			Отказ = Истина;
		КонецЕсли;
		
	КонецЕсли;
	
	Если НалоговаяЛьготаПоНалоговойБазе = Перечисления.ВидНалоговойЛьготыПоНалоговойБазеПоЗемельномуНалогу.НеОблагаемаяНалогомПлощадь Тогда
		
		Если ДоляНеОблагаемойНалогомПлощадиЧислитель = 0 Тогда
			ОбщегоНазначения.СообщитьОбОшибке("Не указан числитель дроби - доли не облагаемой налогом площади!");
			Отказ = Истина;
		КонецЕсли;
		
		Если ДоляНеОблагаемойНалогомПлощадиЗнаменатель = 0 Тогда
			ОбщегоНазначения.СообщитьОбОшибке("Не указан знаменатель дроби - доли не облагаемой налогом площади!");
			Отказ = Истина;
		КонецЕсли;
		
		Если НЕ ДоляНеОблагаемойНалогомПлощадиЧислитель = 0
		   И НЕ ДоляНеОблагаемойНалогомПлощадиЗнаменатель = 0
		   И ДоляНеОблагаемойНалогомПлощадиЧислитель > ДоляНеОблагаемойНалогомПлощадиЗнаменатель Тогда
			ОбщегоНазначения.СообщитьОбОшибке("Для не облагаемой налогом площади: числитель дроби больше знаменателя!");
			Отказ = Истина;
		КонецЕсли;
		
	КонецЕсли;
	
	Если НалоговаяЛьготаПоНалоговойБазе = Перечисления.ВидНалоговойЛьготыПоНалоговойБазеПоЗемельномуНалогу.УменьшениеНалоговойБазы Тогда
		
		Если НЕ УменьшениеНалоговойБазыПоСтатье391 И НЕ УменьшениеНалоговойБазыНаСумму Тогда
			ОбщегоНазначения.СообщитьОбОшибке("Не выбран ни один из способов уменьшения налоговой базы!");
			Отказ = Истина;
		КонецЕсли;
		
		Если УменьшениеНалоговойБазыПоСтатье391 Тогда
			
			Если ПустаяСтрока(КодНалоговойЛьготыУменьшениеНалоговойБазыПоСтатье391) Тогда
				ОбщегоНазначения.СообщитьОбОшибке("Не указан код льготы при уменьшении налоговой базы по ст. 391 НК РФ на 10 000 рублей!");
				Отказ = Истина;
			КонецЕсли;
			
		КонецЕсли;
		
		Если УменьшениеНалоговойБазыНаСумму Тогда
			
			Если НеОблагаемаяНалогомСумма = 0 Тогда
				ОбщегоНазначения.СообщитьОбОшибке("Не указана установленная местным нормативным актом сумма уменьшения налоговой базы!");
				Отказ = Истина;
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
	
	Если СнижениеНалоговойСтавки Тогда
		
		Если СниженнаяНалоговаяСтавка = 0 Тогда
			ОбщегоНазначения.СообщитьОбОшибке("Не указана сниженная налоговая ставка!");
			Отказ = Истина;
		КонецЕсли;
		
	КонецЕсли;
	
	Если ВариантУменьшенияСуммыНалога = Перечисления.ВидНалоговойЛьготыПоСуммеНалогаПоЗемельномуНалогу.УменьшениеСуммыНалогаВПроцентах Тогда
		
		Если ПроцентУменьшенияСуммыНалога = 0 Тогда
			ОбщегоНазначения.СообщитьОбОшибке("Не указан процент уменьшения налога!");
			Отказ = Истина;
		КонецЕсли;
		
		Если ПроцентУменьшенияСуммыНалога > 100 Тогда
			ОбщегоНазначения.СообщитьОбОшибке("Процент уменьшения налога превышает 100%!");
			Отказ = Истина;
		КонецЕсли;
		
	КонецЕсли;
	
	Если ВариантУменьшенияСуммыНалога = Перечисления.ВидНалоговойЛьготыПоСуммеНалогаПоЗемельномуНалогу.УменьшениеСуммыНалогаНаСумму Тогда
		
		Если СуммаУменьшенияСуммыНалога = 0 Тогда
			ОбщегоНазначения.СообщитьОбОшибке("Не указана сумма уменьшения налога!");
			Отказ = Истина;
		КонецЕсли;
		
	КонецЕсли;
		
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(СписокПараметров, ЭтаФорма);
	
	Если ТипЗнч(ВладелецФормы) = Тип("Форма") 
	   И НЕ (ВладелецФормы.ЭлементыФормы.Найти("НалоговаяЛьгота") = Неопределено) Тогда
	   
		ВладелецФормы.ЭлементыФормы.НалоговаяЛьгота.Заголовок = УправлениеВнеоборотнымиАктивами.ПредставлениеНалоговойЛьготыПоЗемельномуНалогу(СписокПараметров);
		
	КонецЕсли;
	
	Модифицированность = Ложь;
	
КонецПроцедуры // ЗаписатьДанные()

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ДЛЯ УПРАВЛЕНИЯ ВНЕШНИМ ВИДОМ ФОРМЫ

// Управление видимостью и доступностью элементов формы.
//
Процедура УстановитьВидимость()
	
	ЕстьОсвобождениеОтНалогообложенияПоСтатье395 = (НалоговаяЛьготаПоНалоговойБазе = Перечисления.ВидНалоговойЛьготыПоНалоговойБазеПоЗемельномуНалогу.ОсвобождениеОтНалогообложенияПоСтатье395);
	
	ЭлементыФормы.КодНалоговойЛьготыОсвобождениеОтНалогообложенияПоСтатье395.Доступность               = ЕстьОсвобождениеОтНалогообложенияПоСтатье395;
	ЭлементыФОрмы.КодНалоговойЛьготыОсвобождениеОтНалогообложенияПоСтатье395.АвтоОтметкаНезаполненного = ЕстьОсвобождениеОтНалогообложенияПоСтатье395;
	ЭлементыФОрмы.КодНалоговойЛьготыОсвобождениеОтНалогообложенияПоСтатье395.ОтметкаНезаполненного     = ЕстьОсвобождениеОтНалогообложенияПоСтатье395 И ПустаяСтрока(КодНалоговойЛьготыОсвобождениеОтНалогообложенияПоСтатье395);
	
	ЕстьНеОблагаемаяНалогомПлощадь = (НалоговаяЛьготаПоНалоговойБазе = Перечисления.ВидНалоговойЛьготыПоНалоговойБазеПоЗемельномуНалогу.НеОблагаемаяНалогомПлощадь);
	
	ЭлементыФормы.ДоляНеОблагаемойНалогомПлощадиЧислитель.Доступность               = ЕстьНеОблагаемаяНалогомПлощадь;
	ЭлементыФормы.ДоляНеОблагаемойНалогомПлощадиЧислитель.АвтоОтметкаНезаполненного = ЕстьНеОблагаемаяНалогомПлощадь;
	ЭлементыФормы.ДоляНеОблагаемойНалогомПлощадиЧислитель.ОтметкаНезаполненного     = ЕстьНеОблагаемаяНалогомПлощадь И НЕ ЗначениеЗаполнено(ДоляНеОблагаемойНалогомПлощадиЧислитель);
	
	ЭлементыФормы.ДоляНеОблагаемойНалогомПлощадиЗнаменатель.Доступность               = ЕстьНеОблагаемаяНалогомПлощадь;
	ЭлементыФормы.ДоляНеОблагаемойНалогомПлощадиЗнаменатель.АвтоОтметкаНезаполненного = ЕстьНеОблагаемаяНалогомПлощадь;
	ЭлементыФормы.ДоляНеОблагаемойНалогомПлощадиЗнаменатель.ОтметкаНезаполненного     = ЕстьНеОблагаемаяНалогомПлощадь И НЕ ЗначениеЗаполнено(ДоляНеОблагаемойНалогомПлощадиЗнаменатель);
	
	ЕстьУменьшениеНалоговойБазы = (НалоговаяЛьготаПоНалоговойБазе = Перечисления.ВидНалоговойЛьготыПоНалоговойБазеПоЗемельномуНалогу.УменьшениеНалоговойБазы);
	
	ЭлементыФормы.УменьшениеНалоговойБазыПоСтатье391.Доступность = ЕстьУменьшениеНалоговойБазы; 
	ЭлементыФормы.УменьшениеНалоговойБазыНаСумму.Доступность     = ЕстьУменьшениеНалоговойБазы; 
	
	ЭлементыФормы.КодНалоговойЛьготыУменьшениеНалоговойБазыПоСтатье391.Доступность               = ЕстьУменьшениеНалоговойБазы И УменьшениеНалоговойБазыПоСтатье391;
	ЭлементыФормы.КодНалоговойЛьготыУменьшениеНалоговойБазыПоСтатье391.АвтоОтметкаНезаполненного = ЕстьУменьшениеНалоговойБазы И УменьшениеНалоговойБазыПоСтатье391;
	ЭлементыФормы.КодНалоговойЛьготыУменьшениеНалоговойБазыПоСтатье391.ОтметкаНезаполненного     = ЕстьУменьшениеНалоговойБазы И УменьшениеНалоговойБазыПоСтатье391 И ПустаяСтрока(КодНалоговойЛьготыУменьшениеНалоговойБазыПоСтатье391);
	
	ЭлементыФормы.НеОблагаемаяНалогомСумма.Доступность                  = ЕстьУменьшениеНалоговойБазы И УменьшениеНалоговойБазыНаСумму;
	ЭлементыФормы.НеОблагаемаяНалогомСумма.АвтоОтметкаНезаполненного    = ЕстьУменьшениеНалоговойБазы И УменьшениеНалоговойБазыНаСумму;
	ЭлементыФормы.НеОблагаемаяНалогомСумма.ОтметкаНезаполненного        = ЕстьУменьшениеНалоговойБазы И УменьшениеНалоговойБазыНаСумму И ПустаяСтрока(НеОблагаемаяНалогомСумма);
	ЭлементыФормы.НадписьВалютаСуммаУменьшенияНалоговойБазы.Доступность = ЕстьУменьшениеНалоговойБазы И УменьшениеНалоговойБазыНаСумму;
	
	ЭлементыФормы.СниженнаяНалоговаяСтавка.Доступность               = СнижениеНалоговойСтавки;
	ЭлементыФормы.СниженнаяНалоговаяСтавка.АвтоОтметкаНезаполненного = СнижениеНалоговойСтавки;
	ЭлементыФормы.СниженнаяНалоговаяСтавка.ОтметкаНезаполненного     = СнижениеНалоговойСтавки И СниженнаяНалоговаяСтавка = 0;
	ЭлементыФормы.НадписьПроцентСниженнойНалоговойСтавки.Доступность = СнижениеНалоговойСтавки;
	
	УменьшениеСуммыНалогаВПроцентах = (ВариантУменьшенияСуммыНалога = Перечисления.ВидНалоговойЛьготыПоСуммеНалогаПоЗемельномуНалогу.УменьшениеСуммыНалогаВПроцентах);
	УменьшениеСуммыНалогаНаСумму    = (ВариантУменьшенияСуммыНалога = Перечисления.ВидНалоговойЛьготыПоСуммеНалогаПоЗемельномуНалогу.УменьшениеСуммыНалогаНаСумму);
	
	ЭлементыФормы.ПроцентУменьшенияСуммыНалога.Доступность               = УменьшениеСуммыНалогаВПроцентах;
	ЭлементыФормы.ПроцентУменьшенияСуммыНалога.АвтоОтметкаНезаполненного = УменьшениеСуммыНалогаВПроцентах;
	ЭлементыФормы.ПроцентУменьшенияСуммыНалога.ОтметкаНезаполненного     = УменьшениеСуммыНалогаВПроцентах И ПроцентУменьшенияСуммыНалога = 0;
	ЭлементыФормы.НадписьПроцентУменьшенияСуммыНалога.Доступность        = УменьшениеСуммыНалогаВПроцентах;
	
	ЭлементыФормы.СуммаУменьшенияСуммыНалога.Доступность               = УменьшениеСуммыНалогаНаСумму;
	ЭлементыФормы.СуммаУменьшенияСуммыНалога.АвтоОтметкаНезаполненного = УменьшениеСуммыНалогаНаСумму;
	ЭлементыФормы.СуммаУменьшенияСуммыНалога.ОтметкаНезаполненного     = УменьшениеСуммыНалогаНаСумму И СуммаУменьшенияСуммыНалога = 0;
	ЭлементыФормы.НадписьВалютаУменьшенияСуммыНалога.Доступность       = УменьшениеСуммыНалогаНаСумму;
	
КонецПроцедуры // УстановитьВидимость()

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

// Обработчик события "ПриОткрытии" формы.
//
Процедура ПриОткрытии()
	
	ЗаполнитьЗначенияСвойств(ЭтаФорма, СписокПараметров);
	
	СнижениеНалоговойСтавки = ЗначениеЗаполнено(СниженнаяНалоговаяСтавка);
	УменьшениеСуммыНалога   = ЗначениеЗаполнено(СуммаУменьшенияСуммыНалога) ИЛИ ЗначениеЗаполнено(ПроцентУменьшенияСуммыНалога);
	
	Если НЕ УменьшениеСуммыНалога Тогда
		ВариантУменьшенияСуммыНалога = Перечисления.ВидНалоговойЛьготыПоСуммеНалогаПоЗемельномуНалогу.НеПрименяется;
	ИначеЕсли ЗначениеЗаполнено(ПроцентУменьшенияСуммыНалога) Тогда
		ВариантУменьшенияСуммыНалога = Перечисления.ВидНалоговойЛьготыПоСуммеНалогаПоЗемельномуНалогу.УменьшениеСуммыНалогаВПроцентах;
	ИначеЕсли ЗначениеЗаполнено(СуммаУменьшенияСуммыНалога) Тогда
		ВариантУменьшенияСуммыНалога = Перечисления.ВидНалоговойЛьготыПоСуммеНалогаПоЗемельномуНалогу.УменьшениеСуммыНалогаНаСумму;
	КонецЕсли;
	
	УстановитьВидимость();
	
КонецПроцедуры // ПриОткрытии()

// Обработчик события "ПередЗакрытием" формы.
// Вызывает процедуру установки видимости элементов управления формы.
//
Процедура ПередЗакрытием(Отказ, СтандартнаяОбработка)
	
	Если Модифицированность Тогда
		
		ТекстВопроса =
		"Данные были изменены. Сохранить изменения?";
		
		КодВозврата = Вопрос(ТекстВопроса, РежимДиалогаВопрос.ДаНетОтмена);
			
		Если КодВозврата = КодВозвратаДиалога.Отмена Тогда
			
			Отказ = Истина;
			
		ИначеЕсли КодВозврата = КодВозвратаДиалога.Да Тогда
			
			ЗаписатьДанные(Отказ);
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры // ПередЗакрытием()

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ФОРМЫ

// Обработчик события "ПриИзменении" переключателя "Льготы по налоговой базе".
//
Процедура НалоговаяЛьготаПоНалоговойБазеПриИзменении(Элемент)
	
	Если Элемент.Значение = Перечисления.ВидНалоговойЛьготыПоНалоговойБазеПоЗемельномуНалогу.НеПрименяется
	 ИЛИ Элемент.Значение = Перечисления.ВидНалоговойЛьготыПоНалоговойБазеПоЗемельномуНалогу.ОсвобождениеОтНалогообложенияМестное Тогда
	 
		РаботаСДиалогами.СохранитьЗначениеРеквизитаФормы("КодНалоговойЛьготыОсвобождениеОтНалогообложенияПоСтатье395", ЭтаФорма, мСтруктураСохраняемыхРеквизитов);
		РаботаСДиалогами.СохранитьЗначениеРеквизитаФормы("ДоляНеОблагаемойНалогомПлощадиЧислитель", ЭтаФорма, мСтруктураСохраняемыхРеквизитов);
		РаботаСДиалогами.СохранитьЗначениеРеквизитаФормы("ДоляНеОблагаемойНалогомПлощадиЗнаменатель", ЭтаФорма, мСтруктураСохраняемыхРеквизитов);
		РаботаСДиалогами.СохранитьЗначениеРеквизитаФормы("КодНалоговойЛьготыУменьшениеНалоговойБазыПоСтатье391", ЭтаФорма, мСтруктураСохраняемыхРеквизитов);
		РаботаСДиалогами.СохранитьЗначениеРеквизитаФормы("НеОблагаемаяНалогомСумма", ЭтаФорма, мСтруктураСохраняемыхРеквизитов);
		РаботаСДиалогами.СохранитьЗначениеРеквизитаФормы("УменьшениеНалоговойБазыПоСтатье391", ЭтаФорма, мСтруктураСохраняемыхРеквизитов);
		РаботаСДиалогами.СохранитьЗначениеРеквизитаФормы("УменьшениеНалоговойБазыНаСумму", ЭтаФорма, мСтруктураСохраняемыхРеквизитов);
	 
	ИначеЕсли Элемент.Значение = Перечисления.ВидНалоговойЛьготыПоНалоговойБазеПоЗемельномуНалогу.ОсвобождениеОтНалогообложенияПоСтатье395 Тогда
		
		РаботаСДиалогами.ВосстановитьЗначениеРеквизитаФормы("КодНалоговойЛьготыОсвобождениеОтНалогообложенияПоСтатье395", ЭтаФорма, мСтруктураСохраняемыхРеквизитов);
		
		РаботаСДиалогами.СохранитьЗначениеРеквизитаФормы("ДоляНеОблагаемойНалогомПлощадиЧислитель", ЭтаФорма, мСтруктураСохраняемыхРеквизитов);
		РаботаСДиалогами.СохранитьЗначениеРеквизитаФормы("ДоляНеОблагаемойНалогомПлощадиЗнаменатель", ЭтаФорма, мСтруктураСохраняемыхРеквизитов);
		РаботаСДиалогами.СохранитьЗначениеРеквизитаФормы("КодНалоговойЛьготыУменьшениеНалоговойБазыПоСтатье391", ЭтаФорма, мСтруктураСохраняемыхРеквизитов);
		РаботаСДиалогами.СохранитьЗначениеРеквизитаФормы("НеОблагаемаяНалогомСумма", ЭтаФорма, мСтруктураСохраняемыхРеквизитов);
		РаботаСДиалогами.СохранитьЗначениеРеквизитаФормы("УменьшениеНалоговойБазыПоСтатье391", ЭтаФорма, мСтруктураСохраняемыхРеквизитов);
		РаботаСДиалогами.СохранитьЗначениеРеквизитаФормы("УменьшениеНалоговойБазыНаСумму", ЭтаФорма, мСтруктураСохраняемыхРеквизитов);
		
	ИначеЕсли Элемент.Значение = Перечисления.ВидНалоговойЛьготыПоНалоговойБазеПоЗемельномуНалогу.НеОблагаемаяНалогомПлощадь Тогда
		
		РаботаСДиалогами.ВосстановитьЗначениеРеквизитаФормы("ДоляНеОблагаемойНалогомПлощадиЧислитель", ЭтаФорма, мСтруктураСохраняемыхРеквизитов);
		РаботаСДиалогами.ВосстановитьЗначениеРеквизитаФормы("ДоляНеОблагаемойНалогомПлощадиЗнаменатель", ЭтаФорма, мСтруктураСохраняемыхРеквизитов);
		
		РаботаСДиалогами.СохранитьЗначениеРеквизитаФормы("КодНалоговойЛьготыОсвобождениеОтНалогообложенияПоСтатье395", ЭтаФорма, мСтруктураСохраняемыхРеквизитов);
		РаботаСДиалогами.СохранитьЗначениеРеквизитаФормы("КодНалоговойЛьготыУменьшениеНалоговойБазыПоСтатье391", ЭтаФорма, мСтруктураСохраняемыхРеквизитов);
		РаботаСДиалогами.СохранитьЗначениеРеквизитаФормы("НеОблагаемаяНалогомСумма", ЭтаФорма, мСтруктураСохраняемыхРеквизитов);
		РаботаСДиалогами.СохранитьЗначениеРеквизитаФормы("УменьшениеНалоговойБазыПоСтатье391", ЭтаФорма, мСтруктураСохраняемыхРеквизитов);
		РаботаСДиалогами.СохранитьЗначениеРеквизитаФормы("УменьшениеНалоговойБазыНаСумму", ЭтаФорма, мСтруктураСохраняемыхРеквизитов);
		
	ИначеЕсли Элемент.Значение = Перечисления.ВидНалоговойЛьготыПоНалоговойБазеПоЗемельномуНалогу.УменьшениеНалоговойБазы Тогда
		
		РаботаСДиалогами.ВосстановитьЗначениеРеквизитаФормы("КодНалоговойЛьготыУменьшениеНалоговойБазыПоСтатье391", ЭтаФорма, мСтруктураСохраняемыхРеквизитов);
		РаботаСДиалогами.ВосстановитьЗначениеРеквизитаФормы("НеОблагаемаяНалогомСумма", ЭтаФорма, мСтруктураСохраняемыхРеквизитов);
		РаботаСДиалогами.ВосстановитьЗначениеРеквизитаФормы("УменьшениеНалоговойБазыПоСтатье391", ЭтаФорма, мСтруктураСохраняемыхРеквизитов);
		РаботаСДиалогами.ВосстановитьЗначениеРеквизитаФормы("УменьшениеНалоговойБазыНаСумму", ЭтаФорма, мСтруктураСохраняемыхРеквизитов);
		
		РаботаСДиалогами.СохранитьЗначениеРеквизитаФормы("КодНалоговойЛьготыОсвобождениеОтНалогообложенияПоСтатье395", ЭтаФорма, мСтруктураСохраняемыхРеквизитов);
		РаботаСДиалогами.СохранитьЗначениеРеквизитаФормы("ДоляНеОблагаемойНалогомПлощадиЧислитель", ЭтаФорма, мСтруктураСохраняемыхРеквизитов);
		РаботаСДиалогами.СохранитьЗначениеРеквизитаФормы("ДоляНеОблагаемойНалогомПлощадиЗнаменатель", ЭтаФорма, мСтруктураСохраняемыхРеквизитов);
		
	КонецЕсли; 
	
	УстановитьВидимость();
	
КонецПроцедуры // НалоговаяЛьготаПоНалоговойБазеПриИзменении()

// Обработчик события "ПриИзменении" флага "Не облагаемая налогом сумма
// на федеральном уровне".
//
Процедура УменьшениеНалоговойБазыПоСтатье391ПриИзменении(Элемент)
	
	Если Элемент.Значение Тогда
		
		РаботаСДиалогами.ВосстановитьЗначениеРеквизитаФормы("КодНалоговойЛьготыУменьшениеНалоговойБазыПоСтатье391", ЭтаФорма, мСтруктураСохраняемыхРеквизитов);
		
	Иначе
	
		РаботаСДиалогами.СохранитьЗначениеРеквизитаФормы("КодНалоговойЛьготыУменьшениеНалоговойБазыПоСтатье391", ЭтаФорма, мСтруктураСохраняемыхРеквизитов);
		
	КонецЕсли;
	
	УстановитьВидимость();
	
КонецПроцедуры // НеОблагаемаяНалогомСуммаНаФедеральномУровнеПриИзменении()

// Обработчик события "ПриИзменении" флага "Не облагаемая налогом сумма".
//
Процедура УменьшениеНалоговойБазыНаСуммуПриИзменении(Элемент)
	
	Если Элемент.Значение Тогда
		
		РаботаСДиалогами.ВосстановитьЗначениеРеквизитаФормы("НеОблагаемаяНалогомСумма", ЭтаФорма, мСтруктураСохраняемыхРеквизитов);
		
	Иначе
	
		РаботаСДиалогами.СохранитьЗначениеРеквизитаФормы("НеОблагаемаяНалогомСумма", ЭтаФорма, мСтруктураСохраняемыхРеквизитов);
		
	КонецЕсли;
	
	УстановитьВидимость();
	
КонецПроцедуры // НеОблагаемаяНалогомСуммаПриИзменении()

// Обработчик события "НачалоВыбора" полей ввода "Код льготы" (по статье 395).
//
Процедура КодНалоговойЛьготыПоСтатье395НачалоВыбора(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
    КодЛьготы = УправлениеВнеоборотнымиАктивами.ПолучитьФормуВыбораДляКода(Элемент.Значение,
                "РегистрацияЗемельныхУчастков",
                "ЛьготыПоОсобождениюОтЗемельногоНалога",
                Период).ОткрытьМодально();

	
	Если НЕ (КодЛьготы = Неопределено) Тогда
		Элемент.Значение = КодЛьготы;
	КонецЕсли;
	
КонецПроцедуры // КодНалоговойЛьготыПоСтатье395НачалоВыбора()

// Обработчик события "НачалоВыбора" полей ввода "Код льготы" (по статье 391).
//
Процедура КодНалоговойЛьготыПоСтатье391НачалоВыбора(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
    КодЛьготы = УправлениеВнеоборотнымиАктивами.ПолучитьФормуВыбораДляКода(Элемент.Значение,
                "РегистрацияЗемельныхУчастков",
                "ЛьготыПоУменьшениюЗемельногоНалога",
                Период).ОткрытьМодально();
	
	Если НЕ (КодЛьготы = Неопределено) Тогда
		Элемент.Значение = КодЛьготы;
	КонецЕсли;
	
КонецПроцедуры // КодНалоговойЛьготыПоСтатье391НачалоВыбора()

// Обработчик события "ПриИзменении" переключателя "Уменьшение суммы налога".
//
Процедура ВариантУменьшенияСуммыНалогаПриИзменении(Элемент)
	
	Если Элемент.Значение = Перечисления.ВидНалоговойЛьготыПоСуммеНалогаПоЗемельномуНалогу.УменьшениеСуммыНалогаВПроцентах Тогда
		
		РаботаСДиалогами.ВосстановитьЗначениеРеквизитаФормы("ПроцентУменьшенияСуммыНалога", ЭтаФорма, мСтруктураСохраняемыхРеквизитов);
		РаботаСДиалогами.СохранитьЗначениеРеквизитаФормы("СуммаУменьшенияСуммыНалога", ЭтаФорма, мСтруктураСохраняемыхРеквизитов);
		
	ИначеЕсли Элемент.Значение = Перечисления.ВидНалоговойЛьготыПоСуммеНалогаПоЗемельномуНалогу.УменьшениеСуммыНалогаНаСумму Тогда
		
		РаботаСДиалогами.ВосстановитьЗначениеРеквизитаФормы("СуммаУменьшенияСуммыНалога", ЭтаФорма, мСтруктураСохраняемыхРеквизитов);
		РаботаСДиалогами.СохранитьЗначениеРеквизитаФормы("ПроцентУменьшенияСуммыНалога", ЭтаФорма, мСтруктураСохраняемыхРеквизитов);
		
	Иначе
		
		РаботаСДиалогами.СохранитьЗначениеРеквизитаФормы("ПроцентУменьшенияСуммыНалога", ЭтаФорма, мСтруктураСохраняемыхРеквизитов);
		РаботаСДиалогами.СохранитьЗначениеРеквизитаФормы("СуммаУменьшенияСуммыНалога", ЭтаФорма, мСтруктураСохраняемыхРеквизитов);
		
	КонецЕсли;
	
	УстановитьВидимость();

КонецПроцедуры // ВариантУменьшенияСуммыНалогаПриИзменении()

// Обработчик события "ПриИзменении" переключателя "Снижение налоговой ставки".
//
Процедура СнижениеНалоговойСтавкиПриИзменении(Элемент)
	
	Если Элемент.Значение Тогда
		РаботаСДиалогами.ВосстановитьЗначениеРеквизитаФормы("СниженнаяНалоговаяСтавка", ЭтаФорма, мСтруктураСохраняемыхРеквизитов);
	Иначе
		РаботаСДиалогами.СохранитьЗначениеРеквизитаФормы("СниженнаяНалоговаяСтавка", ЭтаФорма, мСтруктураСохраняемыхРеквизитов);
	КонецЕсли;
	
	УстановитьВидимость();
	
КонецПроцедуры // СнижениеНалоговойСтавкиПриИзменении()

// Обработчик события "Нажатие" кнопки "ОК".
//
Процедура КнопкаОКНажатие(Элемент)
	
	Отказ = Ложь;
	
	ЗаписатьДанные(Отказ);
	
	Если НЕ Отказ Тогда
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры // КнопкаОКНажатие()

// Обработчик события "Нажатие" кнопки "Отмена".
//
Процедура КнопкаОтменаНажатие(Элемент)
	
	Модифицированность = Ложь;
	
	Закрыть();
	
КонецПроцедуры // КнопкаОтменаНажатие(()

////////////////////////////////////////////////////////////////////////////////
// ОПЕРАТОРЫ ОСНОВНОЙ ПРОГРАММЫ

мСтруктураСохраняемыхРеквизитов = Новый Структура;