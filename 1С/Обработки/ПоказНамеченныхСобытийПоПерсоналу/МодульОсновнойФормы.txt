////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ МОДУЛЯ

// Диалог настройки периода
Перем мНастройкаПериода;

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

Процедура УстановитьЗаголовкиОрганизацийВФорме()
	ЭлементыФормы.НадписьОрганизация.Заголовок = ОбщегоНазначения.ПреобразоватьСтрокуИнтерфейса("Организация:");
	ЭлементыФормы.Организация.Подсказка = ОбщегоНазначения.ПреобразоватьСтрокуИнтерфейса("Организация");
	ЭлементыФормы.ТабличноеПолеРезультаты.Колонки.Организация.ТекстШапки = ОбщегоНазначения.ПреобразоватьСтрокуИнтерфейса("Организация");
КонецПроцедуры // УстановитьЗаголовкиОрганизацийВФорме()

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

Процедура ПриОткрытии()

	// Установить настройку периода по умолчанию
	мНастройкаПериода.ВариантНастройки = ВариантНастройкиПериода.Период;
	
	Если НЕ ЗначениеЗаполнено(ДатаНачала) И НЕ ЗначениеЗаполнено(ДатаОкончания) Тогда
		ДатаНачала		= НачалоМесяца(ОбщегоНазначения.ПолучитьРабочуюДату());
		ДатаОкончания	= КонецМесяца(ОбщегоНазначения.ПолучитьРабочуюДату());
	КонецЕсли;

	Если Организация.Пустая() Тогда
		Организация = УправлениеПользователями.ПолучитьЗначениеПоУмолчанию(глЗначениеПеременной("глТекущийПользователь"), "ОсновнаяОрганизация");
	КонецЕсли;
	
	УстановитьЗаголовкиОрганизацийВФорме();
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ДЕЙСТВИЯ КОМАНДНЫХ ПАНЕЛЕЙ ФОРМЫ

Процедура КоманднаяПанельРезультатыЗаполнить(Кнопка)
	
	Автозаполнение();
	
	Если ЭлементыФормы.ТабличноеПолеРезультаты.ТекущаяСтрока = Неопределено и Результаты.Количество() > 0 Тогда
		ЭлементыФормы.ТабличноеПолеРезультаты.ТекущаяСтрока = Результаты[0];
	КонецЕсли;
	
КонецПроцедуры // КоманднаяПанельРезультатыЗаполнить()

Процедура КоманднаяПанельРезультатыУстановить(Кнопка)
	
	Для Каждого Строка Из Результаты Цикл
		Если Не Строка.ФормироватьДокумент Тогда
            Строка.ФормироватьДокумент = Истина
		КонецЕсли;	
	КонецЦикла;	
	
КонецПроцедуры

Процедура КоманднаяПанельРезультатыСнять(Кнопка)
	
	Для Каждого Строка Из Результаты Цикл
		Если Строка.ФормироватьДокумент Тогда
            Строка.ФормироватьДокумент = Ложь
		КонецЕсли;	
	КонецЦикла;	
	
КонецПроцедуры

Процедура КоманднаяПанельРезультатыИнвертировать(Кнопка)
	
	Для Каждого Строка Из Результаты Цикл
		Строка.ФормироватьДокумент = Не Строка.ФормироватьДокумент
	КонецЦикла;	
	
КонецПроцедуры

Процедура ОсновныеДействияФормыСоздать(Элемент)
	Перем ДокОрганизация, ДокТЧ, ДокИмяТЧ;
	
	СписокДокументов = РазобратьТаблицуИзменений();
	Для Каждого Док Из СписокДокументов Цикл
		Форма = Документы[Док.ИмяДокумента].ПолучитьФормуНовогоДокумента(, ЭтаФорма, Док.Организация);
		Если Док.Организация <> Неопределено И Не Док.Организация.Пустая() Тогда
			Форма.Организация	= Док.Организация;
		КонецЕсли;
		
		Для Каждого СтрокаТаб Из Док.ТЧ Цикл
			Если Док.ИмяТЧ <> Неопределено Тогда
				СтрокаТЧ = Форма[Док.ИмяТЧ].Добавить();
				ЗаполнитьЗначенияСвойств(СтрокаТЧ, СтрокаТаб);
				Если Док.ИмяДокумента = "УвольнениеИзОрганизаций" Тогда
					СтрокаТЧ.ПорядокРасчетаОтпуска = Перечисления.ПорядокРасчетаОтпуска.ПоКалендарнымДням;
					Форма.РассчитатьКоличествоДнейОтпуска(СтрокаТЧ);
				КонецЕсли;
				
			Иначе
				ЗаполнитьЗначенияСвойств(Форма, СтрокаТаб);
				
			КонецЕсли;
		КонецЦикла;
		
		Форма.Открыть();
	КонецЦикла;

КонецПроцедуры // КнопкаСоздатьНажатие()

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ УПРАВЛЕНИЯ

Процедура ВыбПериодНажатие(Элемент)
	
	мНастройкаПериода.УстановитьПериод(ДатаНачала, КонецДня(ДатаОкончания));
	
	Если мНастройкаПериода.Редактировать() Тогда
		ДатаНачала		= мНастройкаПериода.ПолучитьДатуНачала();
		ДатаОкончания	= мНастройкаПериода.ПолучитьДатуОкончания();
	КонецЕсли;
	
КонецПроцедуры // ВыбПериодНажатие()

////////////////////////////////////////////////////////////////////////////////
// ОПЕРАТОРЫ ОСНОВНОЙ ПРОГРАММЫ

мНастройкаПериода = Новый НастройкаПериода;