////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

// Процедура заполняет табличное поле списком организаций
//
Процедура Автозаполнение() Экспорт
	
	ВводНаОсновании.Очистить();
	
	Если НЕ ЗначениеЗаполнено(Основание) Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	
	ОсновнаяОрганизация = УправлениеПользователями.ПолучитьЗначениеПоУмолчанию(глЗначениеПеременной("глТекущийПользователь"), "ОсновнаяОрганизация");
	
	Запрос.УстановитьПараметр("Основание",				Основание);
	Запрос.УстановитьПараметр("ОсновнаяОрганизация",	ОсновнаяОрганизация);
	
	Если ТипЗнч(Основание) = Тип("Массив") Тогда
		МетаданныеДокумента = Основание.Получить(0).Метаданные();
	Иначе
		МетаданныеДокумента = Основание.Метаданные();
	КонецЕсли;
	ИмяДокумента = МетаданныеДокумента.Имя;
	Если МетаданныеДокумента.Реквизиты.Найти("Сотрудник") = Неопределено Тогда
		Если МетаданныеДокумента.Реквизиты.Найти("ФизЛицо") = Неопределено Тогда
			ФизЛицоВШапкеДокумента = Ложь;
			РеквизитЗапроса = "Сотрудник.ФизЛицо";
		Иначе
			ФизЛицоВШапкеДокумента = Истина;
			РеквизитЗапроса = "ФизЛицо";
		КонецЕсли;
	Иначе
		ФизЛицоВШапкеДокумента = Истина;
		РеквизитЗапроса = "Сотрудник.ФизЛицо";
	КонецЕсли;
	
	Если ИмяДокумента = "ПриемНаРаботу" Тогда
		Если ОсновнаяОрганизация.Пустая() Тогда
			Запрос.Текст =
			"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
			|	ИСТИНА КАК Отметка,
			|	Док.Сотрудник.Организация КАК Организация
			|ИЗ
			|	Документ.ПриемНаРаботу.Работники КАК Док
			|ГДЕ
			|	Док.Ссылка В(&Основание)
			|	И Док.Сотрудник.Организация <> ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)";
			
		Иначе
			Запрос.Текст =
			"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
			|	ВЫБОР
			|		КОГДА Док.Сотрудник.Организация = &ОсновнаяОрганизация
			|			ТОГДА ИСТИНА
			|		ИНАЧЕ ЛОЖЬ
			|	КОНЕЦ КАК Отметка,
			|	Док.Сотрудник.Организация КАК Организация
			|ИЗ
			|	Документ.ПриемНаРаботу.Работники КАК Док
			|ГДЕ
			|	Док.Ссылка В(&Основание)
			|	И Док.Сотрудник.Организация <> ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)";
			
		КонецЕсли;
		
	Иначе
		Если ОсновнаяОрганизация.Пустая() Тогда
			Запрос.Текст =
			"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
			|	ИСТИНА КАК Отметка,
			|	РаботникиОрганизаций.Организация
			|ИЗ
			|	РегистрСведений.РаботникиОрганизаций.СрезПоследних(
			|		,
			|		Сотрудник.Физлицо В
			|			(ВЫБРАТЬ РАЗЛИЧНЫЕ
			|				Док." + РеквизитЗапроса + "
			|			ИЗ
			|				Документ." + ИмяДокумента + ?(ФизЛицоВШапкеДокумента, "", ".Работники") + " КАК Док
			|			ГДЕ
			|				Док.Ссылка В(&Основание))) КАК РаботникиОрганизаций
			|ГДЕ
			|	ВЫБОР
			|		КОГДА РаботникиОрганизаций.ПериодЗавершения <> ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
			|			ТОГДА РаботникиОрганизаций.ПричинаИзмененияСостоянияЗавершения
			|			ИНАЧЕ РаботникиОрганизаций.ПричинаИзмененияСостояния 
			|	КОНЕЦ <> ЗНАЧЕНИЕ(Перечисление.ПричиныИзмененияСостояния.Увольнение)";
			
		Иначе
			Запрос.Текст =
			"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
			|	ВЫБОР
			|		КОГДА РаботникиОрганизаций.Организация = &ОсновнаяОрганизация
			|			ТОГДА ИСТИНА
			|		ИНАЧЕ ЛОЖЬ
			|	КОНЕЦ КАК Отметка,
			|	РаботникиОрганизаций.Организация
			|ИЗ
			|	РегистрСведений.РаботникиОрганизаций.СрезПоследних(
			|		,
			|		Сотрудник.Физлицо В
			|			(ВЫБРАТЬ РАЗЛИЧНЫЕ
			|				Док." + РеквизитЗапроса + "
			|			ИЗ
			|				Документ." + ИмяДокумента + ?(ФизЛицоВШапкеДокумента, "", ".Работники") + " КАК Док
			|			ГДЕ
			|				Док.Ссылка В(&Основание))) КАК РаботникиОрганизаций
			|ГДЕ
			|	ВЫБОР
			|		КОГДА РаботникиОрганизаций.ПериодЗавершения <> ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
			|			ТОГДА РаботникиОрганизаций.ПричинаИзмененияСостоянияЗавершения
			|			ИНАЧЕ РаботникиОрганизаций.ПричинаИзмененияСостояния 
			|	КОНЕЦ <> ЗНАЧЕНИЕ(Перечисление.ПричиныИзмененияСостояния.Увольнение)";
			
		КонецЕсли;
		
	КонецЕсли;
	
	ВводНаОсновании.Загрузить(Запрос.Выполнить().Выгрузить());
	
КонецПроцедуры // Автозаполнение()

// Функция возвращает имя документа в регл учете в зависимости от 
// документа-основания
//
Функция ПолучитьИмяДокумента() Экспорт
	
	Если ТипЗнч(Основание) = Тип("ДокументСсылка.ПриемНаРаботу") Тогда
		Возврат "ПриемНаРаботуВОрганизацию";
		
	ИначеЕсли ТипЗнч(Основание) = Тип("ДокументСсылка.КадровоеПеремещение") Тогда
		Возврат "КадровоеПеремещениеОрганизаций";
		
	ИначеЕсли ТипЗнч(Основание) = Тип("ДокументСсылка.Увольнение") Тогда
		Возврат "УвольнениеИзОрганизаций";
		
	ИначеЕсли ТипЗнч(Основание) = Тип("ДокументСсылка.ПланированиеОтпуска") Тогда
		Возврат "ГрафикОтпусковОрганизаций";
		
	Иначе
		Возврат ОтпускаОрганизацийПереопределяемый.ПолучитьИмяДокументаДополнительно(Основание);
		
	КонецЕсли;
	
КонецФункции // ПолучитьИмяДокумента()

////////////////////////////////////////////////////////////////////////////////
// ОПЕРАТОРЫ ОСНОВНОЙ ПРОГРАММЫ