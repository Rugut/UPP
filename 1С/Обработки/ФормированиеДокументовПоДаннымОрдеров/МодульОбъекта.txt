#Если Клиент Тогда
// Основная функция действия формирования документов.
//
// Параметры
//	УдалятьУчтенныеДокументы - булево, если Истина, 
//			то в транзакции формирования документов
//			сформированные документы будут проводиться
//			а учтенные - помечаться на удаление.
//
// Возвращаемое значение
//  Истина - документы сформированы успешно
Функция СформироватьДокументы() Экспорт
	
	Заголовок   = "Формирование документов по настройке " + Настройка + " за период " + ПредставлениеПериода(ДатаНачалаПериода, КонецДня(ДатаОкончанияПериода));
	ТекстОшибки = "";
	
	//Выполним логику, находящуюся в регламенте
	МассивСформированныеДокументы = Неопределено;
	МассивУчтенныеДокументы		  = Неопределено;
		
	ЭкземплярРегламента = РегламентноеФормированиеДокументов.ПолучитьЭкземплярРегламента(Настройка);
	
	ОбъектНастройки = Настройка.ПолучитьОбъект();
	
	// Заблокируем объект настройки
	// Вызов метода Разблокировать() выполнять не обязательно, он будет вызван неявно при выходе из процедуры
	Попытка
		ОбъектНастройки.Заблокировать();
	Исключение
		ТекстОшибки = "Ошибка при попытке заблокировать настройку: " + ОписаниеОшибки();
		ОбщегоНазначения.СообщитьОбОшибке(ТекстОшибки, , Заголовок);
		Возврат Ложь;
	КонецПопытки;
	
	НачатьТранзакцию();
	
	// Формируем документы
	Если НЕ РегламентноеФормированиеДокументов.СформироватьДокументы(ЭкземплярРегламента, ДатаНачалаПериода, ДатаОкончанияПериода, ТекстОшибки, МассивСформированныеДокументы, МассивУчтенныеДокументы) Тогда
		ОтменитьТранзакцию();
		ОбщегоНазначения.СообщитьОбОшибке(ТекстОшибки, , Заголовок);
		Возврат Ложь;
	КонецЕсли;
	
	// Запишем дату, по которую сформированы документы
	Если МассивСформированныеДокументы.Количество() > 0 Тогда
		Если НЕ РегламентноеФормированиеДокументов.УстановитьПериодФормированияДокументов(ОбъектНастройки, ДатаОкончанияПериода, ТекстОшибки) Тогда
			ОтменитьТранзакцию();
			ОбщегоНазначения.СообщитьОбОшибке("Ошибка при записи даты, по которую учтены данные сформированных документов: " + ТекстОшибки, , Заголовок);
			Возврат Ложь;
		КонецЕсли;
	КонецЕсли;
	ЗафиксироватьТранзакцию();

	// Подготовим таблицу учтенных документов
	ТаблицаУчтенныеДокументы = Новый ТаблицаЗначений();
	ТаблицаУчтенныеДокументы.Колонки.Добавить("Документ");
	ТаблицаУчтенныеДокументы.Колонки.Добавить("Проведен", Новый ОписаниеТипов("Булево"));
	
	Для Каждого Документ Из МассивУчтенныеДокументы Цикл
		ТаблицаУчтенныеДокументы.Добавить().Документ = Документ;
	КонецЦикла;
		
	// Пометим на удаление учтенные документы и проведем сформированные
	Если НЕ РегламентноеФормированиеДокументов.УдалитьУчтенныеДокументы(ЭкземплярРегламента, ТаблицаУчтенныеДокументы, Ложь, ТекстОшибки) Тогда
		ОбщегоНазначения.СообщитьОбОшибке(ТекстОшибки, , Заголовок);
	КонецЕсли;
	
	Если НЕ РегламентноеФормированиеДокументов.ПровестиСформированныеДокументы(ЭкземплярРегламента, МассивСформированныеДокументы, Ложь, ТекстОшибки) Тогда
		ОбщегоНазначения.СообщитьОбОшибке(ТекстОшибки, , Заголовок);
	КонецЕсли;
	
	// Поместим списки документов в табличные части
	СформированныеДокументы.Очистить();
	Для Каждого Документ Из МассивСформированныеДокументы Цикл
		СформированныеДокументы.Добавить().Документ = Документ;
	КонецЦикла;

	// В табличную часть запишем информацию с учетом флагов "Проведен"
	УчтенныеДокументы.Загрузить(ТаблицаУчтенныеДокументы);
	
	Возврат Истина;
	
КонецФункции //СформироватьДокументы()

// Заполняет период формирования по данным настройки
//
Процедура ЗаполнитьДаннымиНастройки() Экспорт
	
	//Период определяется настройкой
	Если Настройка.Пустая() Тогда
		ДатаНачалаПериода		= '0001-01-01';
		ДатаОкончанияПериода 	= '0001-01-01';
	Иначе
		ДатаНачалаПериода		= РегламентноеФормированиеДокументов.ПолучитьДатуНачалаСледующегоПериодаФормированияДокументов(Настройка);
		ДатаОкончанияПериода 	= РабочаяДата;
	КонецЕсли;
	
	СформированныеДокументы.Очистить();
	УчтенныеДокументы.Очистить();
	
КонецПроцедуры
#КонецЕсли