Перем АктивныеЭлементыСхемы;

Перем ЦветФонаАктивнойДекорации;
Перем ЦветФонаНеактивнойДекорации;


////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ СХЕМЫ

// Процедура отрабатывает выбор элемента схемы
//
Процедура СхемаАдминистрированияВыбор(Элемент)
	
	Если Элемент.ТекущийЭлемент = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ИмяЭлементаСхемы = Элемент.ТекущийЭлемент.Имя;	
	Если Лев(ИмяЭлементаСхемы, 5) = "Лапка" Тогда
		ИмяЭлементаСхемы = Сред(ИмяЭлементаСхемы, 7);
	КонецЕсли; 
	
	Если НЕ АктивныеЭлементыСхемы.Свойство(ИмяЭлементаСхемы) 
		ИЛИ НЕ АктивныеЭлементыСхемы[ИмяЭлементаСхемы] Тогда
		
		Возврат;
	КонецЕсли; 
	
	
	Если ИмяЭлементаСхемы = "ПраваДоступа" Тогда
		
		НастройкаПравДоступа.РедактироватьПраваДоступа(Неопределено);
		
	ИначеЕсли ИмяЭлементаСхемы = "ФормаСпискаПользователей" Тогда
		
		Справочники.Пользователи.ПолучитьФормуСписка().Открыть();
		
	ИначеЕсли ИмяЭлементаСхемы = "ГрупповаяОбработкаПользователейПрофили" 
		ИЛИ ИмяЭлементаСхемы = "ГрупповаяОбработкаПользователейКомандныеПанели" Тогда
		
		УправлениеПользователями.ОткрытьГрупповуюОбработкуПользователей();
		
	ИначеЕсли ИмяЭлементаСхемы = "ДополнительныеПрава" Тогда
		
		РегистрыСведений.ЗначенияДополнительныхПравПользователя.ПолучитьФорму("ФормаРедактирования").Открыть();
	ИначеЕсли ИмяЭлементаСхемы = "ФормаГруппыПользователейСПодбором" 
		ИЛИ ИмяЭлементаСхемы = "ФормаГруппыПользователей" Тогда
		
		ФормаДействия = ПолучитьФорму("ФормаГруппыПользователей", ЭтаФорма, ИмяЭлементаСхемы);
		ФормаДействия.ОтображатьПодборПользователей = (ИмяЭлементаСхемы = "ФормаГруппыПользователейСПодбором");
		ФормаДействия.Открыть();
		
	Иначе
		
		ПолучитьФорму(ИмяЭлементаСхемы, ЭтаФорма, ИмяЭлементаСхемы).Открыть();
		
	КонецЕсли;
	
КонецПроцедуры

// Процедура изменяет фон элементов схемы согласно их активности
//
Процедура УстановитьАктивностьЭлементовСхемы()
	
	Если АктивныеЭлементыСхемы = Неопределено Тогда
		АктивныеЭлементыСхемы = Новый Структура;
		
		АктивныеЭлементыСхемы.Вставить("ФормаПрофилиПолномочийПользователей", Истина);
		АктивныеЭлементыСхемы.Вставить("ФормаСпискаПользователей", Истина);
		АктивныеЭлементыСхемы.Вставить("ГрупповаяОбработкаПользователейПрофили", Истина);
		АктивныеЭлементыСхемы.Вставить("ГрупповаяОбработкаПользователейШаблоныНастроек", Истина);
		АктивныеЭлементыСхемы.Вставить("ГрупповаяОбработкаПользователейКомандныеПанели", Истина);
		АктивныеЭлементыСхемы.Вставить("КомандныеПанелиПользователей", Истина);
		АктивныеЭлементыСхемы.Вставить("ФормаГруппыПользователей", Истина);
		АктивныеЭлементыСхемы.Вставить("ФормаГруппыПользователейСПодбором", Истина);
		
		
		АктивныеЭлементыСхемы.Вставить("ДатаЗапретаРедактирования", Истина);
		
		АктивныеЭлементыСхемы.Вставить("ДополнительныеПрава", Истина);
	КонецЕсли;

	ИспользоватьОграниченияПравДоступаНаУровнеЗаписей = Константы.ИспользоватьОграниченияПравДоступаНаУровнеЗаписей.Получить();
	
	// В данном контексте группы пользователей используются для настройки прав доступа
	АктивныеЭлементыСхемы.Вставить("ПраваДоступа", ИспользоватьОграниченияПравДоступаНаУровнеЗаписей);
	
	Если ИспользоватьОграниченияПравДоступаНаУровнеЗаписей Тогда
		
		ИспользуемыеВидыОбъектовДоступа = НастройкаПравДоступа.ПолучитьИспользуемыеВидыОбъектовДоступа();
		
		ИспользуютсяКонтрагенты = ИспользуемыеВидыОбъектовДоступа.Найти(Перечисления.ВидыОбъектовДоступа.Контрагенты) <> Неопределено;
		ИспользуютсяФизЛица = ИспользуемыеВидыОбъектовДоступа.Найти(Перечисления.ВидыОбъектовДоступа.ФизическиеЛица) <> Неопределено;
		ИспользуютсяЗаявки = ИспользуемыеВидыОбъектовДоступа.Найти(Перечисления.ВидыОбъектовДоступа.ЗаявкиКандидатов) <> Неопределено;
		
		ДоступностьЭлементов = ИспользуютсяКонтрагенты ИЛИ ИспользуютсяФизЛица ИЛИ ИспользуютсяЗаявки;
		
		АктивныеЭлементыСхемы.Вставить("ФормаГруппыДоступа", ДоступностьЭлементов);
		
	Иначе
		
		АктивныеЭлементыСхемы.Вставить("ФормаГруппыДоступа", Ложь);
		
	КонецЕсли; 
	
	// Установим цвет фона элементов
	Для каждого ЭлементСхемы Из ЭлементыФормы.СхемаАдминистрирования.ЭлементыГрафическойСхемы Цикл
		Если ТипЗнч(ЭлементСхемы) = Тип("ЭлементГрафическойСхемыДекорация") Тогда
			ИмяЭлементаСхемы = ЭлементСхемы.Имя;
			Если НЕ АктивныеЭлементыСхемы.Свойство(ИмяЭлементаСхемы) Тогда
				Продолжить;
			КонецЕсли;  
			
			Если АктивныеЭлементыСхемы[ИмяЭлементаСхемы] Тогда
				ЭлементСхемы.ЦветФона = ЦветФонаАктивнойДекорации;
			Иначе
				ЭлементСхемы.ЦветФона = ЦветФонаНеактивнойДекорации;
			КонецЕсли; 
		КонецЕсли;
	КонецЦикла; 
	
КонецПроцедуры // УстановитьАктивностьЭлементовСхемы

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

// Обработчик события ПриОткрытии формы
//
Процедура ПриОткрытии()
	
	УстановитьАктивностьЭлементовСхемы();
	
КонецПроцедуры

// Обработчик события ОбработкаОповещения формы
//
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ИзмененыИспользуемыеВидыОбъектовДоступа"
		ИЛИ ИмяСобытия = "ИзмененоИспользоватьОграниченияПравДоступаНаУровнеЗаписей" Тогда
		
		УстановитьАктивностьЭлементовСхемы();
	КонецЕсли; 
	
КонецПроцедуры


АктивныеЭлементыСхемы = Неопределено;
ЦветФонаАктивнойДекорации   = WebЦвета.ДымчатоБелый;
ЦветФонаНеактивнойДекорации = Новый Цвет(224, 224, 224);