Перем мИспользуютсяКонтрагенты;
Перем мИспользуютсяФизЛица;
Перем мИспользуютсяЗаявки;

Процедура УстановитьАктивнуюЗакладку()
	
	Если мИспользуютсяКонтрагенты Тогда
		АктивнаяЗакладка = ЭлементыФормы.ПанельГруппДоступа.Страницы.ГруппыДоступаКонтрагентов;
		
	ИначеЕсли мИспользуютсяФизЛица Тогда
		АктивнаяЗакладка = ЭлементыФормы.ПанельГруппДоступа.Страницы.ГруппыДоступаФизическихЛиц;
		
	ИначеЕсли мИспользуютсяЗаявки Тогда
		АктивнаяЗакладка = ЭлементыФормы.ПанельГруппДоступа.Страницы.ГруппыЗаявокКандидатов;
		
	Иначе
		АктивнаяЗакладка = ЭлементыФормы.ПанельГруппДоступа.Страницы.ГруппыДоступаКонтрагентов;
	КонецЕсли; 
	
	ЭлементыФормы.ПанельГруппДоступа.ТекущаяСтраница = АктивнаяЗакладка;
	
КонецПроцедуры // УстановитьАктивнуюЗакладку

Процедура УстановитьВидимостьЗакладок()

	ИспользуемыеВидыОбъектовДоступа = НастройкаПравДоступа.ПолучитьИспользуемыеВидыОбъектовДоступа();
	
	мИспользуютсяКонтрагенты = ИспользуемыеВидыОбъектовДоступа.Найти(Перечисления.ВидыОбъектовДоступа.Контрагенты) <> Неопределено;
	мИспользуютсяФизЛица = ИспользуемыеВидыОбъектовДоступа.Найти(Перечисления.ВидыОбъектовДоступа.ФизическиеЛица) <> Неопределено;
	мИспользуютсяЗаявки = ИспользуемыеВидыОбъектовДоступа.Найти(Перечисления.ВидыОбъектовДоступа.ЗаявкиКандидатов) <> Неопределено;
	
	ЭлементыФормы.ПанельГруппыДоступаКонтрагентов.Страницы.ГруппыДоступаКонтрагентов.Видимость = мИспользуютсяКонтрагенты;
	ЭлементыФормы.ПанельГруппыДоступаКонтрагентов.Страницы.НастройкаНедоступна.Видимость = НЕ мИспользуютсяКонтрагенты;
	
	ЭлементыФормы.ПанельГруппыДоступаФизическихЛиц.Страницы.ГруппыДоступаФизическихЛиц.Видимость = мИспользуютсяФизЛица;
	ЭлементыФормы.ПанельГруппыДоступаФизическихЛиц.Страницы.НастройкаНедоступна.Видимость = НЕ мИспользуютсяФизЛица;
	
	ЭлементыФормы.ПанельГруппыЗаявокКандидатов.Страницы.ГруппыЗаявокКандидатов.Видимость = мИспользуютсяЗаявки;
	ЭлементыФормы.ПанельГруппыЗаявокКандидатов.Страницы.НастройкаНедоступна.Видимость = НЕ мИспользуютсяЗаявки;
	
КонецПроцедуры // УстановитьВидимостьЗакладок

Процедура ДействияФормыПраваДоступаПользователейКТекущемуЭлементу(Кнопка)
	
	ТекущиеДанные = Неопределено;
	
	Если ЭлементыФормы.ПанельГруппДоступа.ТекущаяСтраница.Имя = "ГруппыДоступаКонтрагентов" Тогда
		ТекущиеДанные = ЭлементыФормы.ГруппыДоступаКонтрагентов.ТекущиеДанные;
	ИначеЕсли ЭлементыФормы.ПанельГруппДоступа.ТекущаяСтраница.Имя = "ГруппыДоступаФизическихЛиц" Тогда
		ТекущиеДанные = ЭлементыФормы.ГруппыДоступаФизическихЛиц.ТекущиеДанные;
	ИначеЕсли ЭлементыФормы.ПанельГруппДоступа.ТекущаяСтраница.Имя = "ГруппыЗаявокКандидатов" Тогда
		ТекущиеДанные = ЭлементыФормы.ГруппыЗаявокКандидатов.ТекущиеДанные;
	КонецЕсли; 
	
	Если ТекущиеДанные <> Неопределено Тогда
		НастройкаПравДоступа.РедактироватьПраваДоступа(ТекущиеДанные.Ссылка);
	КонецЕсли;
	
КонецПроцедуры

Процедура ДействияФормыПраваДоступаПользователейКоВсемуСправочнику(Кнопка)
	
	Если ЭлементыФормы.ПанельГруппДоступа.ТекущаяСтраница.Имя = "ГруппыДоступаКонтрагентов" Тогда
		ТекущиеДанные = Справочники.ГруппыДоступаКонтрагентов.ПустаяСсылка();
	ИначеЕсли ЭлементыФормы.ПанельГруппДоступа.ТекущаяСтраница.Имя = "ГруппыДоступаФизическихЛиц" Тогда
		ТекущиеДанные = Справочники.ГруппыДоступаФизическихЛиц.ПустаяСсылка();
	ИначеЕсли ЭлементыФормы.ПанельГруппДоступа.ТекущаяСтраница.Имя = "ГруппыЗаявокКандидатов" Тогда
		ТекущиеДанные = Справочники.ГруппыЗаявокКандидатов.ПустаяСсылка();
	Иначе
		Возврат;
	КонецЕсли; 
	
	НастройкаПравДоступа.РедактироватьПраваДоступа(ТекущиеДанные);
	
КонецПроцедуры

Процедура ГруппыЗаявокКандидатовПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	Оповестить("ЗаписанаГруппаЗаявок", , ВладелецФормы);
	
КонецПроцедуры

Процедура ПередОткрытием(Отказ, СтандартнаяОбработка)
	
	Если НЕ ПараметрыСеанса.ИспользоватьОграниченияПравДоступаНаУровнеЗаписей Тогда
		Отказ = Истина;
		Предупреждение("Ограничение доступа на уровне записей не используется");
		Возврат;
	КонецЕсли;
	
	УстановитьВидимостьЗакладок();
	
КонецПроцедуры

Процедура ПриОткрытии()
	
	УстановитьАктивнуюЗакладку();
	
КонецПроцедуры

Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ИзмененыИспользуемыеВидыОбъектовДоступа" Тогда
		УстановитьВидимостьЗакладок();
	КонецЕсли; 
	
КонецПроцедуры

Процедура НадписьОткрытьФормуНастройкиНажатие(Элемент)
	
	НастройкаПравДоступа.ОткрытьПараметрыДоступаНаУровнеЗаписей();
	
КонецПроцедуры