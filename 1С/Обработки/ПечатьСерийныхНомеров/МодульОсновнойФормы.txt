//////////////////////////////////////////////////////////////////////////////////
//// ПРОЦЕДУРЫ И ФУНКЦИИ ОБЩЕГО НАЗНАЧЕНИЯ

// Возвращает строку, представляющую вызов обработки в меню
//
// Параметры
//	Нет
//
// Возвращаемое значение:
//	Строка, представляющая вызов обработки или "", если вызов обработки запрещён
//
Функция ПредставлениеВМеню() Экспорт

	Возврат ?(Константы.ИспользоватьСерийныеНомера.Получить(),
			  "Серийные номера",
			  "");

КонецФункции // ПредставлениеВМеню()

// Осуществляет печать серийных номеров для переданного документа
//
// Параметры
//	Товары - табличная часть товары
//	Номера - табличная часть номера
//
Процедура Печать(Товары, Номера) Экспорт
	Перем Номер;
	Перем НомераПоКлючу;
	Перем МассивНомеров;
	Перем Товар;
	Перем Строка;
	
	НомераПоКлючу = Новый Соответствие();
	Для Каждого Номер Из Номера Цикл
		МассивНомеров = НомераПоКлючу[Номер.КлючСвязи];
		Если МассивНомеров = Неопределено Тогда
			МассивНомеров = Новый Массив();
		КонецЕсли;
		МассивНомеров.Добавить(Номер.СерийныйНомер);
		НомераПоКлючу[Номер.КлючСвязи] = МассивНомеров;
	КонецЦикла;
	
	Для Каждого Товар Из Товары Цикл
		МассивНомеров = НомераПоКлючу[Товар.КлючСвязи];
		Если МассивНомеров = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		Для Каждого Номер Из МассивНомеров Цикл
			Строка = ПолеТоваров.Добавить();
			Строка.Номенклатура = Товар.Номенклатура;
			Строка.Качество = Товар.Качество;
			Строка.Характеристика = Товар.ХарактеристикаНоменклатуры;
			Строка.Серия = Товар.СерияНоменклатуры;
			Строка.СерийныйНомер = Номер;
			Строка.Количество = 1;
		КонецЦикла;
	КонецЦикла;
	
	Если ПолеТоваров.Количество() > 0 Тогда
		ЭтаФорма.Открыть();
	Иначе
		Сообщить("Список серийных номеров пуст", СтатусСообщения.Информация);
	КонецЕсли;
	
КонецПроцедуры // Печать()

//////////////////////////////////////////////////////////////////////////////////
//// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

// Обработчик вызывается перед открытием формы
//
// Параметры
//	Отказ		 			- булево, отказ от открытия формы
//	СтандартнаяОбработка    - булево, признак выполнения стандартной (системной)
//							  обработки события
//
Процедура ПередОткрытием(Отказ, СтандартнаяОбработка)

	Попытка
		КомпонентШК = Новый COMОбъект("V8.Barcod.1");
	Исключение
		Сообщить(
			"Компонента 1С:Печать штрих-кодов не установлена на данном компьютере!",
			СтатусСообщения.Важное);
		Отказ = Истина;
	КонецПопытки;

КонецПроцедуры // ПередОткрытием()

// Обработчик события открытия формы
//
// Параметры
//	Нет
//
Процедура ПриОткрытии()

	ЭлементыФормы.ФорматЭтикетки.СписокВыбора.Добавить(1,"Принтер этикеток");
	ЭлементыФормы.ФорматЭтикетки.СписокВыбора.Добавить(2,"А4");
	Если УправлениеПользователями.ПолучитьЗначениеПоУмолчанию(глЗначениеПеременной("глТекущийПользователь"),
									 "ПринтерЭтикеток") Тогда
		ФорматЭтикетки = 1;
		ВысотаЭтикетки = УправлениеПользователями.ПолучитьЗначениеПоУмолчанию(глЗначениеПеременной("глТекущийПользователь"),
													 "ПринтерЭтикетокВысота");
		ШиринаЭтикетки = УправлениеПользователями.ПолучитьЗначениеПоУмолчанию(глЗначениеПеременной("глТекущийПользователь"),
													 "ПринтерЭтикетокШирина");
	Иначе
		ФорматЭтикетки = 2;
		ШиринаЭтикетки = 38;
		ВысотаЭтикетки = 20;
		ЭлементыФормы.ШиринаЭтикетки.Доступность = Ложь;
		ЭлементыФормы.ВысотаЭтикетки.Доступность = Ложь;
	КонецЕсли;
	СразуНаПринтер = УправлениеПользователями.ПолучитьЗначениеПоУмолчанию(глЗначениеПеременной("глТекущийПользователь"),
												 "СразуНаПринтер");
КонецПроцедуры // ПриОткрытии()

//////////////////////////////////////////////////////////////////////////////////
//// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ РЕКВИЗИТОВ ШАПКИ

// Обработчик события изменения формата этикетки
//
// Параметры
//	Элемент		- поле выбора формата этикетки
//
Процедура ФорматЭтикеткиПриИзменении(Элемент)

	Если Элемент.Значение = 2 Тогда
		ШиринаЭтикетки = 38;
		ВысотаЭтикетки = 20;
		ЭлементыФормы.ШиринаЭтикетки.Доступность = Ложь;
		ЭлементыФормы.ВысотаЭтикетки.Доступность = Ложь;
	Иначе
		ЭлементыФормы.ШиринаЭтикетки.Доступность = Истина;
		ЭлементыФормы.ВысотаЭтикетки.Доступность = Истина;
	КонецЕсли;

КонецПроцедуры // ФорматЭтикеткиПриИзменении()

// Обработчик нажатия на кнопку "изменить количество"
//
// Параметры
//	Элемент		- кнопка "Изменить количество"
//
Процедура КоличествоКопийНажатие(Элемент)

	Перем Количество;
	
	Количество = 1;
	ВвестиЧисло(Количество, "Введите количество копий для всех серийных номеров:");
	ПолеТоваров.ЗаполнитьЗначения(Количество, "Количество");

КонецПроцедуры // КоличествоКопийНажатие()

//////////////////////////////////////////////////////////////////////////////////
//// ПРОЦЕДУРЫ - ДЕЙСТВИЯ КОМАНДНЫХ ПАНЕЛЕЙ ФОРМЫ

// Обработка нажатия на кнопку "Печать"
//
// Параметры
//	Элемент		- кнопка "Печать"
//
Процедура КоманднаяПанельФормыПечатать(Кнопка)
	
	ТекКолонка = 1;
	ТекСтрока  = 1;

	Таб                     = Новый ТабличныйДокумент;
	Таб.ИмяПараметровПечати = 	"ПАРАМЕТРЫ_ПЕЧАТИ_Этикетка"+СокрЛП(ИмяКомпьютера());
	Макет                   = ПолучитьОбщийМакет("Этикетка");
	ЭтикеткаОбласть         = Макет.ПолучитьОбласть(1,1,4,2);
	ОбластьНоменклатура     = ЭтикеткаОбласть.Области.Номенклатура;
	ОбластьШтрихкод         = ЭтикеткаОбласть.Области.ОбластьШтрихкод;
	РисунокШтрихкод         = ЭтикеткаОбласть.Рисунки.Штрихкод;
	ОбластьЦена             = ЭтикеткаОбласть.Области.Цена;

	ОбластьНоменклатура.ВысотаСтроки = ВысотаЭтикетки * 2.65 * 0.3;
	ОбластьШтрихкод.ВысотаСтроки     = ВысотаЭтикетки * 2.65 * 0.5;
	РисунокШтрихкод.Расположить(ОбластьШтрихкод);
	ОбластьЦена.ВысотаСтроки         = ВысотаЭтикетки * 2.65 * 0.2;

	ЭтикеткаОбласть.Область(2,2,2,2).ШиринаКолонки = ШиринаЭтикетки*0.53;

	Если ФорматЭтикетки  = 1 Тогда //принтер этикеток.
		ВсегоСтрок       = 1;
		КоличествоКолонок=1;
	ИначеЕсли ФорматЭтикетки = 2 Тогда //А4 по колонкам.

		ОбластьГраницаСправа      = Макет.ПолучитьОбласть(2,4,4,4);
		ОбластьНизГраницыСправа   = Макет.ПолучитьОбласть(5,4,5,4);
		ОбластьГраницаСнизу       = Макет.ПолучитьОбласть(6,2,6,2);
		ОбластьНачалоГраницыСнизу = Макет.ПолучитьОбласть(6,1,6,1);

		ОбластьГраницаСправа.Рисунки.ГраницаСправа.Высота = ВысотаЭтикетки*1.09;
		ОбластьГраницаСнизу.Рисунки.ГраницаСнизу.Ширина   = ШиринаЭтикетки;

		ОбластьГраницаСправа.Область(1,1,1,1).ВысотаСтроки = ВысотаЭтикетки*2.65;
		ОбластьГраницаСнизу.Область(1,1,1,1).ШиринаКолонки = ШиринаЭтикетки*0.53;

		ВсегоСтрок        = 19;
		КоличествоКолонок = 5;
	КонецЕсли;

	ВсегоКолонок = КоличествоКолонок;
	Если ВсегоКолонок > 1 Тогда
		ВсегоКолонок = ВсегоКолонок+ВсегоКолонок - 1; //добавляем разделители колонок.
	КонецЕсли;

	ВывелиХотяБыОдинШтрихКод = Ложь;
	Для Каждого СтрокаИзСписка Из ПолеТоваров Цикл
		ЭтикеткаОбласть.Рисунки.Штрихкод.Объект.АвтоТип = Истина;
		ЭтикеткаОбласть.Рисунки.Штрихкод.Объект.Сообщение = СокрЛП(СтрокаИзСписка.СерийныйНомер.Код);
		ОбластьНоменклатура.Текст        = "" + СтрокаИзСписка.Номенклатура;
		Для Копий =1 По СтрокаИзСписка.Количество Цикл
			Если ТекКолонка>ВсегоКолонок Тогда //новая строка.
				Если ВсегоСтрок<>1 Тогда //выведем разделитель.
					Для Сч = 1 по ВсегоКолонок Цикл
						Если Сч = 1 Тогда
							Таб.Вывести(ОбластьНачалоГраницыСнизу);
						Иначе
							Таб.Присоединить(ОбластьНачалоГраницыСнизу);
						КонецЕсли;
						Таб.Присоединить(ОбластьГраницаСнизу);
						Таб.Присоединить(ОбластьНизГраницыСправа);
						Сч = Сч + 1;
					КонецЦикла;
					ТекСтрока = ТекСтрока + 1;
				КонецЕсли;
				Если ТекСтрока/ВсегоСтрок = Цел(ТекСтрока/ВсегоСтрок) Тогда
					Таб.ВывестиГоризонтальныйРазделительСтраниц();
				КонецЕсли;
				ТекСтрока = ТекСтрока + 1;
				ТекКолонка=1;
			КонецЕсли;
			Если ТекКолонка<>1 Тогда //выведем разделитель.
				Таб.Присоединить(ОбластьГраницаСправа);
				ТекКолонка = ТекКолонка + 1;
				Таб.Присоединить(ЭтикеткаОбласть);
				ТекКолонка = ТекКолонка + 1;
			Иначе
				Таб.Вывести(ЭтикеткаОбласть);
				//Таб.Показать();
				ТекКолонка = ТекКолонка + 1;
			КонецЕсли;

			ВывелиХотяБыОдинШтрихКод = Истина;
		КонецЦикла;
	КонецЦикла;

	Если ВывелиХотяБыОдинШтрихКод Тогда
		Если СразуНаПринтер Тогда
			Таб.Напечатать();
		Иначе
			Таб.Защита              = Ложь;
			Таб.ТолькоПросмотр      = Истина;
			Таб.ОтображатьСетку     = Ложь;
			Таб.ОтображатьЗаголовки = Ложь;
			Таб.Показать("Этикетка");
		КонецЕсли;

	КонецЕсли;

КонецПроцедуры // КоманднаяПанельФормыПечатать()