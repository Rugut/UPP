////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОБЩЕГО НАЗНАЧЕНИЯ

Процедура ПроверитьПравильностьВыполненияШаговМастера(Отказ, СтрокаСообщения)
	
	Если ПустаяСтрока(ЭтаФорма.ИмяФайлаВнешнейОбработкиОбработчиковСобытий) Тогда
		
		ВывестиСообщениеОбОшибке(Отказ, 4, СтрокаСообщения, "Укажите имя файла внешней обработки.");
		
	ИначеЕсли ПустаяСтрока(ИмяВременногоФайлаОбработчиковСобытий) Тогда
		
		ВывестиСообщениеОбОшибке(Отказ, 2, СтрокаСообщения, "Код обработчиков не выгружен.");
		
	КонецЕсли; 
	
	Если Отказ Тогда
		
		Возврат;
		
	КонецЕсли; 
	
	ФайлВнешнейОбработкиОбработчиковСобытий = Новый Файл(ЭтаФорма.ИмяФайлаВнешнейОбработкиОбработчиковСобытий);
	ВременныйФайлОбработчиковСобытий        = Новый Файл(ИмяВременногоФайлаОбработчиковСобытий);
	
	Если Не ФайлВнешнейОбработкиОбработчиковСобытий.Существует() Тогда
		
		ВывестиСообщениеОбОшибке(Отказ, 4, СтрокаСообщения, "Указанный файл внешней обработки не существует.");
	
	ИначеЕсли Не ВременныйФайлОбработчиковСобытий.Существует() Тогда
		
		ВывестиСообщениеОбОшибке(Отказ, 2, СтрокаСообщения, "Код обработчиков не выгружен.");
		
	ИначеЕсли ФайлВнешнейОбработкиОбработчиковСобытий.ПолучитьВремяИзменения() 
		   < ВременныйФайлОбработчиковСобытий.ПолучитьВремяИзменения() Тогда
		
		ВывестиСообщениеОбОшибке(Отказ, 4, СтрокаСообщения, "Файл внешней обработки не обновлен данными из последней выгрузки.");
		
	КонецЕсли; 
	
КонецПроцедуры 

Процедура УстановитьВидимость()
	
	//Выделение красным цветом шагов мастера, которые выполнены неправильно
	УстановитьВыделениеРамки("Рамка_Шаг_2", ПустаяСтрока(ИмяВременногоФайлаОбработчиковСобытий));
	УстановитьВыделениеРамки("Рамка_Шаг_4", ПустаяСтрока(ЭтаФорма.ИмяФайлаВнешнейОбработкиОбработчиковСобытий));
	
	ЗаголовокРамки = "1. Выберите режим отладки кода алгоритмов ("
	           + ЭлементыФормы["ПереключательОтладкаАлгоритмов_"+Строка(ЭтаФорма.РежимОтладкиАлгоритмов)].Заголовок+")";
			   
	УстановитьЗаголовокРамки("Рамка_Шаг_1", ЗаголовокРамки);
	УстановитьЗаголовокРамки("Рамка_Шаг_2", "2. Выгрузите код обработчиков");
	УстановитьЗаголовокРамки("Рамка_Шаг_3", "3. Пояснения к созданию файла внешней обработки");
	УстановитьЗаголовокРамки("Рамка_Шаг_4", "4. Создайте (обновите) файл внешней обработки");
	
	ЭлементыФормы.КнопкаОткрытьФайл.Доступность = Не ПустаяСтрока(ИмяВременногоФайлаОбработчиковСобытий);
	
	//Устанавливаем текущие панели с текстом подсказок
	УстановитьСтраницуПанелиПодсказки("ПанельПодсказкиШаг_1", ЭтаФорма.РежимОтладкиАлгоритмов);
	
КонецПроцедуры 

Процедура ВывестиСообщениеОбОшибке(Отказ, НомерШага, СтрокаСообщения, СообщениеОбОшибке) 
	
	//Выделяем рамку красным цветом
	УстановитьВыделениеРамки("Рамка_Шаг_"+Строка(НомерШага), Истина);
	
	//Формируем сообщение об ошибке 
	СтрокаСообщения = "Шаг №" + Строка(НомерШага) + ": " + СообщениеОбОшибке;
	
	Отказ = Истина;
	
КонецПроцедуры  

Процедура УстановитьЗаголовокРамки(ИмяРамки, Заголовок); 
	
	ЭлементыФормы[ИмяРамки].Заголовок = Заголовок;
	
КонецПроцедуры

Процедура УстановитьВыделениеРамки(ИмяРамки, НадоВыделитьРамку = Ложь) 
	
	РамкаШагаМастера = ЭлементыФормы[ИмяРамки];
	
	Если НадоВыделитьРамку Тогда
		
		РамкаШагаМастера.ЦветРамки = ЦветаСтиля.ЦветОсобогоТекста;
		
	Иначе
		
		РамкаШагаМастера.ЦветРамки = ЦветаСтиля.ЦветРамки;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПоказатьОбработчикиСобытийВОкне()
	
	ФормаОбработчиков = ПолучитьФорму("ФормаРедактораТекстовогоДокумента", ЭтаФорма);
	
	ЗаполнитьПолеТекстовогоДокумента(ФормаОбработчиков.ЭлементыФормы, "ПолеОбработчикиСобытий", ИмяВременногоФайлаОбработчиковСобытий);
	ЗаполнитьПолеТекстовогоДокумента(ФормаОбработчиков.ЭлементыФормы, "ПолеОшибкиВыгрузки",     ИмяВременногоФайлаПротоколаОбмена);
	
	ФормаОбработчиков.Открыть();
	
КонецПроцедуры

Процедура ЗаполнитьПолеТекстовогоДокумента(ЭлементыОткрытойФормы, ИмяПоля, ИмяФайла) 
	
	Если Не ПустаяСтрока(ИмяФайла) Тогда
		
		Файл = Новый Файл(ИмяФайла);
		
		Если Файл.Существует() Тогда
			
			//Заполняем данными текстовое поле
			ЭлементыОткрытойФормы[ИмяПоля].Прочитать(ИмяФайла);
			
			//Определяем видимость страницы панели обработчиков
			ТекущаяСтраницаПанелиОбработчиков = ЭлементыОткрытойФормы.ПанельОбработчиков.Страницы["Страница_" + ИмяПоля];
			
			ТекущаяСтраницаПанелиОбработчиков.Видимость = (Файл.Размер() <> 0); 
			
			//Определяем текущую страницу
			Если ТекущаяСтраницаПанелиОбработчиков.Видимость Тогда
				
				ЭлементыОткрытойФормы.ПанельОбработчиков.ТекущаяСтраница = ТекущаяСтраницаПанелиОбработчиков;
			
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

// Открывает диалог выбора файла
//
// Параметры:
//  Элемент                - Элемент управления, для которого выбираем файл
//  ПроверятьСуществование - Если Истина, то выбор отменяется если файл не существует
// 
Процедура ВыборФайла(Элемент, ПроверятьСуществование = Ложь, Знач РасширениеПоУмолчанию = "txt")
	
	ДиалогВыбораФайла = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);

	Если РасширениеПоУмолчанию = "txt" Тогда
		
		ДиалогВыбораФайла.Фильтр     = "Файл выгрузки обработчиков событий (*.txt)|*.txt";
		ДиалогВыбораФайла.Расширение = "txt";
		
	ИначеЕсли РасширениеПоУмолчанию = "epf" Тогда
		
		ДиалогВыбораФайла.Фильтр     = "Файл внешней обработки обработчиков событий (*.epf)|*.epf";
		ДиалогВыбораФайла.Расширение = "epf";
			
	КонецЕсли;
	
	ДиалогВыбораФайла.Заголовок                    = "Выберите файл";
	ДиалогВыбораФайла.ПредварительныйПросмотр      = Ложь;
	ДиалогВыбораФайла.ИндексФильтра                = 0;
	ДиалогВыбораФайла.ПолноеИмяФайла               = Элемент.Значение;
	ДиалогВыбораФайла.ПроверятьСуществованиеФайла  = ПроверятьСуществование;
	
	Если ДиалогВыбораФайла.Выбрать() Тогда
		
		Элемент.Значение = ДиалогВыбораФайла.ПолноеИмяФайла;
		
		Если Элемент = ЭлементыФормы.ИмяФайлаВнешнейОбработкиОбработчиковСобытий Тогда 
			
			ИмяФайлаВнешнейОбработкиОбработчиковСобытийПриИзменении(Элемент)
			
		КонецЕсли;
				
	КонецЕсли;
	
КонецПроцедуры

Процедура УстановитьЗначенияРеквизитовОбработки()
	
	ОбработкаОбъект.ИмяФайлаВнешнейОбработкиОбработчиковСобытий = ЭтаФорма.ИмяФайлаВнешнейОбработкиОбработчиковСобытий;
	ОбработкаОбъект.РежимОтладкиАлгоритмов                      = ЭтаФорма.РежимОтладкиАлгоритмов;
	ОбработкаОбъект.ОбработчикиСобытийЧитаемИзФайлаПравилОбмена = ЭтаФорма.ОбработчикиСобытийЧитаемИзФайлаПравилОбмена;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

Процедура ПриОткрытии()
	
	ЭтаФорма.Заголовок = "Настройка отладки обработчиков при "
	      + ?(ЭтаФорма.ОбработчикиСобытийЧитаемИзФайлаПравилОбмена, "выгрузке", "загрузке") + " данных";
		  
	ЭлементыФормы.КнопкаВыгрузитьКодОбработчиков.Заголовок = "Сформировать модуль отладки " 
	      + ?(ЭтаФорма.ОбработчикиСобытийЧитаемИзФайлаПравилОбмена, "выгрузки", "загрузки");
	
	УстановитьВидимость();
	
КонецПроцедуры

Процедура ПриЗакрытии()
	
	УдалитьВременныеФайлы(ИмяВременногоФайлаОбработчиковСобытий);
	УдалитьВременныеФайлы(ИмяВременногоФайлаПротоколаОбмена);
	
КонецПроцедуры


////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ УПРАВЛЕНИЯ ФОРМЫ

Процедура КнопкаГотовоНажатие(Элемент)
	
	Отказ = Ложь;
	
	СтрокаСообщения = "";
	ПроверитьПравильностьВыполненияШаговМастера(Отказ, СтрокаСообщения);
	
	Если Отказ Тогда
		Предупреждение("Не все необходимые шаги выполнены:" + Символы.ПС + Символы.ПС + СтрокаСообщения); 
		Возврат;
	КонецЕсли; 
	
	УстановитьЗначенияРеквизитовОбработки();
	
	ЭтаФорма.Закрыть(Истина);
	
КонецПроцедуры

Процедура ИмяФайлаВнешнейОбработкиОбработчиковСобытийНачалоВыбора(Элемент, СтандартнаяОбработка)
	
	ВыборФайла(Элемент, Истина, "epf");
	
КонецПроцедуры

Процедура КнопкаОткрытьФайлНажатие(Элемент)
	
	ПоказатьОбработчикиСобытийВОкне();

КонецПроцедуры

Процедура ИмяФайлаВнешнейОбработкиОбработчиковСобытийПриИзменении(Элемент)
	
	УстановитьВидимость();
	
КонецПроцедуры

Процедура КнопкаВыгрузитьКодОбработчиковНажатие(Элемент)
	
	//Возможно выгрузку уже совершали ранее...
	Если Не ПустаяСтрока(ИмяВременногоФайлаОбработчиковСобытий) Тогда
		
		Ответ = Вопрос("Произвести выгрузку обработчиков повторно (Да) или открыть существующую выгрузку (Нет)?", РежимДиалогаВопрос.ДаНетОтмена,,КодВозвратаДиалога.Нет);
		
		//Открываем имеющийся файл выгрузки
		Если Ответ = КодВозвратаДиалога.Нет Тогда
			
			ПоказатьОбработчикиСобытийВОкне();
			
			Возврат;
			
		ИначеЕсли Ответ = КодВозвратаДиалога.Отмена Тогда
			
			Возврат;
			
		КонецЕсли;
		
	КонецЕсли; 
	
	//Проверка на указание имени файла правил или файла обмена
	Если ЭтаФорма.ОбработчикиСобытийЧитаемИзФайлаПравилОбмена И ПустаяСтрока(ИмяФайлаПравилОбмена) Тогда
		
		Предупреждение("Выберите файл правил обмена.");
		Возврат;
		
	ИначеЕсли НЕ ЭтаФорма.ОбработчикиСобытийЧитаемИзФайлаПравилОбмена И ПустаяСтрока(ИмяФайлаОбмена) Тогда
		
		Предупреждение("Выберите файл обмена.");
		Возврат;
		
	КонецЕсли;

	Отказ = Ложь;

	УстановитьЗначенияРеквизитовОбработки();

	ВыгрузитьОбработчикиСобытий(Отказ);
	
	Если Не Отказ Тогда
		
		УстановитьВидимость();
		
		ПоказатьОбработчикиСобытийВОкне();
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПереключательОтладкаАлгоритмов_0ПриИзменении(Элемент)
	
	УстановитьВидимость();
	
КонецПроцедуры

Процедура УстановитьСтраницуПанелиПодсказки(ИмяПанели, ИндексСтраницы) 
	
	ЭлементыФормы[ИмяПанели].ТекущаяСтраница = ЭлементыФормы[ИмяПанели].Страницы["Подсказка_" + Строка(ИндексСтраницы)];
	
КонецПроцедуры