Процедура КнопкаВыполнитьНажатие(Кнопка)
	Записать();
	Закрыть();
КонецПроцедуры


Функция ПолучитьСтратегиюПоОсновнойФормеСписка(МенеджерОбъекта, ОбъектМетаданные)
	
	Если ОбъектМетаданные.ОсновнаяФормаСписка <> Неопределено Тогда
		ОсновнаяФормаСписка = МенеджерОбъекта.ПолучитьФорму(ОбъектМетаданные.ОсновнаяФормаСписка.Имя);
		Попытка
			Автонумерация = Неопределено;
			Для Каждого ЭлементУправления ИЗ ОсновнаяФормаСписка.ЭлементыФормы Цикл
				Если ТипЗнч(ЭлементУправления) = Тип("ТабличноеПоле") Тогда
					Попытка
						ЭлементНайден = (Метаданные.НайтиПоТипу(ТипЗнч(ОсновнаяФормаСписка[ЭлементУправления.Данные])) = ОбъектМетаданные);
					Исключение
						ЭлементНайден = Ложь;
					КонецПопытки;
					
					Если ЭлементНайден Тогда
						Автонумерация = ЭлементУправления.Автонумерация;
						Прервать;
					КонецЕсли;				
				КонецЕсли;
			КонецЦикла;
			Если Автонумерация = Неопределено ИЛИ Автонумерация = АвтонумерацияВФорме.Авто Тогда
				Возврат Перечисления.СтратегияРедактированияНомеровОбъектов.ПустаяСсылка();
			Иначе
				Возврат ПолучитьСтратегиюПоУмолчаниюДляОбъекта(ОбъектМетаданные);			
			КонецЕсли;
		Исключение
			Возврат Перечисления.СтратегияРедактированияНомеровОбъектов.ПустаяСсылка();
		КонецПопытки;
	Иначе
		Возврат Перечисления.СтратегияРедактированияНомеровОбъектов.ПустаяСсылка();		
	КонецЕсли;
	
КонецФункции

Функция ПолучитьСтратегиюПоОсновнойФормеОбъекта(МенеджерОбъекта, ОбъектМетаданные)	
	
	Если ОбъектМетаданные.ОсновнаяФормаОбъекта <> Неопределено Тогда
		ОсновнаяФормаОбъекта = МенеджерОбъекта.ПолучитьФорму(ОбъектМетаданные.ОсновнаяФормаОбъекта.Имя);
		Попытка
			Если ОсновнаяФормаОбъекта.Автонумерация = АвтонумерацияВФорме.Авто Тогда
				Возврат Перечисления.СтратегияРедактированияНомеровОбъектов.ПустаяСсылка();
			КонецЕсли;
		Исключение
			Возврат Перечисления.СтратегияРедактированияНомеровОбъектов.ПустаяСсылка();
		КонецПопытки;		
		
		Возврат ПолучитьСтратегиюПоУмолчаниюДляОбъекта(ОбъектМетаданные);
		
	Иначе
		Возврат Перечисления.СтратегияРедактированияНомеровОбъектов.ПустаяСсылка();		
	КонецЕсли;	

КонецФункции

Функция ПолучитьСохраненнуюСтратегиюДляОбъекта(ТаблицаЗапроса, СтруктураПоиска)
	
	СтрокаСохраненнойСтратегии = ТаблицаЗапроса.НайтиСтроки(СтруктураПоиска);
	
	Если СтрокаСохраненнойСтратегии.Количество() > 0 Тогда
		Возврат СтрокаСохраненнойСтратегии[0].СтратегияРедактированияНомераОбъекта;
	Иначе
		Возврат Перечисления.СтратегияРедактированияНомеровОбъектов.ПустаяСсылка();
	КонецЕсли;						
	
КонецФункции

Функция ПолучитьСтратегиюПоУмолчаниюДляОбъекта(МетаданныеОбъекта)
	Возврат Перечисления.СтратегияРедактированияНомеровОбъектов.НеДоступно;
КонецФункции

Процедура ДеревоОбъектовПриПолученииДанных(Элемент, ОформленияСтрок)
	Для Каждого ОформлениеСтроки ИЗ ОформленияСтрок Цикл
		
		Если ОформлениеСтроки.ДанныеСтроки.Уровень() = 0 Тогда
			ОформлениеСтроки.Ячейки.СтратегияРедактирования.ТолькоПросмотр = Истина;			
			Если ОформлениеСтроки.ДанныеСтроки.ОбъектИмя = "Справочники" Тогда
				ОформлениеСтроки.Ячейки.Объект.УстановитьКартинку(БиблиотекаКартинок.Справочник);
			ИначеЕсли ОформлениеСтроки.ДанныеСтроки.ОбъектИмя = "Документы" Тогда
				ОформлениеСтроки.Ячейки.Объект.УстановитьКартинку(БиблиотекаКартинок.Документ);
			ИначеЕсли ОформлениеСтроки.ДанныеСтроки.ОбъектИмя = "ПланыВидовХарактеристик" Тогда
				ОформлениеСтроки.Ячейки.Объект.УстановитьКартинку(БиблиотекаКартинок.ПланВидовХарактеристик);
			КонецЕсли;
		Иначе
			
			Если ОформлениеСтроки.Ячейки.СтратегияРедактирования.Значение = Перечисления.СтратегияРедактированияНомеровОбъектов.ПустаяСсылка() Тогда
				ОформлениеСтроки.Ячейки.СтратегияРедактирования.УстановитьТекст("Не поддерживается");
				ОформлениеСтроки.Ячейки.СтратегияРедактирования.ТолькоПросмотр = Истина;
			КонецЕсли;
						
			Если ОформлениеСтроки.ДанныеСтроки.Родитель.ОбъектИмя = "Справочники" Тогда
				ОформлениеСтроки.Ячейки.Объект.УстановитьКартинку(БиблиотекаКартинок.СправочникОбъект);
			ИначеЕсли ОформлениеСтроки.ДанныеСтроки.Родитель.ОбъектИмя = "Документы" Тогда
				ОформлениеСтроки.Ячейки.Объект.УстановитьКартинку(БиблиотекаКартинок.ДокументОбъект);
			ИначеЕсли ОформлениеСтроки.ДанныеСтроки.Родитель.ОбъектИмя = "ПланыВидовХарактеристик" Тогда
				ОформлениеСтроки.Ячейки.Объект.УстановитьКартинку(БиблиотекаКартинок.ПланВидовХарактеристикОбъект);
			КонецЕсли;
			
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

Процедура ДеревоОбъектовПриАктивизацииСтроки(Элемент)
	ЭлементыФормы.КоманднаяПанельДереваОбъектов.Кнопки.ПодменюОбновитьНумерацию.Кнопки.ОбновитьНумерациюТекущегоОбъекта.Доступность = (Элемент.ТекущиеДанные <> Неопределено И Элемент.ТекущиеДанные.Уровень() > 0);
КонецПроцедуры

Процедура ОсновныеДействияФормыСохранить(Кнопка)
	Записать();
КонецПроцедуры

Процедура ПриОткрытии()
		
	ЗаполнениеДереваОбъекта();
	
КонецПроцедуры

Процедура ПоказатьХодВыполнения(ФормаИндикации)
	
	Если ФормаИндикации = Неопределено Тогда
		Возврат;
	КонецЕсли;	
	
	ФормаИндикации.Значение = ФормаИндикации.Значение + 1;
	
КонецПроцедуры

Процедура ЗаполнениеДереваОбъекта(ЗаполнятьТолькоПоМетаданным = Ложь)
	
	 Перем ФормаИндикации;
		
	ДеревоОбъектов.Строки.Очистить();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СтратегииРедактированияНомеровОбъектов.ТипОбъекта КАК ТипОбъекта,
	|	СтратегииРедактированияНомеровОбъектов.ВидОбъекта КАК ВидОбъекта,
	|	СтратегииРедактированияНомеровОбъектов.СтратегияРедактированияНомераОбъекта КАК СтратегияРедактированияНомераОбъекта
	|ИЗ
	|	РегистрСведений.СтратегииРедактированияНомеровОбъектов КАК СтратегииРедактированияНомеровОбъектов";
	
	ТаблицаЗапроса = Запрос.Выполнить().Выгрузить();
	ТаблицаЗапроса.Индексы.Добавить("ТипОбъекта, ВидОбъекта");
	СтруктураПоиска = Новый Структура("ТипОбъекта, ВидОбъекта");
	
	НетЗаписейВРегистре = ТаблицаЗапроса.Количество() = 0;

	
	
		
	ФормаИндикации = ПолучитьОбщуюФорму("ХодВыполненияОбработкиДанных");	
	ФормаИндикации.КомментарийОбработкиДанных = "Начальное заполнение дерева объектов метаданных";	
	ФормаИндикации.МаксимальноеЗначение = Метаданные.Справочники.Количество()
										  + Метаданные.Документы.Количество()
										  + Метаданные.ПланыВидовХарактеристик.Количество();
	ФормаИндикации.Открыть();
	
	
		
	
	// Справочники	
	Если Метаданные.Справочники.Количество() > 0 Тогда		
			
		СтрокаРодителя = ДеревоОбъектов.Строки.Добавить();
		СтрокаРодителя.Объект = "Справочники";
		СтрокаРодителя.ОбъектИмя = "Справочники";
		СтруктураПоиска.ТипОбъекта = "Справочники";
		
		ФормаИндикации.КомментарийЗначения = "Анализ форм справочников";
				
		Для Каждого ОбъектМетаданные ИЗ Метаданные.Справочники Цикл
			
			ПоказатьХодВыполнения(ФормаИндикации);
			
			Если ОбъектМетаданные.АвтоНумерация Тогда
				
				СтрокаОбъекта = СтрокаРодителя.Строки.Добавить();
				СтрокаОбъекта.Объект = ?(ПустаяСтрока(ОбъектМетаданные.Синоним), ОбъектМетаданные.Имя, ОбъектМетаданные.Синоним);
				СтрокаОбъекта.ОбъектИмя = ОбъектМетаданные.Имя;
				
				СтруктураПоиска.ВидОбъекта = ОбъектМетаданные.Имя;
				СтратегияРедактирования = ПолучитьСохраненнуюСтратегиюДляОбъекта(ТаблицаЗапроса, СтруктураПоиска);
				
				Если СтратегияРедактирования.Пустая() И НетЗаписейВРегистре ИЛИ ЗаполнятьТолькоПоМетаданным Тогда
					Если ОбъектМетаданные.СпособРедактирования = Метаданные.СвойстваОбъектов.СпособРедактирования.ВСписке Тогда
						СтрокаОбъекта.СтратегияРедактирования = ПолучитьСтратегиюПоОсновнойФормеСписка(Справочники[ОбъектМетаданные.Имя], ОбъектМетаданные);				
					Иначе 					
						СтрокаОбъекта.СтратегияРедактирования = ПолучитьСтратегиюПоОсновнойФормеОбъекта(Справочники[ОбъектМетаданные.Имя], ОбъектМетаданные);
					КонецЕсли;					
				Иначе
					СтрокаОбъекта.СтратегияРедактирования = СтратегияРедактирования;
				КонецЕсли;

			КонецЕсли;
		КонецЦикла;
		
		ЭлементыФормы.ДеревоОбъектов.Развернуть(СтрокаРодителя);
		
	КонецЕсли;
	
	// Документы	
	Если Метаданные.Документы.Количество() > 0 Тогда
		
		СтрокаРодителя = ДеревоОбъектов.Строки.Добавить();
		СтрокаРодителя.Объект = "Документы";
		СтрокаРодителя.ОбъектИмя = "Документы";
		СтруктураПоиска.ТипОбъекта = "Документы";		
		
		ФормаИндикации.КомментарийЗначения = "Анализ форм документов";
		
		Для Каждого ОбъектМетаданные ИЗ Метаданные.Документы Цикл
			
			ПоказатьХодВыполнения(ФормаИндикации);
			
			Если ОбъектМетаданные.АвтоНумерация Тогда
				
				СтрокаОбъекта = СтрокаРодителя.Строки.Добавить();
				СтрокаОбъекта.Объект = ?(ПустаяСтрока(ОбъектМетаданные.Синоним), ОбъектМетаданные.Имя, ОбъектМетаданные.Синоним);
				СтрокаОбъекта.ОбъектИмя = ОбъектМетаданные.Имя;
								
				
				СтруктураПоиска.ВидОбъекта = ОбъектМетаданные.Имя;
				СтратегияРедактирования = ПолучитьСохраненнуюСтратегиюДляОбъекта(ТаблицаЗапроса, СтруктураПоиска);
				
				Если СтратегияРедактирования.Пустая() И НетЗаписейВРегистре ИЛИ ЗаполнятьТолькоПоМетаданным Тогда
					СтрокаОбъекта.СтратегияРедактирования = ПолучитьСтратегиюПоОсновнойФормеОбъекта(Документы[ОбъектМетаданные.Имя], ОбъектМетаданные);
				Иначе
					СтрокаОбъекта.СтратегияРедактирования = СтратегияРедактирования;
				КонецЕсли;
					
			КонецЕсли;
		КонецЦикла;
		
		ЭлементыФормы.ДеревоОбъектов.Развернуть(СтрокаРодителя);
		
	КонецЕсли;
	
	// ПВХ	
	Если Метаданные.ПланыВидовХарактеристик.Количество() > 0 Тогда
		
		СтрокаРодителя = ДеревоОбъектов.Строки.Добавить();
		СтрокаРодителя.Объект = "Планы видов характеристик";
		СтрокаРодителя.ОбъектИмя = "ПланыВидовХарактеристик";
		СтруктураПоиска.ТипОбъекта = "ПланыВидовХарактеристик";
		
		ФормаИндикации.КомментарийЗначения = "Анализ форм планов видов характеристик";
		
		Для Каждого ОбъектМетаданные ИЗ Метаданные.ПланыВидовХарактеристик Цикл
			
			ПоказатьХодВыполнения(ФормаИндикации);
			
			Если ОбъектМетаданные.АвтоНумерация Тогда
				
				СтрокаОбъекта = СтрокаРодителя.Строки.Добавить();
				СтрокаОбъекта.Объект = ?(ПустаяСтрока(ОбъектМетаданные.Синоним), ОбъектМетаданные.Имя, ОбъектМетаданные.Синоним);
				СтрокаОбъекта.ОбъектИмя = ОбъектМетаданные.Имя;
				
				СтруктураПоиска.ВидОбъекта = ОбъектМетаданные.Имя;
				СтратегияРедактирования = ПолучитьСохраненнуюСтратегиюДляОбъекта(ТаблицаЗапроса, СтруктураПоиска);
				
				Если СтратегияРедактирования.Пустая() И НетЗаписейВРегистре ИЛИ ЗаполнятьТолькоПоМетаданным Тогда
					Если ОбъектМетаданные.СпособРедактирования = Метаданные.СвойстваОбъектов.СпособРедактирования.ВСписке Тогда
						СтрокаОбъекта.СтратегияРедактирования = ПолучитьСтратегиюПоОсновнойФормеСписка(ПланыВидовХарактеристик[ОбъектМетаданные.Имя], ОбъектМетаданные);				
					Иначе 					
						СтрокаОбъекта.СтратегияРедактирования = ПолучитьСтратегиюПоОсновнойФормеОбъекта(ПланыВидовХарактеристик[ОбъектМетаданные.Имя], ОбъектМетаданные);
					КонецЕсли;					
				Иначе
					СтрокаОбъекта.СтратегияРедактирования = СтратегияРедактирования;
				КонецЕсли;				
				
			КонецЕсли;
		КонецЦикла;
		
		ЭлементыФормы.ДеревоОбъектов.Развернуть(СтрокаРодителя);
		
	КонецЕсли;
	
	Если ФормаИндикации.Открыта() Тогда
		ФормаИндикации.Закрыть();
	КонецЕсли;
	
КонецПроцедуры

Процедура Записать()
		
	Набор = РегистрыСведений.СтратегииРедактированияНомеровОбъектов.СоздатьНаборЗаписей();
	Для Каждого СтрокаРодителя ИЗ ДеревоОбъектов.Строки Цикл
		Для Каждого СтрокаОбъекта ИЗ СтрокаРодителя.Строки Цикл
			Если НЕ СтрокаОбъекта.СтратегияРедактирования.Пустая() Тогда
				НоваяЗапись = Набор.Добавить();
				НоваяЗапись.ТипОбъекта = СтрокаРодителя.ОбъектИмя;
				НоваяЗапись.ВидОбъекта = СтрокаОбъекта.ОбъектИмя;
				НоваяЗапись.СтратегияРедактированияНомераОбъекта = СтрокаОбъекта.СтратегияРедактирования;				
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;	
	Набор.Записать(Истина);
	
	глЗначениеПеременнойУстановить("КэшСтратегииАвтонумерации", Новый Соответствие);
			
	Если ОбщегоНазначения.ЕстьДругиеПользователиВБазе() Тогда
		Предупреждение("Для остальных пользователей новая стратегия доступности редактирования" + Символы.ПС + "номеров (кодов) объектов вступит в силу после перезапуска их сеансов работы с программой.");
	КонецЕсли;
	
КонецПроцедуры


Процедура КоманднаяПанельДереваОбъектовЗаполнитьПоУмолчанию(Кнопка)
	Ответ = Вопрос("Для всех объектов метаданных стратегия доступности редактирования номера будет установлена" + Символы.ПС + "в значение по умолчанию. Все введенные ранее значения будут потеряны. Продолжить?", РежимДиалогаВопрос.ДаНет);
	Если Ответ = КодВозвратаДиалога.Да Тогда
		ЗаполнениеДереваОбъекта(Истина);
	КонецЕсли;
КонецПроцедуры

Процедура КоманднаяПанельДереваОбъектовОбновитьНумерациюТекущегоОбъекта(Кнопка)
	ТекущиеДанные = ЭлементыФормы.ДеревоОбъектов.ТекущиеДанные;
	ОбновитьНумерациюОбъектов(Метаданные[ТекущиеДанные.Родитель.ОбъектИмя].Найти(ТекущиеДанные.ОбъектИмя));
КонецПроцедуры

Процедура КоманднаяПанельДереваОбъектовОбновитьНумерациюВсехОбъектов(Кнопка)
	ОбновитьНумерациюОбъектов();
КонецПроцедуры


ДеревоОбъектов.Колонки.Добавить("ОбъектИмя");