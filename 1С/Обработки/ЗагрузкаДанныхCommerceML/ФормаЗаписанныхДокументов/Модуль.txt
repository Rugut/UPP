Процедура ОбновитьСостоянияДокументов()
	
	Индекс = 0;
	
	Пока Индекс < ТаблицаЗаписанныхДокументов.Количество() Цикл
		
		СтрокаТаблицы = ТаблицаЗаписанныхДокументов[Индекс];
		
		Попытка
			
			Если СтрокаТаблицы.Документ.ПолучитьОбъект() = Неопределено Тогда
				
				ТаблицаЗаписанныхДокументов.Удалить(СтрокаТаблицы);
				Продолжить;
				
			КонецЕсли;
			
		Исключение
			
			ТаблицаЗаписанныхДокументов.Удалить(СтрокаТаблицы);
			Продолжить;
			
		КонецПопытки;
		
		СтрокаТаблицы.ПометкаУдаления  = СтрокаТаблицы.Документ.ПометкаУдаления;
		СтрокаТаблицы.ДокументПроведен  = СтрокаТаблицы.Документ.Проведен;
		
		Если СтрокаТаблицы.Документ.ПометкаУдаления Тогда
			
			СтрокаТаблицы.ДокументПроведен = Ложь;
			
		ИначеЕсли СтрокаТаблицы.Документ.Проведен Тогда
			
			СтрокаТаблицы.ПометкаУдаления  = Ложь;
						
		КонецЕсли;
				
		Индекс = Индекс + 1;
	
	КонецЦикла;	
	
КонецПроцедуры

Процедура КоманднаяПанельСформированныеДокументыУстановитьПометки(Кнопка)
	
	Для каждого Строка из ТаблицаЗаписанныхДокументов Цикл
		
		Строка.Пометка = Истина;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура КоманднаяПанельСформированныеДокументыСнятьПометки(Кнопка)
	
	Для каждого Строка из ТаблицаЗаписанныхДокументов Цикл
		
		Строка.Пометка = Ложь;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура КоманднаяПанельСформированныеДокументыПровести(Кнопка)
	
	Для каждого Строка из ТаблицаЗаписанныхДокументов Цикл
		
		Если Строка.Пометка Тогда
			
			ДокументОбъект = Строка.Документ.ПолучитьОбъект();
			
			Если ДокументОбъект <> Неопределено И НЕ ДокументОбъект.ПометкаУдаления Тогда
				
				Попытка
					
					ДокументОбъект.Записать(РежимЗаписиДокумента.Проведение);
										
				Исключение
					
					ОбщегоНазначения.СообщитьОбОшибке("Возникла ошибка при проведении документа: " + Строка(ДокументОбъект)+ Символы.ПС + ОписаниеОшибки());
					
				КонецПопытки;
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
	ОбновитьСостоянияДокументов();
	
КонецПроцедуры

Процедура КоманднаяПанельСформированныеДокументыПометитьНаУдаление(Кнопка)
	
	Для каждого Строка из ТаблицаЗаписанныхДокументов Цикл
		
		Если Не Строка.Пометка Тогда
			Продолжить;
		КонецЕсли;
			
		Док = Строка.Документ.ПолучитьОбъект();
		
		Если Док <> Неопределено Тогда
			
			Попытка
			
				Док.УстановитьПометкуУдаления(НЕ Док.ПометкаУдаления);
				
			Исключение
				
				ОбщегоНазначения.СообщитьОбОшибке("Возникла ошибка при установке/снятии пометки удаления документа: " + Строка(Док)+ Символы.ПС + ОписаниеОшибки());
				
			КонецПопытки;
							
		КонецЕсли;			
				
	КонецЦикла;
	
	ОбновитьСостоянияДокументов();
	
КонецПроцедуры

Процедура КоманднаяПанельСформированныеДокументыУдалить(Кнопка)
	
	КодВозврата = Вопрос("Внимание! Выбранные документы будут удалены без возможности восстановления
	|и без проверки ссылочной целостности!", РежимДиалогаВопрос.ОКОтмена,,, "Формирование заказов поставщикам");
	
	Если КодВозврата <> КодВозвратаДиалога.ОК Тогда
		
		Возврат;
		
	КонецЕсли;
	
	Для каждого Строка из ТаблицаЗаписанныхДокументов Цикл
		
		Если Не Строка.Пометка Тогда
			Продолжить;
		КонецЕсли;
			
		Док = Строка.Документ.ПолучитьОбъект();
		
		Если Док <> Неопределено Тогда
			
			Попытка
			
				Док.Удалить();
			
			Исключение
				
				ОбщегоНазначения.СообщитьОбОшибке("Возникла ошибка при удалении документа: " + Строка(Док)+ Символы.ПС + ОписаниеОшибки());
				
			КонецПопытки;
							
		КонецЕсли;			
				
	КонецЦикла;
	
	ОбновитьСостоянияДокументов();
	
КонецПроцедуры

Процедура КоманднаяПанельСформированныеДокументыПечать(Кнопка)
	
	Для каждого Строка из ТаблицаЗаписанныхДокументов Цикл
		
		Если НЕ Строка.Пометка Тогда
			Продолжить;
		КонецЕсли;
			
		ДокументОбъект = Строка.Документ.ПолучитьОбъект();
		
		Если ДокументОбъект <> Неопределено Тогда
			
			Попытка
				
				Попытка
					СтруктураВнутреннихПечатныхФорм = ДокументОбъект.ПолучитьСтруктуруПечатныхФорм()
				Исключение
					СтруктураВнутреннихПечатныхФорм = Новый Структура;
				КонецПопытки;
				
				ДеревоМакетов = УниверсальныеМеханизмы.ПолучитьДеревоМакетовПечати(ДокументОбъект.Ссылка, СтруктураВнутреннихПечатныхФорм);
				
				СтрокаПоУмолчанию = УниверсальныеМеханизмы.ПолучитьСтрокуДереваМакетовПоУмолчанию(ДокументОбъект.Метаданные().Имя, ДеревоМакетов);
				
				Если СтрокаПоУмолчанию = Неопределено Тогда
					ОбщегоНазначения.СообщитьОбОшибке("Не найдена форма печати по умолчанию документа: " + Строка(ДокументОбъект));
					Продолжить;
				КонецЕсли;
				
				ДокументОбъект.Печать(СтрокаПоУмолчанию.Имя);
				
			Исключение
				
				ОбщегоНазначения.СообщитьОбОшибке("Возникла ошибка при печати документа: " + Строка(ДокументОбъект)+ Символы.ПС + ОписаниеОшибки());
				
			КонецПопытки;
			
		КонецЕсли;			
				                                                                         																				 
	КонецЦикла;
	
КонецПроцедуры

Процедура ОбновитьИнформациюОЗагруженныхДокументах()
	
	Если ОбъектыУспешноСохранены Тогда
		
		ЭлементыФормы.НадписьЗагруженныхДокументов.Заголовок = "Загрузка документов успешно завершена.";
		ЭлементыФормы.КартинкаЗагрузкиДокументов.Картинка = БиблиотекаКартинок.ВыполненоУспешно32;
				
	Иначе
		ЭлементыФормы.НадписьЗагруженныхДокументов.Заголовок = "Во время загрузки документов произошли ошибки.";
		ЭлементыФормы.КартинкаЗагрузкиДокументов.Картинка = БиблиотекаКартинок.ВыполненоСОшибками32;
				
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередОткрытием(Отказ, СтандартнаяОбработка)
	
	ОбновитьСостоянияДокументов();
	ОбновитьИнформациюОЗагруженныхДокументах();
		
КонецПроцедуры

Процедура ТаблицаЗаписанныхДокументовПриПолученииДанных(Элемент, ОформленияСтрок)
	
	Для каждого ОформлениеСтроки из ОформленияСтрок Цикл
		
		ОформлениеСтроки.Ячейки.Документ.ОтображатьКартинку = Истина;
		
		Если ОформлениеСтроки.ДанныеСтроки.ПометкаУдаления Тогда
			
			ОформлениеСтроки.Ячейки.Документ.ИндексКартинки = 1;
			
		ИначеЕсли ОформлениеСтроки.ДанныеСтроки.ДокументПроведен Тогда
			
			ОформлениеСтроки.Ячейки.Документ.ИндексКартинки = 0;
			
		Иначе
			
			ОформлениеСтроки.Ячейки.Документ.ИндексКартинки = 2;
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ТаблицаЗаписанныхДокументовВыбор(Элемент, ВыбраннаяСтрока, Колонка, СтандартнаяОбработка)
	
	Если Колонка.Имя = "Документ" Тогда
		
		ВыбраннаяСтрока.Документ.ПолучитьОбъект().ПолучитьФорму().Открыть();
		
	КонецЕсли;
	
КонецПроцедуры

Процедура КоманднаяПанельСформированныеДокументыОбновить(Кнопка)
	
	ОбновитьСостоянияДокументов();
	
КонецПроцедуры