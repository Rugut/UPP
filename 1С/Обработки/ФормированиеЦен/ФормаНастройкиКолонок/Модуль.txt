//////////////////////////////////////////////////////////////////////////////////
//// ПРОЦЕДУРЫ И ФУНКЦИИ ОБЩЕГО НАЗНАЧЕНИЯ

// Процедура заполняет недостающие колонки дерева ДеревоКолонок
//
Процедура ЗаполнитьДерево() Экспорт

	ДеревоКолонок.Колонки.Добавить("КолонкаИмя", Новый ОписаниеТипов("Строка"));

КонецПроцедуры // ЗаполнитьДерево()

// Процедура добавляет в дерево новую строку - значение параметра колонки.
//
// Параметры:
//  Представление - строковое представление колонки, как ее будет видеть пользователь.
//  Имя           - имя колонки.
//  Пометка       - начальная пометка колонки.
//
Процедура ДобавитьВДеревоСтроку(Представление, Имя, Пометка) Экспорт

	НоваяСтрока = ДеревоКолонок.Строки.Добавить();
	НоваяСтрока.КолонкаСтрока    = Представление;
	НоваяСтрока.КолонкаИмя       = Имя;
	НоваяСтрока.Пометка          = Пометка;

КонецПроцедуры // ДобавитьВДеревоСтроку()

// Процедура восстанавливает положения колонок в зависимости от установленных флажков.
//
Процедура ВосстановитьПоложенияКолонок()

	// Установим начальные положения колонок
	ИмяСохраненныхЗначений = ВладелецФормы.ДокументОбъект.Метаданные().Имя;
	Для Каждого СтрокаДерева Из ДеревоКолонок.Строки Цикл
		ИмяСохраненныхЗначений = ИмяСохраненныхЗначений + "_" + СтрокаДерева.Пометка;
	КонецЦикла;

	// Получили имя, под которым положения были сохранены. Теперь получим значения
	Для Каждого СтрокаДерева Из ДеревоКолонок.Строки Цикл
		ТекущееПоложениеКолонки = ВосстановитьЗначение(ИмяСохраненныхЗначений + "_" + ДеревоКолонок.Строки.Индекс(СтрокаДерева));
		ТекущееПоложениеКолонки = ?(НЕ ЗначениеЗаполнено(ТекущееПоложениеКолонки), СтрокаДерева.ПоложениеКолонки, ТекущееПоложениеКолонки);
		СтрокаДерева.ПоложениеКолонки = ТекущееПоложениеКолонки;
	КонецЦикла;

КонецПроцедуры // ВосстановитьПоложенияКолонок()

// Процедура сохраняет положения колонок в зависимости от установленных флажков
// (с учетом только что переключенного флажка).
//
// Параметры:
//  ПриПереключенииФлажка - Булево, признак того, что сохранять положения колонок надо после переключения флажка.
//
Процедура СохранитьПоложенияКолонок(ПриПереключенииФлажка)

	// Сохраним положения колонок для данного набора данных
	ИмяСохраненныхЗначений = ВладелецФормы.ДокументОбъект.Метаданные().Имя;
	Для Каждого СтрокаДерева Из ДеревоКолонок.Строки Цикл
		Если СтрокаДерева = ЭлементыФормы.ДеревоКолонок.ТекущаяСтрока И ПриПереключенииФлажка Тогда
			ИмяСохраненныхЗначений = ИмяСохраненныхЗначений + "_" + (НЕ СтрокаДерева.Пометка);
		Иначе
			ИмяСохраненныхЗначений = ИмяСохраненныхЗначений + "_" + СтрокаДерева.Пометка;
		КонецЕсли;
	КонецЦикла;
	Для Каждого СтрокаДерева Из ДеревоКолонок.Строки Цикл
		СохранитьЗначение(ИмяСохраненныхЗначений + "_" + ДеревоКолонок.Строки.Индекс(СтрокаДерева), СтрокаДерева.ПоложениеКолонки);
	КонецЦикла;

КонецПроцедуры // СохранитьПоложенияКолонок()

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ДЕЙСТВИЯ КОМАНДНЫХ ПАНЕЛЕЙ ФОРМЫ

// Процедура вызывается при нажатии кнопка "ОК" формы
//
Процедура ОсновныеДействияФормыОК(Кнопка)

	// Сохраним положения колонок
	СохранитьПоложенияКолонок(Ложь);

	СтруктураВозвращаемыхЗначений = Новый Структура();
	СтруктураВозвращаемыхЗначений.Вставить("ДеревоКолонок", ДеревоКолонок);
	СтруктураВозвращаемыхЗначений.Вставить("Команда",     "НастройкаПараметров");
	ОповеститьОВыборе(СтруктураВозвращаемыхЗначений);

КонецПроцедуры // ОсновныеДействияФормыОК()

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

// Процедура - обработчик события "ПриОткрытии" формы
//
Процедура ПриОткрытии()

	ЭлементФормы = ЭлементыФормы.ДеревоКолонок.Колонки.ПоложениеКолонки.ЭлементУправления;
	ЭлементФормы.СписокВыбора.Добавить(ПоложениеКолонки.ВТойЖеКолонке);
	ЭлементФормы.СписокВыбора.Добавить(ПоложениеКолонки.НаСледующейСтроке);
	ЭлементФормы.СписокВыбора.Добавить(ПоложениеКолонки.НоваяКолонка);

	// Установим начальные положения колонок
	ВосстановитьПоложенияКолонок();

КонецПроцедуры // ПриОткрытии()

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ РЕКВИЗИТОВ ТАБЛИЧНОГО ПОЛЯ

// Процедура - обработчик события "ПриИзмененииФлажка" табличного поля ДеревоКолонок
//
Процедура ДеревоКолонокПриИзмененииФлажка(Элемент, Колонка)

	// Установим начальные положения колонок, предварительно сохранив предыдущие
	СохранитьПоложенияКолонок(Истина);
	ВосстановитьПоложенияКолонок();

КонецПроцедуры // ДеревоКолонокПриИзмененииФлажка()