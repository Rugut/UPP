////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ МОДУЛЯ

// Будем хранить текущие значения следующих полей табличной части для пересчета
// цены при их изменении.
Перем мТекущаяЕдиница, мТекущаяВалюта, мТекущаяВалютаБазовая;

// Необходимы переменные для сохранения вызывающего документа
Перем ДокументОбъект Экспорт;
Перем ФормаДокументОбъекта Экспорт;

Перем НадоОбновитьДокумент;
Перем ЕстьБазовыйТипЦен;
Перем ЭтоНеДинамическийТипЦен;

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОБЩЕГО НАЗНАЧЕНИЯ

// Процедура пересчитывает цену для указанной строки
// по базовой цене.
//
Процедура РассчитатьЦенуПоБазовойЦене(ТекущаяСтрока)

	ВалютаТекущая = ТипЦен.ВалютаЦены;

	Если ТекущаяСтрока.СпособРасчетаЦены = Перечисления.СпособыРасчетаЦены.ПоПроцентнойНаценкеНаБазовыйТип Тогда

		// Наценим.
		ПолученнаяЦена = ТекущаяСтрока.ЦенаБазовая * (1 + ТекущаяСтрока.ПроцентСкидкиНаценки / 100);
		ВалютаТекущая = ТекущаяСтрока.ВалютаБазовая;
	ИначеЕсли ТекущаяСтрока.СпособРасчетаЦены = Перечисления.СпособыРасчетаЦены.ПоВхождениюБазовойЦеныВДиапазон Тогда

		// Рассчитаем по диапазонам.
		ПолученнаяЦена = Ценообразование.ПолучитьЦенуПоЦеновымДиапазонам(ТипЦен, ДатаЦенСкидок, ТекущаяСтрока.ЦенаБазовая, ВалютаТекущая, ТекущаяСтрока.ВалютаБазовая);

		Если ПодставлятьВалютуИзДиапазона Тогда
			ТекущаяСтрока.Валюта = ВалютаТекущая;
		КонецЕсли;
	КонецЕсли;

	ПолученнаяЦена = Ценообразование.ПересчитатьЦенуПриИзмененииВалюты(ПолученнаяЦена, ВалютаТекущая, ТекущаяСтрока.Валюта, 0, 1, ДатаЦенСкидок);
	ПолученнаяЦена = Ценообразование.ОкруглитьЦену(ПолученнаяЦена, ТипЦен.ПорядокОкругления, ТипЦен.ОкруглятьВБольшуюСторону);

	ТекущаяСтрока.Цена = ПолученнаяЦена;

КонецПроцедуры //РассчитатьЦенуПоБазовойЦене()

// Процедура пересчитывает цену при интерактивном изменении
// реквизитов ТЧ.
//
Процедура ПересчитатьЦенуПриИзмененииРеквизитов()

	ТекущаяСтрока = ЭлементыФормы.Товары.ТекущиеДанные;

	// Если задана базовая цена, то рассчитаем цену на ее основании.
	Если ЕстьБазовыйТипЦен
	   И ЭтоНеДинамическийТипЦен
	   И ПересчитыватьЦенуПриИзмененииРеквизитов Тогда
		РассчитатьЦенуПоБазовойЦене(ТекущаяСтрока);
	КонецЕсли;

КонецПроцедуры //ПересчитатьЦенуПриИзмененииРеквизитов()

// Процедуры выполняет необходимые действия при установке нового значения в поле
// выбора действия с ценами.
//
// Параметры:
//  ТекущийЭлементДействий - число, индекс устанавливаемого действия в списке действий
//
Процедура ПриИзмененииТекущегоДействия(ТекущийЭлементДействий)

	// Назначим новое действие кнопке "Выполнить". Само действие возьмем из списка значений.
	ЭлементыФормы.КнопкаВыполнить.УстановитьДействие("Нажатие", СписокДействий[ТекущийЭлементДействий].Значение);

	ДействиеНеВыполнено = Истина;
	// Будем считать, что по умолчанию никакие реквизиты не нужны.
	ЭлементыФормы.ПолеВводаВариантаЗачения.Видимость = Ложь;
	ЭлементыФормы.ПолеВыбораЗнакаИзменения.Видимость = Ложь;
	ЭлементыФормы.ПолеВводаВеличиныИзменения.Видимость = Ложь;
	ЭлементыФормы.ПолеВыбораЕдиницыИзменения.Видимость = Ложь;

	ЭлементыФормы.ПолеВводаВариантаЗачения.БыстрыйВыбор = Истина;

	// Заполним свзанные поля и реквизиты
	Если СписокДействий[ТекущийЭлементДействий].Представление = "Удалить (обнулить)" Тогда

		// В этом случае ничего дополнительного учтанавливать не нужно.

	ИначеЕсли СписокДействий[ТекущийЭлементДействий].Представление = "Установить единицу" Тогда

		// Здесь надо выбирать из справочника ОКЕИ
		ЭлементыФормы.ПолеВводаВариантаЗачения.Видимость   = Истина;
		ЭлементыФормы.ПолеВводаВариантаЗачения.Значение    = 
		                 Справочники.КлассификаторЕдиницИзмерения.ПустаяСсылка();

	ИначеЕсли СписокДействий[ТекущийЭлементДействий].Представление = "Установить валюту" Тогда

		// Здесь надо выбирать из справочника валют
		ЭлементыФормы.ПолеВводаВариантаЗачения.Видимость          = Истина;
		ЭлементыФормы.ПолеВводаВариантаЗачения.Значение           = ТипЦен.ВалютаЦены;

	ИначеЕсли СписокДействий[ТекущийЭлементДействий].Представление = "Установить наценку" Тогда

		// Здесь надо задать число
		ЭлементыФормы.ПолеВводаВариантаЗачения.Видимость = Истина;
		ЭлементыФормы.ПолеВводаВариантаЗачения.Значение = ТипЦен.ПроцентСкидкиНаценки;

	ИначеЕсли СписокДействий[ТекущийЭлементДействий].Представление = "Установить способ расчета цены" Тогда

		// Здесь надо выбирать из перечисления "Способы расчета цены".
		ЭлементыФормы.ПолеВводаВариантаЗачения.Видимость = Истина;
		ЭлементыФормы.ПолеВводаВариантаЗачения.Значение = ТипЦен.СпособРасчетаЦены;

	ИначеЕсли СписокДействий[ТекущийЭлементДействий].Представление = "Рассчитать по наценке от базовой цены" Тогда

		// В этом случае ничего дополнительного учтанавливать не нужно.

	ИначеЕсли СписокДействий[ТекущийЭлементДействий].Представление = "Рассчитать по формуле" Тогда

		// Здесь надо выбирать из справочника типов цен
		ЭлементыФормы.ПолеВводаВариантаЗачения.Видимость          = Истина;
		ЭлементыФормы.ПолеВводаВариантаЗачения.Значение           = ТипЦен;

		ЭлементыФормы.ПолеВыбораЗнакаИзменения.Видимость    = Истина;
		ЭлементыФормы.ПолеВыбораЗнакаИзменения.СписокВыбора = СписокЗнаковИзменения;
		ЗнакИзменения = СписокЗнаковИзменения[0].Значение;

		ЭлементыФормы.ПолеВводаВеличиныИзменения.Видимость = Истина;

		ЭлементыФормы.ПолеВыбораЕдиницыИзменения.Видимость    = Истина;
		ЭлементыФормы.ПолеВыбораЕдиницыИзменения.СписокВыбора = СписокЕдиницИзменения;
		ЕдиницаИзменения = СписокЕдиницИзменения[0].Значение;

	ИначеЕсли СписокДействий[ТекущийЭлементДействий].Представление = "Рассчитать по ценам контрагента" Тогда

		// Здесь надо выбирать из справочника типов цен
		ЭлементыФормы.ПолеВводаВариантаЗачения.БыстрыйВыбор       = Ложь;
		ЭлементыФормы.ПолеВводаВариантаЗачения.Видимость          = Истина;
		ЭлементыФормы.ПолеВводаВариантаЗачения.Значение           = Справочники.ТипыЦенНоменклатурыКонтрагентов.ПустаяСсылка();

		ЭлементыФормы.ПолеВыбораЗнакаИзменения.Видимость    = Истина;
		ЭлементыФормы.ПолеВыбораЗнакаИзменения.СписокВыбора = СписокЗнаковИзменения;
		ЗнакИзменения = СписокЗнаковИзменения[0].Значение;

		ЭлементыФормы.ПолеВводаВеличиныИзменения.Видимость = Истина;

		ЭлементыФормы.ПолеВыбораЕдиницыИзменения.Видимость    = Истина;
		ЭлементыФормы.ПолеВыбораЕдиницыИзменения.СписокВыбора = СписокЕдиницИзменения;
		ЕдиницаИзменения = СписокЕдиницИзменения[0].Значение;

	ИначеЕсли СписокДействий[ТекущийЭлементДействий].Представление = "Округлить" Тогда

		// Здесь надо выбирать из перечисления  порядки округления
		ЭлементыФормы.ПолеВводаВариантаЗачения.Видимость          = Истина;
		ЭлементыФормы.ПолеВводаВариантаЗачения.Значение           = ТипЦен.ПорядокОкругления;

	ИначеЕсли СписокДействий[ТекущийЭлементДействий].Представление = "Изменить цены на %" Тогда

		// Здесь надо задать число
		ЭлементыФормы.ПолеВводаВариантаЗачения.Видимость = Истина;
		ЭлементыФормы.ПолеВводаВариантаЗачения.Значение  = 0;

	КонецЕсли;

КонецПроцедуры // ПриИзмененииТекущегоДействия()

// Выполняет действия при изменении номенклатуры или характеристики номенклатуры
// в строке табличной части.
//
// Параметры:
//  ТекущаяСтрока - строка табличной части, в которой изменили номенклатуру;
//
Процедура ПриИзмененииНоменклатурыИлиХарактеристики(ТекущаяСтрока)

	// Если какие-то значения оказались не заполненными, то заполним их по умолчанию.
	Если НЕ ЗначениеЗаполнено(ТекущаяСтрока.Валюта) Тогда
		ТекущаяСтрока.Валюта = ТипЦен.ВалютаЦены;
	КонецЕсли;

	Если НЕ ЗначениеЗаполнено(ТекущаяСтрока.ЕдиницаИзмерения) Тогда
		ТекущаяСтрока.ЕдиницаИзмерения = ТекущаяСтрока.Номенклатура.ЕдиницаХраненияОстатков;
	КонецЕсли;

	Если НЕ ЗначениеЗаполнено(ТекущаяСтрока.СпособРасчетаЦены) Тогда
		ТекущаяСтрока.СпособРасчетаЦены = ТипЦен.СпособРасчетаЦены;
	КонецЕсли;

	Если НЕ ЗначениеЗаполнено(ТекущаяСтрока.ПроцентСкидкиНаценки) И ТекущаяСтрока.СпособРасчетаЦены =Перечисления.СпособыРасчетаЦены.ПоПроцентнойНаценкеНаБазовыйТип Тогда
		ТекущаяСтрока.ПроцентСкидкиНаценки = ТипЦен.ПроцентСкидкиНаценки;
	КонецЕсли;

	Если ЕстьБазовыйТипЦен И ЭтоНеДинамическийТипЦен Тогда
		ТекущаяСтрока.ЦенаБазовая = Ценообразование.ПолучитьЦенуНоменклатуры(ТекущаяСтрока.Номенклатура, ТекущаяСтрока.ХарактеристикаНоменклатуры, ТипЦен.БазовыйТипЦен, 
									ДатаЦенСкидок , ТекущаяСтрока.ЕдиницаИзмерения, ТекущаяСтрока.ВалютаБазовая);
		ПересчитатьЦенуПриИзмененииРеквизитов();
	КонецЕсли;

	// Запомним текущие значения реквизитов для пересчета при их изменении.
	мТекущаяЕдиница = ТекущаяСтрока.ЕдиницаИзмерения;
	мТекущаяВалюта  = ТекущаяСтрока.Валюта;

КонецПроцедуры // ПриИзмененииНоменклатурыИлиХарактеристики()

// Выполняет действия при изменении единицы в строке табличной части.
//
// Параметры:
//  ТекущаяСтрока - строка табличной части, в которой изменили номенклатуру;
//  ТипЦен        - ссылка на элемент справочника, определяет тип цен; 
//  СтараяЕдиница - ссылка на элемент справочника, определяет значение единицы до ее изменения; 
//
Процедура ПриИзмененииЕдиницы(ТекущаяСтрока, ТипЦен, СтараяЕдиница)

	// Пересчитаем цену, если выбрали новую единицу и была задана старая.
	ТекущаяСтрока.Цена = Ценообразование.ПересчитатьЦенуПриИзмененииЕдиницы(ТекущаяСтрока.Цена, СтараяЕдиница, 
	                                                          ТекущаяСтрока.ЕдиницаИзмерения);

	// Еще надо округлить по типу цен.
	ТекущаяСтрока.Цена = Ценообразование.ОкруглитьЦену(ТекущаяСтрока.Цена, ТипЦен.ПорядокОкругления, 
	                                 ТипЦен.ОкруглятьВБольшуюСторону);

	// Запомним старую Единицу
	СтараяЕдиница = ТекущаяСтрока.ЕдиницаИзмерения;

КонецПроцедуры // ПриИзмененииЕдиницы()

// Выполняет действия при изменении валюты в строке табличной части.
//
// Параметры:
//  ТекущаяСтрока - строка табличной части, в которой изменили номенклатуру;
//  ТипЦен        - ссылка на элемент справочника, определяет тип цен; 
//  СтараяВалюта  - ссылка на элемент справочника, определяет значение валюты до ее изменения; 
//  ДатаЦен       - дата, на которую назначаются цены
//
Процедура ПриИзмененииВалюты(ТекущаяСтрока, ТипЦен, СтараяВалюта, ДатаЦен)

	// Пересчитаем цену, если выбрали новую валюту и была задана старая.
	ТекущаяСтрока.Цена = Ценообразование.ПересчитатьЦенуПриИзмененииВалюты(ТекущаяСтрока.Цена, СтараяВалюта, ТекущаяСтрока.Валюта, ,
														 , ДатаЦен);
	// Еще надо округлить по типу цен.
	ТекущаяСтрока.Цена = Ценообразование.ОкруглитьЦену(ТекущаяСтрока.Цена, ТипЦен.ПорядокОкругления, 
	                                 ТипЦен.ОкруглятьВБольшуюСторону);

	// Запомним старую валюту
	СтараяВалюта = ТекущаяСтрока.Валюта;

КонецПроцедуры // ПриИзмененииВалюты()

// Выполняет действия при изменении способа расчета в строке табличной части.
//
// Параметры:
//  ТекущаяСтрока - строка табличной части, в которой изменили способ расчета;
//
Процедура ПриИзмененииСпособарасчета(ТекущаяСтрока)

	Если НЕ ЗначениеЗаполнено(ТекущаяСтрока.СпособРасчетаЦены) Тогда
		ТекущаяСтрока.СпособРасчетаЦены = ТипЦен.СпособРасчетаЦены;
	КонецЕсли;

	Если ТекущаяСтрока.СпособРасчетаЦены = Перечисления.СпособыРасчетаЦены.ПоВхождениюБазовойЦеныВДиапазон Тогда
		ТекущаяСтрока.ПроцентСкидкиНаценки = 0;
	ИначеЕсли ТекущаяСтрока.СпособРасчетаЦены = Перечисления.СпособыРасчетаЦены.ПоПроцентнойНаценкеНаБазовыйТип Тогда
		Если ТекущаяСтрока.ПроцентСкидкиНаценки = 0 Тогда
			ТекущаяСтрока.ПроцентСкидкиНаценки = ТипЦен.ПроцентСкидкиНаценки;
			ТекущаяСтрока.ПроцентСкидкиНаценки = Ценообразование.ПолучитьПроцентСкидкиНаценкиЦеныНоменклатуры(ТекущаяСтрока.Номенклатура,
			   ТекущаяСтрока.ХарактеристикаНоменклатуры, ТипЦен, ДатаЦенСкидок, , , Истина);
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры // ПриИзмененииСпособарасчета()

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

// Обработчик события "ПередОткрытием"формы.
//
Процедура ПередОткрытием(Отказ, СтандартнаяОбработка)

	// Если не установлено значения объекта редактируемого документа изменения цен компании,
	// то форму обработки открывать не нужно.
	Если ДокументОбъект = Неопределено Тогда
		Отказ = Истина;
		Предупреждение("Не задан документ изменения цен компании! " 
		               + "Эта обработка вызывается из формы данного документа.", 60, "Установка цен номенклатуры");
		Возврат;
	КонецЕсли;

	// Цены динамического типа не редактируются.
	//Если ТипЦен.Рассчитывается = Истина Тогда
	//	Отказ = Истина;
	//	Предупреждение("Цены динамически рассчитываемые не редактируются!", 60, "Формирование цен");
	//	Возврат;
	//КонецЕсли;

КонецПроцедуры // ПередОткрытием()

// Обработчик события "ПриОткрытии"формы.
//
Процедура ПриОткрытии()

	НадоОбновитьДокумент = Ложь;
	ЕстьБазовыйТипЦен = ЗначениеЗаполнено(ТипЦен.БазовыйТипЦен);
	ЭтоНеДинамическийТипЦен = Не ТипЦен.Рассчитывается;

	// Если нет базового типа цен, то колонку базовой цены, базовой валюты,
	// наценки и способа расчета показывать смысла нет
	ЭлементыФормы.Товары.Колонки.ЦенаБазовая.Видимость = ЕстьБазовыйТипЦен И ЭтоНеДинамическийТипЦен;
	ЭлементыФормы.Товары.Колонки.ВалютаБазовая.Видимость = ЕстьБазовыйТипЦен И ЭтоНеДинамическийТипЦен;
	ЭлементыФормы.Товары.Колонки.ПроцентСкидкиНаценки.Видимость = ЕстьБазовыйТипЦен;
	ЭлементыФормы.Товары.Колонки.СпособРасчетаЦены.Видимость = ЕстьБазовыйТипЦен;
	ЭлементыФормы.Товары.Колонки.Валюта.Видимость = ЭтоНеДинамическийТипЦен;
	ЭлементыФормы.Товары.Колонки.ЕдиницаИзмерения.Видимость = ЭтоНеДинамическийТипЦен;
	ЭлементыФормы.Товары.Колонки.Цена.Видимость = ЭтоНеДинамическийТипЦен;

	// Восстановим настройку пересчета цены при изменении валюты, единицы, процента наценки.
	ПересчитыватьЦенуПриИзмененииРеквизитов = ВосстановитьЗначение("ФормированиеЦенПересчитыватьЦенуПриИзмененииРеквизитов");
	ПодставлятьВалютуИзДиапазона = ВосстановитьЗначение("ФормированиеЦенПодставлятьВалютуИзДиапазона");

	// Заполним список действий с табличной частью.
	СписокДействий.Очистить();

	Если ЭтоНеДинамическийТипЦен Тогда
		СписокДействий.Добавить(Новый Действие("КнопкаВыполнитьНажатиеОбнулитьЦены"), "Удалить (обнулить)");
		СписокДействий.Добавить(Новый Действие("КнопкаВыполнитьНажатиеУстановитьЕдиницуЦен"), "Установить единицу");
		СписокДействий.Добавить(Новый Действие("КнопкаВыполнитьНажатиеУстановитьВалютуЦен"), "Установить валюту");
	КонецЕсли;

	Если ЕстьБазовыйТипЦен Тогда
		СписокДействий.Добавить(Новый Действие("КнопкаВыполнитьНажатиеУстановитьНаценкуЦен"), "Установить наценку");
		СписокДействий.Добавить(Новый Действие("КнопкаВыполнитьНажатиеУстановитьСпособРасчета"), "Установить способ расчета цены");
		Если ЭтоНеДинамическийТипЦен Тогда
			СписокДействий.Добавить(Новый Действие("КнопкаВыполнитьНажатиеИзменитьЦенуПоСпособуРасчета"), "Рассчитать цены по базовой цене");
		КонецЕсли;
	КонецЕсли;

	Если ЭтоНеДинамическийТипЦен Тогда
		СписокДействий.Добавить(Новый Действие("КнопкаВыполнитьНажатиеРассчитатьЦеныПоФормуле"), "Рассчитать по формуле");
		СписокДействий.Добавить(Новый Действие("КнопкаВыполнитьНажатиеРассчитатьЦеныКонтрагентаПоФормуле"), "Рассчитать по ценам контрагента");
		СписокДействий.Добавить(Новый Действие("КнопкаВыполнитьНажатиеОкруглитьЦены"), "Округлить");
		СписокДействий.Добавить(Новый Действие("КнопкаВыполнитьНажатиеИзменитьЦену"), "Изменить цены на %");
	КонецЕсли;

	// Заполним список единиц измерения
	СписокЕдиницИзменения.Очистить();
	СписокЕдиницИзменения.Добавить("%"            , "%");
	СписокЕдиницИзменения.Добавить("Единиц валюты", "Единиц валюты");

	// Заполним список знаков измерения
	СписокЗнаковИзменения.Очистить();
	СписокЗнаковИзменения.Добавить(1,  "+");
	СписокЗнаковИзменения.Добавить(-1, "-");

	// Заполним список действий для выбора из выпадающего списка
	ЭлементыФормы.ПолеВыбораДействия.СписокВыбора = СписокДействий;

	// Попробуем вспомнить последнее выбранное значение из списка действий
	ТекущееДействие = ВосстановитьЗначение("ФормированиеЦенТекущееДействие");
	
	Если ТипЗнч(ТекущееДействие) <> Тип("Действие") 
	 ИЛИ СписокДействий.НайтиПоЗначению(ТекущееДействие) = Неопределено Тогда
		
		// Нет сохраненного значения
		ТекущееДействие = СписокДействий[0].Значение;
	КонецЕсли;
	ЭлементыФормы.ПолеВыбораДействия.Значение = ТекущееДействие;

	// Установим элементы формы в зависимости от выбранного действия
	ПриИзмененииТекущегоДействия(СписокДействий.Индекс(СписокДействий.НайтиПоЗначению(ТекущееДействие)));

	// Установим пометку
	Для каждого СтрокаТабличнойЧасти Из Товары Цикл
		СтрокаТабличнойЧасти.Пометка = Истина;
	КонецЦикла;
	
	ДействиеНеВыполнено = Истина;

КонецПроцедуры // ПриОткрытии()

// Обработчик события "ОбработкаВыбора"формы.
//
Процедура ОбработкаВыбора(ЗначениеВыбора, Источник)

	ВыборкаЗначенийВыбора = ЗначениеВыбора.Выбрать();
	Пока ВыборкаЗначенийВыбора.Следующий() цикл
		СтрокаТабличнойЧасти                          = Товары.Добавить();
		СтрокаТабличнойЧасти.Пометка                  = Истина;
		СтрокаТабличнойЧасти.Номенклатура             = ВыборкаЗначенийВыбора.Номенклатура;
		СтрокаТабличнойЧасти.Цена                     = ВыборкаЗначенийВыбора.Цена;
		СтрокаТабличнойЧасти.ПроцентСкидкиНаценки     = ВыборкаЗначенийВыбора.ПроцентСкидкиНаценки;
		СтрокаТабличнойЧасти.Валюта                   = ВыборкаЗначенийВыбора.Валюта;
		СтрокаТабличнойЧасти.ЕдиницаИзмерения         = ВыборкаЗначенийВыбора.ЕдиницаИзмерения;
	КонецЦикла;

КонецПроцедуры // ОбработкаВыбора()

// Процедура - обработчик события "ПередЗакрытием" формы.
// Если действие не было выполнено задает вопрос пользователю.
//
Процедура ПередЗакрытием(Отказ, СтандартнаяОбработка)

	Если ОбработкаОбъект.ПолучитьФорму().Открыта() Тогда

		Если (Не ДействиеНеВыполнено Или Модифицированность) И Не НадоОбновитьДокумент Тогда
			Ответ = Вопрос("Перенести изменения в документ?", РежимДиалогаВопрос.ДаНет);

			Если Ответ = КодВозвратаДиалога.Да Тогда
				НадоОбновитьДокумент = Истина;
			КонецЕсли;
		КонецЕсли;
		
		Если НадоОбновитьДокумент Тогда
			Если ФормаДокументОбъекта <> Неопределено Тогда
				НомерСоответствияТекущейКолонки = ФормаДокументОбъекта.мСоответствиеТиповЦен.Получить(ТипЦен);
			КонецЕсли;

			// Действи выполняется, если еще не закрыли документ
			Если ДокументОбъект <> Неопределено
			   И НомерСоответствияТекущейКолонки <> Неопределено
			   И ФормаДокументОбъекта.ЭлементыФормы.ТаблицаЦен.Колонки.Найти("цена"+НомерСоответствияТекущейКолонки) <> Неопределено
			   И ФормаДокументОбъекта.ЭлементыФормы.ТаблицаЦен.Колонки.Найти("валюта"+НомерСоответствияТекущейКолонки) <> Неопределено
			   И ФормаДокументОбъекта.ЭлементыФормы.ТаблицаЦен.Колонки.Найти("единица"+НомерСоответствияТекущейКолонки) <> Неопределено
			   И ФормаДокументОбъекта.ЭлементыФормы.ТаблицаЦен.Колонки.Найти("процент"+НомерСоответствияТекущейКолонки) <> Неопределено
			   И ФормаДокументОбъекта.ЭлементыФормы.ТаблицаЦен.Колонки.Найти("СпособРасчета"+НомерСоответствияТекущейКолонки) <> Неопределено Тогда

				// Перенесем в ТЧ документа все строки из ТЧ данной формы.
				Для Каждого СтрокаТабличнойЧасти Из Товары Цикл

					СтруктураОтбора = Новый Структура();
					СтруктураОтбора.Вставить("ХарактеристикаНоменклатуры", СтрокаТабличнойЧасти.ХарактеристикаНоменклатуры);
					СтруктураОтбора.Вставить("Номенклатура",               СтрокаТабличнойЧасти.Номенклатура);

					СтрокаТаблицыЦен = ОбработкаТабличныхЧастей.НайтиСтрокуТабЧасти(ФормаДокументОбъекта.ТаблицаЦен, СтруктураОтбора);

					Если СтрокаТаблицыЦен = Неопределено Тогда
						СтрокаТаблицыЦен = ФормаДокументОбъекта.ТаблицаЦен.Добавить();
						СтрокаТаблицыЦен.Номенклатура               = СтрокаТабличнойЧасти.Номенклатура;
						СтрокаТаблицыЦен.ХарактеристикаНоменклатуры = СтрокаТабличнойЧасти.ХарактеристикаНоменклатуры;
						ФормаДокументОбъекта.ЭлементыФормы.ТаблицаЦен.ТекущаяСтрока = СтрокаТаблицыЦен;
						СтрокаТаблицыЦенФормы = ФормаДокументОбъекта.ЭлементыФормы.ТаблицаЦен.ТекущаяСтрока;
						ФормаДокументОбъекта.ПриИзмененииНоменклатуры(СтрокаТаблицыЦенФормы);
					КонецЕСли;

					СтрокаТаблицыЦен["цена"+НомерСоответствияТекущейКолонки] = СтрокаТабличнойЧасти.Цена;
					СтрокаТаблицыЦен["валюта"+НомерСоответствияТекущейКолонки] = СтрокаТабличнойЧасти.Валюта;
					СтрокаТаблицыЦен["единица"+НомерСоответствияТекущейКолонки] = СтрокаТабличнойЧасти.ЕдиницаИзмерения;
					СтрокаТаблицыЦен["процент"+НомерСоответствияТекущейКолонки] = СтрокаТабличнойЧасти.ПроцентСкидкиНаценки;
					СтрокаТаблицыЦен["СпособРасчета"+НомерСоответствияТекущейКолонки] = СтрокаТабличнойЧасти.СпособРасчетаЦены;

				КонецЦикла;
				
				ДокументОбъект.Товары.Загрузить(Товары.Выгрузить());
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры //ПередЗакрытием()

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ДЕЙСТВИЯ КОМАНДНЫХ ПАНЕЛЕЙ ФОРМЫ

// Процедура вызывается при нажатии кнопки "УстановитьФлажки" командной панели
// табличного поля "Товары", устанавливает флажки во всех строках 
// табличной части "Товары".
//
Процедура КоманднаяПанельТоварыДействиеУстановитьФлажки(Кнопка)

	Для каждого СтрокаТабличнойЧасти Из Товары Цикл
		СтрокаТабличнойЧасти.Пометка = Истина;
	КонецЦикла; 

КонецПроцедуры

// Процедура вызывается при нажатии кнопки "СнатьФлажки" командной панели
// табличного поля "Товары", снимает флажки во всех строках 
// табличной части "Товары".
//
Процедура КоманднаяПанельТоварыДействиеСнятьФлажки(Кнопка)

	Для каждого СтрокаТабличнойЧасти Из Товары Цикл
		СтрокаТабличнойЧасти.Пометка = Ложь;
	КонецЦикла;

КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ РЕКВИЗИТОВ ШАПКИ

// Процедура - обработчик события "Нажатие" кнопки "Выполнить".
// Действия по нажатию кнопки назначаются при выборе 
// текущего действия в процедуре ПриИзмененииТекущегоДействия().
// Данный обработчик обнуляет все помеченные цены.
//
Процедура КнопкаВыполнитьНажатиеОбнулитьЦены(Элемент)

	Для каждого СтрокаТабличнойЧасти Из Товары Цикл
		Если СтрокаТабличнойЧасти.Пометка Тогда
			СтрокаТабличнойЧасти.ПроцентСкидкиНаценки = 0;
			СтрокаТабличнойЧасти.Цена                 = 0;
			СтрокаТабличнойЧасти.Валюта               = Неопределено;
			СтрокаТабличнойЧасти.ЕдиницаИзмерения     = Неопределено;
		КонецЕсли;
	КонецЦикла;
	ДействиеНеВыполнено = Ложь;

КонецПроцедуры // КнопкаВыполнитьНажатиеОбнулитьЦены()

// Процедура - обработчик события "Нажатие" кнопки "Выполнить".
// Действия по нажатию кнопки назначаются при выборе 
// текущего действия в процедуре ПриИзмененииТекущегоДействия().
// Данный обработчик устанавливает единицу с выбранным ОКЕИ для 
// всех помеченных строк табличной части.
//
Процедура КнопкаВыполнитьНажатиеУстановитьЕдиницуЦен(Элемент)

	// Должен быть выбран ОКЕИ
	Если НЕ ЗначениеЗаполнено(ВариантЗначения) Тогда
		Предупреждение("Не выбрана единица измерения!", 60, "Формирование цен");

		ТекущийЭлемент = ЭлементыФормы.ПолеВводаВариантаЗачения;
		Возврат;
	КонецЕсли; 

	//Сформируем текст запроса для поиска единицы с выбранным ОКЕИ
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Владелец", Товары.ВыгрузитьКолонку("Номенклатура"));
	Запрос.УстановитьПараметр("ОКЕИ",     ВариантЗначения);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ЕдиницыИзмерения.Владелец,
	|	ЕдиницыИзмерения.Ссылка      КАК ЕдиницаИзмерения,
	|	ЕдиницыИзмерения.Коэффициент
	|ИЗ
	|	Справочник.ЕдиницыИзмерения КАК ЕдиницыИзмерения
	|
	|ГДЕ
	|	  ЕдиницыИзмерения.Владелец В (&Владелец)
	|	И ЕдиницыИзмерения.ЕдиницаПоКлассификатору = &ОКЕИ";

	ВыборкаИзЗапроса = Запрос.Выполнить().Выбрать();

	// Найдем у данной номенклатуры единицу с таким ОКЕИ
	// Если нашли, то меняем единицу и пересчитваем цену,
	// не нашли - сообщаем об этом.
	Для каждого СтрокаТабличнойЧасти Из Товары Цикл
		Если СтрокаТабличнойЧасти.Пометка Тогда

			// Сбросим выборку для послеудующего поиска
			ВыборкаИзЗапроса.Сбросить();

			// Ищем в выборке номенклатуру
			Если ВыборкаИзЗапроса.НайтиСледующий(СтрокаТабличнойЧасти.Номенклатура,"Владелец")  Тогда

				// У данной номенклатуры есть цена с таким ОКЕИ
				// Изменяем единицу, пересчитываем цену, если просят.
				Если ПересчитыватьЦенуПриИзмененииРеквизитов Тогда
					
					НоваяЦена = ?(СтрокаТабличнойЧасти.ЕдиницаИзмерения.Коэффициент = 0, 0,
					              СтрокаТабличнойЧасти.Цена * ВыборкаИзЗапроса.Коэффициент 
					              / СтрокаТабличнойЧасти.ЕдиницаИзмерения.Коэффициент);
					СтрокаТабличнойЧасти.Цена    = Ценообразование.ОкруглитьЦену(НоваяЦена, ТипЦен.ПорядокОкругления,
					                                      ТипЦен.ОкруглятьВБольшуюСторону);
				КонецЕсли; 

				СтрокаТабличнойЧасти.ЕдиницаИзмерения = ВыборкаИзЗапроса.ЕдиницаИзмерения;
				
			Иначе
				Сообщить("У номенклатуры """ + СокрЛП(СтрокаТабличнойЧасти.Номенклатура)  
				        + """ нет единицы измерения с ОКЕИ """ + СокрЛП(ВариантЗначения)
				        + """!", СтатусСообщения.Важное);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;

	ДействиеНеВыполнено = Ложь;

КонецПроцедуры // КнопкаВыполнитьНажатиеУстановитьЕдиницуЦен()

// Процедура - обработчик события "Нажатие" кнопки "Выполнить".
// Действия по нажатию кнопки назначаются при выборе 
// текущего действия в процедуре ПриИзмененииТекущегоДействия().
// Данный обработчик устанавливает выбранную валюту для 
// всех помеченных строк табличной части.
//
Процедура КнопкаВыполнитьНажатиеУстановитьВалютуЦен(Элемент)

	// Должна быть выбрана новая валюта цены
	Если НЕ ЗначениеЗаполнено(ВариантЗначения) Тогда
		
		Предупреждение("Не выбрана новая валюта цены!", 60, "Формирование цен");
		ТекущийЭлемент = ЭлементыФормы.ПолеВводаВариантаЗачения;
		Возврат;
		
	КонецЕсли; 

	Для каждого СтрокаТабличнойЧасти Из Товары Цикл
		Если СтрокаТабличнойЧасти.Пометка Тогда

			Если ПересчитыватьЦенуПриИзмененииРеквизитов Тогда
				НоваяЦена = Ценообразование.ПересчитатьЦенуПриИзмененииВалюты(СтрокаТабличнойЧасти.Цена, СтрокаТабличнойЧасти.Валюта,
				                                                ВариантЗначения, , , ДатаЦенСкидок);
				СтрокаТабличнойЧасти.Цена   = Ценообразование.ОкруглитьЦену(НоваяЦена, ТипЦен.ПорядокОкругления,
					                                    ТипЦен.ОкруглятьВБольшуюСторону);
			КонецЕсли;

			СтрокаТабличнойЧасти.Валюта = ВариантЗначения;
			
		КонецЕсли;
	КонецЦикла;

	ДействиеНеВыполнено = Ложь;

КонецПроцедуры // КнопкаВыполнитьНажатиеУстановитьВалютуЦен()

// Процедура - обработчик события "Нажатие" кнопки "Выполнить".
// Действия по нажатию кнопки назначаются при выборе 
// текущего действия в процедуре ПриИзмененииТекущегоДействия().
// Данный обработчик устанавливает заданную наценку для 
// всех помеченных строк табличной части.
//
Процедура КнопкаВыполнитьНажатиеУстановитьСпособРасчета(Элемент)

	Если НЕ ЗначениеЗаполнено(ВариантЗначения) Тогда
		Предупреждение("Необходимо выбрать вариант расчета цены!");
		Возврат;
	КонецЕсли;

	Для каждого СтрокаТабличнойЧасти Из Товары Цикл
		Если СтрокаТабличнойЧасти.Пометка Тогда
			СтрокаТабличнойЧасти.СпособРасчетаЦены = ВариантЗначения;

			ПриИзмененииСпособарасчета(СтрокаТабличнойЧасти);
		КонецЕсли;
	КонецЦикла;

	ДействиеНеВыполнено = Ложь;

КонецПроцедуры // КнопкаВыполнитьНажатиеУстановитьСпособРасчета()

// Процедура - обработчик события "Нажатие" кнопки "Выполнить".
// Действия по нажатию кнопки назначаются при выборе 
// текущего действия в процедуре ПриИзмененииТекущегоДействия().
// Данный обработчик устанавливает заданную наценку для 
// всех помеченных строк табличной части.
//
Процедура КнопкаВыполнитьНажатиеУстановитьНаценкуЦен(Элемент)

	Для каждого СтрокаТабличнойЧасти Из Товары Цикл
		Если СтрокаТабличнойЧасти.Пометка Тогда
			Если СтрокаТабличнойЧасти.СпособРасчетаЦены = Перечисления.СпособыРасчетаЦены.ПоПроцентнойНаценкеНаБазовыйТип Тогда
				СтрокаТабличнойЧасти.ПроцентСкидкиНаценки = ВариантЗначения;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;

	ДействиеНеВыполнено = Ложь;

КонецПроцедуры // КнопкаВыполнитьНажатиеУстановитьНаценкуЦен(Элемент)

// Процедура - обработчик события "Нажатие" кнопки "Выполнить".
// Действия по нажатию кнопки назначаются при выборе 
// текущего действия в процедуре ПриИзмененииТекущегоДействия().
// Данный обработчик рассчитывает цену в зависимости от установленного
// для этой номенклатуры способа расчета: <По процентной наценке>
// или <По диапазонам базовой цены>.
//
Процедура КнопкаВыполнитьНажатиеИзменитьЦенуПоСпособуРасчета(Элемент)

	Для Каждого СтрокаТабличнойЧасти Из Товары Цикл
		Если СтрокаТабличнойЧасти.Пометка Тогда
			РассчитатьЦенуПоБазовойЦене(СтрокаТабличнойЧасти);
		КонецЕсли;
	КонецЦикла;

	ДействиеНеВыполнено = Ложь;

КонецПроцедуры // КнопкаВыполнитьНажатиеИзменитьЦенуПоСпособуРасчета()

// Процедура - обработчик события "Нажатие" кнопки "Выполнить".
// Действия по нажатию кнопки назначаются при выборе 
// текущего действия в процедуре ПриИзмененииТекущегоДействия().
// Данный обработчик рассчитывает цену по заданной формуле 
// для всех помеченных строк табличной части.
//
Процедура КнопкаВыполнитьНажатиеРассчитатьЦеныПоФормуле(Элемент)

	// Должен быть выбран базовый тип цен
	Если НЕ ЗначениеЗаполнено(ВариантЗначения) Тогда
		
		Предупреждение("Не выбран базовый тип цен!", 60, "Формирование цен");
		ТекущийЭлемент = ЭлементыФормы.ПолеВводаВариантаЗачения;
		Возврат;

	КонецЕсли;

	// Должен быть выбран знак
	Если НЕ ЗначениеЗаполнено(ЭлементыФормы.ПолеВыбораЗнакаИзменения.Значение) Тогда
		
		Предупреждение("Не выбран знак изменения!", 60, "Формирование цен");
		ТекущийЭлемент = ЭлементыФормы.ПолеВыбораЗнакаИзменения;
		Возврат;

	КонецЕсли;

	// Должен быть выбрана единица изменения
	Если НЕ ЗначениеЗаполнено(ЭлементыФормы.ПолеВыбораЕдиницыИзменения.Значение) Тогда

		Предупреждение("Не выбрана единица изменения!", 60, "Формирование цен");
		ТекущийЭлемент = ЭлементыФормы.ПолеВыбораЕдиницыИзменения;
		Возврат;

	КонецЕсли;

	// Надо получить цены выбранного типа и пересчитать их по заданной формуле.
	// Цены базового типа достанем запросом.

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Номенклатура"  , Товары.ВыгрузитьКолонку("Номенклатура"));
	Запрос.УстановитьПараметр("ТипЦен",         ВариантЗначения);
	Запрос.УстановитьПараметр("ДатаЦен" ,       ДатаЦенСкидок);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ЦеныНоменклатурыСрезПоследних.Цена,
	|	ЦеныНоменклатурыСрезПоследних.Валюта,
	|	ЦеныНоменклатурыСрезПоследних.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	ЦеныНоменклатурыСрезПоследних.Номенклатура,
	|	ЦеныНоменклатурыСрезПоследних.ХарактеристикаНоменклатуры
	|ИЗ
	|	РегистрСведений.ЦеныНоменклатуры.СрезПоследних(&ДатаЦен, Номенклатура В(&Номенклатура) И ТипЦен =&ТипЦен) 
	|	             КАК ЦеныНоменклатурыСрезПоследних";

	ВыборкаИзЗапроса = Запрос.Выполнить().Выбрать();

	// Найдем у данной номенклатуры значение базовой цены
	Для каждого СтрокаТабличнойЧасти Из Товары Цикл
		Если СтрокаТабличнойЧасти.Пометка Тогда

			// Сбросим выборку для последующего поиска.
			ВыборкаИзЗапроса.Сбросить();

			СтруктураПоиска = Новый Структура();
			СтруктураПоиска.Вставить("ХарактеристикаНоменклатуры", СтрокаТабличнойЧасти.ХарактеристикаНоменклатуры);
			СтруктураПоиска.Вставить("Номенклатура",               СтрокаТабличнойЧасти.Номенклатура);

			// Ищем в выборке номенклатуру.
			Если ВыборкаИзЗапроса.НайтиСледующий(СтруктураПоиска) Тогда

				// Нашли базовую цену.
				// Пересчитываем ее в валюту заданного типа цен,
				// единицы цены.
				ЦенаБазовая = Ценообразование.ПересчитатьЦенуПриИзмененииВалюты(ВыборкаИзЗапроса.Цена, ВыборкаИзЗапроса.Валюта,
				                                                  СтрокаТабличнойЧасти.Валюта, , , ДатаЦенСкидок);

				ЦенаБазовая = Ценообразование.ПересчитатьЦенуПриИзмененииЕдиницы(ЦенаБазовая, ВыборкаИзЗапроса.ЕдиницаИзмерения,
				                                                  СтрокаТабличнойЧасти.ЕдиницаИзмерения);
			Иначе
				ЦенаБазовая = 0;
			КонецЕсли;

			// Теперь применим формулу
			Если СокрЛП(ЕдиницаИзменения) = "%" Тогда
				СтрокаТабличнойЧасти.Цена = ЦенаБазовая * (100 + ЗнакИзменения * ВеличинаИзменения) / 100;
			Иначе // надо просто прибавить
				СтрокаТабличнойЧасти.Цена = ЦенаБазовая + ЗнакИзменения * ВеличинаИзменения;
			КонецЕсли;

			// Не забудем округлить.
			СтрокаТабличнойЧасти.Цена = Ценообразование.ОкруглитьЦену(СтрокаТабличнойЧасти.Цена, 
			                          ТипЦен.ПорядокОкругления, ТипЦен.ОкруглятьВБольшуюСторону);
		КонецЕсли;
	КонецЦикла;

	ДействиеНеВыполнено = Ложь;

КонецПроцедуры // КнопкаВыполнитьНажатиеРассчитатьЦеныПоФормуле()

// Процедура - обработчик события "Нажатие" кнопки "Выполнить".
// Действия по нажатию кнопки назначаются при выборе 
// текущего действия в процедуре ПриИзмененииТекущегоДействия().
// Данный обработчик рассчитывает цену по заданной формуле 
// для всех помеченных строк табличной части.
//
Процедура КнопкаВыполнитьНажатиеРассчитатьЦеныКонтрагентаПоФормуле(Элемент)

	// Должен быть выбран базовый тип цен
	Если НЕ ЗначениеЗаполнено(ВариантЗначения) Тогда
		Предупреждение("Не выбран базовый тип цен!", 60, "Формирование цен");
		ТекущийЭлемент = ЭлементыФормы.ПолеВводаВариантаЗачения;
		Возврат;
	КонецЕсли;

	// Должен быть выбран знак
	Если НЕ ЗначениеЗаполнено(ЭлементыФормы.ПолеВыбораЗнакаИзменения.Значение) Тогда
		Предупреждение("Не выбран знак изменения!", 60, "Формирование цен");
		ТекущийЭлемент = ЭлементыФормы.ПолеВыбораЗнакаИзменения;
		Возврат;
	КонецЕсли;

	// Должен быть выбрана единица изменения
	Если НЕ ЗначениеЗаполнено(ЭлементыФормы.ПолеВыбораЕдиницыИзменения.Значение) Тогда

		Предупреждение("Не выбрана единица изменения!", 60, "Формирование цен");
		ТекущийЭлемент = ЭлементыФормы.ПолеВыбораЕдиницыИзменения;
		Возврат;

	КонецЕсли;

	// Надо получить цены выбранного типа и пересчитать их по заданной формуле.
	// Цены базового типа достанем запросом.
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Номенклатура"  , Товары.ВыгрузитьКолонку("Номенклатура"));
	Запрос.УстановитьПараметр("ТипЦен",         ВариантЗначения);
	Запрос.УстановитьПараметр("ДатаЦен" ,       ДатаЦенСкидок);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ЦеныНоменклатурыКонтрагентовСрезПоследних.Цена,
	|	ЦеныНоменклатурыКонтрагентовСрезПоследних.Валюта,
	|	ЦеныНоменклатурыКонтрагентовСрезПоследних.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	ЦеныНоменклатурыКонтрагентовСрезПоследних.Номенклатура,
	|	ЦеныНоменклатурыКонтрагентовСрезПоследних.ХарактеристикаНоменклатуры
	|ИЗ
	|	РегистрСведений.ЦеныНоменклатурыКонтрагентов.СрезПоследних(&ДатаЦен, Номенклатура В(&Номенклатура) И ТипЦен =&ТипЦен) 
	|	             КАК ЦеныНоменклатурыКонтрагентовСрезПоследних";

	ВыборкаИзЗапроса = Запрос.Выполнить().Выбрать();

	// Найдем у данной номенклатуры значение базовой цены
	Для каждого СтрокаТабличнойЧасти Из Товары Цикл
		Если СтрокаТабличнойЧасти.Пометка Тогда

			// Сбросим выборку для последующего поиска.
			ВыборкаИзЗапроса.Сбросить();

			СтруктураПоиска = Новый Структура();
			СтруктураПоиска.Вставить("ХарактеристикаНоменклатуры", СтрокаТабличнойЧасти.ХарактеристикаНоменклатуры);
			СтруктураПоиска.Вставить("Номенклатура",               СтрокаТабличнойЧасти.Номенклатура);

			// Ищем в выборке номенклатуру.
			Если ВыборкаИзЗапроса.НайтиСледующий(СтруктураПоиска) Тогда

				// Нашли базовую цену.
				// Пересчитываем ее в валюту заданного типа цен,
				// единицы цены.
				ЦенаБазовая = Ценообразование.ПересчитатьЦенуПриИзмененииВалюты(ВыборкаИзЗапроса.Цена, ВыборкаИзЗапроса.Валюта,
				                                                  СтрокаТабличнойЧасти.Валюта, , , ДатаЦенСкидок);

				// выставляем основую единицу
				СтрокаТабличнойЧасти.ЕдиницаИзмерения = ВыборкаИзЗапроса.ЕдиницаИзмерения;

			Иначе

				СтрокаТабличнойЧасти.ЕдиницаИзмерения = Неопределено;
				ЦенаБазовая = 0;

			КонецЕсли;

			// Теперь применим формулу
			Если СокрЛП(ЕдиницаИзменения) = "%" Тогда

				СтрокаТабличнойЧасти.Цена = ЦенаБазовая * (100 + ЗнакИзменения * ВеличинаИзменения) / 100;

			Иначе // надо просто прибавить

				СтрокаТабличнойЧасти.Цена = ЦенаБазовая + ЗнакИзменения * ВеличинаИзменения;

			КонецЕсли;

			// Не забудем округлить.
			СтрокаТабличнойЧасти.Цена = Ценообразование.ОкруглитьЦену(СтрокаТабличнойЧасти.Цена, 
			                          ТипЦен.ПорядокОкругления, ТипЦен.ОкруглятьВБольшуюСторону);
			
		КонецЕсли;
	КонецЦикла;

	ДействиеНеВыполнено = Ложь;

КонецПроцедуры // КнопкаВыполнитьНажатиеРассчитатьЦеныКонтрагентаПоФормуле()

// Процедура - обработчик события "Нажатие" кнопки "Выполнить".
// Действия по нажатию кнопки назначаются при выборе 
// текущего действия в процедуре ПриИзмененииТекущегоДействия().
// Данный обработчик обнуляет все помеченные цены.
//
Процедура КнопкаВыполнитьНажатиеОкруглитьЦены(Элемент)

	// Должен быть выбран прядок округления
	Если НЕ ЗначениеЗаполнено(ВариантЗначения) Тогда

		Предупреждение("Не выбран порядок округления!", 60, "Формирование цен");
		ТекущийЭлемент = ЭлементыФормы.ПолеВводаВариантаЗачения;
		Возврат;

	КонецЕсли;

	Для каждого СтрокаТабличнойЧасти Из Товары Цикл
		Если СтрокаТабличнойЧасти.Пометка Тогда

			// Округлим.
			СтрокаТабличнойЧасти.Цена = Ценообразование.ОкруглитьЦену(СтрокаТабличнойЧасти.Цена, 
			                          ВариантЗначения, ТипЦен.ОкруглятьВБольшуюСторону);
		КонецЕсли;
	КонецЦикла;

	ДействиеНеВыполнено = Ложь;

КонецПроцедуры // КнопкаВыполнитьНажатиеОкруглитьЦены()

// Процедура - обработчик события "Нажатие" кнопки "Выполнить".
// Действия по нажатию кнопки назначаются при выборе 
// текущего действия в процедуре ПриИзмененииТекущегоДействия().
// Данный обработчик изменяет цену на заданный процент.
//
Процедура КнопкаВыполнитьНажатиеИзменитьЦену(Элемент)

	Для каждого СтрокаТабличнойЧасти Из Товары Цикл
		Если СтрокаТабличнойЧасти.Пометка Тогда

			СтрокаТабличнойЧасти.Цена = СтрокаТабличнойЧасти.Цена * (100 + ВариантЗначения) / 100;

		КонецЕсли;
	КонецЦикла;

	ДействиеНеВыполнено = Ложь;

КонецПроцедуры // КнопкаВыполнитьНажатиеИзменитьЦену()

// Процедура - обработчик события "ПриИзменении" поля выбора текущего действия.
//
Процедура ПолеВыбораДействияПриИзменении(Элемент)

	// Установим элементы формы в зависимости от выбранного действия
	ПриИзмененииТекущегоДействия(СписокДействий.Индекс(СписокДействий.НайтиПоЗначению(Элемент.Значение)));

КонецПроцедуры

// Процедура - обработчик события "Нажатие" кнопки "ОК".
//
Процедура КнопкаОКНажатие(Элемент)

	Если ДействиеНеВыполнено Тогда
		Ответ = Вопрос("Выбранное действие не было выполнено! Продолжить?", РежимДиалогаВопрос.ДаНет);
		Если Ответ = КодВозвратаДиалога.Нет Тогда
			Возврат;
		Иначе
			ДействиеНеВыполнено = Ложь;
		КонецЕсли;
	КонецЕсли;

	// Сохраним значения настройки пересчета цены при изменении валюты, единицы, процента наценки,
	// и текущего выбранного значения в списке действий.
	СохранитьЗначение("ФормированиеЦенПересчитыватьЦенуПриИзмененииРеквизитов", ПересчитыватьЦенуПриИзмененииРеквизитов);
	СохранитьЗначение("ФормированиеЦенПодставлятьВалютуИзДиапазона", ПодставлятьВалютуИзДиапазона);
	
	СохранитьЗначение("ФормированиеЦенТекущееДействие", ЭлементыФормы.ПолеВыбораДействия.Значение);
	
	НадоОбновитьДокумент = Истина;
	// Можно закрывать форму.
	Закрыть();

КонецПроцедуры // КнопкаОКНажатие()

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ТЧ Товары

// Процедура - обработчик события "ПриНачалеРедактирования" табличной части
//
Процедура ТоварыПриНачалеРедактирования(Элемент, НоваяСтрока)

	ТекущаяСтрока = ЭлементыФормы.Товары.ТекущаяСтрока;
	// Новые строки будем по умолчанию помечать.
	Если НоваяСтрока Тогда
		ТекущаяСтрока.Пометка = Истина;
	КонецЕсли; 

	// Запомним текущие значения реквизитов для пересчета при их изменении.
	мТекущаяЕдиница = ТекущаяСтрока.ЕдиницаИзмерения;
	мТекущаяВалюта = ТекущаяСтрока.Валюта;
	мТекущаяВалютаБазовая = ТекущаяСтрока.ВалютаБазовая;

КонецПроцедуры // ТоварыПриНачалеРедактирования()

// Процедура - обработчик события "ПриИзменении" поля ввода номенклатуры
// в строке табличной части "Товары".
//
Процедура ТоварыНоменклатураПриИзменении(Элемент)

	ТекущаяСтрока = ЭлементыФормы.Товары.ТекущиеДанные;

	// Общие действия при изменении номенклатуры.
	ПриИзмененииНоменклатурыИлиХарактеристики(ТекущаяСтрока);

КонецПроцедуры // ТоварыНоменклатураПриИзменении()

// Процедура - обработчик события "ПриИзменении" поля ввода номенклатуры
// в строке табличной части "Товары".
//
Процедура ТоварыХарактеристикаНоменклатурыПриИзменении(Элемент)

	ТекущаяСтрока = ЭлементыФормы.Товары.ТекущиеДанные;

	// Общие действия при изменении характеристики номенклатуры.
	ПриИзмененииНоменклатурыИлиХарактеристики(ТекущаяСтрока);

КонецПроцедуры // ТоварыНоменклатураПриИзменении()

// Процедура - обработчик события "ПриИзменении" поля ввода процента скидки-наценки
// в строке табличной части "Товары".
//
Процедура ТоварыПроцентСкидкиНаценкиПриИзменении(Элемент)

	ПересчитатьЦенуПриИзмененииРеквизитов();

КонецПроцедуры //ТоварыПроцентСкидкиНаценкиПриИзменении()

// Процедура - обработчик события "ПриИзменении" поля ввода валюты цены
// в строке табличной части "Товары".
//
Процедура ТоварыВалютаПриИзменении(Элемент)

	ТекущаяСтрока = ЭлементыФормы.Товары.ТекущиеДанные;

	Если ПересчитыватьЦенуПриИзмененииРеквизитов Тогда
		ПриИзмененииВалюты(ТекущаяСтрока, ТипЦен, мТекущаяВалюта, ДатаЦенСкидок);
	КонецЕсли;

КонецПроцедуры // ТоварыВалютаПриИзменении()

// Процедура - обработчик события "ПриИзменении" поля ввода единицы измерения
// в строке табличной части "Товары".
//
Процедура ТоварыЕдиницаПриИзменении(Элемент)

	ТекущаяСтрока = ЭлементыФормы.Товары.ТекущиеДанные;

	Если ПересчитыватьЦенуПриИзмененииРеквизитов Тогда
		Если НЕ ЗначениеЗаполнено(ТекущаяСтрока.ЕдиницаИзмерения) Тогда
			ТекущаяСтрока.Цена = 0;
		Иначе
			ПриИзмененииЕдиницы(ТекущаяСтрока, ТипЦен, мТекущаяЕдиница);
			Если ЕстьБазовыйТипЦен Тогда
				ТекущаяСтрока.ЦенаБазовая = Ценообразование.ПолучитьЦенуНоменклатуры(ТекущаяСтрока.Номенклатура, ТекущаяСтрока.ХарактеристикаНоменклатуры,
							ТипЦен.БазовыйТипЦен, ДатаЦенСкидок, ТекущаяСтрока.ЕдиницаИзмерения, ТекущаяСтрока.ВалютаБазовая);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры // ТоварыВалютаПриИзменении()

// Процедура - обработчик события "ПриИзменении" поля ввода "ВалютаБазовая"
// в строке табличной части "Товары".
//
Процедура ТоварыВалютаБазоваяПриИзменении(Элемент)

	ТекущаяСтрока = ЭлементыФормы.Товары.ТекущиеДанные;

	// Пересчитаем цену, если выбрали новую валюту и была задана старая.
	ТекущаяСтрока.ЦенаБазовая = Ценообразование.ПересчитатьЦенуПриИзмененииВалюты(ТекущаяСтрока.ЦенаБазовая, мТекущаяВалютаБазовая, ТекущаяСтрока.ВалютаБазовая, , , ДатаЦенСкидок);

	// Запомним старую валюту
	мТекущаяВалютаБазовая = ТекущаяСтрока.ВалютаБазовая;

КонецПроцедуры //ТоварыВалютаБазоваяПриИзменении()

// Процедура - обработчик события "ПриИзменении" поля ввода "ЦенаБазовая"
// в строке табличной части "Товары".
//
Процедура ТоварыЦенаБазоваяПриИзменении(Элемент)

	ПересчитатьЦенуПриИзмененииРеквизитов();

КонецПроцедуры //ТоварыЦенаБазоваяПриИзменении()

// Процедура - обработчик события "ПриИзменении" поля ввода "СпособРасчетаЦены"
// в строке табличной части "Товары".
//
Процедура ТоварыСпособРасчетаЦеныПриИзменении(Элемент)

	ТекущаяСтрока = ЭлементыФормы.Товары.ТекущиеДанные;

	ПриИзмененииСпособарасчета(ТекущаяСтрока);

	ПересчитатьЦенуПриИзмененииРеквизитов();

КонецПроцедуры //ТоварыСпособРасчетаЦеныПриИзменении()

// Обработчик события "ПриАктивизацииЯчейки" ТЧ.
//
Процедура ТоварыПриАктивизацииЯчейки(Элемент)

	Если Элемент.ТекущаяКолонка <> Неопределено
	   И Элемент.ТекущиеДанные <> Неопределено
	   И Элемент.ТекущаяКолонка.Имя = "ПроцентСкидкиНаценки" Тогда
		ТекущаяСтрока = Элемент.ТекущиеДанные;

		Если ТекущаяСтрока.СпособРасчетаЦены = Перечисления.СпособыРасчетаЦены.ПоВхождениюБазовойЦеныВДиапазон Тогда
			Элемент.ТекущаяКолонка.ТолькоПросмотр = Истина;
		Иначе
			Элемент.ТекущаяКолонка.ТолькоПросмотр = Ложь;
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры //ТоварыПриАктивизацииЯчейки()