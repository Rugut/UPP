// ВывестиСекцию
//
Процедура ВывестиСекцию(Таб, ИмяОбласти, Наименование, ПоказательНаименование = Неопределено, ПоказательСумма = Неопределено, Расшифровка = Неопределено)

	Область = ПолучитьМакет("Макет").ПолучитьОбласть(ИмяОбласти);

	Если ИмяОбласти = "Заголовок" Тогда
		Область.Области.НаименованиеЗаголовок.Значение = Наименование;
	ИначеЕсли ИмяОбласти = "Показатель" Тогда
		//Область.Параметры.Заполнить(Новый Структура("ПоказательНаименование, ПоказательСумма", ПоказательНаименование, ПоказательСумма));

		Область.Области.ПоказательНаименование.Значение = ПоказательНаименование;
		Если ТипЗнч(ПоказательСумма) = Тип("Число") Тогда
			// Если есть дробная часть, анализируем, чтобы после зпт. не было более 2 чисел,
			// иначе переводим значение в строку и отображаем как строка. ЕНВД.
			Если ПоказательСумма - Цел(ПоказательСумма) <> 0 Тогда
				// Для случае, когда необходимо в расшифровке отобразить показатели коэффициенты
				// со значениями после запятой. Например в декларации по ЕНВД.
				СтрокаПоказателя = Формат(ПоказательСумма - Цел(ПоказательСумма));
				// Если 0.123 - т.е. длина 5, тогда конвертируем в строку, иначе оставим как есть.
				Если СтрДлина(СтрокаПоказателя) > 4 Тогда
					// Переведем значение в строку и её будем отображать в расшифровке.
					ПоказательСумма = Формат(ПоказательСумма);
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;

		Область.Области.ПоказательСумма.Значение = ПоказательСумма;

	КонецЕсли;

	Если Расшифровка <> Неопределено Тогда
		Если ИмяОбласти = "Заголовок" Тогда
			Область.Область(1,1,Область.ВысотаТаблицы,Область.ШиринаТаблицы).Расшифровка = Расшифровка;
		Иначе
			Область.Область(1,1,Область.ВысотаТаблицы,Область.ШиринаТаблицы).Расшифровка = Расшифровка;
		КонецЕсли;
	КонецЕсли;

	Таб.Вывести(Область);

КонецПроцедуры // ВывестиСекцию

// Процедура формирует и выводит в поле табличного документа на форме расшифровку значения показателя,
// соответствующего текущей ячейке активного поля табличного документа вызывающей формы (задана в переменной модуля "Форма").
// В случае, если для показателя, соответсвующего текущей активной ячейки расшифровка неопределена, то расшифровка 
// выводится для всех показателей, для которых расшифровка определена.
// Процедура вызывается из модуля формы отчета.
//
// Параметры:
//	Нет.
//
Процедура Показать(Расшифровка, СписокРасшифровкиПоказателей, СписокРасшифровкиформулы) Экспорт

	Показатели = СписокРасшифровкиПоказателей.НайтиСтроки(Новый Структура("Номер", Расшифровка));
	Формула    = СписокРасшифровкиформулы.НайтиСтроки(Новый Структура("Номер", Расшифровка));

	Если Показатели.Количество() = 0 Тогда
		Сообщить("Расшифровка для данного соотношения отсутствует.", СтатусСообщения.Информация);
		Возврат;
	КонецЕсли;

	Таб = ЭлементыФормы.ПолеРасшифровки;
	Таб.Очистить();

	ВывестиСекцию(Таб, "Заголовок", Формула[0].ТекстовоеПредставлениеФормулы,,,"ЗаголовокАктивизироватьСоотношения");

	Для Каждого Стр Из Показатели Цикл
		СтруктураРасш = Новый Структура;
		СтруктураРасш.Вставить("ИмяПоказателя", Стр.ИмяПоказателя);
		СтруктураРасш.Вставить("СсылкаНаОтчет", Стр.СсылкаНаДокументРО);
		СтруктураРасш.Вставить("ИмяСтраницы", Стр.ИмяСтраницы);
		СтруктураРасш.Вставить("Страница", Стр.Страница);
		СтруктураРасш.Вставить("ДополнительныеВозможности", Стр.ДополнительныеВозможности);

		ВывестиСекцию(Таб, "Показатель", "", Стр.ПредставлениеПоказателя, Стр.ЗначениеПоказателя, СтруктураРасш);
	КонецЦикла;

	Открыть();

КонецПроцедуры // Показать

// ПолеРасшифровкиОбработкаРасшифровки
// Процедура - обработчик события "Обработка расшифровки" поля табличного документа "ПолеРасшифровки" формы
// Открывает стандартный отчет, соответствующий активной ячейки поля табличного документа
//
Процедура ПолеРасшифровкиОбработкаРасшифровки(Элемент, Расшифровка, СтандартнаяОбработка)

	СтандартнаяОбработка = Ложь;
	Если ТипЗнч(Расшифровка) = Тип("Строка") Тогда
		ПерейтиВРодительскийОтчет();
	Иначе

		Если Расшифровка.ДополнительныеВозможности <> Неопределено Тогда
			Если Найти(Расшифровка.ДополнительныеВозможности, "ВывестиТекстОтчетНеОткрывать:") > 0 Тогда
				Сообщить(Сред(Расшифровка.ДополнительныеВозможности, 30), СтатусСообщения.Информация);
				Возврат;
			КонецЕсли;
		КонецЕсли;

		АктивизироватьЯчейкуОтчета(Расшифровка)
	КонецЕсли;

КонецПроцедуры // ПолеРасшифровкиОбработкаРасшифровки

// Процедура - обработчик события "Нажатие" кнопки "Сохранить" нижней командной панели формы
// Сохраняет табличный документ в виде файла MXL, в который выводятся расшифровки выбранных
// пользователем показателей
//
Процедура ОсновныеДействияФормыКнопкаСохранить(Кнопка)
	
	Диалог = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Сохранение);
	Диалог.МножественныйВыбор = Ложь;
	Диалог.ПроверятьСуществованиеФайла = Истина;
	Диалог.Расширение = "mxl";
	Диалог.Фильтр = "Табличные документы (*.mxl)|*.mxl";
	Если НЕ Диалог.Выбрать() Тогда
		Возврат;
	КонецЕсли;

	Табл = ЭлементыФормы.ПолеРасшифровки;

	Табл.ОтображатьЗаголовки = Ложь;
	Табл.ОтображатьСетку = Ложь;

	Попытка
		Табл.Записать(Диалог.ПолноеИмяФайла, ТипФайлаТабличногоДокумента.MXL);
	Исключение
		Сообщить("Не удалось записать файл на диск. Возможно, диск защищен от записи или недостаточно места на диске", СтатусСообщения.Внимание);
	КонецПопытки;

КонецПроцедуры // ОсновныеДействияФормыКнопкаСохранить

// ПерейтиВРодительскийОтчет
//
Процедура ПерейтиВРодительскийОтчет()
	Если ЭтаФорма.НачальноеЗначениеВыбора <> Неопределено И ЭтаФорма.НачальноеЗначениеВыбора.Открыта() Тогда
		ЭтаФорма.НачальноеЗначениеВыбора.Активизировать();
	КонецЕсли;
КонецПроцедуры // ПерейтиВРодительскийОтчет

// Процедура - обработчик события "Нажатие" кнопки "КнопкаАктивизироватьРодительскийОтчет" нижней командной панели формы
// Активизирует форму родительского отчета, если та еще не закрыта
//
Процедура ОсновныеДействияФормыКнопкаАктивизироватьРодительскийОтчет(Кнопка)

	ПерейтиВРодительскийОтчет();

КонецПроцедуры

// Процедура формирует необходимый набор параметров и вызывает экспортную
// процедуру формы регламентированного отчета, соответствующего переданной в качестве
// параметра строке ВыбраннаяСтрока. Вызываемая экспортная процедура активизирует
// описываемую ячейку одного из табличных документов, расположенных на форме.
//
// Параметры:
//	ВыбраннаяСтрока - строка табличного поля ТаблицаСообщений формы, ячейку, 
//						соответствующую которой, следует активизировать.
//
Процедура АктивизироватьЯчейкуОтчета(Расшифровка)

	ТекДок = Расшифровка.СсылкаНаОтчет;

	Если ТекДок = Неопределено Тогда
		Возврат;
	КонецЕсли;

	РасшифровкаИмяПоказателя = Расшифровка.ИмяПоказателя;
	СтруктураДопПараметровКСпецПоказателям = ОпределитьСпециальныеПараметрыПоказателя(РасшифровкаИмяПоказателя);

	Если СтруктураДопПараметровКСпецПоказателям = Неопределено Тогда
		ИмяПоказателя = РасшифровкаИмяПоказателя;
	Иначе
		ИмяПоказателя = СтруктураДопПараметровКСпецПоказателям.КоординатаНаЛисте;
	КонецЕсли;

	ТекДок.ПолучитьФорму().Открыть();
	Раздел = Расшифровка.ИмяСтраницы;
	Ячейка = Новый Структура;
	Ячейка.Вставить("Раздел", Раздел);
	Ячейка.Вставить("Страница", Расшифровка.Страница); // номер листа в многостраничном разделе, если не многостр. разд., то "".
	Ячейка.Вставить("Строка", "");
	Ячейка.Вставить("Графа", "");
	Ячейка.Вставить("СтрокаПП", "");
	Ячейка.Вставить("ИмяЯчейки", ИмяПоказателя);
	Ячейка.Вставить("Описание", "Не задано значение");

	Попытка
		РегламентированнаяОтчетность.ФормаРеглОтчета(ТекДок.ИсточникОтчета, ТекДок.ВыбраннаяФорма, , ТекДок.Ссылка).АктивизироватьЯчейку(Ячейка);
	Исключение
	КонецПопытки;

КонецПроцедуры // АктивизироватьЯчейкуОтчета