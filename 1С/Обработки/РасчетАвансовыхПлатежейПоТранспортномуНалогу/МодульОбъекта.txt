Перем мКодыЛьгот Экспорт;
Перем мВидыТранспортныхСредств Экспорт;
Перем мКодыОКЕИ Экспорт;
Перем мКоэфКолонка9 Экспорт;

// загружает данные стандартные данные из Макет.Списки
//
Процедура ЗагрузитьПоказателиИзМакета (РазделВМакете) Экспорт
	
	МакетСоставаПоказателей = ПолучитьМакет("Списки2011Кв1");

	ТекОбласть = МакетСоставаПоказателей.Области.Найти(РазделВМакете);

	Если ТекОбласть <> Неопределено Тогда	
	
		Для Ном = ТекОбласть.Верх По ТекОбласть.Низ Цикл
			// Перебираем строки макета.
			КодПоказателя = СокрП(МакетСоставаПоказателей.Область(Ном, 1).Текст);
			Название = СокрП(МакетСоставаПоказателей.Область(Ном, 2).Текст);
			Если КодПоказателя = "###" Тогда
				Прервать;
			Иначе

				Если РазделВМакете = "КодыЛьгот" Тогда
					НовСтрока = мКодыЛьгот.Добавить();
				ИначеЕсли РазделВМакете = "ВидыТранспортныхСредств" Тогда
					НовСтрока = мВидыТранспортныхСредств.Добавить();
				ИначеЕсли РазделВМакете = "КодыОКЕИ" Тогда
					НовСтрока = мКодыОКЕИ.Добавить();
				ИначеЕсли РазделВМакете = "КоэфКолонка9" Тогда
					НовСтрока = мКоэфКолонка9.Добавить();
				КонецЕсли;

				НовСтрока.Код= КодПоказателя;
				НовСтрока.Название = Название;

			КонецЕсли;

		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры // ЗагрузитьПоказателиИзМакета

Процедура СформироватьОтчет() Экспорт
	
	ДокументРезультат = Новый ТабличныйДокумент;
	
	Макет      = ПолучитьМакет("СправкаРасчетТН");
	МакетОбщий = ПолучитьОбщийМакет("СправкаРасчет");
	
	ЗаголовокОтчета         = МакетОбщий.ПолучитьОбласть("Заголовок");
	ОбластьШапка            = Макет.ПолучитьОбласть("Шапка");
	ОбластьСтрока           = Макет.ПолучитьОбласть("Строка");
	ОбластьИтого            = Макет.ПолучитьОбласть("Итого");
	ОбластьОкончаниеТаблицы = Макет.ПолучитьОбласть("ОкончаниеТаблицы");
	ОбластьПодвал           = МакетОбщий.ПолучитьОбласть("Подвал");
	
	ЗаголовокОтчета.Параметры.Период = "" + ПредставлениеПериода(НачалоКвартала(Период), КонецКвартала(Период), "ФП = Истина");
	СведенияОбОрганизации = УправлениеКонтактнойИнформацией.СведенияОЮрФизЛице(Организация, Период);
#Если Клиент Тогда
	НазваниеОрганизации = ФормированиеПечатныхФорм.ОписаниеОрганизации(СведенияОбОрганизации, "ПолноеНаименование");
	ЗаголовокОтчета.Параметры.НазваниеОрганизации = НазваниеОрганизации;
#КонецЕсли
	ЗаголовокОтчета.Параметры.ДатаСоставления     = КонецКвартала(Период);
	ЗаголовокОтчета.Параметры.НазваниеОтчета      = "Авансовый платеж по транспортному налогу";
	ДокументРезультат.Вывести(ЗаголовокОтчета,1);
	
	ВысотаЗаголовка = ДокументРезультат.ВысотаТаблицы;
	ДокументРезультат.Вывести(ОбластьШапка,1);
	
	ТаблицаДляПечати = ТранспортныеСредства.Выгрузить();
	ТаблицаДляПечати.Сортировать("КодОКАТО, НомерСтроки");
	ТекущийОКАТО = ?(ТаблицаДляПечати.Количество() > 0, ТаблицаДляПечати[0].КодОКАТО, "");
	НС = 1;
	Для Каждого Строка Из ТаблицаДляПечати Цикл
		Если ТекущийОКАТО <> Строка.КодОКАТО Тогда
			СтрокаПоОКАТО = АвансовыеПлатежи.Найти(ТекущийОКАТО, "КодОКАТО");
			ОбластьИтого.Параметры.КодОКАТО = ТекущийОКАТО;
			ОбластьИтого.Параметры.СуммаНалогаПоОКАТО = ?(ЗначениеЗаполнено(СтрокаПоОКАТО), СтрокаПоОКАТО.Сумма, 0);
			ДокументРезультат.Вывести(ОбластьИтого,1);
			ТекущийОКАТО = Строка.КодОКАТО;
		КонецЕсли;
		ОбластьСтрока.Параметры.Заполнить(Строка);
		ОбластьСтрока.Параметры.НомерСтроки = НС;
		ДокументРезультат.Вывести(ОбластьСтрока,1);
		НС = НС + 1;
	КонецЦикла;
	
	СтрокаПоОКАТО = АвансовыеПлатежи.Найти(ТекущийОКАТО, "КодОКАТО");
	ОбластьИтого.Параметры.КодОКАТО = ТекущийОКАТО;
	ОбластьИтого.Параметры.СуммаНалогаПоОКАТО = ?(ЗначениеЗаполнено(СтрокаПоОКАТО), СтрокаПоОКАТО.Сумма, 0);
	ДокументРезультат.Вывести(ОбластьИтого,1);
	
	ДокументРезультат.Вывести(ОбластьОкончаниеТаблицы,1);
			
	ДокументРезультат.Вывести(ОбластьПодвал,1);
	
	// Зафиксируем заголовок отчета
	ДокументРезультат.ФиксацияСверху=9;
	
	ДокументРезультат.Показать();
	
	// Присвоим имя для сохранения параметров печати табличного документа
	ДокументРезультат.ИмяПараметровПечати = "Пересчет стоимости отложенных налоговых активов и обязательств";
	
	ДокументРезультат.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;
	ДокументРезультат.АвтоМасштаб = Истина;
	
	УправлениеОтчетами.УстановитьКолонтитулыПоУмолчанию(ДокументРезультат, ЗаголовокОтчета, Строка(глЗначениеПеременной("глТекущийПользователь")));
	
КонецПроцедуры

мКодыЛьгот = Новый ТаблицаЗначений;
мКодыЛьгот.Колонки.Добавить("Код",,,6);
мКодыЛьгот.Колонки.Добавить("Название",,,70);

мВидыТранспортныхСредств = Новый ТаблицаЗначений;
мВидыТранспортныхСредств.Колонки.Добавить("Код",,,6);
мВидыТранспортныхСредств.Колонки.Добавить("Название",,,70);

мКодыОКЕИ = Новый ТаблицаЗначений;
мКодыОКЕИ.Колонки.Добавить("Код",,,5);
мКодыОКЕИ.Колонки.Добавить("Название",,,70);

мКоэфКолонка9 = Новый ТаблицаЗначений;
мКоэфКолонка9.Колонки.Добавить("Код",,,5);
мКоэфКолонка9.Колонки.Добавить("Название",,,70);