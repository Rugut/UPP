Процедура ДействияФормыСписокСчетовФактур(Кнопка)

	Форма = Документы.СчетФактураВыданный.ПолучитьФорму("ФормаСписка",,);
	Форма.ДокументСписок.Отбор.ВидСчетаФактуры.Установить(Перечисления.ВидСчетаФактурыВыставленного.НалоговыйАгент);
	Форма.ДокументСписок.Отбор.Организация.Установить(Организация);
	
	Если НачалоПериода = '00010101' И КонецПериода = '00010101' Тогда
		Форма.ДокументСписок.Отбор.Дата.Использование = Ложь;
	Иначе
		Если НачалоПериода = '00010101' Тогда
			Форма.ДокументСписок.Отбор.Дата.ВидСравнения = ВидСравнения.МеньшеИлиРавно;
			Форма.ДокументСписок.Отбор.Дата.Значение = КонецДня(КонецПериода);
		ИначеЕсли КонецПериода = '00010101' Тогда
			Форма.ДокументСписок.Отбор.Дата.ВидСравнения = ВидСравнения.БольшеИлиРавно;
			Форма.ДокументСписок.Отбор.Дата.Значение = НачалоПериода;
		Иначе
			Форма.ДокументСписок.Отбор.Дата.ВидСравнения = ВидСравнения.ИнтервалВключаяГраницы;
			Форма.ДокументСписок.Отбор.Дата.ЗначениеС = НачалоПериода;
			Форма.ДокументСписок.Отбор.Дата.ЗначениеПо= КонецДня(КонецПериода);
		КонецЕсли; 
		Форма.ДокументСписок.Отбор.Дата.Использование = Истина;
	КонецЕсли; 
	
	Форма.Открыть();
	
КонецПроцедуры

Процедура ОбновитьПодвал()
	
	ЭлементыФормы.Сумма.Значение = ОбщегоНазначения.ФорматСумм(Список.Итог("Сумма"));
	ЭлементыФормы.НДС.Значение = ОбщегоНазначения.ФорматСумм(Список.Итог("СуммаНДС"));
	
КонецПроцедуры

Процедура ПроверкаЗаполненияТабличнойЧасти(Отказ)
	
	СтруктураОбязательныхПолей = Новый структура("Контрагент, ДоговорКонтрагента, ДокументОснование, Сумма, СтавкаНДС, Дата");
	
	КолонкиСписка = Список.ВыгрузитьКолонки();
	
	// Цикл по строкам табличной части.
	Для каждого СтрокаТаблицы Из Список Цикл
		
		СтрокаНачалаСообщенияОбОшибке = "В строке номер "+ СокрЛП(СтрокаТаблицы.НомерСтроки) + " : ";
		
		// Цикл по проверяемым полям
		Для каждого КлючЗначение Из СтруктураОбязательныхПолей Цикл
			
			Значение = СтрокаТаблицы[КлючЗначение.Ключ];
			Если Не ЗначениеЗаполнено(Значение) Тогда
				
				ПредставлениеРеквизита = КолонкиСписка.Колонки[КлючЗначение.Ключ].Заголовок;
				СтрокаСообщения = "Не заполнено значение реквизита """ + СокрЛП(ПредставлениеРеквизита) + """!";
				
				ОбщегоНазначения.СообщитьОбОшибке(СтрокаНачалаСообщенияОбОшибке + СтрокаСообщения, Отказ);
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ОбновлениеОтображения()
	
	ОбновитьПодвал();
	
КонецПроцедуры

Процедура ПриОткрытии()
	
	Если Не ЗначениеЗаполнено(Организация) Тогда
		Организация = УправлениеПользователями.ПолучитьЗначениеПоУмолчанию(глЗначениеПеременной("глТекущийПользователь"), "ОсновнаяОрганизация");
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(НачалоПериода) Тогда 
		НачалоПериода = НачалоКвартала(ТекущаяДата());
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(КонецПериода) Тогда 
		КонецПериода = КонецКвартала(ТекущаяДата());
	КонецЕсли;
	
КонецПроцедуры

Процедура КнопкаВыполнитьНажатие(Кнопка)
	
	Отказ = Ложь;
	ПроверкаЗаполненияТабличнойЧасти(Отказ);
	
	Если Отказ Тогда
		
		Предупреждение("Некорректно заполнен список данных для формирования счетов-фактур. Обработка остановлена.");
		Для Каждого СтрокаТаблицы Из Список Цикл
			СтрокаТаблицы.СчетФактураСформирован = Ложь;
		КонецЦикла;
		Возврат;
		
	КонецЕсли;
	
	ЕстьОшибки = Ложь;
	СформироватьСчетаФактуры(ЕстьОшибки);
										
КонецПроцедуры

Процедура КоманднаяПанельЗаполнить(Кнопка)
	
	Отказ = Ложь;
	СообщениеОбОшибке = "Регистрация не выполнена.";
	АктивизироватьЭлемент = неопределено;
	
	Если НЕ ЗначениеЗаполнено(Организация) Тогда
		СообщениеОбОшибке = СообщениеОбОшибке+Символы.ПС+" - не указана организация";
		Отказ = Истина;
		Если АктивизироватьЭлемент = неопределено Тогда
			АктивизироватьЭлемент = ЭлементыФормы.Организация;
		КонецЕсли; 
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(НачалоПериода) Тогда
		СообщениеОбОшибке = СообщениеОбОшибке+Символы.ПС+" - не указана дата начала заполнения";
		Отказ = Истина;
		Если АктивизироватьЭлемент = неопределено Тогда
			АктивизироватьЭлемент = ЭлементыФормы.НачалоПериода;
		КонецЕсли; 
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(КонецПериода) Тогда
		СообщениеОбОшибке = СообщениеОбОшибке+Символы.ПС+" - не указана дата окончания заполнения";
		Отказ = Истина;
		Если АктивизироватьЭлемент = неопределено Тогда
			АктивизироватьЭлемент = ЭлементыФормы.КонецПериода;
		КонецЕсли; 
	КонецЕсли;
	
	Если НачалоПериода > КонецПериода Тогда
		СообщениеОбОшибке = СообщениеОбОшибке+Символы.ПС+" - дата начала заполнения больше даты окончния";
		Отказ = Истина;
		Если АктивизироватьЭлемент = неопределено Тогда
			АктивизироватьЭлемент = ЭлементыФормы.НачалоПериода;
		КонецЕсли; 
	КонецЕсли;
	
	Если Отказ Тогда
		Предупреждение(СообщениеОбОшибке);
		ТекущийЭлемент = АктивизироватьЭлемент; 
		Возврат;
	КонецЕсли; 
	
	Если Список.Количество() > 0 Тогда
		
		ТекстВопроса = "Перед заполнением табличная часть будет очищена. Заполнить?";
		Ответ        = Вопрос(ТекстВопроса, РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Да, Метаданные().Представление());
		
		Если Ответ = КодВозвратаДиалога.Нет Тогда
			Возврат;
		КонецЕсли; 
		
	КонецЕсли;
	
	ЗаполнитьДокумент();
	
КонецПроцедуры

Процедура КнопкаВыбораПериодаНажатие(Элемент)
	
	РаботаСДиалогами.ОбработчикНастройкаПериодаНажатие(НачалоПериода, КонецПериода);
	
	Если КонецПериода < НачалоПериода Тогда
		КонецПериода = НачалоПериода;
	КонецЕсли;
	
КонецПроцедуры

Процедура ОрганизацияНачалоВыбора(Элемент, СтандартнаяОбработка)
	
	Если Список.Количество() > 0 Тогда
		Если Вопрос("При смене организации необходимо очистить табличную часть. Очистить?", РежимДиалогаВопрос.ДаНет) = КодВозвратаДиалога.Да Тогда
			Список.Очистить();
		Иначе
			СтандартнаяОбработка = Ложь;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура СписокСуммаПриИзменении(Элемент)
	
	ТекущиеДанные = ЭлементыФормы.Список.ТекущиеДанные;
	Если ЗначениеЗаполнено(ТекущиеДанные.СтавкаНДС) Тогда
		ТекущиеДанные.СуммаНДС = УчетНДС.РассчитатьСуммуНДС(ТекущиеДанные.Сумма, Истина, Истина, УчетНДС.ПолучитьСтавкуНДС(ТекущиеДанные.СтавкаНДС));
	КонецЕсли;
	
КонецПроцедуры

Процедура СписокСтавкаНДСПриИзменении(Элемент)
	
	ТекущиеДанные = ЭлементыФормы.Список.ТекущиеДанные;
	Если ЗначениеЗаполнено(ТекущиеДанные.СтавкаНДС) Тогда
		ТекущиеДанные.СуммаНДС = УчетНДС.РассчитатьСуммуНДС(ТекущиеДанные.Сумма, Истина, Истина, УчетНДС.ПолучитьСтавкуНДС(ТекущиеДанные.СтавкаНДС));
	КонецЕсли;
	
КонецПроцедуры