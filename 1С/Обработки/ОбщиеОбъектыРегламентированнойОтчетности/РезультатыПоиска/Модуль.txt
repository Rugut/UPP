////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

// Процедура открывает форму отчета и активизирует ячейку в табличном документе, 
// соответствующую текущей строке таблицы результатов поиска.
// 
Процедура АктивизироватьТекущийРезультатПоиска()
	
	Попытка
		ВладелецФормы.АктивизироватьЯчейку(ЭлементыФормы.РезультатыПоиска.ТекущаяСтрока);
	Исключение
		РегламентированнаяОтчетность.роАктивизироватьЯчейку(ВладелецФормы, ЭлементыФормы.РезультатыПоиска.ТекущаяСтрока);
	КонецПопытки;
	ВладелецФормы.Активизировать();
	
КонецПроцедуры

//////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ДИАЛОГА

// Процедура - обработчик события ПередОткрытием формы.
//
Процедура ПередОткрытием(Отказ, СтандартнаяОбработка)
	
	Для Каждого Стр Из РезультатыПоиска Цикл
		Если ТипЗнч(Стр.НайденоВСтроке) = Тип("Дата") Тогда
			Стр.НайденоВСтроке = Формат(Стр.НайденоВСтроке, "ДФ=dd.MM.yyyy");
		КонецЕсли;
		Стр.НайденоВСтроке = СтрЗаменить(Стр.НайденоВСтроке, Символы.ПС, " | ");
	КонецЦикла;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ДЕЙСТВИЯ КОМАНДНЫХ ПАНЕЛЕЙ ФОРМЫ

// Процедура - действие кнопки Назад командной панели КоманднаяПанельРезультатыПоиска.
//
Процедура КоманднаяПанельРезультатыПоискаНазад(Кнопка)
	
	Если ЭлементыФормы.РезультатыПоиска.ТекущаяСтрока = Неопределено Тогда
		Возврат;
	КонецЕсли;
	ИндексТекущейСтроки = РезультатыПоиска.Индекс(ЭлементыФормы.РезультатыПоиска.ТекущаяСтрока);
	Если ИндексТекущейСтроки = 0 ИЛИ ИндексТекущейСтроки = Неопределено Тогда
		Возврат;
	Иначе
		ЭлементыФормы.РезультатыПоиска.ТекущаяСтрока = РезультатыПоиска.Получить(ИндексТекущейСтроки - 1);
	КонецЕсли;
	АктивизироватьТекущийРезультатПоиска();
	
КонецПроцедуры

// Процедура - действие кнопки Вперед командной панели КоманднаяПанельРезультатыПоиска.
//
Процедура КоманднаяПанельРезультатыПоискаВперед(Кнопка)
	
	Если ЭлементыФормы.РезультатыПоиска.ТекущаяСтрока = Неопределено Тогда
		Возврат;
	КонецЕсли;
	ИндексТекущейСтроки = РезультатыПоиска.Индекс(ЭлементыФормы.РезультатыПоиска.ТекущаяСтрока);
	Если ИндексТекущейСтроки = РезультатыПоиска.Количество() - 1 ИЛИ ИндексТекущейСтроки = Неопределено Тогда
		Возврат;
	Иначе
		ЭлементыФормы.РезультатыПоиска.ТекущаяСтрока = РезультатыПоиска.Получить(ИндексТекущейСтроки + 1);
	КонецЕсли;
	АктивизироватьТекущийРезультатПоиска();
	
КонецПроцедуры

//////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ДИАЛОГА

// Процедура - обработчик события Выбор табличного поля РезультатыПоиска.
//
Процедура РезультатыПоискаВыбор(Элемент, ВыбраннаяСтрока, Колонка, СтандартнаяОбработка)
	
	АктивизироватьТекущийРезультатПоиска();
	
КонецПроцедуры

// Процедура - обработчик события ПриПолученииДанных табличного поля РезультатыПоиска.
//
Процедура РезультатыПоискаПриПолученииДанных(Элемент, ОформленияСтрок)
	
	Для Каждого ОформлениеСтроки ИЗ ОформленияСтрок Цикл
		Если ЗначениеЗаполнено(ОформлениеСтроки.ДанныеСтроки.Страница) И ОформлениеСтроки.ДанныеСтроки.Страница <> 1 Тогда
			ОформлениеСтроки.Ячейки.НаименованиеЛиста.УстановитьТекст(ОформлениеСтроки.ДанныеСтроки.НаименованиеЛиста + " (страница №" + ОформлениеСтроки.ДанныеСтроки.Страница + ")");
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры