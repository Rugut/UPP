// Процедура - обработчик события ПередОткрытием формы.
//
Процедура ПередОткрытием(Отказ, СтандартнаяОбработка)
	
	ЖурналОтчетов.Отбор.ИсточникОтчета.Использование = Истина;
	ЖурналОтчетов.Отбор.ИсточникОтчета.ВидСравнения = ВидСравнения.Равно;

	Если ВладелецФормы.мВыбраннаяФормаКалендаря = Неопределено Тогда
		ЖурналОтчетов.Отбор.ИсточникОтчета.Значение = ВладелецФормы.ЭлементыФормы.ДеревоОтчетов.ТекущиеДанные.ИсточникОтчета;
	Иначе
		ЖурналОтчетов.Отбор.ИсточникОтчета.Значение = ВладелецФормы.мВыбраннаяФормаКалендаря;
	КонецЕсли;

	Если ВладелецФормы.мПериодичностьКалендарь = Неопределено Тогда
		// Режим из ЦУО
		ВладелецФормыПериодичность = ВладелецФормы.Периодичность;
	Иначе
		ВладелецФормыПериодичность = "Уникальная";
		// Установим отбор по периодичности.
		Если ВладелецФормы.мПериодичностьКалендарь = "Месячная" Тогда
			ЖурналОтчетов.Отбор.Периодичность.Установить(Перечисления.Периодичность.Месяц);
		КонецЕсли;

		Если ВладелецФормы.мПериодичностьКалендарь = "Квартальная" Тогда
			ЖурналОтчетов.Отбор.Периодичность.Установить(Перечисления.Периодичность.Квартал);
		КонецЕсли;

	КонецЕсли;

	Если ВладелецФормы.мДатаКонцаПериодаОтчетаКалендарь = Неопределено Тогда
		ВладелецФормыДатаКонцаПериодаОтчета = ВладелецФормы.ДатаКонцаПериодаОтчета;
	Иначе
		ВладелецФормыДатаКонцаПериодаОтчета = ВладелецФормы.мДатаКонцаПериодаОтчетаКалендарь;
	КонецЕсли;

	Если ВладелецФормы.Организация.Количество() = 0 Тогда
		ЖурналОтчетов.Отбор.Организация.Использование = Ложь;
	Иначе
		ЖурналОтчетов.Отбор.Организация.Использование = Истина;
		ЖурналОтчетов.Отбор.Организация.ВидСравнения = ВидСравнения.ВСписке;
		ЖурналОтчетов.Отбор.Организация.Значение = ВладелецФормы.Организация;
	КонецЕсли;
		
	Если НЕ ВладелецФормы.ОтборКодИФНС Тогда
		ЖурналОтчетов.Отбор.КодИМНС.Использование = Ложь;
	Иначе
		ЖурналОтчетов.Отбор.КодИМНС.Использование = Истина;
		ЖурналОтчетов.Отбор.КодИМНС.ВидСравнения = ВидСравнения.Равно;
		ЖурналОтчетов.Отбор.КодИМНС.Значение = ВладелецФормы.КодИФНС;
	КонецЕсли;
		
	Если НЕ ВладелецФормы.ОтборПериод Тогда
		ЖурналОтчетов.Отбор.ДатаНачала.Использование = Ложь;
		ЖурналОтчетов.Отбор.ДатаОкончания.Использование = Ложь;
	Иначе                            
		Если ВладелецФормыПериодичность = "Произвольный" Тогда
			ЖурналОтчетов.Отбор.ДатаНачала.Использование = Истина;
			ЖурналОтчетов.Отбор.ДатаОкончания.Использование = Истина;
			ЖурналОтчетов.Отбор.ДатаНачала.ВидСравнения = ВидСравнения.БольшеИлиРавно;
			ЖурналОтчетов.Отбор.ДатаНачала.Значение = ВладелецФормы.ДатаНачалаПериодаОтчета;
			ЖурналОтчетов.Отбор.ДатаОкончания.ВидСравнения = ВидСравнения.МеньшеИлиРавно;
			ЖурналОтчетов.Отбор.ДатаОкончания.Значение = ВладелецФормы.ДатаКонцаПериодаОтчета;
		Иначе
			ЖурналОтчетов.Отбор.ДатаНачала.Использование = Ложь;
			ЖурналОтчетов.Отбор.ДатаОкончания.Использование = Истина;
			ЖурналОтчетов.Отбор.ДатаОкончания.ВидСравнения = ВидСравнения.ИнтервалВключаяГраницы;
			ЖурналОтчетов.Отбор.ДатаОкончания.ЗначениеС = НачалоДня(ВладелецФормыДатаКонцаПериодаОтчета);
			ЖурналОтчетов.Отбор.ДатаОкончания.ЗначениеПо = КонецДня(ВладелецФормыДатаКонцаПериодаОтчета);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

// Процедура - действие кнопки ОК командной панели ОсновныеДействияФормы.
//
Процедура ОсновныеДействияФормыОК(Кнопка)
	
	Если ЭлементыФормы.ЖурналОтчетов.ТекущиеДанные = Неопределено Тогда
		Предупреждение("Выберите отчет для открытия!");
		Возврат;
	КонецЕсли;
	Закрыть(ЭлементыФормы.ЖурналОтчетов.ТекущиеДанные.Ссылка);
	
КонецПроцедуры

// Процедура - обработчик события Выбор табличного поля ЖурналОтчетов.
//
Процедура ЖурналОтчетовВыбор(Элемент, ВыбраннаяСтрока, Колонка, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Закрыть(ВыбраннаяСтрока.Ссылка);
	
КонецПроцедуры

// Процедура - действие кнопки Новый командной панели ОсновныеДействияФормы.
//
Процедура ОсновныеДействияФормыНовый(Кнопка)
	
	Закрыть(Истина);
	
КонецПроцедуры

// Процедура - обработчик события ПриПолученииДанных табличного поля ЖурналОтчетов.
//
Процедура ЖурналОтчетовПриПолученииДанных(Элемент, ОформленияСтрок)
	
	Для Каждого ОформлениеСтроки Из ОформленияСтрок Цикл
		ОформлениеСтроки.Ячейки.Период.УстановитьТекст(ПредставлениеПериода(ОформлениеСтроки.ДанныеСтроки.ДатаНачала, КонецДня(ОформлениеСтроки.ДанныеСтроки.ДатаОкончания), "ФП=Истина"));
		ОформлениеСтроки.Ячейки.Вид.УстановитьТекст(РегламентированнаяОтчетность.ПредставлениеВидаДокумента(ОформлениеСтроки.ДанныеСтроки.Вид));
	КонецЦикла;
	
КонецПроцедуры

ЖурналОтчетов.Колонки.Добавить("ДатаНачала");
ЖурналОтчетов.Колонки.Добавить("ДатаОкончания");