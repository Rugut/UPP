Перем ТекстХТМЛ;
Перем ТекстЗаголовокХТМЛ;
Перем ТекстЗавершенияХТМЛ;
Перем КартинкаИнформация;
Перем КартинкаВажнаяИнформация;
Перем КартинкаОшибка;
Перем КартинкаОткрыть Экспорт;
Перем КартинкаЗакрыть Экспорт;
Перем ИДБлокаХТМЛ;
Перем СпецСимволы;

Функция ДобавитьСообщение(ТекстСообщения, ВидСообщения, 
							РасшифровкаСообщения = НеОпределено, 
							РодительскаяСтрока = НеОпределено, 
							РаскрытьСообщение = Истина) Экспорт
							
	Если ТипЗнч(РодительскаяСтрока) = Тип("СтрокаДереваЗначений") Тогда
		НоваяСтрока = РодительскаяСтрока.Строки.Добавить();
	Иначе
		НоваяСтрока = Сообщения.Строки.Добавить();
	КонецЕсли;
	НоваяСтрока.Текст = ТекстСообщения;
	НоваяСтрока.Расшифровка = РасшифровкаСообщения;
	НоваяСтрока.ВидСообщения = ВидСообщения;
	Если НЕ РаскрытьСообщение Тогда
		СвернутьПоУмолчанию.Добавить(НоваяСтрока);
	КонецЕсли;
	Возврат НоваяСтрока;

КонецФункции

Процедура УдалитьСообщения() Экспорт
	
	Сообщения.Строки.Очистить();
	СвернутьПоУмолчанию.Очистить();
	
	#Если ТолстыйКлиентОбычноеПриложение Тогда
	
	Форма = ПолучитьФорму();
	Форма.ЭлементыФормы.HTMLДокумент.УстановитьТекст("<HTML><HEAD></HEAD><BODY scroll=no></BODY></HTML>");
	
	#КонецЕсли
	
КонецПроцедуры

Процедура СоздатьТекстХТМЛ(Сообщения,НомерСтрокиРодителя)
	
	СчетчикСтрок = 0;
	Для Каждого Строка Из Сообщения.Строки Цикл
		Расшифровка = "";
		ТекстСообщения = Строка.Текст;
		
		Если Строка.Расшифровка <> НеОпределено Тогда
			Для Счетчик = 0 По Строка.Расшифровка.ВГраница() Цикл
				ПозицияСпецСимволы = Найти(ТекстСообщения,СпецСимволы);
				Если ПозицияСпецСимволы <> 0 Тогда
					ТекстСообщения = Сред(ТекстСообщения,1,ПозицияСпецСимволы - 1) + 
					" <a id=""comment" + НомерСтрокиРодителя + Прав("00" + Строка(СчетчикСтрок),3) + "R" + Строка(Счетчик) + """ href=""#"">" + Строка.Расшифровка[Счетчик].Представление + "</a>" +
					Сред(ТекстСообщения,ПозицияСпецСимволы + 2);
					
				Иначе
					ТекстСообщения = ТекстСообщения + " <a id=""comment" + НомерСтрокиРодителя + Прав("00" + Строка(СчетчикСтрок),3) + "R" + Строка(Счетчик) + """ href=""#"">" + Строка.Расшифровка[Счетчик].Представление + "</a>";
					
				КонецЕсли;
				
			КонецЦикла;
			
		КонецЕсли;
		
		Если Строка.ВидСообщения = Перечисления.ВидыСообщений.Раздел Тогда
			ТекстХТМЛ = ТекстХТМЛ + Символы.ПС + "<h6>";
			
			Если Строка.Строки.Количество() <> 0 Тогда
				Если СвернутьПоУмолчанию.Найти(Строка) = Неопределено Тогда
					ТекстХТМЛ = ТекстХТМЛ + Символы.ПС + "<a href=""#""><img id=""openclose" + Прав(("00" + Строка(ИДБлокаХТМЛ)),3) + """ src=""КартинкаЗакрыть"" WIDTH=""9"" HEIGHT=""7"" ALIGN=""right"" BORDER=""0"" ALT=""Свернуть...""></a>";
					
				Иначе
					ТекстХТМЛ = ТекстХТМЛ + Символы.ПС + "<a href=""#""><img id=""openclose" + Прав(("00" + Строка(ИДБлокаХТМЛ)),3) + """ src=""КартинкаОткрыть"" WIDTH=""9"" HEIGHT=""7"" ALIGN=""right"" BORDER=""0"" ALT=""Подробнее...""></a>";
					
				КонецЕсли;
			КонецЕсли;
			
			ТекстХТМЛ = ТекстХТМЛ + " " + ТекстСообщения;
			
		ИначеЕсли Строка.ВидСообщения = Перечисления.ВидыСообщений.Таблица ИЛИ Строка.ВидСообщения = Перечисления.ВидыСообщений.ТаблицаСворачиваемаяСИтогами Тогда
			
			Если ТипЗнч(ТекстСообщения) <> Тип("ТаблицаЗначений") Тогда
				Продолжить;
			КонецЕсли;
			
			Если Строка.ВидСообщения = Перечисления.ВидыСообщений.ТаблицаСворачиваемаяСИтогами Тогда
			
				СвернутьСтроки = (СвернутьПоУмолчанию.Найти(Строка) <> Неопределено);
				
				ТекстХТМЛ = ТекстХТМЛ + Символы.ПС + "<li class=""subinfotable""><div>";
				
				Если СвернутьСтроки  Тогда
					ТекстХТМЛ = ТекстХТМЛ + Символы.ПС + "<a href=""#""><img id=""openclose" + Прав(("00" + Строка(ИДБлокаХТМЛ)),3) + """ src=""КартинкаОткрыть"" WIDTH=""9"" HEIGHT=""7"" ALIGN=""right"" BORDER=""0"" ALT=""Подробнее...""></a>";
				Иначе
					ТекстХТМЛ = ТекстХТМЛ + Символы.ПС + "<a href=""#""><img id=""openclose" + Прав(("00" + Строка(ИДБлокаХТМЛ)),3) + """ src=""КартинкаЗакрыть"" WIDTH=""9"" HEIGHT=""7"" ALIGN=""right"" BORDER=""0"" ALT=""Свернуть...""></a>";
				КонецЕсли;
				
			Иначе
				
				СвернутьСтроки = Ложь;
				
			КонецЕсли;
			
			Если Строка.ВидСообщения = Перечисления.ВидыСообщений.ТаблицаСворачиваемаяСИтогами Тогда
			
				ТекстХТМЛ = ТекстХТМЛ + Символы.ПС + "<table cellspacing=""0"" id=""subitems" + Прав(("00" + Строка(ИДБлокаХТМЛ)),3) + "_ti" + """ ><tbody>";
			
			Иначе
			
				ТекстХТМЛ = ТекстХТМЛ + Символы.ПС + "<table cellspacing=""0""><tbody>";
			
			КонецЕсли; 

			ТекстХТМЛ = ТекстХТМЛ + Символы.ПС + "<tr>";
			Для Каждого Колонка Из ТекстСообщения.Колонки Цикл
				ТекстХТМЛ = ТекстХТМЛ + Символы.ПС + "<th>" + ?(ПустаяСтрока(Колонка.Заголовок), Колонка.Имя, Колонка.Заголовок) + "</th>";
			КонецЦикла;
			ТекстХТМЛ = ТекстХТМЛ + Символы.ПС + "</tr>";
			
			Для Каждого СтрокаТаблицы Из ТекстСообщения Цикл
				ТекстХТМЛ = ТекстХТМЛ + Символы.ПС + "<tr style=""display:" + ?(СвернутьСтроки, "none", "") + ";"">";
				Для Каждого Колонка Из ТекстСообщения.Колонки Цикл
					
					width = 120;
					align = "left";
					Значение = СтрокаТаблицы[Колонка.Имя];
					Если ТипЗнч(Значение) = Тип("Число") Тогда
				
						width = 70;
						align = "right";
				
					КонецЕсли; 
				
					ТекстХТМЛ = ТекстХТМЛ + Символы.ПС + "<td style=""width:" + Формат(width, "ЧГ=") + "px;text-align:" + align + """>" + Значение + "</td>";
				КонецЦикла;
			КонецЦикла;
			
			Если Строка.ВидСообщения = Перечисления.ВидыСообщений.ТаблицаСворачиваемаяСИтогами Тогда
			
				ТекстХТМЛ = ТекстХТМЛ + Символы.ПС + "<tr>";
				
				ПерваяКолонка = Ложь;
			
				Для Каждого Колонка Из ТекстСообщения.Колонки Цикл
					
					Если НЕ ПерваяКолонка Тогда
					
						ПерваяКолонка = Истина;
						
						ТекстХТМЛ = ТекстХТМЛ + Символы.ПС + "<th style=""text-align:center;"">ИТОГО:</th>";
					
					Иначе
					
						ИтогоПоКолонке = ТекстСообщения.Итог(Колонка.Имя);
						
						ИтогоПоКолонке = ?(ИтогоПоКолонке = Неопределено, 0, ИтогоПоКолонке);
					
						ТекстХТМЛ = ТекстХТМЛ + Символы.ПС + "<th style=""text-align:right;"">" + ИтогоПоКолонке + "</th>";
						
					КонецЕсли; 
					
				
				КонецЦикла;
				
				ТекстХТМЛ = ТекстХТМЛ + Символы.ПС + "</tr>";
			
			КонецЕсли; 
			
			
			ТекстХТМЛ = ТекстХТМЛ + Символы.ПС + "</tbody></table>";
			
		Иначе
			Если Строка.Родитель = Неопределено Тогда
				Если Строка.ВидСообщения = Перечисления.ВидыСообщений.Информация Тогда
					ТекстХТМЛ = ТекстХТМЛ + Символы.ПС + "<li class=""info""><div>";
					
				ИначеЕсли Строка.ВидСообщения = Перечисления.ВидыСообщений.ВажнаяИнформация Тогда
					ТекстХТМЛ = ТекстХТМЛ + Символы.ПС + "<li class=""warning""><div>";
					
				ИначеЕсли Строка.ВидСообщения = Перечисления.ВидыСообщений.Ошибка Тогда
					ТекстХТМЛ = ТекстХТМЛ + Символы.ПС + "<li class=""error""><div>";
					
				КонецЕсли;
				
			Иначе
				Если Строка.ВидСообщения = Перечисления.ВидыСообщений.Информация Тогда
					ТекстХТМЛ = ТекстХТМЛ + Символы.ПС + "<li class=""subinfo""><div>";
					
				ИначеЕсли Строка.ВидСообщения = Перечисления.ВидыСообщений.ВажнаяИнформация Тогда
					ТекстХТМЛ = ТекстХТМЛ + Символы.ПС + "<li class=""subwarning""><div>";
					
				ИначеЕсли Строка.ВидСообщения = Перечисления.ВидыСообщений.Ошибка Тогда
					ТекстХТМЛ = ТекстХТМЛ + Символы.ПС + "<li class=""suberror""><div>";
					
				КонецЕсли;
				
			КонецЕсли;
			
			
			Если Строка.Строки.Количество() <> 0 Тогда
				Если СвернутьПоУмолчанию.Найти(Строка) = Неопределено Тогда
					ТекстХТМЛ = ТекстХТМЛ + Символы.ПС + "<a href=""#""><img id=""openclose" + Прав(("00" + Строка(ИДБлокаХТМЛ)),3) + """ src=""КартинкаЗакрыть"" WIDTH=""9"" HEIGHT=""7"" ALIGN=""right"" BORDER=""0"" ALT=""Свернуть...""></a>";
					
				Иначе
					ТекстХТМЛ = ТекстХТМЛ + Символы.ПС + "<a href=""#""><img id=""openclose" + Прав(("00" + Строка(ИДБлокаХТМЛ)),3) + """ src=""КартинкаОткрыть"" WIDTH=""9"" HEIGHT=""7"" ALIGN=""right"" BORDER=""0"" ALT=""Подробнее...""></a>";
					
				КонецЕсли;
			КонецЕсли;
			
			ТекстХТМЛ = ТекстХТМЛ + " " + ТекстСообщения + "</div>";
			
		КонецЕсли;
		
		Если Строка.Строки.Количество() <> 0 Тогда
			Если СвернутьПоУмолчанию.Найти(Строка) = Неопределено Тогда
				ТекстХТМЛ = ТекстХТМЛ + Символы.ПС + "<ul class=""block"" id=""subitems" + Прав(("00" + Строка(ИДБлокаХТМЛ)),3) + """>";
				
			Иначе
				ТекстХТМЛ = ТекстХТМЛ + Символы.ПС + "<ul class=""none"" id=""subitems" + Прав(("00" + Строка(ИДБлокаХТМЛ)),3) + """>";
				
			КонецЕсли;
			
			ИДБлокаХТМЛ = ИДБлокаХТМЛ + 1;
			СоздатьТекстХТМЛ(Строка,НомерСтрокиРодителя + Прав("00"  + Строка(СчетчикСтрок),3));
			ТекстХТМЛ = ТекстХТМЛ + Символы.ПС + "</ul> </li>"
			
		ИначеЕсли Строка.ВидСообщения = Перечисления.ВидыСообщений.Раздел Тогда
			ТекстХТМЛ = ТекстХТМЛ + "</h6>";
			
		ИначеЕсли Строка.ВидСообщения = Перечисления.ВидыСообщений.Таблица Тогда
			
		ИначеЕсли Строка.ВидСообщения = Перечисления.ВидыСообщений.ТаблицаСворачиваемаяСИтогами Тогда

			Если СвернутьСтроки Тогда
				
				ТекстХТМЛ = ТекстХТМЛ + Символы.ПС + "<ul class=""none"" id=""subitems" + Прав(("00" + Строка(ИДБлокаХТМЛ)),3) + """>";
				
			Иначе
				
				ТекстХТМЛ = ТекстХТМЛ + Символы.ПС + "<ul class=""block"" id=""subitems" + Прав(("00" + Строка(ИДБлокаХТМЛ)),3) + """>";
				
			КонецЕсли;
			ИДБлокаХТМЛ = ИДБлокаХТМЛ + 1;
			СоздатьТекстХТМЛ(Строка,НомерСтрокиРодителя + Прав("00"  + Строка(СчетчикСтрок),3));
			ТекстХТМЛ = ТекстХТМЛ + Символы.ПС + "</ul> </li>"
			
		Иначе
			
			ТекстХТМЛ = ТекстХТМЛ + "</li>";
			
		КонецЕсли;
		
		СчетчикСтрок = СчетчикСтрок + 1;
	КонецЦикла;
	
КонецПроцедуры

Процедура ПоказатьСообщения(ЗаголовокФормы = НеОпределено) Экспорт
	
	#Если ТолстыйКлиентОбычноеПриложение Тогда
		
	Если Сообщения.Строки.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Форма = ПолучитьФорму();
	Если ЗаголовокФормы = НеОпределено Тогда
		Форма.Заголовок = "Внимание!";
	Иначе
		Форма.Заголовок = ЗаголовокФормы;
	КонецЕсли;
	
	ТекстХТМЛ = ТекстЗаголовокХТМЛ;
	ИДБлокаХТМЛ =0;
	СоздатьТекстХТМЛ(Сообщения,"");
	ТекстХТМЛ = ТекстХТМЛ + ТекстЗавершенияХТМЛ;
	
	КартинкаИнформация			= РаботаСДиалогами.ПолучитьПутьККартинкеДляHTML(БиблиотекаКартинок.СообщениеИнформация, Форма.ЭлементыФормы.HTMLДокумент);
	КартинкаВажнаяИнформация	= РаботаСДиалогами.ПолучитьПутьККартинкеДляHTML(БиблиотекаКартинок.СообщениеВажнаяИнформация, Форма.ЭлементыФормы.HTMLДокумент);
	КартинкаОшибка				= РаботаСДиалогами.ПолучитьПутьККартинкеДляHTML(БиблиотекаКартинок.СообщениеОшибка, Форма.ЭлементыФормы.HTMLДокумент);
	КартинкаОткрыть				= РаботаСДиалогами.ПолучитьПутьККартинкеДляHTML(БиблиотекаКартинок.СообщениеОткрыть, Форма.ЭлементыФормы.HTMLДокумент);
	КартинкаЗакрыть				= РаботаСДиалогами.ПолучитьПутьККартинкеДляHTML(БиблиотекаКартинок.СообщениеЗакрыть, Форма.ЭлементыФормы.HTMLДокумент);
	
	ТекстХТМЛ	= СтрЗаменить(ТекстХТМЛ,"КартинкаИнформация",КартинкаИнформация);
	ТекстХТМЛ	= СтрЗаменить(ТекстХТМЛ,"КартинкаВажнаяИнформация",КартинкаВажнаяИнформация);
	ТекстХТМЛ	= СтрЗаменить(ТекстХТМЛ,"КартинкаОшибка",КартинкаОшибка);
	
	ТекстХТМЛ	= СтрЗаменить(ТекстХТМЛ,"КартинкаОткрыть",КартинкаОткрыть);
	ТекстХТМЛ	= СтрЗаменить(ТекстХТМЛ,"КартинкаЗакрыть",КартинкаЗакрыть);
	
	Форма.ЭлементыФормы.HTMLДокумент.Установитьтекст(ТекстХТМЛ);
	
	Форма.Открыть();
	
	#КонецЕсли
	
КонецПроцедуры

Процедура РаскрытьКомментарийРасшифровки(УказательНаРасшифровку) Экспорт
	СтрокаНомерРасшифровки = Сред(УказательНаРасшифровку,Найти(УказательНаРасшифровку,"R"));
	УказательНаРасшифровку = СтрЗаменить(УказательНаРасшифровку,СтрокаНомерРасшифровки,"");
	СтрокаНомерРасшифровки = СтрЗаменить(СтрокаНомерРасшифровки,"R","");
	
	ВыбраннаяСтрока = Сообщения;
	Пока СтрДлина(УказательНаРасшифровку) <> 0	Цикл
		СтрокаИндексСтроки =Лев(УказательНаРасшифровку,3);
		УказательНаРасшифровку = Сред(УказательНаРасшифровку,4);
		ВыбраннаяСтрока =  ВыбраннаяСтрока.Строки.Получить(Число(СтрокаИндексСтроки));
	КонецЦикла;
	Расшифровка = ВыбраннаяСтрока.Расшифровка[Число(СтрокаНомерРасшифровки)].Расшифровка;
	Если Расшифровка = НеОпределено Тогда
		Возврат;
	КонецЕсли;
	Если ТипЗнч(Расшифровка) = Тип("Массив") Тогда
		Действие = Расшифровка[0];
		Параметры = Новый Массив;
		
		Для Счетчик = 1 По Расшифровка.ВГраница() Цикл
			Параметры.Добавить(Расшифровка[Счетчик]);
		КонецЦикла;
		
		Если Действие = "ПоказатьСправку" Тогда
			#Если ТолстыйКлиентОбычноеПриложение Тогда
				ОткрытьСправку(Параметры[0]);
			#КонецЕсли
			
		ИначеЕсли Действие = "ОткрытьФормуСписка" Тогда
			Выполнить Параметры[0]+".ПолучитьФормуСписка().Открыть();";
			
		ИначеЕсли Действие = "ОткрытьОтчет" Тогда
			ИмяОтчета = Параметры[0];
			Если Метаданные.Отчеты.Найти(ИмяОтчета) <> Неопределено Тогда
					ФормаОтчета = Отчеты[Параметры[0]].ПолучитьФорму();
					ФормаОтчета.Открыть();
				КонецЕсли;
				
		Иначе
			Выполнить Действие + "(Параметры);";
			
		КонецЕсли;
	Иначе
		#Если ТолстыйКлиентОбычноеПриложение Тогда
			ОткрытьЗначение(Расшифровка);
		#КонецЕсли
	КонецЕсли;
КонецПроцедуры

СпецСимволы = "%%";
Сообщения.Колонки.Добавить("Текст");          	// текст
Сообщения.Колонки.Добавить("ВидСообщения"); 	// вид сообщения (у каждого - своя "картинка")
Сообщения.Колонки.Добавить("Расшифровка");    	// массив структур "представление + выполняемое действие"
СвернутьПоУмолчанию = Новый Массив;

ТекстХТМЛ = "";
ТекстЗаголовокХТМЛ ="
|	<HTML><HEAD>
|	<style type=text/css>
|		h6 {
|			padding-bottom: 0em;
|		};
|		div {
|			padding: 1 4 3 4;
|			border-style: solid;
|			border-width: 0;
|			border-color:#DADAC4;
|		};
|		a {
|			padding-top: 0em;
|			padding-bottom: 0.2em;
|		};
|		body {
|			font-family: ""MS Sans Serif"";
//|			font-size: 1;
|			margin-right: 0em;
|			scrollbar-face-color:#FFFBF0;
|			scrollbar-darkshadow-color:#fff;
|			scrollbar-highlight-color:#DADAC4;
|			scrollbar-arrow-color:#708090;
|			scrollbar-3dlight-color:#fff;
|			scrollbar-track-color:#FFFBF0;
|		};
|		ul {
|			margin-top: 0em;
|			margin-bottom: 0.2em;
|			margin-left: 1.5em;};
|		ul.none {display:none;};
|		ul.block {display:block;};
|		li {
|			list-style-type: none;
|			margin: 0;
|			padding: 0;
|		};
|		li.error {
|			background: URL(""КартинкаОшибка"") no-repeat;
|			padding-left: 15px;
|			float: left;
|			width: 100%;
|		};
|		li.info {
|			background: URL(""КартинкаИнформация"") no-repeat;
|			padding-left: 15px;
|			float: left;
|			width: 100%;
|		};
|		li.warning {
|			background: URL(""КартинкаВажнаяИнформация"") no-repeat;]
|			padding-left: 15px;
|			float: left;
|			width: 100%;
|		};
|		li.suberror {
|			background: URL(""КартинкаОшибка"") no-repeat;
|			margin-left: -10px;
|			padding-left: 15px;
|		};
|		li.subinfo {
|			background: URL(""КартинкаИнформация"") no-repeat;
|			margin-left: -10px;
|			padding-left: 15px;
|		};
|		li.subinfotable {
|			margin-left: -10px;
|			padding-left: 15px;
|		};
|		li.subwarning {
|			background: URL(""КартинкаВажнаяИнформация"") no-repeat;
|			margin-left: -10px;
|			padding-left: 15px;
|		};
|		img {margin-top: 3px;};
|		table {
|			border-width: 1px;
|			border-spacing: 5px;
|			border-style: solid;
|			font-family: ""MS Sans Serif"";
//|			font-size: 1;
|			margin-right: 0em;
|			border-color:#A0A0A4; 
|		};
|		td {
|			border-width: 1px;
|			border-style: solid;
|			font-size:12;
|		};
|		th {
|			border-width: 1px;
|			border-style: solid;
|			background-color:#F0F0F0;
|			width:60px;
|			font-size:12;
|		};
|	</style>
|	</HEAD>
|	<BODY vlink=#000 link=#000 scroll=auto><FONT face=""MS Sans Serif"" size=1>
|";
ТекстЗавершенияХТМЛ ="
|	</FONT></BODY></HTML>
|"