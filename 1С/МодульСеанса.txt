Процедура УстановкаПараметровСеанса(ТребуемыеПараметры)
	
	Если ТребуемыеПараметры = Неопределено Тогда
		// раздел "безусловной" инициализации параметров сеанса
		
		ПолныеПрава.УстановитьПараметрыМеханизмаОграниченияПравДоступа();
		
	Иначе		
		
		УстановленныеПараметры = Новый Структура;
		
		Для Каждого ИмяПараметра ИЗ ТребуемыеПараметры Цикл
			УстановитьЗначениеПараметраСеанса(ИмяПараметра, УстановленныеПараметры);
		КонецЦикла;
		
		// СтандартныеПодсистемы
		СтандартныеПодсистемыСервер.УстановкаПараметровСеанса(ТребуемыеПараметры);
		// Конец СтандартныеПодсистемы
		
	КонецЕсли;
	
КонецПроцедуры

Процедура УстановитьЗначениеПараметраСеанса(ИмяПараметра, УстановленныеПараметры)
	
	Если УстановленныеПараметры.Свойство(ИмяПараметра) Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыРИБ = Новый Структура;
	ПараметрыРИБ.Вставить("НаличиеОбменаУПП");
	ПараметрыРИБ.Вставить("ВсеУзлыОбменаУПП");
	ПараметрыРИБ.Вставить("СоответствиеОрганизацийИУзловОбменаУПП");
	ПараметрыРИБ.Вставить("СоответствиеУзловИДатДляОбменаУПП");
	
	ПараметрыРИБ.Вставить("ИспользованиеРИБ");
	ПараметрыРИБ.Вставить("ПрефиксУзлаРаспределеннойИнформационнойБазы");
	ПараметрыРИБ.Вставить("ВсеУзлыДляОбменаССайтомТоварами");
	ПараметрыРИБ.Вставить("ВсеУзлыДляОбменаССайтомЗаказами");
	ПараметрыРИБ.Вставить("НаличиеОбменаССайтом");
	
	ПараметрыРИБ.Вставить("ВсеУзлыОбменаУТУПП");
	ПараметрыРИБ.Вставить("НаличиеОбменаУТУПП");
	ПараметрыРИБ.Вставить("СоответствиеОрганизацийИУзловОбменаУТУПП");
	ПараметрыРИБ.Вставить("СоответствиеУзловИДатДляОбменаУТУПП");
	ПараметрыРИБ.Вставить("СохранятьИнформациюОМестеСозданияОбъектов");
	
	ПараметрыРИБ.Вставить("НаличиеОбменаСРозничнойТорговлей");
	ПараметрыРИБ.Вставить("ВсеУзлыОбменаСРозничнойТорговлей");
	ПараметрыРИБ.Вставить("СоответствиеУзловИДатДляОбменаУправлениеПредприятиемРозничнаяТорговля");
	ПараметрыРИБ.Вставить("СоответствиеРеквизитовИУзловОбменаСРозничнойТорговлей");
	
	Если ИмяПараметра = "ОбщиеЗначения" Тогда
		ПараметрыСеанса.ОбщиеЗначения = Новый ХранилищеЗначения(Новый Структура);
		УстановленныеПараметры.Вставить("ОбщиеЗначения");
		
	ИначеЕсли ПараметрыРИБ.Свойство(ИмяПараметра) Тогда	
		ПолныеПрава.ОпределитьФактИспользованияРИБ();
		ДополнитьСписокУстановленныхПараметров(УстановленныеПараметры, ПараметрыРИБ);
		
	ИначеЕсли ИмяПараметра = "ТекущийПользователь" Тогда
		ПользователиПолныеПрава.ОпределитьТекущегоПользователя("ТекущийПользователь", УстановленныеПараметры);
		
	ИначеЕсли ИмяПараметра = "ГраницыЗапретаИзмененияДанных" Тогда		
		ПолныеПрава.УстановитьПараметрГраницыЗапретаИзмененияДанных();
		УстановленныеПараметры.Вставить("ГраницыЗапретаИзмененияДанных");
		
	КонецЕсли;
	
	//ПодключаемоеОборудование
	МенеджерОборудованияСервер.УстановитьПараметрыСеансаПодключаемогоОборудования(ИмяПараметра, УстановленныеПараметры);
	//Конец ПодключаемоеОборудование
	
КонецПроцедуры

Процедура ДополнитьСписокУстановленныхПараметров(УстановленныеПараметры, ПараметрыРаздела)
	
	Для Каждого КлючИЗначение ИЗ ПараметрыРаздела Цикл
		УстановленныеПараметры.Вставить(КлючИЗначение.Ключ);
	КонецЦикла;
		
КонецПроцедуры