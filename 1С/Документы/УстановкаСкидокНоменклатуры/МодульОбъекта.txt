Перем мУдалятьДвижения;

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОКУМЕНТА

#Если Клиент Тогда
// Процедура осуществляет печать документа. Можно направить печать на 
// экран или принтер, а также распечатать необходимое количество копий.
//
//  Название макета печати передается в качестве параметра,
// по переданному названию находим имя макета в соответствии.
//
// Параметры:
//  НазваниеМакета - строка, название макета.
//
Процедура Печать(ИмяМакета, КоличествоЭкземпляров = 1, НаПринтер = Ложь) Экспорт

	Если ЭтоНовый() Тогда
		Предупреждение("Документ можно распечатать только после его записи");
		Возврат;
	ИначеЕсли Не УправлениеДопПравамиПользователей.РазрешитьПечатьНепроведенныхДокументов(Проведен) Тогда
		Предупреждение("Недостаточно полномочий для печати непроведенного документа!");
		Возврат;
	КонецЕсли;

	Если Не РаботаСДиалогами.ПроверитьМодифицированность(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;

	Если ТипЗнч(ИмяМакета) = Тип("ДвоичныеДанные") Тогда

		ТабДокумент = УниверсальныеМеханизмы.НапечататьВнешнююФорму(Ссылка, ИмяМакета);
		
		Если ТабДокумент = Неопределено Тогда
			Возврат
		КонецЕсли;
		
	КонецЕсли;

	УниверсальныеМеханизмы.НапечататьДокумент(ТабДокумент, КоличествоЭкземпляров, НаПринтер, ОбщегоНазначения.СформироватьЗаголовокДокумента(ЭтотОбъект), Ссылка);

КонецПроцедуры // Печать()

// Возвращает доступные варианты печати документа
//
// Возвращаемое значение:
//  Структура, каждая строка которой соответствует одному из вариантов печати
//  
Функция ПолучитьСтруктуруПечатныхФорм() Экспорт
	
	Возврат Новый Структура;

КонецФункции // ПолучитьСтруктуруПечатныхФорм()

#КонецЕсли

// Проверяет необходимость расчета автоматических скидок.
//
// Параметры:
//  ТекВид - вид автоматических скидок, для которого нужно выполнить проверку.
//
Функция РассчитыватьАвтоматическиеСкидки(ТекВид)
	ИспользованиеСкидок = ОбщегоНазначения.ПолучитьПараметрыУчетнойПолитикиУпр(Дата);
	Если НЕ ЗначениеЗаполнено(ИспользованиеСкидок) Тогда
		Возврат Ложь;
	КонецЕсли;

	Если ТекВид = Перечисления.УсловияСкидкиНаценки.ПоКоличествуТовара Тогда
		Результат = ИспользованиеСкидок.ИспользоватьСкидкиПоКоличествуТовара;
	ИначеЕсли ТекВид = Перечисления.УсловияСкидкиНаценки.ПоСуммеДокумента Тогда
		Результат = ИспользованиеСкидок.ИспользоватьСкидкиПоСуммеДокумента;
	ИначеЕсли ТекВид = Перечисления.УсловияСкидкиНаценки.ПоДисконтнойКарте Тогда
		Результат = ИспользованиеСкидок.ИспользоватьСкидкиПоДисконтнойКарте;
	ИначеЕсли ТекВид = Перечисления.УсловияСкидкиНаценки.ПоВидуОплаты Тогда
		Результат = ИспользованиеСкидок.ИспользоватьСкидкиПоВидуОплаты;
	Иначе
		Результат = Ложь;
	КонецЕсли;

	Возврат Результат;

КонецФункции // РассчитыватьАвтоматическиеСкидки()

// Функция возвращает массив строк из ТЧ "СоставНабора"
// по заданному ключу строки.
//
// Параметры:
//  КлючСтроки – ключ по которому нужно отобрать строки.
//  СтрокаТабличнойЧасти – строка ТЧ "Бонусы". Если указана,
//   и СтрокаТабличнойЧасти.Номенклатура.Комплект,
//   то в массиве возвращается только эта строка.
//
// Возвращаемое значение:
//  Массив, состоящий из строк ТЧ "СоставНабора"
//  или из одного элемента - переданная СтрокаТабличнойЧасти.
//
Функция ПолучитьСоставСтроки(КлючСтроки, СтрокаТабличнойЧасти = Неопределено) Экспорт

	Если СтрокаТабличнойЧасти <> Неопределено И Не СтрокаТабличнойЧасти.Номенклатура.Комплект Тогда
		МассивЭлементов = Новый Массив;
		МассивЭлементов.Добавить(СтрокаТабличнойЧасти);
	Иначе
		СтруктураПоиска = Новый Структура;
		СтруктураПоиска.Вставить("КлючСтроки", КлючСтроки);

		МассивЭлементов = СоставНабора.НайтиСтроки(СтруктураПоиска);
	КонецЕсли;

	Возврат МассивЭлементов;

КонецФункции // ПолучитьСоставСтроки()

// Функция определяет пуст ли состав ТЧ "СоставНабора"
// по заданному ключу строки.
//
// Параметры:
//  КлючСтроки – ключ, по которому нужно определить, пуст ли состав.
//
// Возвращаемое значение:
//  Булево. Истина - если, состав по заданному ключу пустой.
//
Функция СоставСтрокиНеЗаполнен(КлючСтроки) Экспорт

	Результат = (СоставНабора.Найти(КлючСтроки, "КлючСтроки") = Неопределено);

	Возврат Результат;

КонецФункции // СоставСтрокиНеЗаполнен()

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ДЛЯ ОБЕСПЕЧЕНИЯ ПРОВЕДЕНИЯ ДОКУМЕНТА

// Проверяет правильность заполнения шапки документа.
// Если какой-то из реквизитов, влияющих на проведение, не заполнен или
// заполнен некорректно, то устанавливается флаг отказа в проведении.
//
// Параметры:
//  ВыборкаПоШапкеДокумента - выборка из результата запроса по шапке документа.
//  Отказ - флаг отказа в проведении.
//  Заголовок - заголовок сообщения об ошибках.
//
Процедура ПроверитьЗаполнениеШапки(СтруктураШапкиДокумента, Отказ, Заголовок)

	// Укажем, что надо проверить.
	СтруктураОбязательныхПолей = Новый Структура;
	СтруктураОбязательныхПолей.Вставить("ВидОперации");
	СтруктураОбязательныхПолей.Вставить("ДатаНачала");

	Если ВидОперации <> Перечисления.ВидыОперацийУстановкаСкидокНоменклатуры.НатуральныеСкидки Тогда
		СтруктураОбязательныхПолей.Вставить("Валюта");
	КонецЕсли;

	Если ДляВсейНоменклатуры И ВидОперации = Перечисления.ВидыОперацийУстановкаСкидокНоменклатуры.ПоНоменклатуре Тогда
		СтруктураОбязательныхПолей.Вставить("Качество");
	КонецЕсли;

	Если (ВидОперации = Перечисления.ВидыОперацийУстановкаСкидокНоменклатуры.ПоНоменклатуре
	 Или ВидОперации = Перечисления.ВидыОперацийУстановкаСкидокНоменклатуры.ПоЦеновымГруппам)
	   И (Условие = Перечисления.УсловияСкидкиНаценки.ПоВидуОплаты
	 Или Условие = Перечисления.УсловияСкидкиНаценки.ПоВидуДисконтныхКарт
	 Или Условие = Перечисления.УсловияСкидкиНаценки.ПоДисконтнойКарте) Тогда
		СтруктураОбязательныхПолей.Вставить("ЗначениеУсловия");
	КонецЕсли;

	// Теперь вызовем общую процедуру проверки.
	ЗаполнениеДокументов.ПроверитьЗаполнениеШапкиДокумента(ЭтотОбъект, СтруктураОбязательныхПолей, Отказ, Заголовок);

	Если ДатаНачала > ДатаОкончания И ЗначениеЗаполнено(ДатаОкончания) Тогда
		ОбщегоНазначения.СообщитьОбОшибке("Дата начала действия скидки не может быть больше даты окончания действия скидки" , Отказ, Заголовок);
	КонецЕсли;

КонецПроцедуры // ПроверитьЗаполнениеШапки()

// Проверяет правильность заполнения табличной части "Товары".
// Если какой-то из реквизитов, влияющих на проведение, не заполнен или
// заполнен некорректно, то устанавливается флаг отказа в проведении.
//
// Параметры:
//  ВыборкаПоШапкеДокумента - выборка из результата запроса по шапке документа.
//  Отказ - флаг отказа в проведении.
//  Заголовок - заголовок сообщения об ошибках.
//
Процедура ПроверитьЗаполнениеТабличнойЧастиТовары(ТаблицаПоТоварам, СтруктураШапкиДокумента, Отказ, Заголовок)

	// Укажем, что надо проверить.
	СтруктураОбязательныхПолей = Новый Структура("Номенклатура, Качество");

	// Теперь вызовем общую процедуру проверки.
	ЗаполнениеДокументов.ПроверитьЗаполнениеТабличнойЧасти(ЭтотОбъект, "Товары", СтруктураОбязательныхПолей, Отказ, Заголовок);

КонецПроцедуры // ПроверитьЗаполнениеТабличнойЧастиТовары()

// Проверяет правильность заполнения табличной части "ЦеновыеГруппы".
// Если какой-то из реквизитов, влияющих на проведение, не заполнен или
// заполнен некорректно, то устанавливается флаг отказа в проведении.
//
// Параметры:
//  ВыборкаПоШапкеДокумента - выборка из результата запроса по шапке документа.
//  Отказ - флаг отказа в проведении.
//  Заголовок - заголовок сообщения об ошибках.
//
Процедура ПроверитьЗаполнениеТабличнойЧастиЦеновыеГруппы(СтруктураШапкиДокумента, Отказ, Заголовок)

	Если ЦеновыеГруппы.Количество() = 0 Тогда
		ОбщегоНазначения.СообщитьОбОшибке("Необходимо заполнить перечень ценовых групп!", Отказ, Заголовок);
	КонецЕсли;

	// Укажем, что надо проверить.
	СтруктураОбязательныхПолей = Новый Структура("ЦеноваяГруппа,  Качество");

	// Теперь вызовем общую процедуру проверки.
	ЗаполнениеДокументов.ПроверитьЗаполнениеТабличнойЧасти(ЭтотОбъект, "ЦеновыеГруппы", СтруктураОбязательныхПолей, Отказ, Заголовок);

КонецПроцедуры // ПроверитьЗаполнениеТабличнойЧастиЦеновыеГруппы()

// Проверяет правильность заполнения табличной части "Бонусы".
// Если какой-то из реквизитов, влияющих на проведение, не заполнен или
// заполнен некорректно, то устанавливается флаг отказа в проведении.
//
// Параметры:
//  ВыборкаПоШапкеДокумента - выборка из результата запроса по шапке документа.
//  Отказ - флаг отказа в проведении.
//  Заголовок - заголовок сообщения об ошибках.
//
Процедура ПроверитьЗаполнениеТабличнойЧастиБонусы(СтруктураШапкиДокумента, Отказ, Заголовок)

	Если Бонусы.Количество() = 0 Тогда
		ОбщегоНазначения.СообщитьОбОшибке("Необходимо заполнить перечень бонусов!", Отказ, Заголовок);
	КонецЕсли;

	// Укажем, что надо проверить.
	СтруктураОбязательныхПолей = Новый Структура("Номенклатура, Качество, СпецПредложение");

	// Теперь вызовем общую процедуру проверки.
	ЗаполнениеДокументов.ПроверитьЗаполнениеТабличнойЧасти(ЭтотОбъект, "Бонусы", СтруктураОбязательныхПолей, Отказ, Заголовок);

	// Здесь услуг быть не должно.
	ПредставлениеТабличнойЧасти = ЭтотОбъект.Метаданные().ТабличныеЧасти["Бонусы"].Представление();

	Для Каждого СтрокаБонус Из Бонусы Цикл
		Если (ЗначениеЗаполнено(СтрокаБонус.Номенклатура)
		   И СтрокаБонус.Номенклатура.Услуга)
		 Или (ЗначениеЗаполнено(СтрокаБонус.СпецПредложение)
		   И СтрокаБонус.СпецПредложение.Услуга) Тогда
			СтрокаНачалаСообщенияОбОшибке = "В строке номер """+ СокрЛП(СтрокаБонус.НомерСтроки) +
			                                """ табличной части """ + ПредставлениеТабличнойЧасти + """: ";

			ОбщегоНазначения.СообщитьОбОшибке(СтрокаНачалаСообщенияОбОшибке + "содержится услуга. Услуг здесь быть не должно!", Отказ, Заголовок);
		КонецЕсли;
	КонецЦикла;

КонецПроцедуры // ПроверитьЗаполнениеТабличнойЧастиБонусы()

// Проверяет правильность заполнения табличной части "СоставНабора".
// Если какой-то из реквизитов, влияющих на проведение, не заполнен или
// заполнен некорректно, то устанавливается флаг отказа в проведении.
//
// Параметры:
//  ВыборкаПоШапкеДокумента - выборка из результата запроса по шапке документа.
//  Отказ - флаг отказа в проведении.
//  Заголовок - заголовок сообщения об ошибках.
//
Процедура ПроверитьЗаполнениеТабличнойЧастиСоставНабора(СтруктураШапкиДокумента, Отказ, Заголовок)

	ПредставлениеТабличнойЧасти = ЭтотОбъект.Метаданные().ТабличныеЧасти["Бонусы"].Представление();

	Для Каждого СтрокаБонус Из Бонусы Цикл
		Если СтрокаБонус.Номенклатура.Комплект Тогда
			Если СоставСтрокиНеЗаполнен(СтрокаБонус.КлючСтроки - 1) Тогда
				СтрокаНачалаСообщенияОбОшибке = "Для комплекта номенклатуры в строке номер """+ СокрЛП(СтрокаБонус.НомерСтроки) +
				                                """ табличной части """ + ПредставлениеТабличнойЧасти + """: ";

				ОбщегоНазначения.СообщитьОбОшибке(СтрокаНачалаСообщенияОбОшибке + "не задан состав!", Отказ, Заголовок);
			КонецЕсли;
		КонецЕсли;

		Если СоставСтрокиНеЗаполнен(СтрокаБонус.КлючСтроки) Тогда
			СтрокаНачалаСообщенияОбОшибке = "Для спец. предложения в строке номер """+ СокрЛП(СтрокаБонус.НомерСтроки) +
			                                """ табличной части """ + ПредставлениеТабличнойЧасти + """: ";

			ОбщегоНазначения.СообщитьОбОшибке(СтрокаНачалаСообщенияОбОшибке + "не задан состав!", Отказ, Заголовок);
		КонецЕсли;
	КонецЦикла;

КонецПроцедуры // ПроверитьЗаполнениеТабличнойЧастиСоставНабора()

// Выполняет движения по регистрам.
//
Процедура ДвиженияПоРегистрам(СтруктураШапкиДокумента,ТаблицаПоТоварам, ТаблицаПоПолучателям, ТаблицаПоДнямНедели, ТаблицаПоЦеновымГруппам, ТаблицаПоБонусам, Отказ, Заголовок)

	Если СтруктураШапкиДокумента.ДляВсехПолучателей Тогда
		ТаблицаПоПолучателям.Очистить();

		ПустойПолучатель = ТаблицаПоПолучателям.Добавить();
		Если ВидСкидки = Перечисления.ВидыСкидок.Оптовая Тогда
			ПустойПолучатель.Получатель = Справочники.ДоговорыКонтрагентов.ПустаяСсылка();
		Иначе
			ПустойПолучатель.Получатель = Справочники.Склады.ПустаяСсылка();
		КонецЕсли;
	КонецЕсли;

	Если ВидОперации = Перечисления.ВидыОперацийУстановкаСкидокНоменклатуры.ПоНоменклатуре Тогда
		Если СтруктураШапкиДокумента.ДляВсейНоменклатуры Тогда
			ТаблицаПоТоварам.Очистить();

			ПустойТовар = ТаблицаПоТоварам.Добавить();
			ПустойТовар.Номенклатура               = Неопределено;
			ПустойТовар.ХарактеристикаНоменклатуры = Неопределено;
			ПустойТовар.Качество                   = СтруктураШапкиДокумента.Качество;
			ПустойТовар.ПроцентСкидкиНаценки       = СтруктураШапкиДокумента.ПроцентСкидкиНаценки;
			ПустойТовар.ОграничениеСкидкиНаценки   = СтруктураШапкиДокумента.ОграничениеСкидкиНаценки;
		КонецЕсли;

		НаборДвижений = Движения.СкидкиНаценкиНоменклатуры;
		ТаблицаДвижений = НаборДвижений.Выгрузить();

		Для каждого ТекСтрокаПолучателя Из ТаблицаПоПолучателям Цикл
			Для каждого ТекСтрокаТовара Из ТаблицаПоТоварам Цикл
				Движение = ТаблицаДвижений.Добавить();
				ПустойПолучатель = НЕ ЗначениеЗаполнено(ТекСтрокаПолучателя.Получатель);
				ПустойКонтрагент = НЕ ЗначениеЗаполнено(ТекСтрокаПолучателя.Контрагент);
				Если ПустойКонтрагент Тогда
					ПолучательДвижения = ТекСтрокаПолучателя.Получатель;
				Иначе
					Если ПустойПолучатель Тогда
						ПолучательДвижения = ТекСтрокаПолучателя.Контрагент;
					Иначе
						ПолучательДвижения = ТекСтрокаПолучателя.Получатель;
					КонецЕсли;
				КонецЕсли;

				Движение.ПолучательСкидки           = ПолучательДвижения;

				Движение.Номенклатура               = ТекСтрокаТовара.Номенклатура;
				Движение.ХарактеристикаНоменклатуры = ТекСтрокаТовара.ХарактеристикаНоменклатуры;
				Движение.Качество                   = ТекСтрокаТовара.Качество;

				Движение.Условие                    = ?(СтруктураШапкиДокумента.Условие = Перечисления.УсловияСкидкиНаценки.БезУсловий,
														Перечисления.УсловияСкидкиНаценки.ПоКоличествуТовара, СтруктураШапкиДокумента.Условие);
				Движение.ЗначениеУсловия            = ?(СтруктураШапкиДокумента.Условие = Перечисления.УсловияСкидкиНаценки.БезУсловий,
														0, СтруктураШапкиДокумента.ЗначениеУсловия);
				
				Движение.ПроцентСкидкиНаценки       = ТекСтрокаТовара.ПроцентСкидкиНаценки;
				Движение.ОграничениеСкидкиНаценки   = ТекСтрокаТовара.ОграничениеСкидкиНаценки;
				
				Движение.Валюта                     = СтруктураШапкиДокумента.Валюта;
			КонецЦикла;
		КонецЦикла;

		ТаблицаДвижений.ЗаполнитьЗначения(СтруктураШапкиДокумента.ДатаОкончания,"ДатаОкончания");

		Если Не Отказ Тогда
			НаборДвижений.мПериод          = СтруктураШапкиДокумента.ДатаНачала;
			НаборДвижений.мТаблицаДвижений = ТаблицаДвижений;
			Движения.СкидкиНаценкиНоменклатуры.ВыполнитьДвижения();
		КонецЕсли;
	ИначеЕсли ВидОперации = Перечисления.ВидыОперацийУстановкаСкидокНоменклатуры.ПоЦеновымГруппам Тогда
		Если ТаблицаПоЦеновымГруппам.Количество() <> 0 Тогда
			НаборДвижений = Движения.СкидкиНаценкиПоЦеновымГруппам;
			ТаблицаДвижений = НаборДвижений.Выгрузить();

			Для каждого ТекСтрокаПолучателя Из ТаблицаПоПолучателям Цикл
				Для каждого ТекСтрокаЦеноваяГруппа Из ТаблицаПоЦеновымГруппам Цикл
					Движение=ТаблицаДвижений.Добавить();
					ПустойПолучатель = НЕ ЗначениеЗаполнено(ТекСтрокаПолучателя.Получатель);
					ПустойКонтрагент = НЕ ЗначениеЗаполнено(ТекСтрокаПолучателя.Контрагент);

					Если ПустойКонтрагент Тогда
						ПолучательДвижения = ТекСтрокаПолучателя.Получатель;
					Иначе
						Если ПустойПолучатель Тогда
							ПолучательДвижения = ТекСтрокаПолучателя.Контрагент;
						Иначе
							ПолучательДвижения = ТекСтрокаПолучателя.Получатель;
						КонецЕсли;
					КонецЕсли;

					Движение.ПолучательСкидки           = ПолучательДвижения;

					Движение.ЦеноваяГруппа              = ТекСтрокаЦеноваяГруппа.ЦеноваяГруппа;
					Движение.Качество                   = ТекСтрокаЦеноваяГруппа.Качество;

					Движение.Условие                    = ?(СтруктураШапкиДокумента.Условие = Перечисления.УсловияСкидкиНаценки.БезУсловий,
					Перечисления.УсловияСкидкиНаценки.ПоКоличествуТовара, СтруктураШапкиДокумента.Условие);
					Движение.ЗначениеУсловия            = ?(СтруктураШапкиДокумента.Условие = Перечисления.УсловияСкидкиНаценки.БезУсловий,
					0, СтруктураШапкиДокумента.ЗначениеУсловия);

					Движение.ПроцентСкидкиНаценки       = ТекСтрокаЦеноваяГруппа.ПроцентСкидкиНаценки;
					Движение.ОграничениеСкидкиНаценки   = ТекСтрокаЦеноваяГруппа.ОграничениеСкидкиНаценки;

					Движение.Валюта                     = СтруктураШапкиДокумента.Валюта;
				КонецЦикла;
			КонецЦикла;

			ТаблицаДвижений.ЗаполнитьЗначения(СтруктураШапкиДокумента.ДатаОкончания,"ДатаОкончания");

			Если Не Отказ Тогда
				НаборДвижений.мПериод          = СтруктураШапкиДокумента.ДатаНачала;
				НаборДвижений.мТаблицаДвижений = ТаблицаДвижений;
				Движения.СкидкиНаценкиПоЦеновымГруппам.ВыполнитьДвижения();
			КонецЕсли;
		КонецЕсли;
	ИначеЕсли ВидОперации = Перечисления.ВидыОперацийУстановкаСкидокНоменклатуры.НатуральныеСкидки Тогда
		Если ТаблицаПоБонусам.Количество() <> 0 Тогда
			НаборДвижений = Движения.СкидкиНоменклатурыНатуральные;
			ТаблицаДвижений = НаборДвижений.Выгрузить();

			Для Каждого ТекСтрокаПолучателя Из ТаблицаПоПолучателям Цикл
				Для Каждого ТекСтрокаБонус Из ТаблицаПоБонусам Цикл
					Движение = ТаблицаДвижений.Добавить();
					ПустойПолучатель = НЕ ЗначениеЗаполнено(ТекСтрокаПолучателя.Получатель);
					ПустойКонтрагент = НЕ ЗначениеЗаполнено(ТекСтрокаПолучателя.Контрагент);

					Если ПустойКонтрагент Тогда
						ПолучательДвижения = ТекСтрокаПолучателя.Получатель;
					Иначе
						Если ПустойПолучатель Тогда
							ПолучательДвижения = ТекСтрокаПолучателя.Контрагент;
						Иначе
							ПолучательДвижения = ТекСтрокаПолучателя.Получатель;
						КонецЕсли;
					КонецЕсли;

					Движение.ПолучательСкидки = ПолучательДвижения;
					Движение.Номенклатура = ТекСтрокаБонус.Номенклатура;
					Движение.ХарактеристикаНоменклатуры = ТекСтрокаБонус.ХарактеристикаНоменклатуры;
					Движение.Качество = ТекСтрокаБонус.Качество;
					Движение.СпецПредложение = ТекСтрокаБонус.СпецПредложение;
					Движение.ХарактеристикаСпецПредложения = ТекСтрокаБонус.ХарактеристикаСпецПредложения;
					Движение.Количество = ТекСтрокаБонус.Количество * ТекСтрокаБонус.ЕдиницаИзмерения.Коэффициент / ТекСтрокаБонус.Номенклатура.ЕдиницаХраненияОстатков.Коэффициент;
				КонецЦикла;
			КонецЦикла;

			ТаблицаДвижений.ЗаполнитьЗначения(СтруктураШапкиДокумента.ДатаОкончания,"ДатаОкончания");

			Если Не Отказ Тогда
				НаборДвижений.мПериод          = СтруктураШапкиДокумента.ДатаНачала;
				НаборДвижений.мТаблицаДвижений = ТаблицаДвижений;
				Движения.СкидкиНоменклатурыНатуральные.ВыполнитьДвижения();
			КонецЕсли;

			НаборДвижений = Движения.СпецПредложения;
			ТаблицаДвижений = НаборДвижений.Выгрузить();

			Для Каждого ТекСтрокаБонус Из ТаблицаПоБонусам Цикл
				СоставКомплекта = ПолучитьСоставСтроки(ТекСтрокаБонус.КлючСтроки - 1, ТекСтрокаБонус);

				//Запишем движения комплектующих спец. предложения.
				//Комплектующие, за которые даются бонусы.
				Для Каждого СтрокаКомплектующей Из СоставКомплекта Цикл
					Движение = ТаблицаДвижений.Добавить();
					Движение.Номенклатура = ТекСтрокаБонус.СпецПредложение;
					Движение.ХарактеристикаНоменклатуры = ТекСтрокаБонус.ХарактеристикаСпецПредложения;
					Движение.Комплектующая = СтрокаКомплектующей.Номенклатура;
					Движение.ХарактеристикаКомплектующей = СтрокаКомплектующей.ХарактеристикаНоменклатуры;
					Движение.Количество = СтрокаКомплектующей.Количество * СтрокаКомплектующей.ЕдиницаИзмерения.Коэффициент / СтрокаКомплектующей.Номенклатура.ЕдиницаХраненияОстатков.Коэффициент;
					Движение.ЭтоБонус = Ложь;
				КонецЦикла;

				//И комплектующие-бонусы.
				СоставКомплекта = ПолучитьСоставСтроки(ТекСтрокаБонус.КлючСтроки);

				Для Каждого СтрокаКомплектующей Из СоставКомплекта Цикл
					Движение = ТаблицаДвижений.Добавить();
					Движение.Номенклатура = ТекСтрокаБонус.СпецПредложение;
					Движение.ХарактеристикаНоменклатуры = ТекСтрокаБонус.ХарактеристикаСпецПредложения;
					Движение.Комплектующая = СтрокаКомплектующей.Номенклатура;
					Движение.ХарактеристикаКомплектующей = СтрокаКомплектующей.ХарактеристикаНоменклатуры;
					Движение.Количество = СтрокаКомплектующей.Количество * СтрокаКомплектующей.ЕдиницаИзмерения.Коэффициент / СтрокаКомплектующей.Номенклатура.ЕдиницаХраненияОстатков.Коэффициент;
					Движение.ЭтоБонус = Истина;
				КонецЦикла;
			КонецЦикла;

			Если Не Отказ Тогда
				НаборДвижений.мТаблицаДвижений = ТаблицаДвижений;
				НаборДвижений.мПериод          = СтруктураШапкиДокумента.ДатаНачала;
				Движения.СпецПредложения.ВыполнитьДвижения();
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;

	ЗаписыватьДвиженияПоВремениСкидок = Ложь;
	НаборДвиженийВремяДействияСкидок = Движения.ВремяДействияСкидок;
	ТаблицаДвижений = НаборДвиженийВремяДействияСкидок.Выгрузить();
	ВремяНачала = Неопределено;
	ВремяОкончания = Неопределено;

	Для каждого ДеньНедели Из ТаблицаПоДнямНедели Цикл
		Если ДеньНедели.Выбран Тогда
			Движение                = ТаблицаДвижений.Добавить();
			Движение.ВремяНачала    = ДеньНедели.ВремяНачала;
			Движение.ВремяОкончания = ДеньНедели.ВремяОкончания;
			Движение.ДеньНедели     = ДеньНедели.ДеньНедели;

			Если ВремяНачала = Неопределено Тогда
				ВремяНачала = ДеньНедели.ВремяНачала;
			ИначеЕсли ВремяНачала <> ДеньНедели.ВремяНачала Тогда
				ЗаписыватьДвиженияПоВремениСкидок = Истина;
			КонецЕсли;

			Если ЗначениеЗаполнено(ДеньНедели.ВремяНачала)
			 Или Движение.ВремяОкончания <> '00010101235959' Тогда
				ЗаписыватьДвиженияПоВремениСкидок = Истина;
			КонецЕсли;

			Если ВремяОкончания = Неопределено Тогда
				ВремяОкончания = ДеньНедели.ВремяОкончания;
			ИначеЕсли ВремяОкончания <> ДеньНедели.ВремяОкончания Тогда
				ЗаписыватьДвиженияПоВремениСкидок = Истина;
			КонецЕсли;
		Иначе
			ЗаписыватьДвиженияПоВремениСкидок = Истина;
		КонецЕсли;
		
	КонецЦикла;

	Если Не Отказ И ЗаписыватьДвиженияПоВремениСкидок Тогда
		НаборДвиженийВремяДействияСкидок.мПериод          = СтруктураШапкиДокумента.ДатаНачала;
		НаборДвиженийВремяДействияСкидок.мТаблицаДвижений = ТаблицаДвижений;
		Движения.ВремяДействияСкидок.ВыполнитьДвижения();
	КонецЕсли;

КонецПроцедуры // ДвиженияПоРегистрам()

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

// Процедура - обработчик события "ОбработкаПроведения".
//
Процедура ОбработкаПроведения(Отказ, Режим)

	
	Если мУдалятьДвижения Тогда
		ОбщегоНазначения.УдалитьДвиженияРегистратора(ЭтотОбъект, Отказ);
	КонецЕсли;

	// Заголовок для сообщений об ошибках проведения.
	Заголовок = ОбщегоНазначения.ПредставлениеДокументаПриПроведении(Ссылка);

	ТекВид = ?(Условие = Перечисления.УсловияСкидкиНаценки.БезУсловий, Перечисления.УсловияСкидкиНаценки.ПоКоличествуТовара, Условие);
	Если Условие = Перечисления.УсловияСкидкиНаценки.БезУсловий Тогда
		ТекВид = Перечисления.УсловияСкидкиНаценки.ПоКоличествуТовара;
	ИначеЕсли Условие = Перечисления.УсловияСкидкиНаценки.ПоВидуДисконтныхКарт Тогда
		ТекВид = Перечисления.УсловияСкидкиНаценки.ПоДисконтнойКарте;
	Иначе
		ТекВид = Условие;
	КонецЕсли;

	Если Не РассчитыватьАвтоматическиеСкидки(ТекВид) Тогда
		НаименованиеВида = "";
		Если ТекВид = Перечисления.УсловияСкидкиНаценки.ПоКоличествуТовара Тогда
			НаименованиеВида = "Скидка по количеству товара";
		ИначеЕсли ТекВид = Перечисления.УсловияСкидкиНаценки.ПоСуммеДокумента Тогда
			НаименованиеВида = "Скидка по сумме документа";
		ИначеЕсли ТекВид = Перечисления.УсловияСкидкиНаценки.ПоДисконтнойКарте Тогда
			НаименованиеВида = "Скидка по дисконтной карте";
		ИначеЕсли ТекВид = Перечисления.УсловияСкидкиНаценки.ПоВидуОплаты Тогда
			НаименованиеВида = "Скидка по виду оплаты";
		КонецЕсли;

		ОбщегоНазначения.СообщитьОбОшибке("В учетной политике не указано использование скидок этого вида ("+ НаименованиеВида + ")."
		                    + Символы.ПС + "Скидка не может быть назначена." , Отказ, Заголовок);
	КонецЕсли;

	// Сформируем структуру реквизитов шапки документа.
	СтруктураШапкиДокумента = ОбщегоНазначения.СформироватьСтруктуруШапкиДокумента(ЭтотОбъект);

	Если ВидОперации = Перечисления.ВидыОперацийУстановкаСкидокНоменклатуры.ПоНоменклатуре
		И Не ДляВсейНоменклатуры И Товары.Количество() = 0 Тогда
		ОбщегоНазначения.СообщитьОбОшибке("Необходимо заполнить перечень товаров или установить флажок ""Для всей номенклатуры""!", Отказ, Заголовок);
	КонецЕсли;

	Если ПолучателиСкидки.Количество() = 0 И Не ДляВсехПолучателей Тогда
		ОбщегоНазначения.СообщитьОбОшибке("Необходимо заполнить перечень получателей скидки или установить флажок ""Для всех получателей""!", Отказ, Заголовок);
	КонецЕсли;

	ПроверитьЗаполнениеШапки(СтруктураШапкиДокумента, Отказ, Заголовок);

	СтруктураПолей = Новый Структура;
	СтруктураПолей.Вставить("Номенклатура"              , "Номенклатура");
	СтруктураПолей.Вставить("Услуга"                    , "Номенклатура.Услуга");
	СтруктураПолей.Вставить("ХарактеристикаНоменклатуры", "ХарактеристикаНоменклатуры");
	СтруктураПолей.Вставить("Качество"                  , "Качество");
	СтруктураПолей.Вставить("ПроцентСкидкиНаценки"      , "ПроцентСкидкиНаценки");
	СтруктураПолей.Вставить("ОграничениеСкидкиНаценки"  , "ОграничениеСкидкиНаценки");

	РезультатЗапросаПоТоварам = УправлениеЗапасами.СформироватьЗапросПоТабличнойЧасти(ЭтотОбъект, "Товары", СтруктураПолей);
	ТаблицаПоТоварам = РезультатЗапросаПоТоварам.Выгрузить();

	СтруктураПолей = Новый Структура;
	СтруктураПолей.Вставить("Контрагент", "Контрагент");
	СтруктураПолей.Вставить("Получатель", "Получатель");

	РезультатЗапросаПоПолучателям = УправлениеЗапасами.СформироватьЗапросПоТабличнойЧасти(ЭтотОбъект, "ПолучателиСкидки", СтруктураПолей);
	ТаблицаПоПолучателям = РезультатЗапросаПоПолучателям.Выгрузить();

	СтруктураПолей = Новый Структура;
	СтруктураПолей.Вставить("Выбран"        , "Выбран");
	СтруктураПолей.Вставить("ДеньНедели"    , "ДеньНедели");
	СтруктураПолей.Вставить("ВремяНачала"   , "ВремяНачала");
	СтруктураПолей.Вставить("ВремяОкончания", "ВремяОкончания");

	РезультатЗапросаПоДнямНедели = УправлениеЗапасами.СформироватьЗапросПоТабличнойЧасти(ЭтотОбъект, "ВремяПоДнямНедели", СтруктураПолей);
	ТаблицаПоДнямНедели = РезультатЗапросаПоДнямНедели.Выгрузить();

	СтруктураПолей = Новый Структура;
	СтруктураПолей.Вставить("ЦеноваяГруппа"           , "ЦеноваяГруппа");
	СтруктураПолей.Вставить("Качество"                , "Качество");
	СтруктураПолей.Вставить("ПроцентСкидкиНаценки"    , "ПроцентСкидкиНаценки");
	СтруктураПолей.Вставить("ОграничениеСкидкиНаценки", "ОграничениеСкидкиНаценки");

	РезультатЗапросаПоЦеновымГруппам = УправлениеЗапасами.СформироватьЗапросПоТабличнойЧасти(ЭтотОбъект, "ЦеновыеГруппы", СтруктураПолей);
	ТаблицаПоЦеновымГруппам = РезультатЗапросаПоЦеновымГруппам.Выгрузить();
	
	СтруктураПолей = Новый Структура;
	СтруктураПолей.Вставить("Номенклатура"                 , "Номенклатура");
	СтруктураПолей.Вставить("ХарактеристикаНоменклатуры"   , "ХарактеристикаНоменклатуры");
	СтруктураПолей.Вставить("Качество"                     , "Качество");
	СтруктураПолей.Вставить("СпецПредложение"              , "СпецПредложение");
	СтруктураПолей.Вставить("ХарактеристикаСпецПредложения", "ХарактеристикаСпецПредложения");
	СтруктураПолей.Вставить("КлючСтроки"                   , "КлючСтроки");
	СтруктураПолей.Вставить("Количество"                   , "Количество");
	СтруктураПолей.Вставить("ЕдиницаИзмерения"             , "ЕдиницаИзмерения");

	РезультатЗапросаПоБонусам = УправлениеЗапасами.СформироватьЗапросПоТабличнойЧасти(ЭтотОбъект, "Бонусы", СтруктураПолей);
	ТаблицаПоБонусам = РезультатЗапросаПоБонусам.Выгрузить();

	Если ВидОперации = Перечисления.ВидыОперацийУстановкаСкидокНоменклатуры.ПоНоменклатуре Тогда
		ПроверитьЗаполнениеТабличнойЧастиТовары(ТаблицаПоТоварам, СтруктураШапкиДокумента, Отказ, Заголовок);
	ИначеЕсли ВидОперации = Перечисления.ВидыОперацийУстановкаСкидокНоменклатуры.ПоЦеновымГруппам Тогда
		ПроверитьЗаполнениеТабличнойЧастиЦеновыеГруппы(СтруктураШапкиДокумента, Отказ, Заголовок);
	ИначеЕсли ВидОперации = Перечисления.ВидыОперацийУстановкаСкидокНоменклатуры.НатуральныеСкидки Тогда
		ПроверитьЗаполнениеТабличнойЧастиБонусы(СтруктураШапкиДокумента, Отказ, Заголовок);
		ПроверитьЗаполнениеТабличнойЧастиСоставНабора(СтруктураШапкиДокумента, Отказ, Заголовок);
	КонецЕсли;

	Если Не Отказ Тогда
		ДвиженияПоРегистрам(СтруктураШапкиДокумента, ТаблицаПоТоварам, ТаблицаПоПолучателям, ТаблицаПоДнямНедели, ТаблицаПоЦеновымГруппам, ТаблицаПоБонусам, Отказ, Заголовок);
	КонецЕсли;

КонецПроцедуры // ОбработкаПроведения()

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	Если ОбменДанными.Загрузка  Тогда
		Возврат;
	КонецЕсли;


	 
	мУдалятьДвижения = НЕ ЭтоНовый();
	
КонецПроцедуры // ПередЗаписью

Процедура ПриЗаписи(Отказ)
	Если ОбменДанными.Загрузка  Тогда
		Возврат;
	КонецЕсли;
КонецПроцедуры