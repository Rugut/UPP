Перем мУдалятьДвижения;


Перем мВалютаРегламентированногоУчета Экспорт;

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОКУМЕНТА

// Заполняет ТЧ Товары и Услуги по расчетному документу
//
Процедура ЗаполнитьПоРасчетномуДокументу(РежимДобавления) Экспорт

	Перем ВидыЦенностейПоСчетамУчета;
	
	Если НЕ ЗначениеЗаполнено(РасчетныйДокумент) тогда
		Возврат;
	КонецЕсли;

	Если ТоварыИУслуги.Количество() > 0 И Не РежимДобавления Тогда

		#Если Клиент Тогда
		ТекстВопроса = "Перед заполнением табличная часть будет очищена. Заполнить?";
		Ответ        = Вопрос(ТекстВопроса, РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Да, Метаданные().Представление());

		Если Ответ = КодВозвратаДиалога.Нет Тогда
			Возврат;
		КонецЕсли; 
		#КонецЕсли

		ТоварыИУслуги.Очистить();

	КонецЕсли;
	
	ТаблицаДокумента = УчетНДС.ПолучитьТаблицуДокументаНДС(РасчетныйДокумент, , Истина);
	Если ТаблицаДокумента = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ТаблицаДокумента.Колонки.Найти("СуммаБезНДСВал") <> Неопределено Тогда
		Если ТаблицаДокумента.Колонки.Найти("Сумма") <> Неопределено Тогда
			ТаблицаДокумента.Колонки.Удалить("Сумма");
		КонецЕсли;
		ТаблицаДокумента.Колонки.СуммаБезНДСВал.Имя = "Сумма";
		Если ТаблицаДокумента.Колонки.Найти("СуммаНДС") = Неопределено Тогда
			Если ТаблицаДокумента.Колонки.Найти("НДСВал") <> Неопределено Тогда
				ТаблицаДокумента.Колонки.НДСВал.Имя = "СуммаНДС";
			Иначе
				ТаблицаДокумента.Колонки.НДС.Имя = "СуммаНДС";
			КонецЕсли;
		КонецЕсли;
	ИначеЕсли ТаблицаДокумента.Колонки.Найти("СуммаБезНДС") <> Неопределено Тогда
		Если ТаблицаДокумента.Колонки.Найти("Сумма") <> Неопределено Тогда
			ТаблицаДокумента.Колонки.Удалить("Сумма");
		КонецЕсли;
		ТаблицаДокумента.Колонки.СуммаБезНДС.Имя = "Сумма";
		Если ТаблицаДокумента.Колонки.Найти("СуммаНДС") = Неопределено Тогда
			ТаблицаДокумента.Колонки.НДС.Имя = "СуммаНДС";
		КонецЕсли;
	Иначе
		Если ТаблицаДокумента.Колонки.Найти("СуммаНДС") = Неопределено Тогда
			ТаблицаДокумента.Колонки.НДС.Имя = "СуммаНДС";
		КонецЕсли;
	КонецЕсли;	
	
	ОбщегоНазначения.ЗагрузитьВТаблицуЗначений(ТаблицаДокумента, ТоварыИУслуги);
	ТоварыИУслуги.Свернуть("ВидЦенности, Номенклатура, СчетУчетаБУ, СчетДоходовБУ, СубконтоБУ, СчетУчетаНДСПоРеализации, СтавкаНДС, 
							|Коэффициент, Событие", "Количество, Цена, Сумма, СуммаНДС");
							
	ПересчитыватьЗаполненнуюЦену = Не (РасчетныйДокумент.Метаданные().Реквизиты.Найти("СуммаВключаетНДС") <> Неопределено
			И РасчетныйДокумент.СуммаВключаетНДС = СуммаВключаетНДС);
	
	Для Каждого СтрокаДокумента Из ТоварыИУслуги Цикл
		
		Если СуммаВключаетНДС Тогда
			СтрокаДокумента.Сумма = СтрокаДокумента.Сумма + СтрокаДокумента.СуммаНДС;
		КонецЕсли;
		Если (СтрокаДокумента.Цена = 0 или ПересчитыватьЗаполненнуюЦену) 
			И СтрокаДокумента.Сумма <> 0 
			Тогда
			Если СтрокаДокумента.Количество = 0 Тогда
				СтрокаДокумента.Количество = 1;
			КонецЕсли;
			СтрокаДокумента.Цена = СтрокаДокумента.Сумма / СтрокаДокумента.Количество;
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(СтрокаДокумента.ВидЦенности) Тогда
			СтрокаДокумента.ВидЦенности = УчетНДС.ОпределитьВидЦенности(СтрокаДокумента.Номенклатура, СтрокаДокумента.СчетУчетаБУ);
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(СтрокаДокумента.Событие) Тогда
			Если СтрокаДокумента.ВидЦенности = Перечисления.ВидыЦенностей.НалоговыйАгентАренда
					Или СтрокаДокумента.ВидЦенности = Перечисления.ВидыЦенностей.НалоговыйАгентИностранцы
					Или СтрокаДокумента.ВидЦенности = Перечисления.ВидыЦенностей.НалоговыйАгентРеализацияИмущества
					Или СтрокаДокумента.ВидЦенности = Перечисления.ВидыЦенностей.НалоговыйАгентКомитент Тогда
				СтрокаДокумента.Событие	= Перечисления.СобытияПоНДСПродажи.НДСНачисленКУплате;
			Иначе
				СтрокаДокумента.Событие = Перечисления.СобытияПоНДСПродажи.Реализация;
			КонецЕсли;
		КонецЕсли;
		
	КонецЦикла;    	

КонецПроцедуры // ЗаполнитьПоРасчетномуДокументу()

// Заполняет счета БУ и НУ в строке табличной части
//
Процедура ЗаполнитьСчетаУчетаВСтрокеТабЧастиРегл(СтрокаТЧ, ИмяТабЧасти, ЗаполнятьБУ, ЗаполнятьНУ=Ложь) Экспорт
	
	ЗаполнитьСчетаУчетаВТабЧасти(СтрокаТЧ, ИмяТабЧасти, ЗаполнятьБУ, Ложь);
	
КонецПроцедуры // ЗаполнитьСчетаУчетаВСтрокеТабЧасти()

// Заполняет счета БУ и НУ в табличной части документа
//
Процедура ЗаполнитьСчетаУчетаВТабЧасти(ТабличнаяЧасть, ИмяТабЧасти, ЗаполнятьБУ, ЗаполнятьНУ=Ложь) Экспорт
	
	//Заполняем счета учета вне зависимости от режима "Определять счета при проведении документов"
	СчетаУчетаВДокументах.ЗаполнитьСчетаУчетаТабличнойЧасти(ИмяТабЧасти, ТабличнаяЧасть, ЭтотОбъект, ЗаполнятьБУ, Ложь,,,Истина);
	
КонецПроцедуры // ЗаполнитьСчетаУчетаВТабЧасти()

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ДЛЯ ОБЕСПЕЧЕНИЯ ПРОВЕДЕНИЯ ДОКУМЕНТА

Процедура ПроверитьЗаполнениеШапки(СтруктураШапкиДокумента, Отказ, Заголовок)

	СтруктураОбязательныхПолей = Новый Структура("Организация");
	Если Не (СтруктураШапкиДокумента.ИспользоватьДокументРасчетовКакСчетФактуру И УчетНДС.ДляСчетаФактурыНеТребуетсяКонтрагент(СтруктураШапкиДокумента.РасчетныйДокумент)) Тогда 
		СтруктураОбязательныхПолей.Вставить("Контрагент");
		СтруктураОбязательныхПолей.Вставить("ДоговорКонтрагента");
	КонецЕсли;
	
	Если СтруктураШапкиДокумента.ЗаписьДополнительногоЛиста Тогда
		СтруктураОбязательныхПолей.Вставить("КорректируемыйПериод");
	КонецЕсли;

	// Теперь позовем общую процедуру проверки.
	ЗаполнениеДокументов.ПроверитьЗаполнениеШапкиДокумента(ЭтотОбъект, СтруктураОбязательныхПолей, Отказ, Заголовок);

КонецПроцедуры // ПроверитьЗаполнениеШапки()

// Дополняет структуру шапки документа значениями, требуемыми для проведения
//
Процедура ДополнитьСтруктуруШапкиДокумента(СтруктураШапкиДокумента, Отказ)
	
	СтруктураШапкиДокумента.Вставить("ОтражатьВНалоговомУчете", Ложь);
	СтруктураШапкиДокумента.Вставить("ОтражатьВНалоговомУчетеУСН", Ложь);
	СтруктураПолейУчетнойПолитикиНУ = Новый Структура("СложныйУчетНДС, МоментОпределенияНалоговойБазыНДС");
	ОбщегоНазначения.ДополнитьПоложениямиУчетнойПолитики(СтруктураШапкиДокумента, СтруктураШапкиДокумента.Дата, Отказ, СтруктураШапкиДокумента.Организация, "Нал", СтруктураПолейУчетнойПолитикиНУ);
	
КонецПроцедуры

// Выгружает результат запроса в табличную часть, добавляет ей необходимые колонки для проведения.
//
// Параметры: 
//  РезультатЗапросаПоТоварам - результат запроса по табличной части "Товары",
//  СтруктураШапкиДокумента   - выборка по результату запроса по шапке документа.
//
// Возвращаемое значение:
//  Сформированная таблица значений.
//
Функция ПодготовитьТаблицуТоваров(РезультатЗапросаПоТоварам, СтруктураШапкиДокумента)

	ТаблицаТоваров = РезультатЗапросаПоТоварам.Выгрузить();

	ТаблицаТоваров.Колонки.Добавить("Период");
	ТаблицаТоваров.Колонки.Добавить("Активность");
	ТаблицаТоваров.Колонки.Добавить("Организация");
	ТаблицаТоваров.Колонки.Добавить("Покупатель");
	ТаблицаТоваров.Колонки.Добавить("ДоговорКонтрагента");
	ТаблицаТоваров.Колонки.Добавить("СчетФактура");
	ТаблицаТоваров.Колонки.Добавить("СуммаБезНДС");
	ТаблицаТоваров.Колонки.Добавить("ЗаписьДополнительногоЛиста");
	ТаблицаТоваров.Колонки.Добавить("КорректируемыйПериод");

	Для Каждого СтрокаТаблицы Из ТаблицаТоваров Цикл
		СтрокаТаблицы.Организация = СтруктураШапкиДокумента.Организация;
		СтрокаТаблицы.Покупатель = СтруктураШапкиДокумента.Контрагент;
		СтрокаТаблицы.ДоговорКонтрагента = СтруктураШапкиДокумента.ДоговорКонтрагента;
		СтрокаТаблицы.СчетФактура = ?(СтруктураШапкиДокумента.ИспользоватьДокументРасчетовКакСчетФактуру, 
										СтруктураШапкиДокумента.РасчетныйДокумент, СтруктураШапкиДокумента.Ссылка);
		СтрокаТаблицы.ЗаписьДополнительногоЛиста = СтруктураШапкиДокумента.ЗаписьДополнительногоЛиста;
		Если СтруктураШапкиДокумента.ЗаписьДополнительногоЛиста Тогда
			СтрокаТаблицы.КорректируемыйПериод = СтруктураШапкиДокумента.КорректируемыйПериод;
		КонецЕсли;
		
		Если СтруктураШапкиДокумента.ПрямаяЗаписьВКнигу Тогда
			Если СтруктураШапкиДокумента.ВалютаДокумента <> СтруктураШапкиДокумента.ВалютаРегламентированногоУчета Тогда
				СтрокаТаблицы.Сумма = МодульВалютногоУчета.ПересчитатьИзВалютыВВалюту(СтрокаТаблицы.Сумма, СтруктураШапкиДокумента.ВалютаДокумента,
																					СтруктураШапкиДокумента.ВалютаРегламентированногоУчета,
																					СтруктураШапкиДокумента.КурсВзаиморасчетов, 1,
																					СтруктураШапкиДокумента.КратностьВзаиморасчетов, 1);
				СтрокаТаблицы.НДС = МодульВалютногоУчета.ПересчитатьИзВалютыВВалюту(СтрокаТаблицы.НДС, СтруктураШапкиДокумента.ВалютаДокумента,
																					СтруктураШапкиДокумента.ВалютаРегламентированногоУчета,
																					СтруктураШапкиДокумента.КурсВзаиморасчетов, 1,
																					СтруктураШапкиДокумента.КратностьВзаиморасчетов, 1);
			КонецЕсли;
			СтрокаТаблицы.СуммаБезНДС = СтрокаТаблицы.Сумма - ?(СтруктураШапкиДокумента.СуммаВключаетНДС, СтрокаТаблицы.НДС, 0);
		КонецЕсли;
		Если Не ЗначениеЗаполнено(СтрокаТаблицы.Событие) Тогда
			СтрокаТаблицы.Событие = Перечисления.СобытияПоНДСПродажи.Реализация;
		КонецЕсли;
	КонецЦикла;

	Если СтруктураШапкиДокумента.ПрямаяЗаписьВКнигу Тогда
		ТаблицаТоваров.Свернуть("Период, Активность, Организация, ВидЦенности, Покупатель, ДоговорКонтрагента, Событие, СчетФактура, СтавкаНДС, ЗаписьДополнительногоЛиста, КорректируемыйПериод " 
								+ ?(СтруктураШапкиДокумента.ФормироватьПроводки, ", СчетУчетаНДСПоРеализации, СубконтоБУ, СчетУчетаБУ, СчетДоходовБУ", "")
								+ ?((не ТаблицаТоваров.Найти(Истина, "ЗаписьДополнительногоЛиста") = неопределено) и СтруктураШапкиДокумента.ФормироватьСторнирующиеЗаписиДопЛистовВручную, ", СторнирующаяЗаписьДопЛиста", "")
								, "Количество, Цена, Сумма, СуммаБезНДС, НДС");
		Если Не ТаблицаТоваров.Найти(Истина, "ЗаписьДополнительногоЛиста") = Неопределено 
			И Не СтруктураШапкиДокумента.ФормироватьСторнирующиеЗаписиДопЛистовВручную 
			Тогда
			ТаблицаТоваров.Колонки.Добавить("СторнирующаяЗаписьДопЛиста", Новый ОписаниеТипов("Булево"));
		КонецЕсли;
	КонецЕсли;

	Возврат ТаблицаТоваров;

КонецФункции // ПодготовитьТаблицуТоваров()

// Проверяет правильность заполнения строк табличной части "Товары".
//
// Параметры:
// Параметры: 
//  ТаблицаПоТоварам        - таблица значений, содержащая данные для проведения и проверки ТЧ Товары
//  СтруктураШапкиДокумента - выборка из результата запроса по шапке документа,
//  Отказ                   - флаг отказа в проведении.
//  Заголовок               - строка, заголовок сообщения об ошибке проведения.
//
Процедура ПроверитьЗаполнениеТабличнойЧастиТовары(ТаблицаПоТоварам, СтруктураШапкиДокумента, Отказ, Заголовок)

	ИмяТабличнойЧасти = "ТоварыИУслуги";

	// Укажем, что надо проверить:
	СтруктураОбязательныхПолей = Новый Структура("Сумма, СтавкаНДС"+?(СтруктураШапкиДокумента.ПрямаяЗаписьВКнигу,"",", Номенклатура"));

	// Теперь вызовем общую процедуру проверки.
	ЗаполнениеДокументов.ПроверитьЗаполнениеТабличнойЧасти(ЭтотОбъект, ИмяТабличнойЧасти, СтруктураОбязательныхПолей, Отказ, Заголовок);

КонецПроцедуры // ПроверитьЗаполнениеТабличнойЧастиТовары()

Функция ПодготовитьТаблицуДокументовОплаты(РезультатЗапросаПоДокументамОплаты, СтруктураШапкиДокумента)

	ТаблицаДокументовОплаты = РезультатЗапросаПоДокументамОплаты.Выгрузить();
	ТаблицаДокументовОплаты.Колонки.Добавить("Период");
	ТаблицаДокументовОплаты.Колонки.Добавить("Активность");
	ТаблицаДокументовОплаты.Колонки.Добавить("Организация");
	ТаблицаДокументовОплаты.Колонки.Добавить("Покупатель");
	ТаблицаДокументовОплаты.Колонки.Добавить("Событие");
	ТаблицаДокументовОплаты.Колонки.Добавить("СчетФактура");
	ТаблицаДокументовОплаты.Колонки.Добавить("ЗаписьДополнительногоЛиста");
	ТаблицаДокументовОплаты.Колонки.Добавить("КорректируемыйПериод");
	ТаблицаДокументовОплаты.Колонки.Добавить("СторнирующаяЗаписьДопЛиста");
	
	ТаблицаДокументовОплаты.ЗаполнитьЗначения(СтруктураШапкиДокумента.Организация,       "Организация");
	ТаблицаДокументовОплаты.ЗаполнитьЗначения(СтруктураШапкиДокумента.Контрагент,        "Покупатель");
	ТаблицаДокументовОплаты.ЗаполнитьЗначения(?(СтруктураШапкиДокумента.ИспользоватьДокументРасчетовКакСчетФактуру, 
												СтруктураШапкиДокумента.РасчетныйДокумент, СтруктураШапкиДокумента.Ссылка), "СчетФактура");
	ТаблицаДокументовОплаты.ЗаполнитьЗначения(Перечисления.СобытияПоНДСПродажи.Реализация, "Событие");
	ТаблицаДокументовОплаты.ЗаполнитьЗначения(СтруктураШапкиДокумента.ЗаписьДополнительногоЛиста, "ЗаписьДополнительногоЛиста");
	ТаблицаДокументовОплаты.ЗаполнитьЗначения(СтруктураШапкиДокумента.КорректируемыйПериод, "КорректируемыйПериод");
	
	ТаблицаДокументовОплаты.Свернуть("Период, Активность, Организация, Покупатель, Событие, СчетФактура, ДатаОплаты, ДокументОплаты, ЗаписьДополнительногоЛиста, КорректируемыйПериод, СторнирующаяЗаписьДопЛиста");

	Возврат ТаблицаДокументовОплаты;

КонецФункции  //ПодготовитьТаблицуДокументовОплаты()

// Проверяет правильность заполнения строк табличной части "ДокументыОплаты".
//
// Параметры:
// Параметры: 
//  ТаблицаПоТоварам        - таблица значений, содержащая данные для проведения и проверки ТЧ Товары
//  СтруктураШапкиДокумента - выборка из результата запроса по шапке документа,
//  Отказ                   - флаг отказа в проведении.
//  Заголовок               - строка, заголовок сообщения об ошибке проведения.
//
Процедура ПроверитьЗаполнениеТабличнойЧастиДокументыОплаты(ТаблицаПоДокументамОплаты, СтруктураШапкиДокумента, Отказ, Заголовок)

	Если СтруктураШапкиДокумента.ПрямаяЗаписьВКнигу Тогда
		// Укажем, что надо проверить:
		СтруктураОбязательныхПолей = Новый Структура("ДатаОплаты");
		
		// Теперь вызовем общую процедуру проверки.
		ЗаполнениеДокументов.ПроверитьЗаполнениеТабличнойЧасти(ЭтотОбъект, "ДокументыОплаты", СтруктураОбязательныхПолей, Отказ, Заголовок);
	КонецЕсли;
	
КонецПроцедуры // ПроверитьЗаполнениеТабличнойЧастиТовары()

// Процедура выполняет движения по регистрам
//
Процедура ДвиженияПоРегистрам(РежимПроведения, СтруктураШапкиДокумента, ТаблицаПоТоварам, ТаблицаПоДокументамОплаты, Отказ, Заголовок)
	
	Если СтруктураШапкиДокумента.ПрямаяЗаписьВКнигу Тогда
		ТаблицаПоТоварам.Свернуть("Период, Активность, Организация, ВидЦенности, Покупатель, ДоговорКонтрагента, Событие, СчетФактура, СтавкаНДС, ЗаписьДополнительногоЛиста, КорректируемыйПериод " 
								+ ?(СтруктураШапкиДокумента.ФормироватьПроводки, ", СчетУчетаНДСПоРеализации, СубконтоБУ", "")
								+ ?((Не ТаблицаПоТоварам.Найти(Истина, "ЗаписьДополнительногоЛиста") = Неопределено) И СтруктураШапкиДокумента.ФормироватьСторнирующиеЗаписиДопЛистовВручную, ", СторнирующаяЗаписьДопЛиста", "")
								, "СуммаБезНДС, НДС");
	КонецЕсли;
	
	Если СтруктураШапкиДокумента.ПрямаяЗаписьВКнигу И СтруктураШапкиДокумента.ФормироватьПроводки Тогда
		ПроводкиБУ = Движения.Хозрасчетный;
		Для Каждого СтрокаТаблицы Из ТаблицаПоТоварам Цикл
			// Проводки по вычету в случае упрощенного учета НДС
			
			Проводка = ПроводкиБУ.Добавить();
			Проводка.Период      = СтруктураШапкиДокумента.Дата;
			Проводка.Организация = СтруктураШапкиДокумента.Организация;
			Проводка.Сумма       = СтрокаТаблицы.НДС;
			Проводка.Содержание  = "Выделен НДС";

			Проводка.СчетДт = СтрокаТаблицы.СчетУчетаНДСПоРеализации;
			БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, 1,           СтрокаТаблицы.СубконтоБУ);
			БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, "СтавкиНДС", СтрокаТаблицы.СтавкаНДС);

			Проводка.СчетКт = ПланыСчетов.Хозрасчетный.НДС;
			БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт, "ВидыПлатежейВГосБюджет", Перечисления.ВидыПлатежейВГосБюджет.Налог);

		КонецЦикла;
	КонецЕсли;
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	Если СтруктураШапкиДокумента.ПрямаяЗаписьВКнигу Тогда
		// Прямая запись в книгу покупок
		ТаблицаДвижений_НДСЗаписиКнигиПродаж = Движения.НДСЗаписиКнигиПродаж.Выгрузить();
		ОбщегоНазначения.ЗагрузитьВТаблицуЗначений(ТаблицаПоТоварам,ТаблицаДвижений_НДСЗаписиКнигиПродаж);
		ОбщегоНазначения.ЗагрузитьВТаблицуЗначений(ТаблицаПоДокументамОплаты, ТаблицаДвижений_НДСЗаписиКнигиПродаж);
		ДополнитьСторнирующимиЗаписямиДопЛистов(СтруктураШапкиДокумента, ТаблицаДвижений_НДСЗаписиКнигиПродаж, ТаблицаПоДокументамОплаты);
		
		Для Каждого СтрокаДвижения Из ТаблицаДвижений_НДСЗаписиКнигиПродаж Цикл
			Если Не (СтрокаДвижения.ВидЦенности = Перечисления.ВидыЦенностей.АвансыПолученные
					Или СтрокаДвижения.ВидЦенности = Перечисления.ВидыЦенностей.АвансыПолученные0
					Или СтрокаДвижения.ВидЦенности = Перечисления.ВидыЦенностей.НалоговыйАгентКомитент) Тогда
				СтрокаДвижения.ДоговорКонтрагента = Неопределено;
			КонецЕсли;
		КонецЦикла;
		
		Движения.НДСЗаписиКнигиПродаж.мПериод 			= СтруктураШапкиДокумента.Дата;
		Движения.НДСЗаписиКнигиПродаж.мТаблицаДвижений	= ТаблицаДвижений_НДСЗаписиКнигиПродаж;
		Движения.НДСЗаписиКнигиПродаж.ДобавитьДвижение();
	    Возврат;
		
	КонецЕсли;
	
	СчетОтнесенияНДС = ?(СтруктураШапкиДокумента.МоментОпределенияНалоговойБазыНДС = Перечисления.МоментыОпределенияНалоговойБазыНДС.ПоОплате,
						ПланыСчетов.Хозрасчетный.РасчетыПоНДСотложенномуДляУплатыВБюджет,
						ПланыСчетов.Хозрасчетный.НДС);
			
	УчетНДСФормированиеДвижений.СформироватьДвиженияПоРегиструНДСНачисленный_ОтражениеРеализации(СтруктураШапкиДокумента, ТаблицаПоТоварам,, Отказ, СчетОтнесенияНДС);
	
КонецПроцедуры // ДвиженияПоРегистрам()

Процедура ДополнитьСторнирующимиЗаписямиДопЛистов(СтруктураШапкиДокумента, ТаблицаДвижений_НДСЗаписиКнигиПродаж, ТаблицаПоДокументамОплаты)
	
	Если СтруктураШапкиДокумента.ФормироватьСторнирующиеЗаписиДопЛистовВручную Тогда
		Возврат;
	КонецЕсли;
	
	ТаблицаДвижений_НДСЗаписиКнигиПродаж.Индексы.Добавить("ЗаписьДополнительногоЛиста");
	Если ТаблицаДвижений_НДСЗаписиКнигиПродаж.Найти(Истина, "ЗаписьДополнительногоЛиста") = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ТаблицаДвижений_НДСЗаписиКнигиПродаж.Колонки.Найти("СторнирующаяЗаписьДопЛиста") = Неопределено Тогда
		ТаблицаДвижений_НДСЗаписиКнигиПродаж.Колонки.Добавить("СторнирующаяЗаписьДопЛиста", Новый ОписаниеТипов("Булево"));
	КонецЕсли; 

	СвернутаяТаблицаЗаписейДопЛиста = ТаблицаДвижений_НДСЗаписиКнигиПродаж.Скопировать();
	Для каждого ТекущаяСтрока Из СвернутаяТаблицаЗаписейДопЛиста Цикл
		// Авансы отрабатываются с учетом ставки НДС (на каждую ставку отдельный счет-фактура)
		Если НЕ (ТекущаяСтрока.ВидЦенности = Перечисления.ВидыЦенностей.АвансыПолученные
			ИЛИ ТекущаяСтрока.ВидЦенности = Перечисления.ВидыЦенностей.АвансыПолученные0) Тогда
			ТекущаяСтрока.СтавкаНДС = Неопределено;
		КонецЕсли; 
		// Авансы и налоговый агент (комитент) отрабатываются с учетом договора (на каждуй
		// договор отдельный счет-фактура или отдельное лист декларации)
		Если НЕ (ТекущаяСтрока.ВидЦенности = Перечисления.ВидыЦенностей.АвансыПолученные
			ИЛИ ТекущаяСтрока.ВидЦенности = Перечисления.ВидыЦенностей.АвансыПолученные0
			ИЛИ ТекущаяСтрока.ВидЦенности = Перечисления.ВидыЦенностей.НалоговыйАгентКомитент) Тогда
			// Авансы отрабатываются с учетом ставки НДС (на каждую ставку отдельный счет-фактура)
			ТекущаяСтрока.ДоговорКонтрагента = Неопределено;
		КонецЕсли; 
	КонецЦикла; 
	
	СвернутаяТаблицаЗаписейДопЛиста.Свернуть("СчетФактура,СтавкаНДС,ДоговорКонтрагента,ЗаписьДополнительногоЛиста,КорректируемыйПериод","СуммаБезНДС,НДС");
	
	СвернутаяТаблицаЗаписейДопЛиста.Индексы.Добавить("ЗаписьДополнительногоЛиста");
	СтрокиДопЛистов = СвернутаяТаблицаЗаписейДопЛиста.НайтиСтроки(Новый Структура("ЗаписьДополнительногоЛиста", Истина));
	
	КэшПараметровУП = Новый Соответствие;
	ПараметрыУПКорректируемогоПериода = Неопределено;
	
	Запрос = Новый Запрос();
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("СтрокиДопЛистов", СвернутаяТаблицаЗаписейДопЛиста);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СтрокиДопЛистов.СчетФактура КАК СчетФактура,
	|	СтрокиДопЛистов.КорректируемыйПериод КАК КорректируемыйПериод
	|ПОМЕСТИТЬ ВТСтрокиДопЛистов
	|ИЗ
	|	&СтрокиДопЛистов КАК СтрокиДопЛистов
	|ГДЕ
	|	СтрокиДопЛистов.ЗаписьДополнительногоЛиста = ИСТИНА
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	СчетФактура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	ВТСтрокиДопЛистов.КорректируемыйПериод
	|ИЗ
	|	ВТСтрокиДопЛистов КАК ВТСтрокиДопЛистов
	|
	|УПОРЯДОЧИТЬ ПО
	|	ВТСтрокиДопЛистов.КорректируемыйПериод";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		НДСНалоговыйПериодДополнительногоЛиста = УчетНДС.ПолучитьУПНДСНалоговыйПериод(СтруктураШапкиДокумента.Организация, Выборка.КорректируемыйПериод, КэшПараметровУП[НачалоМесяца(Выборка.КорректируемыйПериод)]);	
		НачалоНалоговогоПериодаКорректировки = ?(НДСНалоговыйПериодДополнительногоЛиста = Перечисления.Периодичность.Квартал, НачалоКвартала(Выборка.КорректируемыйПериод), НачалоМесяца(Выборка.КорректируемыйПериод));
	Иначе
		Возврат;
	КонецЕсли;	
		
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	НДСЗаписиКнигиПродажОбороты.Организация,
	|	НДСЗаписиКнигиПродажОбороты.Покупатель,
	|	НДСЗаписиКнигиПродажОбороты.СчетФактура,
	|	НДСЗаписиКнигиПродажОбороты.ВидЦенности,
	|	НДСЗаписиКнигиПродажОбороты.СтавкаНДС,
	|	НДСЗаписиКнигиПродажОбороты.ДатаОплаты,
	|	НДСЗаписиКнигиПродажОбороты.ДокументОплаты,
	|	НДСЗаписиКнигиПродажОбороты.Событие,
	|	НДСЗаписиКнигиПродажОбороты.СуммаБезНДСОборот * -1 КАК СуммаБезНДССторно,
	|	НДСЗаписиКнигиПродажОбороты.НДСОборот * -1 КАК НДССторно,
	|	НДСЗаписиКнигиПродажОбороты.СуммаБезНДСОборот КАК СуммаБезНДС,
	|	НДСЗаписиКнигиПродажОбороты.НДСОборот КАК НДС,
	|	НДСЗаписиКнигиПродажОбороты.ДоговорКонтрагента,
	|	НДСЗаписиКнигиПродажОбороты.ЗаписьДополнительногоЛиста,
	|	НДСЗаписиКнигиПродажОбороты.КорректируемыйПериод,
	|	НДСЗаписиКнигиПродажОбороты.Период
	|ИЗ
	|	РегистрНакопления.НДСЗаписиКнигиПродаж.Обороты(
	|			&НачалоПериода,
	|			&КонецПериодаГраница,
	|			Квартал,
	|			СчетФактура В
	|				(ВЫБРАТЬ
	|					ВТСтрокиДопЛистов.СчетФактура
	|				ИЗ
	|					ВТСтрокиДопЛистов КАК ВТСтрокиДопЛистов)) КАК НДСЗаписиКнигиПродажОбороты";
	
	Запрос.УстановитьПараметр("НачалоПериода", НачалоНалоговогоПериодаКорректировки);
	Запрос.УстановитьПараметр("КонецПериодаГраница", Новый Граница(КонецДня(СтруктураШапкиДокумента.Дата), ВидГраницы.Включая));
		
	ВыборкаДляПоиска = Запрос.Выполнить().Выбрать();
	
	ОтработанныеСФпоПериодам = Новый ТаблицаЗначений();
	ОтработанныеСФпоПериодам.Колонки.Добавить("СчетФактура");
	ОтработанныеСФпоПериодам.Колонки.Добавить("НалоговыйПериод");
	ОтработанныеСФпоПериодам.Колонки.Добавить("СтавкаНДС");
	ОтработанныеСФпоПериодам.Колонки.Добавить("ДоговорКонтрагента");
	ОтработанныеСФпоПериодам.Индексы.Добавить("СчетФактура,НалоговыйПериод");
		
	Для каждого СтрокаДопЛиста Из СтрокиДопЛистов Цикл
		
		НДСНалоговыйПериодДополнительногоЛиста = УчетНДС.ПолучитьУПНДСНалоговыйПериод(СтруктураШапкиДокумента.Организация, СтрокаДопЛиста.КорректируемыйПериод, КэшПараметровУП[НачалоМесяца(СтрокаДопЛиста.КорректируемыйПериод)]);
		НачалоНалоговогоПериодаКорректировки = ?(НДСНалоговыйПериодДополнительногоЛиста = Перечисления.Периодичность.Квартал, НачалоКвартала(СтрокаДопЛиста.КорректируемыйПериод),НачалоМесяца(СтрокаДопЛиста.КорректируемыйПериод));
		КонецНалоговогоПериодаКорректировки = ?(НДСНалоговыйПериодДополнительногоЛиста = Перечисления.Периодичность.Квартал, КонецКвартала(СтрокаДопЛиста.КорректируемыйПериод), КонецМесяца(СтрокаДопЛиста.КорректируемыйПериод));
		
		ОтборОтработанныхЗаписей = Новый Структура("СчетФактура, НалоговыйПериод", СтрокаДопЛиста.СчетФактура, НачалоНалоговогоПериодаКорректировки);
		Если ЗначениеЗаполнено(СтрокаДопЛиста.СтавкаНДС) Тогда
			ОтборОтработанныхЗаписей.Вставить("СтавкаНДС", СтрокаДопЛиста.СтавкаНДС);
		КонецЕсли; 
		Если ЗначениеЗаполнено(СтрокаДопЛиста.ДоговорКонтрагента) Тогда
			ОтборОтработанныхЗаписей.Вставить("ДоговорКонтрагента", СтрокаДопЛиста.ДоговорКонтрагента);
		КонецЕсли; 
				
		Если НЕ (ОтработанныеСФпоПериодам.НайтиСтроки(ОтборОтработанныхЗаписей).Количество() = 0) Тогда
			// СФ по данному налоговому периоду уже отработан
			Продолжить;
		Иначе
			НоваяЗапись = ОтработанныеСФпоПериодам.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяЗапись, ОтборОтработанныхЗаписей);
		КонецЕсли; 
				
		
		// Структура поиска для отбора записей
		СтруктураПоиска = Новый Структура();
		СтруктураПоиска.Вставить("СчетФактура", СтрокаДопЛиста.СчетФактура);
		Если ЗначениеЗаполнено(СтрокаДопЛиста.СтавкаНДС) Тогда
			СтруктураПоиска.Вставить("СтавкаНДС", СтрокаДопЛиста.СтавкаНДС);
		КонецЕсли;
		Если ЗначениеЗаполнено(СтрокаДопЛиста.ДоговорКонтрагента) Тогда
			СтруктураПоиска.Вставить("ДоговорКонтрагента", СтрокаДопЛиста.ДоговорКонтрагента);
		КонецЕсли;
		
		Пока ВыборкаДляПоиска.НайтиСледующий(СтруктураПоиска) Цикл
			Если (ВыборкаДляПоиска.ЗаписьДополнительногоЛиста 
				И ВыборкаДляПоиска.КорректируемыйПериод >= НачалоНалоговогоПериодаКорректировки 
				И ВыборкаДляПоиска.КорректируемыйПериод <= КонецНалоговогоПериодаКорректировки)
				ИЛИ (НЕ ВыборкаДляПоиска.ЗаписьДополнительногоЛиста
			    И ВыборкаДляПоиска.Период = НачалоНалоговогоПериодаКорректировки) Тогда
				
				СтрокаТаблицыДвижений = ТаблицаДвижений_НДСЗаписиКнигиПродаж.Добавить();
				
				ЗаполнитьЗначенияСвойств(СтрокаТаблицыДвижений, ВыборкаДляПоиска);
				
				СтрокаТаблицыДвижений.ЗаписьДополнительногоЛиста = Истина;
				СтрокаТаблицыДвижений.СторнирующаяЗаписьДопЛиста = Истина;
								
				СтрокаТаблицыДвижений.СуммаБезНДС = ВыборкаДляПоиска.СуммаБезНДССторно; 
				СтрокаТаблицыДвижений.НДС         = ВыборкаДляПоиска.НДССторно;
				
				СтрокаТаблицыДвижений.КорректируемыйПериод = СтрокаДопЛиста.КорректируемыйПериод;
				СтрокаТаблицыДвижений.ДатаСобытия 		   = СтруктураШапкиДокумента.Дата;
																				
				СтрокаТаблицыДвижений = ТаблицаДвижений_НДСЗаписиКнигиПродаж.Добавить();
				
				ЗаполнитьЗначенияСвойств(СтрокаТаблицыДвижений, ВыборкаДляПоиска);
									
				СтрокаТаблицыДвижений.ЗаписьДополнительногоЛиста = Истина;
				СтрокаТаблицыДвижений.СторнирующаяЗаписьДопЛиста = Ложь;
				
				СтрокаТаблицыДвижений.СуммаБезНДС = ВыборкаДляПоиска.СуммаБезНДС; 
				СтрокаТаблицыДвижений.НДС         = ВыборкаДляПоиска.НДС;
				
				СтрокаТаблицыДвижений.КорректируемыйПериод = СтрокаДопЛиста.КорректируемыйПериод;
				СтрокаТаблицыДвижений.ДатаСобытия 		   = СтруктураШапкиДокумента.Дата;
											
			КонецЕсли;	 
		КонецЦикла;	
	КонецЦикла;
	
	Если ТаблицаДвижений_НДСЗаписиКнигиПродаж.Найти(Истина, "СторнирующаяЗаписьДопЛиста") <> Неопределено Тогда
		КоличествоСтрок = ТаблицаПоДокументамОплаты.Количество();
		Для К = 0 По КоличествоСтрок - 1 Цикл
			НоваяСтрока = ТаблицаПоДокументамОплаты.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, ТаблицаПоДокументамОплаты[К]);
			НоваяСтрока.СторнирующаяЗаписьДопЛиста = Истина;
		КонецЦикла;
	КонецЕсли;
    	
КонецПроцедуры

// Процедура формирует структуру шапки документа и дополнительных полей.
//
Процедура ПодготовитьСтруктуруШапкиДокумента(Заголовок, СтруктураШапкиДокумента, Отказ) Экспорт
	
	СтруктураШапкиДокумента = ОбщегоНазначения.СформироватьСтруктуруШапкиДокумента(ЭтотОбъект);
	
	ДеревоПолейЗапросаПоШапке = УправлениеЗапасами.СформироватьДеревоПолейЗапросаПоШапке();
	УправлениеЗапасами.ДобавитьСтрокуВДеревоПолейЗапросаПоШапке(ДеревоПолейЗапросаПоШапке, "ДоговорКонтрагента"  , "Организация", "ДоговорОрганизация");
	УправлениеЗапасами.ДобавитьСтрокуВДеревоПолейЗапросаПоШапке(ДеревоПолейЗапросаПоШапке, "ДоговорКонтрагента"  , "ВидДоговора", "ВидДоговора");
	УправлениеЗапасами.ДобавитьСтрокуВДеревоПолейЗапросаПоШапке(ДеревоПолейЗапросаПоШапке, "ТипЦен"              , "ЦенаВключаетНДС"      , "ЦенаВключаетНДС");
	
	// Сформируем запрос на дополнительные параметры, нужные при проведении, по данным шапки документа
	СтруктураШапкиДокумента = УправлениеЗапасами.СформироватьЗапросПоДеревуПолей(ЭтотОбъект, ДеревоПолейЗапросаПоШапке, СтруктураШапкиДокумента, мВалютаРегламентированногоУчета);
	
 	ДополнитьСтруктуруШапкиДокумента(СтруктураШапкиДокумента, Отказ);
	
КонецПроцедуры

Процедура ПодготовитьТаблицыДокумента(СтруктураШапкиДокумента, ТаблицаПоТоварам, ТаблицаПоДокументамОплаты) Экспорт
	
	СтруктураПолей = Новый Структура();
	СтруктураПолей.Вставить("Номенклатура"	, "Номенклатура");
	СтруктураПолей.Вставить("ВидЦенности"	, "ВидЦенности");
	СтруктураПолей.Вставить("Количество"	, "Количество");
	СтруктураПолей.Вставить("Цена"			, "Цена");
	СтруктураПолей.Вставить("Сумма"			, "Сумма");
	СтруктураПолей.Вставить("СтавкаНДС"		, "СтавкаНДС");
	СтруктураПолей.Вставить("НДС"			, "СуммаНДС");
	СтруктураПолей.Вставить("СчетУчетаБУ"	, "СчетУчетаБУ");
	СтруктураПолей.Вставить("СчетДоходовБУ" , "СчетДоходовБУ");
	СтруктураПолей.Вставить("СчетУчетаНДСПоРеализации"	, "СчетУчетаНДСПоРеализации");
	СтруктураПолей.Вставить("СубконтоБУ"	, "СубконтоБУ");
	СтруктураПолей.Вставить("СторнирующаяЗаписьДопЛиста", "СторнирующаяЗаписьДопЛиста");
	СтруктураПолей.Вставить("Событие", 		"Событие");

	РезультатЗапросаПоТоварам = УправлениеЗапасами.СформироватьЗапросПоТабличнойЧасти(ЭтотОбъект, "ТоварыИУслуги", СтруктураПолей);

	// Подготовим таблицу товаров для проведения.
	ТаблицаПоТоварам = ПодготовитьТаблицуТоваров(РезультатЗапросаПоТоварам, СтруктураШапкиДокумента);

	Если Не СтруктураШапкиДокумента.ПрямаяЗаписьВКнигу Тогда
		БухгалтерскийУчетРасчетовСКонтрагентами.ПодготовкаТаблицыЗначенийДляЦелейПриобретенияИРеализации(ТаблицаПоТоварам, СтруктураШапкиДокумента, Ложь, мВалютаРегламентированногоУчета);
	КонецЕсли;
		
	ТаблицаПоДокументамОплаты = Неопределено;
	Если СтруктураШапкиДокумента.ПрямаяЗаписьВКнигу Тогда
		СтруктураПолей.Очистить();
		СтруктураПолей.Вставить("ДокументОплаты", 	"ДокументОплаты");
		СтруктураПолей.Вставить("ДатаОплаты", 		"ДатаОплаты");
		РезультатЗапросаПоДокументамОплаты = УправлениеЗапасами.СформироватьЗапросПоТабличнойЧасти(ЭтотОбъект, "ДокументыОплаты", СтруктураПолей);
		ТаблицаПоДокументамОплаты = ПодготовитьТаблицуДокументовОплаты(РезультатЗапросаПоДокументамОплаты, СтруктураШапкиДокумента);
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

// Процедура вызывается перед записью документа 
//
Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка  Тогда
		Возврат;
	КонецЕсли;

	// Посчитать суммы документа и записать ее в соответствующий реквизит шапки для показа в журналах
	СуммаДокумента = ТоварыИУслуги.Итог("Сумма") + ?(Не СуммаВключаетНДС, ТоварыИУслуги.Итог("СуммаНДС"), 0);
	
	Если НЕ ЗначениеЗаполнено(ВалютаДокумента) Тогда
		ВалютаДокумента = мВалютаРегламентированногоУчета;
	КонецЕсли; 

	Если Не ИспользоватьДокументРасчетовКакСчетФактуру Тогда
		
		УчетНДС.СинхронизацияПометкиНаУдалениеУСчетаФактуры(ЭтотОбъект);
		
	КонецЕсли;
	
	Если Не ПрямаяЗаписьВКнигу И ФормироватьПроводки Тогда
		ФормироватьПроводки = Ложь;
	КонецЕсли;

	мУдалятьДвижения = Не ЭтоНовый();
	
КонецПроцедуры // ПередЗаписью

Процедура ОбработкаЗаполнения(Основание)
	
	Если Не (ЗначениеЗаполнено(Основание) И ЭтотОбъект.Метаданные().Реквизиты.РасчетныйДокумент.Тип.СодержитТип(ТипЗнч(Основание))) Тогда
		Возврат;
	КонецЕсли;
	
	// Заполним реквизиты из стандартного набора.
	ЗаполнениеДокументов.ЗаполнитьШапкуДокументаПоОснованию(ЭтотОбъект, Основание);
	
	РасчетныйДокумент = Основание;

	ИспользоватьДокументРасчетовКакСчетФактуру = Истина;
	
	ЗаполнитьПоРасчетномуДокументу(Ложь);
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)

	Перем СтруктураШапкиДокумента, ТаблицаПоТоварам, ТаблицаПоДокументамОплаты;
	
	Если мУдалятьДвижения Тогда
		ОбщегоНазначения.УдалитьДвиженияРегистратора(ЭтотОбъект, Отказ);
	КонецЕсли;

	// Заголовок для сообщений об ошибках проведения.
	Заголовок = "Проведение документа """ + СокрЛП(Ссылка) + """: ";

	ДокументСоздан_НО_НДС = (ЗначениеЗаполнено(РасчетныйДокумент)) и (ТипЗнч(РасчетныйДокумент) = Тип("ДокументСсылка.ВводНачальныхОстатковНДС"));

	Если ДокументСоздан_НО_НДС Тогда
		// Проверка и дополнительная обработка не требуются
		Возврат;
	КонецЕсли; 
	
	ПодготовитьСтруктуруШапкиДокумента(Заголовок, СтруктураШапкиДокумента, Отказ);
	
	// Проверим правильность заполнения шапки документа
	ПроверитьЗаполнениеШапки(СтруктураШапкиДокумента, Отказ, Заголовок);

	ПодготовитьТаблицыДокумента(СтруктураШапкиДокумента, ТаблицаПоТоварам, ТаблицаПоДокументамОплаты);
	
	ПроверитьЗаполнениеТабличнойЧастиТовары(ТаблицаПоТоварам, СтруктураШапкиДокумента, Отказ, Заголовок);
	
	Если СтруктураШапкиДокумента.ПрямаяЗаписьВКнигу Тогда
		ПроверитьЗаполнениеТабличнойЧастиДокументыОплаты(ТаблицаПоТоварам, СтруктураШапкиДокумента, Отказ, Заголовок);
	КонецЕсли;
	
	Если Не Отказ Тогда
		
		ДвиженияПоРегистрам(РежимПроведения, СтруктураШапкиДокумента, ТаблицаПоТоварам, ТаблицаПоДокументамОплаты, Отказ, Заголовок);
		УчетНДС.СинхронизацияПроведенияУСчетаФактуры(ЭтотОбъект, "СчетФактураВыданный", Отказ)
		
	КонецЕсли;

КонецПроцедуры // ОбработкаПроведения()


Процедура ОбработкаУдаленияПроведения(Отказ)

	УчетНДС.СинхронизацияПроведенияУСчетаФактуры(ЭтотОбъект, "СчетФактураВыданный", Отказ);
КонецПроцедуры // ОбработкаУдаленияПроведения

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка  Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ИспользоватьДокументРасчетовКакСчетФактуру Тогда
		
		УчетНДС.ПроверитьСоответствиеРеквизитовСчетаФактуры(ЭтотОбъект);
		
	КонецЕсли;
															
КонецПроцедуры

мВалютаРегламентированногоУчета = глЗначениеПеременной("ВалютаРегламентированногоУчета");