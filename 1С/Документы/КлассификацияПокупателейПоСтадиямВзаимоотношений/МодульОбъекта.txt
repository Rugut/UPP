Перем мУдалятьДвижения;

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОКУМЕНТА

#Если Клиент Тогда
// Процедура осуществляет печать документа. Можно направить печать на 
// экран или принтер, а также распечатать необходимое количество копий.
//
//  Название макета печати передается в качестве параметра,
// по переданному названию находим имя макета в соответствии.
//
// Параметры:
//  НазваниеМакета - строка, название макета.
//
Процедура Печать(ИмяМакета, КоличествоЭкземпляров = 1, НаПринтер = Ложь) Экспорт

	Если ЭтоНовый() Тогда
		Предупреждение("Документ можно распечатать только после его записи");
		Возврат;
	ИначеЕсли Не УправлениеДопПравамиПользователей.РазрешитьПечатьНепроведенныхДокументов(Проведен) Тогда
		Предупреждение("Недостаточно полномочий для печати непроведенного документа!");
		Возврат;
	КонецЕсли;

	Если Не РаботаСДиалогами.ПроверитьМодифицированность(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;

	Если ТипЗнч(ИмяМакета) = Тип("ДвоичныеДанные") Тогда

		ТабДокумент = УниверсальныеМеханизмы.НапечататьВнешнююФорму(Ссылка, ИмяМакета);
		
		Если ТабДокумент = Неопределено Тогда
			Возврат
		КонецЕсли; 
		
	КонецЕсли;

	УниверсальныеМеханизмы.НапечататьДокумент(ТабДокумент, КоличествоЭкземпляров, НаПринтер, ОбщегоНазначения.СформироватьЗаголовокДокумента(ЭтотОбъект), Ссылка);

КонецПроцедуры // Печать

#КонецЕсли

// Возвращает доступные варианты печати документа
//
// Возвращаемое значение:
//  Структура, каждая строка которой соответствует одному из вариантов печати
//  
Функция ПолучитьСтруктуруПечатныхФорм() Экспорт
	
	Возврат Новый Структура;

КонецФункции // ПолучитьСтруктуруПечатныхФорм()



////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ДЛЯ ОБЕСПЕЧЕНИЯ ПРОВЕДЕНИЯ ДОКУМЕНТА

// Процедура проверяет наличие строк с одинаковыми контрагентами в табличной части
//
// Параметры
//  Отказ - булево
//
// Возвращаемое значение:
//   Строка, список наименований повторяющихся контрагентов
//
Процедура ПроверитьТабличнуюЧасть(Отказ)

	Запрос = Новый Запрос;
	
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	ТаблицаКонтрагентов.НомерСтроки            КАК НомерСтроки,
	|	ТаблицаКонтрагентов.Контрагент             КАК Контрагент,
	|	1                                          КАК Количество,
	|	ВЫБОР КОГДА ТаблицаКонтрагентов.СтадияВзаимоотношений = &ПустаяСтадия И ТаблицаКонтрагентов.XYZКлассификация = &ПустойКласс ТОГДА
	|		ИСТИНА
	|	ИНАЧЕ
	|		ЛОЖЬ
	|	КОНЕЦ КАК ПустыеЗначения
	|
	|ИЗ
	|	Документ.КлассификацияПокупателейПоСтадиямВзаимоотношений.ТаблицаРаспределенияКонтрагентов КАК ТаблицаКонтрагентов
	|
	|ГДЕ
	|	ТаблицаКонтрагентов.Ссылка = &ТекущийДокумент
	|
	|";
	
	Запрос.УстановитьПараметр("ТекущийДокумент", Ссылка);
	Запрос.УстановитьПараметр("ПустаяСтадия", Перечисления.СтадииВзаимоотношенийСПокупателями.ПустаяСсылка());
	Запрос.УстановитьПараметр("ПустойКласс", Перечисления.XYZКлассификация.ПустаяСсылка());
	
	ТаблицаЗапроса = Запрос.Выполнить().Выгрузить();
	
	СтрокаОшибок = "";
	
	СтрокиСПустымиЗначениями = ТаблицаЗапроса.НайтиСтроки(Новый Структура("ПустыеЗначения", Истина));
	Если СтрокиСПустымиЗначениями.Количество() > 0 Тогда
		Отказ = Истина;
		Для каждого СтрокаПустыхЗначений Из СтрокиСПустымиЗначениями Цикл
			СтрокаОшибок = СтрокаОшибок + ?(НЕ ПустаяСтрока(СтрокаОшибок), Символы.ПС, "") + "В строке " + СтрокаПустыхЗначений.НомерСтроки + " не указаны стадия и класс контрагента!";
		КонецЦикла;
	КонецЕсли;
	
	ТаблицаЗапроса.Свернуть("Контрагент", "Количество");
	СписокКонтрагентов = "";
	Для каждого СтрокаТаблицы Из ТаблицаЗапроса Цикл
		Если СтрокаТаблицы.Количество > 1 Тогда
			СписокКонтрагентов = СписокКонтрагентов + Символы.ПС + СтрокаТаблицы.Контрагент;
		КонецЕсли; 
	КонецЦикла;
	
	Если НЕ ПустаяСтрока(СписокКонтрагентов) Тогда
		Отказ = Истина;
		СтрокаОшибок = СтрокаОшибок + "Найдены строки с повторяющимися контрагентами!" + СписокКонтрагентов;
	КонецЕсли; 
	
	Если НЕ ПустаяСтрока(СтрокаОшибок) Тогда
		ОбщегоНазначения.СообщитьОбОшибке(СтрокаОшибок, Отказ);
	КонецЕсли; 
	
КонецПроцедуры // ПроверитьТабличнуюЧасть()

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

// Процедура - обработчик события "ОбработкаПроведения".
//
Процедура ОбработкаПроведения(Отказ, Режим)
	
	
	Если мУдалятьДвижения Тогда
		ОбщегоНазначения.УдалитьДвиженияРегистратора(ЭтотОбъект, Отказ);
	КонецЕсли;

	ПроверитьТабличнуюЧасть(Отказ);
	
	Для Каждого ТекСтрокаТаблицаРаспределенияКонтрагентов Из ТаблицаРаспределенияКонтрагентов Цикл
		Движение = Движения.СтадииВзаимоотношенийСПокупателями.Добавить();
		Движение.Период                     = Дата;
		Движение.Контрагент                 = ТекСтрокаТаблицаРаспределенияКонтрагентов.Контрагент;
		Движение.Стадия                     = ТекСтрокаТаблицаРаспределенияКонтрагентов.СтадияВзаимоотношений;
		Движение.КлассПостоянногоПокупателя = ТекСтрокаТаблицаРаспределенияКонтрагентов.XYZКлассификация;
	КонецЦикла;

	Попытка
		Движения.СтадииВзаимоотношенийСПокупателями.Записать();
	Исключение
		Отказ = Истина;
		ОбщегоНазначения.СообщитьОбОшибке(ОписаниеОшибки(), Отказ);
	КонецПопытки;

КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	Если ОбменДанными.Загрузка  Тогда
		Возврат;
	КонецЕсли;


	 
	мУдалятьДвижения = НЕ ЭтоНовый();
	
КонецПроцедуры // ПередЗаписью