Перем ЗаказПокупателя Экспорт;

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ РЕКВИЗИТОВ ФОРМЫ

// Процедура вызывается при нажатии кнопки "Разместить"
//
Процедура ОсновныеДействияФормыРазместить(Кнопка)

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Номенклатура", ТаблицаНераспределенныхКомплектов.ВыгрузитьКолонку("Номенклатура"));
	Запрос.УстановитьПараметр("ХарактеристикаНоменклатуры", ТаблицаНераспределенныхКомплектов.ВыгрузитьКолонку("ХарактеристикаНоменклатуры"));
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	КомплектующиеНоменклатуры.Номенклатура                  КАК Номенклатура,
	|	КомплектующиеНоменклатуры.ХарактеристикаНоменклатуры    КАК ХарактеристикаНоменклатуры,
	|	КомплектующиеНоменклатуры.Комплектующая                 КАК Комплектующая,
	|	КомплектующиеНоменклатуры.ХарактеристикаКомплектующей   КАК ХарактеристикаКомплектующей,
	|	СУММА(КомплектующиеНоменклатуры.Количество)             КАК Количество,
	|	КомплектующиеНоменклатуры.ЕдиницаИзмерения              КАК ЕдиницаИзмерения
	|ИЗ
	|	РегистрСведений.КомплектующиеНоменклатуры КАК КомплектующиеНоменклатуры
	|ГДЕ
	|	КомплектующиеНоменклатуры.Номенклатура В(&Номенклатура) И
	|	КомплектующиеНоменклатуры.ХарактеристикаНоменклатуры В(&ХарактеристикаНоменклатуры)
	|СГРУППИРОВАТЬ ПО
	|	КомплектующиеНоменклатуры.Номенклатура,
	|	КомплектующиеНоменклатуры.ХарактеристикаНоменклатуры,
	|	КомплектующиеНоменклатуры.Комплектующая,
	|	КомплектующиеНоменклатуры.ХарактеристикаКомплектующей,
	|	КомплектующиеНоменклатуры.ЕдиницаИзмерения
	|";
	Выборка = Запрос.Выполнить().Выбрать();

	ТаблицаКомплектующих = Новый ТаблицаЗначений;
	ТаблицаКомплектующих.Колонки.Добавить("Номенклатура");
	ТаблицаКомплектующих.Колонки.Добавить("ХарактеристикаНоменклатуры");
	ТаблицаКомплектующих.Колонки.Добавить("Количество", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15, 3)));
	ТаблицаКомплектующих.Колонки.Добавить("ЕдиницаИзмерения");
	ТаблицаКомплектующих.Колонки.Добавить("Коэффициент");

	Для Каждого СтрокаТаблицы Из ТаблицаНераспределенныхКомплектов Цикл

		Если СтрокаТаблицы.Заполняем > 0 Тогда

			СтруктураПоиска = Новый Структура();
			СтруктураПоиска.Вставить("Номенклатура", СтрокаТаблицы.Номенклатура);
			СтруктураПоиска.Вставить("ХарактеристикаНоменклатуры", СтрокаТаблицы.ХарактеристикаНоменклатуры);

			Выборка.Сбросить();
			Пока Выборка.НайтиСледующий(СтруктураПоиска) Цикл
				НоваяСтрока = ТаблицаКомплектующих.Добавить();
				НоваяСтрока.Номенклатура = Выборка.Комплектующая;
				НоваяСтрока.ХарактеристикаНоменклатуры = Выборка.ХарактеристикаКомплектующей;
				НоваяСтрока.ЕдиницаИзмерения = Выборка.ЕдиницаИзмерения;
				НоваяСтрока.Коэффициент = Выборка.ЕдиницаИзмерения.Коэффициент;
				НоваяСтрока.Количество = Выборка.Количество * СтрокаТаблицы.Заполняем;
			КонецЦикла;
		КонецЕсли;

	КонецЦикла;

	ТаблицаКомплектующих.Свернуть("Номенклатура, ХарактеристикаНоменклатуры, ЕдиницаИзмерения, Коэффициент", "Количество");

	// Возможно, часть комплектующих уже была зарезервирована или размещена. Учтем это.
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Товар", Перечисления.ТоварТара.Товар);
	Запрос.УстановитьПараметр("ЗаказПокупателя", ЗаказПокупателя);
	Запрос.УстановитьПараметр("Номенклатура", ТаблицаКомплектующих.ВыгрузитьКолонку("Номенклатура"));
	Запрос.УстановитьПараметр("ХарактеристикаНоменклатуры", ТаблицаКомплектующих.ВыгрузитьКолонку("ХарактеристикаНоменклатуры"));
	Запрос.Текст = "ВЫБРАТЬ
	|Номенклатура,
	|ХарактеристикаНоменклатуры,
    |Сумма(Количество) КАК Количество
	|ИЗ
	|(ВЫБРАТЬ Номенклатура,
	|ХарактеристикаНоменклатуры,
	|КоличествоОстаток КАК Количество
	|ИЗ
	|РегистрНакопления.ТоварыВРезервеНаСкладах.Остатки(,Номенклатура В (&Номенклатура) И ХарактеристикаНоменклатуры В (&ХарактеристикаНоменклатуры)
	|                                                  И ДокументРезерва = &ЗаказПокупателя) КАК Резервы
	|ОБЪЕДИНИТЬ ВСЕ
    |ВЫБРАТЬ Номенклатура,
	|ХарактеристикаНоменклатуры,
    |КоличествоОстаток
	|ИЗ
	|	РегистрНакопления.РазмещениеЗаказовПокупателей.Остатки(,Номенклатура В (&Номенклатура) И ХарактеристикаНоменклатуры В (&ХарактеристикаНоменклатуры)
	|	                                             И ЗаказПокупателя = &ЗаказПокупателя И ТоварТара = &Товар) КАК Заказано
    |ОБЪЕДИНИТЬ ВСЕ
    |ВЫБРАТЬ Номенклатура,
	|ХарактеристикаНоменклатуры,
    |(-1)*КоличествоОстаток
	|ИЗ
	|	РегистрНакопления.ЗаказыПокупателей.Остатки(,Номенклатура В (&Номенклатура) И ХарактеристикаНоменклатуры В (&ХарактеристикаНоменклатуры)
	|	                                             И ЗаказПокупателя = &ЗаказПокупателя) КАК Заказы ) КАК ВложенныйЗапрос
    |СГРУППИРОВАТЬ ПО Номенклатура, ХарактеристикаНоменклатуры";
	Выборка = Запрос.Выполнить().Выбрать();

	Пока Выборка.Следующий() Цикл
		Если Выборка.Количество > 0 Тогда
			СтруктураПоиска = Новый Структура();
			СтруктураПоиска.Вставить("Номенклатура", Выборка.Номенклатура);
			СтруктураПоиска.Вставить("ХарактеристикаНоменклатуры", Выборка.ХарактеристикаНоменклатуры);
			НайденныеСтроки = ТаблицаКомплектующих.НайтиСтроки(СтруктураПоиска);
			Если НайденныеСтроки.Количество() > 0 Тогда
				СтрокаТаблицыКомплектующих = НайденныеСтроки[0];
				СтрокаТаблицыКомплектующих.Количество = СтрокаТаблицыКомплектующих.Количество 
				                                        - (Выборка.Количество
				                                           * СтрокаТаблицыКомплектующих.Номенклатура.ЕдиницаХраненияОстатков.Коэффициент
				                                           / СтрокаТаблицыКомплектующих.ЕдиницаИзмерения.Коэффициент);
				Если СтрокаТаблицыКомплектующих.Количество <= 0 Тогда
					ТаблицаКомплектующих.Удалить(СтрокаТаблицыКомплектующих);
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;

	Если ТаблицаКомплектующих.Количество() = 0 Тогда
		Предупреждение("Все необходимые комплектующие для выбранного вами количества комплектов уже зарезервированы (размещены)!");
	КонецЕсли;

	СтруктураВозвращаемыхЗначений = Новый Структура();
	СтруктураВозвращаемыхЗначений.Вставить("Команда", "ЗаполнениеКомплектующих");
	СтруктураВозвращаемыхЗначений.Вставить("ЗаказПокупателя", ЗаказПокупателя);
	СтруктураВозвращаемыхЗначений.Вставить("Товары", ТаблицаКомплектующих);
	ОповеститьОВыборе(СтруктураВозвращаемыхЗначений);

КонецПроцедуры // ОсновныеДействияФормыРазместить()

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ РЕКВИЗИТОВ ТАБЛИЦЫ НЕРАСПРЕДЕЛЕННЫХ КОМПЛЕКТОВ

// Процедура - обработчик события "ПриИзменении" реквизита "Заполняем" 
// таблицы нераспределенных комплектов
//
Процедура ТаблицаНераспределенныхКомплектовЗаполняемПриИзменении(Элемент)

	СтрокаТабличнойЧасти = ЭлементыФормы.ТаблицаНераспределенныхКомплектов.ТекущиеДанные;
	Если СтрокаТабличнойЧасти.Заполняем > СтрокаТабличнойЧасти.НеРазмещено Тогда
		Предупреждение("Комплектов для заполнения выбрано больше, чем не размещено!");
	КонецЕсли;

КонецПроцедуры // ТаблицаНераспределенныхКомплектовЗаполняемПриИзменении()

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ДЕЙСТВИЯ КОМАНДНЫХ ПАНЕЛЕЙ ФОРМЫ

// Процедура вызывается при нажатии кнопки "Не заполнять ничего"
//
Процедура КоманднаяПанельТаблицыРаспределенияНеЗаполнятьНичего(Кнопка)

	Для Каждого Строка Из ТаблицаНераспределенныхКомплектов Цикл
		Строка.Заполняем = 0;
	КонецЦикла;

КонецПроцедуры // КоманднаяПанельТаблицыРаспределенияНеЗаполнятьНичего()

// Процедура вызывается при нажатии кнопки "Заполнить все"
//
Процедура КоманднаяПанельТаблицыРаспределенияЗаполнитьВсе(Кнопка)

	Для Каждого Строка Из ТаблицаНераспределенныхКомплектов Цикл
		Строка.Заполняем = Строка.НеРазмещено;
	КонецЦикла;

КонецПроцедуры // КоманднаяПанельТаблицыРаспределенияЗаполнитьВсе()