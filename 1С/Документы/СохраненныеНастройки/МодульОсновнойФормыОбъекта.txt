Процедура ПередОткрытием(Отказ, СтандартнаяОбработка)
	
	Если ЭтоНовый() Тогда
		НовыйЭлемент = Пользователи.Добавить();
		НовыйЭлемент.Пользователь = глЗначениеПеременной("глТекущийПользователь");
		НовыйЭлемент.ПравоИзменения = Истина;
	КонецЕсли;
	ОбновитьДоступностьЭлементовФормы();
	
	Если НЕ Предопределенный тогда
		ЭлементыФормы.КоманднаяПанельНижняя.Кнопки.Удалить(ЭлементыФормы.КоманднаяПанельНижняя.Кнопки.ВернутьсяКЗаводскимНастройкам);
	КонецЕсли;
КонецПроцедуры

Процедура ОбновитьДоступностьЭлементовФормы()
	
	ДоступныеНастройки = ТиповыеОтчеты.ПолучитьСписокДоступныхВариантов(СправочникОбъект.Ссылка.НастраиваемыйОбъект, глЗначениеПеременной("глТекущийПользователь"));
	Элемент = ДоступныеНастройки.НайтиПоЗначению(СправочникОбъект.Ссылка);
	
	Доступность = (ТипНастройки = Перечисления.ТипыНастроек.НастройкиОтчета) И (Элемент <> Неопределено И Элемент.Пометка ИЛИ ЭтоНовый() ИЛИ РольДоступна("ПолныеПрава"));
	ЭлементыФормы.Наименование.ТолькоПросмотр = Не Доступность;
	ЭлементыФормы.Описание.ТолькоПросмотр = Не Доступность;
	ЭлементыФормы.Пользователи.ТолькоПросмотр = Не Доступность;
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ)
			
	Если ЭлементыФормы.Пользователи.ТолькоПросмотр Тогда
		Возврат;
	КонецЕсли;
	
	СтрокаСТекущимПользователем = ПолучитьСтрокуСТекущимПользователем();
	
	Если ТипЗнч(ВладелецФормы) = Тип("Форма") И (СтрокаСТекущимПользователем = Неопределено) Тогда
		// Если вариант отчета открыт из формы отчета - нельзя удалять себя из списка пользователей
		СпроситьДобавитьТекущегоПользователяВСписок(Отказ, "Текущий пользователь должен присутствовать в списке пользователей. Добавить?");
	ИначеЕсли СтрокаСТекущимПользователем <> Неопределено И СтрокаСТекущимПользователем.ПравоИзменения Тогда
		// Право изменения как было так и осталось
		Возврат;
	ИначеЕсли РольДоступна("ПолныеПрава") Тогда
		// Пользователь с полными правами, но без права изменения
		Возврат;
	ИначеЕсли Не РольДоступна("ПолныеПрава") И СтрокаСТекущимПользователем = Неопределено Тогда
		// Пользователь удалил себя из пользователей
		СпроситьДобавитьТекущегоПользователяВСписок(Отказ, "Текущий пользователь удален из списка пользователей. Добавить текущего пользователя с правом измения варианта отчета?");
	ИначеЕсли Не РольДоступна("ПолныеПрава") И Не СтрокаСТекущимПользователем.ПравоИзменения Тогда
		// Пользователь лишил себя права изменения. Заставим его вернуть себе это право
		СпроситьДобавитьТекущегоПользователяВСписок(Отказ, "Вы лишили себя изменения варианта отчета. Установить право для строки " + СтрокаСТекущимПользователем.Пользователь + "?", СтрокаСТекущимПользователем);
	КонецЕсли;
	
КонецПроцедуры

Процедура СпроситьДобавитьТекущегоПользователяВСписок(Отказ, ТекстВопроса, Строка = Неопределено)
	
	Ответ = Вопрос(ТекстВопроса, РежимДиалогаВопрос.ДаНет);
	Если Ответ = КодВозвратаДиалога.Да Тогда
		Если Строка = Неопределено Тогда
			Строка = Пользователи.Добавить();
			Строка.Пользователь = глЗначениеПеременной("глТекущийПользователь");
		КонецЕсли;
		Строка.ПравоИзменения = Истина;
	КонецЕсли;
	Отказ = Истина;
	
КонецПроцедуры

Функция ПолучитьСтрокуСТекущимПользователем()
	
	Для каждого Строка Из Пользователи Цикл
		НайденнаяСтрока = Неопределено;
		Если ТипЗнч(Строка.Пользователь) = Тип("СправочникСсылка.Пользователи")
		   И Строка.Пользователь = глЗначениеПеременной("глТекущийПользователь") Тогда
			Если Строка.ПравоИзменения Тогда
				Возврат Строка;
			Иначе
				НайденнаяСтрока = Строка;
			КонецЕсли;
		ИначеЕсли ТипЗнч(Строка.Пользователь) = Тип("СправочникСсылка.ГруппыПользователей") Тогда
			Если Строка.Пользователь = Справочники.ГруппыПользователей.ВсеПользователи Тогда
				Возврат Строка;
			Иначе
				Для каждого ПользовательГруппы Из Строка.Пользователь.ПользователиГруппы Цикл
					Если ПользовательГруппы.Пользователь = глЗначениеПеременной("глТекущийПользователь") Тогда
						Если Строка.ПравоИзменения Тогда
							Возврат Строка;
						Иначе
							НайденнаяСтрока = Строка;
						КонецЕсли;
					КонецЕсли;
				КонецЦикла;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	Возврат НайденнаяСтрока;
	
КонецФункции

Процедура КоманднаяПанельПользователиДобавитьПользователя(Кнопка)
	ЭлементыФормы.Пользователи.ДобавитьСтроку();
	СтрокаПользователя = ЭлементыФормы.Пользователи.ТекущаяСтрока;
	ФормаПользователя = Справочники.Пользователи.ПолучитьФормуВыбора(,ЭлементыФормы.Пользователи.ТекущаяКолонка.ЭлементУправления, ЭлементыФормы.Пользователи.ТекущаяСтрока);
	ФормаПользователя.Открыть();
КонецПроцедуры

Процедура КоманднаяПанельПользователиДобавитьГруппу(Кнопка)
	ЭлементыФормы.Пользователи.ДобавитьСтроку();
	СтрокаПользователя = ЭлементыФормы.Пользователи.ТекущаяСтрока;
	ФормаПользователя = Справочники.ГруппыПользователей.ПолучитьФормуВыбора(,ЭлементыФормы.Пользователи.ТекущаяКолонка.ЭлементУправления, ЭлементыФормы.Пользователи.ТекущаяСтрока);
	ФормаПользователя.Открыть();
КонецПроцедуры

Процедура ПользователиПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	Если Элемент.ТекущаяСтрока.Пользователь = Неопределено тогда
		Элемент.ТекущаяСтрока.Пользователь =  Справочники.Пользователи.ПустаяСсылка();
	КонецЕсли;
КонецПроцедуры

Процедура КоманднаяПанельНижняяВернутьсяКЗаводскимНастройкам(Кнопка)
	
	Если Вопрос("Сейчас будет загружена настройка по умолчанию. Это удалит предыдущую настройку варианта. Загрузить?", РежимДиалогаВопрос.ДаНет) = КодВозвратаДиалога.Нет тогда
		Возврат;
	КонецЕсли;
	ЗначениеНастройки = ХранилищеНастроек.Получить();
	Если ЗначениеНастройки <> Неопределено тогда
		ЗначениеНастройки.Удалить("Изменялась");
		ХранилищеНастроек = Новый ХранилищеЗначения(ЗначениеНастройки);
		Записать();
	КонецЕсли;
	
	НазваниеМакета = Справочники.СохраненныеНастройки.ПолучитьИмяПредопределенного(Ссылка);
	ОтчетНазвание = ТиповыеОтчеты.ПолучитьИмяОтчета(НазваниеМакета);
	ТиповыеОтчеты.ЗагрузитьНастройкуПредопределенногоЭлемента(НазваниеМакета, ОтчетНазвание, ,истина);
	
	Прочитать();
	Если ТипЗнч(Этаформа.ВладелецФормы) = Тип("Форма") тогда
		Этаформа.ВладелецФормы.ОтчетОбъект.ПрименитьНастройку();
		ТиповыеОтчеты.УправлениеОтображениемЭлементовФормыТиповогоОтчета(Этаформа.ВладелецФормы.ОтчетОбъект, Этаформа.ВладелецФормы);
		ТиповыеОтчеты.ПерерисоватьПанельНастроек(Этаформа.ВладелецФормы.ОтчетОбъект, Этаформа.ВладелецФормы, );
		Этаформа.ВладелецФормы.Обновить();
	КонецЕсли;
КонецПроцедуры


Процедура КоманднаяПанельНижняяЗагрузитьНастройкуИзФайла(Кнопка)
	
	БылиИзменения = Ложь;
	
	Если Вопрос("Загрузка удалит текущую настройку. Загрузить?", РежимДиалогаВопрос.ДаНет) = КодВозвратаДиалога.Нет тогда
		Возврат;
	КонецЕсли;
	ФормаВыбораКаталога = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	ФормаВыбораКаталога.Заголовок = "Выберите файл настройки";
	ФормаВыбораКаталога.Фильтр = "Настройка варианта отчета(*.vrp)|*.vrp";
	
	Если ФормаВыбораКаталога.Выбрать() тогда
		Каталог = ФормаВыбораКаталога.Каталог;
	Иначе
		Возврат;
	КонецЕсли;
	ТекстовыйДокумент = Новый ТекстовыйДокумент;
	ТекстовыйДокумент.Прочитать(ФормаВыбораКаталога.ПолноеИмяФайла);
	Настройка = ТиповыеОтчеты.ПолучитьНастройкуОтчета(ТекстовыйДокумент.ПолучитьТекст());
	Если Настройка.Версия > ТиповыеОтчеты.РабочаяВерсияНастройкиОтчетов() тогда
		Предупреждение("Данная конфигурация не поддерживает версию выбранной настройки. Обновите версию конфигурации.");
		Возврат;
	КонецЕсли;
	
	Если НастраиваемыйОбъект <> Настройка.НастраиваемыйОбъект тогда
		НастраиваемыйОбъект = Настройка.НастраиваемыйОбъект;
		БылиИзменения = истина;
	КонецЕсли;
	
	Если ТипНастройки <> Перечисления.ТипыНастроек.НастройкиОтчета тогда
		ТипНастройки = Перечисления.ТипыНастроек.НастройкиОтчета;
		БылиИзменения = истина;
	КонецЕсли;
	
	Если Наименование = "" тогда
		Наименование = Настройка.Наименование;
		БылиИзменения = истина;
	КонецЕсли;
	
	Если Описание <> Настройка.Описание тогда
		Описание = Настройка.Описание;
		БылиИзменения = истина;
	КонецЕсли;
	
	Если Пользователи.Количество() = 0 тогда
		НоваяСтрока = Пользователи.Добавить();
		НоваяСтрока.Пользователь = Справочники.ГруппыПользователей.ВсеПользователи;
		НоваяСтрока.ПравоИзменения = истина;
		БылиИзменения = истина;
	КонецЕсли;
	
	НастройкаТекущая = ХранилищеНастроек.Получить();
	Если НастройкаТекущая = Неопределено или НЕ НастройкаТекущая.Свойство("Изменялась", истина) тогда
		ХранилищеНастроек = Новый ХранилищеЗначения(Настройка.ХранилищеНастроек.Получить());
		БылиИзменения = истина;
	КонецЕсли;
	
	Модифицированность = БылиИзменения;
	
КонецПроцедуры

Процедура КоманднаяПанельНижняяСохранитьНастройку(Кнопка)
	ФормаВыбораКаталога = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.ВыборКаталога);
	ФормаВыбораКаталога.Заголовок = "Выберите каталог сохранения настройки";
	Каталог = "";
	Если ФормаВыбораКаталога.Выбрать() тогда
		Каталог = ФормаВыбораКаталога.Каталог;
	Иначе
		Возврат;
	КонецЕсли;
	ТекстовыйДокумент = Новый ТекстовыйДокумент;
	ОбъектНастройки = Ссылка;
	ТиповыеОтчеты.ПолучитьФайлСНастройкойОтчета(ОбъектНастройки, ТекстовыйДокумент);
	ИмяФайла = ТиповыеОтчеты.ПолучитьИмяФайлаДляНастройки(ОбъектНастройки.Наименование, ОбъектНастройки.Ссылка);
	Файл = Новый Файл(Каталог + "\" + СокрЛП(ИмяФайла) + ".vrp");
	Если Файл.Существует() тогда
		Если Вопрос("Файл ("+СокрЛП(ИмяФайла) + ".vrp) существует в указаном каталоге. Перезаписать его?", РежимДиалогаВопрос.ДаНет) = КодВозвратаДиалога.Нет тогда
			Возврат;
		КонецЕсли;
	КонецЕсли;
	ТекстовыйДокумент.Записать(Каталог + "\" + СокрЛП(ИмяФайла) + ".vrp");
КонецПроцедуры