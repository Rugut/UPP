////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ МОДУЛЯ

Перем мВалютаУправленческогоУчета;
Перем мВалютаРегламентированногоУчета;
Перем мМетодПланированияУчетаЗатрат Экспорт;
Перем мУдалятьДвижения;

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

Процедура Автозаполнение() Экспорт
	
	Если мМетодПланированияУчетаЗатрат = Перечисления.МетодыПланированияУчетаЗатратНаПерсонал.КассовыйМетодБезВыделенияНалогов
		ИЛИ мМетодПланированияУчетаЗатрат = Перечисления.МетодыПланированияУчетаЗатратНаПерсонал.КассовыйМетодСВыделениемНалогов Тогда
		
		ЗаполнитьКассовымМетодом();
		
	Иначе
		ЗаполнитьМетодомНачислений();
		
	КонецЕсли;
	
КонецПроцедуры  // АвтозаполнениеПроводок

Процедура РаспределитьПоКассовомуМетоду() Экспорт
	
	Запрос = ПодготовитьВременныеТаблицы(ДобавитьМесяц(ПериодРегистрации, -1));
	Если Запрос = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ВыделятьНалоги = мМетодПланированияУчетаЗатрат = Перечисления.МетодыПланированияУчетаЗатратНаПерсонал.КассовыйМетодСВыделениемНалогов;
	
	Запрос.УстановитьПараметр("парамПериодРегистрацииВзаиморасчетов",	ПериодРегистрации);
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СУММА(ОтражениеВУчете.Сумма) КАК Сумма,
	|	ОтражениеВУчете.КатегорияЗатраты КАК КатегорияЗатраты
	|ИЗ
	|	(ВЫБРАТЬ
	|		ЗНАЧЕНИЕ(Перечисление.КатегорииЗатраты.Зарплата) КАК КатегорияЗатраты,
	|		ВзаиморасчетыСРаботниками.СуммаУпр КАК Сумма
	|	ИЗ
	|		РегистрНакопления.ВзаиморасчетыСРаботниками КАК ВзаиморасчетыСРаботниками
	|	ГДЕ
	|		ВзаиморасчетыСРаботниками.Период МЕЖДУ &парамПериодРегистрации И &парамКонецПериодаРегистрации
	|		И ВзаиморасчетыСРаботниками.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ЗНАЧЕНИЕ(Перечисление.КатегорииЗатраты.Зарплата),
	|		ВзаиморасчетыСРаботникамиОрганизаций.СуммаВзаиморасчетов / ЕСТЬNULL(КурсыВалютДляРасчетовСПерсоналом.Курс, 1) * ЕСТЬNULL(КурсыВалютДляРасчетовСПерсоналом.Кратность, 1)
	|	ИЗ
	|		РегистрНакопления.ВзаиморасчетыСРаботникамиОрганизаций КАК ВзаиморасчетыСРаботникамиОрганизаций
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалютДляРасчетовСПерсоналом КАК КурсыВалютДляРасчетовСПерсоналом
	|			ПО (КурсыВалютДляРасчетовСПерсоналом.Период = &парамПериодРегистрации)
	|				И (КурсыВалютДляРасчетовСПерсоналом.Валюта = &парамВалютаУправленческогоУчета)
	|	ГДЕ
	|		ВзаиморасчетыСРаботникамиОрганизаций.Период МЕЖДУ &парамПериодРегистрацииВзаиморасчетов И КОНЕЦПЕРИОДА(&парамПериодРегистрацииВзаиморасчетов, МЕСЯЦ)
	|		И ВзаиморасчетыСРаботникамиОрганизаций.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		" + ?(ВыделятьНалоги, "ЗНАЧЕНИЕ(Перечисление.КатегорииЗатраты.ЕСН)", "ЗНАЧЕНИЕ(Перечисление.КатегорииЗатраты.Зарплата)") + ",
	|		ПромежуточнаяТаблица.Сумма / ЕСТЬNULL(КурсыВалютДляРасчетовСПерсоналом.Курс, 1) * ЕСТЬNULL(КурсыВалютДляРасчетовСПерсоналом.Кратность, 1)
	|	ИЗ
	|		ВТ_ПромежуточнаяТаблица КАК ПромежуточнаяТаблица
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалютДляРасчетовСПерсоналом КАК КурсыВалютДляРасчетовСПерсоналом
	|			ПО (КурсыВалютДляРасчетовСПерсоналом.Период = &парамПериодРегистрации)
	|				И (КурсыВалютДляРасчетовСПерсоналом.Валюта = &парамВалютаУправленческогоУчета)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		" + ?(ВыделятьНалоги, "ЗНАЧЕНИЕ(Перечисление.КатегорииЗатраты.НДФЛ)", "ЗНАЧЕНИЕ(Перечисление.КатегорииЗатраты.Зарплата)") + ",
	|		НДФЛРасчетыСБюджетом.Налог / ЕСТЬNULL(КурсыВалютДляРасчетовСПерсоналом.Курс, 1) * ЕСТЬNULL(КурсыВалютДляРасчетовСПерсоналом.Кратность, 1)
	|	ИЗ
	|		РегистрНакопления.НДФЛРасчетыСБюджетом КАК НДФЛРасчетыСБюджетом
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалютДляРасчетовСПерсоналом КАК КурсыВалютДляРасчетовСПерсоналом
	|			ПО (КурсыВалютДляРасчетовСПерсоналом.Период = &парамПериодРегистрации)
	|				И (КурсыВалютДляРасчетовСПерсоналом.Валюта = &парамВалютаУправленческогоУчета)
	|	ГДЕ
	|		НДФЛРасчетыСБюджетом.ИсчисленоИзЗарплаты
	|		И НДФЛРасчетыСБюджетом.Период МЕЖДУ &парамПериодРегистрации И &парамКонецПериодаРегистрации
	|		И НДФЛРасчетыСБюджетом.ВидСтроки = ЗНАЧЕНИЕ(Перечисление.НДФЛРасчетыСБюджетомВидСтроки.Начисление)
	|		И НДФЛРасчетыСБюджетом.Налог <> 0) КАК ОтражениеВУчете
	|
	|СГРУППИРОВАТЬ ПО
	|	ОтражениеВУчете.КатегорияЗатраты
	|
	|УПОРЯДОЧИТЬ ПО
	|	КатегорияЗатраты
	|АВТОУПОРЯДОЧИВАНИЕ";
	
	СуммыКРаспределению.Загрузить(Запрос.Выполнить().Выгрузить());
	
КонецПроцедуры // РаспределитьПоКассовомуМетоду

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОБЩЕГО НАЗНАЧЕНИЯ

Функция ПодготовитьВременныеТаблицы(АктуальныйПериодРегистрации)
	
	// Проверим, что задан курс валюты для расчетов с персоналом
	Если мВалютаРегламентированногоУчета <> мВалютаУправленческогоУчета Тогда
		СрезКурсаВалюты	= РегистрыСведений.КурсыВалютДляРасчетовСПерсоналом.Получить(НачалоМесяца(ПериодРегистрации), Новый Структура("Валюта", мВалютаУправленческогоУчета));
		Если Не ЗначениеЗаполнено(СрезКурсаВалюты.Курс) ИЛИ Не ЗначениеЗаполнено(СрезКурсаВалюты.Кратность) Тогда
			ОбщегоНазначения.СообщитьОбОшибке("Не задан курс " + мВалютаУправленческогоУчета + " для расчетов с персоналом на " + ПредставлениеПериода(НачалоМесяца(ПериодРегистрации), КонецМесяца(ПериодРегистрации))+ "!");
			Возврат Неопределено;
		КонецЕсли;
	КонецЕсли;
	
	ОтражениеВУчете.Очистить();
	
	// фактически - создаем структуру промежуточной таблицы
	ПромежуточнаяТаблица = ОтражениеВУчете.Выгрузить();
	
	Запрос = Новый Запрос;
	
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("парамГод",						Год(АктуальныйПериодРегистрации));
	Запрос.УстановитьПараметр("парамНачалоГода",				НачалоГода(АктуальныйПериодРегистрации));
	Запрос.УстановитьПараметр("парамПериодРегистрации",			АктуальныйПериодРегистрации);
	Запрос.УстановитьПараметр("парамКонецПериодаРегистрации",	КонецМесяца(АктуальныйПериодРегистрации));
	Запрос.УстановитьПараметр("парамВалютаУправленческогоУчета",мВалютаУправленческогоУчета);
	
	// УУДоходыПоВР
	//	Поля:
	//		ОбособленноеПодразделение
	//		Сотрудник
	//		Физлицо
	//		ВидРасчета
	//		ПериодРегистрации
	//		Подразделение
	//		СтатьяЗатрат
	//		НоменклатурнаяГруппа
	//		ОбъектСтроительства
	//		Результат
	//	
	//	Описание:
	//	Данные отражения начислений в упручете
	
	УУДоходыПоВРТекст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Начисления.ОбособленноеПодразделение КАК ОбособленноеПодразделение,
	|	Начисления.Сотрудник КАК Сотрудник,
	|	Начисления.Физлицо,
	|	Начисления.ВидРасчета КАК ВидРасчета,
	|	Начисления.ПериодРегистрации КАК ПериодРегистрации,
	|	Начисления.ПериодДействия КАК ПериодДействия,
	|	ВЫБОР
	|		КОГДА Начисления.Подразделение = ЗНАЧЕНИЕ(Справочник.Подразделения.ПустаяСсылка)
	|				И (НЕ СоответствиеПодразделенийИПодразделенийОрганизаций.Подразделение ЕСТЬ NULL )
	|			ТОГДА СоответствиеПодразделенийИПодразделенийОрганизаций.Подразделение
	|		ИНАЧЕ Начисления.Подразделение
	|	КОНЕЦ КАК Подразделение,
	|	Начисления.СтатьяЗатрат,
	|	Начисления.НоменклатурнаяГруппа,
	|	Начисления.ОбъектСтроительства,
	|	ВЫБОР
	|		КОГДА Начисления.Подразделение = ЗНАЧЕНИЕ(Справочник.Подразделения.ПустаяСсылка)
	|				И (НЕ СоответствиеПодразделенийИПодразделенийОрганизаций.Подразделение ЕСТЬ NULL )
	|			ТОГДА Начисления.Результат / ЕСТЬNULL(СоответствиеПодразделенийКоличество.КоличествоПодразделений, 1)
	|		ИНАЧЕ Начисления.Результат
	|	КОНЕЦ КАК Результат,
	|	Начисления.ЭтоСтатьяЗатратЕНВД
	|ПОМЕСТИТЬ ВТ_УУДоходыПоВР
	|ИЗ
	|	(ВЫБРАТЬ
	|		ОсновныеНачисления.ОбособленноеПодразделение КАК ОбособленноеПодразделение,
	|		ОсновныеНачисления.Сотрудник КАК Сотрудник,
	|		ОсновныеНачисления.ФизЛицо КАК Физлицо,
	|		ОсновныеНачисления.ВидРасчета КАК ВидРасчета,
	|		ОсновныеНачисления.ПериодРегистрации КАК ПериодРегистрации,
	|		ОсновныеНачисления.ПериодДействия КАК ПериодДействия,
	|		ВЫБОР
	|			КОГДА РаспределениеЗаработкаРаботников.Подразделение ЕСТЬ НЕ NULL 
	|					И РаспределениеЗаработкаРаботников.Подразделение <> ЗНАЧЕНИЕ(Справочник.Подразделения.ПустаяСсылка)
	|				ТОГДА РаспределениеЗаработкаРаботников.Подразделение
	|			КОГДА УчетЗаработкаРаботников.Подразделение ЕСТЬ НЕ NULL 
	|					И УчетЗаработкаРаботников.Подразделение <> ЗНАЧЕНИЕ(Справочник.Подразделения.ПустаяСсылка)
	|				ТОГДА УчетЗаработкаРаботников.Подразделение
	|			КОГДА Работники.Подразделение ЕСТЬ НЕ NULL 
	|					И Работники.Подразделение <> ЗНАЧЕНИЕ(Справочник.Подразделения.ПустаяСсылка)
	|				ТОГДА Работники.Подразделение
	|			ИНАЧЕ ЗНАЧЕНИЕ(Справочник.Подразделения.ПустаяСсылка)
	|		КОНЕЦ КАК Подразделение,
	|		ЕСТЬNULL(ОсновныеНачисления.ВидРасчета.СпособОтраженияВУпрУчете.СтатьяЗатрат, ЕСТЬNULL(РаспределениеЗаработкаРаботников.СпособОтраженияВУпрУчете.СтатьяЗатрат, ЕСТЬNULL(УчетЗаработкаРаботников.СпособОтраженияВУпрУчете.СтатьяЗатрат, СпособыОтраженияЗарплатыВУпрУчете.СтатьяЗатрат))) КАК СтатьяЗатрат,
	|		ЕСТЬNULL(ОсновныеНачисления.ВидРасчета.СпособОтраженияВУпрУчете.НоменклатурнаяГруппа, ЕСТЬNULL(РаспределениеЗаработкаРаботников.СпособОтраженияВУпрУчете.НоменклатурнаяГруппа, ЕСТЬNULL(УчетЗаработкаРаботников.СпособОтраженияВУпрУчете.НоменклатурнаяГруппа, СпособыОтраженияЗарплатыВУпрУчете.НоменклатурнаяГруппа))) КАК НоменклатурнаяГруппа,
	|		ЕСТЬNULL(ОсновныеНачисления.ВидРасчета.СпособОтраженияВУпрУчете.ОбъектСтроительства, ЕСТЬNULL(РаспределениеЗаработкаРаботников.СпособОтраженияВУпрУчете.ОбъектСтроительства, ЕСТЬNULL(УчетЗаработкаРаботников.СпособОтраженияВУпрУчете.ОбъектСтроительства, СпособыОтраженияЗарплатыВУпрУчете.ОбъектСтроительства))) КАК ОбъектСтроительства,
	|		ОсновныеНачисления.Результат * ЕСТЬNULL(РаспределениеЗаработкаРаботников.ДоляСпособаОтражения, 100) / 100 КАК Результат,
	|		ВЫБОР
	|			КОГДА ОсновныеНачисления.ВидРасчета.ОтнесениеРасходовКДеятельностиЕНВД = ЗНАЧЕНИЕ(Перечисление.ОтнесениеРасходовКДеятельностиЕНВД.РасходыОтносятсяКЕНВД)
	|				ТОГДА ИСТИНА
	|			КОГДА ЕНВДПроцент.СчетДт ЕСТЬ НЕ NULL
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ЛОЖЬ
	|		КОНЕЦ КАК ЭтоСтатьяЗатратЕНВД,
	|		ОсновныеНачисления.ПодразделениеОрганизации КАК ПодразделениеОрганизации
	|	ИЗ
	|		РегистрРасчета.ОсновныеНачисленияРаботниковОрганизаций КАК ОсновныеНачисления
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.УчетЗаработкаРаботников КАК УчетЗаработкаРаботников
	|				ВНУТРЕННЕЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|					МАКСИМУМ(УчетЗаработкаРаботников.Период) КАК Период,
	|					УчетЗаработкаРаботников.Физлицо КАК Физлицо
	|				ИЗ
	|					РегистрРасчета.ОсновныеНачисленияРаботниковОрганизаций КАК ОсновныеНачисления
	|						ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.УчетЗаработкаРаботников КАК УчетЗаработкаРаботников
	|						ПО ОсновныеНачисления.ФизЛицо = УчетЗаработкаРаботников.Физлицо
	|							И ОсновныеНачисления.ПериодРегистрации >= УчетЗаработкаРаботников.Период
	|				ГДЕ
	|					ОсновныеНачисления.ПериодРегистрации = &парамПериодРегистрации
	|				
	|				СГРУППИРОВАТЬ ПО
	|					УчетЗаработкаРаботников.Физлицо) КАК УчетЗаработкаСрез
	|				ПО УчетЗаработкаРаботников.Физлицо = УчетЗаработкаСрез.Физлицо
	|					И УчетЗаработкаРаботников.Период = УчетЗаработкаСрез.Период
	|			ПО ОсновныеНачисления.ФизЛицо = УчетЗаработкаРаботников.Физлицо
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РаспределениеЗаработкаРаботников КАК РаспределениеЗаработкаРаботников
	|			ПО ОсновныеНачисления.ПериодРегистрации = РаспределениеЗаработкаРаботников.ПериодРегистрации
	|				И ОсновныеНачисления.ФизЛицо = РаспределениеЗаработкаРаботников.Физлицо
	|			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.СпособыОтраженияЗарплатыВУпрУчете КАК СпособыОтраженияЗарплатыВУпрУчете
	|			ПО (СпособыОтраженияЗарплатыВУпрУчете.Ссылка = ЗНАЧЕНИЕ(Справочник.СпособыОтраженияЗарплатыВУпрУчете.ОтражениеНачисленийПоУмолчанию))
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПроцентДеятельностиЕНВДСотрудников КАК ЕНВДПроцент
	|			ПО ОсновныеНачисления.Сотрудник = ЕНВДПроцент.Сотрудник
	|				И (ВЫБОР
	|					КОГДА ОсновныеНачисления.ВидРасчета.СтратегияОтраженияВУчете = ЗНАЧЕНИЕ(Перечисление.СтратегииОтраженияВРеглУчетеНачислений.КакЗаданоНаНачалоСобытия)
	|						ТОГДА НАЧАЛОПЕРИОДА(ОсновныеНачисления.ДатаНачалаСобытия, МЕСЯЦ)
	|					КОГДА ОсновныеНачисления.ПериодДействия > ОсновныеНачисления.ПериодРегистрации
	|						ТОГДА ОсновныеНачисления.ПериодРегистрации
	|					ИНАЧЕ ОсновныеНачисления.ПериодДействия
	|				КОНЕЦ = ЕНВДПроцент.ПериодРегистрации)
	|				И ((НЕ ОсновныеНачисления.Сторно))
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.Работники КАК Работники
	|				ВНУТРЕННЕЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|					МАКСИМУМ(Работники.Период) КАК Период,
	|					Работники.ФизЛицо КАК Физлицо
	|				ИЗ
	|					РегистрРасчета.ОсновныеНачисленияРаботниковОрганизаций КАК ОсновныеНачисления
	|						ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.Работники КАК Работники
	|						ПО ОсновныеНачисления.ФизЛицо = Работники.ФизЛицо
	|							И ОсновныеНачисления.ПериодРегистрации >= Работники.Период
	|				ГДЕ
	|					ОсновныеНачисления.ПериодРегистрации = &парамПериодРегистрации
	|				
	|				СГРУППИРОВАТЬ ПО
	|					Работники.ФизЛицо) КАК РаботникиСрез
	|				ПО Работники.ФизЛицо = РаботникиСрез.Физлицо
	|					И Работники.Период = РаботникиСрез.Период
	|			ПО ОсновныеНачисления.ФизЛицо = Работники.ФизЛицо
	|				И (Работники.ПричинаИзмененияСостояния <> ЗНАЧЕНИЕ(Перечисление.ПричиныИзмененияСостояния.Увольнение))
	|	ГДЕ
	|		ОсновныеНачисления.ПериодРегистрации = &парамПериодРегистрации
	|		И ОсновныеНачисления.Результат <> 0
	|		И ОсновныеНачисления.ВидРасчета.СпособОтраженияВУпрУчете <> ЗНАЧЕНИЕ(Справочник.СпособыОтраженияЗарплатыВУпрУчете.НеОтражатьВУпрУчете)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ДополнительныеНачисления.ОбособленноеПодразделение,
	|		ДополнительныеНачисления.Сотрудник,
	|		ДополнительныеНачисления.ФизЛицо,
	|		ДополнительныеНачисления.ВидРасчета,
	|		ДополнительныеНачисления.ПериодРегистрации,
	|		ДополнительныеНачисления.ПериодРегистрации,
	|		ВЫБОР
	|			КОГДА РаспределениеЗаработкаРаботников.Подразделение ЕСТЬ НЕ NULL 
	|					И РаспределениеЗаработкаРаботников.Подразделение <> ЗНАЧЕНИЕ(Справочник.Подразделения.ПустаяСсылка)
	|				ТОГДА РаспределениеЗаработкаРаботников.Подразделение
	|			КОГДА УчетЗаработкаРаботников.Подразделение ЕСТЬ НЕ NULL 
	|					И УчетЗаработкаРаботников.Подразделение <> ЗНАЧЕНИЕ(Справочник.Подразделения.ПустаяСсылка)
	|				ТОГДА УчетЗаработкаРаботников.Подразделение
	|			КОГДА Работники.Подразделение ЕСТЬ НЕ NULL 
	|					И Работники.Подразделение <> ЗНАЧЕНИЕ(Справочник.Подразделения.ПустаяСсылка)
	|				ТОГДА Работники.Подразделение
	|			ИНАЧЕ ЕСТЬNULL(СоответствиеПодразделенийИПодразделенийОрганизаций.Подразделение, ЗНАЧЕНИЕ(Справочник.Подразделения.ПустаяСсылка))
	|		КОНЕЦ,
	|		ЕСТЬNULL(ДополнительныеНачисления.ВидРасчета.СпособОтраженияВУпрУчете.СтатьяЗатрат, ЕСТЬNULL(РаспределениеЗаработкаРаботников.СпособОтраженияВУпрУчете.СтатьяЗатрат, ЕСТЬNULL(УчетЗаработкаРаботников.СпособОтраженияВУпрУчете.СтатьяЗатрат, СпособыОтраженияЗарплатыВУпрУчете.СтатьяЗатрат))),
	|		ЕСТЬNULL(ДополнительныеНачисления.ВидРасчета.СпособОтраженияВУпрУчете.НоменклатурнаяГруппа, ЕСТЬNULL(РаспределениеЗаработкаРаботников.СпособОтраженияВУпрУчете.НоменклатурнаяГруппа, ЕСТЬNULL(УчетЗаработкаРаботников.СпособОтраженияВУпрУчете.НоменклатурнаяГруппа, СпособыОтраженияЗарплатыВУпрУчете.НоменклатурнаяГруппа))),
	|		ЕСТЬNULL(ДополнительныеНачисления.ВидРасчета.СпособОтраженияВУпрУчете.ОбъектСтроительства, ЕСТЬNULL(РаспределениеЗаработкаРаботников.СпособОтраженияВУпрУчете.ОбъектСтроительства, ЕСТЬNULL(УчетЗаработкаРаботников.СпособОтраженияВУпрУчете.ОбъектСтроительства, СпособыОтраженияЗарплатыВУпрУчете.ОбъектСтроительства))),
	|		ДополнительныеНачисления.Результат * ЕСТЬNULL(РаспределениеЗаработкаРаботников.ДоляСпособаОтражения, 100) / 100,
	|		ВЫБОР
	|			КОГДА ДополнительныеНачисления.ВидРасчета.ОтнесениеРасходовКДеятельностиЕНВД = ЗНАЧЕНИЕ(Перечисление.ОтнесениеРасходовКДеятельностиЕНВД.РасходыОтносятсяКЕНВД)
	|				ТОГДА ИСТИНА
	|			КОГДА ЕНВДПроцент.СчетДт ЕСТЬ НЕ NULL
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ЛОЖЬ
	|		КОНЕЦ,
	|		ДополнительныеНачисления.ПодразделениеОрганизации
	|	ИЗ
	|		РегистрРасчета.ДополнительныеНачисленияРаботниковОрганизаций КАК ДополнительныеНачисления
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СоответствиеПодразделенийИПодразделенийОрганизаций КАК СоответствиеПодразделенийИПодразделенийОрганизаций
	|			ПО ДополнительныеНачисления.ОбособленноеПодразделение = СоответствиеПодразделенийИПодразделенийОрганизаций.Организация
	|				И ДополнительныеНачисления.ПодразделениеОрганизации = СоответствиеПодразделенийИПодразделенийОрганизаций.ПодразделениеОрганизации
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.УчетЗаработкаРаботников КАК УчетЗаработкаРаботников
	|				ВНУТРЕННЕЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|					МАКСИМУМ(УчетЗаработкаРаботников.Период) КАК Период,
	|					УчетЗаработкаРаботников.Физлицо КАК Физлицо
	|				ИЗ
	|					РегистрРасчета.ДополнительныеНачисленияРаботниковОрганизаций КАК ДополнительныеНачисления
	|						ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.УчетЗаработкаРаботников КАК УчетЗаработкаРаботников
	|						ПО ДополнительныеНачисления.ФизЛицо = УчетЗаработкаРаботников.Физлицо
	|							И ДополнительныеНачисления.ПериодРегистрации >= УчетЗаработкаРаботников.Период
	|				ГДЕ
	|					НАЧАЛОПЕРИОДА(ДополнительныеНачисления.ПериодРегистрации, МЕСЯЦ) = &парамПериодРегистрации
	|				
	|				СГРУППИРОВАТЬ ПО
	|					УчетЗаработкаРаботников.Физлицо) КАК УчетЗаработкаСрез
	|				ПО УчетЗаработкаРаботников.Период = УчетЗаработкаСрез.Период
	|					И УчетЗаработкаРаботников.Физлицо = УчетЗаработкаСрез.Физлицо
	|			ПО ДополнительныеНачисления.ФизЛицо = УчетЗаработкаРаботников.Физлицо
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РаспределениеЗаработкаРаботников КАК РаспределениеЗаработкаРаботников
	|			ПО ДополнительныеНачисления.ПериодРегистрации = РаспределениеЗаработкаРаботников.ПериодРегистрации
	|				И ДополнительныеНачисления.ФизЛицо = РаспределениеЗаработкаРаботников.Физлицо
	|			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.СпособыОтраженияЗарплатыВУпрУчете КАК СпособыОтраженияЗарплатыВУпрУчете
	|			ПО (СпособыОтраженияЗарплатыВУпрУчете.Ссылка = ЗНАЧЕНИЕ(Справочник.СпособыОтраженияЗарплатыВУпрУчете.ОтражениеНачисленийПоУмолчанию))
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПроцентДеятельностиЕНВДСотрудников КАК ЕНВДПроцент
	|			ПО ДополнительныеНачисления.Сотрудник = ЕНВДПроцент.Сотрудник
	|				И (ЕНВДПроцент.ПериодРегистрации = НАЧАЛОПЕРИОДА(ДополнительныеНачисления.ПериодРегистрации, МЕСЯЦ))
	|				И ((НЕ ДополнительныеНачисления.Сторно))
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.Работники КАК Работники
	|				ВНУТРЕННЕЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|					МАКСИМУМ(Работники.Период) КАК Период,
	|					Работники.ФизЛицо КАК Физлицо
	|				ИЗ
	|					РегистрРасчета.ДополнительныеНачисленияРаботниковОрганизаций КАК ДополнительныеНачисления
	|						ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.Работники КАК Работники
	|						ПО ДополнительныеНачисления.ФизЛицо = Работники.ФизЛицо
	|							И ДополнительныеНачисления.ПериодРегистрации >= Работники.Период
	|				ГДЕ
	|					ДополнительныеНачисления.ПериодРегистрации = &парамПериодРегистрации
	|				
	|				СГРУППИРОВАТЬ ПО
	|					Работники.ФизЛицо) КАК РаботникиСрез
	|				ПО Работники.Период = РаботникиСрез.Период
	|					И Работники.ФизЛицо = РаботникиСрез.Физлицо
	|			ПО ДополнительныеНачисления.ФизЛицо = Работники.ФизЛицо
	|				И (Работники.ПричинаИзмененияСостояния <> ЗНАЧЕНИЕ(Перечисление.ПричиныИзмененияСостояния.Увольнение))
	|	ГДЕ
	|		НАЧАЛОПЕРИОДА(ДополнительныеНачисления.ПериодРегистрации, МЕСЯЦ) = &парамПериодРегистрации
	|		И ДополнительныеНачисления.Результат <> 0
	|		И ДополнительныеНачисления.ВидРасчета.СпособОтраженияВУпрУчете <> ЗНАЧЕНИЕ(Справочник.СпособыОтраженияЗарплатыВУпрУчете.НеОтражатьВУпрУчете)) КАК Начисления
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СоответствиеПодразделенийИПодразделенийОрганизаций КАК СоответствиеПодразделенийИПодразделенийОрганизаций
	|		ПО Начисления.ОбособленноеПодразделение = СоответствиеПодразделенийИПодразделенийОрганизаций.Организация
	|			И Начисления.ПодразделениеОрганизации = СоответствиеПодразделенийИПодразделенийОрганизаций.ПодразделениеОрганизации
	|			И (Начисления.Подразделение = ЗНАЧЕНИЕ(Справочник.Подразделения.ПустаяСсылка))
	|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|			СоответствиеПодразделенийИПодразделенийОрганизаций.Организация КАК Организация,
	|			СоответствиеПодразделенийИПодразделенийОрганизаций.ПодразделениеОрганизации КАК ПодразделениеОрганизации,
	|			КОЛИЧЕСТВО(РАЗЛИЧНЫЕ СоответствиеПодразделенийИПодразделенийОрганизаций.Подразделение) КАК КоличествоПодразделений
	|		ИЗ
	|			РегистрСведений.СоответствиеПодразделенийИПодразделенийОрганизаций КАК СоответствиеПодразделенийИПодразделенийОрганизаций
	|		
	|		СГРУППИРОВАТЬ ПО
	|			СоответствиеПодразделенийИПодразделенийОрганизаций.Организация,
	|			СоответствиеПодразделенийИПодразделенийОрганизаций.ПодразделениеОрганизации) КАК СоответствиеПодразделенийКоличество
	|		ПО Начисления.ОбособленноеПодразделение = СоответствиеПодразделенийКоличество.Организация
	|			И Начисления.ПодразделениеОрганизации = СоответствиеПодразделенийКоличество.ПодразделениеОрганизации
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОбособленноеПодразделение,
	|	Сотрудник,
	|	ВидРасчета,
	|	ПериодРегистрации,
	|	ПериодДействия";
	
	Запрос.Текст = УУДоходыПоВРТекст;
	Запрос.Выполнить();
	
	Если АктуальныйПериодРегистрации < ПроведениеРасчетов.ДатаЗаменыЕСНСтраховымиВзносами() Тогда
		
		//////////////////////////////////////////////////////////////////////////////////////////////////
		// отражение исчисленного ЕСН
		//
		
		
		// УчетнаяПолитикаНалоговыйУчет
		// Таблица УчетнаяПолитикаНалоговыйУчетУСН - это список периодов, когда организация переходила на УСН
		// поля:
		//		УСН,
		//		Месяц - месяц налогового периода
		// Описание:
		//	Выбираем Из Периоды (таблица - список периодов с начала года по текущий период)
		//	Внутреннее соединение с "псевдосрезом" последних регистра УчетнаяПолитикаНалоговыйУчет
		//	по равенству периодов
		//	условие: что организация использует УСН
		
		ТекстЗапроса =
		"ВЫБРАТЬ
		|	&парамПериодРегистрации КАК Месяц,
		|	""поле УСН"" КАК УСН,
		|	ЕСТЬNULL(УчетнаяПолитикаНалоговыйУчет.ОрганизацияЯвляетсяПлательщикомЕНВД, ЛОЖЬ) КАК ОрганизацияЯвляетсяПлательщикомЕНВД,
		|	ЕСТЬNULL(УчетнаяПолитикаНалоговыйУчет.УплачиватьДобровольныеВзносыВФСС, ЛОЖЬ) КАК УплачиватьДобровольныеВзносыВФСС
		|ПОМЕСТИТЬ ВТ_УчетнаяПолитикаНалоговыйУчет
		|ИЗ
		|	РегистрСведений.УчетнаяПолитикаНалоговыйУчет.СрезПоследних(&парамКонецПериодаРегистрации, ) КАК УчетнаяПолитикаНалоговыйУчет
		|ГДЕ
		|	&ДопУсловие
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Месяц";
		
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"РегистрСведений.УчетнаяПолитикаНалоговыйУчет", ЗаполнениеРегламентированнойОтчетностиПереопределяемый.ИмяУчетнойПолитики()); 
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, """поле УСН""", ЗаполнениеРегламентированнойОтчетностиПереопределяемый.ТекстПоляУСН4аФСС()); 
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ДопУсловие", ЗаполнениеРегламентированнойОтчетностиПереопределяемый.ТекстПоляУсловияУСН());
		Запрос.Текст = ТекстЗапроса;
		
		Запрос.Выполнить();
		
		
		// УчетнаяПолитикаНалоговыйУчетУплачиватьДобровольныеВзносыВФСС
		// Таблица УчетнаяПолитикаНалоговыйУчетУСН - это список периодов, когда организация уплачивала добровольные взносы в ФСС
		// поля:
		//		УплачиватьДобровольныеВзносыВФСС,
		//		Месяц - месяц налогового периода
		// Описание:
		//	Выбираем Из Периоды (таблица - список периодов с начала года по текущий период)
		//	Внутреннее соединение с "псевдосрезом" последних регистра УчетнаяПолитикаНалоговыйУчет
		//	по равенству периодов
		//	условие: что организация уплачивает добровольные взносы в ФСС
		
		УчетнаяПолитикаНалоговыйУчетУплачиватьДобровольныеВзносыВФСС = 
		"ВЫБРАТЬ
		|	&парамПериодРегистрации КАК Месяц,
		|	УчетнаяПолитикаНалоговыйУчетСрезПоследних.Организация КАК Организация,
		|	УчетнаяПолитикаНалоговыйУчетСрезПоследних.УплачиватьДобровольныеВзносыВФСС
		|ИЗ
		|	РегистрСведений.УчетнаяПолитикаНалоговыйУчет.СрезПоследних(&парамКонецПериодаРегистрации, ) КАК УчетнаяПолитикаНалоговыйУчетСрезПоследних
		|ГДЕ
		|	УчетнаяПолитикаНалоговыйУчетСрезПоследних.УплачиватьДобровольныеВзносыВФСС";
		УчетнаяПолитикаНалоговыйУчетУплачиватьДобровольныеВзносыВФСС = СтрЗаменить(УчетнаяПолитикаНалоговыйУчетУплачиватьДобровольныеВзносыВФСС,"РегистрСведений.УчетнаяПолитикаНалоговыйУчет", ЗаполнениеРегламентированнойОтчетностиПереопределяемый.ИмяУчетнойПолитики()); 
		
		// для заполнения данных по ЕСН нужно получить максимальные ставки ЕСН
		// и передать их в качестве параметров в запрос ЕСНИсчисленный
		Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	Организации.Ссылка КАК Организация,
		|	ЕСТЬNULL(СведенияОСтавкахЕСНиПФР.ФедеральныйБюджетВПроцентах, 0) КАК СтавкаФедеральныйБюджет,
		|	ЕСТЬNULL(СведенияОСтавкахЕСНиПФР.ФССвПроцентах, 0) КАК СтавкаФСС,
		|	ЕСТЬNULL(СведенияОСтавкахЕСНиПФР.ФФОМСвПроцентах, 0) КАК СтавкаФФОМС,
		|	ЕСТЬNULL(СведенияОСтавкахЕСНиПФР.ТФОМСвПроцентах, 0) КАК СтавкаТФОМС,
		|	ЕСТЬNULL(СведенияОСтавкахЕСНиПФР.ПФРНакопительная1вПроцентах, 0) КАК СтавкаПФРНакопительная1,
		|	ЕСТЬNULL(СведенияОСтавкахЕСНиПФР.ПФРНакопительная2вПроцентах, 0) КАК СтавкаПФРНакопительная2,
		|	ЕСТЬNULL(СведенияОСтавкахЕСНиПФР.ПФРСтраховая1вПроцентах, 0) КАК СтавкаПФРСтраховая1,
		|	ЕСТЬNULL(СведенияОСтавкахЕСНиПФР.ПФРСтраховая2вПроцентах, 0) КАК СтавкаПФРСтраховая2
		|ПОМЕСТИТЬ ВТ_СведенияОСтавкахЕСНиПФР
		|ИЗ
		|	Справочник.Организации КАК Организации
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СведенияОСтавкахЕСНиПФР КАК СведенияОСтавкахЕСНиПФР
		|		ПО Организации.ВидСтавокЕСНиПФР = СведенияОСтавкахЕСНиПФР.ВидСтавокЕСНиПФР
		|ГДЕ
		|	СведенияОСтавкахЕСНиПФР.Год = &парамГод
		|	И СведенияОСтавкахЕСНиПФР.НомерСтрокиСтавок = 1
		|	И Организации.ГоловнаяОрганизация = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Организация";
		Запрос.Выполнить();
		
		
		// ЕСНДоходы
		// Поля:
		//		Сотрудник
		//		ФизЛицо
		//		ПериодРегистрации
		//		КодДоходаЕСН
		//		ОблагаетсяЕНВД
		//		Результат
		//		
		//	
		//	Описание:
		//	Данные отражения доходов в учете ЕСН, помещаем во временную таблицу ВТ_ЕСНДоходы
		
		ЕСНДоходыТекст =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	Доходы.Сотрудник КАК Сотрудник,
		|	Доходы.ФизЛицо КАК ФизЛицо,
		|	Доходы.ПериодРегистрации КАК ПериодРегистрации,
		|	Доходы.КодДоходаЕСН КАК КодДоходаЕСН,
		|	Доходы.ОблагаетсяЕНВД КАК ОблагаетсяЕНВД,
		|	СУММА(Доходы.Результат) КАК Результат,
		|	ГОД(Доходы.ПериодРегистрации) КАК Год
		|ПОМЕСТИТЬ ВТ_ЕСНДоходы
		|ИЗ
		|	(ВЫБРАТЬ
		|		ЕСНОсновныеНачисления.Сотрудник КАК Сотрудник,
		|		ЕСНОсновныеНачисления.ФизЛицо КАК ФизЛицо,
		|		ЕСНОсновныеНачисления.КодДоходаЕСН КАК КодДоходаЕСН,
		|		ЕСНОсновныеНачисления.ОблагаетсяЕНВД КАК ОблагаетсяЕНВД,
		|		ЕСНОсновныеНачисления.ПериодРегистрации КАК ПериодРегистрации,
		|		ЕСНОсновныеНачисления.Результат КАК Результат
		|	ИЗ
		|		РегистрРасчета.ЕСНОсновныеНачисления КАК ЕСНОсновныеНачисления
		|	ГДЕ
		|		ЕСНОсновныеНачисления.ПериодРегистрации = &парамПериодРегистрации
		|		И (ЕСНОсновныеНачисления.КодДоходаЕСН.ВходитВБазуФОМС
		|				ИЛИ ЕСНОсновныеНачисления.КодДоходаЕСН.ВходитВБазуФСС
		|				ИЛИ ЕСНОсновныеНачисления.КодДоходаЕСН.ВходитВБазуФедеральныйБюджет)
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		ЕСНДополнительныеНачисления.Сотрудник,
		|		ЕСНДополнительныеНачисления.ФизЛицо,
		|		ЕСНДополнительныеНачисления.КодДоходаЕСН,
		|		ЕСНДополнительныеНачисления.ОблагаетсяЕНВД,
		|		ЕСНДополнительныеНачисления.ПериодРегистрации,
		|		ЕСНДополнительныеНачисления.Результат - ЕСНДополнительныеНачисления.Скидка
		|	ИЗ
		|		РегистрРасчета.ЕСНДополнительныеНачисления КАК ЕСНДополнительныеНачисления
		|	ГДЕ
		|		ЕСНДополнительныеНачисления.ПериодРегистрации = &парамПериодРегистрации
		|		И (ЕСНДополнительныеНачисления.КодДоходаЕСН.ВходитВБазуФОМС
		|				ИЛИ ЕСНДополнительныеНачисления.КодДоходаЕСН.ВходитВБазуФСС
		|				ИЛИ ЕСНДополнительныеНачисления.КодДоходаЕСН.ВходитВБазуФедеральныйБюджет)) КАК Доходы
		|
		|СГРУППИРОВАТЬ ПО
		|	Доходы.Сотрудник,
		|	Доходы.КодДоходаЕСН,
		|	Доходы.ПериодРегистрации,
		|	Доходы.ОблагаетсяЕНВД,
		|	Доходы.ФизЛицо,
		|	ГОД(Доходы.ПериодРегистрации)
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ФизЛицо,
		|	ПериодРегистрации";
		Запрос.Текст = ЕСНДоходыТекст;
		Запрос.Выполнить();
		
		
		// ДанныеОбИнвалидности
		//	Поля:
		//		Период
		//		Физлицо
		//
		// Описание:
		//	таблица списка периодов-физлиц которые являются инвалидами
		//
		ДанныеОбИнвалидностиТекст =
		"ВЫБРАТЬ
		|	&парамПериодРегистрации КАК Период,
		|	СведенияОбИнвалидностиФизлиц.Физлицо
		|ИЗ
		|	РегистрСведений.СведенияОбИнвалидностиФизлиц.СрезПоследних(&парамКонецПериодаРегистрации, ) КАК СведенияОбИнвалидностиФизлиц
		|ГДЕ
		|	СведенияОбИнвалидностиФизлиц.Инвалидность";
		
		
		// ЕСНДоходыИнвалидовТекст
		//	Поля:
		//		ФизЛицо
		//		ПериодРегистрации
		//		ДоходИнв... - доход инвалидов по каждому фонду
		//
		//	Описание:
		//	список доходов инвалидов по работникам-периодам регистрации, помещаем во временную таблицу ВТ_ЕСНДоходыИнвалидов
		//	
		
		ЕСНДоходыИнвалидовТекст =
		"ВЫБРАТЬ
		|	ЕСНДоходыОсновная.ФизЛицо КАК ФизЛицо,
		|	ЕСНДоходыОсновная.ПериодРегистрации КАК ПериодРегистрации,
		|	ГОД(ЕСНДоходыОсновная.ПериодРегистрации) КАК Год,
		|	СУММА(ВЫБОР
		|			КОГДА ДанныеОбИнвалидности.Физлицо ЕСТЬ НЕ NULL И ЕСНДоходыОсновная.КодДоходаЕСН.ВходитВБазуФедеральныйБюджет
		|				ТОГДА ЕСНДоходыОсновная.Результат
		|			ИНАЧЕ 0
		|		КОНЕЦ) КАК ДоходИнвФБ,
		|	СУММА(ВЫБОР
		|			КОГДА ДанныеОбИнвалидности.Физлицо ЕСТЬ НЕ NULL И ЕСНДоходыОсновная.КодДоходаЕСН.ВходитВБазуФОМС
		|				ТОГДА ЕСНДоходыОсновная.Результат
		|			ИНАЧЕ 0
		|		КОНЕЦ) КАК ДоходИнвФОМС,
		|	СУММА(ВЫБОР
		|			КОГДА ДанныеОбИнвалидности.Физлицо ЕСТЬ НЕ NULL И ЕСНДоходыОсновная.КодДоходаЕСН.ВходитВБазуФСС
		|				ТОГДА ЕСНДоходыОсновная.Результат
		|			ИНАЧЕ 0
		|		КОНЕЦ) КАК ДоходИнвФСС
		|ПОМЕСТИТЬ ВТ_ЕСНДоходыИнвалидов
		|ИЗ
		|	ВТ_ЕСНДоходы КАК ЕСНДоходыОсновная
		|		ЛЕВОЕ СОЕДИНЕНИЕ ("+ ДанныеОбИнвалидностиТекст +") КАК ДанныеОбИнвалидности
		|		ПО ДанныеОбИнвалидности.ФизЛицо = ЕСНДоходыОсновная.ФизЛицо
		|			И ДанныеОбИнвалидности.Период = ЕСНДоходыОсновная.ПериодРегистрации
		|
		|ГДЕ
		|	НЕ (ДанныеОбИнвалидности.ФизЛицо ЕСТЬ NULL)
		|
		|СГРУППИРОВАТЬ ПО
		|	ЕСНДоходыОсновная.ФизЛицо,
		|	ЕСНДоходыОсновная.ПериодРегистрации,
		|	ГОД(ЕСНДоходыОсновная.ПериодРегистрации),
		|	ДанныеОбИнвалидности.Физлицо
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ФизЛицо,
		|	ПериодРегистрации,
		|	Год";
		Запрос.Текст = ЕСНДоходыИнвалидовТекст;
		Запрос.Выполнить();
		
		
		// ЕСНДоходыБезСкидокИнвалидов
		//	Поля:
		//		ФизЛицо
		//		ПериодРегистрации
		//		ДоходБезСкидокИнв... - доход инвалидов по каждому фонду
		//
		//	Описание:
		//	список доходов инвалидов по работникам-периодам регистрации
		//	с учетом ограничения льготы по ст. 239 НК РФ
		//	
		ЕСНДоходыБезСкидокИнвалидовТекст = 
		"ВЫБРАТЬ
		|	ЕСНДоходыИнвалидов.ФизЛицо КАК Физлицо,
		|	ЕСНДоходыИнвалидов.ПериодРегистрации КАК ПериодРегистрации,
		|	ВЫБОР
		|		КОГДА СУММА(ЕСНДоходыИнвалидовНарастающимИтогом.ДоходИнвФБ) ЕСТЬ NULL 
		|			ТОГДА ВЫБОР
		|					КОГДА ЕСНДоходыИнвалидов.ДоходИнвФБ > 100000
		|						ТОГДА 100000
		|					ИНАЧЕ ЕСНДоходыИнвалидов.ДоходИнвФБ
		|				КОНЕЦ
		|		КОГДА СУММА(ЕСНДоходыИнвалидовНарастающимИтогом.ДоходИнвФБ) >= 100000
		|			ТОГДА 0
		|		КОГДА ЕСНДоходыИнвалидов.ДоходИнвФБ > 100000 - СУММА(ЕСНДоходыИнвалидовНарастающимИтогом.ДоходИнвФБ)
		|			ТОГДА 100000 - СУММА(ЕСНДоходыИнвалидовНарастающимИтогом.ДоходИнвФБ)
		|		ИНАЧЕ ЕСНДоходыИнвалидов.ДоходИнвФБ
		|	КОНЕЦ КАК ДоходБезСкидокИнвФБ,
		|	ВЫБОР
		|		КОГДА СУММА(ЕСНДоходыИнвалидовНарастающимИтогом.ДоходИнвФОМС) ЕСТЬ NULL 
		|			ТОГДА ВЫБОР
		|					КОГДА ЕСНДоходыИнвалидов.ДоходИнвФОМС > 100000
		|						ТОГДА 100000
		|					ИНАЧЕ ЕСНДоходыИнвалидов.ДоходИнвФОМС
		|				КОНЕЦ
		|		КОГДА СУММА(ЕСНДоходыИнвалидовНарастающимИтогом.ДоходИнвФОМС) >= 100000
		|			ТОГДА 0
		|		КОГДА ЕСНДоходыИнвалидов.ДоходИнвФОМС > 100000 - СУММА(ЕСНДоходыИнвалидовНарастающимИтогом.ДоходИнвФОМС)
		|			ТОГДА 100000 - СУММА(ЕСНДоходыИнвалидовНарастающимИтогом.ДоходИнвФОМС)
		|		ИНАЧЕ ЕСНДоходыИнвалидов.ДоходИнвФОМС
		|	КОНЕЦ КАК ДоходБезСкидокИнвФОМС,
		|	ВЫБОР
		|		КОГДА СУММА(ЕСНДоходыИнвалидовНарастающимИтогом.ДоходИнвФСС) ЕСТЬ NULL 
		|			ТОГДА ВЫБОР
		|					КОГДА ЕСНДоходыИнвалидов.ДоходИнвФСС > 100000
		|						ТОГДА 100000
		|					ИНАЧЕ ЕСНДоходыИнвалидов.ДоходИнвФСС
		|				КОНЕЦ
		|		КОГДА СУММА(ЕСНДоходыИнвалидовНарастающимИтогом.ДоходИнвФСС) >= 100000
		|			ТОГДА 0
		|		КОГДА ЕСНДоходыИнвалидов.ДоходИнвФСС > 100000 - СУММА(ЕСНДоходыИнвалидовНарастающимИтогом.ДоходИнвФСС)
		|			ТОГДА 100000 - СУММА(ЕСНДоходыИнвалидовНарастающимИтогом.ДоходИнвФСС)
		|		ИНАЧЕ ЕСНДоходыИнвалидов.ДоходИнвФСС
		|	КОНЕЦ КАК ДоходБезСкидокИнвФСС
		|ПОМЕСТИТЬ ВТ_ЕСНДоходыБезСкидокИнвалидов
		|ИЗ
		|	ВТ_ЕСНДоходыИнвалидов КАК ЕСНДоходыИнвалидов
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ЕСНДоходыИнвалидов КАК ЕСНДоходыИнвалидовНарастающимИтогом
		|		ПО ЕСНДоходыИнвалидов.ПериодРегистрации > ЕСНДоходыИнвалидовНарастающимИтогом.ПериодРегистрации
		|			И ЕСНДоходыИнвалидов.Год = ЕСНДоходыИнвалидовНарастающимИтогом.Год
		|			И ЕСНДоходыИнвалидов.ФизЛицо = ЕСНДоходыИнвалидовНарастающимИтогом.ФизЛицо
		|
		|СГРУППИРОВАТЬ ПО
		|	ЕСНДоходыИнвалидов.ФизЛицо,
		|	ЕСНДоходыИнвалидов.ДоходИнвФБ,
		|	ЕСНДоходыИнвалидов.ДоходИнвФОМС,
		|	ЕСНДоходыИнвалидов.ДоходИнвФСС,
		|	ЕСНДоходыИнвалидов.ПериодРегистрации
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Физлицо,
		|	ПериодРегистрации";
		Запрос.Текст = ЕСНДоходыБезСкидокИнвалидовТекст;
		Запрос.Выполнить();
		
		//	ЕСНДоходыОблагаемые
		//	Поля:
		//		ФизЛицо
		//		ПериодРегистрации
		//		ДоходБезСкидок ...   по каждому фонду
		//		ДоходБезСкидокИнв ...
		//
		//	Описание:
		//	таблица облагаемых доходов за месяц с учетом ограничения доходов инвалидов
		
		ЕСНДоходыОблагаемыеТекст = 
		"ВЫБРАТЬ
		|	ЕСНДоходыБезСкидок.ФизЛицо,
		|	ЕСНДоходыБезСкидок.ПериодРегистрации,
		|	ЕСНДоходыБезСкидок.ДоходБезСкидокФБ КАК ДоходБезСкидокФБ,
		|	ЕСНДоходыБезСкидок.ДоходБезСкидокФОМС КАК ДоходБезСкидокФОМС,
		|	ЕСНДоходыБезСкидок.ДоходБезСкидокФСС КАК ДоходБезСкидокФСС,
		|	ЕСТЬNULL(ЕСНДоходыБезСкидокИнвалидов.ДоходБезСкидокИнвФБ, 0) КАК ДоходБезСкидокИнвФБ,
		|	ЕСТЬNULL(ЕСНДоходыБезСкидокИнвалидов.ДоходБезСкидокИнвФОМС, 0) КАК ДоходБезСкидокИнвФОМС,
		|	ЕСТЬNULL(ЕСНДоходыБезСкидокИнвалидов.ДоходБезСкидокИнвФСС, 0) КАК ДоходБезСкидокИнвФСС
		|ИЗ
		|	(ВЫБРАТЬ
		|		ЕСНДоходыОсновная.ФизЛицо КАК ФизЛицо,
		|		ЕСНДоходыОсновная.ПериодРегистрации КАК ПериодРегистрации,
		|		СУММА(ВЫБОР
		|				КОГДА ЕСНДоходыОсновная.КодДоходаЕСН.ВходитВБазуФедеральныйБюджет
		|					ТОГДА ЕСНДоходыОсновная.Результат
		|				ИНАЧЕ 0
		|			КОНЕЦ) КАК ДоходБезСкидокФБ,
		|		СУММА(ВЫБОР
		|				КОГДА ЕСНДоходыОсновная.КодДоходаЕСН.ВходитВБазуФОМС
		|					ТОГДА ЕСНДоходыОсновная.Результат
		|				ИНАЧЕ 0
		|			КОНЕЦ) КАК ДоходБезСкидокФОМС,
		|		СУММА(ВЫБОР
		|				КОГДА ЕСНДоходыОсновная.КодДоходаЕСН.ВходитВБазуФСС
		|					ТОГДА ЕСНДоходыОсновная.Результат
		|				ИНАЧЕ 0
		|			КОНЕЦ) КАК ДоходБезСкидокФСС
		|	ИЗ
		|		ВТ_ЕСНДоходы КАК ЕСНДоходыОсновная
		|
		|	СГРУППИРОВАТЬ ПО
		|		ЕСНДоходыОсновная.ФизЛицо,
		|		ЕСНДоходыОсновная.ПериодРегистрации) КАК ЕСНДоходыБезСкидок
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ЕСНДоходыБезСкидокИнвалидов КАК ЕСНДоходыБезСкидокИнвалидов
		|		ПО ЕСНДоходыБезСкидок.ПериодРегистрации = ЕСНДоходыБезСкидокИнвалидов.ПериодРегистрации
		|			И ЕСНДоходыБезСкидок.ФизЛицо = ЕСНДоходыБезСкидокИнвалидов.ФизЛицо";
		
		
		// ЕСНДоходыЗаГод
		//	Поля:
		//		Сотрудник
		//		КодДоходаЕСН
		//		ПериодРегистрации
		//		Результат			- доход за период
		//		РезультатЗаГод		- доход за год нарастающим итогом
		//	Описание:
		//	соединение таблицы ЕСНДоходыЗаГод с самой собой для получения
		//	нарастающего итога
		//
		
		ЕСНДоходыЗаГодТекст = 
		"ВЫБРАТЬ
		|	ЕСНДоходыОсновная.Сотрудник,
		|	ЕСНДоходыОсновная.Физлицо,
		|	ЕСНДоходыОсновная.КодДоходаЕСН,
		|	ЕСНДоходыОсновная.ОблагаетсяЕНВД,
		|	ЕСНДоходыОсновная.ПериодРегистрации,
		|	ЕСНДоходыОсновная.Результат,
		|	СУММА(ЕСНДоходыВспомогательная.Результат) КАК РезультатЗаГод
		|ИЗ
		|	ВТ_ЕСНДоходы КАК ЕСНДоходыОсновная
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ЕСНДоходы КАК ЕСНДоходыВспомогательная
		|		ПО ЕСНДоходыВспомогательная.ПериодРегистрации <= ЕСНДоходыОсновная.ПериодРегистрации
		|			И ЕСНДоходыОсновная.Год = ЕСНДоходыВспомогательная.Год
		|			И ЕСНДоходыОсновная.Сотрудник = ЕСНДоходыВспомогательная.Сотрудник
		|			И ЕСНДоходыОсновная.КодДоходаЕСН = ЕСНДоходыВспомогательная.КодДоходаЕСН
		|			И ЕСНДоходыОсновная.ОблагаетсяЕНВД = ЕСНДоходыВспомогательная.ОблагаетсяЕНВД
		|
		|СГРУППИРОВАТЬ ПО
		|	ЕСНДоходыОсновная.Сотрудник,
		|	ЕСНДоходыОсновная.Физлицо,
		|	ЕСНДоходыОсновная.КодДоходаЕСН,
		|	ЕСНДоходыОсновная.ОблагаетсяЕНВД,
		|	ЕСНДоходыОсновная.Результат,
		|	ЕСНДоходыОсновная.ПериодРегистрации";
		
		
		// ЕСНДоходыПоВР
		//	Поля:
		//		ОбособленноеПодразделение
		//		Сотрудник
		//		ФизЛицо
		//		ВидРасчета
		//		КодДоходаЕСН
		//		ОблагаетсяЕНВД
		//		ПериодРегистрации
		//		ПериодДействия
		//		Результат
		//
		// Описание:
		//	список доходов по кодам ЕСН в разрезе видов расчета
		//
		
		ЕСНДоходыПоВРТекст =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	Доходы.ОбособленноеПодразделение КАК ОбособленноеПодразделение,
		|	Доходы.Сотрудник КАК Сотрудник,
		|	Доходы.ФизЛицо КАК ФизЛицо,
		|	Доходы.ВидРасчета КАК ВидРасчета,
		|	Доходы.КодДоходаЕСН,
		|	Доходы.ОблагаетсяЕНВД КАК ОблагаетсяЕНВД,
		|	Доходы.ПериодРегистрации КАК ПериодРегистрации,
		|	Доходы.ПериодДействия КАК ПериодДействия,
		|	СУММА(Доходы.Результат) КАК Результат
		|ПОМЕСТИТЬ ВТ_ЕСНДоходыПоВР
		|ИЗ
		|	(ВЫБРАТЬ
		|		ЕСНОсновныеНачисления.ОбособленноеПодразделение КАК ОбособленноеПодразделение,
		|		ЕСНОсновныеНачисления.Сотрудник КАК Сотрудник,
		|		ЕСНОсновныеНачисления.ФизЛицо КАК ФизЛицо,
		|		ЕСНОсновныеНачисления.ВидРасчета КАК ВидРасчета,
		|		ЕСНОсновныеНачисления.КодДоходаЕСН КАК КодДоходаЕСН,
		|		ЕСНОсновныеНачисления.ОблагаетсяЕНВД КАК ОблагаетсяЕНВД,
		|		ЕСНОсновныеНачисления.ПериодРегистрации КАК ПериодРегистрации,
		|		ЕСНОсновныеНачисления.ПериодДействия КАК ПериодДействия,
		|		ЕСНОсновныеНачисления.Результат КАК Результат
		|	ИЗ
		|		РегистрРасчета.ЕСНОсновныеНачисления КАК ЕСНОсновныеНачисления
		|	ГДЕ
		|		ЕСНОсновныеНачисления.ПериодРегистрации = &парамПериодРегистрации
		|		И (ЕСНОсновныеНачисления.КодДоходаЕСН.ВходитВБазуФОМС
		|				ИЛИ ЕСНОсновныеНачисления.КодДоходаЕСН.ВходитВБазуФСС
		|				ИЛИ ЕСНОсновныеНачисления.КодДоходаЕСН.ВходитВБазуФедеральныйБюджет)
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		ЕСНДополнительныеНачисления.ОбособленноеПодразделение,
		|		ЕСНДополнительныеНачисления.Сотрудник,
		|		ЕСНДополнительныеНачисления.ФизЛицо,
		|		ЕСНДополнительныеНачисления.ВидРасчета,
		|		ЕСНДополнительныеНачисления.КодДоходаЕСН,
		|		ЕСНДополнительныеНачисления.ОблагаетсяЕНВД,
		|		ЕСНДополнительныеНачисления.ПериодРегистрации,
		|		ЕСНДополнительныеНачисления.ПериодРегистрации,
		|		ЕСНДополнительныеНачисления.Результат - ЕСНДополнительныеНачисления.Скидка
		|	ИЗ
		|		РегистрРасчета.ЕСНДополнительныеНачисления КАК ЕСНДополнительныеНачисления
		|	ГДЕ
		|		ЕСНДополнительныеНачисления.ПериодРегистрации = &парамПериодРегистрации
		|		И (ЕСНДополнительныеНачисления.КодДоходаЕСН.ВходитВБазуФОМС
		|				ИЛИ ЕСНДополнительныеНачисления.КодДоходаЕСН.ВходитВБазуФСС
		|				ИЛИ ЕСНДополнительныеНачисления.КодДоходаЕСН.ВходитВБазуФедеральныйБюджет)) КАК Доходы
		|
		|СГРУППИРОВАТЬ ПО
		|	Доходы.ОбособленноеПодразделение,
		|	Доходы.Сотрудник,
		|	Доходы.ВидРасчета,
		|	Доходы.КодДоходаЕСН,
		|	Доходы.ПериодРегистрации,
		|	Доходы.ПериодДействия,
		|	Доходы.ОблагаетсяЕНВД,
		|	Доходы.ФизЛицо
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ОбособленноеПодразделение,
		|	Сотрудник,
		|	ОблагаетсяЕНВД,
		|	ВидРасчета,
		|	ПериодРегистрации,
		|	ПериодДействия";
		
		Запрос.Текст = ЕСНДоходыПоВРТекст;
		Запрос.Выполнить();
		
		//	ЕСНДоходыУУДоходы
		//	Поля:
		//		ОбособленноеПодразделение
		//		Сотрудник
		//		Физлицо
		//		ВидРасчета
		//		ПериодРегистрации
		//		ПериодДействия
		//		КодДоходаЕСН
		//		ОблагаетсяЕНВД
		//		РезультатЕСН
		//		Подразделение
		//		СтатьяЗатрат
		//		НоменклатурнаяГруппа
		//		ОбъектСтроительства
		//		РезультатУУ
		//	Описание:
		//	соединение таблиц с доходами в разрезе кодов дохода ЕСН и в 
		//	разрезе отражения в упручете
		//	сопоставление выполняется по видам расчета
		//
		
		ЕСНДоходыУУДоходыТекст =
		"ВЫБРАТЬ
		|	ЕСНДоходыПоВР.ОбособленноеПодразделение,
		|	ЕСНДоходыПоВР.Сотрудник,
		|	ЕСНДоходыПоВР.Физлицо,
		|	ЕСНДоходыПоВР.ВидРасчета,
		|	ЕСНДоходыПоВР.ПериодРегистрации,
		|	ЕСНДоходыПоВР.ПериодДействия,
		|	ЕСНДоходыПоВР.КодДоходаЕСН,
		|	ЕСНДоходыПоВР.ОблагаетсяЕНВД,
		|	ЕСНДоходыПоВР.Результат КАК РезультатЕСН,
		|	УУДоходыПоВР.Подразделение,
		|	УУДоходыПоВР.СтатьяЗатрат,
		|	УУДоходыПоВР.НоменклатурнаяГруппа,
		|	УУДоходыПоВР.ОбъектСтроительства,
		|	УУДоходыПоВР.Результат КАК РезультатУУ
		|ИЗ
		|	ВТ_ЕСНДоходыПоВР КАК ЕСНДоходыПоВР
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_УУДоходыПоВР КАК УУДоходыПоВР
		|		ПО ЕСНДоходыПоВР.Сотрудник = УУДоходыПоВР.Сотрудник
		|			И ЕСНДоходыПоВР.ОбособленноеПодразделение = УУДоходыПоВР.ОбособленноеПодразделение
		|			И ЕСНДоходыПоВР.ОблагаетсяЕНВД = УУДоходыПоВР.ЭтоСтатьяЗатратЕНВД
		|			И ЕСНДоходыПоВР.ВидРасчета = УУДоходыПоВР.ВидРасчета
		|			И ЕСНДоходыПоВР.ПериодДействия = УУДоходыПоВР.ПериодДействия
		|			И ЕСНДоходыПоВР.ПериодРегистрации = УУДоходыПоВР.ПериодРегистрации";
		
		// БУДоходыСводПоВР
		//	Поля:
		//		Сотрудник
		//		ВидРасчета
		//		ПериодРегистрации
		//		Результат
		//
		// Описание:
		//	доходы, отраженные в бухучете, сведенные по видам расчета
		//
		
		БУДоходыСводПоВРТекст =
		"ВЫБРАТЬ
		|	Доходы.Сотрудник,
		|	Доходы.ФизЛицо,
		|	Доходы.ВидРасчета,
		|	Доходы.ПериодРегистрации,
		|	Доходы.ПериодДействия,
		|	СУММА(Доходы.Результат) КАК Результат
		|ИЗ
		|	(ВЫБРАТЬ
		|		ДоходыПоВР.Сотрудник КАК Сотрудник,
		|		ДоходыПоВР.ФизЛицо КАК ФизЛицо,
		|		ДоходыПоВР.ВидРасчета КАК ВидРасчета,
		|		ДоходыПоВР.ПериодРегистрации КАК ПериодРегистрации,
		|		ДоходыПоВР.ПериодДействия КАК ПериодДействия,
		|		ДоходыПоВР.Результат КАК Результат
		|	ИЗ
		|		ВТ_УУДоходыПоВР КАК ДоходыПоВР) КАК Доходы
		|
		|СГРУППИРОВАТЬ ПО
		|	Доходы.Сотрудник,
		|	Доходы.ФизЛицо,
		|	Доходы.ВидРасчета,
		|	Доходы.ПериодРегистрации,
		|	Доходы.ПериодДействия";
		
		
		// КоэффициентыОтраженияЕСНТекст
		//	Поля:
		//		ФизЛицо
		//		ПериодРегистрации
		//		ПериодДействия
		//		Подразделение
		//		СтатьяЗатрат
		//		НоменклатурнаяГруппа
		//		ОбъектСтроительства
		//		БазаФедеральныйБюджет
		//		БазаФСС
		//		БазаФОМС
		//		БазаПФР_ЕНВД
		//		БазаПФР_НеЕНВД
		//		БазаФСС_БЛ
		// Описание:
		//	Таблица коэффициентов отражения составных частей исчисленного ЕСН по счетам упручета
		//
		КоэффициентыОтраженияЕСНТекст =
		"ВЫБРАТЬ
		|	ЕСНДоходыУУДоходы.ФизЛицо,
		|	ЕСНДоходыУУДоходы.ПериодРегистрации,
		|	ЕСНДоходыУУДоходы.ПериодДействия,
		|	ЕСНДоходыУУДоходы.Подразделение,
		|	ЕСНДоходыУУДоходы.СтатьяЗатрат,
		|	ЕСНДоходыУУДоходы.НоменклатурнаяГруппа,
		|	ЕСНДоходыУУДоходы.ОбъектСтроительства,
		|	ВЫБОР
		|		КОГДА ЕСНДоходыУУДоходы.ОблагаетсяЕНВД
		|				ИЛИ УчетнаяПолитикаНалоговыйУчетУСН.УСН ЕСТЬ НЕ NULL
		|				ИЛИ НЕ ЕСНДоходыУУДоходы.КодДоходаЕСН.ВходитВБазуФедеральныйБюджет
		|				ИЛИ ЕСНДоходыЗаГод.Результат = 0
		|				ИЛИ БУДоходыСводПоВР.Результат = 0
		|				ИЛИ ЕСНДоходыОблагаемые.ДоходБезСкидокФБ = 0
		|			ТОГДА 0
		|		ИНАЧЕ ЕСНДоходыУУДоходы.РезультатЕСН / БУДоходыСводПоВР.Результат * ЕСНДоходыУУДоходы.РезультатУУ * (ЕСНДоходыОблагаемые.ДоходБезСкидокФБ - ЕСНДоходыОблагаемые.ДоходБезСкидокИнвФБ) / ЕСНДоходыОблагаемые.ДоходБезСкидокФБ
		|	КОНЕЦ КАК БазаФедеральныйБюджет,
		|	ВЫБОР
		|		КОГДА ЕСНДоходыУУДоходы.ОблагаетсяЕНВД
		|				ИЛИ УчетнаяПолитикаНалоговыйУчетУСН.УСН ЕСТЬ НЕ NULL
		|				ИЛИ НЕ ЕСНДоходыУУДоходы.КодДоходаЕСН.ВходитВБазуФСС
		|				ИЛИ ЕСНДоходыЗаГод.Результат = 0
		|				ИЛИ БУДоходыСводПоВР.Результат = 0
		|				ИЛИ ЕСНДоходыОблагаемые.ДоходБезСкидокФСС = 0
		|			ТОГДА 0
		|		ИНАЧЕ ЕСНДоходыУУДоходы.РезультатЕСН / БУДоходыСводПоВР.Результат * ЕСНДоходыУУДоходы.РезультатУУ * (ЕСНДоходыОблагаемые.ДоходБезСкидокФСС - ЕСНДоходыОблагаемые.ДоходБезСкидокИнвФСС) / ЕСНДоходыОблагаемые.ДоходБезСкидокФСС
		|	КОНЕЦ КАК БазаФСС,
		|	ВЫБОР
		|		КОГДА ЕСНДоходыУУДоходы.ОблагаетсяЕНВД
		|				ИЛИ УчетнаяПолитикаНалоговыйУчетУСН.УСН ЕСТЬ НЕ NULL
		|				ИЛИ НЕ ЕСНДоходыУУДоходы.КодДоходаЕСН.ВходитВБазуФОМС
		|				ИЛИ ЕСНДоходыЗаГод.Результат = 0
		|				ИЛИ БУДоходыСводПоВР.Результат = 0
		|				ИЛИ ЕСНДоходыОблагаемые.ДоходБезСкидокФОМС = 0
		|			ТОГДА 0
		|		ИНАЧЕ ЕСНДоходыУУДоходы.РезультатЕСН / БУДоходыСводПоВР.Результат * ЕСНДоходыУУДоходы.РезультатУУ * (ЕСНДоходыОблагаемые.ДоходБезСкидокФОМС - ЕСНДоходыОблагаемые.ДоходБезСкидокИнвФОМС) / ЕСНДоходыОблагаемые.ДоходБезСкидокФОМС
		|	КОНЕЦ КАК БазаФОМС,
		|	ВЫБОР
		|		КОГДА НЕ ЕСНДоходыУУДоходы.ОблагаетсяЕНВД
		|				ИЛИ НЕ ЕСНДоходыУУДоходы.КодДоходаЕСН.ВходитВБазуФедеральныйБюджет
		|				ИЛИ ЕСНДоходыЗаГод.Результат = 0
		|				ИЛИ БУДоходыСводПоВР.Результат = 0
		|				ИЛИ ЕСНДоходыОблагаемые.ДоходБезСкидокФБ = 0
		|			ТОГДА 0
		|		ИНАЧЕ ЕСНДоходыУУДоходы.РезультатЕСН / БУДоходыСводПоВР.Результат * ЕСНДоходыУУДоходы.РезультатУУ
		|	КОНЕЦ КАК БазаПФР_ЕНВД,
		|	ВЫБОР
		|		КОГДА ЕСНДоходыУУДоходы.ОблагаетсяЕНВД
		|				ИЛИ НЕ ЕСНДоходыУУДоходы.КодДоходаЕСН.ВходитВБазуФедеральныйБюджет
		|				ИЛИ ЕСНДоходыЗаГод.Результат = 0
		|				ИЛИ БУДоходыСводПоВР.Результат = 0
		|				ИЛИ ЕСНДоходыОблагаемые.ДоходБезСкидокФБ = 0
		|			ТОГДА 0
		|		ИНАЧЕ ЕСНДоходыУУДоходы.РезультатЕСН / БУДоходыСводПоВР.Результат * ЕСНДоходыУУДоходы.РезультатУУ
		|	КОНЕЦ КАК БазаПФР_НеЕНВД,
		|	ВЫБОР
		|		КОГДА (НЕ ЕСНДоходыУУДоходы.ОблагаетсяЕНВД И УчетнаяПолитикаНалоговыйУчетУСН.УСН ЕСТЬ NULL)
		|				ИЛИ НЕ УчетнаяПолитикаНалоговыйУчетУплачиватьДобровольныеВзносыВФСС.УплачиватьДобровольныеВзносыВФСС
		|				ИЛИ НЕ ЕСНДоходыУУДоходы.КодДоходаЕСН.ВходитВБазуФедеральныйБюджет
		|				ИЛИ ЕСНДоходыЗаГод.Результат = 0
		|				ИЛИ БУДоходыСводПоВР.Результат = 0
		|				ИЛИ ЕСНДоходыОблагаемые.ДоходБезСкидокФБ = 0
		|			ТОГДА 0
		|		ИНАЧЕ ЕСНДоходыУУДоходы.РезультатЕСН / БУДоходыСводПоВР.Результат * ЕСНДоходыУУДоходы.РезультатУУ
		|	КОНЕЦ КАК БазаФСС_БЛ
		|ИЗ
		|	("+ ЕСНДоходыУУДоходыТекст +") КАК ЕСНДоходыУУДоходы
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_УчетнаяПолитикаНалоговыйУчет КАК УчетнаяПолитикаНалоговыйУчетУСН
		|		ПО (ЕСНДоходыУУДоходы.ПериодРегистрации = УчетнаяПолитикаНалоговыйУчетУСН.Месяц)
		|			И ЕСНДоходыУУДоходы.ОбособленноеПодразделение = УчетнаяПолитикаНалоговыйУчетУСН.УСН
		|		ЛЕВОЕ СОЕДИНЕНИЕ ("+УчетнаяПолитикаНалоговыйУчетУплачиватьДобровольныеВзносыВФСС+") КАК УчетнаяПолитикаНалоговыйУчетУплачиватьДобровольныеВзносыВФСС
		|		ПО (ЕСНДоходыУУДоходы.ПериодРегистрации = УчетнаяПолитикаНалоговыйУчетУплачиватьДобровольныеВзносыВФСС.Месяц)
		|			И ЕСНДоходыУУДоходы.ОбособленноеПодразделение = УчетнаяПолитикаНалоговыйУчетУплачиватьДобровольныеВзносыВФСС.Организация
		|		ЛЕВОЕ СОЕДИНЕНИЕ ("+ БУДоходыСводПоВРТекст +") КАК БУДоходыСводПоВР
		|		ПО ЕСНДоходыУУДоходы.Сотрудник = БУДоходыСводПоВР.Сотрудник
		|			И ЕСНДоходыУУДоходы.ВидРасчета = БУДоходыСводПоВР.ВидРасчета
		|			И ЕСНДоходыУУДоходы.ПериодРегистрации = БУДоходыСводПоВР.ПериодРегистрации
		|			И ЕСНДоходыУУДоходы.ПериодДействия = БУДоходыСводПоВР.ПериодДействия
		|		ЛЕВОЕ СОЕДИНЕНИЕ ("+ ЕСНДоходыЗаГодТекст +") КАК ЕСНДоходыЗаГод
		|		ПО ЕСНДоходыУУДоходы.Сотрудник = ЕСНДоходыЗаГод.Сотрудник
		|			И ЕСНДоходыУУДоходы.КодДоходаЕСН = ЕСНДоходыЗаГод.КодДоходаЕСН
		|			И ЕСНДоходыУУДоходы.ОблагаетсяЕНВД = ЕСНДоходыЗаГод.ОблагаетсяЕНВД
		|			И ЕСНДоходыУУДоходы.ПериодРегистрации = ЕСНДоходыЗаГод.ПериодРегистрации
		|		ЛЕВОЕ СОЕДИНЕНИЕ ("+ ЕСНДоходыОблагаемыеТекст +") КАК ЕСНДоходыОблагаемые
		|		ПО ЕСНДоходыУУДоходы.ФизЛицо = ЕСНДоходыОблагаемые.ФизЛицо
		|			И ЕСНДоходыУУДоходы.ПериодРегистрации = ЕСНДоходыОблагаемые.ПериодРегистрации";
		
		
		// КоэффициентыОтраженияЕСНБезПДТекст
		//	Поля:
		//		ФизЛицо
		//		ПериодРегистрации
		//		ПериодДействия
		//		Подразделение
		//		СтатьяЗатрат
		//		НоменклатурнаяГруппа
		//		ОбъектСтроительства
		//		БазаФедеральныйБюджет
		//		БазаФСС
		//		БазаФОМС
		//		БазаПФР_ЕНВД
		//		БазаПФР_НеЕНВД
		//		БазаФСС_БЛ
		// Описание:
		// таблица коэффициентов отражения составных частей исчисленного ЕСН по счетам бухучета - группировка запроса ОтражениеЕСНТекст
		
		КоэффициентыОтраженияЕСНБезПДТекст =
		"ВЫБРАТЬ
		|	ЕСНДоходыУУДоходы.ФизЛицо,
		|	ЕСНДоходыУУДоходы.ПериодРегистрации,
		|	ЕСНДоходыУУДоходы.ПериодДействия,
		|	ЗНАЧЕНИЕ(Перечисление.КатегорииЗатраты.Зарплата) КАК КатегорияЗатраты,
		|	ЕСНДоходыУУДоходы.Подразделение,
		|	ЕСНДоходыУУДоходы.СтатьяЗатрат,
		|	ЕСНДоходыУУДоходы.НоменклатурнаяГруппа,
		|	ЕСНДоходыУУДоходы.ОбъектСтроительства,
		|	СУММА(ЕСНДоходыУУДоходы.БазаФедеральныйБюджет) КАК БазаФедеральныйБюджет,
		|	СУММА(ЕСНДоходыУУДоходы.БазаФСС) КАК БазаФСС,
		|	СУММА(ЕСНДоходыУУДоходы.БазаФОМС) КАК БазаФОМС,
		|	СУММА(ЕСНДоходыУУДоходы.БазаПФР_ЕНВД) КАК БазаПФР_ЕНВД,
		|	СУММА(ЕСНДоходыУУДоходы.БазаПФР_НеЕНВД) КАК БазаПФР_НеЕНВД,
		|	СУММА(ЕСНДоходыУУДоходы.БазаФСС_БЛ) КАК БазаФСС_БЛ
		|ИЗ
		|	("+ КоэффициентыОтраженияЕСНТекст +") КАК ЕСНДоходыУУДоходы
		|
		|СГРУППИРОВАТЬ ПО
		|	ЕСНДоходыУУДоходы.ФизЛицо,
		|	ЕСНДоходыУУДоходы.ПериодРегистрации,
		|	ЕСНДоходыУУДоходы.ПериодДействия,
		|	ЗНАЧЕНИЕ(Перечисление.КатегорииЗатраты.Зарплата),
		|	ЕСНДоходыУУДоходы.Подразделение,
		|	ЕСНДоходыУУДоходы.СтатьяЗатрат,
		|	ЕСНДоходыУУДоходы.НоменклатурнаяГруппа,
		|	ЕСНДоходыУУДоходы.ОбъектСтроительства
		|
		|УПОРЯДОЧИТЬ ПО
		|	ЕСНДоходыУУДоходы.Физлицо,
		|	ЕСНДоходыУУДоходы.ПериодРегистрации,
		|	ЕСНДоходыУУДоходы.Подразделение,
		|	ЕСНДоходыУУДоходы.СтатьяЗатрат
		|АВТОУПОРЯДОЧИВАНИЕ";
		
		Запрос.Текст = КоэффициентыОтраженияЕСНБезПДТекст;
		
		ОтражениеВБУБезПеридовДействия = Запрос.Выполнить().Выгрузить();
		ОтражениеВБУБезПеридовДействия.Индексы.Добавить("ФизЛицо, ПериодРегистрации");
		
		
		// ЕСНИсчисленный
		//	Поля:
		//		ФизЛицо
		//		ПериодРегистрации
		//		ФедеральныйБюджет
		//		ФСС
		//		ФФОМС
		//		ТФОМС
		//		ПФРНакопительная_НеЕНВД - в части видов деятельности, не облагаемых ЕНВД
		//		ПФРСтраховая_НеЕНВД 
		//		ПФРНакопительная_ЕНВД - в части видов деятельности, облагаемых ЕНВД     
		//		ПФРСтраховая_ЕНВД
		// Описание:
		// просуммированный исчисленный ЕСН и страховые взносы в ПФР в разрезе физлиц
		//
		
		ЕСНИсчисленныйТекст =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ЕСНПоРаботникам.ФизЛицо,
		|	ЕСНПоРаботникам.ПериодРегистрации,
		|	СУММА(ЕСНПоРаботникам.ФедеральныйБюджет) КАК ФедеральныйБюджет,
		|	СУММА(ЕСНПоРаботникам.ФСС) КАК ФСС,
		|	СУММА(ЕСНПоРаботникам.ФФОМС) КАК ФФОМС,
		|	СУММА(ЕСНПоРаботникам.ТФОМС) КАК ТФОМС,
		|	СУММА(ЕСНПоРаботникам.ПФРНакопительная - ЕСНПоРаботникам.ПФРНакопительнаяЕНВД) КАК ПФРНакопительная_НеЕНВД,
		|	СУММА(ЕСНПоРаботникам.ПФРСтраховая - ЕСНПоРаботникам.ПФРСтраховаяЕНВД) КАК ПФРСтраховая_НеЕНВД,
		|	СУММА(ЕСНПоРаботникам.ПФРНакопительнаяЕНВД) КАК ПФРНакопительная_ЕНВД,
		|	СУММА(ЕСНПоРаботникам.ПФРСтраховаяЕНВД) КАК ПФРСтраховая_ЕНВД
		|ИЗ
		|	(ВЫБРАТЬ
		|		ЕСНИсчисленный.ФизЛицо КАК ФизЛицо,
		|		НАЧАЛОПЕРИОДА(ЕСНИсчисленный.Период, МЕСЯЦ) КАК ПериодРегистрации,
		|		ВЫБОР
		|			КОГДА УчетнаяПолитикаНалоговыйУчетУСН.УСН
		|				ТОГДА 0
		|			ИНАЧЕ СУММА(ЕСНИсчисленный.ФедеральныйБюджет - ЕСНИсчисленный.ПФРНакопительная - ЕСНИсчисленный.ПФРСтраховая + ЕСНИсчисленный.ПФРНакопительнаяЕНВД + ЕСНИсчисленный.ПФРСтраховаяЕНВД - ЕСТЬNULL(ЕСНДоходыБезСкидокИнвалидов.ДоходБезСкидокИнвФБ, 0) / 100 * (СведенияОСтавкахЕСНиПФР.СтавкаФедеральныйБюджет - ВЫБОР
		|						КОГДА ГОД(ЕСНИсчисленный.ФизЛицо.ДатаРождения) < 1967
		|							ТОГДА СведенияОСтавкахЕСНиПФР.СтавкаПФРНакопительная1 + СведенияОСтавкахЕСНиПФР.СтавкаПФРСтраховая1
		|						ИНАЧЕ СведенияОСтавкахЕСНиПФР.СтавкаПФРНакопительная2 + СведенияОСтавкахЕСНиПФР.СтавкаПФРСтраховая2
		|					КОНЕЦ))
		|		КОНЕЦ КАК ФедеральныйБюджет,
		|		ВЫБОР
		|			КОГДА УчетнаяПолитикаНалоговыйУчетУСН.УСН
		|				ТОГДА 0
		|			ИНАЧЕ СУММА(ЕСНИсчисленный.ФСС - ЕСТЬNULL(ЕСНДоходыБезСкидокИнвалидов.ДоходБезСкидокИнвФСС, 0) * СведенияОСтавкахЕСНиПФР.СтавкаФСС / 100)
		|		КОНЕЦ КАК ФСС,
		|		ВЫБОР
		|			КОГДА УчетнаяПолитикаНалоговыйУчетУСН.УСН
		|				ТОГДА 0
		|			ИНАЧЕ СУММА(ЕСНИсчисленный.ФФОМС - ЕСТЬNULL(ЕСНДоходыБезСкидокИнвалидов.ДоходБезСкидокИнвФОМС, 0) * СведенияОСтавкахЕСНиПФР.СтавкаФФОМС / 100)
		|		КОНЕЦ КАК ФФОМС,
		|		ВЫБОР
		|			КОГДА УчетнаяПолитикаНалоговыйУчетУСН.УСН
		|				ТОГДА 0
		|			ИНАЧЕ СУММА(ЕСНИсчисленный.ТФОМС - ЕСТЬNULL(ЕСНДоходыБезСкидокИнвалидов.ДоходБезСкидокИнвФОМС, 0) * СведенияОСтавкахЕСНиПФР.СтавкаТФОМС / 100)
		|		КОНЕЦ КАК ТФОМС,
		|		СУММА(ЕСНИсчисленный.ПФРНакопительная) КАК ПФРНакопительная,
		|		СУММА(ЕСНИсчисленный.ПФРСтраховая) КАК ПФРСтраховая,
		|		СУММА(ЕСНИсчисленный.ПФРНакопительнаяЕНВД) КАК ПФРНакопительнаяЕНВД,
		|		СУММА(ЕСНИсчисленный.ПФРСтраховаяЕНВД) КАК ПФРСтраховаяЕНВД
		|	ИЗ
		|		РегистрНакопления.ЕСНИсчисленный КАК ЕСНИсчисленный
		|			ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ЕСНДоходыБезСкидокИнвалидов КАК ЕСНДоходыБезСкидокИнвалидов
		|			ПО ЕСНИсчисленный.Период = ЕСНДоходыБезСкидокИнвалидов.ПериодРегистрации
		|				И ЕСНИсчисленный.ФизЛицо = ЕСНДоходыБезСкидокИнвалидов.Физлицо
		|			ЛЕВОЕ СОЕДИНЕНИЕ ВТ_УчетнаяПолитикаНалоговыйУчет КАК УчетнаяПолитикаНалоговыйУчетУСН
		|			ПО ЕСНИсчисленный.Период = УчетнаяПолитикаНалоговыйУчетУСН.Месяц
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_СведенияОСтавкахЕСНиПФР КАК СведенияОСтавкахЕСНиПФР
		|			ПО ЕСНИсчисленный.Организация = СведенияОСтавкахЕСНиПФР.Организация
		|	ГДЕ
		|		ЕСНИсчисленный.Период = &парамПериодРегистрации
		|	
		|	СГРУППИРОВАТЬ ПО
		|		УчетнаяПолитикаНалоговыйУчетУСН.УСН,
		|		ЕСНИсчисленный.ФизЛицо,
		|		НАЧАЛОПЕРИОДА(ЕСНИсчисленный.Период, МЕСЯЦ)) КАК ЕСНПоРаботникам
		|
		|СГРУППИРОВАТЬ ПО
		|	ЕСНПоРаботникам.ФизЛицо,
		|	ЕСНПоРаботникам.ПериодРегистрации";
		
		Запрос.Текст = ЕСНИсчисленныйТекст;
		
		Отбор = Новый Структура("ФизЛицо");
		
		МетодНачисленийСВыделениемНалогов = мМетодПланированияУчетаЗатрат = Перечисления.МетодыПланированияУчетаЗатратНаПерсонал.МетодНачисленийСВыделениемНалогов;
		
		Выборка = Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл
			
			Отбор.ФизЛицо				= Выборка.ФизЛицо;
			
			СтрокиОтраженияПоФизЛицу	= ОтражениеВБУБезПеридовДействия.НайтиСтроки(Отбор);
			
			// ФедеральныйБюджет
			Коэффициенты = ВыделитьКоэффициентыОтраженияИзМассиваСтрок(СтрокиОтраженияПоФизЛицу,"БазаФедеральныйБюджет");
			РезультатыФБ = ОбщегоНазначенияЗК.РаспределитьПропорционально(Выборка.ФедеральныйБюджет, Коэффициенты);
			
			// ФСС
			Коэффициенты = ВыделитьКоэффициентыОтраженияИзМассиваСтрок(СтрокиОтраженияПоФизЛицу,"БазаФСС");
			РезультатыФСС = ОбщегоНазначенияЗК.РаспределитьПропорционально(Выборка.ФСС, Коэффициенты);
			
			// ФОМС
			Коэффициенты = ВыделитьКоэффициентыОтраженияИзМассиваСтрок(СтрокиОтраженияПоФизЛицу,"БазаФОМС");
			РезультатыФФОМС = ОбщегоНазначенияЗК.РаспределитьПропорционально(Выборка.ФФОМС, Коэффициенты);
			РезультатыТФОМС = ОбщегоНазначенияЗК.РаспределитьПропорционально(Выборка.ТФОМС, Коэффициенты);
			
			// ПФР по видам деятельности, не облагаемым ЕНВД
			Коэффициенты = ВыделитьКоэффициентыОтраженияИзМассиваСтрок(СтрокиОтраженияПоФизЛицу,"БазаПФР_НеЕНВД");
			РезультатыПФРНакопительная_НеЕНВД = ОбщегоНазначенияЗК.РаспределитьПропорционально(Выборка.ПФРНакопительная_НеЕНВД, Коэффициенты);
			РезультатыПФРСтраховая_НеЕНВД = ОбщегоНазначенияЗК.РаспределитьПропорционально(Выборка.ПФРСтраховая_НеЕНВД, Коэффициенты);
			
			// ПФР по видам деятельности, облагаемым ЕНВД
			Коэффициенты = ВыделитьКоэффициентыОтраженияИзМассиваСтрок(СтрокиОтраженияПоФизЛицу,"БазаПФР_ЕНВД");
			РезультатыПФРНакопительная_ЕНВД = ОбщегоНазначенияЗК.РаспределитьПропорционально(Выборка.ПФРНакопительная_ЕНВД, Коэффициенты);
			РезультатыПФРСтраховая_ЕНВД = ОбщегоНазначенияЗК.РаспределитьПропорционально(Выборка.ПФРСтраховая_ЕНВД, Коэффициенты);
			
			Индекс = 0;
			Для Каждого СтрокаОтражения Из СтрокиОтраженияПоФизЛицу Цикл
				//////////////////////////////////////////////////////////
				// ЕСН
				
				Если РезультатыФБ <> Неопределено Тогда
					
					СтрокаТЧ = ПромежуточнаяТаблица.Добавить();
					ЗаполнитьРеквизитыСтроки(СтрокаТЧ, СтрокаОтражения, МетодНачисленийСВыделениемНалогов, Перечисления.КатегорииЗатраты.ЕСН);
					
					// ФедеральныйБюджет
					СтрокаТЧ.Сумма = РезультатыФБ[Индекс];
				КонецЕсли;
				
				Если РезультатыФСС <> Неопределено Тогда
					СтрокаТЧ = ПромежуточнаяТаблица.Добавить();
					ЗаполнитьРеквизитыСтроки(СтрокаТЧ, СтрокаОтражения, МетодНачисленийСВыделениемНалогов, Перечисления.КатегорииЗатраты.ЕСН);
					
					// ФСС
					СтрокаТЧ.Сумма			= РезультатыФСС[Индекс];
				КонецЕсли;
				
				Если РезультатыФФОМС <> Неопределено Тогда
					СтрокаТЧ = ПромежуточнаяТаблица.Добавить();
					ЗаполнитьРеквизитыСтроки(СтрокаТЧ, СтрокаОтражения, МетодНачисленийСВыделениемНалогов, Перечисления.КатегорииЗатраты.ЕСН);
					
					// ФФОМС
					СтрокаТЧ.Сумма			= РезультатыФФОМС[Индекс];
				КонецЕсли;
				
				Если РезультатыТФОМС <> Неопределено Тогда
					СтрокаТЧ = ПромежуточнаяТаблица.Добавить();
					ЗаполнитьРеквизитыСтроки(СтрокаТЧ, СтрокаОтражения, МетодНачисленийСВыделениемНалогов, Перечисления.КатегорииЗатраты.ЕСН);
					
					// ТФОМС
					СтрокаТЧ.Сумма			= РезультатыТФОМС[Индекс];
				КонецЕсли;
				
				//////////////////////////////////////////////////////////
				// ПФР
				
				Если РезультатыПФРНакопительная_НеЕНВД <> Неопределено Тогда
					СтрокаТЧ = ПромежуточнаяТаблица.Добавить();
					ЗаполнитьРеквизитыСтроки(СтрокаТЧ, СтрокаОтражения, МетодНачисленийСВыделениемНалогов, Перечисления.КатегорииЗатраты.ЕСН);
					
					// ПФРНакопительная
					СтрокаТЧ.Сумма = РезультатыПФРНакопительная_НеЕНВД[Индекс];
				КонецЕсли;
				
				Если РезультатыПФРСтраховая_НеЕНВД <> Неопределено Тогда
					СтрокаТЧ = ПромежуточнаяТаблица.Добавить();
					ЗаполнитьРеквизитыСтроки(СтрокаТЧ, СтрокаОтражения, МетодНачисленийСВыделениемНалогов, Перечисления.КатегорииЗатраты.ЕСН);
					
					// ПФРСтраховая
					СтрокаТЧ.Сумма = РезультатыПФРСтраховая_НеЕНВД[Индекс];
				КонецЕсли;
				
				Если РезультатыПФРНакопительная_ЕНВД <> Неопределено Тогда
					СтрокаТЧ = ПромежуточнаяТаблица.Добавить();
					ЗаполнитьРеквизитыСтроки(СтрокаТЧ, СтрокаОтражения, МетодНачисленийСВыделениемНалогов, Перечисления.КатегорииЗатраты.ЕСН);
					
					// ПФРНакопительная
					СтрокаТЧ.Сумма = РезультатыПФРНакопительная_ЕНВД[Индекс];
				КонецЕсли;
				
				Если РезультатыПФРСтраховая_ЕНВД <> Неопределено Тогда
					СтрокаТЧ = ПромежуточнаяТаблица.Добавить();
					ЗаполнитьРеквизитыСтроки(СтрокаТЧ, СтрокаОтражения, МетодНачисленийСВыделениемНалогов, Перечисления.КатегорииЗатраты.ЕСН);
					
					// ПФРСтраховая
					СтрокаТЧ.Сумма = РезультатыПФРСтраховая_ЕНВД[Индекс];
				КонецЕсли;
				Индекс = Индекс + 1;
				
			КонецЦикла;
		КонецЦикла;
		
		СтавкаФСС_БЛ = "0.03";
		ТекстЗапроса =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ЕСНСведенияОДоходах.ФизЛицо,
		|	СУММА(ВЫБОР
		|			КОГДА &ДопУсловие
		|					ИЛИ ЕСНСведенияОДоходах.ОблагаетсяЕНВД
		|				ТОГДА ЕСНСведенияОДоходах.Результат - ЕСНСведенияОДоходах.Скидка
		|			ИНАЧЕ 0
		|		КОНЕЦ) * 0.03 КАК Результат
		|ИЗ
		|	РегистрНакопления.ЕСНСведенияОДоходах КАК ЕСНСведенияОДоходах
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.УчетнаяПолитикаНалоговыйУчет.СрезПоследних(&парамКонецПериодаРегистрации, ) КАК УчетнаяПолитикаНалоговыйУчет
		|		ПО (УчетнаяПолитикаНалоговыйУчет.Организация = ЕСНСведенияОДоходах.ОбособленноеПодразделение)
		|			И (УчетнаяПолитикаНалоговыйУчет.УплачиватьДобровольныеВзносыВФСС)
		|ГДЕ
		|	ЕСНСведенияОДоходах.Период = &парамПериодРегистрации
		|	И ЕСНСведенияОДоходах.ОбособленноеПодразделение В
		|			(ВЫБРАТЬ
		|				УчетнаяПолитикаНалоговыйУчетСрезПоследних.Организация КАК Организация
		|			ИЗ
		|				РегистрСведений.УчетнаяПолитикаНалоговыйУчет.СрезПоследних(&парамКонецПериодаРегистрации, ) КАК УчетнаяПолитикаНалоговыйУчетСрезПоследних
		|			ГДЕ
		|				УчетнаяПолитикаНалоговыйУчетСрезПоследних.УплачиватьДобровольныеВзносыВФСС)
		|	И ЕСНСведенияОДоходах.КодДоходаЕСН.ВходитВБазуФедеральныйБюджет
		|	И ЕСНСведенияОДоходах.Результат <> 0
		|
		|СГРУППИРОВАТЬ ПО
		|	ЕСНСведенияОДоходах.ФизЛицо";
		
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"РегистрСведений.УчетнаяПолитикаНалоговыйУчет", ЗаполнениеРегламентированнойОтчетностиПереопределяемый.ИмяУчетнойПолитики()); 
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ДопУсловие", ЗаполнениеРегламентированнойОтчетностиПереопределяемый.ТекстПоляУсловияУСН());
		Запрос.Текст = ТекстЗапроса; 
		
		Выборка = Запрос.Выполнить().Выбрать();
		
		Пока Выборка.Следующий() Цикл
			
			Отбор.ФизЛицо = Выборка.ФизЛицо;
			
			СтрокиОтраженияПоФизЛицу = ОтражениеВБУБезПеридовДействия.НайтиСтроки(Отбор);
			
			Коэффициенты = ВыделитьКоэффициентыОтраженияИзМассиваСтрок(СтрокиОтраженияПоФизЛицу,"БазаФСС_БЛ");
			РезультатыФСС_БЛ = ОбщегоНазначенияЗК.РаспределитьПропорционально(Выборка.Результат, Коэффициенты);
			
			Если РезультатыФСС_БЛ = Неопределено Тогда
				Продолжить;
			КонецЕсли;
			
			Индекс = 0;
			Для каждого СтрокаОтражения Из СтрокиОтраженияПоФизЛицу Цикл
				Если РезультатыФСС_БЛ[Индекс] <> 0 Тогда
					СтрокаТЧ = ПромежуточнаяТаблица.Добавить();
					ЗаполнитьРеквизитыСтроки(СтрокаТЧ, СтрокаОтражения, МетодНачисленийСВыделениемНалогов, Перечисления.КатегорииЗатраты.ЕСН);
					
					СтрокаТЧ.Сумма = РезультатыФСС_БЛ[Индекс];
				КонецЕсли;
				
				Индекс = Индекс + 1;
			КонецЦикла;
			
		КонецЦикла;
		
		//
		// конец отражения исчисленного ЕСН
		//////////////////////////////////////////////////////////////////////////////////////////////////
		
		//////////////////////////////////////////////////////////////////////////////////////////////////
		// формирование проводок по ФСС страхование от несчастных случаев
		//
		
		// ДоходыДляФСС_НС
		//	Результат - база для уплаты взносов по ФСС НС уже учтена льготная ставка 
		//				по зарплате инвалидов
		//	Подразделение
		//	СтатьяЗатрат
		//	НоменклатурнаяГруппа
		//	ОбъектСтроительства
		//
		ПроцентТарифаДляИнвалидовТекст = "0.6";
		
		ДоходыДляФСС_НСТекст = 
		"ВЫБРАТЬ
		|	СведенияИнвалидность.Инвалидность,
		|	СведенияИнвалидность.Физлицо КАК Физлицо
		|ПОМЕСТИТЬ ВТ_СведенияОбИнвалидности
		|ИЗ
		|	РегистрСведений.СведенияОбИнвалидностиФизлиц.СрезПоследних(
		|			&парамПериодРегистрации,
		|			Физлицо В
		|				(ВЫБРАТЬ РАЗЛИЧНЫЕ
		|					ОтражениеВРеглУчете.Физлицо
		|				ИЗ
		|					ВТ_УУДоходыПоВР КАК ОтражениеВРеглУчете
		|				ГДЕ
		|					ОтражениеВРеглУчете.ВидРасчета.КодДоходаФСС_НС = ЗНАЧЕНИЕ(Перечисление.ДоходыФСС_НС.Облагается))) КАК СведенияИнвалидность
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Физлицо
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СтавкаВзносаНаСтрахованиеОтНесчастныхСлучаев.Ставка,
		|	СтавкаВзносаНаСтрахованиеОтНесчастныхСлучаев.Организация КАК Организация
		|ПОМЕСТИТЬ ВТ_СтавкаВзносаНаСтрахование
		|ИЗ
		|	РегистрСведений.СтавкаВзносаНаСтрахованиеОтНесчастныхСлучаев.СрезПоследних(
		|			&парамПериодРегистрации,
		|			Организация В
		|				(ВЫБРАТЬ РАЗЛИЧНЫЕ
		|					ОтражениеВРеглУчете.ОбособленноеПодразделение
		|				ИЗ
		|					ВТ_УУДоходыПоВР КАК ОтражениеВРеглУчете
		|				ГДЕ
		|					ОтражениеВРеглУчете.ВидРасчета.КодДоходаФСС_НС = ЗНАЧЕНИЕ(Перечисление.ДоходыФСС_НС.Облагается))) КАК СтавкаВзносаНаСтрахованиеОтНесчастныхСлучаев
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Организация
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЗНАЧЕНИЕ(Перечисление.КатегорииЗатраты.Зарплата) КАК КатегорияЗатраты,
		|	ВЫБОР
		|		КОГДА СведенияИнвалидность.Инвалидность
		|				ИЛИ ПравоПримененияЛьготПоЕСН.ПравоНаЛьготы
		|			ТОГДА ОтражениеВРеглУчете.Результат * " + ПроцентТарифаДляИнвалидовТекст + "
		|		ИНАЧЕ ОтражениеВРеглУчете.Результат
		|	КОНЕЦ КАК Результат,
		|	ОтражениеВРеглУчете.ОбособленноеПодразделение КАК ОбособленноеПодразделение,
		|	ОтражениеВРеглУчете.Подразделение КАК Подразделение,
		|	ОтражениеВРеглУчете.СтатьяЗатрат КАК СтатьяЗатрат,
		|	ОтражениеВРеглУчете.НоменклатурнаяГруппа КАК НоменклатурнаяГруппа,
		|	ОтражениеВРеглУчете.ОбъектСтроительства КАК ОбъектСтроительства,
		|	ЕСТЬNULL(СтавкаВзносаНаСтрахованиеОтНесчастныхСлучаев.Ставка / 100, 0) КАК СтавкаВзноса
		|ИЗ
		|	ВТ_УУДоходыПоВР КАК ОтражениеВРеглУчете
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_СведенияОбИнвалидности КАК СведенияИнвалидность
		|		ПО ОтражениеВРеглУчете.ФизЛицо = СведенияИнвалидность.Физлицо
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПравоПримененияЛьготПоЕСН КАК ПравоПримененияЛьготПоЕСН
		|		ПО (ПравоПримененияЛьготПоЕСН.Период = &парамПериодРегистрации)
		|			И ОтражениеВРеглУчете.ОбособленноеПодразделение = ПравоПримененияЛьготПоЕСН.Организация
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_СтавкаВзносаНаСтрахование КАК СтавкаВзносаНаСтрахованиеОтНесчастныхСлучаев
		|		ПО ОтражениеВРеглУчете.ОбособленноеПодразделение = СтавкаВзносаНаСтрахованиеОтНесчастныхСлучаев.Организация
		|ГДЕ
		|	ОтражениеВРеглУчете.ВидРасчета.КодДоходаФСС_НС = ЗНАЧЕНИЕ(Перечисление.ДоходыФСС_НС.Облагается)
		|ИТОГИ
		|	СУММА(Результат),
		|	МАКСИМУМ(СтавкаВзноса)
		|ПО
		|	ОбособленноеПодразделение";
		
		Запрос.Текст = ДоходыДляФСС_НСТекст;
		
		Выборка = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		
		Пока Выборка.Следующий() Цикл
			
			Всего = Выборка.Результат * Выборка.СтавкаВзноса;
			
			Коэффициенты = Новый Массив;
			ВыборкаПоОрганизации = Выборка.Выбрать();
			Пока ВыборкаПоОрганизации.Следующий() Цикл
				Коэффициенты.Добавить(ВыборкаПоОрганизации.Результат);
			КонецЦикла;
			
			СуммыПоСчетам = ОбщегоНазначенияЗК.РаспределитьПропорционально(Всего, Коэффициенты);
			
			Если СуммыПоСчетам = Неопределено Тогда
				Продолжить;
			КонецЕсли;
			
			ВыборкаПоОрганизации = Выборка.Выбрать();
			Индекс = 0;
			Пока ВыборкаПоОрганизации.Следующий() Цикл
				СтрокаТЧ = ПромежуточнаяТаблица.Добавить();
				ЗаполнитьРеквизитыСтроки(СтрокаТЧ, ВыборкаПоОрганизации, МетодНачисленийСВыделениемНалогов, Перечисления.КатегорииЗатраты.ЕСН);
				
				СтрокаТЧ.Сумма	= СуммыПоСчетам[Индекс];
				Индекс = Индекс + 1;
			КонецЦикла;
		КонецЦикла;
		
		//
		// формирование проводок по ФСС страхование от несчастных случаев
		//////////////////////////////////////////////////////////////////////////////////////////////////
		
	Иначе
		
		//////////////////////////////////////////////////////////////////////////////////////////////////
		// отражение исчисленных взносов
		//
		
		// ЕСНДоходы
		// Поля:
		//		Сотрудник
		//		ФизЛицо
		//		ПериодРегистрации
		//		КодДоходаЕСН
		//		ОблагаетсяЕНВД
		//		Результат
		//		
		//	
		//	Описание:
		//	Данные отражения доходов в учете ЕСН, помещаем во временную таблицу ВТ_ЕСНДоходы
		
		//	ЕСНДоходыОблагаемые
		//	Поля:
		//		ФизЛицо
		//		ПериодРегистрации
		//		ДоходБезСкидок ...   по каждому фонду
		//		ДоходБезСкидокИнв ...
		//
		//	Описание:
		//	таблица облагаемых доходов за месяц с учетом ограничения доходов инвалидов
		
		// ЕСНДоходыЗаГод
		//	Поля:
		//		Сотрудник
		//		КодДоходаЕСН
		//		ПериодРегистрации
		//		Результат			- доход за период
		//		РезультатЗаГод		- доход за год нарастающим итогом
		//	Описание:
		//	соединение таблицы ЕСНДоходыЗаГод с самой собой для получения
		//	нарастающего итога
		//
		
		// ЕСНДоходыПоВР
		//	Поля:
		//		ОбособленноеПодразделение
		//		Сотрудник
		//		ФизЛицо
		//		ВидРасчета
		//		КодДоходаЕСН
		//		ОблагаетсяЕНВД
		//		ПериодРегистрации
		//		ПериодДействия
		//		Результат
		//
		// Описание:
		//	список доходов по кодам ЕСН в разрезе видов расчета
		//
		
		//	ЕСНДоходыУУДоходы
		//	Поля:
		//		ОбособленноеПодразделение
		//		Сотрудник
		//		Физлицо
		//		ВидРасчета
		//		ПериодРегистрации
		//		ПериодДействия
		//		КодДоходаЕСН
		//		ОблагаетсяЕНВД
		//		РезультатЕСН
		//		Подразделение
		//		СтатьяЗатрат
		//		НоменклатурнаяГруппа
		//		ОбъектСтроительства
		//		РезультатУУ
		//	Описание:
		//	соединение таблиц с доходами в разрезе кодов дохода ЕСН и в 
		//	разрезе отражения в упручете
		//	сопоставление выполняется по видам расчета
		//
		
		// БУДоходыСводПоВР
		//	Поля:
		//		Сотрудник
		//		ВидРасчета
		//		ПериодРегистрации
		//		Результат
		//
		// Описание:
		//	доходы, отраженные в бухучете, сведенные по видам расчета
		//
		
		// КоэффициентыОтраженияЕСНТекст
		//	Поля:
		//		ФизЛицо
		//		ПериодРегистрации
		//		ПериодДействия
		//		Подразделение
		//		СтатьяЗатрат
		//		НоменклатурнаяГруппа
		//		ОбъектСтроительства
		//		БазаФСС
		//		БазаФОМС
		//		БазаПФР_ЕНВД
		//		БазаПФР_НеЕНВД
		// Описание:
		//	Таблица коэффициентов отражения составных частей исчисленного ЕСН по счетам упручета
		//
		
		Запрос.Текст =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	Доходы.Сотрудник КАК Сотрудник,
		|	Доходы.ФизЛицо КАК ФизЛицо,
		|	Доходы.ПериодРегистрации КАК ПериодРегистрации,
		|	Доходы.КодДоходаЕСН КАК КодДоходаЕСН,
		|	Доходы.ОблагаетсяЕНВД КАК ОблагаетсяЕНВД,
		|	СУММА(Доходы.Результат) КАК Результат,
		|	ГОД(Доходы.ПериодРегистрации) КАК Год
		|ПОМЕСТИТЬ ВТ_ЕСНДоходы
		|ИЗ
		|	(ВЫБРАТЬ
		|		ЕСНОсновныеНачисления.Сотрудник КАК Сотрудник,
		|		ЕСНОсновныеНачисления.ФизЛицо КАК ФизЛицо,
		|		ЕСНОсновныеНачисления.КодДоходаЕСН КАК КодДоходаЕСН,
		|		ЕСНОсновныеНачисления.ОблагаетсяЕНВД КАК ОблагаетсяЕНВД,
		|		ЕСНОсновныеНачисления.ПериодРегистрации КАК ПериодРегистрации,
		|		ЕСНОсновныеНачисления.Результат КАК Результат
		|	ИЗ
		|		РегистрРасчета.ЕСНОсновныеНачисления КАК ЕСНОсновныеНачисления
		|	ГДЕ
		|		ЕСНОсновныеНачисления.ПериодРегистрации = &парамПериодРегистрации
		|		И (ЕСНОсновныеНачисления.КодДоходаЕСН.ВходитВБазуФОМС
		|				ИЛИ ЕСНОсновныеНачисления.КодДоходаЕСН.ВходитВБазуФСС
		|				ИЛИ ЕСНОсновныеНачисления.КодДоходаЕСН.ВходитВБазуПФР)
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		ЕСНДополнительныеНачисления.Сотрудник,
		|		ЕСНДополнительныеНачисления.ФизЛицо,
		|		ЕСНДополнительныеНачисления.КодДоходаЕСН,
		|		ЕСНДополнительныеНачисления.ОблагаетсяЕНВД,
		|		ЕСНДополнительныеНачисления.ПериодРегистрации,
		|		ЕСНДополнительныеНачисления.Результат - ЕСНДополнительныеНачисления.Скидка
		|	ИЗ
		|		РегистрРасчета.ЕСНДополнительныеНачисления КАК ЕСНДополнительныеНачисления
		|	ГДЕ
		|		ЕСНДополнительныеНачисления.ПериодРегистрации = &парамПериодРегистрации
		|		И (ЕСНДополнительныеНачисления.КодДоходаЕСН.ВходитВБазуФОМС
		|				ИЛИ ЕСНДополнительныеНачисления.КодДоходаЕСН.ВходитВБазуФСС
		|				ИЛИ ЕСНДополнительныеНачисления.КодДоходаЕСН.ВходитВБазуПФР)) КАК Доходы
		|
		|СГРУППИРОВАТЬ ПО
		|	Доходы.Сотрудник,
		|	Доходы.КодДоходаЕСН,
		|	Доходы.ПериодРегистрации,
		|	Доходы.ОблагаетсяЕНВД,
		|	Доходы.ФизЛицо,
		|	ГОД(Доходы.ПериодРегистрации)
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ФизЛицо,
		|	ПериодРегистрации
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЕСНДоходыОсновная.ФизЛицо КАК ФизЛицо,
		|	ЕСНДоходыОсновная.ПериодРегистрации КАК ПериодРегистрации,
		|	СУММА(ВЫБОР
		|			КОГДА ЕСНДоходыОсновная.КодДоходаЕСН.ВходитВБазуПФР
		|				ТОГДА ЕСНДоходыОсновная.Результат
		|			ИНАЧЕ 0
		|		КОНЕЦ) КАК ДоходБезСкидокФБ,
		|	СУММА(ВЫБОР
		|			КОГДА ЕСНДоходыОсновная.КодДоходаЕСН.ВходитВБазуФОМС
		|				ТОГДА ЕСНДоходыОсновная.Результат
		|			ИНАЧЕ 0
		|		КОНЕЦ) КАК ДоходБезСкидокФОМС,
		|	СУММА(ВЫБОР
		|			КОГДА ЕСНДоходыОсновная.КодДоходаЕСН.ВходитВБазуФСС
		|				ТОГДА ЕСНДоходыОсновная.Результат
		|			ИНАЧЕ 0
		|		КОНЕЦ) КАК ДоходБезСкидокФСС
		|ПОМЕСТИТЬ ВТДоходыОблагаемые
		|ИЗ
		|	ВТ_ЕСНДоходы КАК ЕСНДоходыОсновная
		|
		|СГРУППИРОВАТЬ ПО
		|	ЕСНДоходыОсновная.ФизЛицо,
		|	ЕСНДоходыОсновная.ПериодРегистрации
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЕСНДоходыОсновная.Сотрудник,
		|	ЕСНДоходыОсновная.ФизЛицо,
		|	ЕСНДоходыОсновная.КодДоходаЕСН,
		|	ЕСНДоходыОсновная.ОблагаетсяЕНВД,
		|	ЕСНДоходыОсновная.ПериодРегистрации,
		|	ЕСНДоходыОсновная.Результат,
		|	СУММА(ЕСНДоходыВспомогательная.Результат) КАК РезультатЗаГод
		|ПОМЕСТИТЬ ВТДоходыЗаГод
		|ИЗ
		|	ВТ_ЕСНДоходы КАК ЕСНДоходыОсновная
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ЕСНДоходы КАК ЕСНДоходыВспомогательная
		|		ПО (ЕСНДоходыВспомогательная.ПериодРегистрации <= ЕСНДоходыОсновная.ПериодРегистрации)
		|			И ЕСНДоходыОсновная.Год = ЕСНДоходыВспомогательная.Год
		|			И ЕСНДоходыОсновная.Сотрудник = ЕСНДоходыВспомогательная.Сотрудник
		|			И ЕСНДоходыОсновная.КодДоходаЕСН = ЕСНДоходыВспомогательная.КодДоходаЕСН
		|			И ЕСНДоходыОсновная.ОблагаетсяЕНВД = ЕСНДоходыВспомогательная.ОблагаетсяЕНВД
		|
		|СГРУППИРОВАТЬ ПО
		|	ЕСНДоходыОсновная.Сотрудник,
		|	ЕСНДоходыОсновная.ФизЛицо,
		|	ЕСНДоходыОсновная.КодДоходаЕСН,
		|	ЕСНДоходыОсновная.ОблагаетсяЕНВД,
		|	ЕСНДоходыОсновная.Результат,
		|	ЕСНДоходыОсновная.ПериодРегистрации
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	Доходы.ОбособленноеПодразделение КАК ОбособленноеПодразделение,
		|	Доходы.Сотрудник КАК Сотрудник,
		|	Доходы.ФизЛицо КАК ФизЛицо,
		|	Доходы.ВидРасчета КАК ВидРасчета,
		|	Доходы.КодДоходаЕСН,
		|	Доходы.ОблагаетсяЕНВД КАК ОблагаетсяЕНВД,
		|	Доходы.ПериодРегистрации КАК ПериодРегистрации,
		|	Доходы.ПериодДействия КАК ПериодДействия,
		|	СУММА(Доходы.Результат) КАК Результат
		|ПОМЕСТИТЬ ВТ_ЕСНДоходыПоВР
		|ИЗ
		|	(ВЫБРАТЬ
		|		ЕСНОсновныеНачисления.ОбособленноеПодразделение КАК ОбособленноеПодразделение,
		|		ЕСНОсновныеНачисления.Сотрудник КАК Сотрудник,
		|		ЕСНОсновныеНачисления.ФизЛицо КАК ФизЛицо,
		|		ЕСНОсновныеНачисления.ВидРасчета КАК ВидРасчета,
		|		ЕСНОсновныеНачисления.КодДоходаЕСН КАК КодДоходаЕСН,
		|		ЕСНОсновныеНачисления.ОблагаетсяЕНВД КАК ОблагаетсяЕНВД,
		|		ЕСНОсновныеНачисления.ПериодРегистрации КАК ПериодРегистрации,
		|		ЕСНОсновныеНачисления.ПериодДействия КАК ПериодДействия,
		|		ЕСНОсновныеНачисления.Результат КАК Результат
		|	ИЗ
		|		РегистрРасчета.ЕСНОсновныеНачисления КАК ЕСНОсновныеНачисления
		|	ГДЕ
		|		ЕСНОсновныеНачисления.ПериодРегистрации = &парамПериодРегистрации
		|		И (ЕСНОсновныеНачисления.КодДоходаЕСН.ВходитВБазуФОМС
		|				ИЛИ ЕСНОсновныеНачисления.КодДоходаЕСН.ВходитВБазуФСС
		|				ИЛИ ЕСНОсновныеНачисления.КодДоходаЕСН.ВходитВБазуПФР)
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		ЕСНДополнительныеНачисления.ОбособленноеПодразделение,
		|		ЕСНДополнительныеНачисления.Сотрудник,
		|		ЕСНДополнительныеНачисления.ФизЛицо,
		|		ЕСНДополнительныеНачисления.ВидРасчета,
		|		ЕСНДополнительныеНачисления.КодДоходаЕСН,
		|		ЕСНДополнительныеНачисления.ОблагаетсяЕНВД,
		|		ЕСНДополнительныеНачисления.ПериодРегистрации,
		|		ЕСНДополнительныеНачисления.ПериодРегистрации,
		|		ЕСНДополнительныеНачисления.Результат - ЕСНДополнительныеНачисления.Скидка
		|	ИЗ
		|		РегистрРасчета.ЕСНДополнительныеНачисления КАК ЕСНДополнительныеНачисления
		|	ГДЕ
		|		ЕСНДополнительныеНачисления.ПериодРегистрации = &парамПериодРегистрации
		|		И (ЕСНДополнительныеНачисления.КодДоходаЕСН.ВходитВБазуФОМС
		|				ИЛИ ЕСНДополнительныеНачисления.КодДоходаЕСН.ВходитВБазуФСС
		|				ИЛИ ЕСНДополнительныеНачисления.КодДоходаЕСН.ВходитВБазуПФР)) КАК Доходы
		|
		|СГРУППИРОВАТЬ ПО
		|	Доходы.ОбособленноеПодразделение,
		|	Доходы.Сотрудник,
		|	Доходы.ВидРасчета,
		|	Доходы.КодДоходаЕСН,
		|	Доходы.ПериодРегистрации,
		|	Доходы.ПериодДействия,
		|	Доходы.ОблагаетсяЕНВД,
		|	Доходы.ФизЛицо
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ОбособленноеПодразделение,
		|	Сотрудник,
		|	ОблагаетсяЕНВД,
		|	ВидРасчета,
		|	ПериодРегистрации,
		|	ПериодДействия
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЕСНДоходыПоВР.ОбособленноеПодразделение,
		|	ЕСНДоходыПоВР.Сотрудник,
		|	ЕСНДоходыПоВР.ФизЛицо,
		|	ЕСНДоходыПоВР.ВидРасчета,
		|	ЕСНДоходыПоВР.ПериодРегистрации,
		|	ЕСНДоходыПоВР.ПериодДействия,
		|	ЕСНДоходыПоВР.КодДоходаЕСН,
		|	ЕСНДоходыПоВР.ОблагаетсяЕНВД,
		|	ЕСНДоходыПоВР.Результат КАК РезультатЕСН,
		|	УУДоходыПоВР.Подразделение,
		|	УУДоходыПоВР.СтатьяЗатрат,
		|	УУДоходыПоВР.НоменклатурнаяГруппа,
		|	УУДоходыПоВР.ОбъектСтроительства,
		|	УУДоходыПоВР.Результат КАК РезультатУУ
		|ПОМЕСТИТЬ ВТЕСНДоходыУУДоходы
		|ИЗ
		|	ВТ_ЕСНДоходыПоВР КАК ЕСНДоходыПоВР
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_УУДоходыПоВР КАК УУДоходыПоВР
		|		ПО ЕСНДоходыПоВР.Сотрудник = УУДоходыПоВР.Сотрудник
		|			И ЕСНДоходыПоВР.ОбособленноеПодразделение = УУДоходыПоВР.ОбособленноеПодразделение
		|			И ЕСНДоходыПоВР.ОблагаетсяЕНВД = УУДоходыПоВР.ЭтоСтатьяЗатратЕНВД
		|			И ЕСНДоходыПоВР.ВидРасчета = УУДоходыПоВР.ВидРасчета
		|			И ЕСНДоходыПоВР.ПериодДействия = УУДоходыПоВР.ПериодДействия
		|			И ЕСНДоходыПоВР.ПериодРегистрации = УУДоходыПоВР.ПериодРегистрации
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Доходы.Сотрудник,
		|	Доходы.ФизЛицо,
		|	Доходы.ВидРасчета,
		|	Доходы.ПериодРегистрации,
		|	Доходы.ПериодДействия,
		|	СУММА(Доходы.Результат) КАК Результат
		|ПОМЕСТИТЬ ВТБУДоходыСводПоВР
		|ИЗ
		|	(ВЫБРАТЬ
		|		ДоходыПоВР.Сотрудник КАК Сотрудник,
		|		ДоходыПоВР.ФизЛицо КАК ФизЛицо,
		|		ДоходыПоВР.ВидРасчета КАК ВидРасчета,
		|		ДоходыПоВР.ПериодРегистрации КАК ПериодРегистрации,
		|		ДоходыПоВР.ПериодДействия КАК ПериодДействия,
		|		ДоходыПоВР.Результат КАК Результат
		|	ИЗ
		|		ВТ_УУДоходыПоВР КАК ДоходыПоВР) КАК Доходы
		|
		|СГРУППИРОВАТЬ ПО
		|	Доходы.Сотрудник,
		|	Доходы.ФизЛицо,
		|	Доходы.ВидРасчета,
		|	Доходы.ПериодРегистрации,
		|	Доходы.ПериодДействия
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЕСНДоходыУУДоходы.ФизЛицо,
		|	ЕСНДоходыУУДоходы.ПериодРегистрации,
		|	ЕСНДоходыУУДоходы.ПериодДействия,
		|	ЕСНДоходыУУДоходы.Подразделение,
		|	ЕСНДоходыУУДоходы.СтатьяЗатрат,
		|	ЕСНДоходыУУДоходы.НоменклатурнаяГруппа,
		|	ЕСНДоходыУУДоходы.ОбъектСтроительства,
		|	ВЫБОР
		|		КОГДА (НЕ ЕСНДоходыУУДоходы.КодДоходаЕСН.ВходитВБазуФСС)
		|				ИЛИ ЕСНДоходыЗаГод.Результат = 0
		|				ИЛИ БУДоходыСводПоВР.Результат = 0
		|				ИЛИ ЕСНДоходыОблагаемые.ДоходБезСкидокФСС = 0
		|			ТОГДА 0
		|		ИНАЧЕ ЕСНДоходыУУДоходы.РезультатЕСН / БУДоходыСводПоВР.Результат * ЕСНДоходыУУДоходы.РезультатУУ
		|	КОНЕЦ КАК БазаФСС,
		|	ВЫБОР
		|		КОГДА (НЕ ЕСНДоходыУУДоходы.КодДоходаЕСН.ВходитВБазуФОМС)
		|				ИЛИ ЕСНДоходыЗаГод.Результат = 0
		|				ИЛИ БУДоходыСводПоВР.Результат = 0
		|				ИЛИ ЕСНДоходыОблагаемые.ДоходБезСкидокФОМС = 0
		|			ТОГДА 0
		|		ИНАЧЕ ЕСНДоходыУУДоходы.РезультатЕСН / БУДоходыСводПоВР.Результат * ЕСНДоходыУУДоходы.РезультатУУ
		|	КОНЕЦ КАК БазаФОМС,
		|	ВЫБОР
		|		КОГДА (НЕ ЕСНДоходыУУДоходы.ОблагаетсяЕНВД)
		|				ИЛИ (НЕ ЕСНДоходыУУДоходы.КодДоходаЕСН.ВходитВБазуПФР)
		|				ИЛИ ЕСНДоходыЗаГод.Результат = 0
		|				ИЛИ БУДоходыСводПоВР.Результат = 0
		|				ИЛИ ЕСНДоходыОблагаемые.ДоходБезСкидокФБ = 0
		|			ТОГДА 0
		|		ИНАЧЕ ЕСНДоходыУУДоходы.РезультатЕСН / БУДоходыСводПоВР.Результат * ЕСНДоходыУУДоходы.РезультатУУ
		|	КОНЕЦ КАК БазаПФР_ЕНВД,
		|	ВЫБОР
		|		КОГДА ЕСНДоходыУУДоходы.ОблагаетсяЕНВД
		|				ИЛИ (НЕ ЕСНДоходыУУДоходы.КодДоходаЕСН.ВходитВБазуПФР)
		|				ИЛИ ЕСНДоходыЗаГод.Результат = 0
		|				ИЛИ БУДоходыСводПоВР.Результат = 0
		|				ИЛИ ЕСНДоходыОблагаемые.ДоходБезСкидокФБ = 0
		|			ТОГДА 0
		|		ИНАЧЕ ЕСНДоходыУУДоходы.РезультатЕСН / БУДоходыСводПоВР.Результат * ЕСНДоходыУУДоходы.РезультатУУ
		|	КОНЕЦ КАК БазаПФР_НеЕНВД
		|ПОМЕСТИТЬ ВТКоэффициентыОтражения
		|ИЗ
		|	ВТЕСНДоходыУУДоходы КАК ЕСНДоходыУУДоходы
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТБУДоходыСводПоВР КАК БУДоходыСводПоВР
		|		ПО ЕСНДоходыУУДоходы.Сотрудник = БУДоходыСводПоВР.Сотрудник
		|			И ЕСНДоходыУУДоходы.ВидРасчета = БУДоходыСводПоВР.ВидРасчета
		|			И ЕСНДоходыУУДоходы.ПериодРегистрации = БУДоходыСводПоВР.ПериодРегистрации
		|			И ЕСНДоходыУУДоходы.ПериодДействия = БУДоходыСводПоВР.ПериодДействия
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТДоходыЗаГод КАК ЕСНДоходыЗаГод
		|		ПО ЕСНДоходыУУДоходы.Сотрудник = ЕСНДоходыЗаГод.Сотрудник
		|			И ЕСНДоходыУУДоходы.КодДоходаЕСН = ЕСНДоходыЗаГод.КодДоходаЕСН
		|			И ЕСНДоходыУУДоходы.ОблагаетсяЕНВД = ЕСНДоходыЗаГод.ОблагаетсяЕНВД
		|			И ЕСНДоходыУУДоходы.ПериодРегистрации = ЕСНДоходыЗаГод.ПериодРегистрации
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТДоходыОблагаемые КАК ЕСНДоходыОблагаемые
		|		ПО ЕСНДоходыУУДоходы.ФизЛицо = ЕСНДоходыОблагаемые.ФизЛицо
		|			И ЕСНДоходыУУДоходы.ПериодРегистрации = ЕСНДоходыОблагаемые.ПериодРегистрации
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЕСНДоходыУУДоходы.ФизЛицо КАК ФизЛицо,
		|	ЗНАЧЕНИЕ(Перечисление.КатегорииЗатраты.Зарплата) КАК КатегорияЗатраты,
		|	ЕСНДоходыУУДоходы.Подразделение КАК Подразделение,
		|	ЕСНДоходыУУДоходы.СтатьяЗатрат КАК СтатьяЗатрат,
		|	ЕСНДоходыУУДоходы.НоменклатурнаяГруппа,
		|	ЕСНДоходыУУДоходы.ОбъектСтроительства,
		|	СУММА(ЕСНДоходыУУДоходы.БазаФСС) КАК БазаФСС,
		|	СУММА(ЕСНДоходыУУДоходы.БазаФОМС) КАК БазаФОМС,
		|	СУММА(ЕСНДоходыУУДоходы.БазаПФР_ЕНВД) КАК БазаПФР_ЕНВД,
		|	СУММА(ЕСНДоходыУУДоходы.БазаПФР_НеЕНВД) КАК БазаПФР_НеЕНВД
		|ИЗ
		|	ВТКоэффициентыОтражения КАК ЕСНДоходыУУДоходы
		|
		|СГРУППИРОВАТЬ ПО
		|	ЕСНДоходыУУДоходы.ФизЛицо,
		|	ЕСНДоходыУУДоходы.ПериодРегистрации,
		|	ЕСНДоходыУУДоходы.ПериодДействия,
		|	ЕСНДоходыУУДоходы.Подразделение,
		|	ЕСНДоходыУУДоходы.СтатьяЗатрат,
		|	ЕСНДоходыУУДоходы.НоменклатурнаяГруппа,
		|	ЕСНДоходыУУДоходы.ОбъектСтроительства
		|
		|УПОРЯДОЧИТЬ ПО
		|	ФизЛицо,
		|	ЕСНДоходыУУДоходы.ПериодРегистрации,
		|	Подразделение,
		|	СтатьяЗатрат
		|АВТОУПОРЯДОЧИВАНИЕ";
		
		ОтражениеВБУБезПеридовДействия = Запрос.Выполнить().Выгрузить();
		ОтражениеВБУБезПеридовДействия.Индексы.Добавить("ФизЛицо");
		
		
		// Взносы исчисленные
		//	Поля:
		//		ФизЛицо
		//		ФСС
		//		ФФОМС
		//		ТФОМС
		//		ПФРНакопительная_НеЕНВД - в части видов деятельности, не облагаемых ЕНВД
		//		ПФРСтраховая_НеЕНВД 
		//		ПФРНакопительная_ЕНВД - в части видов деятельности, облагаемых ЕНВД     
		//		ПФРСтраховая_ЕНВД
		// Описание:
		// обороты по страховым взносам в разрезе физлиц
		//
		
		Запрос.Текст =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ВзносыПоРаботникам.ФизЛицо,
		|	ВзносыПоРаботникам.ФССОборот КАК ФСС,
		|	ВзносыПоРаботникам.ФФОМСОборот КАК ФФОМС,
		|	ВзносыПоРаботникам.ТФОМСОборот КАК ТФОМС,
		|	ВзносыПоРаботникам.ПФРНакопительнаяОборот КАК ПФРНакопительная_НеЕНВД,
		|	ВзносыПоРаботникам.ПФРСтраховаяОборот КАК ПФРСтраховая_НеЕНВД,
		|	ВзносыПоРаботникам.ПФРНакопительнаяЕНВДОборот КАК ПФРНакопительная_ЕНВД,
		|	ВзносыПоРаботникам.ПФРСтраховаяЕНВДОборот КАК ПФРСтраховая_ЕНВД,
		|	ВзносыПоРаботникам.ФССНесчастныеСлучаиОборот КАК ФССНесчастныеСлучаи
		|ИЗ
		|	РегистрНакопления.СтраховыеВзносыИсчисленные.Обороты(&парамПериодРегистрации, &парамКонецПериодаРегистрации, , ) КАК ВзносыПоРаботникам";
		
		Отбор = Новый Структура("ФизЛицо");
		
		МетодНачисленийСВыделениемНалогов = мМетодПланированияУчетаЗатрат = Перечисления.МетодыПланированияУчетаЗатратНаПерсонал.МетодНачисленийСВыделениемНалогов;
		
		Выборка = Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл
			
			Отбор.ФизЛицо				= Выборка.ФизЛицо;
			
			СтрокиОтраженияПоФизЛицу	= ОтражениеВБУБезПеридовДействия.НайтиСтроки(Отбор);
			
			// ФСС
			Коэффициенты = ВыделитьКоэффициентыОтраженияИзМассиваСтрок(СтрокиОтраженияПоФизЛицу,"БазаФСС");
			РезультатыФСС = ОбщегоНазначенияЗК.РаспределитьПропорционально(Выборка.ФСС, Коэффициенты);
			
			// ФСС
			Коэффициенты = ВыделитьКоэффициентыОтраженияИзМассиваСтрок(СтрокиОтраженияПоФизЛицу,"БазаФСС");
			РезультатыФСС_НС = ОбщегоНазначенияЗК.РаспределитьПропорционально(Выборка.ФССНесчастныеСлучаи, Коэффициенты);
			
			// ФОМС
			Коэффициенты = ВыделитьКоэффициентыОтраженияИзМассиваСтрок(СтрокиОтраженияПоФизЛицу,"БазаФОМС");
			РезультатыФФОМС = ОбщегоНазначенияЗК.РаспределитьПропорционально(Выборка.ФФОМС, Коэффициенты);
			РезультатыТФОМС = ОбщегоНазначенияЗК.РаспределитьПропорционально(Выборка.ТФОМС, Коэффициенты);
			
			// ПФР по видам деятельности, не облагаемым ЕНВД
			Коэффициенты = ВыделитьКоэффициентыОтраженияИзМассиваСтрок(СтрокиОтраженияПоФизЛицу,"БазаПФР_НеЕНВД");
			РезультатыПФРНакопительная_НеЕНВД = ОбщегоНазначенияЗК.РаспределитьПропорционально(Выборка.ПФРНакопительная_НеЕНВД, Коэффициенты);
			РезультатыПФРСтраховая_НеЕНВД = ОбщегоНазначенияЗК.РаспределитьПропорционально(Выборка.ПФРСтраховая_НеЕНВД, Коэффициенты);
			
			// ПФР по видам деятельности, облагаемым ЕНВД
			Коэффициенты = ВыделитьКоэффициентыОтраженияИзМассиваСтрок(СтрокиОтраженияПоФизЛицу,"БазаПФР_ЕНВД");
			РезультатыПФРНакопительная_ЕНВД = ОбщегоНазначенияЗК.РаспределитьПропорционально(Выборка.ПФРНакопительная_ЕНВД, Коэффициенты);
			РезультатыПФРСтраховая_ЕНВД = ОбщегоНазначенияЗК.РаспределитьПропорционально(Выборка.ПФРСтраховая_ЕНВД, Коэффициенты);
			
			Индекс = 0;
			Для Каждого СтрокаОтражения Из СтрокиОтраженияПоФизЛицу Цикл
				
				Если РезультатыФСС <> Неопределено Тогда
					СтрокаТЧ = ПромежуточнаяТаблица.Добавить();
					ЗаполнитьРеквизитыСтроки(СтрокаТЧ, СтрокаОтражения, МетодНачисленийСВыделениемНалогов, Перечисления.КатегорииЗатраты.ЕСН);
					
					// ФСС
					СтрокаТЧ.Сумма			= РезультатыФСС[Индекс];
					
				КонецЕсли;
				
				Если РезультатыФСС_НС <> Неопределено Тогда
					СтрокаТЧ = ПромежуточнаяТаблица.Добавить();
					ЗаполнитьРеквизитыСтроки(СтрокаТЧ, СтрокаОтражения, МетодНачисленийСВыделениемНалогов, Перечисления.КатегорииЗатраты.ЕСН);
					
					// ФСС НС
					СтрокаТЧ.Сумма			= РезультатыФСС_НС[Индекс];
					
				КонецЕсли;
				
				Если РезультатыФФОМС <> Неопределено Тогда
					СтрокаТЧ = ПромежуточнаяТаблица.Добавить();
					ЗаполнитьРеквизитыСтроки(СтрокаТЧ, СтрокаОтражения, МетодНачисленийСВыделениемНалогов, Перечисления.КатегорииЗатраты.ЕСН);
					
					// ФФОМС
					СтрокаТЧ.Сумма			= РезультатыФФОМС[Индекс];
				КонецЕсли;
				
				Если РезультатыТФОМС <> Неопределено Тогда
					СтрокаТЧ = ПромежуточнаяТаблица.Добавить();
					ЗаполнитьРеквизитыСтроки(СтрокаТЧ, СтрокаОтражения, МетодНачисленийСВыделениемНалогов, Перечисления.КатегорииЗатраты.ЕСН);
					
					// ТФОМС
					СтрокаТЧ.Сумма			= РезультатыТФОМС[Индекс];
				КонецЕсли;
				
				Если РезультатыПФРНакопительная_НеЕНВД <> Неопределено Тогда
					СтрокаТЧ = ПромежуточнаяТаблица.Добавить();
					ЗаполнитьРеквизитыСтроки(СтрокаТЧ, СтрокаОтражения, МетодНачисленийСВыделениемНалогов, Перечисления.КатегорииЗатраты.ЕСН);
					
					// ПФРНакопительная
					СтрокаТЧ.Сумма = РезультатыПФРНакопительная_НеЕНВД[Индекс];
				КонецЕсли;
				
				Если РезультатыПФРСтраховая_НеЕНВД <> Неопределено Тогда
					СтрокаТЧ = ПромежуточнаяТаблица.Добавить();
					ЗаполнитьРеквизитыСтроки(СтрокаТЧ, СтрокаОтражения, МетодНачисленийСВыделениемНалогов, Перечисления.КатегорииЗатраты.ЕСН);
					
					// ПФРСтраховая
					СтрокаТЧ.Сумма = РезультатыПФРСтраховая_НеЕНВД[Индекс];
				КонецЕсли;
				
				Если РезультатыПФРНакопительная_ЕНВД <> Неопределено Тогда
					СтрокаТЧ = ПромежуточнаяТаблица.Добавить();
					ЗаполнитьРеквизитыСтроки(СтрокаТЧ, СтрокаОтражения, МетодНачисленийСВыделениемНалогов, Перечисления.КатегорииЗатраты.ЕСН);
					
					// ПФРНакопительная
					СтрокаТЧ.Сумма = РезультатыПФРНакопительная_ЕНВД[Индекс];
				КонецЕсли;
				
				Если РезультатыПФРСтраховая_ЕНВД <> Неопределено Тогда
					СтрокаТЧ = ПромежуточнаяТаблица.Добавить();
					ЗаполнитьРеквизитыСтроки(СтрокаТЧ, СтрокаОтражения, МетодНачисленийСВыделениемНалогов, Перечисления.КатегорииЗатраты.ЕСН);
					
					// ПФРСтраховая
					СтрокаТЧ.Сумма = РезультатыПФРСтраховая_ЕНВД[Индекс];
				КонецЕсли;
				
				Индекс = Индекс + 1;
				
			КонецЦикла;
		КонецЦикла;
		
		//
		// конец отражения исчисленных взносов
		//////////////////////////////////////////////////////////////////////////////////////////////////
		
	КонецЕсли;
	
	//////////////////////////////////////////////////////////////////////////////////////////////////
	// отражение начислений
	//
	
	// УУНачисления
	//	Поля:
	//		Подразделение
	//		СтатьяЗатрат
	//		НоменклатурнаяГруппа
	//		ОбъектСтроительства
	//		Сумма
	// Объединение:
	// 
	
	Запрос.УстановитьПараметр("ПромежуточнаяТаблица",	ПромежуточнаяТаблица);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ПромежуточнаяТаблица.КатегорияЗатраты,
	|	ПромежуточнаяТаблица.Подразделение,
	|	ПромежуточнаяТаблица.СтатьяЗатрат,
	|	ПромежуточнаяТаблица.НоменклатурнаяГруппа,
	|	ПромежуточнаяТаблица.ОбъектСтроительства,
	|	ПромежуточнаяТаблица.Сумма
	|ПОМЕСТИТЬ ВТ_ПромежуточнаяТаблица
	|ИЗ
	|	&ПромежуточнаяТаблица КАК ПромежуточнаяТаблица";
	Запрос.Выполнить();
	
	// сдельный заработок по упр.нарядам рассматриваем отдельно, в этот документ он попадать не должен
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Начисления.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ВТ_ОтражаемыеНачисления
	|ИЗ
	|	ПланВидовРасчета.УправленческиеНачисления КАК Начисления
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовРасчета.УправленческиеНачисления.Показатели КАК Показатели1
	|		ПО Начисления.Ссылка = Показатели1.Ссылка
	|			И (Показатели1.НомерСтроки = 1)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовРасчета.УправленческиеНачисления.Показатели КАК Показатели2
	|		ПО Начисления.Ссылка = Показатели2.Ссылка
	|			И (Показатели2.НомерСтроки = 2)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовРасчета.УправленческиеНачисления.Показатели КАК Показатели3
	|		ПО Начисления.Ссылка = Показатели3.Ссылка
	|			И (Показатели3.НомерСтроки = 3)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовРасчета.УправленческиеНачисления.Показатели КАК Показатели4
	|		ПО Начисления.Ссылка = Показатели4.Ссылка
	|			И (Показатели4.НомерСтроки = 4)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовРасчета.УправленческиеНачисления.Показатели КАК Показатели5
	|		ПО Начисления.Ссылка = Показатели5.Ссылка
	|			И (Показатели5.НомерСтроки = 5)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовРасчета.УправленческиеНачисления.Показатели КАК Показатели6
	|		ПО Начисления.Ссылка = Показатели6.Ссылка
	|			И (Показатели6.НомерСтроки = 6)
	|ГДЕ
	|	((НЕ Начисления.ЗачетОтработанногоВремени)
	|			ИЛИ ЕСТЬNULL(Показатели1.Показатель, ЗНАЧЕНИЕ(Справочник.ПоказателиСхемМотивации.ПустаяСсылка)) <> ЗНАЧЕНИЕ(Справочник.ПоказателиСхемМотивации.СдельнаяВыработка)
	|				И ЕСТЬNULL(Показатели2.Показатель, ЗНАЧЕНИЕ(Справочник.ПоказателиСхемМотивации.ПустаяСсылка)) <> ЗНАЧЕНИЕ(Справочник.ПоказателиСхемМотивации.СдельнаяВыработка)
	|				И ЕСТЬNULL(Показатели3.Показатель, ЗНАЧЕНИЕ(Справочник.ПоказателиСхемМотивации.ПустаяСсылка)) <> ЗНАЧЕНИЕ(Справочник.ПоказателиСхемМотивации.СдельнаяВыработка)
	|				И ЕСТЬNULL(Показатели4.Показатель, ЗНАЧЕНИЕ(Справочник.ПоказателиСхемМотивации.ПустаяСсылка)) <> ЗНАЧЕНИЕ(Справочник.ПоказателиСхемМотивации.СдельнаяВыработка)
	|				И ЕСТЬNULL(Показатели5.Показатель, ЗНАЧЕНИЕ(Справочник.ПоказателиСхемМотивации.ПустаяСсылка)) <> ЗНАЧЕНИЕ(Справочник.ПоказателиСхемМотивации.СдельнаяВыработка)
	|				И ЕСТЬNULL(Показатели6.Показатель, ЗНАЧЕНИЕ(Справочник.ПоказателиСхемМотивации.ПустаяСсылка)) <> ЗНАЧЕНИЕ(Справочник.ПоказателиСхемМотивации.СдельнаяВыработка))
	|	И Начисления.Ссылка <> ЗНАЧЕНИЕ(ПланВидовРасчета.УправленческиеНачисления.НачисленоПоБухгалтерии)
	|	И Начисления.Ссылка <> ЗНАЧЕНИЕ(ПланВидовРасчета.УправленческиеНачисления.НачисленоПоБухгалтерииСдельно)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка";
	Запрос.Выполнить();
	
	//
	// отражение начислений
	//////////////////////////////////////////////////////////////////////////////////////////////////
	
	Возврат Запрос;
	
КонецФункции


Процедура ЗаполнитьМетодомНачислений()
	
	Запрос = ПодготовитьВременныеТаблицы(ПериодРегистрации);
	Если Запрос = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	// из регистра расчетов УправленческиеНачисления берем начисления только по утвержденным схемам мотивации
	// пропускаем тех сотрудников, кому в рег.учете назначено доначисление по данным упр.учета, так как их затраты 
	// уже учтены в данных регучета
	
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	УправленческиеНачисления.ФизЛицо КАК ФизЛицо,
	|	УправленческиеНачисления.ВидРасчета,
	|	УправленческиеНачисления.ВидРасчета.ЗачетОтработанногоВремени КАК ВидРасчетаЗачетОтработанногоВремени,
	|	УправленческиеНачисления.ВидРасчета.СпособОтраженияВУпрУчете КАК СпособОтраженияВУпрУчете,
	|	УправленческиеНачисления.ВидРасчета.СпособОтраженияВУпрУчете.СтатьяЗатрат КАК СпособОтраженияВУпрУчетеСтатьяЗатрат,
	|	УправленческиеНачисления.ВидРасчета.СпособОтраженияВУпрУчете.НоменклатурнаяГруппа КАК СпособОтраженияВУпрУчетеНоменклатурнаяГруппа,
	|	УправленческиеНачисления.ВидРасчета.СпособОтраженияВУпрУчете.ОбъектСтроительства КАК СпособОтраженияВУпрУчетеОбъектСтроительства,
	|	УправленческиеНачисления.Подразделение,
	|	УправленческиеНачисления.ПериодДействияНачало,
	|	УправленческиеНачисления.Результат
	|ПОМЕСТИТЬ ВТУправленческиеНачисления
	|ИЗ
	|	РегистрРасчета.УправленческиеНачисления КАК УправленческиеНачисления
	|ГДЕ
	|	УправленческиеНачисления.ПериодРегистрации = &парамПериодРегистрации
	|	И УправленческиеНачисления.ВидРасчета В
	|			(ВЫБРАТЬ
	|				ВидыРасчета.Ссылка
	|			ИЗ
	|				ВТ_ОтражаемыеНачисления КАК ВидыРасчета)
	|	И (НЕ УправленческиеНачисления.ПоВременнойСхемеМотивации)
	|	И (НЕ УправленческиеНачисления.ФизЛицо В
	|				(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|					ПлановыеНачисленияРаботниковОрганизацийСрезПоследних.Сотрудник.Физлицо
	|				ИЗ
	|					РегистрСведений.ПлановыеНачисленияРаботниковОрганизаций.СрезПоследних(&парамПериодРегистрации, Сотрудник.Физлицо В
	|						(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|							УправленческиеНачисления.ФизЛицо
	|						ИЗ
	|							РегистрРасчета.УправленческиеНачисления КАК УправленческиеНачисления
	|						ГДЕ
	|							УправленческиеНачисления.ПериодРегистрации = &парамПериодРегистрации)) КАК ПлановыеНачисленияРаботниковОрганизацийСрезПоследних
	|				ГДЕ
	|					ПлановыеНачисленияРаботниковОрганизацийСрезПоследних.ВидРасчетаИзмерение.СпособРасчета = ЗНАЧЕНИЕ(Перечисление.СпособыРасчетаОплатыТруда.ДоначислениеПоУправленческомуУчету)))
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ФизЛицо
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОтражениеВУчете.КатегорияЗатраты КАК КатегорияЗатраты,
	|	ОтражениеВУчете.Подразделение КАК Подразделение,
	|	ОтражениеВУчете.СтатьяЗатрат КАК СтатьяЗатрат,
	|	ОтражениеВУчете.НоменклатурнаяГруппа КАК НоменклатурнаяГруппа,
	|	ОтражениеВУчете.ОбъектСтроительства КАК ОбъектСтроительства,
	|	СУММА(ОтражениеВУчете.Сумма) КАК Сумма
	|ИЗ
	|	(ВЫБРАТЬ
	|		ЗНАЧЕНИЕ(Перечисление.КатегорииЗатраты.Зарплата) КАК КатегорияЗатраты,
	|		ВЫБОР
	|			КОГДА РаспределениеЗаработкаРаботников.Подразделение <> ЗНАЧЕНИЕ(Справочник.Подразделения.ПустаяСсылка)
	|				ТОГДА РаспределениеЗаработкаРаботников.Подразделение
	|			КОГДА УчетЗаработкаРаботников.Подразделение <> ЗНАЧЕНИЕ(Справочник.Подразделения.ПустаяСсылка)
	|				ТОГДА УчетЗаработкаРаботников.Подразделение
	|			ИНАЧЕ ОтражениеВУпрУчете.Подразделение
	|		КОНЕЦ КАК Подразделение,
	|		ВЫБОР
	|			КОГДА ОтражениеВУпрУчете.СпособОтраженияВУпрУчете <> ЗНАЧЕНИЕ(Справочник.СпособыОтраженияЗарплатыВУпрУчете.ПустаяСсылка)
	|				ТОГДА ОтражениеВУпрУчете.СпособОтраженияВУпрУчетеСтатьяЗатрат
	|			КОГДА РаспределениеЗаработкаРаботников.СпособОтраженияВУпрУчете <> ЗНАЧЕНИЕ(Справочник.СпособыОтраженияЗарплатыВУпрУчете.ПустаяСсылка)
	|				ТОГДА РаспределениеЗаработкаРаботников.СпособОтраженияВУпрУчете.СтатьяЗатрат
	|			КОГДА УчетЗаработкаРаботников.СпособОтраженияВУпрУчете <> ЗНАЧЕНИЕ(Справочник.СпособыОтраженияЗарплатыВУпрУчете.ПустаяСсылка)
	|				ТОГДА УчетЗаработкаРаботников.СпособОтраженияВУпрУчете.СтатьяЗатрат
	|			ИНАЧЕ СпособыОтраженияЗарплатыВУпрУчете.СтатьяЗатрат
	|		КОНЕЦ КАК СтатьяЗатрат,
	|		ВЫБОР
	|			КОГДА ОтражениеВУпрУчете.СпособОтраженияВУпрУчете <> ЗНАЧЕНИЕ(Справочник.СпособыОтраженияЗарплатыВУпрУчете.ПустаяСсылка)
	|				ТОГДА ОтражениеВУпрУчете.СпособОтраженияВУпрУчетеНоменклатурнаяГруппа
	|			КОГДА РаспределениеЗаработкаРаботников.СпособОтраженияВУпрУчете <> ЗНАЧЕНИЕ(Справочник.СпособыОтраженияЗарплатыВУпрУчете.ПустаяСсылка)
	|				ТОГДА РаспределениеЗаработкаРаботников.СпособОтраженияВУпрУчете.НоменклатурнаяГруппа
	|			КОГДА УчетЗаработкаРаботников.СпособОтраженияВУпрУчете <> ЗНАЧЕНИЕ(Справочник.СпособыОтраженияЗарплатыВУпрУчете.ПустаяСсылка)
	|				ТОГДА УчетЗаработкаРаботников.СпособОтраженияВУпрУчете.НоменклатурнаяГруппа
	|			ИНАЧЕ СпособыОтраженияЗарплатыВУпрУчете.НоменклатурнаяГруппа
	|		КОНЕЦ КАК НоменклатурнаяГруппа,
	|		ВЫБОР
	|			КОГДА ОтражениеВУпрУчете.СпособОтраженияВУпрУчете <> ЗНАЧЕНИЕ(Справочник.СпособыОтраженияЗарплатыВУпрУчете.ПустаяСсылка)
	|				ТОГДА ОтражениеВУпрУчете.СпособОтраженияВУпрУчетеОбъектСтроительства
	|			КОГДА РаспределениеЗаработкаРаботников.СпособОтраженияВУпрУчете <> ЗНАЧЕНИЕ(Справочник.СпособыОтраженияЗарплатыВУпрУчете.ПустаяСсылка)
	|				ТОГДА РаспределениеЗаработкаРаботников.СпособОтраженияВУпрУчете.ОбъектСтроительства
	|			КОГДА УчетЗаработкаРаботников.СпособОтраженияВУпрУчете <> ЗНАЧЕНИЕ(Справочник.СпособыОтраженияЗарплатыВУпрУчете.ПустаяСсылка)
	|				ТОГДА УчетЗаработкаРаботников.СпособОтраженияВУпрУчете.ОбъектСтроительства
	|			ИНАЧЕ СпособыОтраженияЗарплатыВУпрУчете.ОбъектСтроительства
	|		КОНЕЦ КАК ОбъектСтроительства,
	|		ОтражениеВУпрУчете.Результат * ЕСТЬNULL(РаспределениеЗаработкаРаботников.ДоляСпособаОтражения, 100) / 100 КАК Сумма
	|	ИЗ
	|		ВТУправленческиеНачисления КАК ОтражениеВУпрУчете
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.УчетЗаработкаРаботников КАК УчетЗаработкаРаботников
	|				ВНУТРЕННЕЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|					Начисления.ФизЛицо КАК ФизЛицо,
	|					МАКСИМУМ(УчетЗаработкаРаботников.Период) КАК Период
	|				ИЗ
	|					ВТУправленческиеНачисления КАК Начисления
	|						ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.УчетЗаработкаРаботников КАК УчетЗаработкаРаботников
	|						ПО Начисления.ФизЛицо = УчетЗаработкаРаботников.Физлицо
	|							И Начисления.ПериодДействияНачало >= УчетЗаработкаРаботников.Период
	|				
	|				СГРУППИРОВАТЬ ПО
	|					Начисления.ФизЛицо) КАК СведенияОРаботникахСрез
	|				ПО УчетЗаработкаРаботников.Физлицо = СведенияОРаботникахСрез.ФизЛицо
	|					И УчетЗаработкаРаботников.Период = СведенияОРаботникахСрез.Период
	|			ПО ОтражениеВУпрУчете.ФизЛицо = УчетЗаработкаРаботников.Физлицо
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РаспределениеЗаработкаРаботников КАК РаспределениеЗаработкаРаботников
	|			ПО ОтражениеВУпрУчете.ФизЛицо = РаспределениеЗаработкаРаботников.Физлицо
	|				И (РаспределениеЗаработкаРаботников.ПериодРегистрации = &парамПериодРегистрации)
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.СпособыОтраженияЗарплатыВУпрУчете КАК СпособыОтраженияЗарплатыВУпрУчете
	|			ПО (СпособыОтраженияЗарплатыВУпрУчете.Ссылка = ЗНАЧЕНИЕ(Справочник.СпособыОтраженияЗарплатыВУпрУчете.ОтражениеНачисленийПоУмолчанию))
	|	ГДЕ
	|		ВЫБОР
	|				КОГДА ОтражениеВУпрУчете.СпособОтраженияВУпрУчете <> ЗНАЧЕНИЕ(Справочник.СпособыОтраженияЗарплатыВУпрУчете.ПустаяСсылка)
	|					ТОГДА ОтражениеВУпрУчете.СпособОтраженияВУпрУчете
	|				КОГДА УчетЗаработкаРаботников.СпособОтраженияВУпрУчете <> ЗНАЧЕНИЕ(Справочник.СпособыОтраженияЗарплатыВУпрУчете.ПустаяСсылка)
	|					ТОГДА УчетЗаработкаРаботников.СпособОтраженияВУпрУчете
	|				ИНАЧЕ ЗНАЧЕНИЕ(Справочник.СпособыОтраженияЗарплатыВУпрУчете.ПустаяСсылка)
	|			КОНЕЦ <> ЗНАЧЕНИЕ(Справочник.СпособыОтраженияЗарплатыВУпрУчете.НеОтражатьВУпручете)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ЗНАЧЕНИЕ(Перечисление.КатегорииЗатраты.Зарплата),
	|		ОтражениеВРеглУчете.Подразделение,
	|		ОтражениеВРеглУчете.СтатьяЗатрат,
	|		ОтражениеВРеглУчете.НоменклатурнаяГруппа,
	|		ОтражениеВРеглУчете.ОбъектСтроительства,
	|		ОтражениеВРеглУчете.Результат / ЕСТЬNULL(КурсыВалютДляРасчетовСПерсоналом.Курс, 1) * ЕСТЬNULL(КурсыВалютДляРасчетовСПерсоналом.Кратность, 1)
	|	ИЗ
	|		ВТ_УУДоходыПоВР КАК ОтражениеВРеглУчете
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалютДляРасчетовСПерсоналом КАК КурсыВалютДляРасчетовСПерсоналом
	|			ПО (КурсыВалютДляРасчетовСПерсоналом.Период = &парамПериодРегистрации)
	|				И (КурсыВалютДляРасчетовСПерсоналом.Валюта = &парамВалютаУправленческогоУчета)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ПромежуточнаяТаблица.КатегорияЗатраты,
	|		ПромежуточнаяТаблица.Подразделение,
	|		ПромежуточнаяТаблица.СтатьяЗатрат,
	|		ПромежуточнаяТаблица.НоменклатурнаяГруппа,
	|		ПромежуточнаяТаблица.ОбъектСтроительства,
	|		ПромежуточнаяТаблица.Сумма / ЕСТЬNULL(КурсыВалютДляРасчетовСПерсоналом.Курс, 1) * ЕСТЬNULL(КурсыВалютДляРасчетовСПерсоналом.Кратность, 1)
	|	ИЗ
	|		ВТ_ПромежуточнаяТаблица КАК ПромежуточнаяТаблица
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалютДляРасчетовСПерсоналом КАК КурсыВалютДляРасчетовСПерсоналом
	|			ПО (КурсыВалютДляРасчетовСПерсоналом.Период = &парамПериодРегистрации)
	|				И (КурсыВалютДляРасчетовСПерсоналом.Валюта = &парамВалютаУправленческогоУчета)) КАК ОтражениеВУчете
	|
	|СГРУППИРОВАТЬ ПО
	|	ОтражениеВУчете.КатегорияЗатраты,
	|	ОтражениеВУчете.Подразделение,
	|	ОтражениеВУчете.СтатьяЗатрат,
	|	ОтражениеВУчете.НоменклатурнаяГруппа,
	|	ОтражениеВУчете.ОбъектСтроительства
	|
	|УПОРЯДОЧИТЬ ПО
	|	Подразделение,
	|	СтатьяЗатрат,
	|	НоменклатурнаяГруппа,
	|	ОбъектСтроительства,
	|	КатегорияЗатраты
	|АВТОУПОРЯДОЧИВАНИЕ";
	
	ОтражениеВУчете.Загрузить(Запрос.Выполнить().Выгрузить());
	
	//
	// отражение начислений
	//////////////////////////////////////////////////////////////////////////////////////////////////
	
КонецПроцедуры // ЗаполнитьМетодомНачислений

Процедура ЗаполнитьКассовымМетодом()
	
	Запрос = Новый Запрос;
	
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("парамПериодРегистрации",			ДобавитьМесяц(ПериодРегистрации,-1));
	Запрос.УстановитьПараметр("парамВалютаУправленческогоУчета",мВалютаУправленческогоУчета);
	
	// Проверим, что задан курс валюты для расчетов с персоналом
	Если мВалютаРегламентированногоУчета <> мВалютаУправленческогоУчета Тогда
		СрезКурсаВалюты	= РегистрыСведений.КурсыВалютДляРасчетовСПерсоналом.Получить(НачалоМесяца(ПериодРегистрации), Новый Структура("Валюта", мВалютаУправленческогоУчета));
		Если Не ЗначениеЗаполнено(СрезКурсаВалюты.Курс) ИЛИ Не ЗначениеЗаполнено(СрезКурсаВалюты.Кратность) Тогда
			ОбщегоНазначения.СообщитьОбОшибке("Не задан курс " + мВалютаУправленческогоУчета + " для расчетов с персоналом на " + ПредставлениеПериода(НачалоМесяца(ПериодРегистрации), КонецМесяца(ПериодРегистрации))+ "!");
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	ОтражениеВУчете.Очистить();
	
	// сдельный заработок по упр.нарядам рассматриваем отдельно, в этот документ он попадать не должен
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Начисления.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ВТ_ОтражаемыеНачисления
	|ИЗ
	|	ПланВидовРасчета.УправленческиеНачисления КАК Начисления
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовРасчета.УправленческиеНачисления.Показатели КАК Показатели1
	|		ПО Начисления.Ссылка = Показатели1.Ссылка
	|			И (Показатели1.НомерСтроки = 1)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовРасчета.УправленческиеНачисления.Показатели КАК Показатели2
	|		ПО Начисления.Ссылка = Показатели2.Ссылка
	|			И (Показатели2.НомерСтроки = 2)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовРасчета.УправленческиеНачисления.Показатели КАК Показатели3
	|		ПО Начисления.Ссылка = Показатели3.Ссылка
	|			И (Показатели3.НомерСтроки = 3)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовРасчета.УправленческиеНачисления.Показатели КАК Показатели4
	|		ПО Начисления.Ссылка = Показатели4.Ссылка
	|			И (Показатели4.НомерСтроки = 4)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовРасчета.УправленческиеНачисления.Показатели КАК Показатели5
	|		ПО Начисления.Ссылка = Показатели5.Ссылка
	|			И (Показатели5.НомерСтроки = 5)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовРасчета.УправленческиеНачисления.Показатели КАК Показатели6
	|		ПО Начисления.Ссылка = Показатели6.Ссылка
	|			И (Показатели6.НомерСтроки = 6)
	|ГДЕ
	|	((НЕ Начисления.ЗачетОтработанногоВремени)
	|			ИЛИ ЕСТЬNULL(Показатели1.Показатель, ЗНАЧЕНИЕ(Справочник.ПоказателиСхемМотивации.ПустаяСсылка)) <> ЗНАЧЕНИЕ(Справочник.ПоказателиСхемМотивации.СдельнаяВыработка)
	|				И ЕСТЬNULL(Показатели2.Показатель, ЗНАЧЕНИЕ(Справочник.ПоказателиСхемМотивации.ПустаяСсылка)) <> ЗНАЧЕНИЕ(Справочник.ПоказателиСхемМотивации.СдельнаяВыработка)
	|				И ЕСТЬNULL(Показатели3.Показатель, ЗНАЧЕНИЕ(Справочник.ПоказателиСхемМотивации.ПустаяСсылка)) <> ЗНАЧЕНИЕ(Справочник.ПоказателиСхемМотивации.СдельнаяВыработка)
	|				И ЕСТЬNULL(Показатели4.Показатель, ЗНАЧЕНИЕ(Справочник.ПоказателиСхемМотивации.ПустаяСсылка)) <> ЗНАЧЕНИЕ(Справочник.ПоказателиСхемМотивации.СдельнаяВыработка)
	|				И ЕСТЬNULL(Показатели5.Показатель, ЗНАЧЕНИЕ(Справочник.ПоказателиСхемМотивации.ПустаяСсылка)) <> ЗНАЧЕНИЕ(Справочник.ПоказателиСхемМотивации.СдельнаяВыработка)
	|				И ЕСТЬNULL(Показатели6.Показатель, ЗНАЧЕНИЕ(Справочник.ПоказателиСхемМотивации.ПустаяСсылка)) <> ЗНАЧЕНИЕ(Справочник.ПоказателиСхемМотивации.СдельнаяВыработка))
	|	И Начисления.Ссылка <> ЗНАЧЕНИЕ(ПланВидовРасчета.УправленческиеНачисления.НачисленоПоБухгалтерии)
	|	И Начисления.Ссылка <> ЗНАЧЕНИЕ(ПланВидовРасчета.УправленческиеНачисления.НачисленоПоБухгалтерииСдельно)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка";
	Запрос.Выполнить();
	
	// из регистра расчетов УправленческиеНачисления берем начисления только по утвержденным схемам мотивации
	// пропускаем тех сотрудников, кому в рег.учете назначено доначисление по данным упр.учета, так как их затраты 
	// уже учтены в данных регучета
	
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	УправленческиеНачисления.ФизЛицо КАК ФизЛицо,
	|	УправленческиеНачисления.ВидРасчета,
	|	УправленческиеНачисления.ВидРасчета.ЗачетОтработанногоВремени КАК ВидРасчетаЗачетОтработанногоВремени,
	|	УправленческиеНачисления.ВидРасчета.СпособОтраженияВУпрУчете КАК СпособОтраженияВУпрУчете,
	|	УправленческиеНачисления.ВидРасчета.СпособОтраженияВУпрУчете.СтатьяЗатрат КАК СпособОтраженияВУпрУчетеСтатьяЗатрат,
	|	УправленческиеНачисления.ВидРасчета.СпособОтраженияВУпрУчете.НоменклатурнаяГруппа КАК СпособОтраженияВУпрУчетеНоменклатурнаяГруппа,
	|	УправленческиеНачисления.ВидРасчета.СпособОтраженияВУпрУчете.ОбъектСтроительства КАК СпособОтраженияВУпрУчетеОбъектСтроительства,
	|	УправленческиеНачисления.Подразделение,
	|	УправленческиеНачисления.ПериодДействияНачало,
	|	УправленческиеНачисления.Результат
	|ПОМЕСТИТЬ ВТУправленческиеНачисления
	|ИЗ
	|	РегистрРасчета.УправленческиеНачисления КАК УправленческиеНачисления
	|ГДЕ
	|	УправленческиеНачисления.ПериодРегистрации = &парамПериодРегистрации
	|	И УправленческиеНачисления.ВидРасчета В
	|			(ВЫБРАТЬ
	|				ВидыРасчета.Ссылка
	|			ИЗ
	|				ВТ_ОтражаемыеНачисления КАК ВидыРасчета)
	|	И (НЕ УправленческиеНачисления.ПоВременнойСхемеМотивации)
	|	И (НЕ УправленческиеНачисления.ФизЛицо В
	|				(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|					ПлановыеНачисленияРаботниковОрганизацийСрезПоследних.Сотрудник.Физлицо
	|				ИЗ
	|					РегистрСведений.ПлановыеНачисленияРаботниковОрганизаций.СрезПоследних(&парамПериодРегистрации, Сотрудник.Физлицо В
	|						(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|							УправленческиеНачисления.ФизЛицо
	|						ИЗ
	|							РегистрРасчета.УправленческиеНачисления КАК УправленческиеНачисления
	|						ГДЕ
	|							УправленческиеНачисления.ПериодРегистрации = &парамПериодРегистрации)) КАК ПлановыеНачисленияРаботниковОрганизацийСрезПоследних
	|				ГДЕ
	|					ПлановыеНачисленияРаботниковОрганизацийСрезПоследних.ВидРасчетаИзмерение.СпособРасчета = ЗНАЧЕНИЕ(Перечисление.СпособыРасчетаОплатыТруда.ДоначислениеПоУправленческомуУчету)))
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ФизЛицо";
	Запрос.Выполнить();
	
	// УУДоходыПоВР
	//	Поля:
	//		ОбособленноеПодразделение
	//		Сотрудник
	//		Физлицо
	//		ВидРасчета
	//		ПериодРегистрации
	//		Подразделение
	//		СтатьяЗатрат
	//		НоменклатурнаяГруппа
	//		ОбъектСтроительства
	//		Результат
	//	
	//	Описание:
	//	Данные отражения начислений в упручете
	
	УУДоходыПоВРТекст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Начисления.ОбособленноеПодразделение КАК ОбособленноеПодразделение,
	|	Начисления.Сотрудник КАК Сотрудник,
	|	Начисления.Физлицо,
	|	Начисления.ВидРасчета КАК ВидРасчета,
	|	Начисления.ПериодРегистрации КАК ПериодРегистрации,
	|	Начисления.ПериодДействия КАК ПериодДействия,
	|	ВЫБОР
	|		КОГДА Начисления.Подразделение = ЗНАЧЕНИЕ(Справочник.Подразделения.ПустаяСсылка)
	|				И (НЕ СоответствиеПодразделенийИПодразделенийОрганизаций.Подразделение ЕСТЬ NULL )
	|			ТОГДА СоответствиеПодразделенийИПодразделенийОрганизаций.Подразделение
	|		ИНАЧЕ Начисления.Подразделение
	|	КОНЕЦ КАК Подразделение,
	|	Начисления.СтатьяЗатрат,
	|	Начисления.НоменклатурнаяГруппа,
	|	Начисления.ОбъектСтроительства,
	|	ВЫБОР
	|		КОГДА Начисления.Подразделение = ЗНАЧЕНИЕ(Справочник.Подразделения.ПустаяСсылка)
	|				И (НЕ СоответствиеПодразделенийИПодразделенийОрганизаций.Подразделение ЕСТЬ NULL )
	|			ТОГДА Начисления.Результат / ЕСТЬNULL(СоответствиеПодразделенийКоличество.КоличествоПодразделений, 1)
	|		ИНАЧЕ Начисления.Результат
	|	КОНЕЦ КАК Результат,
	|	Начисления.ЭтоСтатьяЗатратЕНВД
	|ПОМЕСТИТЬ ВТ_УУДоходыПоВР
	|ИЗ
	|	(ВЫБРАТЬ
	|		ОсновныеНачисления.ОбособленноеПодразделение КАК ОбособленноеПодразделение,
	|		ОсновныеНачисления.Сотрудник КАК Сотрудник,
	|		ОсновныеНачисления.ФизЛицо КАК Физлицо,
	|		ОсновныеНачисления.ВидРасчета КАК ВидРасчета,
	|		ОсновныеНачисления.ПериодРегистрации КАК ПериодРегистрации,
	|		ОсновныеНачисления.ПериодДействия КАК ПериодДействия,
	|		ВЫБОР
	|			КОГДА РаспределениеЗаработкаРаботников.Подразделение ЕСТЬ НЕ NULL 
	|					И РаспределениеЗаработкаРаботников.Подразделение <> ЗНАЧЕНИЕ(Справочник.Подразделения.ПустаяСсылка)
	|				ТОГДА РаспределениеЗаработкаРаботников.Подразделение
	|			КОГДА УчетЗаработкаРаботников.Подразделение ЕСТЬ НЕ NULL 
	|					И УчетЗаработкаРаботников.Подразделение <> ЗНАЧЕНИЕ(Справочник.Подразделения.ПустаяСсылка)
	|				ТОГДА УчетЗаработкаРаботников.Подразделение
	|			КОГДА Работники.Подразделение ЕСТЬ НЕ NULL 
	|					И Работники.Подразделение <> ЗНАЧЕНИЕ(Справочник.Подразделения.ПустаяСсылка)
	|				ТОГДА Работники.Подразделение
	|			ИНАЧЕ ЗНАЧЕНИЕ(Справочник.Подразделения.ПустаяСсылка)
	|		КОНЕЦ КАК Подразделение,
	|		ЕСТЬNULL(ОсновныеНачисления.ВидРасчета.СпособОтраженияВУпрУчете.СтатьяЗатрат, ЕСТЬNULL(РаспределениеЗаработкаРаботников.СпособОтраженияВУпрУчете.СтатьяЗатрат, ЕСТЬNULL(УчетЗаработкаРаботников.СпособОтраженияВУпрУчете.СтатьяЗатрат, СпособыОтраженияЗарплатыВУпрУчете.СтатьяЗатрат))) КАК СтатьяЗатрат,
	|		ЕСТЬNULL(ОсновныеНачисления.ВидРасчета.СпособОтраженияВУпрУчете.НоменклатурнаяГруппа, ЕСТЬNULL(РаспределениеЗаработкаРаботников.СпособОтраженияВУпрУчете.НоменклатурнаяГруппа, ЕСТЬNULL(УчетЗаработкаРаботников.СпособОтраженияВУпрУчете.НоменклатурнаяГруппа, СпособыОтраженияЗарплатыВУпрУчете.НоменклатурнаяГруппа))) КАК НоменклатурнаяГруппа,
	|		ЕСТЬNULL(ОсновныеНачисления.ВидРасчета.СпособОтраженияВУпрУчете.ОбъектСтроительства, ЕСТЬNULL(РаспределениеЗаработкаРаботников.СпособОтраженияВУпрУчете.ОбъектСтроительства, ЕСТЬNULL(УчетЗаработкаРаботников.СпособОтраженияВУпрУчете.ОбъектСтроительства, СпособыОтраженияЗарплатыВУпрУчете.ОбъектСтроительства))) КАК ОбъектСтроительства,
	|		ОсновныеНачисления.Результат * ЕСТЬNULL(РаспределениеЗаработкаРаботников.ДоляСпособаОтражения, 100) / 100 КАК Результат,
	//|		ВЫБОР
	//|			КОГДА (ЕСТЬNULL(ОсновныеНачисления.ВидРасчета.СпособОтраженияВУпрУчете.СтатьяЗатрат, ЕСТЬNULL(РаспределениеЗаработкаРаботников.СпособОтраженияВУпрУчете.СтатьяЗатрат, ЕСТЬNULL(УчетЗаработкаРаботников.СпособОтраженияВУпрУчете.СтатьяЗатрат, СпособыОтраженияЗарплатыВУпрУчете.СтатьяЗатрат)))).ОтнесениеРасходовКДеятельностиЕНВД = ЗНАЧЕНИЕ(Перечисление.ОтнесениеРасходовКДеятельностиЕНВД.РасходыОтносятсяКЕНВД)
	//|				ТОГДА ИСТИНА
	//|			ИНАЧЕ ЛОЖЬ
	//|		КОНЕЦ КАК ЭтоСтатьяЗатратЕНВД,
	|		ВЫБОР
	|			КОГДА ОсновныеНачисления.ВидРасчета.ОтнесениеРасходовКДеятельностиЕНВД = ЗНАЧЕНИЕ(Перечисление.ОтнесениеРасходовКДеятельностиЕНВД.РасходыОтносятсяКЕНВД)
	|				ТОГДА ИСТИНА
	|			КОГДА ЕНВДПроцент.СчетДт ЕСТЬ НЕ NULL
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ЛОЖЬ
	|		КОНЕЦ КАК ЭтоСтатьяЗатратЕНВД,
	|		ОсновныеНачисления.ПодразделениеОрганизации КАК ПодразделениеОрганизации
	|	ИЗ
	|		РегистрРасчета.ОсновныеНачисленияРаботниковОрганизаций КАК ОсновныеНачисления
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.УчетЗаработкаРаботников КАК УчетЗаработкаРаботников
	|				ВНУТРЕННЕЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|					МАКСИМУМ(УчетЗаработкаРаботников.Период) КАК Период,
	|					УчетЗаработкаРаботников.Физлицо КАК Физлицо
	|				ИЗ
	|					РегистрРасчета.ОсновныеНачисленияРаботниковОрганизаций КАК ОсновныеНачисления
	|						ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.УчетЗаработкаРаботников КАК УчетЗаработкаРаботников
	|						ПО ОсновныеНачисления.ФизЛицо = УчетЗаработкаРаботников.Физлицо
	|							И ОсновныеНачисления.ПериодРегистрации >= УчетЗаработкаРаботников.Период
	|				ГДЕ
	|					ОсновныеНачисления.ПериодРегистрации = &парамПериодРегистрации
	|				
	|				СГРУППИРОВАТЬ ПО
	|					УчетЗаработкаРаботников.Физлицо) КАК УчетЗаработкаСрез
	|				ПО УчетЗаработкаРаботников.Физлицо = УчетЗаработкаСрез.Физлицо
	|					И УчетЗаработкаРаботников.Период = УчетЗаработкаСрез.Период
	|			ПО ОсновныеНачисления.ФизЛицо = УчетЗаработкаРаботников.Физлицо
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РаспределениеЗаработкаРаботников КАК РаспределениеЗаработкаРаботников
	|			ПО ОсновныеНачисления.ПериодРегистрации = РаспределениеЗаработкаРаботников.ПериодРегистрации
	|				И ОсновныеНачисления.ФизЛицо = РаспределениеЗаработкаРаботников.Физлицо
	|			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.СпособыОтраженияЗарплатыВУпрУчете КАК СпособыОтраженияЗарплатыВУпрУчете
	|			ПО (СпособыОтраженияЗарплатыВУпрУчете.Ссылка = ЗНАЧЕНИЕ(Справочник.СпособыОтраженияЗарплатыВУпрУчете.ОтражениеНачисленийПоУмолчанию))
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.Работники КАК Работники
	|				ВНУТРЕННЕЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|					МАКСИМУМ(Работники.Период) КАК Период,
	|					Работники.ФизЛицо КАК Физлицо
	|				ИЗ
	|					РегистрРасчета.ОсновныеНачисленияРаботниковОрганизаций КАК ОсновныеНачисления
	|						ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.Работники КАК Работники
	|						ПО ОсновныеНачисления.ФизЛицо = Работники.ФизЛицо
	|							И ОсновныеНачисления.ПериодРегистрации >= Работники.Период
	|				ГДЕ
	|					ОсновныеНачисления.ПериодРегистрации = &парамПериодРегистрации
	|				
	|				СГРУППИРОВАТЬ ПО
	|					Работники.ФизЛицо) КАК РаботникиСрез
	|				ПО Работники.ФизЛицо = РаботникиСрез.Физлицо
	|					И Работники.Период = РаботникиСрез.Период
	|			ПО ОсновныеНачисления.ФизЛицо = Работники.ФизЛицо
	|				И (Работники.ПричинаИзмененияСостояния <> ЗНАЧЕНИЕ(Перечисление.ПричиныИзмененияСостояния.Увольнение))
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПроцентДеятельностиЕНВДСотрудников КАК ЕНВДПроцент
	|			ПО ОсновныеНачисления.Сотрудник = ЕНВДПроцент.Сотрудник
	|				И (ВЫБОР
	|					КОГДА ОсновныеНачисления.ВидРасчета.СтратегияОтраженияВУчете = ЗНАЧЕНИЕ(Перечисление.СтратегииОтраженияВРеглУчетеНачислений.КакЗаданоНаНачалоСобытия)
	|						ТОГДА НАЧАЛОПЕРИОДА(ОсновныеНачисления.ДатаНачалаСобытия, МЕСЯЦ)
	|					КОГДА ОсновныеНачисления.ПериодДействия > ОсновныеНачисления.ПериодРегистрации
	|						ТОГДА ОсновныеНачисления.ПериодРегистрации
	|					ИНАЧЕ ОсновныеНачисления.ПериодДействия
	|				КОНЕЦ = ЕНВДПроцент.ПериодРегистрации)
	|				И ((НЕ ОсновныеНачисления.Сторно))
	|	ГДЕ
	|		ОсновныеНачисления.ПериодРегистрации = &парамПериодРегистрации
	|		И ОсновныеНачисления.Результат <> 0
	|		И ОсновныеНачисления.ВидРасчета.СпособОтраженияВУпрУчете <> ЗНАЧЕНИЕ(Справочник.СпособыОтраженияЗарплатыВУпрУчете.НеОтражатьВУпрУчете)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ДополнительныеНачисления.ОбособленноеПодразделение,
	|		ДополнительныеНачисления.Сотрудник,
	|		ДополнительныеНачисления.ФизЛицо,
	|		ДополнительныеНачисления.ВидРасчета,
	|		ДополнительныеНачисления.ПериодРегистрации,
	|		ДополнительныеНачисления.ПериодРегистрации,
	|		ВЫБОР
	|			КОГДА РаспределениеЗаработкаРаботников.Подразделение ЕСТЬ НЕ NULL 
	|					И РаспределениеЗаработкаРаботников.Подразделение <> ЗНАЧЕНИЕ(Справочник.Подразделения.ПустаяСсылка)
	|				ТОГДА РаспределениеЗаработкаРаботников.Подразделение
	|			КОГДА УчетЗаработкаРаботников.Подразделение ЕСТЬ НЕ NULL 
	|					И УчетЗаработкаРаботников.Подразделение <> ЗНАЧЕНИЕ(Справочник.Подразделения.ПустаяСсылка)
	|				ТОГДА УчетЗаработкаРаботников.Подразделение
	|			КОГДА Работники.Подразделение ЕСТЬ НЕ NULL 
	|					И Работники.Подразделение <> ЗНАЧЕНИЕ(Справочник.Подразделения.ПустаяСсылка)
	|				ТОГДА Работники.Подразделение
	|			ИНАЧЕ ЕСТЬNULL(СоответствиеПодразделенийИПодразделенийОрганизаций.Подразделение, ЗНАЧЕНИЕ(Справочник.Подразделения.ПустаяСсылка))
	|		КОНЕЦ,
	|		ЕСТЬNULL(ДополнительныеНачисления.ВидРасчета.СпособОтраженияВУпрУчете.СтатьяЗатрат, ЕСТЬNULL(РаспределениеЗаработкаРаботников.СпособОтраженияВУпрУчете.СтатьяЗатрат, ЕСТЬNULL(УчетЗаработкаРаботников.СпособОтраженияВУпрУчете.СтатьяЗатрат, СпособыОтраженияЗарплатыВУпрУчете.СтатьяЗатрат))),
	|		ЕСТЬNULL(ДополнительныеНачисления.ВидРасчета.СпособОтраженияВУпрУчете.НоменклатурнаяГруппа, ЕСТЬNULL(РаспределениеЗаработкаРаботников.СпособОтраженияВУпрУчете.НоменклатурнаяГруппа, ЕСТЬNULL(УчетЗаработкаРаботников.СпособОтраженияВУпрУчете.НоменклатурнаяГруппа, СпособыОтраженияЗарплатыВУпрУчете.НоменклатурнаяГруппа))),
	|		ЕСТЬNULL(ДополнительныеНачисления.ВидРасчета.СпособОтраженияВУпрУчете.ОбъектСтроительства, ЕСТЬNULL(РаспределениеЗаработкаРаботников.СпособОтраженияВУпрУчете.ОбъектСтроительства, ЕСТЬNULL(УчетЗаработкаРаботников.СпособОтраженияВУпрУчете.ОбъектСтроительства, СпособыОтраженияЗарплатыВУпрУчете.ОбъектСтроительства))),
	|		ДополнительныеНачисления.Результат * ЕСТЬNULL(РаспределениеЗаработкаРаботников.ДоляСпособаОтражения, 100) / 100,
	//|		ВЫБОР
	//|			КОГДА (ЕСТЬNULL(ДополнительныеНачисления.ВидРасчета.СпособОтраженияВУпрУчете.СтатьяЗатрат, ЕСТЬNULL(РаспределениеЗаработкаРаботников.СпособОтраженияВУпрУчете.СтатьяЗатрат, ЕСТЬNULL(УчетЗаработкаРаботников.СпособОтраженияВУпрУчете.СтатьяЗатрат, СпособыОтраженияЗарплатыВУпрУчете.СтатьяЗатрат)))).ОтнесениеРасходовКДеятельностиЕНВД = ЗНАЧЕНИЕ(Перечисление.ОтнесениеРасходовКДеятельностиЕНВД.РасходыОтносятсяКЕНВД)
	//|				ТОГДА ИСТИНА
	//|			ИНАЧЕ ЛОЖЬ
	//|		КОНЕЦ,
	|		ВЫБОР
	|			КОГДА ДополнительныеНачисления.ВидРасчета.ОтнесениеРасходовКДеятельностиЕНВД = ЗНАЧЕНИЕ(Перечисление.ОтнесениеРасходовКДеятельностиЕНВД.РасходыОтносятсяКЕНВД)
	|				ТОГДА ИСТИНА
	|			КОГДА ЕНВДПроцент.СчетДт ЕСТЬ НЕ NULL
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ЛОЖЬ
	|		КОНЕЦ КАК ЭтоСтатьяЗатратЕНВД,
	|		ДополнительныеНачисления.ПодразделениеОрганизации
	|	ИЗ
	|		РегистрРасчета.ДополнительныеНачисленияРаботниковОрганизаций КАК ДополнительныеНачисления
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СоответствиеПодразделенийИПодразделенийОрганизаций КАК СоответствиеПодразделенийИПодразделенийОрганизаций
	|			ПО ДополнительныеНачисления.ОбособленноеПодразделение = СоответствиеПодразделенийИПодразделенийОрганизаций.Организация
	|				И ДополнительныеНачисления.ПодразделениеОрганизации = СоответствиеПодразделенийИПодразделенийОрганизаций.ПодразделениеОрганизации
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.УчетЗаработкаРаботников КАК УчетЗаработкаРаботников
	|				ВНУТРЕННЕЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|					МАКСИМУМ(УчетЗаработкаРаботников.Период) КАК Период,
	|					УчетЗаработкаРаботников.Физлицо КАК Физлицо
	|				ИЗ
	|					РегистрРасчета.ДополнительныеНачисленияРаботниковОрганизаций КАК ДополнительныеНачисления
	|						ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.УчетЗаработкаРаботников КАК УчетЗаработкаРаботников
	|						ПО ДополнительныеНачисления.ФизЛицо = УчетЗаработкаРаботников.Физлицо
	|							И ДополнительныеНачисления.ПериодРегистрации >= УчетЗаработкаРаботников.Период
	|				ГДЕ
	|					НАЧАЛОПЕРИОДА(ДополнительныеНачисления.ПериодРегистрации, МЕСЯЦ) = &парамПериодРегистрации
	|				
	|				СГРУППИРОВАТЬ ПО
	|					УчетЗаработкаРаботников.Физлицо) КАК УчетЗаработкаСрез
	|				ПО УчетЗаработкаРаботников.Период = УчетЗаработкаСрез.Период
	|					И УчетЗаработкаРаботников.Физлицо = УчетЗаработкаСрез.Физлицо
	|			ПО ДополнительныеНачисления.ФизЛицо = УчетЗаработкаРаботников.Физлицо
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РаспределениеЗаработкаРаботников КАК РаспределениеЗаработкаРаботников
	|			ПО НАЧАЛОПЕРИОДА(ДополнительныеНачисления.ПериодРегистрации, МЕСЯЦ) = РаспределениеЗаработкаРаботников.ПериодРегистрации
	|				И ДополнительныеНачисления.ФизЛицо = РаспределениеЗаработкаРаботников.Физлицо
	|			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.СпособыОтраженияЗарплатыВУпрУчете КАК СпособыОтраженияЗарплатыВУпрУчете
	|			ПО (СпособыОтраженияЗарплатыВУпрУчете.Ссылка = ЗНАЧЕНИЕ(Справочник.СпособыОтраженияЗарплатыВУпрУчете.ОтражениеНачисленийПоУмолчанию))
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.Работники КАК Работники
	|				ВНУТРЕННЕЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|					МАКСИМУМ(Работники.Период) КАК Период,
	|					Работники.ФизЛицо КАК Физлицо
	|				ИЗ
	|					РегистрРасчета.ДополнительныеНачисленияРаботниковОрганизаций КАК ДополнительныеНачисления
	|						ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.Работники КАК Работники
	|						ПО ДополнительныеНачисления.ФизЛицо = Работники.ФизЛицо
	|							И ДополнительныеНачисления.ПериодРегистрации >= Работники.Период
	|				ГДЕ
	|					НАЧАЛОПЕРИОДА(ДополнительныеНачисления.ПериодРегистрации, МЕСЯЦ) = &парамПериодРегистрации
	|				
	|				СГРУППИРОВАТЬ ПО
	|					Работники.ФизЛицо) КАК РаботникиСрез
	|				ПО Работники.Период = РаботникиСрез.Период
	|					И Работники.ФизЛицо = РаботникиСрез.Физлицо
	|			ПО ДополнительныеНачисления.ФизЛицо = Работники.ФизЛицо
	|				И (Работники.ПричинаИзмененияСостояния <> ЗНАЧЕНИЕ(Перечисление.ПричиныИзмененияСостояния.Увольнение))
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПроцентДеятельностиЕНВДСотрудников КАК ЕНВДПроцент
	|			ПО ДополнительныеНачисления.Сотрудник = ЕНВДПроцент.Сотрудник
	|				И (ВЫБОР
	|					КОГДА ДополнительныеНачисления.ВидРасчета.СтратегияОтраженияВУчете = ЗНАЧЕНИЕ(Перечисление.СтратегииОтраженияВРеглУчетеНачислений.КакЗаданоНаНачалоСобытия)
	|						ТОГДА НАЧАЛОПЕРИОДА(ДополнительныеНачисления.ДатаНачалаСобытия, МЕСЯЦ)
	|					ИНАЧЕ ДополнительныеНачисления.ПериодРегистрации
	|				КОНЕЦ = ЕНВДПроцент.ПериодРегистрации)
	|				И ((НЕ ДополнительныеНачисления.Сторно))
	|	ГДЕ
	|		НАЧАЛОПЕРИОДА(ДополнительныеНачисления.ПериодРегистрации, МЕСЯЦ) = &парамПериодРегистрации
	|		И ДополнительныеНачисления.Результат <> 0
	|		И ДополнительныеНачисления.ВидРасчета.СпособОтраженияВУпрУчете <> ЗНАЧЕНИЕ(Справочник.СпособыОтраженияЗарплатыВУпрУчете.НеОтражатьВУпрУчете)) КАК Начисления
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СоответствиеПодразделенийИПодразделенийОрганизаций КАК СоответствиеПодразделенийИПодразделенийОрганизаций
	|		ПО Начисления.ОбособленноеПодразделение = СоответствиеПодразделенийИПодразделенийОрганизаций.Организация
	|			И Начисления.ПодразделениеОрганизации = СоответствиеПодразделенийИПодразделенийОрганизаций.ПодразделениеОрганизации
	|			И (Начисления.Подразделение = ЗНАЧЕНИЕ(Справочник.Подразделения.ПустаяСсылка))
	|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|			СоответствиеПодразделенийИПодразделенийОрганизаций.Организация КАК Организация,
	|			СоответствиеПодразделенийИПодразделенийОрганизаций.ПодразделениеОрганизации КАК ПодразделениеОрганизации,
	|			КОЛИЧЕСТВО(РАЗЛИЧНЫЕ СоответствиеПодразделенийИПодразделенийОрганизаций.Подразделение) КАК КоличествоПодразделений
	|		ИЗ
	|			РегистрСведений.СоответствиеПодразделенийИПодразделенийОрганизаций КАК СоответствиеПодразделенийИПодразделенийОрганизаций
	|		
	|		СГРУППИРОВАТЬ ПО
	|			СоответствиеПодразделенийИПодразделенийОрганизаций.Организация,
	|			СоответствиеПодразделенийИПодразделенийОрганизаций.ПодразделениеОрганизации) КАК СоответствиеПодразделенийКоличество
	|		ПО Начисления.ОбособленноеПодразделение = СоответствиеПодразделенийКоличество.Организация
	|			И Начисления.ПодразделениеОрганизации = СоответствиеПодразделенийКоличество.ПодразделениеОрганизации
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОбособленноеПодразделение,
	|	Сотрудник,
	|	ВидРасчета,
	|	ПериодРегистрации,
	|	ПериодДействия";
	
	Запрос.Текст = УУДоходыПоВРТекст;
	Запрос.Выполнить();
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(Перечисление.КатегорииЗатраты.Зарплата) КАК КатегорияЗатраты,
	|	ОтражениеВУчете.Подразделение КАК Подразделение,
	|	ОтражениеВУчете.СтатьяЗатрат КАК СтатьяЗатрат,
	|	ОтражениеВУчете.НоменклатурнаяГруппа КАК НоменклатурнаяГруппа,
	|	ОтражениеВУчете.ОбъектСтроительства КАК ОбъектСтроительства,
	|	СУММА(ОтражениеВУчете.Сумма) КАК СуммаЗарплата,
	|	СУММА(ВЫБОР
	|			КОГДА ОтражениеВУчете.РаспределятьЕСН
	|				ТОГДА ОтражениеВУчете.Сумма
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК СуммаЕСН,
	|	СУММА(ВЫБОР
	|			КОГДА ОтражениеВУчете.РаспределятьНДФЛ
	|				ТОГДА ОтражениеВУчете.Сумма
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК СуммаНДФЛ
	|ИЗ
	|	(ВЫБРАТЬ
	|		ВЫБОР
	|			КОГДА РаспределениеЗаработкаРаботников.Подразделение <> ЗНАЧЕНИЕ(Справочник.Подразделения.ПустаяСсылка)
	|				ТОГДА РаспределениеЗаработкаРаботников.Подразделение
	|			КОГДА УчетЗаработкаРаботников.Подразделение <> ЗНАЧЕНИЕ(Справочник.Подразделения.ПустаяСсылка)
	|				ТОГДА УчетЗаработкаРаботников.Подразделение
	|			ИНАЧЕ ОтражениеВУпрУчете.Подразделение
	|		КОНЕЦ КАК Подразделение,
	|		ВЫБОР
	|			КОГДА ОтражениеВУпрУчете.СпособОтраженияВУпрУчете <> ЗНАЧЕНИЕ(Справочник.СпособыОтраженияЗарплатыВУпрУчете.ПустаяСсылка)
	|				ТОГДА ОтражениеВУпрУчете.СпособОтраженияВУпрУчетеСтатьяЗатрат
	|			КОГДА РаспределениеЗаработкаРаботников.СпособОтраженияВУпрУчете <> ЗНАЧЕНИЕ(Справочник.СпособыОтраженияЗарплатыВУпрУчете.ПустаяСсылка)
	|				ТОГДА РаспределениеЗаработкаРаботников.СпособОтраженияВУпрУчете.СтатьяЗатрат
	|			КОГДА УчетЗаработкаРаботников.СпособОтраженияВУпрУчете <> ЗНАЧЕНИЕ(Справочник.СпособыОтраженияЗарплатыВУпрУчете.ПустаяСсылка)
	|				ТОГДА УчетЗаработкаРаботников.СпособОтраженияВУпрУчете.СтатьяЗатрат
	|			ИНАЧЕ СпособыОтраженияЗарплатыВУпрУчете.СтатьяЗатрат
	|		КОНЕЦ КАК СтатьяЗатрат,
	|		ВЫБОР
	|			КОГДА ОтражениеВУпрУчете.СпособОтраженияВУпрУчете <> ЗНАЧЕНИЕ(Справочник.СпособыОтраженияЗарплатыВУпрУчете.ПустаяСсылка)
	|				ТОГДА ОтражениеВУпрУчете.СпособОтраженияВУпрУчетеНоменклатурнаяГруппа
	|			КОГДА РаспределениеЗаработкаРаботников.СпособОтраженияВУпрУчете <> ЗНАЧЕНИЕ(Справочник.СпособыОтраженияЗарплатыВУпрУчете.ПустаяСсылка)
	|				ТОГДА РаспределениеЗаработкаРаботников.СпособОтраженияВУпрУчете.НоменклатурнаяГруппа
	|			КОГДА УчетЗаработкаРаботников.СпособОтраженияВУпрУчете <> ЗНАЧЕНИЕ(Справочник.СпособыОтраженияЗарплатыВУпрУчете.ПустаяСсылка)
	|				ТОГДА УчетЗаработкаРаботников.СпособОтраженияВУпрУчете.НоменклатурнаяГруппа
	|			ИНАЧЕ СпособыОтраженияЗарплатыВУпрУчете.НоменклатурнаяГруппа
	|		КОНЕЦ КАК НоменклатурнаяГруппа,
	|		ВЫБОР
	|			КОГДА ОтражениеВУпрУчете.СпособОтраженияВУпрУчете <> ЗНАЧЕНИЕ(Справочник.СпособыОтраженияЗарплатыВУпрУчете.ПустаяСсылка)
	|				ТОГДА ОтражениеВУпрУчете.СпособОтраженияВУпрУчетеОбъектСтроительства
	|			КОГДА РаспределениеЗаработкаРаботников.СпособОтраженияВУпрУчете <> ЗНАЧЕНИЕ(Справочник.СпособыОтраженияЗарплатыВУпрУчете.ПустаяСсылка)
	|				ТОГДА РаспределениеЗаработкаРаботников.СпособОтраженияВУпрУчете.ОбъектСтроительства
	|			КОГДА УчетЗаработкаРаботников.СпособОтраженияВУпрУчете <> ЗНАЧЕНИЕ(Справочник.СпособыОтраженияЗарплатыВУпрУчете.ПустаяСсылка)
	|				ТОГДА УчетЗаработкаРаботников.СпособОтраженияВУпрУчете.ОбъектСтроительства
	|			ИНАЧЕ СпособыОтраженияЗарплатыВУпрУчете.ОбъектСтроительства
	|		КОНЕЦ КАК ОбъектСтроительства,
	|		ОтражениеВУпрУчете.Результат * ЕСТЬNULL(РаспределениеЗаработкаРаботников.ДоляСпособаОтражения, 100) / 100 КАК Сумма,
	|		ЛОЖЬ КАК РаспределятьЕСН,
	|		ЛОЖЬ КАК РаспределятьНДФЛ
	|	ИЗ
	|		ВТУправленческиеНачисления КАК ОтражениеВУпрУчете
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.УчетЗаработкаРаботников КАК УчетЗаработкаРаботников
	|				ВНУТРЕННЕЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|					Начисления.ФизЛицо КАК ФизЛицо,
	|					МАКСИМУМ(УчетЗаработкаРаботников.Период) КАК Период
	|				ИЗ
	|					ВТУправленческиеНачисления КАК Начисления
	|						ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.УчетЗаработкаРаботников КАК УчетЗаработкаРаботников
	|						ПО Начисления.ФизЛицо = УчетЗаработкаРаботников.Физлицо
	|							И Начисления.ПериодДействияНачало >= УчетЗаработкаРаботников.Период
	|				
	|				СГРУППИРОВАТЬ ПО
	|					Начисления.ФизЛицо) КАК СведенияОРаботникахСрез
	|				ПО УчетЗаработкаРаботников.Физлицо = СведенияОРаботникахСрез.ФизЛицо
	|					И УчетЗаработкаРаботников.Период = СведенияОРаботникахСрез.Период
	|			ПО ОтражениеВУпрУчете.ФизЛицо = УчетЗаработкаРаботников.Физлицо
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РаспределениеЗаработкаРаботников КАК РаспределениеЗаработкаРаботников
	|			ПО ОтражениеВУпрУчете.ФизЛицо = РаспределениеЗаработкаРаботников.Физлицо
	|				И (РаспределениеЗаработкаРаботников.ПериодРегистрации = &парамПериодРегистрации)
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.СпособыОтраженияЗарплатыВУпрУчете КАК СпособыОтраженияЗарплатыВУпрУчете
	|			ПО (СпособыОтраженияЗарплатыВУпрУчете.Ссылка = ЗНАЧЕНИЕ(Справочник.СпособыОтраженияЗарплатыВУпрУчете.ОтражениеНачисленийПоУмолчанию))
	|	ГДЕ
	|		ВЫБОР
	|				КОГДА ОтражениеВУпрУчете.СпособОтраженияВУпрУчете <> ЗНАЧЕНИЕ(Справочник.СпособыОтраженияЗарплатыВУпрУчете.ПустаяСсылка)
	|					ТОГДА ОтражениеВУпрУчете.СпособОтраженияВУпрУчете
	|				КОГДА УчетЗаработкаРаботников.СпособОтраженияВУпрУчете <> ЗНАЧЕНИЕ(Справочник.СпособыОтраженияЗарплатыВУпрУчете.ПустаяСсылка)
	|					ТОГДА УчетЗаработкаРаботников.СпособОтраженияВУпрУчете
	|				ИНАЧЕ ЗНАЧЕНИЕ(Справочник.СпособыОтраженияЗарплатыВУпрУчете.ПустаяСсылка)
	|			КОНЕЦ <> ЗНАЧЕНИЕ(Справочник.СпособыОтраженияЗарплатыВУпрУчете.НеОтражатьВУпручете)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ОтражениеВРеглУчете.Подразделение,
	|		ОтражениеВРеглУчете.СтатьяЗатрат,
	|		ОтражениеВРеглУчете.НоменклатурнаяГруппа,
	|		ОтражениеВРеглУчете.ОбъектСтроительства,
	|		ОтражениеВРеглУчете.Результат / ЕСТЬNULL(КурсыВалютДляРасчетовСПерсоналом.Курс, 1) * ЕСТЬNULL(КурсыВалютДляРасчетовСПерсоналом.Кратность, 1),
	|		ВЫБОР
	|			КОГДА ОтражениеВРеглУчете.ВидРасчета.КодДоходаЕСН = ЗНАЧЕНИЕ(Справочник.ДоходыЕСН.ПустаяСсылка)
	|				ТОГДА ЛОЖЬ
	|			ИНАЧЕ ИСТИНА
	|		КОНЕЦ,
	|		ВЫБОР
	|			КОГДА ОтражениеВРеглУчете.ВидРасчета.КодДоходаНДФЛ = ЗНАЧЕНИЕ(Справочник.ДоходыНДФЛ.ПустаяСсылка)
	|				ТОГДА ЛОЖЬ
	|			ИНАЧЕ ИСТИНА
	|		КОНЕЦ
	|	ИЗ
	|		ВТ_УУДоходыПоВР КАК ОтражениеВРеглУчете
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалютДляРасчетовСПерсоналом КАК КурсыВалютДляРасчетовСПерсоналом
	|			ПО (КурсыВалютДляРасчетовСПерсоналом.Период = &парамПериодРегистрации)
	|				И (КурсыВалютДляРасчетовСПерсоналом.Валюта = &парамВалютаУправленческогоУчета)) КАК ОтражениеВУчете
	|
	|СГРУППИРОВАТЬ ПО
	|	ОтражениеВУчете.Подразделение,
	|	ОтражениеВУчете.СтатьяЗатрат,
	|	ОтражениеВУчете.НоменклатурнаяГруппа,
	|	ОтражениеВУчете.ОбъектСтроительства
	|
	|УПОРЯДОЧИТЬ ПО
	|	Подразделение,
	|	СтатьяЗатрат,
	|	НоменклатурнаяГруппа,
	|	ОбъектСтроительства
	|АВТОУПОРЯДОЧИВАНИЕ";
	
	ОтражениеДляРаспределения = Запрос.Выполнить().Выгрузить();
	
	Для Каждого СтрокаТЧ Из СуммыКРаспределению Цикл
		Если СтрокаТЧ.КатегорияЗатраты = Перечисления.КатегорииЗатраты.Зарплата Тогда
			Коэффициенты = ВыделитьКоэффициентыОтраженияИзМассиваСтрок(ОтражениеДляРаспределения,"СуммаЗарплата");
			РезультатыЗарплата = ОбщегоНазначенияЗК.РаспределитьПропорционально(СтрокаТЧ.Сумма, Коэффициенты);
			
		ИначеЕсли СтрокаТЧ.КатегорияЗатраты = Перечисления.КатегорииЗатраты.ЕСН Тогда
			Коэффициенты = ВыделитьКоэффициентыОтраженияИзМассиваСтрок(ОтражениеДляРаспределения,"СуммаЕСН");
			РезультатыЕСН = ОбщегоНазначенияЗК.РаспределитьПропорционально(СтрокаТЧ.Сумма, Коэффициенты);
			
		ИначеЕсли СтрокаТЧ.КатегорияЗатраты = Перечисления.КатегорииЗатраты.НДФЛ Тогда
			Коэффициенты = ВыделитьКоэффициентыОтраженияИзМассиваСтрок(ОтражениеДляРаспределения,"СуммаНДФЛ");
			РезультатыНДФЛ = ОбщегоНазначенияЗК.РаспределитьПропорционально(СтрокаТЧ.Сумма, Коэффициенты);
			
		КонецЕсли;
	КонецЦикла;
	
	ВыделятьНалоги = мМетодПланированияУчетаЗатрат = Перечисления.МетодыПланированияУчетаЗатратНаПерсонал.КассовыйМетодСВыделениемНалогов;
	
	Индекс = 0;
	Для Каждого СтрокаОтражения Из ОтражениеДляРаспределения Цикл
		Если РезультатыЗарплата <> Неопределено Тогда
			СтрокаТЧ = ОтражениеВУчете.Добавить();
			ЗаполнитьРеквизитыСтроки(СтрокаТЧ, СтрокаОтражения);
			
			СтрокаТЧ.Сумма = РезультатыЗарплата[Индекс];
		КонецЕсли;
		
		Если РезультатыЕСН <> Неопределено Тогда
			СтрокаТЧ = ОтражениеВУчете.Добавить();
			ЗаполнитьРеквизитыСтроки(СтрокаТЧ, СтрокаОтражения, ВыделятьНалоги, Перечисления.КатегорииЗатраты.ЕСН);
			
			СтрокаТЧ.Сумма = РезультатыЕСН[Индекс];
		КонецЕсли;
		
		Если РезультатыНДФЛ <> Неопределено Тогда
			СтрокаТЧ = ОтражениеВУчете.Добавить();
			ЗаполнитьРеквизитыСтроки(СтрокаТЧ, СтрокаОтражения, ВыделятьНалоги, Перечисления.КатегорииЗатраты.НДФЛ);
			
			СтрокаТЧ.Сумма = РезультатыНДФЛ[Индекс];
		КонецЕсли;
		
		Индекс = Индекс + 1;
	КонецЦикла;
	
	ОтражениеВУчете.Свернуть("КатегорияЗатраты, Подразделение, СтатьяЗатрат, НоменклатурнаяГруппа, ОбъектСтроительства", "Сумма");
	
КонецПроцедуры // ЗаполнитьКассовымМетодом


// Выделяет коэффициенты отражения из массива переданных строк отражения
//
// Параметры
//  МассивСтрокОтражения - массив строк таблицы значений
//  ИмяКолонки - строка - имя колонки таблицы значений, содержащей 
//                 коэффициенты отражения
//
// Возвращаемое значение:
//   массив чисел - коэффициентов отражения
//
Функция ВыделитьКоэффициентыОтраженияИзМассиваСтрок(МассивСтрокОтражения,ИмяКолонки)
	
	Коэффициенты = Новый Массив;
	
	Для каждого СтрокаОтражения Из МассивСтрокОтражения Цикл
		Коэффициенты.Добавить(СтрокаОтражения[ИмяКолонки]);
	КонецЦикла; 
	
	Возврат Коэффициенты
КонецФункции // ВыделитьКоэффициентыОтраженияИзМассиваСтрок()

Процедура ЗаполнитьРеквизитыСтроки(СтрокаПриемник, СтрокаИсточник, ВыделятьНалоги = Ложь, КатегорияЗатраты = Неопределено)
	
	ЗаполнитьЗначенияСвойств(СтрокаПриемник, СтрокаИсточник, "КатегорияЗатраты, Подразделение, СтатьяЗатрат, НоменклатурнаяГруппа, ОбъектСтроительства");
	
	Если ВыделятьНалоги Тогда
		СтрокаПриемник.КатегорияЗатраты = КатегорияЗатраты;
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ДЛЯ ОБЕСПЕЧЕНИЯ ПРОВЕДЕНИЯ ДОКУМЕНТА


// Формирует запрос по табличной части ОтражениеВУчете
//
//
// Возвращаемое значение:
//  Результат запроса
//
Функция СформироватьЗапросПоДоходам()
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	Отражение.НомерСтроки,
	|	Отражение.Подразделение,
	|	Отражение.СтатьяЗатрат,
	|	Отражение.СтатьяЗатрат.ХарактерЗатрат КАК ХарактерЗатрат,
	|	Отражение.Проект,
	|	Отражение.НоменклатурнаяГруппа,
	|	Отражение.СпособРаспределенияЗатрат КАК СпособРаспределенияЗатратНаВыпуск,
	|	Отражение.ОбъектСтроительства,
	|	Отражение.Сумма КАК Сумма
	|ИЗ
	|	Документ.ОтражениеЗарплатыВУпрУчете.ОтражениеВУчете КАК Отражение
	|ГДЕ
	|	Отражение.Ссылка = &парамРегистратор");
	
	Запрос.УстановитьПараметр("парамРегистратор", Ссылка);
	Возврат Запрос.Выполнить();
КонецФункции

// Проверяет правильность заполнения шапки документа.
// Если какой-то из реквизитов шапки, влияющий на проведение, не заполнен или
// заполнен не корректно, то выставляется флаг отказа в проведении.
// Проверка выполняется по выборке из результата запроса по шапке,
// все проверяемые реквизиты должны быть включены в выборку по шапке.
//
// Параметры: 
//  ВыборкаПоШапкеДокумента	- выборка из результата запроса по шапке документа,
//  Отказ 					- флаг отказа в проведении.
//
Процедура ПроверитьЗаполнениеШапки(Отказ)

	Если НЕ ЗначениеЗаполнено(ПериодРегистрации) Тогда
		ОбщегоНазначения.СообщитьОбОшибке("Не задан период за который выполняется отражение начислений учете!", Отказ);
	КонецЕсли;

КонецПроцедуры // ПроверитьЗаполнениеШапки()

// Проверяет правильность заполнения реквизитов в строке ТЧ "ОтражениеВУчете" документа.
// Если какой-то из реквизитов, влияющий на проведение, не заполнен или
// заполнен некорректно, то выставляется флаг отказа в проведении.
// Проверка выполняется по выборке из результата запроса к ТЧ документа,
// все проверяемые реквизиты должны быть включены в выборку.
//
// Параметры: 
//  ВыборкаПоШапкеДокумента		- выборка из результата запроса по шапке документа,
//  ВыборкаПоСтрокамДокумента	- спозиционированная на определеной строке выборка 
//  							  из результата запроса к ТЧ документа, 
//  Отказ 						- флаг отказа в проведении.
//
Процедура ПроверитьЗаполнениеСтрокиДохода(ВыборкаПоДоходам, Отказ)
	СтрокаНачалаСообщенияОбОшибке = "В строке номер """+ СокрЛП(ВыборкаПоДоходам.НомерСтроки)+": ";

	Если ВыборкаПоДоходам.Подразделение.Пустая() Тогда
		ОбщегоНазначения.СообщитьОбОшибке(СтрокаНачалаСообщенияОбОшибке + "Не указано подразделение!", Отказ);
	КонецЕсли;
	
	Если ВыборкаПоДоходам.СтатьяЗатрат = Справочники.СтатьиЗатрат.ПустаяСсылка() Тогда
		ОбщегоНазначения.СообщитьОбОшибке(СтрокаНачалаСообщенияОбОшибке + "Не указана статья затрат!", Отказ);
	КонецЕсли;
	
	Если ВыборкаПоДоходам.ХарактерЗатрат = Перечисления.ХарактерЗатрат.ВложенияВоВнеоборотныеАктивы и 
		НЕ ЗначениеЗаполнено(ВыборкаПоДоходам.ОбъектСтроительства) Тогда
		ОбщегоНазначения.СообщитьОбОшибке(СтрокаНачалаСообщенияОбОшибке + "Не задан объект строительства!", Отказ);
	КонецЕсли;
		
Конецпроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

Процедура ОбработкаПроведения(Отказ, Режим)
	
	Перем КурсВалютыУпрУчета;
	
	Если мУдалятьДвижения Тогда
		ОбщегоНазначения.УдалитьДвиженияРегистратора(ЭтотОбъект, Отказ);
	КонецЕсли;
	
	ПроверитьЗаполнениеШапки(Отказ);
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	ТаблицаПоДоходам = СформироватьЗапросПоДоходам().Выгрузить();
	
	Для каждого СтрокаТаблицы из ТаблицаПоДоходам Цикл
		ПроверитьЗаполнениеСтрокиДохода(СтрокаТаблицы, Отказ);
	КонецЦикла;
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	ТаблицаПоДоходам.Свернуть("Подразделение,СтатьяЗатрат,Проект,НоменклатурнаяГруппа,СпособРаспределенияЗатратНаВыпуск,ОбъектСтроительства","Сумма");
	
	ВалютаРегламентированногоУчета = Константы.ВалютаРегламентированногоУчета.Получить();
	ПроведениеРасчетовДополнительный.ПолучитьКурсВалютыУпрУчета(ПериодРегистрации, ПериодРегистрации, мВалютаУправленческогоУчета, КурсВалютыУпрУчета, ВалютаРегламентированногоУчета);

	Запрос = Новый Запрос;
	
	ТаблицаЗатрат = УправлениеЗатратами.СформироватьТаблицуЗатрат();
	
	ОбщегоНазначения.ЗагрузитьВТаблицуЗначений(ТаблицаПоДоходам,ТаблицаЗатрат);
	
	Если ТаблицаЗатрат.Количество() > 0 Тогда
		
		ДопПараметры = Новый Структура;
		ДопПараметры.Вставить("КодОперацииНЗП",  Перечисления.КодыОперацийНезавершенноеПроизводство.СписаниеПрочихЗатратВПроизводство);
		ДопПараметры.Вставить("КодОперацииЗатр", Перечисления.КодыОперацийЗатраты.ПрочиеПостоянныеЗатраты);
		
		ВремСтруктураШапкиДокумента = Новый Структура;
		ВремСтруктураШапкиДокумента.Вставить("ОтражатьВУправленческомУчете", Истина);
		ВремСтруктураШапкиДокумента.Вставить("Ссылка", Ссылка);
		ВремСтруктураШапкиДокумента.Вставить("Дата", КонецМесяца(ПериодРегистрации));
		
		УправлениеЗатратами.ДвиженияПоПрочимЗатратам(
			ВремСтруктураШапкиДокумента,
			ТаблицаЗатрат,
			Перечисления.ВидыОтраженияВУчете.ОтражатьВУправленческомУчете
		);
	
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаУдаленияПроведения(Отказ)
	//ОбщегоНазначения.СброситьФактВыполненияОперации(Ссылка);
	
	ОбщегоНазначения.УдалитьДвиженияРегистратора(ЭтотОбъект, Отказ);

КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	Если ОбменДанными.Загрузка  Тогда
		Возврат;
	КонецЕсли;


	 
	мУдалятьДвижения = НЕ ЭтоНовый();
	
КонецПроцедуры // ПередЗаписью

////////////////////////////////////////////////////////////////////////////////
// ОПЕРАТОРЫ ОСНОВНОЙ ПРОГРАММЫ

мВалютаУправленческогоУчета		= Константы.ВалютаУправленческогоУчета.Получить();
мВалютаРегламентированногоУчета	= Константы.ВалютаРегламентированногоУчета.Получить();

мМетодПланированияУчетаЗатрат	= Константы.МетодПланированияУчетаЗатратНаПерсонал.Получить();