Перем КопияДерева;
Перем ДокОбъект;

// Процедура пересчитать значение пометок (установить третье состояние
// при изменении количества показателей)
//
Процедура ПересчетПометок(Дерево)
	
	Для Каждого Узел Из Дерево.Строки Цикл
		
		Если Узел.Строки.Количество() = 0 Тогда
			Продолжить; // В оконечных элементах ничего не изменяем
		Иначе
			ПересчетПометок(Узел);
			Узел.Видимость = ОпределитьЗначениеФлага(Узел);
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры // ПересчетПометок()

// Определяет значение флага по составу пометок коллекции строк.
//
// Параметры:
//  Строки         - коллекция строк дерева значений
//
// Возвращаемое значение:
//  Число, значение флага
// 
Функция ОпределитьЗначениеФлага(Строки)

	НайденыИстина = Ложь;
	НайденыЛожь   = Ложь;

	Для каждого Строка из Строки.Строки Цикл
		Если Строка.Видимость = 2 Тогда
			Возврат 2;
		КонецЕсли;
		
		Если (НЕ НайденыИстина) И (Строка.Видимость) Тогда
			НайденыИстина = Истина;
		КонецЕсли;
		Если (НЕ НайденыЛожь) И (НЕ Строка.Видимость) Тогда
			НайденыЛожь = Истина;
		КонецЕсли;
	КонецЦикла;

	Если      (НайденыИстина) И (НайденыЛожь) Тогда
		Возврат 2;
	ИначеЕсли (НайденыИстина) И (НЕ НайденыЛожь) Тогда
		Возврат 1;
	ИначеЕсли (НЕ НайденыИстина) И (НайденыЛожь) Тогда
		Возврат 0;
	КонецЕсли;

КонецФункции // ОпределитьЗначениеФлага()

// Устанавливает пометку в вышестоящих элементах дерева
//
Процедура ОбходВерхнихУровней(ТекСтрока)

	ТекСтрока.Видимость = ОпределитьЗначениеФлага(ТекСтрока);
	
	Если Не ТекСтрока.Родитель = Неопределено Тогда
		ОбходВерхнихУровней(ТекСтрока.Родитель);
	КонецЕсли;
	
КонецПроцедуры // ОбходВерхнихУровней()

// Устанавливает пометку в подчиненных элементах дерева
//
Процедура ОбходНижнихУровней(ТекСтрока)
	
	Для Каждого Строка Из ТекСтрока.Строки Цикл
		Строка.Видимость = ТекСтрока.Видимость;
		ОбходНижнихУровней(Строка);
	КонецЦикла;
	
КонецПроцедуры // ОбходНижнихУровней()

// Устанавливает пометки в подчиненных строках и устанавливает пометку
// в текущей строке в зависимости от состава пометок в подчиненных строках.
// При значении параметра ИнтерактивнаяУстановкаПометок равным Истина
// возможна установка флага для строки с незаполненным источником.
//
// Параметры:
//  ТекСтрока                     - строка дерева значений
//  ИнтерактивнаяУстановкаПометок - флаг интерактивной установки пометки
// 
Процедура УстановитьПометкиВДереве(ТекСтрока)

	ОбходНижнихУровней (ТекСтрока);
	Если Не ТекСтрока.Родитель = Неопределено Тогда
		ОбходВерхнихУровней(ТекСтрока.Родитель);
	КонецЕсли;
	
КонецПроцедуры // УстановитьПометкиВДереве()

// Процедура установки пометок в дереве
//
Процедура УстановитьПометки(Дерево, Видимость)
	Для Каждого Строка Из Дерево.Строки Цикл
		Строка.Видимость = Видимость;
		УстановитьПометки(Строка, Видимость);
	КонецЦикла;
КонецПроцедуры // УстановитьПометки()

// Процедура снятия пометки со всех элементов
//
Процедура СнятьПометкиНажатие(Элемент)
	
	УстановитьПометки(ДеревоГрупп, 0);
	
КонецПроцедуры // СнятьПометкиНажатие()

// Процедура установки пометки со всех элементов
//
Процедура УстановитьПометкиНажатие(Элемент)
	
	УстановитьПометки(ДеревоГрупп, 1);
	
КонецПроцедуры // УстановитьПометкиНажатие()

// Процедура - обработчик события ПриОткрытии формы
// 
Процедура ПриОткрытии()
	
	Если НачальноеЗначениеВыбора = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ДеревоГрупп        = НачальноеЗначениеВыбора["ДеревоГрупп"].Скопировать();
	ФиксироватьКолонки = НачальноеЗначениеВыбора["ФиксироватьКолонки"];
	
	ПересчетПометок(ДеревоГрупп);
		
КонецПроцедуры // ПриОткрытии()

// Процедура
//
Процедура ОбходДерева(Приемник, Источник)
	
	Для Каждого ТекСтрока Из Источник.Строки Цикл
		СтрокаДерева = Приемник.Строки.Найти( ТекСтрока.Имя, "Имя", Истина);
		Если Не СтрокаДерева = Неопределено Тогда
			СтрокаДерева.Видимость = ТекСтрока.Видимость;
		КонецЕсли;
		ОбходДерева(Приемник, ТекСтрока);
	КонецЦикла;
	
КонецПроцедуры // ОбходДерева()

Процедура ОсновныеДействияФормыОК(Кнопка)
	
	ОбходДерева( НачальноеЗначениеВыбора["ДеревоГрупп"], ДеревоГрупп);
	НачальноеЗначениеВыбора["ФиксироватьКолонки"] = ФиксироватьКолонки;
	
	Закрыть();
	
КонецПроцедуры // ОсновныеДействияФормыОК()

// Процедура - обработчик события ПриИзмененииФлажка формы
// 
Процедура ДеревоПриИзмененииФлажка(Элемент, Колонка)
	
	УстановитьПометкиВДереве(Элемент.ТекущаяСтрока);
	
КонецПроцедуры