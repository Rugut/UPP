Перем мЗаконодательство2006 Экспорт;

Перем мУдалятьДвижения;

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОКУМЕНТА

#Если Клиент Тогда
// Процедура осуществляет печать документа. Можно направить печать на 
// экран или принтер, а также распечатать необходимое количество копий.
//
//  Название макета печати передается в качестве параметра,
// по переданному названию находим имя макета в соответствии.
//
// Параметры:
//  НазваниеМакета - строка, название макета.
//
Процедура Печать(ИмяМакета, КоличествоЭкземпляров = 1, НаПринтер = Ложь) Экспорт

	Если ЭтоНовый() Тогда
		Предупреждение("Документ можно распечатать только после его записи");
		Возврат;
	ИначеЕсли Не УправлениеДопПравамиПользователей.РазрешитьПечатьНепроведенныхДокументов(Проведен) Тогда
		Предупреждение("Недостаточно полномочий для печати непроведенного документа!");
		Возврат;
	КонецЕсли;

	Если Не РаботаСДиалогами.ПроверитьМодифицированность(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;

	Если ТипЗнч(ИмяМакета) = Тип("ДвоичныеДанные") Тогда

		ТабДокумент = УниверсальныеМеханизмы.НапечататьВнешнююФорму(Ссылка, ИмяМакета);
		
		Если ТабДокумент = Неопределено Тогда
			Возврат;
		КонецЕсли; 
		
	КонецЕсли;

	УниверсальныеМеханизмы.НапечататьДокумент(ТабДокумент, КоличествоЭкземпляров, НаПринтер, ОбщегоНазначения.СформироватьЗаголовокДокумента(ЭтотОбъект), Ссылка);

КонецПроцедуры // Печать()

// Возвращает доступные варианты печати документа
//
// Возвращаемое значение:
//  Структура, каждая строка которой соответствует одному из вариантов печати
//  
Функция ПолучитьСтруктуруПечатныхФорм() Экспорт
	
	Возврат Новый Структура;

КонецФункции // ПолучитьСтруктуруПечатныхФорм()

#КонецЕсли

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ДЛЯ АВТОЗАПОЛНЕНИЯ ТАБЛИЧНОЙ ЧАСТИ ДОКУМЕНТА

//Процедура заполнения табличной части по данным подсистемы НДС
Процедура ЗаполнитьДокумент(ОшибкаЗаполнения = Ложь, Сообщать = Истина, СтрокаСообщения = "", ОтменитьПроведение = Ложь) Экспорт
	
	Если Проведен Тогда
		Если ОтменитьПроведение Тогда
			Записать(РежимЗаписиДокумента.ОтменаПроведения);
		Иначе
			ОшибкаЗаполнения = Истина;
			СтрокаСообщения = " перед заполнением требуется отменить проведение документа";
			Если Сообщать Тогда
				ОбщегоНазначения.СообщитьОбОшибке("Документ не заполнен:" + СтрокаСообщения, , Строка(Ссылка));
			КонецЕсли; 
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	ЗаполнитьСтрокиДокумента();

	Если Не (Состав.Количество() > 0 
        ) Тогда
		ОшибкаЗаполнения = Истина;
		СтрокаСообщения = СтрокаСообщения+Символы.ПС+" - не обнаружена неподтвержденаая реализация по ставке 0"
	КонецЕсли;	

   Если ОшибкаЗаполнения Тогда
		Если Сообщать Тогда
			ОбщегоНазначения.СообщитьОбОшибке("Документ не заполнен:" + СтрокаСообщения, , Строка(Ссылка));
		КонецЕсли; 
		Возврат;
	КонецЕсли; 
	
КонецПроцедуры

Процедура ЗаполнитьСтрокиДокумента()

	ТаблицаРезультата = Состав.ВыгрузитьКолонки();

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("КонецПериода",  Новый Граница(КонецДня(Дата), ВидГраницы.Включая));
	Запрос.УстановитьПараметр("Организация",   Организация);
	Запрос.УстановитьПараметр("СостояниеПредположения0", Перечисления.НДССостоянияРеализация0.ОжидаетсяПодтверждение);
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	НДСРеализация0Остатки.СчетФактура,
	|	НДСРеализация0Остатки.ВидЦенности,
	|	НДСРеализация0Остатки.СтавкаНДС,
	|	НДСРеализация0Остатки.СуммаБезНДСОстаток + НДСРеализация0Остатки.НДСОстаток КАК ПродажиСНДС0,
	|	НДСРеализация0Остатки.КурсоваяРазницаОстаток КАК КурсоваяРазница,
	|	НДСРеализация0Остатки.Покупатель
	|ИЗ
	|	РегистрНакопления.НДСРеализация0.Остатки(
	|		&КонецПериода,
	|		Организация = &Организация
	|		    И Состояние = &СостояниеПредположения0) КАК НДСРеализация0Остатки";
	
	РезультатЗапроса = Запрос.Выполнить().Выгрузить();
	
	ОбщегоНазначения.ЗагрузитьВТаблицуЗначений(РезультатЗапроса,ТаблицаРезультата);
	ТаблицаРезультата.ЗаполнитьЗначения(Перечисления.СобытияПоНДСПродажи.ПодтвержденаСтавка0,"Событие");
	ТаблицаРезультата.ЗаполнитьЗначения(Перечисления.СтавкиНДС.НДС18,"СтавкаНДС");

	Состав.Загрузить(ТаблицаРезультата);
	
КонецПроцедуры // ЗаполнитьСтрокиДокумента()

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ДЛЯ ОБЕСПЕЧЕНИЯ ПРОВЕДЕНИЯ ДОКУМЕНТА

// Проверяет правильность заполнения шапки документа.
// Если какой-то из реквизитов шапки, влияющий на проведение, не заполнен или
// заполнен не корректно, то выставляется флаг отказа в проведении.
// Проверяется также правильность заполнения реквизитов ссылочных полей документа.
// Проверка выполняется по объекту и по выборке из результата запроса по шапке.
//
// Параметры: 
//  СтруктураШапкиДокумента - структура, содержащая рексвизиты шапки документа и результаты запроса по шапке,
//  Отказ                   - флаг отказа в проведении,
//  Заголовок               - строка, заголовок сообщения об ошибке проведения.
//
Процедура ПроверитьЗаполнениеШапки(Отказ, Заголовок)
	
	// Укажем, что надо проверить:
	СтруктураОбязательныхПолей = Новый Структура("Организация");
	
	// Теперь вызовем общую процедуру проверки.
	ЗаполнениеДокументов.ПроверитьЗаполнениеШапкиДокумента(ЭтотОбъект, СтруктураОбязательныхПолей, Отказ, Заголовок);
	
	ТребуетсяСтатьяПрочихРасходов = Ложь;
	
	Для Каждого СтрокаТаблицы Из Состав Цикл
		Если СтрокаТаблицы.Событие = Перечисления.СобытияПоНДСПродажи.НеПодтвержденаСтавка0 Тогда
			ТребуетсяСтатьяПрочихРасходов = Истина;
			Продолжить;
		КонецЕсли;
	КонецЦикла;
	Если ТребуетсяСтатьяПрочихРасходов Тогда
		СтруктураОбязательныхПолей.Очистить();
		СтруктураОбязательныхПолей.Вставить("СтатьяПрочихРасходов");
		// Теперь вызовем общую процедуру проверки.
		ЗаполнениеДокументов.ПроверитьЗаполнениеШапкиДокумента(ЭтотОбъект, СтруктураОбязательныхПолей, Неопределено, Заголовок);
	КонецЕсли;
	
КонецПроцедуры // ПроверитьЗаполнениеШапки()

// Выгружает результат запроса в табличную часть, добавляет ей необходимые колонки для проведения.
//
//
Функция ПодготовитьТаблицуПоДокументам(РезультатЗапроса, СтруктураШапкиДокумента)
	
	ТаблицаРезультата = РезультатЗапроса.Выгрузить();
	
	//Принудительная установка ставки НДС 0% по события "Подтверждена ставка 0%".
	СобытиеПодтвержденаСтавка0 = Перечисления.СобытияПоНДСПродажи.ПодтвержденаСтавка0;
	Ставка0 = Перечисления.СтавкиНДС.НДС0;
	Для каждого СтрокаРезультата Из ТаблицаРезультата Цикл
		Если СтрокаРезультата.Событие = СобытиеПодтвержденаСтавка0 Тогда
			СтрокаРезультата.СтавкаНДС = Ставка0;
			СтрокаРезультата.НДС		= 0;
		КонецЕсли;
	КонецЦикла;
	
	//Заполнение организации для записи в реигистры
	ТаблицаРезультата.Колонки.Добавить("Организация");
	ТаблицаРезультата.ЗаполнитьЗначения(СтруктураШапкиДокумента.Организация,"Организация");
	
	ТаблицаРезультата.Колонки.Добавить("ДатаРеализации", Новый ОписаниеТипов("Дата"));
	УчетНДС.ЗаполнитьДатуДокументовВТаблице(ТаблицаРезультата, "ДокРеализации", "ДатаРеализации");
	Возврат ТаблицаРезультата;
	
КонецФункции // ПодготовитьТаблицуПоДокументам()

// Проверяет правильность заполнения строк табличной части.
//
//
Процедура ПроверитьЗаполнениеТабличнойЧастиПоДокументам(ТаблицаПоДокументам, Отказ, Заголовок)
	
	// Укажем, что надо проверить:
	СтруктураОбязательныхПолей = Новый Структура("ВидЦенности, Покупатель, СчетФактура, Событие"); 
	
	// Теперь вызовем общую процедуру проверки.
	ЗаполнениеДокументов.ПроверитьЗаполнениеТабличнойЧасти(ЭтотОбъект, "Состав", СтруктураОбязательныхПолей, Отказ, Заголовок);
	
	Для Каждого СтрокаТЧ из Состав Цикл
		Если СтрокаТЧ.Событие = Перечисления.СобытияПоНДСПродажи.НеПодтвержденаСтавка0 Тогда
			Если НЕ ЗначениеЗаполнено(СтрокаТЧ.СуммаНДС) Тогда
				ОбщегоНазначения.СообщитьОбОшибке("В строке номер """+ СокрЛП(СтрокаТЧ.НомерСтроки) +
			                         """ табличной части: " + "Не заполнен реквизит ""НДС""!", Отказ, Заголовок);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры // ПроверитьЗаполнениеТабличнойЧастиТовары()

// По результату запроса по шапке документа формируем движения по регистрам.
//
//
Процедура ДвиженияПоРегистрам(СтруктураШапкиДокумента, ТаблицаПоДокументам, Отказ, Заголовок);
	
	////////////////////////////////////////////////////////
	//Движения по регистру НДСРеализация0
	
	// Подготовка таблицы движений по регистру НДСРеализация0
	ТаблицаДвижений_НДСРеализация0 = Движения.НДСРеализация0.ВыгрузитьКолонки();
	
	// Списываем остатки по предположению
	ОбщегоНазначения.ЗагрузитьВТаблицуЗначений(ТаблицаПоДокументам,ТаблицаДвижений_НДСРеализация0);
	ТаблицаДвижений_НДСРеализация0.ЗаполнитьЗначения(Перечисления.СтавкиНДС.НДС0,"СтавкаНДС");
	ТаблицаДвижений_НДСРеализация0.ЗаполнитьЗначения(Перечисления.НДССостоянияРеализация0.ОжидаетсяПодтверждение,"Состояние");
	ТаблицаДвижений_НДСРеализация0.ЗаполнитьЗначения(0, "НДС");
	ТаблицаДвижений_НДСРеализация0.ЗаполнитьЗначения(ВидДвиженияНакопления.Расход,"ВидДвижения");
	
	ТаблицаПоДокументам.Колонки.Добавить("Состояние",Новый ОписаниеТипов("ПеречислениеСсылка.НДССостоянияРеализация0"));
	ТаблицаПоДокументам.Колонки.Добавить("ВидДвижения");
	ТаблицаПоДокументам.ЗаполнитьЗначения(ВидДвиженияНакопления.Приход,"ВидДвижения");
	
	// Отражаем подтверждение/неподтверждение 
	Для каждого ТекСтрокаСостав Из ТаблицаПоДокументам Цикл
		Если ТекСтрокаСостав.Событие = Перечисления.СобытияПоНДСПродажи.НеПодтвержденаСтавка0 Тогда
			ТекСтрокаСостав.СуммаБезНДС = ?(СтруктураШапкиДокумента.НДСПриНеподтвержденииСверху, 
											ТекСтрокаСостав.СуммаБезНДС,
											ТекСтрокаСостав.СуммаБезНДС - ТекСтрокаСостав.НДС);
			ТекСтрокаСостав.Состояние   = Перечисления.НДССостоянияРеализация0.НеПодтвержденаРеализация0;
		Иначе
			ТекСтрокаСостав.СтавкаНДС = Перечисления.СтавкиНДС.НДС0;
			ТекСтрокаСостав.Состояние = Перечисления.НДССостоянияРеализация0.ПодтвержденаРеализация0;
		КонецЕсли; 
		НовоеДвижение = ТаблицаДвижений_НДСРеализация0.Добавить();
		ЗаполнитьЗначенияСвойств(НовоеДвижение, ТекСтрокаСостав, , "КурсоваяРазница");
		Если мЗаконодательство2006
			И ТекСтрокаСостав.Событие = Перечисления.СобытияПоНДСПродажи.ПодтвержденаСтавка0 
			И ТекСтрокаСостав.ДатаРеализации < '20111001' Тогда
			// По документам отгрузки с датой до 1 октября 2011 налоговая база по НДС 
			// при подтверждении реализации со ставкой 0% корректируется с учетом курса на дату оплаты
			НовоеДвижение.СуммаБезНДС = НовоеДвижение.СуммаБезНДС + ТекСтрокаСостав.КурсоваяРазница;
			
		КонецЕсли;

	КонецЦикла; 
	
	//Запись движений
	Движения.НДСРеализация0.мПериод          = СтруктураШапкиДокумента.Дата;
	Движения.НДСРеализация0.мТаблицаДвижений = ТаблицаДвижений_НДСРеализация0;
	Движения.НДСРеализация0.ДобавитьДвижение();
	
	//Движения по регистру НДСРеализация0
	////////////////////////////////////////////////////////
	
	СписокСчетовФактур = ОбщегоНазначения.УдалитьПовторяющиесяЭлементыМассива(ТаблицаДвижений_НДСРеализация0.ВыгрузитьКолонку("СчетФактура"),Истина);
	
	ДанныеПоРеализации0 = ПолучитьДанныеПоРеализации0(СтруктураШапкиДокумента,СписокСчетовФактур,Отказ);
	
	СформироватьДвиженияПоНеподтверждениюСтавки0_Реализация(СтруктураШапкиДокумента, ТаблицаПоДокументам,ДанныеПоРеализации0, Отказ, Заголовок);
	
	////////////////////////////////////////////////////////
	// Движения по регистрам приобретения, отражающие подтверждение или неподтверждение ставки 0
	// для всех поступлений МПЗ, связанных с данной реализацией
	СформироватьДвижениеНДСПредъявленныйРеализация0_ОтражениеПодтверждения0(СтруктураШапкиДокумента, ТаблицаПоДокументам, ДанныеПоРеализации0, Отказ, Заголовок);
	//Движения по регистру НДС покупки (и проводки)
	////////////////////////////////////////////////////////
	
	СформироватьКорректирующиеДвиженияРеализация0_Подтверждение0(СтруктураШапкиДокумента, ТаблицаПоДокументам);
	
КонецПроцедуры // ДвиженияПоРегистрам()

Функция ПолучитьДанныеПоРеализации0(СтруктураШапкиДокумента,СписокСчетовФактур,Отказ)

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	НДСРеализация0Обороты.СчетФактура,
		|	ЕСТЬNULL(НДСРеализация0Обороты.СуммаБезНДСПриход, 0) + ЕСТЬNULL(НДСРеализация0Обороты.НДСПриход, 0) КАК СуммаРеализации,
		|	ВЫБОР
		|		КОГДА НДСРеализация0Обороты.СчетФактура.Дата < &Начало2006Года
		|			ТОГДА УчетнаяПолитикаНалоговыйУчет.МоментОпределенияНалоговойБазыНДС
		|		ИНАЧЕ &МоментОпределения_ПоОтгрузке
		|	КОНЕЦ КАК МоментОпределенияНалоговойБазыНДС
		|ИЗ
		|	РегистрНакопления.НДСРеализация0.Обороты(
		|		,
		|		&КонецПериода,
		|		Период,
		|		Организация = &Организация
		|		    И СчетФактура В (&СписокСчетовФактур)
		|		    И Состояние = &СостояниеОжидания) КАК НДСРеализация0Обороты
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.УчетнаяПолитикаОрганизаций КАК УчетнаяПолитикаНалоговыйУчет
		|		ПО НДСРеализация0Обороты.Организация = УчетнаяПолитикаНалоговыйУчет.Организация
		|			И (УчетнаяПолитикаНалоговыйУчет.Период В
		|				(ВЫБРАТЬ
		|					МАКСИМУМ(УчетнаяПолитикаНалоговыйУчет.Период)
		|				ИЗ
		|					РегистрСведений.УчетнаяПолитикаОрганизаций КАК УчетнаяПолитикаНалоговыйУчет
		|				ГДЕ
		|					НДСРеализация0Обороты.СчетФактура.Дата >= УчетнаяПолитикаНалоговыйУчет.Период
		|					И
		|					НДСРеализация0Обороты.Организация = УчетнаяПолитикаНалоговыйУчет.Организация))";
	
	Запрос.УстановитьПараметр("КонецПериода", Новый Граница(КонецДня(СтруктураШапкиДокумента.Дата), ВидГраницы.Включая));
	Запрос.УстановитьПараметр("Организация", СтруктураШапкиДокумента.Организация);
	Запрос.УстановитьПараметр("СписокСчетовФактур", СписокСчетовФактур);
	Запрос.УстановитьПараметр("СостояниеОжидания", Перечисления.НДССостоянияРеализация0.ОжидаетсяПодтверждение);
	Запрос.УстановитьПараметр("Начало2006Года", '20060101');
	Запрос.УстановитьПараметр("МоментОпределения_ПоОтгрузке", Перечисления.МоментыОпределенияНалоговойБазыНДС.ПоОтгрузке);
	
	Возврат Запрос.Выполнить().Выгрузить();

КонецФункции

Процедура СформироватьКорректирующиеДвиженияРеализация0_Подтверждение0(СтруктураШапкиДокумента, ТаблицаПоДокументам);
	
	ТаблицаДвижений_НДСНачисленный = Движения.НДСНачисленный.ВыгрузитьКолонки();
	
	Для Каждого СтрокаТаблицы Из ТаблицаПоДокументам Цикл
		
		Если СтрокаТаблицы.ДатаРеализации < '20111001' 
			И СтрокаТаблицы.КурсоваяРазница <> 0 Тогда
			// По документам отгрузки с датой, начиная с октября 2011, налоговая база по НДС 
			// при подтверждении реализации со ставкой 0% определяется по курсу на дату реализации,
			// корректировки налоговой базы при последующей оплате не производятся
			
			Если СтрокаТаблицы.Состояние = Перечисления.НДССостоянияРеализация0.ПодтвержденаРеализация0 Тогда
				ДвижениеНДСНачисленный = ТаблицаДвижений_НДСНачисленный.Добавить();
				ЗаполнитьЗначенияСвойств(ДвижениеНДСНачисленный, СтрокаТаблицы);
				
				ДвижениеНДСНачисленный.СуммаБезНДС = СтрокаТаблицы.КурсоваяРазница;
				ДвижениеНДСНачисленный.НДС = 0;
				ДвижениеНДСНачисленный.ВидНачисления = Перечисления.НДСВидНачисления.Реализация0;
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Если ТаблицаДвижений_НДСНачисленный.Количество() > 0  Тогда
		Движения.НДСНачисленный.мПериод	= СтруктураШапкиДокумента.Дата;
		Движения.НДСНачисленный.мТаблицаДвижений = ТаблицаДвижений_НДСНачисленный;
		Движения.НДСНачисленный.ВыполнитьПриход(Ложь);
	КонецЕсли;
			
КонецПроцедуры

// Процедура вызывается из процедуры ДвиженияПоРегистрам
// Основная задача - на основании данных документа сформировать движения по неподтверждению ставки 0% 
// в регистре "НДС начисиленный"
// в бухгалтерском учете (проводки)
Процедура СформироватьДвиженияПоНеподтверждениюСтавки0_Реализация(СтруктураШапкиДокумента, ТаблицаПоДокументам,ДанныеПоРеализации0, Отказ, Заголовок)

	// Подготовка таблицы движений по регистру НДСРеализация0
	ТаблицаДвижений_НДСНачисленный = Неопределено;
	СостояниеНеПодтвержденаСтавка0 = Перечисления.НДССостоянияРеализация0.НеПодтвержденаРеализация0;
	Ставка0 = Перечисления.СтавкиНДС.НДС0;
	
	Для каждого СтрокаТаблицы Из ТаблицаПоДокументам Цикл
		Если СтрокаТаблицы.Состояние = СостояниеНеПодтвержденаСтавка0 Тогда
			Если ТаблицаДвижений_НДСНачисленный = Неопределено тогда
				ТаблицаДвижений_НДСНачисленный = Движения.НДСНачисленный.ВыгрузитьКолонки();
			КонецЕсли;
			СтрокаДвиженияСторно0 = ТаблицаДвижений_НДСНачисленный.Добавить();
			СтрокаДвиженияСторно0.Организация = СтруктураШапкиДокумента.Организация;
			СтрокаДвиженияСторно0.СчетФактура = СтрокаТаблицы.СчетФактура;
			СтрокаДвиженияСторно0.ВидЦенности = СтрокаТаблицы.ВидЦенности;
			СтрокаДвиженияСторно0.СтавкаНДС   = Ставка0;
			СтрокаДвиженияСторно0.Покупатель  = СтрокаТаблицы.Покупатель;
			СтрокаДвиженияСторно0.ВидНачисления = Перечисления.НДСВидНачисления.Реализация0;
			
			СтрокаДвиженияСторно0.СуммаБезНДС = (-1)*(СтрокаТаблицы.СуммаБезНДС + ?(СтруктураШапкиДокумента.НДСПриНеподтвержденииСверху, 0, СтрокаТаблицы.НДС));
			СтрокаДвиженияСторно0.НДС         = 0;

			СтрокаДвиженияСторно0.Событие     = Перечисления.СобытияПоНДСПродажи.НеПодтвержденаСтавка0;
			
			СтрокаДвиженияСторно0.ВидДвижения = ВидДвиженияНакопления.Приход;
			
			СтрокаДвижения = ТаблицаДвижений_НДСНачисленный.Добавить();
			СтрокаДвижения.Организация = СтрокаДвиженияСторно0.Организация;
			СтрокаДвижения.СчетФактура = СтрокаДвиженияСторно0.СчетФактура;
			СтрокаДвижения.ВидЦенности = СтрокаДвиженияСторно0.ВидЦенности;
			СтрокаДвижения.СтавкаНДС   = СтрокаТаблицы.СтавкаНДС;
			СтрокаДвижения.Покупатель  = СтрокаТаблицы.Покупатель;
			СтрокаДвижения.ВидНачисления = Перечисления.НДСВидНачисления.Реализация0;
			
			СтрокаДвижения.СуммаБезНДС = СтрокаТаблицы.СуммаБезНДС;
			СтрокаДвижения.НДС         = СтрокаТаблицы.НДС;

			СтрокаДвижения.Событие     = Перечисления.СобытияПоНДСПродажи.НеПодтвержденаСтавка0;
			
			СтрокаДвижения.ВидДвижения = ВидДвиженияНакопления.Приход;
			
			СтрокаИнформацииОРеализации = ДанныеПоРеализации0.Найти(СтрокаДвиженияСторно0.СчетФактура,"СчетФактура");
			Если СтрокаИнформацииОРеализации = Неопределено Тогда
			     СчетУчетаНДС = ПланыСчетов.Хозрасчетный.НДС;
			Иначе
				Если СтрокаИнформацииОРеализации.МоментОпределенияНалоговойБазыНДС = Перечисления.МоментыОпределенияНалоговойБазыНДС.ПоОплате тогда
			 		СчетУчетаНДС = ПланыСчетов.Хозрасчетный.РасчетыПоНДСотложенномуДляУплатыВБюджет;
				Иначе
					СчетУчетаНДС = ПланыСчетов.Хозрасчетный.НДС;
				КонецЕсли;
			КонецЕсли; 
			
			СформироватьПроводкиПоНеподтверждениюСтавки0_Реализация(СтруктураШапкиДокумента, СтрокаДвижения,СчетУчетаНДС, Движения.Хозрасчетный, Движения.Налоговый, Отказ, Заголовок);

		КонецЕсли; 
	КонецЦикла; 
	
	Если не ТаблицаДвижений_НДСНачисленный = Неопределено Тогда
		Движения.НДСНачисленный.мПериод	= СтруктураШапкиДокумента.Дата;
		Движения.НДСНачисленный.мТаблицаДвижений = ТаблицаДвижений_НДСНачисленный;
		Движения.НДСНачисленный.ДобавитьДвижение();
	Иначе 
		Возврат;
	КонецЕсли; 

КонецПроцедуры // СформироватьДвиженияПоНеподтверждениюСтавки0()

// Формирует проводку при неподтверждении ставки НДС по предъявлению НДС .
// Основная задача - отразить изменение состояния НДС в регистре "НДСПокупки", соответствующими
// проводками на субсчетах 19-го счета.
//
Процедура СформироватьПроводкиПоНеподтверждениюСтавки0_Реализация(СтруктураШапкиДокумента, СтрокаДвижения,СчетУчетаНДС, Проводки, ПроводкиНУ, Отказ, Заголовок)

	Если не СтрокаДвижения.Событие = Перечисления.СобытияПоНДСПродажи.НеПодтвержденаСтавка0
		или СтрокаДвижения.НДС = 0 
		Тогда
		Возврат;
	КонецЕсли;
	
	// Проводка по отражению НДС к возмещению из бюджета (в соотв. с письмом МинФина от 27 мая 2003 г. N 16-00-14/177)
	СтрокаДвиженияПроводка = Проводки.Добавить();
	
	// Формируем проводку по доначислению НДС с реализации 
	СтрокаДвиженияПроводка.Период       = СтруктураШапкиДокумента.Дата;
	СтрокаДвиженияПроводка.Содержание   = "Не подтвержден НДС 0% по реализации";
	СтрокаДвиженияПроводка.НомерЖурнала = "НДС";
	СтрокаДвиженияПроводка.Организация  = СтрокаДвижения.Организация;

	СтрокаДвиженияПроводка.СчетДт       = ПланыСчетов.Хозрасчетный.НДСНачисленныйПриНеподтверждении0; //68.22.1
	БухгалтерскийУчет.УстановитьСубконто(СтрокаДвиженияПроводка.СчетДт,СтрокаДвиженияПроводка.СубконтоДт,"Контрагенты",  СтрокаДвижения.Покупатель);
	БухгалтерскийУчет.УстановитьСубконто(СтрокаДвиженияПроводка.СчетДт,СтрокаДвиженияПроводка.СубконтоДт,"СФВыданные",  СтрокаДвижения.СчетФактура);
	
	
	СтрокаДвиженияПроводка.СчетКт = СчетУчетаНДС; // 68.02 или 76.Н
	БухгалтерскийУчет.УстановитьСубконто(СтрокаДвиженияПроводка.СчетКт,СтрокаДвиженияПроводка.СубконтоКт,"Контрагенты",  СтрокаДвижения.Покупатель);
	БухгалтерскийУчет.УстановитьСубконто(СтрокаДвиженияПроводка.СчетКт,СтрокаДвиженияПроводка.СубконтоКт,"ВидыПлатежейВГосБюджет", Перечисления.ВидыПлатежейВГосБюджет.Налог);
	БухгалтерскийУчет.УстановитьСубконто(СтрокаДвиженияПроводка.СчетКт,СтрокаДвиженияПроводка.СубконтоКт,"СФВыданные",  СтрокаДвижения.СчетФактура);
	                  
	СтрокаДвиженияПроводка.Сумма        = СтрокаДвижения.НДС;
	
	// Проводка по списанию НДС с возмещения из бюджета (последующее подтверждение не поддерживается)
	СтрокаДвиженияПроводка = Проводки.Добавить();
	
	// Формируем проводку по доначислению НДС с реализации 
	СтрокаДвиженияПроводка.Период       = СтруктураШапкиДокумента.Дата;
	СтрокаДвиженияПроводка.Содержание   = "Не подтвержден НДС 0% по реализации";
	СтрокаДвиженияПроводка.НомерЖурнала = "НДС";
	СтрокаДвиженияПроводка.Организация  = СтрокаДвижения.Организация;

	СтрокаДвиженияПроводка.СчетДт = ПланыСчетов.Хозрасчетный.ПрочиеРасходыНеОблагаемыеЕНВД; // 91.02
	БухгалтерскийУчет.УстановитьСубконто(СтрокаДвиженияПроводка.СчетДт,СтрокаДвиженияПроводка.СубконтоДт,"ПрочиеДоходыИРасходы", СтруктураШапкиДокумента.СтатьяПрочихРасходов);
	
	СтрокаДвиженияПроводка.СчетКт       = ПланыСчетов.Хозрасчетный.НДСНачисленныйПриНеподтверждении0; //68.22.1
	БухгалтерскийУчет.УстановитьСубконто(СтрокаДвиженияПроводка.СчетКт,СтрокаДвиженияПроводка.СубконтоКт,"Контрагенты",  СтрокаДвижения.Покупатель);
	БухгалтерскийУчет.УстановитьСубконто(СтрокаДвиженияПроводка.СчетКт,СтрокаДвиженияПроводка.СубконтоКт,"СФВыданные",  СтрокаДвижения.СчетФактура);
	
	СтрокаДвиженияПроводка.Сумма        = СтрокаДвижения.НДС;
	
	Если Не СтруктураШапкиДокумента.ОтражатьВНалоговомУчете Тогда
		Возврат;
	КонецЕсли;
	
	СтрокаДвиженияПроводкаНУ = ПроводкиНУ.Добавить();
	
	// Формируем проводку по доначислению НДС с реализации 
	СтрокаДвиженияПроводкаНУ.Период       = СтруктураШапкиДокумента.Дата;
	СтрокаДвиженияПроводкаНУ.Содержание   = "Не подтвержден НДС 0% по реализации";
	СтрокаДвиженияПроводкаНУ.НомерЖурнала = "НДС";
	СтрокаДвиженияПроводкаНУ.Организация  = СтрокаДвижения.Организация;

	СтрокаДвиженияПроводкаНУ.СчетДт = ПланыСчетов.Налоговый.ПрочиеКосвенныеРасходы; // 91.02.9
	БухгалтерскийУчет.УстановитьСубконто(СтрокаДвиженияПроводкаНУ.СчетДт,СтрокаДвиженияПроводкаНУ.СубконтоДт,"ПрочиеДоходыИРасходы", СтруктураШапкиДокумента.СтатьяПрочихРасходов);
		
	СтрокаДвиженияПроводкаНУ.Сумма        = СтрокаДвижения.НДС;
	
КонецПроцедуры // СформироватьПроводкиПоНеподтверждениюСтавки0_Реализация()

// Процедура вызывается из процедуры "Обработка проведения".
// Основная задача - на основании движений по регистру НДСПокупки, сделанных документом
// реализации по ставке 0%, заполнить параметры конкретного движения показывающего
// подтверждение или неподтверждение ставки 0%.
//
Процедура СформироватьДвижениеНДСПредъявленныйРеализация0_ОтражениеПодтверждения0(СтруктураШапкиДокумента, ТаблицаДокументовРеализации, ДанныеПоРеализации0, Отказ,Заголовок)

	Для каждого СтрокаТаблицы Из ТаблицаДокументовРеализации Цикл
		Если ТипЗнч(СтрокаТаблицы.СчетФактура) = Тип("ДокументСсылка.РеализацияОтгруженныхТоваров") Тогда
			СтрокаТаблицы.СчетФактура = СтрокаТаблицы.СчетФактура.ДокументОтгрузки;
		КонецЕсли;
	КонецЦикла;	
	
	Для каждого СтрокаТаблицы Из ДанныеПоРеализации0 Цикл
		Если ТипЗнч(СтрокаТаблицы.СчетФактура) = Тип("ДокументСсылка.РеализацияОтгруженныхТоваров") Тогда
			СтрокаТаблицы.СчетФактура = СтрокаТаблицы.СчетФактура.ДокументОтгрузки;
		КонецЕсли;
	КонецЦикла;		
	
	ТаблицаПодтверждений = ТаблицаДокументовРеализации.Скопировать();
	ТаблицаПодтверждений.Свернуть("СчетФактура,Событие","СуммаБезНДС, НДС");
	ТаблицаПодтверждений.Колонки.Добавить("СуммаРеализации", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15,2)));
	
	Для каждого СтрокаПодтверждения Из ТаблицаПодтверждений Цикл
		СтрокаРеализации = ДанныеПоРеализации0.Найти(СтрокаПодтверждения.СчетФактура, "СчетФактура");
		Если СтрокаРеализации = Неопределено Тогда
			ОбщегоНазначения.СообщитьОбОшибке("По документу <"+Строка(СтрокаПодтверждения.СчетФактура)+"> не обнаружена реализация по ставке НДС 0%.", Отказ, Заголовок);
		ИначеЕсли НЕ ЗначениеЗаполнено(СтрокаРеализации.СуммаРеализации) Тогда
			ОбщегоНазначения.СообщитьОбОшибке("По документу <"+Строка(СтрокаПодтверждения.СчетФактура)+"> не обнаружена реализация по ставке НДС 0%.", Отказ, Заголовок);
		Иначе
			СтрокаПодтверждения.СуммаРеализации = СтрокаРеализации.СуммаРеализации;
		КонецЕсли; 
	КонецЦикла;
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли; 
	
	ДокументыРеализации = ОбщегоНазначения.УдалитьПовторяющиесяЭлементыМассива(ТаблицаДокументовРеализации.ВыгрузитьКолонку("СчетФактура"), Истина);
	
	ТаблицаДвижений_НДСПредъявленныйРеализация0 = Движения.НДСПредъявленныйРеализация0.ВыгрузитьКолонки();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	НДСПредъявленныйРеализация0ОстаткиИОбороты.Организация,
	|	НДСПредъявленныйРеализация0ОстаткиИОбороты.СчетФактура,
	|	НДСПредъявленныйРеализация0ОстаткиИОбороты.ДокументОтгрузки,
	|	НДСПредъявленныйРеализация0ОстаткиИОбороты.ВидЦенности,
	|	НДСПредъявленныйРеализация0ОстаткиИОбороты.СтавкаНДС,
	|	НДСПредъявленныйРеализация0ОстаткиИОбороты.СчетУчетаНДС,
	|	НДСПредъявленныйРеализация0ОстаткиИОбороты.СуммаБезНДСКонечныйОстаток как ОсталосьСуммаБезНДС,
	|	НДСПредъявленныйРеализация0ОстаткиИОбороты.НДСКонечныйОстаток как ОсталосьНДС,
	|	ЕСТЬNULL(НДСПредъявленныйРеализация0ОстаткиИОбороты.СуммаБезНДСПриход, 0) как СуммаБезНДС,
	|	ЕСТЬNULL(НДСПредъявленныйРеализация0ОстаткиИОбороты.НДСПриход, 0) КАК НДС
	|ИЗ
	|	РегистрНакопления.НДСПредъявленныйРеализация0.ОстаткиИОбороты(
	|		,
	|		&КонецПериода,
	|		Период,
	|		,
	|		Организация = &Организация
	|			И ДокументОтгрузки В (&СписокДокументовРеализации)
	|			И Состояние = &СостояниеПредположения0) КАК НДСПредъявленныйРеализация0ОстаткиИОбороты";
	
	Запрос.УстановитьПараметр("КонецПериода", Новый Граница(КонецДня(СтруктураШапкиДокумента.Дата), ВидГраницы.Включая));
	Запрос.УстановитьПараметр("Организация", СтруктураШапкиДокумента.Организация);
	СостояниеОжиданияПодтверждения = Перечисления.НДССостоянияРеализация0.ОжидаетсяПодтверждение;
	Запрос.УстановитьПараметр("СостояниеПредположения0", СостояниеОжиданияПодтверждения);
	Запрос.УстановитьПараметр("СписокДокументовРеализации", ДокументыРеализации);
	
	ТаблицаОжиданияПодтверждения = Запрос.Выполнить().Выгрузить(ОбходРезультатаЗапроса.Прямой);
	
	Для каждого СтрокаПодтверждения Из ТаблицаПодтверждений Цикл
		СтрокиПредположения = ТаблицаОжиданияПодтверждения.НайтиСтроки(Новый Структура("ДокументОтгрузки",СтрокаПодтверждения.СчетФактура));
		Для каждого СтрокаПредположения Из СтрокиПредположения Цикл
			СуммаПодтвержденияПоСтрокеРеализации = СтрокаПодтверждения.СуммаБезНДС + ?(СтрокаПодтверждения.Событие = Перечисления.СобытияПоНДСПродажи.НеПодтвержденаСтавка0 и СтруктураШапкиДокумента.НДСПриНеподтвержденииСверху,0, СтрокаПодтверждения.НДС);
			КПогашениюБезНДС = Окр(СтрокаПредположения.СуммаБезНДС*(СуммаПодтвержденияПоСтрокеРеализации)/СтрокаПодтверждения.СуммаРеализации,2);
			КПогашениюБезНДС = Макс(0, Мин(КПогашениюБезНДС,СтрокаПредположения.ОсталосьСуммаБезНДС));
			КПогашениюНДС = Окр(СтрокаПредположения.НДС*(СуммаПодтвержденияПоСтрокеРеализации)/СтрокаПодтверждения.СуммаРеализации,2);
			КПогашениюНДС = Макс(0, Мин(КПогашениюНДС,СтрокаПредположения.ОсталосьНДС));
			
			Если КПогашениюБезНДС =0 и КПогашениюНДС = 0 тогда
				Продолжить;
			КонецЕсли; 
			
			// регистр НДСПредъявленныйРеализация0
			// погашение суммы предположения 0%
			СтрокаСписанияПредположения = ТаблицаДвижений_НДСПредъявленныйРеализация0.Добавить();
			СтрокаСписанияПредположения.Организация		= СтруктураШапкиДокумента.Организация;
			СтрокаСписанияПредположения.СчетФактура		= СтрокаПредположения.СчетФактура;
			СтрокаСписанияПредположения.Состояние		= СостояниеОжиданияПодтверждения;
			СтрокаСписанияПредположения.ДокументОтгрузки = СтрокаПодтверждения.СчетФактура;
			
			СтрокаСписанияПредположения.ВидЦенности		= СтрокаПредположения.ВидЦенности;
			СтрокаСписанияПредположения.СтавкаНДС		= СтрокаПредположения.СтавкаНДС;
			СтрокаСписанияПредположения.СчетУчетаНДС	= СтрокаПредположения.СчетУчетаНДС;
			//СтрокаСписанияПредположения.Поставщик =   СтрокаПредположения.Поставщик;
			
			СтрокаСписанияПредположения.СуммаБезНДС = КПогашениюБезНДС;
			СтрокаСписанияПредположения.НДС = 		 КПогашениюНДС;
			
			Если СтрокаПодтверждения.Событие = Перечисления.СобытияПоНДСПродажи.ПодтвержденаСтавка0 Тогда
				СтрокаСписанияПредположения.Событие = Перечисления.СобытияПоНДСПокупки.ПодтвержденаСтавка0;
			Иначе
				СтрокаСписанияПредположения.Событие = Перечисления.СобытияПоНДСПокупки.НеПодтвержденаСтавка0;
			КонецЕсли;
			
			СтрокаСписанияПредположения.ВидДвижения = ВидДвиженияНакопления.Расход;
			
			// Отражение подтверждения/неподтверждения 0%
			СтрокаОтраженияПодтверждения = ТаблицаДвижений_НДСПредъявленныйРеализация0.Добавить();
			СтрокаОтраженияПодтверждения.Организация	= СтрокаСписанияПредположения.Организация;
			СтрокаОтраженияПодтверждения.СчетФактура	= СтрокаСписанияПредположения.СчетФактура;
			Если СтрокаСписанияПредположения.Событие	= Перечисления.СобытияПоНДСПокупки.ПодтвержденаСтавка0 Тогда
				СтрокаОтраженияПодтверждения.Состояние	= Перечисления.НДССостоянияРеализация0.ПодтвержденаРеализация0;
			Иначе
				СтрокаОтраженияПодтверждения.Состояние	= Перечисления.НДССостоянияРеализация0.НеПодтвержденаРеализация0
			КонецЕсли;
			
			СтрокаОтраженияПодтверждения.ДокументОтгрузки = СтрокаСписанияПредположения.ДокументОтгрузки;
			
			СтрокаОтраженияПодтверждения.ВидЦенности	= СтрокаСписанияПредположения.ВидЦенности;
			СтрокаОтраженияПодтверждения.СтавкаНДС		= СтрокаСписанияПредположения.СтавкаНДС;
			СтрокаОтраженияПодтверждения.СчетУчетаНДС	= СтрокаСписанияПредположения.СчетУчетаНДС;
			
			СтрокаОтраженияПодтверждения.СуммаБезНДС	= СтрокаСписанияПредположения.СуммаБезНДС;
			СтрокаОтраженияПодтверждения.НДС			= СтрокаСписанияПредположения.НДС;
			
			СтрокаОтраженияПодтверждения.Событие		=   СтрокаСписанияПредположения.Событие;
			
			СтрокаОтраженияПодтверждения.ВидДвижения	= ВидДвиженияНакопления.Приход;
		
			СтрокаПредположения.ОсталосьСуммаБезНДС = СтрокаПредположения.ОсталосьСуммаБезНДС - КПогашениюБезНДС;
			СтрокаПредположения.ОсталосьНДС	 = СтрокаПредположения.ОсталосьНДС    - КПогашениюНДС;
			
		КонецЦикла; 
	
	КонецЦикла; 
	
	Если ТаблицаДвижений_НДСПредъявленныйРеализация0.Количество()>0 Тогда
		Движения.НДСПредъявленныйРеализация0.мПериод = СтруктураШапкиДокумента.Дата;
		Движения.НДСПредъявленныйРеализация0.мТаблицаДвижений = ТаблицаДвижений_НДСПредъявленныйРеализация0;
		Движения.НДСПредъявленныйРеализация0.ДобавитьДвижение();
	КонецЕсли; 
		
КонецПроцедуры // СформироватьДвижениеНДСПокупкиПоОтражениюСтавки0()
 
////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

Процедура ОбработкаПроведения(Отказ, Режим)
	
	Если мУдалятьДвижения Тогда
		ОбщегоНазначения.УдалитьДвиженияРегистратора(ЭтотОбъект, Отказ);
	КонецЕсли;
	
	мЗаконодательство2006 = (Дата >= '20060101');
	
	// Заголовок для сообщений об ошибках проведения.
	Заголовок = "Проведение документа: "+СокрЛП(Ссылка);
	
	// Сформируем запрос на дополнительные параметры, нужные при проведении, по данным шапки документа
	СтруктураШапкиДокумента = ОбщегоНазначения.СформироватьСтруктуруШапкиДокумента(ЭтотОбъект);
	
	// Дополним структуру шапки документа положениями учетной политики
	СтруктураПолейУчетнойПолитикиНУ = Новый Структура("НДСПриНеподтвержденииСверху");
	ОбщегоНазначения.ДополнитьПоложениямиУчетнойПолитики(СтруктураШапкиДокумента, СтруктураШапкиДокумента.Дата, Отказ, СтруктураШапкиДокумента.Организация, "Нал", СтруктураПолейУчетнойПолитикиНУ);
	
	// Проверим правильность заполнения шапки документа
	ПроверитьЗаполнениеШапки(Отказ, Заголовок);
	
	// Подготовим данные необходимые для проведения и проверки заполнения табличной части.
	СтруктураПолей = Новый Структура;
	СтруктураПолей.Вставить("Покупатель",		"Покупатель");
	СтруктураПолей.Вставить("СчетФактура",		"СчетФактура");
	СтруктураПолей.Вставить("ВидЦенности",		"ВидЦенности");
	СтруктураПолей.Вставить("Событие",			"Событие");
	СтруктураПолей.Вставить("СуммаБезНДС",		"ПродажиСНДС0");
	СтруктураПолей.Вставить("СтавкаНДС",		"СтавкаНДС");
	СтруктураПолей.Вставить("НДС",				"СуммаНДС");
	СтруктураПолей.Вставить("КурсоваяРазница",	"КурсоваяРазница");
	СтруктураПолей.Вставить("ДокРеализации",	"СчетФактура");
	СтруктураПолей.Вставить("СтавкаРеализации",	"СтавкаНДС");

	РезультатЗапросаПоДокументам = УправлениеЗапасами.СформироватьЗапросПоТабличнойЧасти(ЭтотОбъект, "Состав", СтруктураПолей);
	ТаблицаПоДокументам = 			ПодготовитьТаблицуПоДокументам(РезультатЗапросаПоДокументам,СтруктураШапкиДокумента);
	
	ПроверитьЗаполнениеТабличнойЧастиПоДокументам(ТаблицаПоДокументам, Отказ, Заголовок);
	
	Если Не Отказ Тогда
		ДвиженияПоРегистрам(СтруктураШапкиДокумента,ТаблицаПоДокументам, Отказ, Заголовок);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	Если ОбменДанными.Загрузка  Тогда
		Возврат;
	КонецЕсли;

	 
	мУдалятьДвижения = НЕ ЭтоНовый();
	
КонецПроцедуры // ПередЗаписью()

мВалютаРегламентированногоУчета = глЗначениеПеременной("ВалютаРегламентированногоУчета");

мЗаконодательство2006 = (Дата >= '20060101');