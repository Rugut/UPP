Перем мУдалятьДвижения;

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОКУМЕНТА


#Если Клиент Тогда
// Процедура осуществляет печать документа. Можно направить печать на 
// экран или принтер, а также распечатать необходимое количество копий.
//
//  Название макета печати передается в качестве параметра,
// по переданному названию находим имя макета в соответствии.
//
// Параметры:
//  НазваниеМакета - строка, название макета.
//
Процедура Печать(ИмяМакета, КоличествоЭкземпляров = 1, НаПринтер = Ложь) Экспорт

	Если ЭтоНовый() Тогда
		Предупреждение("Документ можно распечатать только после его записи");
		Возврат;
	ИначеЕсли Не УправлениеДопПравамиПользователей.РазрешитьПечатьНепроведенныхДокументов(Проведен) Тогда
		Предупреждение("Недостаточно полномочий для печати непроведенного документа!");
		Возврат;
	КонецЕсли;

	Если Не РаботаСДиалогами.ПроверитьМодифицированность(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;

	Если ТипЗнч(ИмяМакета) = Тип("ДвоичныеДанные") Тогда

		ТабДокумент = УниверсальныеМеханизмы.НапечататьВнешнююФорму(Ссылка, ИмяМакета);
		
		Если ТабДокумент = Неопределено Тогда
			Возврат
		КонецЕсли; 
		
	КонецЕсли;

	УниверсальныеМеханизмы.НапечататьДокумент(ТабДокумент, КоличествоЭкземпляров, НаПринтер, ОбщегоНазначения.СформироватьЗаголовокДокумента(ЭтотОбъект), Ссылка);

КонецПроцедуры // Печать

#КонецЕсли

// Возвращает доступные варианты печати документа
//
// Возвращаемое значение:
//  Структура, каждая строка которой соответствует одному из вариантов печати
//  
Функция ПолучитьСтруктуруПечатныхФорм() Экспорт
	
	Возврат Новый Структура;

КонецФункции // ПолучитьСтруктуруПечатныхФорм()

Процедура УстановитьСубконтоПроводки(Куда, Счет, ВидСубконто, Значение)

	Если Счет.ВидыСубконто.Найти(ВидСубконто, "ВидСубконто") <> Неопределено Тогда
		Куда[ВидСубконто] = Значение;
	КонецЕсли;

КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, Режим)
	
	Если мУдалятьДвижения Тогда
		ОбщегоНазначения.УдалитьДвиженияРегистратора(ЭтотОбъект, Отказ);
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Ссылка",      Ссылка);
	Запрос.УстановитьПараметр("Период",      КонецМесяца(ПериодРегистрации));
	Запрос.УстановитьПараметр("Организация", Организация);
	
	Запрос.Текст = 
	"////////////////////////////////////////////////////////////////////////////////
	|// Выборка сведений об учетной политике
	|
	|ВЫБРАТЬ 
	|	УчетнаяПолитикаМеждународныйУчетСрезПоследних.МетодОценкиОсновныхСредств КАК МетодОценкиОсновныхСредств
	|ПОМЕСТИТЬ УчетнаяПолитика
	|ИЗ
	|	РегистрСведений.УчетнаяПолитикаМеждународныйУчет.СрезПоследних(&Период, Организация = &Организация) КАК УчетнаяПолитикаМеждународныйУчетСрезПоследних
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|// Выборка сведений об основных средствах из табличной части документа
	|
	|ВЫБРАТЬ
	|	АмортизацияОСМеждународныйОсновныеСредства.ОсновноеСредство КАК ОсновноеСредство,
	|	АмортизацияОСМеждународныйОсновныеСредства.МетодНачисленияАмортизации КАК МетодНачисленияАмортизации,
	|	АмортизацияОСМеждународныйОсновныеСредства.КоличествоВыпущеннойПродукции КАК КоличествоВыпущеннойПродукции
	|ПОМЕСТИТЬ СведенияДокумента
	|ИЗ
	|	Документ.АмортизацияОСМеждународный.ОсновныеСредства КАК АмортизацияОСМеждународныйОсновныеСредства
	|ГДЕ
	|	АмортизацияОСМеждународныйОсновныеСредства.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|// Выборка сведений об основных средствах из информационной базы
	|
	|ВЫБРАТЬ
	|	ОсновныеСредстваМеждународныйУчетСрезПоследних.ОсновноеСредство КАК ОсновноеСредство,
	|	ОсновныеСредстваМеждународныйУчетСрезПоследних.ДатаПринятияКУчету КАК ДатаПринятияКУчету,
	|	ОсновныеСредстваМеждународныйУчетСрезПоследних.СрокПолезногоИспользования КАК СрокПолезногоИспользования,
	|	ОсновныеСредстваМеждународныйУчетСрезПоследних.СчетУчета КАК СчетУчета,
	|	ОсновныеСредстваМеждународныйУчетСрезПоследних.СчетЗатрат КАК СчетЗатрат,
	|	ОсновныеСредстваМеждународныйУчетСрезПоследних.СчетНачисленияАмортизации КАК СчетНачисленияАмортизации,
	|	ОсновныеСредстваМеждународныйУчетСрезПоследних.СчетСниженияСтоимости КАК СчетСниженияСтоимости,
	|	ОсновныеСредстваМеждународныйУчетСрезПоследних.ЛиквидационнаяСтоимость КАК ЛиквидационнаяСтоимость,
	|	ОсновныеСредстваМеждународныйУчетСрезПоследних.КоэффициентУскорения КАК КоэффициентУскорения,
	|	ОсновныеСредстваМеждународныйУчетСрезПоследних.ПредполагаемыйОбъемПродукции КАК ПредполагаемыйОбъемПродукции,
	|	ОсновныеСредстваМеждународныйУчетСрезПоследних.ПервоначальнаяСтоимость КАК ПервоначальнаяСтоимость,
	|	ОсновныеСредстваМеждународныйУчетСрезПоследних.СправедливаяСтоимость КАК СправедливаяСтоимость,
	|	ОсновныеСредстваМеждународныйУчетСрезПоследних.МестонахождениеОбъекта КАК МестонахождениеОбъекта,
	|	ОсновныеСредстваМеждународныйУчетСрезПоследних.МОЛ КАК МОЛ,
	|	ОсновныеСредстваМеждународныйУчетСрезПоследних.НачислятьАмортизацию КАК НачислятьАмортизацию,
	|	ОсновныеСредстваМеждународныйУчетСрезПоследних.СуммаНачисленнойАмортизации КАК СуммаНачисленнойАмортизации,
	|	ОсновныеСредстваМеждународныйУчетСрезПоследних.Состояние КАК Состояние,
	|	ОсновныеСредстваМеждународныйУчетСрезПоследних.Субконто1 КАК Субконто1,
	|	ОсновныеСредстваМеждународныйУчетСрезПоследних.Субконто2 КАК Субконто2,
	|	ОсновныеСредстваМеждународныйУчетСрезПоследних.Субконто3 КАК Субконто3
	|ПОМЕСТИТЬ СведенияИнформационнойБазы
	|ИЗ
	|	РегистрСведений.ОсновныеСредстваМеждународныйУчет.СрезПоследних(&Период, ОсновноеСредство В	(ВЫБРАТЬ ОсновноеСредство ИЗ СведенияДокумента)) КАК ОсновныеСредстваМеждународныйУчетСрезПоследних
	|ГДЕ
	|	ОсновныеСредстваМеждународныйУчетСрезПоследних.НачислятьАмортизацию = ИСТИНА
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|// Сводная таблица со сведениями документа и информационной базы 
	|
	|ВЫБРАТЬ
	|	СведенияДокумента.*,
	|	СведенияИнформационнойБазы.*
	|ПОМЕСТИТЬ СведенияОС
	|ИЗ
	|	СведенияДокумента КАК СведенияДокумента
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ СведенияИнформационнойБазы КАК СведенияИнформационнойБазы
	|	ПО СведенияДокумента.ОсновноеСредство = СведенияИнформационнойБазы.ОсновноеСредство
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|// Выборка счетов учета основных средств
	|
	|ВЫБРАТЬ 
	|	СведенияОС.СчетУчета КАК Счет
	|ПОМЕСТИТЬ СчетаУчетаОС
	|ИЗ
	|	СведенияОС
	|ГДЕ
	|	МетодНачисленияАмортизации <> ЗНАЧЕНИЕ(Перечисление.МетодыНачисленияАмортизацииОСМеждународный.УменьшаемогоОстатка)	
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ 
	|	СведенияОС.СчетНачисленияАмортизации
	|ИЗ
	|	СведенияОС
	|ГДЕ
	|	МетодНачисленияАмортизации <> ЗНАЧЕНИЕ(Перечисление.МетодыНачисленияАмортизацииОСМеждународный.УменьшаемогоОстатка)	
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	СведенияОС.СчетСниженияСтоимости
	|ИЗ
	|	СведенияОС
	|ГДЕ
	|	МетодНачисленияАмортизации <> ЗНАЧЕНИЕ(Перечисление.МетодыНачисленияАмортизацииОСМеждународный.УменьшаемогоОстатка)	
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|// Выборка остатков по счетам учета основных средств
	|
	|ВЫБРАТЬ
	|	МеждународныйОстатки.Субконто1 КАК ОсновноеСредство,
	|	МеждународныйОстатки.Счет КАК Счет,
	|	МеждународныйОстатки.СуммаОстатокДт КАК СуммаОстатокДт,
	|	МеждународныйОстатки.СуммаОстатокКт КАК СуммаОстатокКт
	|ПОМЕСТИТЬ ОстаткиПоСчетамОС
	|ИЗ
	|	РегистрБухгалтерии.Международный.Остатки(&Период, Счет В (ВЫБРАТЬ РАЗЛИЧНЫЕ Счет Из СчетаУчетаОС), ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоМеждународные.ОсновныеСредства), Организация = &Организация И Субконто1 В (ВЫБРАТЬ ОсновноеСредство ИЗ СведенияОС ГДЕ МетодНачисленияАмортизации <> ЗНАЧЕНИЕ(Перечисление.МетодыНачисленияАмортизацииОСМеждународный.УменьшаемогоОстатка))) КАК МеждународныйОстатки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|// Сведения об основных средствах, амортизация по которым начисляется методом
	|// уменьшаемого остатка
	|
	|ВЫБРАТЬ
	|	СчетаУчетаОССрезПоследних.ОсновноеСредство,
	|	СчетаУчетаОССрезПоследних.Период,
	|	ОсновныеСредстваМеждународныйУчет.СчетУчета,
	|	ОсновныеСредстваМеждународныйУчет.СчетНачисленияАмортизации,
	|	ОсновныеСредстваМеждународныйУчет.СчетСниженияСтоимости
	|ПОМЕСТИТЬ СведенияОСМетодУменьшаемогоОстатка
	|ИЗ
	|	(ВЫБРАТЬ
	|		ДатыСнятияОстатков.ОсновноеСредство КАК ОсновноеСредство,
	|		ДатыСнятияОстатков.Период КАК Период,
	|		МАКСИМУМ(ОсновныеСредстваМеждународныйУчет.Период) КАК ПериодСрезаПоследних
	|	ИЗ
	|		(ВЫБРАТЬ
	|			ИзменениеСтоимостиОССрезПоследних.ОсновноеСредство КАК ОсновноеСредство,
	|			ВЫБОР КОГДА ИзменениеСтоимостиОССрезПоследних.Период > НАЧАЛОПЕРИОДА(&Период, ГОД)
	|				ТОГДА ИзменениеСтоимостиОССрезПоследних.Период
	|				ИНАЧЕ НАЧАЛОПЕРИОДА(&Период, ГОД)
	|			КОНЕЦ КАК Период
	|		ИЗ
	|			(ВЫБРАТЬ
	|				ИзменениеСтоимостиОССрезПервых.ОсновноеСредство КАК ОсновноеСредство,
	|				МАКСИМУМ(ИзменениеСтоимостиОССрезПервых.Период) КАК Период
	|			ИЗ
	|				(ВЫБРАТЬ
	|					ОсновныеСредстваМеждународныйУчет.ОсновноеСредство КАК ОсновноеСредство,
	|					МИНИМУМ(ОсновныеСредстваМеждународныйУчет.Период) КАК Период
	|				ИЗ
	|					РегистрСведений.ОсновныеСредстваМеждународныйУчет КАК ОсновныеСредстваМеждународныйУчет
	|				ГДЕ
	|					ОсновныеСредстваМеждународныйУчет.ОсновноеСредство В (ВЫБРАТЬ ОсновноеСредство ИЗ СведенияОС ГДЕ МетодНачисленияАмортизации = ЗНАЧЕНИЕ(Перечисление.МетодыНачисленияАмортизацииОСМеждународный.УменьшаемогоОстатка))
	|					И ОсновныеСредстваМеждународныйУчет.Период <= &Период
	|			
	|				СГРУППИРОВАТЬ ПО
	|					ОсновныеСредстваМеждународныйУчет.ОсновноеСредство,
	|					ОсновныеСредстваМеждународныйУчет.ПервоначальнаяСтоимость,
	|					ОсновныеСредстваМеждународныйУчет.СправедливаяСтоимость) КАК ИзменениеСтоимостиОССрезПервых
	|  		
	|			СГРУППИРОВАТЬ ПО
	|				ИзменениеСтоимостиОССрезПервых.ОсновноеСредство) КАК ИзменениеСтоимостиОССрезПоследних
	|		) КАК ДатыСнятияОстатков
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОсновныеСредстваМеждународныйУчет КАК ОсновныеСредстваМеждународныйУчет
	|		ПО ДатыСнятияОстатков.ОсновноеСредство = ОсновныеСредстваМеждународныйУчет.ОсновноеСредство
	|		 И ДатыСнятияОстатков.Период >= ОсновныеСредстваМеждународныйУчет.Период
	|	СГРУППИРОВАТЬ ПО
	|		ДатыСнятияОстатков.ОсновноеСредство,
	|		ДатыСнятияОстатков.Период) КАК СчетаУчетаОССрезПоследних
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОсновныеСредстваМеждународныйУчет КАК ОсновныеСредстваМеждународныйУчет
	|	ПО СчетаУчетаОССрезПоследних.ОсновноеСредство = ОсновныеСредстваМеждународныйУчет.ОсновноеСредство
	|	 И СчетаУчетаОССрезПоследних.ПериодСрезаПоследних = ОсновныеСредстваМеждународныйУчет.Период
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|// Выборка счетов учета основных средств, амортизация по которым начисляется
	|// методом уменьшаемого остатка
	|
	|ВЫБРАТЬ 
	|	СведенияОСМетодУменьшаемогоОстатка.СчетУчета КАК Счет
	|ПОМЕСТИТЬ СчетаУчетаОСМетодУменьшаемогоОстатка
	|ИЗ
	|	СведенияОСМетодУменьшаемогоОстатка
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ 
	|	СведенияОСМетодУменьшаемогоОстатка.СчетНачисленияАмортизации
	|ИЗ
	|	СведенияОСМетодУменьшаемогоОстатка
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ 
	|	СведенияОСМетодУменьшаемогоОстатка.СчетСниженияСтоимости
	|ИЗ
	|	СведенияОСМетодУменьшаемогоОстатка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|// Выборка движений по счетам учета основных средств 
	|// (метод уменьшаемого остатка).
	|
	|ВЫБРАТЬ
	|	МеждународныйОстаткиИОбороты.Субконто1 КАК ОсновноеСредство,
	|	МеждународныйОстаткиИОбороты.Счет КАК Счет,
	|	МеждународныйОстаткиИОбороты.Период КАК Период,
	|	МеждународныйОстаткиИОбороты.СуммаКонечныйОстатокДт КАК СуммаОстатокДт,
	|	МеждународныйОстаткиИОбороты.СуммаКонечныйОстатокКт КАК СуммаОстатокКт
	|ПОМЕСТИТЬ ДвиженияПоСчетамОС
	|ИЗ
	|   РегистрБухгалтерии.Международный.ОстаткиИОбороты(НАЧАЛОПЕРИОДА(&Период, ГОД), &Период, Запись, , Счет В (ВЫБРАТЬ РАЗЛИЧНЫЕ Счет Из СчетаУчетаОСМетодУменьшаемогоОстатка), ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоМеждународные.ОсновныеСредства), Организация = &Организация И Субконто1 В (ВЫБРАТЬ ОсновноеСредство ИЗ СведенияОС ГДЕ МетодНачисленияАмортизации = ЗНАЧЕНИЕ(Перечисление.МетодыНачисленияАмортизацииОСМеждународный.УменьшаемогоОстатка))) КАК МеждународныйОстаткиИОбороты
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|// Выборка остатков по счетам учета основных средств, амортизация по которым
	|// начисляется методом уменьшаемого остатка
	|
	|ВЫБРАТЬ
	|	ДвиженияПоСчетамОССрезПоследних.ОсновноеСредство КАК ОсновноеСредство,
	|	ДвиженияПоСчетамОССрезПоследних.Счет КАК Счет,
	|	ДвиженияПоСчетамОС.СуммаОстатокДт КАК СуммаОстатокДт,
	|	ДвиженияПоСчетамОС.СуммаОстатокКт КАК СуммаОстатокКт
	|ПОМЕСТИТЬ ОстаткиПоСчетамОСУменьшаемогоОстатка
	|ИЗ
	|	(ВЫБРАТЬ
	|		СведенияОСМетодУменьшаемогоОстатка.ОсновноеСредство КАК ОсновноеСредство,
	|		ДвиженияПоСчетамОС.Счет КАК Счет,
	|		МАКСИМУМ(ДвиженияПоСчетамОС.Период) КАК Период
	|	ИЗ
	|		СведенияОСМетодУменьшаемогоОстатка
	|		ЛЕВОЕ СОЕДИНЕНИЕ ДвиженияПоСчетамОС КАК ДвиженияПоСчетамОС
	|		ПО СведенияОСМетодУменьшаемогоОстатка.Период >= ДвиженияПоСчетамОС.Период
	|		 И СведенияОСМетодУменьшаемогоОстатка.ОсновноеСредство = ДвиженияПоСчетамОС.ОсновноеСредство
	|	
	|	СГРУППИРОВАТЬ ПО
	|		СведенияОСМетодУменьшаемогоОстатка.ОсновноеСредство,
	|		ДвиженияПоСчетамОС.Счет) КАК ДвиженияПоСчетамОССрезПоследних
	|	ЛЕВОЕ СОЕДИНЕНИЕ ДвиженияПоСчетамОС КАК ДвиженияПоСчетамОС
	|	ПО ДвиженияПоСчетамОССрезПоследних.Период = ДвиженияПоСчетамОС.Период
	|	 И ДвиженияПоСчетамОССрезПоследних.ОсновноеСредство = ДвиженияПоСчетамОС.ОсновноеСредство
	|	 И ДвиженияПоСчетамОССрезПоследних.Счет = ДвиженияПоСчетамОС.Счет
	|";
	Запрос.Выполнить();
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СведенияОС.ОсновноеСредство КАК ОсновноеСредство,
	|	СведенияОС.МетодНачисленияАмортизации КАК МетодНачисленияАмортизации,
	|	СведенияОС.КоличествоВыпущеннойПродукции КАК КоличествоВыпущеннойПродукции,
	|	СведенияОС.ДатаПринятияКУчету КАК ДатаПринятияКУчету,
	|	СведенияОС.СрокПолезногоИспользования КАК СрокПолезногоИспользования,
	|	СведенияОС.СчетУчета КАК СчетУчета,
	|	СведенияОС.СчетЗатрат КАК СчетЗатрат,
	|	СведенияОС.СчетНачисленияАмортизации КАК СчетНачисленияАмортизации,
	|	СведенияОС.СчетСниженияСтоимости КАК СчетСниженияСтоимости,
	|	СведенияОС.ЛиквидационнаяСтоимость КАК ЛиквидационнаяСтоимость,
	|	СведенияОС.КоэффициентУскорения КАК КоэффициентУскорения,
	|	СведенияОС.ПредполагаемыйОбъемПродукции КАК ПредполагаемыйОбъемПродукции,
	|	СведенияОС.ПервоначальнаяСтоимость КАК ПервоначальнаяСтоимость,
	|	СведенияОС.СправедливаяСтоимость КАК СправедливаяСтоимость,
	|	СведенияОС.МестонахождениеОбъекта КАК МестонахождениеОбъекта,
	|	СведенияОС.МОЛ КАК МОЛ,
	|	СведенияОС.НачислятьАмортизацию КАК НачислятьАмортизацию,
	|	СведенияОС.СуммаНачисленнойАмортизации КАК СуммаНачисленнойАмортизации,
	|	СведенияОС.Состояние КАК Состояние,
	|	СведенияОС.Субконто1 КАК Субконто1,
	|	СведенияОС.Субконто2 КАК Субконто2,
	|	СведенияОС.Субконто3 КАК Субконто3,
	|	ЕСТЬNULL(СтоимостьОС.СуммаОстатокДт, 0) - ЕСТЬNULL(АмортизацияОС.СуммаОстатокКт, 0) - ЕСТЬNULL(СнижениеСтоимостиОС.СуммаОстатокКт, 0) КАК ОстаточнаяСтоимость,
	|	УчетнаяПолитика.МетодОценкиОсновныхСредств КАК МетодОценкиОсновныхСредств,
	|	0 КАК Месяц
	|ИЗ
	|	СведенияОС КАК СведенияОС
	|	ЛЕВОЕ СОЕДИНЕНИЕ ОстаткиПоСчетамОС КАК СтоимостьОС
	|	ПО СведенияОС.ОсновноеСредство = СтоимостьОС.ОсновноеСредство
	|	 И СведенияОС.СчетУчета = СтоимостьОС.Счет
	|	ЛЕВОЕ СОЕДИНЕНИЕ ОстаткиПоСчетамОС КАК АмортизацияОС
	|	ПО СведенияОС.ОсновноеСредство = АмортизацияОС.ОсновноеСредство
	|	 И СведенияОС.СчетНачисленияАмортизации = АмортизацияОС.Счет
	|	ЛЕВОЕ СОЕДИНЕНИЕ ОстаткиПоСчетамОС КАК СнижениеСтоимостиОС
	|	ПО СведенияОС.ОсновноеСредство = СнижениеСтоимостиОС.ОсновноеСредство
	|	 И СведенияОС.СчетСниженияСтоимости = СнижениеСтоимостиОС.Счет,
	|	УчетнаяПолитика
	|ГДЕ 
	|	СведенияОС.МетодНачисленияАмортизации <> ЗНАЧЕНИЕ(Перечисление.МетодыНачисленияАмортизацииОСМеждународный.УменьшаемогоОстатка)	
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	СведенияОС.ОсновноеСредство КАК ОсновноеСредство,
	|	СведенияОС.МетодНачисленияАмортизации КАК МетодНачисленияАмортизации,
	|	СведенияОС.КоличествоВыпущеннойПродукции КАК КоличествоВыпущеннойПродукции,
	|	СведенияОС.ДатаПринятияКУчету КАК ДатаПринятияКУчету,
	|	СведенияОС.СрокПолезногоИспользования КАК СрокПолезногоИспользования,
	|	СведенияОС.СчетУчета КАК СчетУчета,
	|	СведенияОС.СчетЗатрат КАК СчетЗатрат,
	|	СведенияОС.СчетНачисленияАмортизации КАК СчетНачисленияАмортизации,
	|	СведенияОС.СчетСниженияСтоимости КАК СчетСниженияСтоимости,
	|	СведенияОС.ЛиквидационнаяСтоимость КАК ЛиквидационнаяСтоимость,
	|	СведенияОС.КоэффициентУскорения КАК КоэффициентУскорения,
	|	СведенияОС.ПредполагаемыйОбъемПродукции КАК ПредполагаемыйОбъемПродукции,
	|	СведенияОС.ПервоначальнаяСтоимость КАК ПервоначальнаяСтоимость,
	|	СведенияОС.СправедливаяСтоимость КАК СправедливаяСтоимость,
	|	СведенияОС.МестонахождениеОбъекта КАК МестонахождениеОбъекта,
	|	СведенияОС.МОЛ КАК МОЛ,
	|	СведенияОС.НачислятьАмортизацию КАК НачислятьАмортизацию,
	|	СведенияОС.СуммаНачисленнойАмортизации КАК СуммаНачисленнойАмортизации,
	|	СведенияОС.Состояние КАК Состояние,
	|	СведенияОС.Субконто1 КАК Субконто1,
	|	СведенияОС.Субконто2 КАК Субконто2,
	|	СведенияОС.Субконто3 КАК Субконто3,
	|	ЕСТЬNULL(СтоимостьОС.СуммаОстатокДт, 0) - ЕСТЬNULL(АмортизацияОС.СуммаОстатокКт, 0) - ЕСТЬNULL(СнижениеСтоимостиОС.СуммаОстатокКт, 0) КАК ОстаточнаяСтоимость,
	|	УчетнаяПолитика.МетодОценкиОсновныхСредств КАК МетодОценкиОсновныхСредств,
	|	МЕСЯЦ(СведенияОСМетодУменьшаемогоОстатка.Период) КАК Месяц
	|ИЗ
	|	СведенияОС КАК СведенияОС
	|	ЛЕВОЕ СОЕДИНЕНИЕ ОстаткиПоСчетамОСУменьшаемогоОстатка КАК СтоимостьОС
	|	ПО СведенияОС.ОсновноеСредство = СтоимостьОС.ОсновноеСредство
	|	 И СведенияОС.СчетУчета = СтоимостьОС.Счет
	|	ЛЕВОЕ СОЕДИНЕНИЕ ОстаткиПоСчетамОСУменьшаемогоОстатка КАК АмортизацияОС
	|	ПО СведенияОС.ОсновноеСредство = АмортизацияОС.ОсновноеСредство
	|	 И СведенияОС.СчетНачисленияАмортизации = АмортизацияОС.Счет
	|	ЛЕВОЕ СОЕДИНЕНИЕ ОстаткиПоСчетамОСУменьшаемогоОстатка КАК СнижениеСтоимостиОС
	|	ПО СведенияОС.ОсновноеСредство = СнижениеСтоимостиОС.ОсновноеСредство
	|	 И СведенияОС.СчетСниженияСтоимости = СнижениеСтоимостиОС.Счет,
	|	ЛЕВОЕ СОЕДИНЕНИЕ СведенияОСМетодУменьшаемогоОстатка КАК СведенияОСМетодУменьшаемогоОстатка
	|	ПО СведенияОС.ОсновноеСредство = СведенияОСМетодУменьшаемогоОстатка.ОсновноеСредство,
	|	УчетнаяПолитика
	|ГДЕ 
	|	СведенияОС.МетодНачисленияАмортизации = ЗНАЧЕНИЕ(Перечисление.МетодыНачисленияАмортизацииОСМеждународный.УменьшаемогоОстатка)	
	|";
	ВыборкаОС = Запрос.Выполнить().Выбрать();
	
	Счета = ПланыСчетов.Международный;
	
	// рассчитываем сальдо по резерву переоценки - для списания пропрционально амортизации
	БухИтогиР = Обработки.БухгалтерскиеИтоги.Создать();
	Резервы = Справочники.ВидыФинРезервов.РезервыНаПереоценку;
	МассивСубконто = Новый Массив();
	МассивСубконто.Добавить(ПланыВидовХарактеристик.ВидыСубконтоМеждународные.ОсновныеСредства);
	МассивСубконто.Добавить(ПланыВидовХарактеристик.ВидыСубконтоМеждународные.Резервы);
	СчетРезервыНаПереоценку = ПланыСчетов.Международный.РезервыНаПереоценкуОСиИС;
	БухИтогиР.РассчитатьИтоги("Международный", "КонечныйОстатокДт,КонечныйОстатокКт", "Сумма", "Счет, Субконто1, Субконто2", Дата, Дата, , ПланыСчетов.Международный.РезервыНаПереоценкуОСиИС, МассивСубконто, , , "Организация", Организация);
		    	
	Пока ВыборкаОС.Следующий() Цикл
		
		Сумма                     = 0;
		СчетУчета                 = ВыборкаОС.СчетУчета;
		СчетЗатрат                = ВыборкаОС.СчетЗатрат;
		СчетНачисленияАмортизации = ВыборкаОС.СчетНачисленияАмортизации;
		СчетСниженияСтоимости     = ВыборкаОС.СчетСниженияСтоимости;
		ДатаУстановки             = НачалоМесяца(ВыборкаОС.ДатаПринятияКУчету);
		ОставшеесяЧислоМесяцев    = ВыборкаОС.СрокПолезногоИспользования - ((Месяц(ПериодРегистрации) + 12*Год(ПериодРегистрации)- (Месяц(ДатаУстановки) + 12*Год(ДатаУстановки)))) + 1;
		Если ВыборкаОС.МетодНачисленияАмортизации = Перечисления.МетодыНачисленияАмортизацииОСМеждународный.Линейный Тогда
			
			Если ОставшеесяЧислоМесяцев > 0 Тогда
				Сумма = (ВыборкаОС.ОстаточнаяСтоимость - ВыборкаОС.ЛиквидационнаяСтоимость) / ОставшеесяЧислоМесяцев;
			Иначе
				Сообщить("По объекту ОС '" + ВыборкаОС.ОсновноеСредство + "' амортизация не начислена:");
				Сообщить("	истек срок полезного использования");
			КонецЕсли;
			
		ИначеЕсли ВыборкаОС.МетодНачисленияАмортизации = Перечисления.МетодыНачисленияАмортизацииОСМеждународный.УменьшаемогоОстатка Тогда
			
			Сумма = ВыборкаОС.ОстаточнаяСтоимость * ВыборкаОС.КоэффициентУскорения * 12 / ВыборкаОС.СрокПолезногоИспользования / (12 - ВыборкаОС.Месяц + 1);
					
		ИначеЕсли ВыборкаОС.МетодНачисленияАмортизации = Перечисления.МетодыНачисленияАмортизацииОСМеждународный.ПропорциональноОбъемуПродукции Тогда

			Если ВыборкаОС.ПредполагаемыйОбъемПродукции = 0 Тогда
				
				Сообщить("По объекту ОС '" + ВыборкаОС.ОсновноеСредство + "' амортизация не начислена:");
				Сообщить("	в регистре сведений не указан предполагаемый объем продукции");
				
			ИначеЕсли ВыборкаОС.КоличествоВыпущеннойПродукции >	ВыборкаОС.ПредполагаемыйОбъемПродукции Тогда
				
				Сообщить("По объекту ОС '" + ВыборкаОС.ОсновноеСредство + "' амортизация не начислена:");
				Сообщить("	в регистре сведений указан предполагаемый объем продукции " + ВыборкаОС.ПредполагаемыйОбъемПродукции + ", в документе - больше: " + ВыборкаОС.КоличествоВыпущеннойПродукции);
				
			Иначе
				
				Если ВыборкаОС.МетодОценкиОсновныхСредств = Перечисления.МетодыОценкиАктивов.ПоПервоначальнойСтоимости Тогда
					Сумма = ВыборкаОС.ПервоначальнаяСтоимость * ВыборкаОС.КоличествоВыпущеннойПродукции / ВыборкаОС.ПредполагаемыйОбъемПродукции;
				ИначеЕсли ВыборкаОС.МетодОценкиОсновныхСредств = Перечисления.МетодыОценкиАктивов.ПоСправедливойСтоимости Тогда
					Сумма = ВыборкаОС.СправедливаяСтоимость * ВыборкаОС.КоличествоВыпущеннойПродукции / ВыборкаОС.ПредполагаемыйОбъемПродукции;
				Иначе
					Сообщить("В учетной политике по международному учету не указан Метод учета основных средств. ОС " + ВыборкаОС.ОсновноеСредство + " не амортизировано.", СтатусСообщения.ОченьВажное);
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЕсли;
		
		Сумма = Мин (Сумма, ВыборкаОС.ОстаточнаяСтоимость);
		Если (ЗначениеЗаполнено(СчетЗатрат)) 
			и (ЗначениеЗаполнено(СчетНачисленияАмортизации)) 
			и (Сумма <> 0) Тогда
			Движение = Движения.Международный.Добавить();
			Движение.Период = КонецМесяца(ПериодРегистрации);
			Движение.СчетДт = СчетЗатрат;
			Движение.СчетКт = СчетНачисленияАмортизации;
			Движение.Организация = Организация;
			Движение.Сумма = Сумма;
			Движение.Содержание = "Амортизация ОС";
			Движение.НомерЖурнала = "Рег";
			
			СтатьяЗатрат = Справочники.СтатьиЗатрат.ПустаяСсылка();
			НоменклатурнаяГруппа = Справочники.НоменклатурныеГруппы.ПустаяСсылка();
			
			Для Ном = 1 по Движение.СчетДт.ВидыСубконто.Количество() Цикл
				ЗначениеСубконто = ВыборкаОС["Субконто" + Ном];
				Движение.СубконтоДт[Движение.СчетДт.ВидыСубконто[Ном-1].ВидСубконто] = ЗначениеСубконто;
				
				Если ТипЗнч(ЗначениеСубконто) = Тип("СправочникСсылка.СтатьиЗатрат") Тогда
					СтатьяЗатрат = ЗначениеСубконто;
				ИначеЕсли ТипЗнч(ЗначениеСубконто) = Тип("СправочникСсылка.НоменклатурныеГруппы") Тогда
					НоменклатурнаяГруппа = ЗначениеСубконто;
				КонецЕсли;
			КонецЦикла;
			
			Если не Движение.СчетКт.ВидыСубконто.Найти(ПланыВидовХарактеристик.ВидыСубконтоМеждународные.ОсновныеСредства,) = Неопределено Тогда
				Движение.СубконтоКт[ПланыВидовХарактеристик.ВидыСубконтоМеждународные.ОсновныеСредства] = ВыборкаОС.ОсновноеСредство;
			КонецЕсли;
			
			// если есть резерв переоценки по данному объекту - списываем его часть на нераспределенную прибыль
			// пропорционально начисленной амортизации
			СуммаРезерва = БухИтогиР.ПолучитьИтог("СуммаКонечныйОстатокКт", "Субконто1, Субконто2", ВыборкаОС.ОсновноеСредство, Резервы);
			Если СуммаРезерва>0 Тогда
				//вычисляем сумму списания резерва
				СумммаСписания =  СуммаРезерва*Сумма/ВыборкаОС.ОстаточнаяСтоимость;
				//если стоимость полностью амортизирована, резерв должен быть списан полностью
				Если Сумма = ВыборкаОС.ОстаточнаяСтоимость Тогда
					СумммаСписания =  СуммаРезерва;  // чтобы не было остатков резерва из-за округления при расчете
				КонецЕсли;	
				//и делаем проводку по списанию резерва
				Движение = Движения.Международный.Добавить();
				Движение.Период = КонецМесяца(ПериодРегистрации);
				Движение.Организация = Организация;
				Движение.СчетДт = ПланыСчетов.Международный.РезервыНаПереоценкуОСиИС;
				Движение.СчетКт = ПланыСчетов.Международный.НераспределеннаяПрибыль;
				УстановитьСубконтоПроводки(Движение.СубконтоДт, Движение.СчетДт, ПланыВидовХарактеристик.ВидыСубконтоМеждународные.ОсновныеСредства, ВыборкаОС.ОсновноеСредство);
				УстановитьСубконтоПроводки(Движение.СубконтоДт, Движение.СчетДт, ПланыВидовХарактеристик.ВидыСубконтоМеждународные.Резервы, Справочники.ВидыФинРезервов.РезервыНаПереоценку);
				Движение.Сумма = СумммаСписания;
				Движение.Содержание = "Списание резерва переоценки ОС";
				Движение.НомерЖурнала = "Рег";
			КонецЕсли;	
			
			// регистр ОсновныеСредстваМеждународныйУчет 
			Движение = Движения.ОсновныеСредстваМеждународныйУчет.Добавить();
			Движение.Период = КонецМесяца(ПериодРегистрации);
			Движение.ОсновноеСредство = ВыборкаОС.ОсновноеСредство;
			Движение.Организация = Организация;
			Движение.ДатаПринятияКУчету = ВыборкаОС.ДатаПринятияКУчету;
			Движение.МестонахождениеОбъекта = ВыборкаОС.МестонахождениеОбъекта;
			Движение.МОЛ = ВыборкаОС.МОЛ;
			Движение.СчетУчета = ВыборкаОС.СчетУчета;
			Движение.СрокПолезногоИспользования = ВыборкаОС.СрокПолезногоИспользования;
			Движение.НачислятьАмортизацию = ВыборкаОС.НачислятьАмортизацию;
			Движение.МетодНачисленияАмортизации = ВыборкаОС.МетодНачисленияАмортизации;
			Движение.СчетНачисленияАмортизации = ВыборкаОС.СчетНачисленияАмортизации;
			Движение.ПервоначальнаяСтоимость = ВыборкаОС.ПервоначальнаяСтоимость;
			Движение.ЛиквидационнаяСтоимость = ВыборкаОС.ЛиквидационнаяСтоимость;
			Движение.СчетЗатрат = ВыборкаОС.СчетЗатрат;
			Движение.Субконто1 = ВыборкаОС.Субконто1;
			Движение.Субконто2 = ВыборкаОС.Субконто2;
			Движение.Субконто3 = ВыборкаОС.Субконто3;
			Движение.ПредполагаемыйОбъемПродукции = ВыборкаОС.ПредполагаемыйОбъемПродукции;
			Движение.СуммаНачисленнойАмортизации = ВыборкаОС.СуммаНачисленнойАмортизации + Сумма;
			Движение.СправедливаяСтоимость = ВыборкаОС.СправедливаяСтоимость;
			Движение.Состояние = ВыборкаОС.Состояние;
			Движение.СчетСниженияСтоимости = ВыборкаОС.СчетСниженияСтоимости;
			Движение.КоэффициентУскорения = ВыборкаОС.КоэффициентУскорения;

			
			Если СчетЗатрат = Счета.ОсновноеПроизводство или СчетЗатрат = Счета.ВспомогательноеПроизводство или СчетЗатрат = Счета.ОбслуживающиеПроизводства Тогда
				// НезавершенноеПроизводствоМеждународныйУчет
				Движение = Движения.НезавершенноеПроизводствоМеждународныйУчет.Добавить();
				Движение.Период = КонецМесяца(ПериодРегистрации);
				Движение.Организация = Организация;
				Движение.Подразделение = ВыборкаОС.МестонахождениеОбъекта;
				Движение.СчетУчета = СчетЗатрат;
				Движение.СтатьяЗатрат = СтатьяЗатрат;
				Движение.НоменклатурнаяГруппа = НоменклатурнаяГруппа;
				Движение.Стоимость = Сумма;
				Движение.КодОперации = Перечисления.КодыОперацийНезавершенноеПроизводство.СписаниеПрочихЗатратВПроизводство;
				
			ИначеЕсли СчетЗатрат = Счета.ОбщепроизводственныеРасходы Тогда
				// ЗатратыМеждународныйУчет
				Движение = Движения.ЗатратыМеждународныйУчет.Добавить();
				Движение.Период = КонецМесяца(ПериодРегистрации);
				Движение.Организация = Организация;
				Движение.Подразделение = ВыборкаОС.МестонахождениеОбъекта;
				Движение.СчетУчета = СчетЗатрат;
				Движение.СтатьяЗатрат = СтатьяЗатрат;
				Движение.НоменклатурнаяГруппа = НоменклатурнаяГруппа;
				Движение.Сумма = Сумма;
				Движение.КодОперации = Перечисления.КодыОперацийЗатраты.ПрочиеПостоянныеЗатраты;

			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	Если ОбменДанными.Загрузка  Тогда
		Возврат;
	КонецЕсли;


	 
	мУдалятьДвижения = НЕ ЭтоНовый();
	
КонецПроцедуры // ПередЗаписью