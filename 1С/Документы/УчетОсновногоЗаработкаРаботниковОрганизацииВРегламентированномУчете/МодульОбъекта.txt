////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ МОДУЛЯ

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

Процедура ЗаполнитьДокумент(Основание) Экспорт
	
	Если ТипЗнч(Основание) = Тип("ДокументСсылка.ПриемНаРаботуВОрганизацию") Тогда
		
		ЗаполнениеДокументов.ЗаполнитьШапкуДокументаПоОснованию(ЭтотОбъект,Основание);
		
		ОтражаемыйДокумент = Основание;
		
		ЗапросДляЗаполнения = Новый Запрос;
		
		ЗапросДляЗаполнения.УстановитьПараметр("ДокументСсылка",	Основание);
		
		ЗапросДляЗаполнения.Текст =
		"ВЫБРАТЬ
		|	ПринятыеРаботники.Сотрудник,
		|	ПринятыеРаботники.ДатаПриема КАК ДатаИзменения
		|ИЗ
		|	Документ.ПриемНаРаботуВОрганизацию.РаботникиОрганизации КАК ПринятыеРаботники
		|ГДЕ
		|	ПринятыеРаботники.Ссылка = &ДокументСсылка";
		
		РаботникиОрганизации.Загрузить(ЗапросДляЗаполнения.Выполнить().Выгрузить());
		
	ИначеЕсли ТипЗнч(Основание) = Тип("ДокументСсылка.КадровоеПеремещениеОрганизаций") Тогда
		
		ЗаполнениеДокументов.ЗаполнитьШапкуДокументаПоОснованию(ЭтотОбъект,Основание);
		
		ОтражаемыйДокумент = Основание;
		
		ЗапросДляЗаполнения = Новый Запрос;
		
		ЗапросДляЗаполнения.УстановитьПараметр("ДокументСсылка",	Основание);
		
		ЗапросДляЗаполнения.Текст =
		"ВЫБРАТЬ
		|	КадровоеПеремещение.Сотрудник,
		|	КадровоеПеремещение.ДатаНачала КАК ДатаИзменения
		|ИЗ
		|	Документ.КадровоеПеремещениеОрганизаций.РаботникиОрганизации КАК КадровоеПеремещение
		|ГДЕ
		|	КадровоеПеремещение.Ссылка = &ДокументСсылка";
		
		РаботникиОрганизации.Загрузить(ЗапросДляЗаполнения.Выполнить().Выгрузить());
		
	КонецЕсли;
	
КонецПроцедуры // ЗаполнитьДокумент()


////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ДЛЯ ОБЕСПЕЧЕНИЯ ПРОВЕДЕНИЯ ДОКУМЕНТА

// Формирует запрос по шапке документа
//
// Параметры:
//	Режим	- режим проведения
//
// Возвращаемое значение:
//	Результат запроса
//
Функция СформироватьЗапросПоШапке(Режим)

	Запрос = Новый Запрос;

	// Установим параметры запроса
	Запрос.УстановитьПараметр("ДокументСсылка" , Ссылка);

	Запрос.Текст =
	"ВЫБРАТЬ
	|	УчетОсновногоЗаработкаРаботниковОрганизацииВРегламентированномУчете.Дата,
	|	УчетОсновногоЗаработкаРаботниковОрганизацииВРегламентированномУчете.Организация КАК ОбособленноеПодразделение,
	|	ВЫБОР
	|		КОГДА УчетОсновногоЗаработкаРаботниковОрганизацииВРегламентированномУчете.Организация.ГоловнаяОрганизация = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|			ТОГДА УчетОсновногоЗаработкаРаботниковОрганизацииВРегламентированномУчете.Организация
	|		ИНАЧЕ УчетОсновногоЗаработкаРаботниковОрганизацииВРегламентированномУчете.Организация.ГоловнаяОрганизация
	|	КОНЕЦ КАК Организация,
	|	УчетОсновногоЗаработкаРаботниковОрганизацииВРегламентированномУчете.Ссылка
	|ИЗ
	|	Документ.УчетОсновногоЗаработкаРаботниковОрганизацииВРегламентированномУчете КАК УчетОсновногоЗаработкаРаботниковОрганизацииВРегламентированномУчете
	|ГДЕ
	|	УчетОсновногоЗаработкаРаботниковОрганизацииВРегламентированномУчете.Ссылка = &ДокументСсылка";

	Возврат Запрос.Выполнить();

КонецФункции // СформироватьЗапросПоШапке()

// Формирует запрос по таблице "РаботникиОрганизации" документа
//
// Параметры:
//	Режим - режим проведения
//
// Возвращаемое значение:
//	Результат запроса.
//
Функция СформироватьЗапросПоРаботникиОрганизации(ВыборкаПоШапкеДокумента)

	Запрос = Новый Запрос;

	// Установим параметры запроса
	Запрос.УстановитьПараметр("ДокументСсылка", Ссылка);
	Запрос.УстановитьПараметр("ГоловнаяОрганизация", ВыборкаПоШапкеДокумента.Организация);

	// Описание текста запроса:
	// 1. Выборка "ТЧРаботникиОрганизации": 
	//		Выбираются строки документа.  
	// 2. Выборка "ПересекающиесяСтроки": 
	//		Среди остальных строк документа ищем строки с одинаковым значением реквизита "Сотрудник"
	// 3. Выборка "ПоследующиеДвижение": 
	//		В регистре УчетОсновногоЗаработкаРаботниковОрганизацииВРегламентированномУчете 
	//		ищем движения, следующие за теми, которые мы собираемся записать в этот же регистр - 
	//		затем проверим, нет ли уже движения на ту дату, которая указана в документе
	//
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТЧРаботникиОрганизации.НомерСтроки КАК НомерСтроки,
	|	ТЧРаботникиОрганизации.Сотрудник,
	|	ТЧРаботникиОрганизации.Сотрудник.Наименование,
	|	ТЧРаботникиОрганизации.ДатаИзменения КАК Период,
	|	ТЧРаботникиОрганизации.СпособОтраженияВБухучете,
	|	ТЧРаботникиОрганизации.ПодпадаетПодЕНВД,
	|	ТЧРаботникиОрганизации.СпособОтраженияВБухучетеБольничногоЗаСчетРаботодателя,
	|	ПересекающиесяСтроки.КонфликтнаяСтрокаНомер,
	|	ПоследующиеДвижение.Регистратор,
	|	ПоследующиеДвижение.РегистраторПредставление,
	|	ВЫБОР
	|		КОГДА ТЧРаботникиОрганизации.Сотрудник.Организация = &ГоловнаяОрганизация
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК ОшибкаНеСоответствиеСотрудникаИОрганизации
	|ИЗ
	|	Документ.УчетОсновногоЗаработкаРаботниковОрганизацииВРегламентированномУчете.РаботникиОрганизации КАК ТЧРаботникиОрганизации
	|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|			ТЧРаботникиОрганизации.НомерСтроки КАК НомерСтроки,
	|			МИНИМУМ(ТЧРаботникиОрганизации2.НомерСтроки) КАК КонфликтнаяСтрокаНомер
	|		ИЗ
	|			Документ.УчетОсновногоЗаработкаРаботниковОрганизацииВРегламентированномУчете.РаботникиОрганизации КАК ТЧРаботникиОрганизации
	|				ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.УчетОсновногоЗаработкаРаботниковОрганизацииВРегламентированномУчете.РаботникиОрганизации КАК ТЧРаботникиОрганизации2
	|				ПО (ТЧРаботникиОрганизации2.Ссылка = &ДокументСсылка)
	|					И ТЧРаботникиОрганизации.НомерСтроки > ТЧРаботникиОрганизации2.НомерСтроки
	|					И ТЧРаботникиОрганизации.ДатаИзменения = ТЧРаботникиОрганизации2.ДатаИзменения
	|					И ТЧРаботникиОрганизации.Сотрудник = ТЧРаботникиОрганизации2.Сотрудник
	|		ГДЕ
	|			ТЧРаботникиОрганизации.Ссылка = &ДокументСсылка
	|		
	|		СГРУППИРОВАТЬ ПО
	|			ТЧРаботникиОрганизации.НомерСтроки) КАК ПересекающиесяСтроки
	|		ПО ТЧРаботникиОрганизации.НомерСтроки = ПересекающиесяСтроки.НомерСтроки
	|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|			МИНИМУМ(УчетОсновногоЗаработкаРаботниковОрганизацииВРегламентированномУчете.Период) КАК ДатаПоследующегоДвижения,
	|			ТЧДокумента.Сотрудник КАК Сотрудник,
	|			УчетОсновногоЗаработкаРаботниковОрганизацииВРегламентированномУчете.Регистратор КАК Регистратор,
	|			ПРЕДСТАВЛЕНИЕ(УчетОсновногоЗаработкаРаботниковОрганизацииВРегламентированномУчете.Регистратор) КАК РегистраторПредставление
	|		ИЗ
	|			Документ.УчетОсновногоЗаработкаРаботниковОрганизацииВРегламентированномУчете.РаботникиОрганизации КАК ТЧДокумента
	|				ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.УчетОсновногоЗаработкаРаботниковОрганизацииВРегламентированномУчете КАК УчетОсновногоЗаработкаРаботниковОрганизацииВРегламентированномУчете
	|				ПО ТЧДокумента.ДатаИзменения <= УчетОсновногоЗаработкаРаботниковОрганизацииВРегламентированномУчете.Период
	|					И ТЧДокумента.Сотрудник = УчетОсновногоЗаработкаРаботниковОрганизацииВРегламентированномУчете.Сотрудник
	|		ГДЕ
	|			ТЧДокумента.Ссылка = &ДокументСсылка
	|		
	|		СГРУППИРОВАТЬ ПО
	|			УчетОсновногоЗаработкаРаботниковОрганизацииВРегламентированномУчете.Регистратор,
	|			ПРЕДСТАВЛЕНИЕ(УчетОсновногоЗаработкаРаботниковОрганизацииВРегламентированномУчете.Регистратор),
	|			ТЧДокумента.Сотрудник) КАК ПоследующиеДвижение
	|		ПО ТЧРаботникиОрганизации.ДатаИзменения = ПоследующиеДвижение.ДатаПоследующегоДвижения
	|			И ТЧРаботникиОрганизации.Сотрудник = ПоследующиеДвижение.Сотрудник
	|ГДЕ
	|	ТЧРаботникиОрганизации.Ссылка = &ДокументСсылка";
	
	Возврат Запрос.Выполнить();

КонецФункции // СформироватьЗапросПоРаботникиОрганизации()

// Проверяет правильность заполнения шапки документа.
// Если какой-то из реквизитов шапки, влияющий на проведение не заполнен или
// заполнен не корректно, то выставляется флаг отказа в проведении.
// Проверка выполняется по выборке из результата запроса по шапке,
// все проверяемые реквизиты должны быть включены в выборку по шапке.
//
// Параметры: 
//	ВыборкаПоШапкеДокумента	- выборка из результата запроса по шапке документа,
//	Отказ					- флаг отказа в проведении,
//	Заголовок				- Заголовок для сообщений об ошибках проведения.
//
Процедура ПроверитьЗаполнениеШапки(ВыборкаПоШапкеДокумента, Отказ, Заголовок)

	Если НЕ ЗначениеЗаполнено(ВыборкаПоШапкеДокумента.ОбособленноеПодразделение) Тогда
		ОбщегоНазначения.ВывестиИнформациюОбОшибке(ОбщегоНазначения.ПреобразоватьСтрокуИнтерфейса("Не указана организация, для которой ведется учет!"), Отказ, Заголовок);
	КонецЕсли;

КонецПроцедуры // ПроверитьЗаполнениеШапки()

// Проверяет правильность заполнения реквизитов в строке ТЧ "РаботникиОрганизации" документа.
// Если какой-то из реквизитов, влияющий на проведение не заполнен или
// заполнен не корректно, то выставляется флаг отказа в проведении.
// Проверка выполняется по выборке из результата запроса по строке ТЧ документа,
// все проверяемые реквизиты должны быть включены в выборку.
//
// Параметры:
//	ВыборкаПоШапкеДокумента		- выборка из результата запроса по шапке документа,
//	ВыборкаПоСтрокамДокумента	- спозиционированная на определенной строке выборка 
//								  из результата запроса по товарам документа, 
//	Отказ 						- флаг отказа в проведении.
//
Процедура ПроверитьЗаполнениеСтрокиРаботникаОрганизации(ВыборкаПоШапкеДокумента, ВыборкаПоСтрокамДокумента, Отказ, Заголовок)

	СтрокаНачалаСообщенияОбОшибке = "В строке номер """+ СокрЛП(ВыборкаПоСтрокамДокумента.НомерСтроки) +
									глЗначениеПеременной("глКоллекцияЗначенийЗаголовков")[""" табл. части ""сотрудники организации"": "];

	// Сотрудник
	Если НЕ ЗначениеЗаполнено(ВыборкаПоСтрокамДокумента.Сотрудник) Тогда
		ОбщегоНазначения.ВывестиИнформациюОбОшибке(СтрокаНачалаСообщенияОбОшибке + "не выбран сотрудник!", Отказ, Заголовок);
	КонецЕсли;
	
	// Организация сотрудника должна совпадать с организацией документа
	Если ВыборкаПоСтрокамДокумента.ОшибкаНеСоответствиеСотрудникаИОрганизации Тогда
		ОбщегоНазначения.ВывестиИнформациюОбОшибке(СтрокаНачалаСообщенияОбОшибке + ОбщегоНазначения.ПреобразоватьСтрокуИнтерфейса("указанный сотрудник оформлен на другую организацию!"), Отказ, Заголовок);
	КонецЕсли;
	
	// Дата
	Если НЕ ЗначениеЗаполнено(ВыборкаПоСтрокамДокумента.Период) Тогда
		ОбщегоНазначения.ВывестиИнформациюОбОшибке(СтрокаНачалаСообщенияОбОшибке + "не указана дата изменения учета сотрудника!", Отказ, Заголовок);
	КонецЕсли;
	
	//// СпособОтраженияВБухучете
	//Если НЕ ЗначениеЗаполнено(ВыборкаПоСтрокамДокумента.СпособОтраженияВБухучете)  Тогда
	//	//ОбщегоНазначения.ВывестиИнформациюОбОшибке(СтрокаНачалаСообщенияОбОшибке + "не указан способ отражения заработка работника в бухгалтерском учете!", Отказ);
	//	Сообщить(СтрокаНачалаСообщенияОбОшибке + "не указан способ отражения заработка работника в бухгалтерском учете!
	//	|Движение по этой строке производиться не будет.")
	//КонецЕсли;
	
	// повторяющиеся строки
	Если ЗначениеЗаполнено(ВыборкаПоСтрокамДокумента.КонфликтнаяСтрокаНомер)  Тогда
		ОбщегоНазначения.ВывестиИнформациюОбОшибке(СтрокаНачалаСообщенияОбОшибке + "по сотруднику " + ВыборкаПоСтрокамДокумента.СотрудникНаименование + " найдена повторяющаяся строка № " + ВыборкаПоСтрокамДокумента.КонфликтнаяСтрокаНомер + "!", Отказ, Заголовок);
	КонецЕсли;
	
	// повторяющиеся строки
	Если ЗначениеЗаполнено(ВыборкаПоСтрокамДокумента.РегистраторПредставление)  Тогда
		Расшифровки = Новый Массив;
		Расшифровки.Добавить(Новый Структура("Представление, Расшифровка", ВыборкаПоСтрокамДокумента.РегистраторПредставление, ВыборкаПоСтрокамДокумента.Регистратор));
		ОбщегоНазначения.ВывестиИнформациюОбОшибке(СтрокаНачалаСообщенияОбОшибке + "способ учета основного заработка сотрудника " + ВыборкаПоСтрокамДокумента.СотрудникНаименование + " уже зарегистрирован ранее документом ", Отказ, Заголовок, , Расшифровки);
	КонецЕсли;
	
КонецПроцедуры // ПроверитьЗаполнениеСтрокиРаботникаОрганизации()

// По строке выборки результата запроса по документу формируем движения по регистрам
//
// Параметры:
//	ВыборкаПоШапкеДокумента					- выборка из результата запроса по шапке документа,
//	СтруктураПроведенияПоРегистрамСведений	- структура, содержащая имена регистров 
//											  сведений по которым надо проводить документ,
//	СтруктураПараметров						- структура параметров проведения,
//
// Возвращаемое значение:
//	Нет.
//
Процедура ДобавитьСтрокуВДвиженияПоРегистрамСведений(ВыборкаПоШапкеДокумента, ВыборкаПоРаботникиОрганизации)

	СтрокаСвойствИзДокумента = "Организация";
	СтрокаСвойствИзСтрокиДокумента = "Период,Сотрудник,СпособОтраженияВБухучете,СпособОтраженияВБухучетеБольничногоЗаСчетРаботодателя,ПодпадаетПодЕНВД";
							
	//Движения по регистру "УчетОсновногоЗаработкаРаботниковОрганизацииВРегламентированномУчете"
	Движение = Движения.УчетОсновногоЗаработкаРаботниковОрганизацииВРегламентированномУчете.Добавить();
	
	ЗаполнитьЗначенияСвойств(Движение,ВыборкаПоШапкеДокумента,СтрокаСвойствИзДокумента);
	ЗаполнитьЗначенияСвойств(Движение,ВыборкаПоРаботникиОрганизации,СтрокаСвойствИзСтрокиДокумента);

КонецПроцедуры // ДобавитьСтрокуВДвиженияПоРегистрамСведений()

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ

Процедура ОбработкаПроведения(Отказ, Режим)

	ОбработкаКомментариев = глЗначениеПеременной("глОбработкаСообщений");
	ОбработкаКомментариев.УдалитьСообщения();
	
	// Заголовок для сообщений об ошибках проведения.
	Заголовок = ОбщегоНазначения.ПредставлениеДокументаПриПроведении(Ссылка);

	РезультатЗапросаПоШапке = СформироватьЗапросПоШапке(Режим);

	// Получим реквизиты шапки из запроса
	ВыборкаПоШапкеДокумента = РезультатЗапросаПоШапке.Выбрать();

	Если ВыборкаПоШапкеДокумента.Следующий() Тогда

		//Надо позвать проверку заполнения реквизитов шапки
		ПроверитьЗаполнениеШапки(ВыборкаПоШапкеДокумента, Отказ, Заголовок);

		// Движения стоит добавлять, если в проведении еще не отказано (отказ =ложь)
		Если НЕ Отказ Тогда
			
			// выполним выборку по табличной части документа
			РезультатЗапросаПоРаботники	= СформироватьЗапросПоРаботникиОрганизации(ВыборкаПоШапкеДокумента);
			ВыборкаСтрокЗапроса			= РезультатЗапросаПоРаботники.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			
			// обходим строки запроса, проверяем данные и формируем движения
			Пока ВыборкаСтрокЗапроса.Следующий() Цикл
				
				ПроверитьЗаполнениеСтрокиРаботникаОрганизации(ВыборкаПоШапкеДокумента, ВыборкаСтрокЗапроса , Отказ, Заголовок);
				Если НЕ Отказ Тогда
					
					//Если ЗначениеЗаполнено(ВыборкаСтрокЗапроса.СпособОтраженияВБухучете) Тогда // Движения с пустым способом отражения не формировать
						ДобавитьСтрокуВДвиженияПоРегистрамСведений(ВыборкаПоШапкеДокумента, ВыборкаСтрокЗапроса);
					//КонецЕсли;
					
				КонецЕсли;
				
			КонецЦикла;
			
		КонецЕсли;

	КонецЕсли;

	ОбработкаКомментариев.ПоказатьСообщения();
	
КонецПроцедуры // ОбработкаПроведения()

Процедура ОбработкаЗаполнения(Основание)
	
	Если	ТипЗнч(Основание) = Тип("ДокументСсылка.КадровоеПеремещениеОрганизаций")
		ИЛИ	ТипЗнч(Основание) = Тип("ДокументСсылка.ПриемНаРаботуВОрганизацию") Тогда
		
		// Заполним реквизиты из стандартного набора по документу основанию.
		ЗаполнениеДокументов.ЗаполнитьШапкуДокументаПоОснованию(ЭтотОбъект, Основание);
		
		Если Основание.Проведен Тогда
			
			ОтражаемыйДокумент = Основание;
			ЭтоКадровоеПеремещение = Ложь;
			Если ТипЗнч(Основание) = Тип("ДокументСсылка.ПриемНаРаботуВОрганизацию") Тогда
				РеквизитДатаС	= "ДатаПриема";
				
			ИначеЕсли ТипЗнч(Основание) = Тип("ДокументСсылка.КадровоеПеремещениеОрганизаций") Тогда
				ЭтоКадровоеПеремещение = Истина;
				РеквизитДатаС	= "ДатаНачала";	
				РеквизитДатаПо	= "ДатаОкончания";
				
			ИначеЕсли ТипЗнч(Основание) = Тип("ДокументСсылка.ВозвратНаРаботуОрганизаций") Тогда
				РеквизитДатаС	= "ДатаВозврата";
				
			Иначе
				Возврат;
				
			КонецЕсли;
			
			Для Каждого  СтрокаОснования Из Основание.РаботникиОрганизации Цикл
				
				// Для даты начала
				НоваяСтрока	= РаботникиОрганизации.Добавить();
				НоваяСтрока.Сотрудник		= СтрокаОснования.Сотрудник;
				НоваяСтрока.ФизЛицо			= СтрокаОснования.ФизЛицо;
				НоваяСтрока.ДатаИзменения	= СтрокаОснования[РеквизитДатаС];
				
				// Для даты окончания
				Если ЭтоКадровоеПеремещение И Не СтрокаОснования.НапомнитьПоЗавершении И ЗначениеЗаполнено(СтрокаОснования.ДатаОкончания) Тогда
					НоваяСтрока = РаботникиОрганизации.Добавить();
					НоваяСтрока.Сотрудник		= СтрокаОснования.Сотрудник;
					НоваяСтрока.ФизЛицо			= СтрокаОснования.ФизЛицо;
					НоваяСтрока.ДатаИзменения	= СтрокаОснования.ДатаОкончания;
				КонецЕсли;
				
			КонецЦикла;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры // ОбработкаЗаполнения()

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	МассивТЧ = Новый Массив();
	МассивТЧ.Добавить(РаботникиОрганизации);
	
	КраткийСоставДокумента = ПроцедурыУправленияПерсоналом.ЗаполнитьКраткийСоставДокумента(МассивТЧ);
	
КонецПроцедуры // ПередЗаписью()

////////////////////////////////////////////////////////////////////////////////
// ОПЕРАТОРЫ ОСНОВНОЙ ПРОГРАММЫ