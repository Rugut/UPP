////////////////////////////////////////////////////////////////////////////////
// ВСПОМОГАТЕЛЬНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

Процедура ЗаполнитьДанныеБольничного(ТекущаяСтрока = Неопределено)

	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	НачислениеПоБольничномуЛисту.Ссылка,
	|	НачислениеПоБольничномуЛисту.Сотрудник,
	|	НачислениеПоБольничномуЛисту.ДатаНачалаСобытия
	|ПОМЕСТИТЬ ВТДокументыЗаполнения
	|ИЗ
	|	Документ.НачислениеПоБольничномуЛисту КАК НачислениеПоБольничномуЛисту
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РеестрСведенийВФССОПособияхПоНетрудоспособности.РаботникиОрганизации КАК РеестрСведенийВФССОПособияхПоНетрудоспособностиРаботникиОрганизации
	|		ПО НачислениеПоБольничномуЛисту.Ссылка = РеестрСведенийВФССОПособияхПоНетрудоспособностиРаботникиОрганизации.ЛистокНетрудоспособности
	|			И (РеестрСведенийВФССОПособияхПоНетрудоспособностиРаботникиОрганизации.Ссылка <> &ТекущийРеестр)
	|			И (РеестрСведенийВФССОПособияхПоНетрудоспособностиРаботникиОрганизации.Ссылка.Проведен)
	|ГДЕ
	|	НачислениеПоБольничномуЛисту.Проведен
	|	И НачислениеПоБольничномуЛисту.ПериодРегистрации >= &ДатаПередачиФССВыплатыПособий
	|	И &ЗаполнятьВсемиДокументами
	|	И НачислениеПоБольничномуЛисту.Организация = &Организация
	|	И НачислениеПоБольничномуЛисту.ПричинаНетрудоспособности <> ЗНАЧЕНИЕ(Перечисление.ПричиныНетрудоспособности.ТравмаНаПроизводстве)
	|	И (НачислениеПоБольничномуЛисту.ПричинаНетрудоспособности <> ЗНАЧЕНИЕ(Перечисление.ПричиныНетрудоспособности.ОбщееЗаболевание)
	|			ИЛИ ДОБАВИТЬКДАТЕ(НачислениеПоБольничномуЛисту.ДатаНачалаСобытия, ДЕНЬ, НачислениеПоБольничномуЛисту.ДнейОплатыРаботодателем) <= ДОБАВИТЬКДАТЕ(НачислениеПоБольничномуЛисту.ДатаНачалаОплаты, ДЕНЬ, НачислениеПоБольничномуЛисту.ДнейОплаты - 1))
	|	И РеестрСведенийВФССОПособияхПоНетрудоспособностиРаботникиОрганизации.Сотрудник ЕСТЬ NULL 
	|	И (НачислениеПоБольничномуЛисту.ПерерассчитываемыйДокумент = ЗНАЧЕНИЕ(Документ.НачислениеПоБольничномуЛисту.ПустаяСсылка)
	|			ИЛИ НачислениеПоБольничномуЛисту.ПерерассчитываемыйДокумент.ПериодРегистрации >= &ДатаПередачиФССВыплатыПособий)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	НачислениеПоБольничномуЛисту.Ссылка,
	|	НачислениеПоБольничномуЛисту.Сотрудник,
	|	НачислениеПоБольничномуЛисту.ДатаНачалаСобытия
	|ИЗ
	|	Документ.НачислениеПоБольничномуЛисту КАК НачислениеПоБольничномуЛисту
	|ГДЕ
	|	НачислениеПоБольничномуЛисту.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА РаботникиОрганизаций.ПериодЗавершения <= ДатыРегистра.ДатаНачалаСобытия
	|				И РаботникиОрганизаций.ПериодЗавершения <> ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|			ТОГДА ВЫБОР
	|					КОГДА РаботникиОрганизаций.ГрафикРаботыЗавершения.СокращенноеРабочееВремя
	|							И РаботникиОрганизаций.ГрафикРаботыЗавершения.ГрафикПолногоРабочегоВремени <> ЗНАЧЕНИЕ(Справочник.ГрафикиРаботы.ПустаяСсылка)
	|							И РаботникиОрганизаций.ГрафикРаботыЗавершения.ГрафикПолногоРабочегоВремени.ДлительностьРабочейНедели <> 0
	|						ТОГДА РаботникиОрганизаций.ГрафикРаботыЗавершения.ДлительностьРабочейНедели / РаботникиОрганизаций.ГрафикРаботыЗавершения.ГрафикПолногоРабочегоВремени.ДлительностьРабочейНедели
	|					ИНАЧЕ 0
	|				КОНЕЦ
	|		КОГДА РаботникиОрганизаций.ГрафикРаботы.СокращенноеРабочееВремя
	|				И РаботникиОрганизаций.ГрафикРаботы.ГрафикПолногоРабочегоВремени <> ЗНАЧЕНИЕ(Справочник.ГрафикиРаботы.ПустаяСсылка)
	|				И РаботникиОрганизаций.ГрафикРаботы.ГрафикПолногоРабочегоВремени.ДлительностьРабочейНедели <> 0
	|			ТОГДА РаботникиОрганизаций.ГрафикРаботы.ДлительностьРабочейНедели / РаботникиОрганизаций.ГрафикРаботы.ГрафикПолногоРабочегоВремени.ДлительностьРабочейНедели
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ДоляВремени,
	|	ВЫБОР
	|		КОГДА РаботникиОрганизаций.ПериодЗавершения <= ДатыРегистра.ДатаНачалаСобытия
	|				И РаботникиОрганизаций.ПериодЗавершения <> ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|			ТОГДА ВЫБОР
	|					КОГДА РаботникиОрганизаций.ПодразделениеОрганизацииЗавершения.КодПоОКАТО <> """"
	|						ТОГДА РаботникиОрганизаций.ПодразделениеОрганизацииЗавершения.РайонныйКоэффициентРФ
	|					ИНАЧЕ РаботникиОрганизаций.ОбособленноеПодразделениеЗавершения.РайонныйКоэффициентРФ
	|				КОНЕЦ
	|		КОГДА РаботникиОрганизаций.ПодразделениеОрганизации.КодПоОКАТО <> """"
	|			ТОГДА РаботникиОрганизаций.ПодразделениеОрганизации.РайонныйКоэффициентРФ
	|		ИНАЧЕ РаботникиОрганизаций.ОбособленноеПодразделение.РайонныйКоэффициентРФ
	|	КОНЕЦ КАК РайонныйКоэффициент,
	|	ДатыРегистра.Ссылка
	|ПОМЕСТИТЬ ВТДанныеСотрудника
	|ИЗ
	|	(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		ДанныеРегистра.Ссылка КАК Ссылка,
	|		ДанныеРегистра.ДатаНачалаСобытия КАК ДатаНачалаСобытия,
	|		ДанныеРегистра.Сотрудник КАК Сотрудник,
	|		МАКСИМУМ(РаботникиОрганизаций.Период) КАК ПериодРегистра
	|	ИЗ
	|		ВТДокументыЗаполнения КАК ДанныеРегистра
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РаботникиОрганизаций КАК РаботникиОрганизаций
	|			ПО ДанныеРегистра.ДатаНачалаСобытия >= РаботникиОрганизаций.Период
	|				И ДанныеРегистра.Сотрудник = РаботникиОрганизаций.Сотрудник
	|	
	|	СГРУППИРОВАТЬ ПО
	|		ДанныеРегистра.ДатаНачалаСобытия,
	|		ДанныеРегистра.Сотрудник,
	|		ДанныеРегистра.Ссылка) КАК ДатыРегистра
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РаботникиОрганизаций КАК РаботникиОрганизаций
	|		ПО ДатыРегистра.ПериодРегистра = РаботникиОрганизаций.Период
	|			И ДатыРегистра.Сотрудник = РаботникиОрганизаций.Сотрудник
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДатыАктуальностиММОТ.ДатаНачалаСобытия,
	|	МинимальнаяОплатаТрудаРФ.Размер КАК Размер
	|ПОМЕСТИТЬ ВТРазмерММОТНаДатуДатаНачалаСобытия
	|ИЗ
	|	(ВЫБРАТЬ
	|		Основной.ДатаНачалаСобытия КАК ДатаНачалаСобытия,
	|		МАКСИМУМ(МинимальнаяОплатаТрудаРФ.Период) КАК ДатаАктуальности
	|	ИЗ
	|		ВТДокументыЗаполнения КАК Основной
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.МинимальнаяОплатаТрудаРФ КАК МинимальнаяОплатаТрудаРФ
	|			ПО Основной.ДатаНачалаСобытия >= МинимальнаяОплатаТрудаРФ.Период
	|	
	|	СГРУППИРОВАТЬ ПО
	|		Основной.ДатаНачалаСобытия) КАК ДатыАктуальностиММОТ
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.МинимальнаяОплатаТрудаРФ КАК МинимальнаяОплатаТрудаРФ
	|		ПО (МинимальнаяОплатаТрудаРФ.Период = ДатыАктуальностиММОТ.ДатаАктуальности)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НачислениеПоБольничномуЛистуУходЗаБольнымЧленомСемьи.НомерСтроки,
	|	НачислениеПоБольничномуЛистуУходЗаБольнымЧленомСемьи.ВозрастЛет,
	|	НачислениеПоБольничномуЛистуУходЗаБольнымЧленомСемьи.ВозрастМесяцев,
	|	НачислениеПоБольничномуЛистуУходЗаБольнымЧленомСемьи.РодственнаяСвязь,
	|	НачислениеПоБольничномуЛистуУходЗаБольнымЧленомСемьи.ФИО,
	|	НачислениеПоБольничномуЛистуУходЗаБольнымЧленомСемьи.Ссылка
	|ПОМЕСТИТЬ ВТУходЗаБольнымЧленомСемьи
	|ИЗ
	|	Документ.НачислениеПоБольничномуЛисту.УходЗаБольнымЧленомСемьи КАК НачислениеПоБольничномуЛистуУходЗаБольнымЧленомСемьи
	|ГДЕ
	|	НачислениеПоБольничномуЛистуУходЗаБольнымЧленомСемьи.Ссылка В
	|			(ВЫБРАТЬ
	|				ДокументыЗаполнения.Ссылка
	|			ИЗ
	|				ВТДокументыЗаполнения КАК ДокументыЗаполнения)
	|	И НачислениеПоБольничномуЛистуУходЗаБольнымЧленомСемьи.НомерСтроки < 3
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НачислениеПоБольничномуЛистуОсвобождениеОтРаботыВЛисткеНетрудоспособности.НомерСтроки,
	|	НачислениеПоБольничномуЛистуОсвобождениеОтРаботыВЛисткеНетрудоспособности.ДатаНачала,
	|	НачислениеПоБольничномуЛистуОсвобождениеОтРаботыВЛисткеНетрудоспособности.ДатаОкончания,
	|	НачислениеПоБольничномуЛистуОсвобождениеОтРаботыВЛисткеНетрудоспособности.Ссылка,
	|	НачислениеПоБольничномуЛистуОсвобождениеОтРаботыВЛисткеНетрудоспособности.ДолжностьВрача,
	|	НачислениеПоБольничномуЛистуОсвобождениеОтРаботыВЛисткеНетрудоспособности.ФИОВрача,
	|	НачислениеПоБольничномуЛистуОсвобождениеОтРаботыВЛисткеНетрудоспособности.ИдентификационныйНомерВрача,
	|	НачислениеПоБольничномуЛистуОсвобождениеОтРаботыВЛисткеНетрудоспособности.ФИОВрачаПредседателяВК,
	|	НачислениеПоБольничномуЛистуОсвобождениеОтРаботыВЛисткеНетрудоспособности.ДолжностьВрачаПредседателяВК,
	|	НачислениеПоБольничномуЛистуОсвобождениеОтРаботыВЛисткеНетрудоспособности.ИдентификационныйНомерВрачаПредседателяВК
	|ПОМЕСТИТЬ ВТОсвобождениеОтРаботы
	|ИЗ
	|	Документ.НачислениеПоБольничномуЛисту.ОсвобождениеОтРаботыВЛисткеНетрудоспособности КАК НачислениеПоБольничномуЛистуОсвобождениеОтРаботыВЛисткеНетрудоспособности
	|ГДЕ
	|	НачислениеПоБольничномуЛистуОсвобождениеОтРаботыВЛисткеНетрудоспособности.Ссылка В
	|			(ВЫБРАТЬ
	|				ДокументыЗаполнения.Ссылка
	|			ИЗ
	|				ВТДокументыЗаполнения КАК ДокументыЗаполнения)
	|	И НачислениеПоБольничномуЛистуОсвобождениеОтРаботыВЛисткеНетрудоспособности.НомерСтроки < 4
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НачислениеПоБольничномуЛистуРасчетСреднего2011.РасчетныйГод,
	|	СУММА(НачислениеПоБольничномуЛистуРасчетСреднего2011.Заработок) КАК Заработок,
	|	НачислениеПоБольничномуЛистуРасчетСреднего2011.Ссылка
	|ПОМЕСТИТЬ ВТРасчет2011
	|ИЗ
	|	Документ.НачислениеПоБольничномуЛисту.РасчетСреднего2011 КАК НачислениеПоБольничномуЛистуРасчетСреднего2011
	|ГДЕ
	|	НачислениеПоБольничномуЛистуРасчетСреднего2011.Ссылка В
	|			(ВЫБРАТЬ
	|				ДокументыЗаполнения.Ссылка
	|			ИЗ
	|				ВТДокументыЗаполнения КАК ДокументыЗаполнения)
	|
	|СГРУППИРОВАТЬ ПО
	|	НачислениеПоБольничномуЛистуРасчетСреднего2011.РасчетныйГод,
	|	НачислениеПоБольничномуЛистуРасчетСреднего2011.Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НачислениеПоБольничномуЛистуГодыРасчетаСреднего2011.НомерСтроки,
	|	НачислениеПоБольничномуЛистуГодыРасчетаСреднего2011.Год,
	|	НачислениеПоБольничномуЛистуГодыРасчетаСреднего2011.Ссылка
	|ПОМЕСТИТЬ ВТГоды2011
	|ИЗ
	|	Документ.НачислениеПоБольничномуЛисту.ГодыРасчетаСреднего2011 КАК НачислениеПоБольничномуЛистуГодыРасчетаСреднего2011
	|ГДЕ
	|	НачислениеПоБольничномуЛистуГодыРасчетаСреднего2011.Ссылка В
	|			(ВЫБРАТЬ
	|				ДокументыЗаполнения.Ссылка
	|			ИЗ
	|				ВТДокументыЗаполнения КАК ДокументыЗаполнения)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СУММА(ВЫБОР
	|			КОГДА НачислениеПоБольничномуЛистуРасчетСреднего.Сотрудник = НачислениеПоБольничномуЛистуРасчетСреднего.Ссылка.Сотрудник
	|				ТОГДА НачислениеПоБольничномуЛистуРасчетСреднего.ОтработаноДней
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ОтработаноДней,
	|	СУММА(НачислениеПоБольничномуЛистуРасчетСреднего.Результат) КАК Результат,
	|	НачислениеПоБольничномуЛистуРасчетСреднего.Ссылка
	|ПОМЕСТИТЬ ВТРасчет2010
	|ИЗ
	|	Документ.НачислениеПоБольничномуЛисту.РасчетСреднего КАК НачислениеПоБольничномуЛистуРасчетСреднего
	|ГДЕ
	|	НачислениеПоБольничномуЛистуРасчетСреднего.Ссылка В
	|			(ВЫБРАТЬ
	|				ДокументыЗаполнения.Ссылка
	|			ИЗ
	|				ВТДокументыЗаполнения КАК ДокументыЗаполнения)
	|
	|СГРУППИРОВАТЬ ПО
	|	НачислениеПоБольничномуЛистуРасчетСреднего.Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СУММА(РасчетСреднегоЗаработка.Результат) КАК Результат,
	|	ВЫБОР
	|		КОГДА НАЧАЛОПЕРИОДА(РасчетСреднегоЗаработка.БазовыйПериодНачало, ГОД) < &ДатаЗакона213ФЗ
	|			ТОГДА &ДатаЗакона213ФЗ
	|		ИНАЧЕ НАЧАЛОПЕРИОДА(РасчетСреднегоЗаработка.БазовыйПериодНачало, ГОД)
	|	КОНЕЦ КАК ДатаОграниченияЗаработка,
	|	РасчетСреднегоЗаработка.Регистратор,
	|	НАЧАЛОПЕРИОДА(РасчетСреднегоЗаработка.БазовыйПериодНачало, ГОД) КАК ГодЗаработка
	|ПОМЕСТИТЬ ВТДанныеРегистраСреднего2011
	|ИЗ
	|	РегистрРасчета.РасчетСреднегоЗаработка КАК РасчетСреднегоЗаработка
	|ГДЕ
	|	РасчетСреднегоЗаработка.Регистратор В
	|			(ВЫБРАТЬ
	|				ДокументыЗаполнения.Ссылка
	|			ИЗ
	|				ВТДокументыЗаполнения КАК ДокументыЗаполнения)
	|	И РасчетСреднегоЗаработка.ВидРасчета = &ВидРасчета
	|
	|СГРУППИРОВАТЬ ПО
	|	ВЫБОР
	|		КОГДА НАЧАЛОПЕРИОДА(РасчетСреднегоЗаработка.БазовыйПериодНачало, ГОД) < &ДатаЗакона213ФЗ
	|			ТОГДА &ДатаЗакона213ФЗ
	|		ИНАЧЕ НАЧАЛОПЕРИОДА(РасчетСреднегоЗаработка.БазовыйПериодНачало, ГОД)
	|	КОНЕЦ,
	|	РасчетСреднегоЗаработка.Регистратор,
	|	НАЧАЛОПЕРИОДА(РасчетСреднегоЗаработка.БазовыйПериодНачало, ГОД)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДатыРегистра.ДатаОграниченияЗаработка,
	|	ЕСТЬNULL(ПредельнаяВеличинаБазыСтраховыхВзносов.Размер, 0) КАК Размер
	|ПОМЕСТИТЬ ВТПредельнаяВеличинаЗаработкаПоГодам
	|ИЗ
	|	(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		ДанныеРегистра.ДатаОграниченияЗаработка КАК ДатаОграниченияЗаработка,
	|		МАКСИМУМ(ПредельнаяВеличинаБазыСтраховыхВзносов.Период) КАК ПериодРегистра
	|	ИЗ
	|		ВТДанныеРегистраСреднего2011 КАК ДанныеРегистра
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПредельнаяВеличинаБазыСтраховыхВзносов КАК ПредельнаяВеличинаБазыСтраховыхВзносов
	|			ПО ДанныеРегистра.ДатаОграниченияЗаработка >= ПредельнаяВеличинаБазыСтраховыхВзносов.Период
	|	
	|	СГРУППИРОВАТЬ ПО
	|		ДанныеРегистра.ДатаОграниченияЗаработка) КАК ДатыРегистра
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПредельнаяВеличинаБазыСтраховыхВзносов КАК ПредельнаяВеличинаБазыСтраховыхВзносов
	|		ПО ДатыРегистра.ПериодРегистра = ПредельнаяВеличинаБазыСтраховыхВзносов.Период
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СУММА(ВЫБОР
	|			КОГДА ДанныеРегистраСреднего.Результат <= ПредельнаяВеличинаЗаработка.Размер
	|				ТОГДА ДанныеРегистраСреднего.Результат
	|			ИНАЧЕ ПредельнаяВеличинаЗаработка.Размер
	|		КОНЕЦ) / 730 КАК Заработок,
	|	ДанныеРегистраСреднего.Регистратор
	|ПОМЕСТИТЬ ВТДанныеОСреднемЗаработке2011
	|ИЗ
	|	ВТДанныеРегистраСреднего2011 КАК ДанныеРегистраСреднего
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТПредельнаяВеличинаЗаработкаПоГодам КАК ПредельнаяВеличинаЗаработка
	|		ПО ДанныеРегистраСреднего.ДатаОграниченияЗаработка = ПредельнаяВеличинаЗаработка.ДатаОграниченияЗаработка
	|
	|СГРУППИРОВАТЬ ПО
	|	ДанныеРегистраСреднего.Регистратор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НачислениеПоБольничномуЛисту.Сотрудник КАК Сотрудник,
	|	НачислениеПоБольничномуЛисту.Физлицо,
	|	НачислениеПоБольничномуЛисту.Ссылка КАК ЛистокНетрудоспособности,
	|	4 КАК КоличествоСтраниц,
	|	НачислениеПоБольничномуЛисту.ПеречислятьПособиеПочтовымПереводом,
	|	НачислениеПоБольничномуЛисту.НаименованиеБанка,
	|	НачислениеПоБольничномуЛисту.БИКБанка,
	|	НачислениеПоБольничномуЛисту.КоррСчетБанка,
	|	НачислениеПоБольничномуЛисту.НомерЛицевогоСчета,
	|	НачислениеПоБольничномуЛисту.АдресПочтовый,
	|	НачислениеПоБольничномуЛисту.АдресРегистрации,
	|	НачислениеПоБольничномуЛисту.ДатаВыходаНаРаботу,
	|	НачислениеПоБольничномуЛисту.УстановленаИзмененаГруппаИнвалидности,
	|	НачислениеПоБольничномуЛисту.ДатаРегистрацииДокументовМСЭ,
	|	НачислениеПоБольничномуЛисту.КодНарушенияРежима,
	|	НачислениеПоБольничномуЛисту.ПериодНахожденияВСтационареСРебенкомС,
	|	НачислениеПоБольничномуЛисту.ПериодНахожденияВСтационареСРебенкомПо,
	|	НачислениеПоБольничномуЛисту.ДатаСправкиБеременной,
	|	НачислениеПоБольничномуЛисту.НомерСправкиБеременной,
	|	НачислениеПоБольничномуЛисту.ДатаИзмененияКодаПричиныНетрудоспособности,
	|	НачислениеПоБольничномуЛисту.ВторойКодПричиныНетрудоспособности,
	|	НачислениеПоБольничномуЛисту.ДополнительныйКодПричиныНетрудоспособности,
	|	НачислениеПоБольничномуЛисту.КодПричиныНетрудоспособности,
	|	НачислениеПоБольничномуЛисту.ПоставленаНаУчетВРанниеСрокиБеременности,
	|	НачислениеПоБольничномуЛисту.ПериодПростояС,
	|	НачислениеПоБольничномуЛисту.ПериодПростояПо,
	|	НачислениеПоБольничномуЛисту.ДатаДействияДокумента,
	|	НачислениеПоБольничномуЛисту.ДатаВыдачиДокумента,
	|	НачислениеПоБольничномуЛисту.ВидДокумента,
	|	НачислениеПоБольничномуЛисту.СерияДокумента,
	|	НачислениеПоБольничномуЛисту.НомерДокумента,
	|	НачислениеПоБольничномуЛисту.Фамилия КАК Фамилия,
	|	НачислениеПоБольничномуЛисту.Имя КАК Имя,
	|	НачислениеПоБольничномуЛисту.Отчество КАК Отчество,
	|	НачислениеПоБольничномуЛисту.СтажЛет,
	|	НачислениеПоБольничномуЛисту.СтажМесяцев,
	|	ВЫБОР
	|		КОГДА НачислениеПоБольничномуЛисту.ВыплатаЗаСчетФедеральногоБюджета = ЗНАЧЕНИЕ(Перечисление.ВыплатыПособийЗаСчетФедеральногоБюджета.ЗачетНестраховыхПериодов)
	|			ТОГДА НачислениеПоБольничномуЛисту.СтажБезНестраховыхПериодовЛет
	|		ИНАЧЕ НачислениеПоБольничномуЛисту.СтажЛет
	|	КОНЕЦ КАК СтажБезНестраховыхПериодовЛет,
	|	ВЫБОР
	|		КОГДА НачислениеПоБольничномуЛисту.ВыплатаЗаСчетФедеральногоБюджета = ЗНАЧЕНИЕ(Перечисление.ВыплатыПособийЗаСчетФедеральногоБюджета.ЗачетНестраховыхПериодов)
	|			ТОГДА НачислениеПоБольничномуЛисту.СтажБезНестраховыхПериодовСтажМесяцев
	|		ИНАЧЕ НачислениеПоБольничномуЛисту.СтажМесяцев
	|	КОНЕЦ КАК СтажБезНестраховыхПериодовСтажМесяцев,
	|	НачислениеПоБольничномуЛисту.УсловияИсчисленияКод1,
	|	НачислениеПоБольничномуЛисту.УсловияИсчисленияКод2,
	|	НачислениеПоБольничномуЛисту.УсловияИсчисленияКод3,
	|	ВЫБОР
	|		КОГДА НачислениеПоБольничномуЛисту.ПричинаНетрудоспособности = ЗНАЧЕНИЕ(Перечисление.ПричиныНетрудоспособности.ПоБеременностиИРодам)
	|			ТОГДА ВЫБОР
	|					КОГДА НачислениеПоБольничномуЛисту.РасчетПособияИзТарифнойСтавки
	|						ТОГДА 2
	|					ИНАЧЕ НачислениеПоБольничномуЛисту.РасчетПоПравилам2010Года
	|				КОНЕЦ
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ПорядокРасчетаСреднегоЗаработка,
	|	НачислениеПоБольничномуЛисту.НомерВходящегоДокумента,
	|	НачислениеПоБольничномуЛисту.ДатаВыдачиБольничного,
	|	НачислениеПоБольничномуЛисту.ПервичныйБольничныйЛист.НомерВходящегоДокумента КАК НомерПервичногоБольничного,
	|	ДОБАВИТЬКДАТЕ(НачислениеПоБольничномуЛисту.ДатаНачалаОплаты, ДЕНЬ, НачислениеПоБольничномуЛисту.ДнейОплаты - 1) КАК ДатаОкончанияОплаты,
	|	НачислениеПоБольничномуЛисту.ДатаНарушенияРежима,
	|	ВЫБОР
	|		КОГДА НачислениеПоБольничномуЛисту.ПричинаНетрудоспособности = ЗНАЧЕНИЕ(Перечисление.ПричиныНетрудоспособности.ОбщееЗаболевание)
	|			ТОГДА ВЫБОР
	|					КОГДА ДОБАВИТЬКДАТЕ(НачислениеПоБольничномуЛисту.ДатаНачалаСобытия, ДЕНЬ, НачислениеПоБольничномуЛисту.ДнейОплатыРаботодателем) > НачислениеПоБольничномуЛисту.ДатаНачалаОплаты
	|						ТОГДА ДОБАВИТЬКДАТЕ(НачислениеПоБольничномуЛисту.ДатаНачалаСобытия, ДЕНЬ, НачислениеПоБольничномуЛисту.ДнейОплатыРаботодателем)
	|					ИНАЧЕ НачислениеПоБольничномуЛисту.ДатаНачалаОплаты
	|				КОНЕЦ
	|		ИНАЧЕ НачислениеПоБольничномуЛисту.ДатаНачалаОплаты
	|	КОНЕЦ КАК ДатаНачалаОплаты,
	|	ПервыйГодРасчета2011.Год КАК ПервыйГодРасчета,
	|	ВторойГодРасчета2011.Год КАК ВторойГодРасчета,
	|	НачислениеПоБольничномуЛисту.КемВыданДокумент,
	|	НачислениеПоБольничномуЛисту.НовыйСтатусНетрудопособного,
	|	НачислениеПоБольничномуЛисту.ДатаНовыйСтатусНетрудопособного,
	|	НачислениеПоБольничномуЛисту.НомерЛисткаПродолжения,
	|	НачислениеПоБольничномуЛисту.ДатаНаправленияВБюроМСЭ,
	|	НачислениеПоБольничномуЛисту.ДатаОсвидетельствованияМСЭ,
	|	НачислениеПоБольничномуЛисту.ДатаОкончанияПутевки,
	|	НачислениеПоБольничномуЛисту.НомерПутевки,
	|	НачислениеПоБольничномуЛисту.ОГРН_Санатория,
	|	НачислениеПоБольничномуЛисту.НомерЛисткаПоОсновномуМестуРаботы,
	|	НачислениеПоБольничномуЛисту.АдресЛПУ,
	|	НачислениеПоБольничномуЛисту.НаименованиеЛПУ,
	|	НачислениеПоБольничномуЛисту.ОГРН_ЛПУ,
	|	НачислениеПоБольничномуЛисту.ЛПУ,
	|	НачислениеПоБольничномуЛисту.Банк,
	|	НачислениеПоБольничномуЛисту.ЯвляетсяДубликатом,
	|	ДанныеСотрудника.РайонныйКоэффициент,
	|	НачислениеПоБольничномуЛисту.ПериодРасчетаСреднегоЗаработкаНачало,
	|	НачислениеПоБольничномуЛисту.ПериодРасчетаСреднегоЗаработкаОкончание,
	|	Расчет2010.ОтработаноДней КАК КалендарныхДнейРасчетногоПериода,
	|	УходПервымЧленомСемьи.ВозрастЛет КАК УходЗаРодственникомЛет1,
	|	УходПервымЧленомСемьи.ВозрастМесяцев КАК УходЗаРодственникомМесяцев1,
	|	УходПервымЧленомСемьи.РодственнаяСвязь КАК УходЗаРодственникомРодственнаяСвязь1,
	|	УходПервымЧленомСемьи.ФИО КАК УходЗаРодственникомФИО1,
	|	УходВторымЧленомСемьи.ВозрастЛет КАК УходЗаРодственникомЛет2,
	|	УходВторымЧленомСемьи.ВозрастМесяцев КАК УходЗаРодственникомМесяцев2,
	|	УходВторымЧленомСемьи.РодственнаяСвязь КАК УходЗаРодственникомРодственнаяСвязь2,
	|	УходВторымЧленомСемьи.ФИО КАК УходЗаРодственникомФИО2,
	|	ВЫБОР
	|		КОГДА ПервыйГодРасчета2011.Год = ГОД(НачислениеПоБольничномуЛисту.ДатаНачалаСобытия) - 2
	|				И ВторойГодРасчета2011.Год = ГОД(НачислениеПоБольничномуЛисту.ДатаНачалаСобытия) - 1
	|			ТОГДА 0
	|		КОГДА ПервыйГодРасчета2011.Год <> ГОД(НачислениеПоБольничномуЛисту.ДатаНачалаСобытия) - 2
	|				И ВторойГодРасчета2011.Год <> ГОД(НачислениеПоБольничномуЛисту.ДатаНачалаСобытия) - 2
	|			ТОГДА ГОД(НачислениеПоБольничномуЛисту.ДатаНачалаСобытия) - 2
	|		ИНАЧЕ ГОД(НачислениеПоБольничномуЛисту.ДатаНачалаСобытия) - 1
	|	КОНЕЦ КАК ЗамененныйПервыйГодРасчета,
	|	ВЫБОР
	|		КОГДА ПервыйГодРасчета2011.Год = ГОД(НачислениеПоБольничномуЛисту.ДатаНачалаСобытия) - 2
	|				И ВторойГодРасчета2011.Год = ГОД(НачислениеПоБольничномуЛисту.ДатаНачалаСобытия) - 1
	|			ТОГДА 0
	|		КОГДА ПервыйГодРасчета2011.Год <> ГОД(НачислениеПоБольничномуЛисту.ДатаНачалаСобытия) - 1
	|				И ВторойГодРасчета2011.Год <> ГОД(НачислениеПоБольничномуЛисту.ДатаНачалаСобытия) - 1
	|				И ПервыйГодРасчета2011.Год <> ГОД(НачислениеПоБольничномуЛисту.ДатаНачалаСобытия) - 2
	|				И ВторойГодРасчета2011.Год <> ГОД(НачислениеПоБольничномуЛисту.ДатаНачалаСобытия) - 2
	|			ТОГДА ГОД(НачислениеПоБольничномуЛисту.ДатаНачалаСобытия) - 1
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ЗамененныйВторойГодРасчета,
	|	ОсвобождениеОтРаботы1.ДатаНачала КАК ОсвобождениеС1,
	|	ОсвобождениеОтРаботы1.ДатаОкончания КАК ОсвобождениеПо1,
	|	ОсвобождениеОтРаботы1.ДолжностьВрача КАК ДолжностьВрача1,
	|	ОсвобождениеОтРаботы1.ФИОВрача КАК ФИОВрача1,
	|	ОсвобождениеОтРаботы2.ДатаНачала КАК ОсвобождениеС2,
	|	ОсвобождениеОтРаботы2.ДатаОкончания КАК ОсвобождениеПо2,
	|	ОсвобождениеОтРаботы2.ДолжностьВрача КАК ДолжностьВрача2,
	|	ОсвобождениеОтРаботы2.ФИОВрача КАК ФИОВрача2,
	|	ОсвобождениеОтРаботы3.ДатаНачала КАК ОсвобождениеС3,
	|	ОсвобождениеОтРаботы3.ДатаОкончания КАК ОсвобождениеПо3,
	|	ОсвобождениеОтРаботы3.ДолжностьВрача КАК ДолжностьВрача3,
	|	ОсвобождениеОтРаботы3.ФИОВрача КАК ФИОВрача3,
	|	ЗаработокПервыйГод.Заработок КАК ЗаработокПервыйГодРасчета,
	|	ЗаработокВторойГод.Заработок КАК ЗаработокВторойГодРасчета,
	|	Расчет2010.Результат КАК ЗаработокЗаПериодРасчета,
	|	НачислениеПоБольничномуЛисту.Дата КАК ДатаПредставленияПакетаДокументов,
	|	ВЫБОР
	|		КОГДА НачислениеПоБольничномуЛисту.Сотрудник.ВидЗанятости = ЗНАЧЕНИЕ(Перечисление.ВидыЗанятостиВОрганизации.Совместительство)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК Совместительство,
	|	ВЫБОР
	|		КОГДА НачислениеПоБольничномуЛисту.ПричинаНетрудоспособности = ЗНАЧЕНИЕ(Перечисление.ПричиныНетрудоспособности.ПоБеременностиИРодам)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыПособийСоциальногоСтрахования.БеременностьРоды)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ВидыПособийСоциальногоСтрахования.Нетрудоспособность)
	|	КОНЕЦ КАК ВидПособия,
	|	НачислениеПоБольничномуЛисту.ДатаНачалаСобытия КАК ДатаНачалаСобытия,
	|	НачислениеПоБольничномуЛисту.ДатаНачала,
	|	НачислениеПоБольничномуЛисту.ДатаОкончания,
	|	НачислениеПоБольничномуЛисту.СерияВходящегоДокумента,
	|	НачислениеПоБольничномуЛисту.ВидРасчета,
	|	ВЫБОР
	|		КОГДА ПервыйГодРасчета2011.Год = ГОД(НачислениеПоБольничномуЛисту.ДатаНачалаСобытия) - 2
	|				И ВторойГодРасчета2011.Год = ГОД(НачислениеПоБольничномуЛисту.ДатаНачалаСобытия) - 1
	|			ТОГДА ДАТАВРЕМЯ(1, 1, 1)
	|		ИНАЧЕ НачислениеПоБольничномуЛисту.Дата
	|	КОНЕЦ КАК ДатаЗаявленияОЗаменеРасчетногоГода,
	|	МРОТнаДатуСобытия.Размер КАК МРОТ,
	|	ДанныеСотрудника.ДоляВремени,
	|	НачислениеПоБольничномуЛисту.ГруппаИнвалидности,
	|	ВЫБОР
	|		КОГДА НачислениеПоБольничномуЛисту.ПричинаНетрудоспособности <> ЗНАЧЕНИЕ(Перечисление.ПричиныНетрудоспособности.ПоБеременностиИРодам)
	|				ИЛИ НЕ НачислениеПоБольничномуЛисту.РасчетПоПравилам2010Года
	|			ТОГДА ВЫБОР
	|					КОГДА ДанныеОСреднемЗаработке2011.Заработок > (ВЫРАЗИТЬ(ЕСТЬNULL(МРОТнаДатуСобытия.Размер, 0) * ВЫБОР
	|								КОГДА ЕСТЬNULL(ДанныеСотрудника.ДоляВремени, 0) = 0
	|									ТОГДА 1
	|								ИНАЧЕ ЕСТЬNULL(ДанныеСотрудника.ДоляВремени, 0)
	|							КОНЕЦ * 24 / 730 КАК ЧИСЛО(15, 2)))
	|						ТОГДА ДанныеОСреднемЗаработке2011.Заработок
	|					ИНАЧЕ ВЫРАЗИТЬ(ЕСТЬNULL(МРОТнаДатуСобытия.Размер, 0) * ВЫБОР
	|								КОГДА ЕСТЬNULL(ДанныеСотрудника.ДоляВремени, 0) = 0
	|									ТОГДА 1
	|								ИНАЧЕ ЕСТЬNULL(ДанныеСотрудника.ДоляВремени, 0)
	|							КОНЕЦ * 24 / 730 КАК ЧИСЛО(15, 2))
	|				КОНЕЦ
	|		КОГДА НЕ НачислениеПоБольничномуЛисту.РасчетПособияИзТарифнойСтавки
	|			ТОГДА ВЫБОР
	|					КОГДА ЕСТЬNULL(Расчет2010.ОтработаноДней, 0) = 0
	|						ТОГДА 0
	|					ИНАЧЕ Расчет2010.Результат / Расчет2010.ОтработаноДней
	|				КОНЕЦ
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СреднийДневнойЗаработок,
	|	ОсвобождениеОтРаботы1.ИдентификационныйНомерВрача КАК ИдентификационныйНомерВрача1,
	|	ОсвобождениеОтРаботы1.ФИОВрачаПредседателяВК КАК ФИОВрачаПредседателяВК1,
	|	ОсвобождениеОтРаботы2.ИдентификационныйНомерВрача КАК ИдентификационныйНомерВрача2,
	|	ОсвобождениеОтРаботы2.ФИОВрачаПредседателяВК КАК ФИОВрачаПредседателяВК2,
	|	ОсвобождениеОтРаботы3.ИдентификационныйНомерВрача КАК ИдентификационныйНомерВрача3,
	|	ОсвобождениеОтРаботы3.ФИОВрачаПредседателяВК КАК ФИОВрачаПредседателяВК3,
	|	ОсвобождениеОтРаботы1.ДолжностьВрачаПредседателяВК КАК ДолжностьВрачаПредседателяВК1,
	|	ОсвобождениеОтРаботы1.ИдентификационныйНомерВрачаПредседателяВК КАК ИдентификационныйНомерВрачаПредседателяВК1,
	|	ОсвобождениеОтРаботы2.ДолжностьВрачаПредседателяВК КАК ДолжностьВрачаПредседателяВК2,
	|	ОсвобождениеОтРаботы2.ИдентификационныйНомерВрачаПредседателяВК КАК ИдентификационныйНомерВрачаПредседателяВК2,
	|	ОсвобождениеОтРаботы3.ДолжностьВрачаПредседателяВК КАК ДолжностьВрачаПредседателяВК3,
	|	ОсвобождениеОтРаботы3.ИдентификационныйНомерВрачаПредседателяВК КАК ИдентификационныйНомерВрачаПредседателяВК3,
	|	НачислениеПоБольничномуЛисту.НаименованиеДокументаНаПроживание,
	|	НачислениеПоБольничномуЛисту.СерияДокументаНаПроживание,
	|	НачислениеПоБольничномуЛисту.НомерДокументаНаПроживание,
	|	НачислениеПоБольничномуЛисту.ДатаВыдачиДокументаНаПроживание,
	|	НачислениеПоБольничномуЛисту.ТарифнаяСтавкаОклад,
	|	НачислениеПоБольничномуЛисту.СреднедневнойЗаработокНаПериодПростоя КАК СреднийЗаработокНаПериодПростоя,
	|	НачислениеПоБольничномуЛисту.АдресПочтовыйКодПоКЛАДР,
	|	НачислениеПоБольничномуЛисту.АдресРегистрацииКодПоКЛАДР,
	|	НачислениеПоБольничномуЛисту.АдресЛПУКодПоКЛАДР,
	|	НачислениеПоБольничномуЛисту.КодПричиныПерерасчета,
	|	ВЫБОР
	|		КОГДА НачислениеПоБольничномуЛисту.ПрименятьЛьготыПриНачисленииПособия
	|				И НачислениеПоБольничномуЛисту.ВыплатаЗаСчетФедеральногоБюджета <> ЗНАЧЕНИЕ(Перечисление.ВыплатыПособийЗаСчетФедеральногоБюджета.ЗачетНестраховыхПериодов)
	|			ТОГДА НачислениеПоБольничномуЛисту.ВыплатаЗаСчетФедеральногоБюджета
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ВыплатыПособийЗаСчетФедеральногоБюджета.ПустаяСсылка)
	|	КОНЕЦ КАК ВыплатаЗаСчетФедеральногоБюджета,
	|	ВЫБОР
	|		КОГДА НачислениеПоБольничномуЛисту.ПерерассчитываемыйДокумент = ЗНАЧЕНИЕ(Документ.НачислениеПоБольничномуЛисту.ПустаяСсылка)
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК Перерасчет,
	|	НачислениеПоБольничномуЛисту.Дата КАК ДатаЗаполняемогоДокумента
	|ИЗ
	|	Документ.НачислениеПоБольничномуЛисту КАК НачислениеПоБольничномуЛисту
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТУходЗаБольнымЧленомСемьи КАК УходПервымЧленомСемьи
	|		ПО (УходПервымЧленомСемьи.НомерСтроки = 1)
	|			И НачислениеПоБольничномуЛисту.Ссылка = УходПервымЧленомСемьи.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТУходЗаБольнымЧленомСемьи КАК УходВторымЧленомСемьи
	|		ПО (УходВторымЧленомСемьи.НомерСтроки = 2)
	|			И НачислениеПоБольничномуЛисту.Ссылка = УходВторымЧленомСемьи.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТОсвобождениеОтРаботы КАК ОсвобождениеОтРаботы1
	|		ПО (ОсвобождениеОтРаботы1.НомерСтроки = 1)
	|			И НачислениеПоБольничномуЛисту.Ссылка = ОсвобождениеОтРаботы1.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТОсвобождениеОтРаботы КАК ОсвобождениеОтРаботы2
	|		ПО (ОсвобождениеОтРаботы2.НомерСтроки = 2)
	|			И НачислениеПоБольничномуЛисту.Ссылка = ОсвобождениеОтРаботы2.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТОсвобождениеОтРаботы КАК ОсвобождениеОтРаботы3
	|		ПО (ОсвобождениеОтРаботы3.НомерСтроки = 3)
	|			И НачислениеПоБольничномуЛисту.Ссылка = ОсвобождениеОтРаботы3.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТГоды2011 КАК ВторойГодРасчета2011
	|			ЛЕВОЕ СОЕДИНЕНИЕ ВТРасчет2011 КАК ЗаработокВторойГод
	|			ПО ВторойГодРасчета2011.Год = ЗаработокВторойГод.РасчетныйГод
	|				И ВторойГодРасчета2011.Ссылка = ЗаработокВторойГод.Ссылка
	|		ПО (ВторойГодРасчета2011.НомерСтроки = 2)
	|			И НачислениеПоБольничномуЛисту.Ссылка = ВторойГодРасчета2011.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТГоды2011 КАК ПервыйГодРасчета2011
	|			ЛЕВОЕ СОЕДИНЕНИЕ ВТРасчет2011 КАК ЗаработокПервыйГод
	|			ПО ПервыйГодРасчета2011.Год = ЗаработокПервыйГод.РасчетныйГод
	|				И ПервыйГодРасчета2011.Ссылка = ЗаработокПервыйГод.Ссылка
	|		ПО (ПервыйГодРасчета2011.НомерСтроки = 1)
	|			И НачислениеПоБольничномуЛисту.Ссылка = ПервыйГодРасчета2011.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТРасчет2010 КАК Расчет2010
	|		ПО НачислениеПоБольничномуЛисту.Ссылка = Расчет2010.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТДанныеСотрудника КАК ДанныеСотрудника
	|		ПО НачислениеПоБольничномуЛисту.Ссылка = ДанныеСотрудника.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТРазмерММОТНаДатуДатаНачалаСобытия КАК МРОТнаДатуСобытия
	|		ПО НачислениеПоБольничномуЛисту.ДатаНачала = МРОТнаДатуСобытия.ДатаНачалаСобытия
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТДанныеОСреднемЗаработке2011 КАК ДанныеОСреднемЗаработке2011
	|		ПО НачислениеПоБольничномуЛисту.Ссылка = ДанныеОСреднемЗаработке2011.Регистратор
	|ГДЕ
	|	НачислениеПоБольничномуЛисту.Ссылка В
	|			(ВЫБРАТЬ
	|				ДокументыЗаполнения.Ссылка
	|			ИЗ
	|				ВТДокументыЗаполнения КАК ДокументыЗаполнения)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Фамилия,
	|	Имя,
	|	Отчество,
	|	Сотрудник,
	|	ДатаНачалаСобытия";

	Запрос.УстановитьПараметр("ДатаПередачиФССВыплатыПособий", ПроцедурыУправленияПерсоналом.ЗначениеУчетнойПолитикиПоПерсоналуОрганизации(глЗначениеПеременной("глУчетнаяПолитикаПоПерсоналуОрганизации"), Организация, "ДатаПередачиФССВыплатыПособий"));
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("ТекущийРеестр", Ссылка);
	Запрос.УстановитьПараметр("ДатаЗакона213ФЗ",ПроведениеРасчетов.ДатаЗаменыЕСНСтраховымиВзносами());
	Запрос.УстановитьПараметр("ВидРасчета", ОбщегоНазначенияЗК.ОсобыйЭлемент(ПланыВидовРасчета.СреднийЗаработок, "ПоЗаработкуПособийС2011"));
	Запрос.УстановитьПараметр("ЗаполнятьВсемиДокументами", ТекущаяСтрока = Неопределено);
	Если ТекущаяСтрока <> Неопределено Тогда
		Запрос.УстановитьПараметр("Ссылка", ТекущаяСтрока.ЛистокНетрудоспособности);
	Иначе
		Запрос.УстановитьПараметр("Ссылка", Документы.НачислениеПоБольничномуЛисту.ПустаяСсылка());
	КонецЕсли;
	Если ТекущаяСтрока <> Неопределено Тогда
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			ЗаполнитьЗначенияСвойств(ТекущаяСтрока, Выборка);
			ТекущаяСтрока.ИнаяПолезнаяИнформация = СформироватьИнаяПолезнаяИнформация(ТекущаяСтрока);
		КонецЕсли;
	Иначе
		
		Результат = Запрос.Выполнить();
		Если Результат.Пустой() Тогда
			РаботникиОрганизации.Очистить();
			Возврат
		КонецЕсли;
		ВременнаяТаблица = Результат.Выгрузить();
		РаботникиОрганизации.Загрузить(ВременнаяТаблица);
		Для каждого СтрокаТЧ Из РаботникиОрганизации Цикл
			СтрокаТЧ.ИнаяПолезнаяИнформация = СформироватьИнаяПолезнаяИнформация(СтрокаТЧ);
		КонецЦикла;
		
		ВременнаяТаблица.Сортировать("ДатаЗаполняемогоДокумента");
		НачальнаяДата = ВременнаяТаблица[0].ДатаЗаполняемогоДокумента;
		ПоследняяДата = ВременнаяТаблица[ВременнаяТаблица.Количество() - 1].ДатаЗаполняемогоДокумента;
		ВременнаяТаблица = "";
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	НачислениеПоБольничномуЛисту.Ссылка,
		|	НачислениеПоБольничномуЛисту.Сотрудник КАК Сотрудник,
		|	НачислениеПоБольничномуЛисту.Сотрудник.Наименование КАК ФИО,
		|	НачислениеПоБольничномуЛисту.Номер,
		|	НачислениеПоБольничномуЛисту.Дата
		|ИЗ
		|	Документ.НачислениеПоБольничномуЛисту КАК НачислениеПоБольничномуЛисту
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РеестрСведенийВФССОПособияхПоНетрудоспособности.РаботникиОрганизации КАК РеестрСведенийВФССОПособияхПоНетрудоспособностиРаботникиОрганизации
		|		ПО НачислениеПоБольничномуЛисту.Ссылка = РеестрСведенийВФССОПособияхПоНетрудоспособностиРаботникиОрганизации.ЛистокНетрудоспособности
		|			И (РеестрСведенийВФССОПособияхПоНетрудоспособностиРаботникиОрганизации.Ссылка <> &ТекущийРеестр)
		|			И (РеестрСведенийВФССОПособияхПоНетрудоспособностиРаботникиОрганизации.Ссылка.Проведен)
		|ГДЕ
		|	НачислениеПоБольничномуЛисту.Проведен
		|	И НачислениеПоБольничномуЛисту.Дата МЕЖДУ &НачальнаяДата И &ПоследняяДата
		|	И НачислениеПоБольничномуЛисту.ПериодРегистрации >= &ДатаПередачиФССВыплатыПособий
		|	И НачислениеПоБольничномуЛисту.Организация = &Организация
		|	И НачислениеПоБольничномуЛисту.ПерерассчитываемыйДокумент <> ЗНАЧЕНИЕ(Документ.НачислениеПоБольничномуЛисту.ПустаяСсылка)
		|	И НачислениеПоБольничномуЛисту.ПерерассчитываемыйДокумент.ПериодРегистрации < &ДатаПередачиФССВыплатыПособий
		|	И НачислениеПоБольничномуЛисту.ПричинаНетрудоспособности <> ЗНАЧЕНИЕ(Перечисление.ПричиныНетрудоспособности.ТравмаНаПроизводстве)
		|	И (НачислениеПоБольничномуЛисту.ПричинаНетрудоспособности <> ЗНАЧЕНИЕ(Перечисление.ПричиныНетрудоспособности.ОбщееЗаболевание)
		|			ИЛИ ДОБАВИТЬКДАТЕ(НачислениеПоБольничномуЛисту.ДатаНачалаСобытия, ДЕНЬ, НачислениеПоБольничномуЛисту.ДнейОплатыРаботодателем) <= ДОБАВИТЬКДАТЕ(НачислениеПоБольничномуЛисту.ДатаНачалаОплаты, ДЕНЬ, НачислениеПоБольничномуЛисту.ДнейОплаты - 1))
		|	И РеестрСведенийВФССОПособияхПоНетрудоспособностиРаботникиОрганизации.Сотрудник ЕСТЬ NULL 
		|
		|УПОРЯДОЧИТЬ ПО
		|	Сотрудник"; 
		Запрос.УстановитьПараметр("НачальнаяДата", НачальнаяДата);
		Запрос.УстановитьПараметр("ПоследняяДата", ПоследняяДата);
		Результат = Запрос.Выполнить();
		Если Результат.Пустой() Тогда
			Возврат
		КонецЕсли;
		ОбработкаКомментариев = глЗначениеПеременной("глОбработкаСообщений");
		ОбработкаКомментариев.УдалитьСообщения();
		СтрокаЗаголовка = ОбработкаКомментариев.ДобавитьСообщение("По следущим сотрудникам обнаружены документы-исправления для начисленных до вступления в пилотный проект пособий. По таким случаям пакет документов для проведения перерасчета пособия (заявление о перерасчете пособия, справка для этого перерасчета, листок нетрудоспособности и опись) передается в ФСС лично.", Перечисления.ВидыСообщений.ВажнаяИнформация);
		Выборка = Результат.Выбрать();
		Пока Выборка.СледующийПоЗначениюПоля("Сотрудник") Цикл
			РодительскаяСтрока = ОбработкаКомментариев.ДобавитьСообщение(Выборка.ФИО, Перечисления.ВидыСообщений.ПустаяСсылка(), СтрокаЗаголовка);
			Пока Выборка.Следующий() Цикл
				МассивРасшифровки = Новый Массив;
				МассивРасшифровки.Добавить(Новый Структура("Представление, Расшифровка", "Документ № " + Выборка.Номер + " от " + Формат(Выборка.Дата,"ДФ=dd.MM.yyyy"), Выборка.Ссылка));
				ОбработкаКомментариев.ДобавитьСообщение("", Перечисления.ВидыСообщений.ПустаяСсылка(), МассивРасшифровки, РодительскаяСтрока, Ложь);
			КонецЦикла;
		КонецЦикла;
		ОбработкаКомментариев.ПоказатьСообщения();
	КонецЕсли;

КонецПроцедуры

#Если ТолстыйКлиентОбычноеПриложение Тогда

Процедура ВписатьВСтрокуКодУсловийИсчисления(СтрокаУсловий, КодУсловий)

	Если ЗначениеЗаполнено(КодУсловий) И Найти("43,44,45,47", КодУсловий) > 0 Тогда
		СтрокаУсловий = СтрокаУсловий + ?(ЗначениеЗаполнено(СтрокаУсловий),", ","") + КодУсловий
	КонецЕсли;

КонецПроцедуры

Функция ОписаниеСтажаВПечатнойФорме(СтажЛет, СтажМесяцев)

	Возврат ?(ЗначениеЗаполнено(СтажЛет), ", " + СтажЛет + " " + ОбщегоНазначения.ФормаМножественногоЧисла("год","года","лет", СтажЛет),"") + ?(ЗначениеЗаполнено(СтажМесяцев), Символы.ПС + СтажМесяцев + " " + ОбщегоНазначения.ФормаМножественногоЧисла("месяц","месяца","месяцев", СтажМесяцев),"");

КонецФункции // ОписаниеСтажаВПечатнойФорме()
	
// Печатает реестр сведений для назначения и выплаты пособий
//
// Параметры
//  нет
//
// Возвращаемое значение:
//   ТабличныйДокумент
//
Функция ПечатьОписьЗаявленийИДокументовВФСС(ИмяМакета)
	
	ОбработкаКомментариев = глЗначениеПеременной("глОбработкаСообщений");
	ОбработкаКомментариев.УдалитьСообщения();
		
	ВыборкаПоШапкеДокумента = ОбменСведениямиОПособияхСФСС.СформироватьЗапросПоШапкеДокумента(ЭтотОбъект).Выбрать();
	ВыборкаПоШапкеДокумента.Следующий();
	
	ВыборкаПоТЧРаботникиОрганизации = СформироватьЗапросПоТЧРаботникиОрганизации().Выбрать();
	
	Отказ = Ложь;
	
	ПроверитьПравильностьЗаполненияДокумента(Отказ, Ложь, ВыборкаПоШапкеДокумента, ВыборкаПоТЧРаботникиОрганизации);
	
	ОбработкаКомментариев.ПоказатьСообщения();
	
	Если Отказ Тогда
	
		Возврат Неопределено;
	
	КонецЕсли; 
	
	ВыборкаПоТЧРаботникиОрганизации.Сбросить();
	
	Возврат ОбменСведениямиОПособияхСФСС.ПечатьОписьЗаявленийИДокументовВФСС(ВыборкаПоШапкеДокумента, ВыборкаПоТЧРаботникиОрганизации, ИмяМакета);
		

КонецФункции // ПечатьРеестрСведенийВФСС()

// Печатает реестр сведений для назначения и выплаты пособий
//
// Параметры
//  нет
//
// Возвращаемое значение:
//   ТабличныйДокумент
//
Функция ПечатьРеестрСведенийВФСС(ИмяМакета = "РеестрСведенийВФСС2011")
	
	ОбработкаКомментариев = глЗначениеПеременной("глОбработкаСообщений");
	ОбработкаКомментариев.УдалитьСообщения();
		
	ВыборкаПоШапкеДокумента = ОбменСведениямиОПособияхСФСС.СформироватьЗапросПоШапкеДокумента(ЭтотОбъект).Выбрать();
	ВыборкаПоШапкеДокумента.Следующий();
	
	ВыборкаПоТЧРаботникиОрганизации = СформироватьЗапросПоТЧРаботникиОрганизации("НеВыделятьПособиеПриПостановкеНаУчетВРанниеСрокиБеременности").Выбрать();
	
	Отказ = Ложь;

	ПроверитьПравильностьЗаполненияДокумента(Отказ, Ложь, ВыборкаПоШапкеДокумента, ВыборкаПоТЧРаботникиОрганизации);
	
	ОбработкаКомментариев.ПоказатьСообщения();
	
	Если Отказ Тогда
	
		Возврат Неопределено;
	
	КонецЕсли; 
	
	ВыборкаПоТЧРаботникиОрганизации.Сбросить();
	
	ТабДокумент = Новый ТабличныйДокумент;
		
	ТабДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_РеестрСведенийВФССОПособияхПоНетрудоспособности";
	ТабДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;
	
	Макет = ПолучитьМакет(ИмяМакета);
	
	ОбластьШапка = Макет.ПолучитьОбласть("Шапка");
	ОбластьСтрока = Макет.ПолучитьОбласть("Строка");
	ОбластьПодвал = Макет.ПолучитьОбласть("Подвал");
	ОбластьПодписи = Макет.ПолучитьОбласть("Подписи");
	ПовторятьПриПечатиСтроки = Макет.ПолучитьОбласть("ПовторятьПриПечати"); // повторяющаяся шапка страницы
	
	// массив с двумя строками - для разбиения на страницы
	ВыводимыеОбласти = Новый Массив();
	ВыводимыеОбласти.Добавить(ОбластьСтрока);
	
	ОбластьШапка.Параметры.Заполнить(ВыборкаПоШапкеДокумента);
	ОбластьПодписи.Параметры.Заполнить(ВыборкаПоШапкеДокумента);
	ТабДокумент.Вывести(ОбластьШапка);
	ТабДокумент.Вывести(ПовторятьПриПечатиСтроки);
	
	ВсегоСтрокДокумента = ВыборкаПоТЧРаботникиОрганизации.Количество();
	ВыведеноСтрок = 0;
	Пока ВыборкаПоТЧРаботникиОрганизации.Следующий() Цикл
		
		ОбластьСтрока.Параметры.Заполнить(ВыборкаПоТЧРаботникиОрганизации);
		
		ОбластьСтрока.Параметры.АдресРегистрации = УправлениеКонтактнойИнформацией.ПолучитьПредставлениеАдресаПоСтрока(ВыборкаПоТЧРаботникиОрганизации.АдресРегистрации);
		Если ВыборкаПоТЧРаботникиОрганизации.ПеречислятьПособиеПочтовымПереводом Тогда
			ОбластьСтрока.Параметры.АдресПочтовый = УправлениеКонтактнойИнформацией.ПолучитьПредставлениеАдресаПоСтрока(ВыборкаПоТЧРаботникиОрганизации.АдресПочтовый);
			ОбластьСтрока.Параметры.НаименованиеБанка = "-";
			ОбластьСтрока.Параметры.БИКБанка = "-";
			ОбластьСтрока.Параметры.НомерЛицевогоСчета = "-";
		Иначе
			ОбластьСтрока.Параметры.АдресПочтовый = "-";
		КонецЕсли;
		
		Если ВыборкаПоТЧРаботникиОрганизации.ВидПособия = Перечисления.ВидыПособийСоциальногоСтрахования.БеременностьРоды Тогда
			Если ИмяМакета = "РеестрСведенийВФСС2011" Тогда
				ОбластьСтрока.Параметры.СерияДокумента = "-";
				ОбластьСтрока.Параметры.НомерДокумента = "-";
				ОбластьСтрока.Параметры.ДатаВыдачиДокумента = "-";
			КонецЕсли;
			ОбластьСтрока.Параметры.ИНН = "-";
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(ОбластьСтрока.Параметры.ИнаяПолезнаяИнформация) Тогда
			ОбластьСтрока.Параметры.ИнаяПолезнаяИнформация = "-"
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(ОбластьСтрока.Параметры.ИНН) Тогда
			ОбластьСтрока.Параметры.ИНН = "-"
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ВыборкаПоТЧРаботникиОрганизации.НомерПервичногоБольничного) Тогда
			ОбластьСтрока.Параметры.НомерВходящегоДокумента = СокрЛП(ВыборкаПоТЧРаботникиОрганизации.НомерВходящегоДокумента) + " (" + СокрЛП(ВыборкаПоТЧРаботникиОрганизации.НомерПервичногоБольничного) + ")";
		КонецЕсли;
		
		ОбластьСтрока.Параметры.ПричинаНетрудоспособности = ВыборкаПоТЧРаботникиОрганизации.КодПричиныНетрудоспособности
			+ ?(ЗначениеЗаполнено(ВыборкаПоТЧРаботникиОрганизации.ДополнительныйКодПричиныНетрудоспособности),", " + ВыборкаПоТЧРаботникиОрганизации.ДополнительныйКодПричиныНетрудоспособности,"")
			+ ?(ЗначениеЗаполнено(ВыборкаПоТЧРаботникиОрганизации.ВторойКодПричиныНетрудоспособности),", " + ВыборкаПоТЧРаботникиОрганизации.ВторойКодПричиныНетрудоспособности + " (" + Формат(ВыборкаПоТЧРаботникиОрганизации.ДатаИзмененияКодаПричиныНетрудоспособности,"ДФ=dd.MM.yyyy") + ")","");
			
		ОбластьСтрока.Параметры.УходЗаРодственником = ?(ЗначениеЗаполнено(ВыборкаПоТЧРаботникиОрганизации.УходЗаРодственникомФИО1),СокрЛП(ВыборкаПоТЧРаботникиОрганизации.УходЗаРодственникомФИО1) 
			+ ОписаниеСтажаВПечатнойФорме(ВыборкаПоТЧРаботникиОрганизации.УходЗаРодственникомЛет1, ВыборкаПоТЧРаботникиОрганизации.УходЗаРодственникомМесяцев1)
			+ ?(ЗначениеЗаполнено(ВыборкаПоТЧРаботникиОрганизации.УходЗаРодственникомРодственнаяСвязь1),", " + ВыборкаПоТЧРаботникиОрганизации.УходЗаРодственникомРодственнаяСвязь1, ""),"")
			+ ?(ЗначениеЗаполнено(ВыборкаПоТЧРаботникиОрганизации.УходЗаРодственникомФИО2), "; " + Символы.ПС + СокрЛП(ВыборкаПоТЧРаботникиОрганизации.УходЗаРодственникомФИО2) 
			+ ОписаниеСтажаВПечатнойФорме(ВыборкаПоТЧРаботникиОрганизации.УходЗаРодственникомЛет2, ВыборкаПоТЧРаботникиОрганизации.УходЗаРодственникомМесяцев2)
			+ ?(ЗначениеЗаполнено(ВыборкаПоТЧРаботникиОрганизации.УходЗаРодственникомРодственнаяСвязь2),", " + ВыборкаПоТЧРаботникиОрганизации.УходЗаРодственникомРодственнаяСвязь2, ""),"");
        Если Не ЗначениеЗаполнено(ОбластьСтрока.Параметры.УходЗаРодственником) Тогда
			ОбластьСтрока.Параметры.УходЗаРодственником = "-";
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ВыборкаПоТЧРаботникиОрганизации.ПериодНахожденияВСтационареСРебенкомС) И ЗначениеЗаполнено(ВыборкаПоТЧРаботникиОрганизации.ПериодНахожденияВСтационареСРебенкомПо) Тогда
			ОбластьСтрока.Параметры.ПериодНахожденияВСтационареСРебенком = ОбменСведениямиОПособияхСФСС.ОписаниеПериодаВПечатнойФорме(ВыборкаПоТЧРаботникиОрганизации.ПериодНахожденияВСтационареСРебенкомС, ВыборкаПоТЧРаботникиОрганизации.ПериодНахожденияВСтационареСРебенкомПо);
		Иначе
			ОбластьСтрока.Параметры.ПериодНахожденияВСтационареСРебенком = "-";
		КонецЕсли;
		
		ОбластьСтрока.Параметры.Освобождение = ОбменСведениямиОПособияхСФСС.ОписаниеПериодаВПечатнойФорме(ВыборкаПоТЧРаботникиОрганизации.ОсвобождениеС1,Макс(ВыборкаПоТЧРаботникиОрганизации.ОсвобождениеПо1,ВыборкаПоТЧРаботникиОрганизации.ОсвобождениеПо2,ВыборкаПоТЧРаботникиОрганизации.ОсвобождениеПо3));
		
		Если ВыборкаПоТЧРаботникиОрганизации.ПоставленаНаУчетВРанниеСрокиБеременности Тогда
			Если ИмяМакета = "РеестрСведенийВФСС2011" Тогда
				ОбластьСтрока.Параметры.ПоставленаНаУчетВРанниеСрокиБеременности = "Да" + Символы.ПС + Формат(ВыборкаПоТЧРаботникиОрганизации.ДатаСправкиБеременной,"ДФ=dd.MM.yyyy")
			КонецЕсли;
		КонецЕсли;
		
		ОбластьСтрока.Параметры.Стаж = СтрЗаменить(ОписаниеСтажаВПечатнойФорме(ВыборкаПоТЧРаботникиОрганизации.СтажЛет, ВыборкаПоТЧРаботникиОрганизации.СтажМесяцев),",","");
		ЛетСтажа = 0;
		МесяцевСтажа = 0;
		ОбменСведениямиОПособияхСФСС.ПодсчитатьРазностьСтажейВГодахИМесяцах(ВыборкаПоТЧРаботникиОрганизации.СтажЛет, ВыборкаПоТЧРаботникиОрганизации.СтажМесяцев, ВыборкаПоТЧРаботникиОрганизации.СтажБезНестраховыхПериодовЛет,ВыборкаПоТЧРаботникиОрганизации.СтажБезНестраховыхПериодовСтажМесяцев, ЛетСтажа, МесяцевСтажа);
		Если ЛетСтажа > 0 Или МесяцевСтажа > 0 Тогда
			ОбластьСтрока.Параметры.НестраховыеПериоды = СтрЗаменить(ОписаниеСтажаВПечатнойФорме(ЛетСтажа, МесяцевСтажа),",","");
		Иначе 
			ОбластьСтрока.Параметры.НестраховыеПериоды = "";
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ВыборкаПоТЧРаботникиОрганизации.СрочныйДоговорС) И ЗначениеЗаполнено(ВыборкаПоТЧРаботникиОрганизации.СрочныйДоговорПо) Тогда
			ОбластьСтрока.Параметры.СрочныйДоговор = ОбменСведениямиОПособияхСФСС.ОписаниеПериодаВПечатнойФорме(ВыборкаПоТЧРаботникиОрганизации.СрочныйДоговорС, ВыборкаПоТЧРаботникиОрганизации.СрочныйДоговорПо);
		Иначе
			ОбластьСтрока.Параметры.СрочныйДоговор = "-";
		КонецЕсли;
		
		УсловияИсчисления = "";
		ВписатьВСтрокуКодУсловийИсчисления(УсловияИсчисления, ВыборкаПоТЧРаботникиОрганизации.УсловияИсчисленияКод1);
		ВписатьВСтрокуКодУсловийИсчисления(УсловияИсчисления, ВыборкаПоТЧРаботникиОрганизации.УсловияИсчисленияКод2);
		ВписатьВСтрокуКодУсловийИсчисления(УсловияИсчисления, ВыборкаПоТЧРаботникиОрганизации.УсловияИсчисленияКод3);
		Если ЗначениеЗаполнено(УсловияИсчисления) Тогда
			ОбластьСтрока.Параметры.УсловияИсчисления = УсловияИсчисления;
		Иначе
			ОбластьСтрока.Параметры.УсловияИсчисления = "-";
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ВыборкаПоТЧРаботникиОрганизации.КодНарушенияРежима) И ЗначениеЗаполнено(ВыборкаПоТЧРаботникиОрганизации.ДатаНарушенияРежима) Тогда
			ОбластьСтрока.Параметры.НарушениеРежима = ВыборкаПоТЧРаботникиОрганизации.КодНарушенияРежима + ", " + Формат(ВыборкаПоТЧРаботникиОрганизации.ДатаНарушенияРежима,"ДФ=dd.MM.yyyy") 
				+ ?(ЗначениеЗаполнено(ВыборкаПоТЧРаботникиОрганизации.УважительнаяПричинаНарушенияРежима)," (" + СокрЛП(ВыборкаПоТЧРаботникиОрганизации.УважительнаяПричинаНарушенияРежима) + ")","");
		Иначе
			ОбластьСтрока.Параметры.НарушениеРежима = "-";
		КонецЕсли;
		
		Если ВыборкаПоТЧРаботникиОрганизации.ПорядокРасчетаСреднегоЗаработка = 0 Тогда
			ОбластьСтрока.Параметры.ПериодРасчетаС = Формат(ВыборкаПоТЧРаботникиОрганизации.ПервыйГодРасчета,"ЧЦ=4; ЧГ=0") + ?(ЗначениеЗаполнено(ВыборкаПоТЧРаботникиОрганизации.ЗамененныйПервыйГодРасчета) И ВыборкаПоТЧРаботникиОрганизации.ПервыйГодРасчета <> ВыборкаПоТЧРаботникиОрганизации.ЗамененныйПервыйГодРасчета, " (" + Формат(ВыборкаПоТЧРаботникиОрганизации.ДатаЗаявленияОЗаменеРасчетногоГода,"ДФ=dd.MM.yyyy") + ")","");
			ОбластьСтрока.Параметры.ПериодРасчетаПо = Формат(ВыборкаПоТЧРаботникиОрганизации.ВторойГодРасчета,"ЧЦ=4; ЧГ=0") + ?(ЗначениеЗаполнено(ВыборкаПоТЧРаботникиОрганизации.ЗамененныйВторойГодРасчета) И ВыборкаПоТЧРаботникиОрганизации.ВторойГодРасчета <> ВыборкаПоТЧРаботникиОрганизации.ЗамененныйВторойГодРасчета, " (" + Формат(ВыборкаПоТЧРаботникиОрганизации.ДатаЗаявленияОЗаменеРасчетногоГода,"ДФ=dd.MM.yyyy") + ")","");
			
			ЗаработокПервыйГодРасчета = ВыборкаПоТЧРаботникиОрганизации.ЗаработокПервыйГодРасчета;
			ЗаработокВторойГодРасчета = ВыборкаПоТЧРаботникиОрганизации.ЗаработокВторойГодРасчета;
			Разница = ВыборкаПоТЧРаботникиОрганизации.СреднийДневнойЗаработок - (ЗаработокПервыйГодРасчета + ЗаработокВторойГодРасчета) / 730;
			Если Разница > -0.02 И Разница < 0.02 Тогда
				СреднийЗаработок = ЗаработокПервыйГодРасчета + ЗаработокВторойГодРасчета
			Иначе
				СреднийЗаработок = ВыборкаПоТЧРаботникиОрганизации.МРОТ * ?(ВыборкаПоТЧРаботникиОрганизации.ДоляВремени > 0 И ВыборкаПоТЧРаботникиОрганизации.ДоляВремени < 1, ВыборкаПоТЧРаботникиОрганизации.ДоляВремени, 1)
			КонецЕсли;
			ОбластьСтрока.Параметры.СреднийЗаработок = Формат(СреднийЗаработок,"ЧЦ=15; ЧДЦ=2; ЧГ=0");
		ИначеЕсли ВыборкаПоТЧРаботникиОрганизации.ПорядокРасчетаСреднегоЗаработка = 1 Тогда
			ОбластьСтрока.Параметры.ПериодРасчетаС = Формат(ВыборкаПоТЧРаботникиОрганизации.ПериодРасчетаСреднегоЗаработкаНачало,"ДФ=dd.MM.yyyy");
			ОбластьСтрока.Параметры.ПериодРасчетаПо = Формат(ВыборкаПоТЧРаботникиОрганизации.ПериодРасчетаСреднегоЗаработкаОкончание,"ДФ=dd.MM.yyyy");
			ОбластьСтрока.Параметры.СреднийЗаработок = Формат(ВыборкаПоТЧРаботникиОрганизации.ЗаработокЗаПериодРасчета,"ЧЦ=15; ЧДЦ=2; ЧГ=0") + " (" + Формат(ВыборкаПоТЧРаботникиОрганизации.КалендарныхДнейРасчетногоПериода,"ЧЦ=3; ЧГ=0") + ")";
		Иначе
			ОбластьСтрока.Параметры.ПериодРасчетаС = "-";
			ОбластьСтрока.Параметры.ПериодРасчетаПо = "-";
			Если ВыборкаПоТЧРаботникиОрганизации.ПорядокРасчетаСреднегоЗаработка = 2 Тогда
				ОбластьСтрока.Параметры.СреднийЗаработок = Формат(ВыборкаПоТЧРаботникиОрганизации.ТарифнаяСтавкаОклад,"ЧЦ=15; ЧДЦ=2; ЧГ=0");
			ИначеЕсли ВыборкаПоТЧРаботникиОрганизации.ПорядокРасчетаСреднегоЗаработка = 3 Тогда
				ОбластьСтрока.Параметры.СреднийЗаработок = Формат(ВыборкаПоТЧРаботникиОрганизации.МРОТ * ?(ВыборкаПоТЧРаботникиОрганизации.РайонныйКоэффициент > 1, ВыборкаПоТЧРаботникиОрганизации.РайонныйКоэффициент, 1),"ЧЦ=15; ЧДЦ=2; ЧГ=0");
			Иначе
				ОбластьСтрока.Параметры.СреднийЗаработок = "";
			КонецЕсли;
		КонецЕсли;
		
		Если ИмяМакета = "РеестрСведенийВФСС2012" Тогда
			Если Не ЗначениеЗаполнено(ОбластьСтрока.Параметры.Отчество) Тогда
				ОбластьСтрока.Параметры.Отчество = "-"
			КонецЕсли;
			Если Не ЗначениеЗаполнено(ОбластьСтрока.Параметры.ДатаСправкиБеременной) Тогда
				ОбластьСтрока.Параметры.ДатаСправкиБеременной = "-"
			КонецЕсли;
			Если Не ЗначениеЗаполнено(ОбластьСтрока.Параметры.НомерСправкиБеременной) Тогда
				ОбластьСтрока.Параметры.НомерСправкиБеременной = "-"
			КонецЕсли;
			Если Не ЗначениеЗаполнено(ОбластьСтрока.Параметры.ТарифнаяСтавкаОклад) Тогда
				ОбластьСтрока.Параметры.ТарифнаяСтавкаОклад = "-"
			КонецЕсли;
			Если Не ЗначениеЗаполнено(ОбластьСтрока.Параметры.ДоляВремени) Тогда
				ОбластьСтрока.Параметры.ДоляВремени = "-";
				ОбластьСтрока.Параметры.ТарифнаяСтавкаОклад = "-"
			КонецЕсли;
			Если Не ЗначениеЗаполнено(ОбластьСтрока.Параметры.НаименованиеДокументаНаПроживание) Тогда
				ОбластьСтрока.Параметры.НаименованиеДокументаНаПроживание = "-"
			КонецЕсли;
			Если Не ЗначениеЗаполнено(ОбластьСтрока.Параметры.СерияДокументаНаПроживание) Тогда
				ОбластьСтрока.Параметры.СерияДокументаНаПроживание = "-"
			КонецЕсли;
			Если Не ЗначениеЗаполнено(ОбластьСтрока.Параметры.НомерДокументаНаПроживание) Тогда
				ОбластьСтрока.Параметры.НомерДокументаНаПроживание = "-"
			КонецЕсли;
			Если Не ЗначениеЗаполнено(ОбластьСтрока.Параметры.ДатаВыдачиДокументаНаПроживание) Тогда
				ОбластьСтрока.Параметры.ДатаВыдачиДокументаНаПроживание = "-"
			КонецЕсли;
		КонецЕсли;
		
		// разбиение на страницы
		ВыведеноСтрок = ВыведеноСтрок + 1;
		
		// Проверим, уместится ли строка на странице или надо открывать новую страницу
		ВывестиПодвалЛиста = Не ФормированиеПечатныхФорм.ПроверитьВыводТабличногоДокумента(ТабДокумент, ВыводимыеОбласти);
		Если Не ВывестиПодвалЛиста И ВыведеноСтрок = ВсегоСтрокДокумента Тогда
			ВыводимыеОбласти.Добавить(ОбластьПодвал);
			Если ВыводитьПодписиВРеестрПособий Тогда
				ВыводимыеОбласти.Добавить(ОбластьПодписи);
			КонецЕсли;
			ВывестиПодвалЛиста = Не ФормированиеПечатныхФорм.ПроверитьВыводТабличногоДокумента(ТабДокумент, ВыводимыеОбласти);
		КонецЕсли;
		Если ВывестиПодвалЛиста Тогда
			ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц();
			ТабДокумент.Вывести(ПовторятьПриПечатиСтроки);
		КонецЕсли;
		
		ТабДокумент.Вывести(ОбластьСтрока);
	
	КонецЦикла; 
	
	ТабДокумент.Вывести(ОбластьПодвал);
	Если ВыводитьПодписиВРеестрПособий Тогда
		ТабДокумент.Вывести(ОбластьПодписи);
	КонецЕсли;
	
	Возврат ТабДокумент;

КонецФункции // ПечатьРеестрСведенийВФСС()

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

//Процедура вывода сведений на печать
Функция Печать(ИмяМакета, КоличествоЭкземпляров = 1, НаПринтер = Ложь) Экспорт
	
	Если Лев(ИмяМакета,29) = "ОписьЗаявленийИДокументовВФСС" Тогда
		
		ТабДокумент = Новый ТабличныйДокумент;
		
		ТабДокумент = ПечатьОписьЗаявленийИДокументовВФСС(ИмяМакета);
		
		Возврат УниверсальныеМеханизмы.НапечататьДокумент(ТабДокумент, КоличествоЭкземпляров, НаПринтер, "Опись заявлений и документов в ФСС");
		
	ИначеЕсли Лев(ИмяМакета, 14) = "РеестрСведений" Тогда
		
		ТабДокумент = ПечатьРеестрСведенийВФСС(ИмяМакета);
		
		Возврат УниверсальныеМеханизмы.НапечататьДокумент(ТабДокумент, КоличествоЭкземпляров, НаПринтер, "Реестр сведений в ФСС",,ТабДокумент);
		
	КонецЕсли;
	
КонецФункции // Печать() 
 
#КонецЕсли

Процедура Автозаполнение(Режим = Неопределено, ТекущаяСтрока = Неопределено) Экспорт
	
	Если Режим = "ДанныеСотрудника" Тогда
		ОбменСведениямиОПособияхСФСС.ЗаполнитьДанныеЗастрахованныхВРеестреПособий(Дата, РаботникиОрганизации, ТекущаяСтрока)
	ИначеЕсли Режим = "ДанныеБольничного" Тогда	
		ТекущийСотрудник = ТекущаяСтрока.Сотрудник;
		ЗаполнитьДанныеБольничного(ТекущаяСтрока);
		Если ТекущийСотрудник <> ТекущаяСтрока.Сотрудник Тогда
			ОбменСведениямиОПособияхСФСС.ЗаполнитьДанныеЗастрахованныхВРеестреПособий(Дата, РаботникиОрганизации, ТекущаяСтрока)
		КонецЕсли;
	Иначе	
		ЗаполнитьДанныеБольничного();
		ОбменСведениямиОПособияхСФСС.ЗаполнитьДанныеЗастрахованныхВРеестреПособий(Дата, РаботникиОрганизации, ,"Фамилия,Имя,Отчество")
	КонецЕсли;
	
КонецПроцедуры

Процедура ПроверитьПравильностьЗаполненияДокумента(ЕстьОшибки, СообщатьОбУспехе = Истина, ВыборкаПоШапкеДокумента = Неопределено, ВыборкаПоРаботникиОрганизации = Неопределено) Экспорт
	
	// Заголовок для сообщений об ошибках проведения.
	Заголовок = ОбщегоНазначения.ПредставлениеДокументаПриПроведении(Ссылка);

	Обработка = глЗначениеПеременной("глОбработкаСообщений");
	СообщенияРазделПроверки = Обработка.ДобавитьСообщение(Заголовок, Перечисления.ВидыСообщений.Информация);
    БылоСтрок = Обработка.Сообщения.Строки.Количество();
	
	Если ВыборкаПоШапкеДокумента = Неопределено Тогда
		ВыборкаПоШапкеДокумента = ОбменСведениямиОПособияхСФСС.СформироватьЗапросПоШапкеДокумента(ЭтотОбъект).Выбрать();
		ВыборкаПоШапкеДокумента.Следующий();
	КонецЕсли;
	Если ВыборкаПоРаботникиОрганизации = Неопределено Тогда
		ВыборкаПоРаботникиОрганизации = СформироватьЗапросПоТЧРаботникиОрганизации().Выбрать();
	КонецЕсли;
	
	Отказ = Ложь;
	
	ОбменСведениямиОПособияхСФСС.ПроверитьДанныеСтрахователя(ВыборкаПоШапкеДокумента, Отказ, Заголовок);
	
	Пока ВыборкаПоРаботникиОрганизации.Следующий() Цикл
		
		ПроверитьЗаполнениеСтрокиТЧРаботникиОрганизации(ВыборкаПоРаботникиОрганизации, Отказ, Заголовок);
		
	КонецЦикла; 
	
	Если Отказ тогда
		ЕстьОшибки = Истина;
	ИначеЕсли Не СообщатьОбУспехе Тогда
		Обработка.Сообщения.Строки.Удалить(СообщенияРазделПроверки);
	КонецЕсли;	 
	
	Если СообщатьОбУспехе И БылоСтрок = Обработка.Сообщения.Строки.Количество() Тогда
		Обработка.ДобавитьСообщение("Ошибок не обнаружено.", Перечисления.ВидыСообщений.Информация, , СообщенияРазделПроверки);		
	КонецЕсли;
	
КонецПроцедуры // ПроверитьПравильностьЗаполненияДокумента()

// Возвращает доступные варианты печати документа
//
// Возвращаемое значение:
//  Структура, каждая строка которой соответствует одному из вариантов печати
//
Функция ПолучитьСтруктуруПечатныхФорм() Экспорт
	
	Возврат Новый Структура("ОписьЗаявленийИДокументовВФСС_2011, РеестрСведенийВФСС2011, Разделитель, ОписьЗаявленийИДокументовВФСС_2012, РеестрСведенийВФСС2012", "Опись заявлений и документов (форма 2011 г.)", "Реестр сведений (форма 2011 г.)", , "Опись заявлений и документов (форма 2012 г.)", "Реестр сведений (форма 2012 г.)");
	
КонецФункции // ПолучитьСтруктуруПечатныхФорм()

// Формирует файл, который можно будет записать на дискетку
//
// Параметры: 
//  Нет
//
// Возвращаемое значение:
//  Строка - содержимое файла
//
Функция СформироватьВыходнойФайл(Отказ, ПроверятьДанные = Истина, ВыборкаПоШапкеДокумента = Неопределено, ВыборкаПоРаботникиОрганизации = Неопределено) Экспорт

	Возврат	ОбменСведениямиОПособияхСФСС.СформироватьФайлРеестра(ЭтотОбъект, Отказ, ПроверятьДанные, ВыборкаПоШапкеДокумента, ВыборкаПоРаботникиОрганизации)
	
КонецФункции // СформироватьВыходнойФайл()

Функция СформироватьИнаяПолезнаяИнформация(ТекущаяСтрока) Экспорт
	
	ИнаяПолезнаяИнформация = "";
	
	Если ЗначениеЗаполнено(ТекущаяСтрока.ПериодПростояС) И ЗначениеЗаполнено(ТекущаяСтрока.ПериодПростояПо) Тогда
		ИнаяПолезнаяИнформация = "Период простоя с " + Формат(ТекущаяСтрока.ПериодПростояС, "ДФ=dd.MM.yyyy") + " по " + Формат(ТекущаяСтрока.ПериодПростояПо, "ДФ=dd.MM.yyyy");
	КонецЕсли; 
	
	Если ТекущаяСтрока.ПорядокРасчетаСреднегоЗаработка = 0 И ЗначениеЗаполнено(ТекущаяСтрока.ДатаЗаявленияОЗаменеРасчетногоГода) Тогда
		ИнаяПолезнаяИнформация = ИнаяПолезнаяИнформация + ?(ПустаяСтрока(ИнаяПолезнаяИнформация), "", ". ") + "Заявление о замене календарных годов от " + Формат(ТекущаяСтрока.ДатаЗаявленияОЗаменеРасчетногоГода, "ДФ=dd.MM.yyyy");
	ИначеЕсли ТекущаяСтрока.ПорядокРасчетаСреднегоЗаработка <> 0 Тогда
		ИнаяПолезнаяИнформация = ИнаяПолезнаяИнформация + ?(ПустаяСтрока(ИнаяПолезнаяИнформация), "", ". ") + "Заявление о расчете пособия по беременности и родам по нормам 2010 года от ___.___.______";
	КонецЕсли; 
	
	Если ТекущаяСтрока.РайонныйКоэффициент > 1 И ЗначениеЗаполнено(ТекущаяСтрока.Район) Тогда
		ИнаяПолезнаяИнформация = ИнаяПолезнаяИнформация + ?(ПустаяСтрока(ИнаяПолезнаяИнформация), "", ". ") + "Районный коэффициент " + Формат(ТекущаяСтрока.РайонныйКоэффициент, "ЧДЦ=2") + ", район " + ТекущаяСтрока.Район;
	КонецЕсли;
	
	Если ТекущаяСтрока.ДоляВремени > 0 И ТекущаяСтрока.ДоляВремени < 1 Тогда
		ИнаяПолезнаяИнформация = ИнаяПолезнаяИнформация + ?(ПустаяСтрока(ИнаяПолезнаяИнформация), "", ". ") + "Застрахованное лицо на момент наступления страхового случая занято на условиях неполного рабочего времени с коэффициентом " + ТекущаяСтрока.ДоляВремени;
	КонецЕсли;
	
	Возврат ИнаяПолезнаяИнформация + ?(ПустаяСтрока(ИнаяПолезнаяИнформация), "", ". ");

КонецФункции // СформироватьСписокДокументовОснования()

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ДЛЯ ОБЕСПЕЧЕНИЯ ПРОВЕДЕНИЯ ДОКУМЕНТА


// Запрос по табличной части РаботникиОрганизации для вывода на печать и проверки корректности заполнения
//
// Параметры
//  Режим - строка, если указано "НеВыделятьПособиеПриПостановкеНаУчетВРанниеСрокиБеременности",
//                 это пособие отдельной строкой резкльтата не выделяется
//
// Возвращаемое значение:
//   результат выполнения запроса
//
Функция СформироватьЗапросПоТЧРаботникиОрганизации(Режим = "") Экспорт
	
	Запрос = Новый Запрос();
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("ДокументСсылка", Ссылка);
	Запрос.УстановитьПараметр("Дата", Дата);
	ВременнаяТаблица = РаботникиОрганизации.Выгрузить(Новый Структура("ПоставленаНаУчетВРанниеСрокиБеременности", Истина),"ДатаСправкиБеременной");
	ВременнаяТаблица.Колонки.Добавить("ДатаСправкиБеременнойСтрокой",Новый ОписаниеТипов("Строка"));
	Для каждого СтрокаТЗ Из ВременнаяТаблица Цикл
		СтрокаТЗ.ДатаСправкиБеременнойСтрокой = Формат(СтрокаТЗ.ДатаСправкиБеременной,"ДФ=dd.MM.yyyy")
	КонецЦикла;
	Запрос.УстановитьПараметр("ВременнаяТаблица", ВременнаяТаблица);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Пособия.ДатаСправкиБеременной,
	|	Пособия.ДатаСправкиБеременнойСтрокой
	|ПОМЕСТИТЬ ВТПереводДатВСтроки
	|ИЗ
	|	&ВременнаяТаблица КАК Пособия";
	Запрос.Выполнить();
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ГОД(ПредельнаяВеличинаБазыСтраховыхВзносов.Период) КАК Год,
	|	ПредельнаяВеличинаБазыСтраховыхВзносов.Размер
	|ПОМЕСТИТЬ ВТПредельныеВеличиныПоГодам
	|ИЗ
	|	РегистрСведений.ПредельнаяВеличинаБазыСтраховыхВзносов КАК ПредельнаяВеличинаБазыСтраховыхВзносов
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Пособия.Фамилия + "" "" + Пособия.Имя + "" "" + Пособия.Отчество КАК ФИОСотрудника,
	|	Пособия.Ссылка,
	|	Пособия.НомерСтроки КАК НомерСтроки,
	|	Пособия.Сотрудник,
	|	Пособия.ФизЛицо,
	|	Пособия.ЛистокНетрудоспособности,
	|	Пособия.ИнаяПолезнаяИнформация,
	|	Пособия.КоличествоСтраниц,
	|	Пособия.ПеречислятьПособиеПочтовымПереводом,
	|	Пособия.НаименованиеБанка,
	|	Пособия.БИКБанка,
	|	Пособия.КоррСчетБанка,
	|	Пособия.НомерЛицевогоСчета,
	|	Пособия.АдресРегистрации,
	|	Пособия.АдресПочтовый,
	|	Пособия.ДатаВыходаНаРаботу,
	|	Пособия.УстановленаИзмененаГруппаИнвалидности,
	|	Пособия.ДатаРегистрацииДокументовМСЭ,
	|	Пособия.КодНарушенияРежима,
	|	Пособия.ПериодНахожденияВСтационареСРебенкомПо,
	|	Пособия.ПериодНахожденияВСтационареСРебенкомС,
	|	Пособия.ДатаСправкиБеременной,
	|	Пособия.НомерСправкиБеременной,
	|	Пособия.ДатаИзмененияКодаПричиныНетрудоспособности,
	|	Пособия.ВторойКодПричиныНетрудоспособности,
	|	Пособия.ДополнительныйКодПричиныНетрудоспособности,
	|	Пособия.КодПричиныНетрудоспособности,
	|	Пособия.ПоставленаНаУчетВРанниеСрокиБеременности,
	|	Пособия.ПериодПростояПо,
	|	Пособия.ПериодПростояС,
	|	Пособия.СреднийЗаработокНаПериодПростоя,
	|	Пособия.ДатаДействияДокумента,
	|	Пособия.ДатаВыдачиДокумента КАК ДатаВыдачиДокумента,
	|	Пособия.НомерДокумента КАК НомерДокумента,
	|	Пособия.СерияДокумента КАК СерияДокумента,
	|	Пособия.ВидДокумента,
	|	Пособия.Отчество,
	|	Пособия.Имя,
	|	Пособия.Фамилия,
	|	Пособия.СтажБезНестраховыхПериодовСтажМесяцев,
	|	Пособия.СтажБезНестраховыхПериодовЛет,
	|	Пособия.СтажМесяцев,
	|	Пособия.СтажЛет,
	|	Пособия.УсловияИсчисленияКод3,
	|	Пособия.УсловияИсчисленияКод2,
	|	Пособия.УсловияИсчисленияКод1,
	|	Пособия.ПорядокРасчетаСреднегоЗаработка,
	|	Пособия.ИНН,
	|	Пособия.СтраховойНомерПФР,
	|	Пособия.НомерВходящегоДокумента,
	|	Пособия.ДатаВыдачиБольничного,
	|	Пособия.НомерПервичногоБольничного,
	|	Пособия.ДатаОкончанияОплаты,
	|	Пособия.УважительнаяПричинаНарушенияРежима,
	|	Пособия.ДатаНарушенияРежима,
	|	Пособия.ДатаНачалаОплаты,
	|	Пособия.ПервыйГодРасчета,
	|	Пособия.ВторойГодРасчета,
	|	Пособия.КемВыданДокумент,
	|	Пособия.ДатаНовыйСтатусНетрудопособного,
	|	Пособия.НовыйСтатусНетрудопособного,
	|	Пособия.ДатаОсвидетельствованияМСЭ,
	|	Пособия.ДатаНаправленияВБюроМСЭ,
	|	Пособия.НомерЛисткаПродолжения,
	|	Пособия.ОГРН_Санатория,
	|	Пособия.НомерПутевки,
	|	Пособия.ДатаОкончанияПутевки,
	|	Пособия.НомерЛисткаПоОсновномуМестуРаботы,
	|	Пособия.ОГРН_ЛПУ,
	|	Пособия.НаименованиеЛПУ,
	|	Пособия.АдресЛПУ,
	|	Пособия.ЯвляетсяДубликатом,
	|	Пособия.Банк,
	|	Пособия.Пол,
	|	Пособия.ДатаРождения,
	|	Пособия.РайонныйКоэффициент,
	|	ВЫБОР
	|		КОГДА Пособия.ПорядокРасчетаСреднегоЗаработка = 2
	|				ИЛИ Пособия.ПорядокРасчетаСреднегоЗаработка = 3
	|			ТОГДА НАЧАЛОПЕРИОДА(Пособия.ДатаНачалаСобытия, МЕСЯЦ)
	|		ИНАЧЕ Пособия.ПериодРасчетаСреднегоЗаработкаНачало
	|	КОНЕЦ КАК ПериодРасчетаСреднегоЗаработкаНачало,
	|	ВЫБОР
	|		КОГДА (Пособия.ПорядокРасчетаСреднегоЗаработка = 2
	|				ИЛИ Пособия.ПорядокРасчетаСреднегоЗаработка = 3)
	|				И Пособия.ДатаНачалаСобытия <> ДАТАВРЕМЯ(1, 1, 1)
	|			ТОГДА КОНЕЦПЕРИОДА(Пособия.ДатаНачалаСобытия, МЕСЯЦ)
	|		ИНАЧЕ Пособия.ПериодРасчетаСреднегоЗаработкаОкончание
	|	КОНЕЦ КАК ПериодРасчетаСреднегоЗаработкаОкончание,
	|	ВЫБОР
	|		КОГДА (Пособия.ПорядокРасчетаСреднегоЗаработка = 2
	|				ИЛИ Пособия.ПорядокРасчетаСреднегоЗаработка = 3)
	|				И Пособия.ДатаНачалаСобытия <> ДАТАВРЕМЯ(1, 1, 1)
	|			ТОГДА ДЕНЬ(КОНЕЦПЕРИОДА(Пособия.ДатаНачалаСобытия, МЕСЯЦ))
	|		ИНАЧЕ Пособия.КалендарныхДнейРасчетногоПериода
	|	КОНЕЦ КАК КалендарныхДнейРасчетногоПериода,
	|	Пособия.УходЗаРодственникомЛет1,
	|	Пособия.УходЗаРодственникомЛет2,
	|	Пособия.УходЗаРодственникомМесяцев1,
	|	Пособия.УходЗаРодственникомМесяцев2,
	|	Пособия.ЗамененныйПервыйГодРасчета,
	|	Пособия.ЗамененныйВторойГодРасчета,
	|	Пособия.ОсвобождениеС1,
	|	Пособия.ОсвобождениеС2,
	|	Пособия.ОсвобождениеС3,
	|	Пособия.ОсвобождениеПо1,
	|	Пособия.ОсвобождениеПо2,
	|	Пособия.ОсвобождениеПо3,
	|	Пособия.ДолжностьВрача1,
	|	Пособия.ДолжностьВрача2,
	|	Пособия.ДолжностьВрача3,
	|	Пособия.ФИОВрача1,
	|	Пособия.ФИОВрача2,
	|	Пособия.ФИОВрача3,
	|	Пособия.УходЗаРодственникомФИО1,
	|	Пособия.УходЗаРодственникомФИО2,
	|	Пособия.УходЗаРодственникомРодственнаяСвязь1,
	|	Пособия.УходЗаРодственникомРодственнаяСвязь2,
	|	Пособия.Район,
	|	ВЫБОР
	|		КОГДА Пособия.ЗаработокПервыйГодРасчета > ПредельнаяВеличинаЗаПервыйГодРасчета.Размер
	|			ТОГДА ПредельнаяВеличинаЗаПервыйГодРасчета.Размер
	|		ИНАЧЕ Пособия.ЗаработокПервыйГодРасчета
	|	КОНЕЦ КАК ЗаработокПервыйГодРасчета,
	|	ВЫБОР
	|		КОГДА Пособия.ЗаработокВторойГодРасчета > ПредельнаяВеличинаЗаВторойГодРасчета.Размер
	|			ТОГДА ПредельнаяВеличинаЗаВторойГодРасчета.Размер
	|		ИНАЧЕ Пособия.ЗаработокВторойГодРасчета
	|	КОНЕЦ КАК ЗаработокВторойГодРасчета,
	|	Пособия.ЗаработокЗаПериодРасчета,
	|	Пособия.ТарифнаяСтавкаОклад,
	|	Пособия.ДатаПредставленияПакетаДокументов,
	|	Пособия.ДатаЗаявленияОЗаменеРасчетногоГода,
	|	ВЫБОР
	|		КОГДА Пособия.ВидПособия = ЗНАЧЕНИЕ(Перечисление.ВидыПособийСоциальногоСтрахования.БеременностьРоды)
	|			ТОГДА ""2""
	|		КОГДА Пособия.ВидПособия = ЗНАЧЕНИЕ(Перечисление.ВидыПособийСоциальногоСтрахования.ПриПостановкеНаУчетВРанниеСрокиБеременности)
	|			ТОГДА ""3""
	|		ИНАЧЕ ""1""
	|	КОНЕЦ КАК ВидВыплаты,
	|	Пособия.ВидПособия,
	|	Пособия.Совместительство,
	|	Пособия.СреднийДневнойЗаработок,
	|	Пособия.МРОТ,
	|	Пособия.ДоляВремени,
	|	Пособия.СрочныйДоговорС,
	|	Пособия.СрочныйДоговорПо,
	|	Пособия.УходЗаРодственникомИспользованоДней1,
	|	Пособия.УходЗаРодственникомИспользованоДней2,
	|	ВЫБОР
	|		КОГДА Пособия.ВидПособия = ЗНАЧЕНИЕ(Перечисление.ВидыПособийСоциальногоСтрахования.ПриПостановкеНаУчетВРанниеСрокиБеременности)
	|			ТОГДА ""Заявление о выплате пособия, Справка о постановке на учет от "" + (ВЫРАЗИТЬ(ЕСТЬNULL(ПереводДатВСтроки.ДатаСправкиБеременнойСтрокой, """") КАК СТРОКА(10))) + "" № "" + Пособия.НомерСправкиБеременной
	|		ИНАЧЕ ""Заявление о выплате пособия, Листок нетруд-ти № "" + Пособия.НомерВходящегоДокумента
	|	КОНЕЦ КАК ДокументыОснования,
	|	Пособия.ВидДокумента.КодИМНС КАК ВидУдостоверенияЛичности,
	|	Пособия.ГруппаИнвалидности,
	|	Пособия.СтатусНалогоплательщика КАК СтатусНалогоплательщикаДляПроверки,
	|	ВЫБОР
	|		КОГДА Пособия.СтатусНалогоплательщика = ЗНАЧЕНИЕ(Перечисление.СтатусыНалогоплательщиковПоНДФЛ.Резидент)
	|			ТОГДА 1
	|		КОГДА Пособия.СтатусНалогоплательщика = ЗНАЧЕНИЕ(Перечисление.СтатусыНалогоплательщиковПоНДФЛ.ВысококвалифицированныйИностранныйСпециалист)
	|			ТОГДА 2
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СтатусНалогоплательщика,
	|	Пособия.ИдентификационныйНомерВрача1,
	|	Пособия.ИдентификационныйНомерВрача2,
	|	Пособия.ИдентификационныйНомерВрача3,
	|	Пособия.ФИОВрачаПредседателяВК1,
	|	Пособия.ФИОВрачаПредседателяВК2,
	|	Пособия.ФИОВрачаПредседателяВК3,
	|	Пособия.ДолжностьВрачаПредседателяВК1,
	|	Пособия.ДолжностьВрачаПредседателяВК2,
	|	Пособия.ДолжностьВрачаПредседателяВК3,
	|	Пособия.ИдентификационныйНомерВрачаПредседателяВК1,
	|	Пособия.ИдентификационныйНомерВрачаПредседателяВК2,
	|	Пособия.ИдентификационныйНомерВрачаПредседателяВК3,
	|	Пособия.НаименованиеДокументаНаПроживание,
	|	Пособия.СерияДокументаНаПроживание,
	|	Пособия.НомерДокументаНаПроживание,
	|	Пособия.ДатаВыдачиДокументаНаПроживание,
	|	Пособия.АдресПочтовыйКодПоКЛАДР,
	|	Пособия.АдресРегистрацииКодПоКЛАДР,
	|	Пособия.АдресЛПУКодПоКЛАДР,
	|	Пособия.ВыплатаЗаСчетФедеральногоБюджета,
	|	Пособия.КодПричиныПерерасчета,
	|	Пособия.Перерасчет
	|ИЗ
	|	Документ.РеестрСведенийВФССОПособияхПоНетрудоспособности.РаботникиОрганизации КАК Пособия
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТПредельныеВеличиныПоГодам КАК ПредельнаяВеличинаЗаПервыйГодРасчета
	|		ПО (ВЫБОР
	|				КОГДА Пособия.ПервыйГодРасчета < 2010
	|					ТОГДА 2010
	|				ИНАЧЕ Пособия.ПервыйГодРасчета
	|			КОНЕЦ = ПредельнаяВеличинаЗаПервыйГодРасчета.Год)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТПредельныеВеличиныПоГодам КАК ПредельнаяВеличинаЗаВторойГодРасчета
	|		ПО (ВЫБОР
	|				КОГДА Пособия.ВторойГодРасчета < 2010
	|					ТОГДА 2010
	|				ИНАЧЕ Пособия.ВторойГодРасчета
	|			КОНЕЦ = ПредельнаяВеличинаЗаВторойГодРасчета.Год)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТПереводДатВСтроки КАК ПереводДатВСтроки
	|		ПО Пособия.ДатаСправкиБеременной = ПереводДатВСтроки.ДатаСправкиБеременной
	|ГДЕ
	|	Пособия.Ссылка = &ДокументСсылка";
	
	Если Режим <> "НеВыделятьПособиеПриПостановкеНаУчетВРанниеСрокиБеременности" Тогда
		
		ТекстЗапроса = ТекстЗапроса + "
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|" + 
		"ВЫБРАТЬ
		|	Пособия.Фамилия + "" "" + Пособия.Имя + "" "" + Пособия.Отчество КАК ФИОСотрудника,
		|	Пособия.Ссылка,
		|	Пособия.НомерСтроки КАК НомерСтроки,
		|	Пособия.Сотрудник,
		|	Пособия.ФизЛицо,
		|	Пособия.ЛистокНетрудоспособности,
		|	Пособия.ИнаяПолезнаяИнформация,
		|	4 КАК КоличествоСтраниц,
		|	Пособия.ПеречислятьПособиеПочтовымПереводом,
		|	Пособия.НаименованиеБанка,
		|	Пособия.БИКБанка,
		|	Пособия.КоррСчетБанка,
		|	Пособия.НомерЛицевогоСчета,
		|	Пособия.АдресРегистрации,
		|	Пособия.АдресПочтовый,
		|	Пособия.ДатаВыходаНаРаботу,
		|	Пособия.УстановленаИзмененаГруппаИнвалидности,
		|	Пособия.ДатаРегистрацииДокументовМСЭ,
		|	Пособия.КодНарушенияРежима,
		|	Пособия.ПериодНахожденияВСтационареСРебенкомПо,
		|	Пособия.ПериодНахожденияВСтационареСРебенкомС,
		|	Пособия.ДатаСправкиБеременной,
		|	Пособия.НомерСправкиБеременной,
		|	Пособия.ДатаИзмененияКодаПричиныНетрудоспособности,
		|	Пособия.ВторойКодПричиныНетрудоспособности,
		|	Пособия.ДополнительныйКодПричиныНетрудоспособности,
		|	Пособия.КодПричиныНетрудоспособности,
		|	Пособия.ПоставленаНаУчетВРанниеСрокиБеременности,
		|	Пособия.ПериодПростояПо,
		|	Пособия.ПериодПростояС,
		|	Пособия.СреднийЗаработокНаПериодПростоя,
		|	Пособия.ДатаДействияДокумента,
		|	Пособия.ДатаВыдачиДокумента,
		|	Пособия.НомерДокумента,
		|	Пособия.СерияДокумента,
		|	Пособия.ВидДокумента,
		|	Пособия.Отчество,
		|	Пособия.Имя,
		|	Пособия.Фамилия,
		|	Пособия.СтажБезНестраховыхПериодовСтажМесяцев,
		|	Пособия.СтажБезНестраховыхПериодовЛет,
		|	Пособия.СтажМесяцев,
		|	Пособия.СтажЛет,
		|	Пособия.УсловияИсчисленияКод3,
		|	Пособия.УсловияИсчисленияКод2,
		|	Пособия.УсловияИсчисленияКод1,
		|	Пособия.ПорядокРасчетаСреднегоЗаработка,
		|	Пособия.ИНН,
		|	Пособия.СтраховойНомерПФР,
		|	Пособия.НомерВходящегоДокумента,
		|	Пособия.ДатаВыдачиБольничного,
		|	Пособия.НомерПервичногоБольничного,
		|	Пособия.ДатаОкончанияОплаты,
		|	Пособия.УважительнаяПричинаНарушенияРежима,
		|	Пособия.ДатаНарушенияРежима,
		|	Пособия.ДатаНачалаОплаты,
		|	Пособия.ПервыйГодРасчета,
		|	Пособия.ВторойГодРасчета,
		|	Пособия.КемВыданДокумент,
		|	Пособия.ДатаНовыйСтатусНетрудопособного,
		|	Пособия.НовыйСтатусНетрудопособного,
		|	Пособия.ДатаОсвидетельствованияМСЭ,
		|	Пособия.ДатаНаправленияВБюроМСЭ,
		|	Пособия.НомерЛисткаПродолжения,
		|	Пособия.ОГРН_Санатория,
		|	Пособия.НомерПутевки,
		|	Пособия.ДатаОкончанияПутевки,
		|	Пособия.НомерЛисткаПоОсновномуМестуРаботы,
		|	Пособия.ОГРН_ЛПУ,
		|	Пособия.НаименованиеЛПУ,
		|	Пособия.АдресЛПУ,
		|	Пособия.ЯвляетсяДубликатом,
		|	Пособия.Банк,
		|	Пособия.Пол,
		|	Пособия.ДатаРождения,
		|	Пособия.РайонныйКоэффициент,
		|	Пособия.ПериодРасчетаСреднегоЗаработкаНачало,
		|	Пособия.ПериодРасчетаСреднегоЗаработкаОкончание,
		|	Пособия.КалендарныхДнейРасчетногоПериода,
		|	Пособия.УходЗаРодственникомЛет1,
		|	Пособия.УходЗаРодственникомЛет2,
		|	Пособия.УходЗаРодственникомМесяцев1,
		|	Пособия.УходЗаРодственникомМесяцев2,
		|	Пособия.ЗамененныйПервыйГодРасчета,
		|	Пособия.ЗамененныйВторойГодРасчета,
		|	Пособия.ОсвобождениеС1,
		|	Пособия.ОсвобождениеС2,
		|	Пособия.ОсвобождениеС3,
		|	Пособия.ОсвобождениеПо1,
		|	Пособия.ОсвобождениеПо2,
		|	Пособия.ОсвобождениеПо3,
		|	Пособия.ДолжностьВрача1,
		|	Пособия.ДолжностьВрача2,
		|	Пособия.ДолжностьВрача3,
		|	Пособия.ФИОВрача1,
		|	Пособия.ФИОВрача2,
		|	Пособия.ФИОВрача3,
		|	Пособия.УходЗаРодственникомФИО1,
		|	Пособия.УходЗаРодственникомФИО2,
		|	Пособия.УходЗаРодственникомРодственнаяСвязь1,
		|	Пособия.УходЗаРодственникомРодственнаяСвязь2,
		|	Пособия.Район,
		|	0 КАК ЗаработокПервыйГодРасчета,
		|	0 КАК ЗаработокВторойГодРасчета,
		|	Пособия.ЗаработокЗаПериодРасчета,
		|	Пособия.ТарифнаяСтавкаОклад,
		|	Пособия.ДатаПредставленияПакетаДокументов,
		|	Пособия.ДатаЗаявленияОЗаменеРасчетногоГода,
		|	""3"" КАК ВидВыплаты,
		|	Пособия.ВидПособия,
		|	Пособия.Совместительство,
		|	Пособия.СреднийДневнойЗаработок,
		|	Пособия.МРОТ,
		|	Пособия.ДоляВремени,
		|	Пособия.СрочныйДоговорС,
		|	Пособия.СрочныйДоговорПо,
		|	Пособия.УходЗаРодственникомИспользованоДней1,
		|	Пособия.УходЗаРодственникомИспользованоДней2,
		|	""Заявление о выплате пособия, Справка о постановке на учет от "" + (ВЫРАЗИТЬ(ЕСТЬNULL(ПереводДатВСтроки.ДатаСправкиБеременнойСтрокой, """") КАК СТРОКА(10))) + "" № "" + Пособия.НомерСправкиБеременной КАК ДокументыОснования,
		|	Пособия.ВидДокумента.КодИМНС,
		|	Пособия.ГруппаИнвалидности,
		|	Пособия.СтатусНалогоплательщика,
		|	ВЫБОР
		|		КОГДА Пособия.СтатусНалогоплательщика = ЗНАЧЕНИЕ(Перечисление.СтатусыНалогоплательщиковПоНДФЛ.Резидент)
		|			ТОГДА 1
		|		КОГДА Пособия.СтатусНалогоплательщика = ЗНАЧЕНИЕ(Перечисление.СтатусыНалогоплательщиковПоНДФЛ.ВысококвалифицированныйИностранныйСпециалист)
		|			ТОГДА 2
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК СтатусНалогоплательщика,
		|	Пособия.ИдентификационныйНомерВрача1,
		|	Пособия.ИдентификационныйНомерВрача2,
		|	Пособия.ИдентификационныйНомерВрача3,
		|	Пособия.ФИОВрачаПредседателяВК1,
		|	Пособия.ФИОВрачаПредседателяВК2,
		|	Пособия.ФИОВрачаПредседателяВК3,
		|	Пособия.ДолжностьВрачаПредседателяВК1,
		|	Пособия.ДолжностьВрачаПредседателяВК2,
		|	Пособия.ДолжностьВрачаПредседателяВК3,
		|	Пособия.ИдентификационныйНомерВрачаПредседателяВК1,
		|	Пособия.ИдентификационныйНомерВрачаПредседателяВК2,
		|	Пособия.ИдентификационныйНомерВрачаПредседателяВК3,
		|	Пособия.НаименованиеДокументаНаПроживание,
		|	Пособия.СерияДокументаНаПроживание,
		|	Пособия.НомерДокументаНаПроживание,
		|	Пособия.ДатаВыдачиДокументаНаПроживание,
		|	Пособия.АдресПочтовыйКодПоКЛАДР,
		|	Пособия.АдресРегистрацииКодПоКЛАДР,
		|	Пособия.АдресЛПУКодПоКЛАДР,
		|	Пособия.ВыплатаЗаСчетФедеральногоБюджета,
		|	Пособия.КодПричиныПерерасчета,
		|	Пособия.Перерасчет
		|ИЗ
		|	Документ.РеестрСведенийВФССОПособияхПоНетрудоспособности.РаботникиОрганизации КАК Пособия
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТПереводДатВСтроки КАК ПереводДатВСтроки
		|		ПО Пособия.ДатаСправкиБеременной = ПереводДатВСтроки.ДатаСправкиБеременной
		|ГДЕ
		|	Пособия.Ссылка = &ДокументСсылка
		|	И Пособия.ПоставленаНаУчетВРанниеСрокиБеременности
		|	И Пособия.ВидПособия = ЗНАЧЕНИЕ(Перечисление.ВидыПособийСоциальногоСтрахования.БеременностьРоды)";
	КонецЕсли;
	
	ТекстЗапроса = ТекстЗапроса + "
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки,
	|	ВидВыплаты";
		
	Запрос.Текст = ТекстЗапроса;

	Возврат Запрос.Выполнить();

КонецФункции // СформироватьЗапросПоТЧРаботникиОрганизации()
 
// Проверяет заполненность полей табличной части "РаботникиОрганизации"
// Проверка ведется по строке выборки по табличной части
//
// Параметры
// Параметры: 
//	СтрокаВыборкиПоТЧРаботникиОрганизации	- выборка из результата запрос
//	Отказ 									- флаг отказа
//	Заголовок								- Заголовок для сообщений об ошибках
//
Процедура ПроверитьЗаполнениеСтрокиТЧРаботникиОрганизации(ВыборкаПоСтрокамДокумента, Отказ, Заголовок)

	ТекстОшибкиСНомеромСтроки = НСтр("ru='В строке номер %НомерСтроки% табличной части ""Пособия"": '");
	ТекстОшибкиСНомеромСтроки = СтрЗаменить(ТекстОшибкиСНомеромСтроки, "%НомерСтроки%", ВыборкаПоСтрокамДокумента.НомерСтроки);
	
	Если ВыборкаПоСтрокамДокумента.ВидПособия = Перечисления.ВидыПособийСоциальногоСтрахования.ПриПостановкеНаУчетВРанниеСрокиБеременности Тогда
		
		ОбменСведениямиОПособияхСФСС.ПроверитьДанныеЗастрахованного(ВыборкаПоСтрокамДокумента, Отказ, Заголовок, ТекстОшибкиСНомеромСтроки);
		
		Если Не ЗначениеЗаполнено(ВыборкаПоСтрокамДокумента.ДатаСправкиБеременной) Тогда
			ОбщегоНазначения.ВывестиИнформациюОбОшибке(ТекстОшибкиСНомеромСтроки + "Не указана дата выдачи справки о постановке на учет в медицинских учреждениях в ранние сроки беременности!", Отказ, Заголовок);
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(ВыборкаПоСтрокамДокумента.НомерСправкиБеременной) Тогда
			ОбщегоНазначения.ВывестиИнформациюОбОшибке(ТекстОшибкиСНомеромСтроки + "Не указан номер справки о постановке на учет в медицинских учреждениях в ранние сроки беременности!", Отказ, Заголовок);
		КонецЕсли;
		
		Возврат	
		
	ИначеЕсли ВыборкаПоСтрокамДокумента.ВидВыплаты = "3" Тогда
		
		Возврат		
		
	КонецЕсли;
	
	ДлинаСуток = 86400;
	
	ОбменСведениямиОПособияхСФСС.ПроверитьДанныеЗастрахованного(ВыборкаПоСтрокамДокумента, Отказ, Заголовок, ТекстОшибкиСНомеромСтроки);
	
	Если ВыборкаПоСтрокамДокумента.Перерасчет И Не ЗначениеЗаполнено(ВыборкаПоСтрокамДокумента.КодПричиныПерерасчета) Тогда
		ОбщегоНазначения.ВывестиИнформациюОбОшибке(ТекстОшибкиСНомеромСтроки + "Для случаев перерасчета пособия необходимо указать код причины перерасчета!", Отказ, Заголовок);
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ВыборкаПоСтрокамДокумента.ВидПособия) Тогда
		ОбщегоНазначения.ВывестиИнформациюОбОшибке(ТекстОшибкиСНомеромСтроки + "Не указан вид пособия!", Отказ, Заголовок);
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ВыборкаПоСтрокамДокумента.НомерВходящегоДокумента) Тогда
		ОбщегоНазначения.ВывестиИнформациюОбОшибке(ТекстОшибкиСНомеромСтроки + "Не указан номер листка нетрудоспособности!", Отказ, Заголовок);
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ВыборкаПоСтрокамДокумента.ДатаВыдачиБольничного) Тогда
		ОбщегоНазначения.ВывестиИнформациюОбОшибке(ТекстОшибкиСНомеромСтроки + "Не указана дата выдачи листка нетрудоспособности!", Отказ, Заголовок);
	КонецЕсли;
	
	Если ВыборкаПоСтрокамДокумента.Совместительство И Не ЗначениеЗаполнено(ВыборкаПоСтрокамДокумента.НомерЛисткаПоОсновномуМестуРаботы) Тогда
		ОбщегоНазначения.ВывестиИнформациюОбОшибке(ТекстОшибкиСНомеромСтроки + "Для совместителя должен быть указан номер листка нетрудоспособности, предъявляемого на основном месте работы!", Отказ, Заголовок);
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ВыборкаПоСтрокамДокумента.КодПричиныНетрудоспособности) Тогда
		ОбщегоНазначения.ВывестиИнформациюОбОшибке(ТекстОшибкиСНомеромСтроки + "Не указан код причины нетрудоспособности!", Отказ, Заголовок);
	КонецЕсли;
	
	Если ВыборкаПоСтрокамДокумента.ВидПособия = Перечисления.ВидыПособийСоциальногоСтрахования.БеременностьРоды Тогда
		Если ВыборкаПоСтрокамДокумента.КодПричиныНетрудоспособности <> "05" Тогда
			ОбщегоНазначения.ВывестиИнформациюОбОшибке(ТекстОшибкиСНомеромСтроки + "Для пособия по беременности и родам должен быть указан код причины нетрудоспособности 05!", Отказ, Заголовок);
		КонецЕсли;
		Если Не ЗначениеЗаполнено(ВыборкаПоСтрокамДокумента.ДатаИзмененияКодаПричиныНетрудоспособности) Тогда
			ОбщегоНазначения.ВывестиИнформациюОбОшибке(ТекстОшибкиСНомеромСтроки + "Для пособия по беременности и родам в поле ""Дата 1"" должна быть указана предполагаемая дата родов!", Отказ, Заголовок);
		КонецЕсли;
		Если ВыборкаПоСтрокамДокумента.ПоставленаНаУчетВРанниеСрокиБеременности Тогда
			Если Не ЗначениеЗаполнено(ВыборкаПоСтрокамДокумента.ДатаСправкиБеременной) Тогда
				ОбщегоНазначения.ВывестиИнформациюОбОшибке(ТекстОшибкиСНомеромСтроки + "Не указана дата выдачи справки о постановке на учет в медицинских учреждениях в ранние сроки беременности!", Отказ, Заголовок);
			КонецЕсли;
			Если Не ЗначениеЗаполнено(ВыборкаПоСтрокамДокумента.НомерСправкиБеременной) Тогда
				ОбщегоНазначения.ВывестиИнформациюОбОшибке(ТекстОшибкиСНомеромСтроки + "Не указан номер справки о постановке на учет в медицинских учреждениях в ранние сроки беременности!", Отказ, Заголовок);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ВыборкаПоСтрокамДокумента.ВторойКодПричиныНетрудоспособности) И Не ЗначениеЗаполнено(ВыборкаПоСтрокамДокумента.ДатаИзмененияКодаПричиныНетрудоспособности) Тогда
		ОбщегоНазначения.ВывестиИнформациюОбОшибке(ТекстОшибкиСНомеромСтроки + "Если изменен код причины нетрудоспособности, в поле ""Дата 1"" должна быть указана дата этого изменения!", Отказ, Заголовок);
	КонецЕсли;
	
	
	Если Не ЗначениеЗаполнено(ВыборкаПоСтрокамДокумента.НаименованиеЛПУ) Тогда
		ОбщегоНазначения.ВывестиИнформациюОбОшибке(ТекстОшибкиСНомеромСтроки + "Не указано наименование ЛПУ!", Отказ, Заголовок);
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ВыборкаПоСтрокамДокумента.ОГРН_ЛПУ) Тогда
		ОбщегоНазначения.ВывестиИнформациюОбОшибке(ТекстОшибкиСНомеромСтроки + "Не указан ОГРН ЛПУ!", Отказ, Заголовок);
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ВыборкаПоСтрокамДокумента.АдресЛПУ) Тогда
		ОбщегоНазначения.ВывестиИнформациюОбОшибке(ТекстОшибкиСНомеромСтроки + "Не указан адрес ЛПУ!", Отказ, Заголовок);
	Иначе	
		СтруктураАдреса = УправлениеКонтактнойИнформацией.ПолучитьСтруктуруАдресаИзСтроки(ВыборкаПоСтрокамДокумента.АдресЛПУ);	
		Если СтруктураАдреса.Свойство("ЗаПределамиРФ") Тогда
			ОбщегоНазначения.ВывестиИнформациюОбОшибке(ТекстОшибкиСНомеромСтроки + "Не удалось разобрать адрес ЛПУ, возможно указан адрес за пределами РФ!", Отказ, Заголовок);
		ИначеЕсли Не ЗначениеЗаполнено(СтруктураАдреса.Индекс) Тогда
			ОбщегоНазначения.ВывестиИнформациюОбОшибке(ТекстОшибкиСНомеромСтроки + "В адресе ЛПУ не указан почтовый индекс!", Отказ, Заголовок);
		КонецЕсли;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ВыборкаПоСтрокамДокумента.АдресЛПУКодПоКЛАДР) Тогда
		ОбщегоНазначения.ВывестиИнформациюОбОшибке(ТекстОшибкиСНомеромСтроки + "Не указан код по КЛАДР адреса ЛПУ!", Отказ, Заголовок);
	ИначеЕсли Не ОбщегоНазначения.ТолькоЦифрыВСтроке(ВыборкаПоСтрокамДокумента.АдресЛПУКодПоКЛАДР) Или СтрДлина(ВыборкаПоСтрокамДокумента.АдресЛПУКодПоКЛАДР) < 17 Тогда
		ОбщегоНазначения.ВывестиИнформациюОбОшибке(ТекстОшибкиСНомеромСтроки + "Код по КЛАДР адреса ЛПУ должен состоять из 17 цифр", Отказ, Заголовок);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ВыборкаПоСтрокамДокумента.ДатаИзмененияКодаПричиныНетрудоспособности) И Найти(",017,018,019,", "," + ВыборкаПоСтрокамДокумента.ДополнительныйКодПричиныНетрудоспособности + ",") > 0 Тогда
		Если Не ЗначениеЗаполнено(ВыборкаПоСтрокамДокумента.ДатаОкончанияПутевки) Тогда
			ОбщегоНазначения.ВывестиИнформациюОбОшибке(ТекстОшибкиСНомеромСтроки + "Если указана дата начала санаторной путевки, то в поле ""Дата 2"" должна быть указана дата окончания путевки!", Отказ, Заголовок);
		КонецЕсли;
		Если Не ЗначениеЗаполнено(ВыборкаПоСтрокамДокумента.НомерПутевки) Тогда
			ОбщегоНазначения.ВывестиИнформациюОбОшибке(ТекстОшибкиСНомеромСтроки + "Если указана дата начала санаторной путевки, то должен быть указан ее номер!", Отказ, Заголовок);
		КонецЕсли;
		Если Не ЗначениеЗаполнено(ВыборкаПоСтрокамДокумента.ОГРН_Санатория) Тогда
			ОбщегоНазначения.ВывестиИнформациюОбОшибке(ТекстОшибкиСНомеромСтроки + "Если указана дата начала санаторной путевки, то должен быть указан ОГРН санатория!", Отказ, Заголовок);
		КонецЕсли;
	КонецЕсли;
	
	СтрокаКодовУхода = ",09,12,13,14,15,";
	ПроверятьДанныеБольногоЧленаСемьи = ЗначениеЗаполнено(ВыборкаПоСтрокамДокумента.КодПричиныНетрудоспособности) И Найти(СтрокаКодовУхода, "," + ВыборкаПоСтрокамДокумента.КодПричиныНетрудоспособности + ",") > 0
					Или ЗначениеЗаполнено(ВыборкаПоСтрокамДокумента.ВторойКодПричиныНетрудоспособности) И Найти(СтрокаКодовУхода, "," + ВыборкаПоСтрокамДокумента.ВторойКодПричиныНетрудоспособности + ",") > 0;
	
	Если ПроверятьДанныеБольногоЧленаСемьи Тогда
		Если Не ЗначениеЗаполнено(ВыборкаПоСтрокамДокумента.УходЗаРодственникомЛет1 + ВыборкаПоСтрокамДокумента.УходЗаРодственникомМесяцев1) Или Не ЗначениеЗаполнено(ВыборкаПоСтрокамДокумента.УходЗаРодственникомРодственнаяСвязь1) Или Не ЗначениеЗаполнено(ВыборкаПоСтрокамДокумента.УходЗаРодственникомФИО1) Тогда
			ОбщегоНазначения.ВывестиИнформациюОбОшибке(ТекстОшибкиСНомеромСтроки + "Для листка нетрудоспособности по уходу должны быть указаны данные больного, за которым осуществлялся уход!", Отказ, Заголовок);
		КонецЕсли;
		Если ЗначениеЗаполнено(ВыборкаПоСтрокамДокумента.УходЗаРодственникомФИО2) Тогда
			Если Не ЗначениеЗаполнено(ВыборкаПоСтрокамДокумента.УходЗаРодственникомЛет2 + ВыборкаПоСтрокамДокумента.УходЗаРодственникомМесяцев2) Тогда
				ОбщегоНазначения.ВывестиИнформациюОбОшибке(ТекстОшибкиСНомеромСтроки + "Если указано ФИО второго больного родственника, за которым осуществлялся уход, то должен быть указан его возраст!", Отказ, Заголовок);
			КонецЕсли;
			Если Не ЗначениеЗаполнено(ВыборкаПоСтрокамДокумента.УходЗаРодственникомРодственнаяСвязь2) Тогда
				ОбщегоНазначения.ВывестиИнформациюОбОшибке(ТекстОшибкиСНомеромСтроки + "Если указано ФИО второго больного родственника, за которым осуществлялся уход, то должна быть указана родственная связь!", Отказ, Заголовок);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ВыборкаПоСтрокамДокумента.ДатаНарушенияРежима) Или ЗначениеЗаполнено(ВыборкаПоСтрокамДокумента.КодНарушенияРежима) Тогда
		Если Не ЗначениеЗаполнено(ВыборкаПоСтрокамДокумента.ДатаНарушенияРежима) Тогда
			ОбщегоНазначения.ВывестиИнформациюОбОшибке(ТекстОшибкиСНомеромСтроки + "Указана дата нарушения режима, но не указан код нарушения режима!", Отказ, Заголовок);
		КонецЕсли;
		Если Не ЗначениеЗаполнено(ВыборкаПоСтрокамДокумента.КодНарушенияРежима) Тогда
			ОбщегоНазначения.ВывестиИнформациюОбОшибке(ТекстОшибкиСНомеромСтроки + "Указан код нарушения режима, но не указана дата нарушения режима!", Отказ, Заголовок);
		КонецЕсли;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ВыборкаПоСтрокамДокумента.ОсвобождениеС1) И Не ЗначениеЗаполнено(ВыборкаПоСтрокамДокумента.ОсвобождениеС2) И Не ЗначениеЗаполнено(ВыборкаПоСтрокамДокумента.ОсвобождениеС3) Тогда
		ОбщегоНазначения.ВывестиИнформациюОбОшибке(ТекстОшибкиСНомеромСтроки + "Должен быть указан хотя бы один период освобождения от работы!", Отказ, Заголовок);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ВыборкаПоСтрокамДокумента.ОсвобождениеС1) И Не ЗначениеЗаполнено(ВыборкаПоСтрокамДокумента.ОсвобождениеПо1) Тогда
		ОбщегоНазначения.ВывестиИнформациюОбОшибке(ТекстОшибкиСНомеромСтроки + "Не указана дата окончания первого периода освобождения от работы!", Отказ, Заголовок);
	ИначеЕсли ЗначениеЗаполнено(ВыборкаПоСтрокамДокумента.ОсвобождениеС1) И ЗначениеЗаполнено(ВыборкаПоСтрокамДокумента.ОсвобождениеПо1) И ВыборкаПоСтрокамДокумента.ОсвобождениеС1 > ВыборкаПоСтрокамДокумента.ОсвобождениеПо1 Тогда
		ОбщегоНазначения.ВывестиИнформациюОбОшибке(ТекстОшибкиСНомеромСтроки + "Неверно указаны даты начала и окончания первого периода освобождения от работы!", Отказ, Заголовок);
	ИначеЕсли ВыборкаПоСтрокамДокумента.ДатаВыдачиБольничного > ВыборкаПоСтрокамДокумента.ОсвобождениеС1 И Не ЗначениеЗаполнено(ВыборкаПоСтрокамДокумента.ПериодНахожденияВСтационареСРебенкомС) И Не ЗначениеЗаполнено(ВыборкаПоСтрокамДокумента.ПериодНахожденияВСтационареСРебенкомПо) Тогда
		ОбщегоНазначения.ВывестиИнформациюОбОшибке(ТекстОшибкиСНомеромСтроки + "Листок нетрудоспособности выдан позже начала освобождения от работы. Проверьте эти даты!", , Заголовок, Перечисления.ВидыСообщений.Информация);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ВыборкаПоСтрокамДокумента.ОсвобождениеС1) Тогда
		Если Не ЗначениеЗаполнено(ВыборкаПоСтрокамДокумента.ФИОВрача1) Тогда
			ОбщегоНазначения.ВывестиИнформациюОбОшибке(ТекстОшибкиСНомеромСтроки + "В строке первого периода освобождения от работы не указано ФИО врача!", Отказ, Заголовок);
		КонецЕсли;
		Если Не ЗначениеЗаполнено(ВыборкаПоСтрокамДокумента.ДолжностьВрача1) Тогда
			ОбщегоНазначения.ВывестиИнформациюОбОшибке(ТекстОшибкиСНомеромСтроки + "В строке первого периода освобождения от работы не указана должность врача!", Отказ, Заголовок);
		КонецЕсли;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ВыборкаПоСтрокамДокумента.ОсвобождениеС2) Тогда
		Если Не ЗначениеЗаполнено(ВыборкаПоСтрокамДокумента.ОсвобождениеПо2) Тогда
			ОбщегоНазначения.ВывестиИнформациюОбОшибке(ТекстОшибкиСНомеромСтроки + "Не указана дата окончания второго периода освобождения от работы!", Отказ, Заголовок);
		ИначеЕсли ЗначениеЗаполнено(ВыборкаПоСтрокамДокумента.ОсвобождениеПо2) И ВыборкаПоСтрокамДокумента.ОсвобождениеС2 > ВыборкаПоСтрокамДокумента.ОсвобождениеПо2 Тогда
			ОбщегоНазначения.ВывестиИнформациюОбОшибке(ТекстОшибкиСНомеромСтроки + "Неверно указаны даты начала и окончания второго периода освобождения от работы!", Отказ, Заголовок);
		ИначеЕсли ВыборкаПоСтрокамДокумента.ОсвобождениеПо1 + ДлинаСуток <> ВыборкаПоСтрокамДокумента.ОсвобождениеС2 Тогда
			ОбщегоНазначения.ВывестиИнформациюОбОшибке(ТекстОшибкиСНомеромСтроки + "Начало второго периода освобождения от работы должно непосредственно следовать за окончанием первого периода!", Отказ, Заголовок);
		КонецЕсли;
		Если Не ЗначениеЗаполнено(ВыборкаПоСтрокамДокумента.ФИОВрача2) Тогда
			ОбщегоНазначения.ВывестиИнформациюОбОшибке(ТекстОшибкиСНомеромСтроки + "В строке второго периода освобождения от работы не указано ФИО врача!", Отказ, Заголовок);
		КонецЕсли;
		Если Не ЗначениеЗаполнено(ВыборкаПоСтрокамДокумента.ДолжностьВрача2) Тогда
			ОбщегоНазначения.ВывестиИнформациюОбОшибке(ТекстОшибкиСНомеромСтроки + "В строке второго периода освобождения от работы не указана должность врача!", Отказ, Заголовок);
		КонецЕсли;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ВыборкаПоСтрокамДокумента.ОсвобождениеС3) Тогда
		Если Не ЗначениеЗаполнено(ВыборкаПоСтрокамДокумента.ОсвобождениеПо3) Тогда
			ОбщегоНазначения.ВывестиИнформациюОбОшибке(ТекстОшибкиСНомеромСтроки + "Не указана дата окончания третьего периода освобождения от работы!", Отказ, Заголовок);
		ИначеЕсли ЗначениеЗаполнено(ВыборкаПоСтрокамДокумента.ОсвобождениеПо3) И ВыборкаПоСтрокамДокумента.ОсвобождениеС3 > ВыборкаПоСтрокамДокумента.ОсвобождениеПо3 Тогда
			ОбщегоНазначения.ВывестиИнформациюОбОшибке(ТекстОшибкиСНомеромСтроки + "Неверно указаны даты начала и окончания третьего периода освобождения от работы!", Отказ, Заголовок);
		ИначеЕсли ВыборкаПоСтрокамДокумента.ОсвобождениеПо2 + ДлинаСуток <> ВыборкаПоСтрокамДокумента.ОсвобождениеС3 Тогда
			ОбщегоНазначения.ВывестиИнформациюОбОшибке(ТекстОшибкиСНомеромСтроки + "Начало третьего периода освобождения от работы должно непосредственно следовать за окончанием второго периода!", Отказ, Заголовок);
		КонецЕсли;
		Если Не ЗначениеЗаполнено(ВыборкаПоСтрокамДокумента.ФИОВрача3) Тогда
			ОбщегоНазначения.ВывестиИнформациюОбОшибке(ТекстОшибкиСНомеромСтроки + "В строке третьего периода освобождения от работы не указано ФИО врача!", Отказ, Заголовок);
		КонецЕсли;
		Если Не ЗначениеЗаполнено(ВыборкаПоСтрокамДокумента.ДолжностьВрача3) Тогда
			ОбщегоНазначения.ВывестиИнформациюОбОшибке(ТекстОшибкиСНомеромСтроки + "В строке третьего периода освобождения от работы не указана должность врача!", Отказ, Заголовок);
		КонецЕсли;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ВыборкаПоСтрокамДокумента.ДатаНачалаОплаты) Тогда
		ОбщегоНазначения.ВывестиИнформациюОбОшибке(ТекстОшибкиСНомеромСтроки + "Не указана дата начала периода выплаты пособия за счет ФСС!", Отказ, Заголовок);
	Иначе
		Если Не ЗначениеЗаполнено(ВыборкаПоСтрокамДокумента.ДатаОкончанияОплаты) Тогда
			ОбщегоНазначения.ВывестиИнформациюОбОшибке(ТекстОшибкиСНомеромСтроки + "Не указана дата окончания периода выплаты пособия за счет ФСС!", Отказ, Заголовок);
		Иначе
			Если ВыборкаПоСтрокамДокумента.ДатаНачалаОплаты > ВыборкаПоСтрокамДокумента.ДатаОкончанияОплаты Тогда
				ОбщегоНазначения.ВывестиИнформациюОбОшибке(ТекстОшибкиСНомеромСтроки + "Неверно указаны даты начала и окончания периода выплаты пособия за счет ФСС!", Отказ, Заголовок);
			Иначе
				Если ЗначениеЗаполнено(ВыборкаПоСтрокамДокумента.ОсвобождениеС1) И ВыборкаПоСтрокамДокумента.ОсвобождениеС1 > ВыборкаПоСтрокамДокумента.ДатаОкончанияОплаты Тогда
					ОбщегоНазначения.ВывестиИнформациюОбОшибке(ТекстОшибкиСНомеромСтроки + "Дата начала освобождения от работы не может быть позже даты начала периода выплаты пособия за счет ФСС!", Отказ, Заголовок);
				КонецЕсли;
				Если ВыборкаПоСтрокамДокумента.ДатаОкончанияОплаты > Макс(ВыборкаПоСтрокамДокумента.ОсвобождениеПо1, ВыборкаПоСтрокамДокумента.ОсвобождениеПо2, ВыборкаПоСтрокамДокумента.ОсвобождениеПо3) Тогда
					ОбщегоНазначения.ВывестиИнформациюОбОшибке(ТекстОшибкиСНомеромСтроки + "Дата окончания периода выплаты пособия за счет ФСС не может быть позже даты окончания периода освобождения от работы!", Отказ, Заголовок);
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ВыборкаПоСтрокамДокумента.НовыйСтатусНетрудопособного) Или ЗначениеЗаполнено(ВыборкаПоСтрокамДокумента.ДатаНовыйСтатусНетрудопособного) Тогда
		СтрокаКодовИное = ",32,33,34,36,";
		Если ЗначениеЗаполнено(ВыборкаПоСтрокамДокумента.НовыйСтатусНетрудопособного) И Найти(СтрокаКодовИное, "," + ВыборкаПоСтрокамДокумента.НовыйСтатусНетрудопособного + ",") > 0 И Не ЗначениеЗаполнено(ВыборкаПоСтрокамДокумента.ДатаНовыйСтатусНетрудопособного) Тогда
			ОбщегоНазначения.ВывестиИнформациюОбОшибке(ТекстОшибкиСНомеромСтроки + "В поле ""Иное"" указан код, требующий указания даты изменения состояния, однако дата изменения состояния нетрудоспособного не указана!", Отказ, Заголовок);
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(ВыборкаПоСтрокамДокумента.НовыйСтатусНетрудопособного) И ЗначениеЗаполнено(ВыборкаПоСтрокамДокумента.ДатаНовыйСтатусНетрудопособного) Тогда
			ОбщегоНазначения.ВывестиИнформациюОбОшибке(ТекстОшибкиСНомеромСтроки + "Указана дата изменения состояния нетрудоспособного, но не указан код в поле ""Иное""!", Отказ, Заголовок);
		КонецЕсли;
	Иначе
		 Если Не ЗначениеЗаполнено(ВыборкаПоСтрокамДокумента.ДатаВыходаНаРаботу) Тогда
			ОбщегоНазначения.ВывестиИнформациюОбОшибке(ТекстОшибкиСНомеромСтроки + "Не указана дата в поле ""Приступить к работе с:""!", Отказ, Заголовок);
		 КонецЕсли;
	КонецЕсли;
	
	Если Найти("," + ВыборкаПоСтрокамДокумента.УсловияИсчисленияКод1 + "," + ВыборкаПоСтрокамДокумента.УсловияИсчисленияКод2 + "," + ВыборкаПоСтрокамДокумента.УсловияИсчисленияКод3 + ",", ",43,") > 0 И Не ЗначениеЗаполнено(ВыборкаПоСтрокамДокумента.ВыплатаЗаСчетФедеральногоБюджета) Тогда
		ОбщегоНазначения.ВывестиИнформациюОбОшибке(ТекстОшибкиСНомеромСтроки + "Для лица, подвергшегося воздействию радиации, не указана причина этого воздействия!", Отказ, Заголовок);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ВыборкаПоСтрокамДокумента.ПериодНахожденияВСтационареСРебенкомС) Или ЗначениеЗаполнено(ВыборкаПоСтрокамДокумента.ПериодНахожденияВСтационареСРебенкомПо) Тогда
		
		Если ЗначениеЗаполнено(ВыборкаПоСтрокамДокумента.ПериодНахожденияВСтационареСРебенкомС) И Не ЗначениеЗаполнено(ВыборкаПоСтрокамДокумента.ПериодНахожденияВСтационареСРебенкомПо) Тогда
			ОбщегоНазначения.ВывестиИнформациюОбОшибке(ТекстОшибкиСНомеромСтроки + "Указана дата начала, но не указана дата окончания периода нахождения в стационаре!", Отказ, Заголовок);
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(ВыборкаПоСтрокамДокумента.ПериодНахожденияВСтационареСРебенкомС) И ЗначениеЗаполнено(ВыборкаПоСтрокамДокумента.ПериодНахожденияВСтационареСРебенкомПо) Тогда
			ОбщегоНазначения.ВывестиИнформациюОбОшибке(ТекстОшибкиСНомеромСтроки + "Указана дата окончания, но не указана дата начала периода нахождения в стационаре!", Отказ, Заголовок);
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ВыборкаПоСтрокамДокумента.ПериодНахожденияВСтационареСРебенкомС) И ЗначениеЗаполнено(ВыборкаПоСтрокамДокумента.ПериодНахожденияВСтационареСРебенкомПо) Тогда
			Если ВыборкаПоСтрокамДокумента.ПериодНахожденияВСтационареСРебенкомС > ВыборкаПоСтрокамДокумента.ПериодНахожденияВСтационареСРебенкомПо Тогда
				ОбщегоНазначения.ВывестиИнформациюОбОшибке(ТекстОшибкиСНомеромСтроки + "Неверно указаны даты начала и окончания периода нахождения в стационаре!", Отказ, Заголовок);
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
	Если ВыборкаПоСтрокамДокумента.НовыйСтатусНетрудопособного = "31" И Не ЗначениеЗаполнено(ВыборкаПоСтрокамДокумента.НомерЛисткаПродолжения) Тогда
		ОбщегоНазначения.ВывестиИнформациюОбОшибке(ТекстОшибкиСНомеромСтроки + "Для продолжающего болеть должен быть указан номер листка нетрудоспособности-продолжения!", Отказ, Заголовок);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ВыборкаПоСтрокамДокумента.ПериодПростояС) И ЗначениеЗаполнено(ВыборкаПоСтрокамДокумента.ПериодПростояПо) Тогда
		Если ВыборкаПоСтрокамДокумента.ПериодПростояС > ВыборкаПоСтрокамДокумента.ПериодПростояПо Тогда
			ОбщегоНазначения.ВывестиИнформациюОбОшибке(ТекстОшибкиСНомеромСтроки + "Неверно указаны даты начала и окончания простоя!", Отказ, Заголовок);
		ИначеЕсли ВыборкаПоСтрокамДокумента.СреднийЗаработокНаПериодПростоя = 0 Тогда
			ОбщегоНазначения.ВывестиИнформациюОбОшибке(ТекстОшибкиСНомеромСтроки + "Не указан средний заработок на период простоя!", Отказ, Заголовок);
		КонецЕсли;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ВыборкаПоСтрокамДокумента.СрочныйДоговорС) Или ЗначениеЗаполнено(ВыборкаПоСтрокамДокумента.СрочныйДоговорПо) Тогда
		
		Если ЗначениеЗаполнено(ВыборкаПоСтрокамДокумента.СрочныйДоговорС) И Не ЗначениеЗаполнено(ВыборкаПоСтрокамДокумента.СрочныйДоговорПо) Тогда
			ОбщегоНазначения.ВывестиИнформациюОбОшибке(ТекстОшибкиСНомеромСтроки + "Указана дата начала, но не указана дата окончания срочного трудового договора!", Отказ, Заголовок);
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(ВыборкаПоСтрокамДокумента.СрочныйДоговорС) И ЗначениеЗаполнено(ВыборкаПоСтрокамДокумента.СрочныйДоговорПо) Тогда
			ОбщегоНазначения.ВывестиИнформациюОбОшибке(ТекстОшибкиСНомеромСтроки + "Указана дата окончания, но не указана дата начала срочного трудового договора!", Отказ, Заголовок);
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ВыборкаПоСтрокамДокумента.СрочныйДоговорС) И ЗначениеЗаполнено(ВыборкаПоСтрокамДокумента.СрочныйДоговорПо) Тогда
			Если ВыборкаПоСтрокамДокумента.СрочныйДоговорС > ВыборкаПоСтрокамДокумента.СрочныйДоговорПо Тогда
				ОбщегоНазначения.ВывестиИнформациюОбОшибке(ТекстОшибкиСНомеромСтроки + "Неверно указаны даты начала и окончания срочного трудового договора!", Отказ, Заголовок);
			КонецЕсли;
		КонецЕсли;
		
	ИначеЕсли Найти("," + ВыборкаПоСтрокамДокумента.УсловияИсчисленияКод1 + "," + ВыборкаПоСтрокамДокумента.УсловияИсчисленияКод2 + "," + ВыборкаПоСтрокамДокумента.УсловияИсчисленияКод3 + ",", ",46,") > 0 Тогда
		ОбщегоНазначения.ВывестиИнформациюОбОшибке(ТекстОшибкиСНомеромСтроки + "Для принятого по срочному трудовому договору сроком менее 6 месяцев должен быть указан период этого договора!", Отказ, Заголовок);
	КонецЕсли;
	
	Если ВыборкаПоСтрокамДокумента.ПорядокРасчетаСреднегоЗаработка = 1 Тогда
		Если Не ЗначениеЗаполнено(ВыборкаПоСтрокамДокумента.ПериодРасчетаСреднегоЗаработкаНачало) И Не ЗначениеЗаполнено(ВыборкаПоСтрокамДокумента.ПериодРасчетаСреднегоЗаработкаОкончание) Тогда
			ОбщегоНазначения.ВывестиИнформациюОбОшибке(ТекстОшибкиСНомеромСтроки + "Для расчета по правилам 2010 года должен быть указан расчетный период!", Отказ, Заголовок);
		Иначе
			
			Если ЗначениеЗаполнено(ВыборкаПоСтрокамДокумента.ПериодРасчетаСреднегоЗаработкаНачало) И Не ЗначениеЗаполнено(ВыборкаПоСтрокамДокумента.ПериодРасчетаСреднегоЗаработкаОкончание) Тогда
				ОбщегоНазначения.ВывестиИнформациюОбОшибке(ТекстОшибкиСНомеромСтроки + "Указана дата начала, но не указана дата окончания расчетного периода!", Отказ, Заголовок);
			КонецЕсли;
			
			Если Не ЗначениеЗаполнено(ВыборкаПоСтрокамДокумента.ПериодРасчетаСреднегоЗаработкаНачало) И ЗначениеЗаполнено(ВыборкаПоСтрокамДокумента.ПериодРасчетаСреднегоЗаработкаОкончание) Тогда
				ОбщегоНазначения.ВывестиИнформациюОбОшибке(ТекстОшибкиСНомеромСтроки + "Указана дата окончания, но не указана дата начала расчетного периода!", Отказ, Заголовок);
			КонецЕсли;
			
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ВыборкаПоСтрокамДокумента.ПериодРасчетаСреднегоЗаработкаНачало) И ЗначениеЗаполнено(ВыборкаПоСтрокамДокумента.ПериодРасчетаСреднегоЗаработкаОкончание) И Не ЗначениеЗаполнено(ВыборкаПоСтрокамДокумента.КалендарныхДнейРасчетногоПериода) Тогда
			ОбщегоНазначения.ВывестиИнформациюОбОшибке(ТекстОшибкиСНомеромСтроки + "Не указано количество календарных дней расчетного периода!", Отказ, Заголовок);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры // ПроверитьЗаполнениеСтрокиТЧРаботникиОрганизации()

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

Процедура ОбработкаПроведения(Отказ, РежимПроведения)

	ОбработкаКомментариев = глЗначениеПеременной("глОбработкаСообщений");
	ОбработкаКомментариев.УдалитьСообщения();
	
	Для Каждого Набор Из Движения Цикл
		Набор.Очистить();
	КонецЦикла;
	
	ВыборкаПоШапкеДокумента = ОбменСведениямиОПособияхСФСС.СформироватьЗапросПоШапкеДокумента(ЭтотОбъект).Выбрать();
	ВыборкаПоШапкеДокумента.Следующий();
	
	ВыборкаПоРаботникиОрганизации = СформироватьЗапросПоТЧРаботникиОрганизации().Выбрать();
	ПроверитьПравильностьЗаполненияДокумента(Отказ, Ложь, ВыборкаПоШапкеДокумента, ВыборкаПоРаботникиОрганизации);
	
	ОбработкаКомментариев.ПоказатьСообщения();
	
	Если Отказ тогда
		Возврат;
	КонецЕсли;
	
	//При проведении файл формируем заново 
	ВыборкаПоРаботникиОрганизации.Сбросить();
	ТекстФайла = СформироватьВыходнойФайл(Отказ, Ложь, ВыборкаПоШапкеДокумента, ВыборкаПоРаботникиОрганизации);

	//Сохраним сформированный файл сведений в регистре сведений
	Запись = Движения.АрхивДанныхРегламентированнойОтчетности.Добавить();
	Запись.Объект = Ссылка;
	Запись.ОписаниеДанных = "Реестр пособий по нетрудоспособности";
	Запись.Данные = ТекстФайла;

КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	МассивТЧ = Новый Массив();
	МассивТЧ.Добавить(РаботникиОрганизации);
	
	КраткийСоставДокумента = ПроцедурыУправленияПерсоналом.ЗаполнитьКраткийСоставДокумента(МассивТЧ, "Сотрудник");

    Для каждого СтрокаТЧ Из РаботникиОрганизации Цикл
	
		Если ЗначениеЗаполнено(СтрокаТЧ.АдресПочтовый) И Не ЗначениеЗаполнено(СтрокаТЧ.АдресПочтовыйКодПоКЛАДР) Тогда
			СтрокаТЧ.АдресПочтовыйКодПоКЛАДР = ОбменСведениямиОПособияхСФСС.КодАдресаПоКЛАДРдляФСС(СтрокаТЧ.АдресПочтовый)
		КонецЕсли;	
		Если ЗначениеЗаполнено(СтрокаТЧ.АдресРегистрации) И Не ЗначениеЗаполнено(СтрокаТЧ.АдресРегистрацииКодПоКЛАДР) Тогда
			СтрокаТЧ.АдресРегистрацииКодПоКЛАДР = ОбменСведениямиОПособияхСФСС.КодАдресаПоКЛАДРдляФСС(СтрокаТЧ.АдресРегистрации)
		КонецЕсли;	
		Если ЗначениеЗаполнено(СтрокаТЧ.АдресЛПУ) И Не ЗначениеЗаполнено(СтрокаТЧ.АдресЛПУКодПоКЛАДР) Тогда
			СтрокаТЧ.АдресЛПУКодПоКЛАДР = ОбменСведениямиОПособияхСФСС.КодАдресаПоКЛАДРдляФСС(СтрокаТЧ.АдресЛПУ)
		КонецЕсли;	
	
	КонецЦикла;
	
КонецПроцедуры

Процедура ОбработкаУдаленияПроведения(Отказ)
	Для Каждого Набор Из Движения Цикл
		Набор.Очистить();
		Набор.Записать();
	КонецЦикла
КонецПроцедуры