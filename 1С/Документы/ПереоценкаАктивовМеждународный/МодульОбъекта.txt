Перем мУдалятьДвижения;

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОКУМЕНТА

#Если Клиент Тогда
// Процедура осуществляет печать документа. Можно направить печать на 
// экран или принтер, а также распечатать необходимое количество копий.
//
//  Название макета печати передается в качестве параметра,
// по переданному названию находим имя макета в соответствии.
//
// Параметры:
//  НазваниеМакета - строка, название макета.
//
Процедура Печать(ИмяМакета, КоличествоЭкземпляров = 1, НаПринтер = Ложь) Экспорт

	Если ЭтоНовый() Тогда
		Предупреждение("Документ можно распечатать только после его записи");
		Возврат;
	ИначеЕсли Не УправлениеДопПравамиПользователей.РазрешитьПечатьНепроведенныхДокументов(Проведен) Тогда
		Предупреждение("Недостаточно полномочий для печати непроведенного документа!");
		Возврат;
	КонецЕсли;

	Если Не РаботаСДиалогами.ПроверитьМодифицированность(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;

	Если ТипЗнч(ИмяМакета) = Тип("ДвоичныеДанные") Тогда

		ТабДокумент = УниверсальныеМеханизмы.НапечататьВнешнююФорму(Ссылка, ИмяМакета);
		
		Если ТабДокумент = Неопределено Тогда
			Возврат
		КонецЕсли; 
		
	КонецЕсли;

	УниверсальныеМеханизмы.НапечататьДокумент(ТабДокумент, КоличествоЭкземпляров, НаПринтер, ОбщегоНазначения.СформироватьЗаголовокДокумента(ЭтотОбъект), Ссылка);

КонецПроцедуры // Печать

// Возвращает доступные варианты печати документа
//
// Возвращаемое значение:
//  Структура, каждая строка которой соответствует одному из вариантов печати
//  
Функция ПолучитьСтруктуруПечатныхФорм() Экспорт
	
	Возврат Новый Структура;

КонецФункции // ПолучитьСтруктуруПечатныхФорм()

#КонецЕсли

Процедура УстановитьСубконтоПроводки(Куда, Счет, ВидСубконто, Значение)

	Если Счет.ВидыСубконто.Найти(ВидСубконто, "ВидСубконто") <> Неопределено Тогда
		Куда[ВидСубконто] = Значение;
	КонецЕсли;

КонецПроцедуры

Процедура ПереоценкаОС(Отказ, СтруктураПараметров)
	
	БухИтогиРС = СтруктураПараметров.БухИтогиРС;
	Итоги = СтруктураПараметров.Итоги;

	СрезСведенийУП = СтруктураПараметров.СрезСведенийУП;
	
	ВидСубконтоОС                  = ПланыВидовХарактеристик.ВидыСубконтоМеждународные.ОсновныеСредства;
	ВидСубконтоРезервы             = ПланыВидовХарактеристик.ВидыСубконтоМеждународные.Резервы;
	ВидСубконтоВидыДоходовРасходов = ПланыВидовХарактеристик.ВидыСубконтоМеждународные.ВидыДоходовИРасходов;
	
	Для Каждого ТекСтрокаСписокОС Из СписокОС Цикл
		// на закладке "ОС и инвестиционная собственность" могут переоцениваться объекты разных классов - поэтому важно,
		// на каком счете учитывается объект - из группы счетов ОсновныеСредства или ИнвестиционнаяСобственностьНетто
		//  отличия состоят в том, что для ОС в случае переоценки в сторону повышения стоимости образуется резерв переоценки 
		// в составе капитала (МСФО 16, п.39)
		// в случае снижения стоимости ранее переоцененного в сторону повышения объекта ОС сначала списывается резерв
		// переоценки, приходящийся на этот объект, и только затем признается убыток (МСФО 16, п.40)
		// в случае переоценки в сторону повышения стоимости объекта, по которому ранее признавался убыток от обесценения,
		// сначала признается доход в сумме ранее признанного и не списанного убытка, а затем образуется резерв переоценки
		// для инвестиционной собственности в любом случае изменение справедливой стоимости относится 
		// на прибыль или убыток (МСФО 40, п.35)
		
		СрезСведенийМежд = РегистрыСведений.ОсновныеСредстваМеждународныйУчет.ПолучитьПоследнее(Дата, Новый Структура("ОсновноеСредство", ТекСтрокаСписокОС.ОС));
		СчетОбъекта = СрезСведенийМежд.СчетУчета; // что это- ОС или ИС?
		СчетУчета = СчетОбъекта.Родитель; // общая группа
		СчетАмортизации = СрезСведенийМежд.СчетНачисленияАмортизации;
		СчетСнижСтоим = СрезСведенийМежд.СчетСниженияСтоимости; 
				
		// если не записан счет учета - это серьезное нарушение целостности учета, объект пропускаем
		Если НЕ ЗначениеЗаполнено(СрезСведенийМежд.СчетУчета) Тогда
			#Если Клиент Тогда
				Предупреждение("Для " + ТекСтрокаСписокОС.ОС + " нет данных о счете учета. Документ не проведен.");
			#КонецЕсли
			Продолжить;  
		КонецЕсли; 
		
		УбытокОтОбесценения = СрезСведенийМежд.УбытокОтОбесценения;  // признанный и не списанный убыток от снижения стоимости
		НовыйУбытокОтОбесценения = УбытокОтОбесценения; // это для записи в регистр сведений
		НоваяАмортизация = СрезСведенийМежд.СуммаНачисленнойАмортизации; // то же
		
		БухИтогиРС.РассчитатьИтоги("Международный", Итоги, "Сумма", "Счет, Субконто1", Дата, Дата, , СчетУчета, ПланыВидовХарактеристик.ВидыСубконтоМеждународные.ОсновныеСредства, , , "Организация", Организация);
		ОстаточнаяСтоимость = БухИтогиРС.ПолучитьИтог("СуммаКонечныйОстатокДт", "Субконто1", ТекСтрокаСписокОС.ОС) - БухИтогиРС.ПолучитьИтог("СуммаКонечныйОстатокКт", "Субконто1", ТекСтрокаСписокОС.ОС);
		
		БухИтогиРС.РассчитатьИтоги("Международный", Итоги, "Сумма", "Счет, Субконто1", Дата, Дата, , СчетОбъекта, ПланыВидовХарактеристик.ВидыСубконтоМеждународные.ОсновныеСредства, , , "Организация", Организация);
		ПервоначальнаяСтоимость = БухИтогиРС.ПолучитьИтог("СуммаКонечныйОстатокДт", "Субконто1", ТекСтрокаСписокОС.ОС) - БухИтогиРС.ПолучитьИтог("СуммаКонечныйОстатокКт", "Субконто1", ТекСтрокаСписокОС.ОС);
		
		    Резервы = Справочники.ВидыФинРезервов.РезервыНаПереоценку;
			МассивСубконто = Новый Массив();
			МассивСубконто.Добавить(ПланыВидовХарактеристик.ВидыСубконтоМеждународные.ОсновныеСредства);
			МассивСубконто.Добавить(ПланыВидовХарактеристик.ВидыСубконтоМеждународные.Резервы);
			СчетРезервыНаПереоценку = ПланыСчетов.Международный.РезервыНаПереоценкуОСиИС;
		БухИтогиРС.РассчитатьИтоги("Международный", Итоги, "Сумма", "Счет, Субконто1, Субконто2", Дата, Дата, , ПланыСчетов.Международный.РезервыНаПереоценкуОСиИС, МассивСубконто, , , "Организация", Организация);
			СальдоПоРезервам = БухИтогиРС.ПолучитьИтог("СуммаКонечныйОстатокКт", "Субконто1, Субконто2", ТекСтрокаСписокОС.ОС, Резервы);
		    		
		Дельта = ТекСтрокаСписокОС.Сумма - ОстаточнаяСтоимость; // прирост или уменьшение стоимости при переоценке
        Если Дельта = 0 Тогда
			// Стоимость не изменилась, пропускаем этот объект
			Продолжить;
		КонецЕсли;
        		
		Если СчетУчета<>ПланыСчетов.Международный.ИнвестиционнаяСобственностьНетто Тогда  // это - объект ОС
			Если Дельта>0 Тогда  // переоценка в сторону увеличения стоимости
				Если УбытокОтОбесценения>0 Тогда  // ранее признавался убыток от обесценения
					// сначала реверсируем убыток от обесценения (признаем доход)
					ПерекрываемыйУбыток = Мин(Дельта,УбытокОтОбесценения);   //сумма признаваемых доходов
					Если (ЗначениеЗаполнено(СчетОбъекта)) И (ЗначениеЗаполнено(СчетСнижСтоим)) Тогда
						Движение = Движения.Международный.Добавить();
						Движение.Период = Дата;
						Движение.СчетДт = СчетСнижСтоим;
						УстановитьСубконтоПроводки(Движение.СубконтоДт, СчетОбъекта, ВидСубконтоОС, ТекСтрокаСписокОС.ОС);
						Движение.СчетКт = ПланыСчетов.Международный.ПрочиеОперационныеДоходы;
						УстановитьСубконтоПроводки(Движение.СубконтоКт, Движение.СчетКт, ВидСубконтоВидыДоходовРасходов, СтатьяПрочихДоходов);
						Движение.Организация = Организация;
						Движение.Сумма = ПерекрываемыйУбыток;
						Движение.Содержание = "Списан ранее признанный убыток от обесценения";
						Движение.НомерЖурнала = "Рег";
						НовыйУбытокОтОбесценения = НовыйУбытокОтОбесценения - ПерекрываемыйУбыток;
					КонецЕсли;	
					// затем, если надо образовывать резерв переоценки - образуем его
					Если (Дельта - ПерекрываемыйУбыток)>0 Тогда
						//смотрим, какая была учетная поплитики по отржению переоценки в части амортизации
						// в соответствии с п.35 МСФО 16, амортизация может либо переоцениваться заново пропорционально 
						// изменению валовой балансовой стоимости актива так, что после переоценки балансовая стоимость актива 
						// равняется его переоцененной стоимости, либо элиминируется (списывается) против валовой балансовой 
						// стоимости, а чистая величина пересчитывается до переоцененной величины актива
						НовыйРезерв = Дельта - ПерекрываемыйУбыток;
						Если СрезСведенийУП.МетодПереоценкиОС = Перечисления.МетодыПереоценкиОС.ПропорциональнаяПереоценкаНакопленнойАмортизации Тогда
							Если ОстаточнаяСтоимость>0 Тогда
								ПереоценкаСтоим = ПервоначальнаяСтоимость*НовыйРезерв/(ОстаточнаяСтоимость+ПерекрываемыйУбыток);  //преоецека на счете учета ОС
								ПереоценкаАморт = ПереоценкаСтоим - НовыйРезерв;  // переоцкенка амортизации
							Иначе    // если ОС полностью амортизировано, то после переоценки вроде можем начать все заново, хотя момент спорный
								ПереоценкаСтоим = НовыйРезерв;
								ПереоценкаАморт = 0;
							КонецЕсли;
							// переоцениваем валовую балансовую стоимость
							Движение = Движения.Международный.Добавить();
							Движение.Организация = Организация;
							Движение.Период = Дата;
							Движение.СчетДт = СчетОбъекта;
							УстановитьСубконтоПроводки(Движение.СубконтоДт, СчетОбъекта, ВидСубконтоОС, ТекСтрокаСписокОС.ОС);
							Движение.СчетКт = ПланыСчетов.Международный.РезервыНаПереоценкуОСиИС;
							УстановитьСубконтоПроводки(Движение.СубконтоКт, Движение.СчетКт, ВидСубконтоОС, ТекСтрокаСписокОС.ОС);
							УстановитьСубконтоПроводки(Движение.СубконтоКт, Движение.СчетКт, ВидСубконтоРезервы, Справочники.ВидыФинРезервов.РезервыНаПереоценку);
							Движение.Сумма = ПереоценкаСтоим;
							Движение.Содержание = "Переоценка стоимости объекта ОС";
							Движение.НомерЖурнала = "Рег";
							// если надо - переоцениваем амортизацию
							Если ПереоценкаАморт>0 Тогда
								Движение = Движения.Международный.Добавить();
								Движение.Период = Дата;
								Движение.Организация = Организация;
								Движение.СчетДт = ПланыСчетов.Международный.РезервыНаПереоценкуОСиИС;
								УстановитьСубконтоПроводки(Движение.СубконтоДт, Движение.СчетДт, ВидСубконтоОС, ТекСтрокаСписокОС.ОС);
								УстановитьСубконтоПроводки(Движение.СубконтоДт, Движение.СчетДт, ВидСубконтоРезервы, Справочники.ВидыФинРезервов.РезервыНаПереоценку);
								Движение.СчетКт = СчетАмортизации;
								УстановитьСубконтоПроводки(Движение.СубконтоКт, Движение.СчетКт, ВидСубконтоОС, ТекСтрокаСписокОС.ОС);
								Движение.Сумма = ПереоценкаАморт;
								Движение.Содержание = "Переоценка амортизации объекта ОС";
								Движение.НомерЖурнала = "Рег";
								НоваяАмортизация = НоваяАмортизация + ПереоценкаАморт;
        					КонецЕсли;	
						Иначе     // накопленная амортизация сначала списывается на счет валовой стоимости
							БухИтогиРС.РассчитатьИтоги("Международный", Итоги, "Сумма", "Счет, Субконто1", Дата, Дата, , СчетАмортизации, ПланыВидовХарактеристик.ВидыСубконтоМеждународные.ОсновныеСредства, , , "Организация", Организация);
							Амортизация = БухИтогиРС.ПолучитьИтог("СуммаКонечныйОстатокКт", "Субконто1", ТекСтрокаСписокОС.ОС) - БухИтогиРС.ПолучитьИтог("СуммаКонечныйОстатокДт", "Субконто1", ТекСтрокаСписокОС.ОС);
							Если Амортизация <> 0 Тогда
								// Списываем всю сумму амортизации
								Движение = Движения.Международный.Добавить();
								Движение.Организация = Организация;
								Движение.Период = Дата;
								Движение.СчетДт = СчетАмортизации;
								УстановитьСубконтоПроводки(Движение.СубконтоДт, СчетАмортизации, ВидСубконтоОС, ТекСтрокаСписокОС.ОС);
								Движение.СчетКт = СчетОбъекта;
								УстановитьСубконтоПроводки(Движение.СубконтоКт, СчетОбъекта, ВидСубконтоОС, ТекСтрокаСписокОС.ОС);
								Движение.Организация = Организация;
								Движение.Сумма = Амортизация;
								Движение.Содержание = "Переоценка ОС - списание амортизации";
								Движение.НомерЖурнала = "Рег";
							КонецЕсли;
							НоваяАмортизация = 0;
							// а затем на всю сумму резерва увеличивается валовая балансовая стоимость ОС
							Движение = Движения.Международный.Добавить();
							Движение.Организация = Организация;
							Движение.Период = Дата;
							Движение.СчетДт = СчетОбъекта;
							УстановитьСубконтоПроводки(Движение.СубконтоДт, СчетОбъекта, ВидСубконтоОС, ТекСтрокаСписокОС.ОС);
							Движение.СчетКт = ПланыСчетов.Международный.РезервыНаПереоценкуОСиИС;
							УстановитьСубконтоПроводки(Движение.СубконтоКт, Движение.СчетКт, ВидСубконтоОС, ТекСтрокаСписокОС.ОС);
							УстановитьСубконтоПроводки(Движение.СубконтоКт, Движение.СчетКт, ВидСубконтоРезервы, Справочники.ВидыФинРезервов.РезервыНаПереоценку);
							Движение.Сумма = НовыйРезерв;
							Движение.Содержание = "Переоценка стоимости объекта ОС после списания амортизации";
							Движение.НомерЖурнала = "Рег";
						КонецЕсли;
					КонецЕсли;	 // надо образовывать резерв переоценки
					
				Иначе      // ранее не признавался убыток от обесценения - все относим на резерв переоценки
					// метод учета переоценки амортизации
					Если СрезСведенийУП.МетодПереоценкиОС = Перечисления.МетодыПереоценкиОС.ПропорциональнаяПереоценкаНакопленнойАмортизации Тогда
						Если ОстаточнаяСтоимость>0 Тогда
							ПереоценкаСтоим = ПервоначальнаяСтоимость*Дельта/ОстаточнаяСтоимость;  //преоецека на счете учета ОС
							ПереоценкаАморт = ПереоценкаСтоим - Дельта;  // переоценка амортизации
						Иначе    // если ОС полностью амортизировано, то после переоценки вроде можем начать все заново, хотя момент спорный
							ПереоценкаСтоим = Дельта;
							ПереоценкаАморт = 0;
						КонецЕсли;
					Иначе
						ПереоценкаСтоим = Дельта;
						ПереоценкаАморт = 0;
						// списываем сумму амортизации
						БухИтогиРС.РассчитатьИтоги("Международный", Итоги, "Сумма", "Счет, Субконто1", Дата, Дата, , СчетАмортизации, ПланыВидовХарактеристик.ВидыСубконтоМеждународные.ОсновныеСредства, , , "Организация", Организация);
						Амортизация = БухИтогиРС.ПолучитьИтог("СуммаКонечныйОстатокКт", "Субконто1", ТекСтрокаСписокОС.ОС) - БухИтогиРС.ПолучитьИтог("СуммаКонечныйОстатокДт", "Субконто1", ТекСтрокаСписокОС.ОС);
						Если Амортизация <> 0 Тогда
							// Списываем всю сумму амортизации
							Движение = Движения.Международный.Добавить();
							Движение.Организация = Организация;
							Движение.Период = Дата;
							Движение.СчетДт = СчетАмортизации;
							УстановитьСубконтоПроводки(Движение.СубконтоДт, СчетАмортизации, ВидСубконтоОС, ТекСтрокаСписокОС.ОС);
							Движение.СчетКт = СчетОбъекта;
							УстановитьСубконтоПроводки(Движение.СубконтоКт, СчетОбъекта, ВидСубконтоОС, ТекСтрокаСписокОС.ОС);
							Движение.Сумма = Амортизация;
							Движение.Содержание = "Переоценка ОС - списание амортизации";
							Движение.НомерЖурнала = "Рег";
						КонецЕсли;
						НоваяАмортизация = 0;
					КонецЕсли;
					// проводка по переоценке валовой балансовой стоимости
					Движение = Движения.Международный.Добавить();
					Движение.Организация = Организация;
					Движение.Период = Дата;
					Движение.СчетДт = СчетОбъекта;
					УстановитьСубконтоПроводки(Движение.СубконтоДт, СчетОбъекта, ВидСубконтоОС, ТекСтрокаСписокОС.ОС);
					Движение.СчетКт = ПланыСчетов.Международный.РезервыНаПереоценкуОСиИС;
					УстановитьСубконтоПроводки(Движение.СубконтоКт, Движение.СчетКт, ВидСубконтоОС, ТекСтрокаСписокОС.ОС);
					УстановитьСубконтоПроводки(Движение.СубконтоКт, Движение.СчетКт, ВидСубконтоРезервы, Справочники.ВидыФинРезервов.РезервыНаПереоценку);
					Движение.Сумма = ПереоценкаСтоим;
					Движение.Содержание = "Переоценка стоимости объекта ОС";
					Движение.НомерЖурнала = "Рег";
					// проводка по переоценке амортизации (если нужна)
					Если ПереоценкаАморт>0 Тогда
						Движение = Движения.Международный.Добавить();
						Движение.Организация = Организация;
						Движение.Период = Дата;
						Движение.СчетДт = ПланыСчетов.Международный.РезервыНаПереоценкуОСиИС;
						УстановитьСубконтоПроводки(Движение.СубконтоДт, Движение.СчетДт, ВидСубконтоОС, ТекСтрокаСписокОС.ОС);
						УстановитьСубконтоПроводки(Движение.СубконтоДт, Движение.СчетДт, ВидСубконтоРезервы, Справочники.ВидыФинРезервов.РезервыНаПереоценку);
						Движение.СчетКт = СчетАмортизации;
						УстановитьСубконтоПроводки(Движение.СубконтоКт, Движение.СчетКт, ВидСубконтоОС, ТекСтрокаСписокОС.ОС);
						Движение.Сумма = ПереоценкаАморт;
						Движение.Содержание = "Переоценка амортизации объекта ОС";
						Движение.НомерЖурнала = "Рег";
						НоваяАмортизация = НоваяАмортизация + ПереоценкаАморт;
					КонецЕсли;
				КонецЕсли;	
			Иначе     // переоценка в сторону снижения стоимости
				Если (УбытокОтОбесценения>0) ИЛИ (СальдоПоРезервам=0) Тогда  // это чистое обесценение - используется другой документ
					Сообщить("("+ТекСтрокаСписокОС.НомерСтроки+") По объекту ОС: '"+ТекСтрокаСписокОС.ОС+"' производится попытка уценения при наличии более раннего обесценения. Рекомендуется провести обесценение, а не переоценку данного объекта.", СтатусСообщения.Важное);
				Иначе	// объект переоценивался - сначала списываем резерв переоценки, потом если надо признаем убыток
					// списание резерва переоценки
					// выявляем, сколько резерва мы можем списать
					НовыйРезерв = - Мин(-Дельта, СальдоПоРезервам);
					// обращаем внимание на учетную политику в части отражения переоценки амортизации
					Если СрезСведенийУП.МетодПереоценкиОС = Перечисления.МетодыПереоценкиОС.ПропорциональнаяПереоценкаНакопленнойАмортизации Тогда
						Если ОстаточнаяСтоимость>0 Тогда
							ПереоценкаСтоим = ПервоначальнаяСтоимость*НовыйРезерв/ОстаточнаяСтоимость;  //преоецека на счете учета ОС
							ПереоценкаАморт = ПереоценкаСтоим - НовыйРезерв;  // переоценка амортизации
						Иначе    // если ОС полностью амортизировано, то уценка невозможна - ниже нуля некуда
							ПереоценкаСтоим = 0;
							ПереоценкаАморт = 0;
						КонецЕсли;
					Иначе
						ПереоценкаСтоим = НовыйРезерв;
						ПереоценкаАморт = 0;
						// списываем сумму амортизации
						БухИтогиРС.РассчитатьИтоги("Международный", Итоги, "Сумма", "Счет, Субконто1", Дата, Дата, , СчетАмортизации, ПланыВидовХарактеристик.ВидыСубконтоМеждународные.ОсновныеСредства, , , "Организация", Организация);
						Амортизация = БухИтогиРС.ПолучитьИтог("СуммаКонечныйОстатокКт", "Субконто1", ТекСтрокаСписокОС.ОС) - БухИтогиРС.ПолучитьИтог("СуммаКонечныйОстатокДт", "Субконто1", ТекСтрокаСписокОС.ОС);
						Если Амортизация <> 0 Тогда
							// Списываем всю сумму амортизации
							Движение = Движения.Международный.Добавить();
							Движение.Организация = Организация;
							Движение.Период = Дата;
							Движение.СчетДт = СчетАмортизации;
							УстановитьСубконтоПроводки(Движение.СубконтоДт, СчетАмортизации, ВидСубконтоОС, ТекСтрокаСписокОС.ОС);
							Движение.СчетКт = СчетОбъекта;
							УстановитьСубконтоПроводки(Движение.СубконтоКт, СчетОбъекта, ВидСубконтоОС, ТекСтрокаСписокОС.ОС);
							Движение.Сумма = Амортизация;
							Движение.Содержание = "Переоценка ОС - списание амортизации";
							Движение.НомерЖурнала = "Рег";
							НоваяАмортизация = 0;
						КонецЕсли;
					КонецЕсли;
                    // отражаем переоценку валовой балансовой стоимости
					Движение = Движения.Международный.Добавить();
					Движение.Организация = Организация;
					Движение.Период = Дата;
					Движение.СчетКт = СчетОбъекта;
					УстановитьСубконтоПроводки(Движение.СубконтоКт, СчетОбъекта, ВидСубконтоОС, ТекСтрокаСписокОС.ОС);
					Движение.СчетДт = ПланыСчетов.Международный.РезервыНаПереоценкуОСиИС;
					УстановитьСубконтоПроводки(Движение.СубконтоДт, Движение.СчетДт, ВидСубконтоОС, ТекСтрокаСписокОС.ОС);
					УстановитьСубконтоПроводки(Движение.СубконтоДт, Движение.СчетДт, ВидСубконтоРезервы, Справочники.ВидыФинРезервов.РезервыНаПереоценку);
					Движение.Сумма = - ПереоценкаСтоим;
					Движение.Содержание = "Переоценка (уценка) стоимости объекта ОС";
					Движение.НомерЖурнала = "Рег";
					// если нужно - переоцениваем амортизацию
					Если ПереоценкаАморт<0 Тогда
						Движение = Движения.Международный.Добавить();
						Движение.Организация = Организация;
						Движение.Период = Дата;
						Движение.СчетКт = ПланыСчетов.Международный.РезервыНаПереоценкуОСиИС;
						УстановитьСубконтоПроводки(Движение.СубконтоКт, Движение.СчетКт, ВидСубконтоОС, ТекСтрокаСписокОС.ОС);
						УстановитьСубконтоПроводки(Движение.СубконтоКт, Движение.СчетКт, ВидСубконтоРезервы, Справочники.ВидыФинРезервов.РезервыНаПереоценку);
						Движение.СчетДт = СчетАмортизации;
						УстановитьСубконтоПроводки(Движение.СубконтоДт, Движение.СчетДт, ВидСубконтоОС, ТекСтрокаСписокОС.ОС);
						Движение.Сумма = - ПереоценкаАморт;
						Движение.Содержание = "Переоценка (уценка) амортизации объекта ОС";
						Движение.НомерЖурнала = "Рег";
						НоваяАмортизация = НоваяАмортизация + ПереоценкаАморт;
					КонецЕсли;
					// признаем убыток, если снижение стоимости превысило ранее имевшийся резерв переоценки
					Если (Дельта+СальдоПоРезервам)<0 Тогда
						Если (ЗначениеЗаполнено(СчетОбъекта)) И (ЗначениеЗаполнено(СчетСнижСтоим)) Тогда
							Движение = Движения.Международный.Добавить();
							Движение.Организация = Организация;
							Движение.Период = Дата;
							Движение.СчетКт = СчетСнижСтоим;
							УстановитьСубконтоПроводки(Движение.СубконтоКт, СчетОбъекта, ВидСубконтоОС, ТекСтрокаСписокОС.ОС);
							Движение.СчетДт = ПланыСчетов.Международный.ПрочиеОперационныеРасходы;
							УстановитьСубконтоПроводки(Движение.СубконтоДт, Движение.СчетДт, ВидСубконтоВидыДоходовРасходов, СтатьяПрочихДоходов);
							Движение.Сумма = -(СальдоПоРезервам+Дельта);
							Движение.Содержание = "Списан ранее признанный убыток от обесценения";
							Движение.НомерЖурнала = "Рег";
							НовыйУбытокОтОбесценения = -(СальдоПоРезервам+Дельта);
						КонецЕсли;
					КонецЕсли;	
				КонецЕсли;	
			КонецЕсли;
					
		Иначе   // это - инвестиционная собственность
			// здесь любое изменение справедливой стоимости отражаем как прибыль/убыток
			Если Дельта>0 Тогда //переоценка в сторону повышения
				Если УбытокОтОбесценения>0 Тогда  // ранее признавался убыток от обесценения
					// сначала реверсируем убыток от обесценения (признаем доход), но на счет снижения стоимости
					ПерекрываемыйУбыток = Мин(Дельта,УбытокОтОбесценения);   //сумма признаваемых доходов
					Если (ЗначениеЗаполнено(СчетОбъекта)) И (ЗначениеЗаполнено(СчетСнижСтоим)) Тогда
						Движение = Движения.Международный.Добавить();
						Движение.Период = Дата;
						Движение.СчетДт = СчетСнижСтоим;
						УстановитьСубконтоПроводки(Движение.СубконтоДт, СчетОбъекта, ВидСубконтоОС, ТекСтрокаСписокОС.ОС);
						Движение.СчетКт = ПланыСчетов.Международный.ПрочиеОперационныеДоходы;
						УстановитьСубконтоПроводки(Движение.СубконтоКт, Движение.СчетКт, ВидСубконтоВидыДоходовРасходов, СтатьяПрочихДоходов);
						Движение.Организация = Организация;
						Движение.Сумма = ПерекрываемыйУбыток;
						Движение.Содержание = "Списан ранее признанный убыток от обесценения";
						Движение.НомерЖурнала = "Рег";
						НовыйУбытокОтОбесценения = НовыйУбытокОтОбесценения - ПерекрываемыйУбыток;
					КонецЕсли;	
					// и затем увеличиваем стоимость объекта, если это нужно
					Если (Дельта - ПерекрываемыйУбыток)>0 Тогда
						// если объект учитывается аналогично ОС - то применяем те же правила, что и по МСФО 16
						// только вместо резерва переоценки признаем прибыли и убытки
						// смотрим, какая была учетная поплитики по отржению переоценки в части амортизации
						// в соответствии с п.35 МСФО 16, амортизация может либо переоцениваться заново пропорционально 
						// изменению валовой балансовой стоимости актива так, что после переоценки балансовая стоимость актива 
						// равняется его переоцененной стоимости, либо элиминируется (списывается) против валовой балансовой 
						// стоимости, а чистая величина пересчитывается до переоцененной величины актива
						НовыйРезерв = Дельта - ПерекрываемыйУбыток;
						Если СрезСведенийУП.МетодПереоценкиОС = Перечисления.МетодыПереоценкиОС.ПропорциональнаяПереоценкаНакопленнойАмортизации Тогда
							Если ОстаточнаяСтоимость>0 Тогда
								ПереоценкаСтоим = ПервоначальнаяСтоимость*НовыйРезерв/(ОстаточнаяСтоимость+ПерекрываемыйУбыток);  //преоецека на счете учета ОС
								ПереоценкаАморт = ПереоценкаСтоим - НовыйРезерв;  // переоценка амортизации
							Иначе    // если ОС полностью амортизировано, то после переоценки вроде можем начать все заново, хотя момент спорный
								ПереоценкаСтоим = НовыйРезерв;
								ПереоценкаАморт = 0;
							КонецЕсли;
							// переоцениваем валовую балансовую стоимость
							Движение = Движения.Международный.Добавить();
							Движение.Организация = Организация;
							Движение.Период = Дата;
							Движение.СчетДт = СчетОбъекта;
							УстановитьСубконтоПроводки(Движение.СубконтоДт, СчетОбъекта, ВидСубконтоОС, ТекСтрокаСписокОС.ОС);
							Движение.СчетКт = ПланыСчетов.Международный.ПрочиеОперационныеДоходы;
							УстановитьСубконтоПроводки(Движение.СубконтоКт, Движение.СчетКт, ВидСубконтоВидыДоходовРасходов, СтатьяПрочихДоходов);
							Движение.Сумма = ПереоценкаСтоим;
							Движение.Содержание = "Переоценка стоимости объекта ИС";
							Движение.НомерЖурнала = "Рег";
							// если надо - переоцениваем амортизацию в уменьшение доходов
							Если ПереоценкаАморт>0 Тогда
								Движение = Движения.Международный.Добавить();
								Движение.Период = Дата;
								Движение.Организация = Организация;
								Движение.СчетДт = ПланыСчетов.Международный.ПрочиеОперационныеДоходы;
								УстановитьСубконтоПроводки(Движение.СубконтоДт, Движение.СчетДт, ВидСубконтоВидыДоходовРасходов, СтатьяПрочихДоходов);
								Движение.СчетКт = СчетАмортизации;
								УстановитьСубконтоПроводки(Движение.СубконтоКт, Движение.СчетКт, ВидСубконтоОС, ТекСтрокаСписокОС.ОС);
								Движение.Сумма = ПереоценкаАморт;
								Движение.Содержание = "Переоценка амортизации объекта ИС";
								Движение.НомерЖурнала = "Рег";
								НоваяАмортизация = НоваяАмортизация + ПереоценкаАморт;
        					КонецЕсли;	
						Иначе     // накопленная амортизация сначала списывается на счет валовой стоимости
							БухИтогиРС.РассчитатьИтоги("Международный", Итоги, "Сумма", "Счет, Субконто1", Дата, Дата, , СчетАмортизации, ПланыВидовХарактеристик.ВидыСубконтоМеждународные.ОсновныеСредства, , , "Организация", Организация);
							Амортизация = БухИтогиРС.ПолучитьИтог("СуммаКонечныйОстатокКт", "Субконто1", ТекСтрокаСписокОС.ОС) - БухИтогиРС.ПолучитьИтог("СуммаКонечныйОстатокДт", "Субконто1", ТекСтрокаСписокОС.ОС);
							Если Амортизация <> 0 Тогда
								// Списываем всю сумму амортизации
								Движение = Движения.Международный.Добавить();
								Движение.Организация = Организация;
								Движение.Период = Дата;
								Движение.СчетДт = СчетАмортизации;
								УстановитьСубконтоПроводки(Движение.СубконтоДт, СчетАмортизации, ВидСубконтоОС, ТекСтрокаСписокОС.ОС);
								Движение.СчетКт = СчетОбъекта;
								УстановитьСубконтоПроводки(Движение.СубконтоКт, СчетОбъекта, ВидСубконтоОС, ТекСтрокаСписокОС.ОС);
								Движение.Организация = Организация;
								Движение.Сумма = Амортизация;
								Движение.Содержание = "Переоценка ИС - списание амортизации";
								Движение.НомерЖурнала = "Рег";
							КонецЕсли;
							НоваяАмортизация = 0;
							// а затем на всю сумму увеличивается валовая балансовая стоимость ОС
							Движение = Движения.Международный.Добавить();
							Движение.Организация = Организация;
							Движение.Период = Дата;
							Движение.СчетДт = СчетОбъекта;
							УстановитьСубконтоПроводки(Движение.СубконтоДт, СчетОбъекта, ВидСубконтоОС, ТекСтрокаСписокОС.ОС);
							Движение.СчетКт = ПланыСчетов.Международный.ПрочиеОперационныеДоходы;
							УстановитьСубконтоПроводки(Движение.СубконтоКт, Движение.СчетКт, ВидСубконтоВидыДоходовРасходов, СтатьяПрочихДоходов);
							Движение.Сумма = НовыйРезерв;
							Движение.Содержание = "Переоценка стоимости объекта ИС после списания амортизации";
							Движение.НомерЖурнала = "Рег";
						КонецЕсли;
					КонецЕсли;	 // надо увеличивать стоимость инвестиционной собственности на основном счете
				Иначе     // убыток от обесценения ранее не признавался или уже списан
					// все относим на основной счет
					Если СрезСведенийУП.МетодПереоценкиОС = Перечисления.МетодыПереоценкиОС.ПропорциональнаяПереоценкаНакопленнойАмортизации Тогда
						Если ОстаточнаяСтоимость>0 Тогда
							ПереоценкаСтоим = ПервоначальнаяСтоимость*Дельта/ОстаточнаяСтоимость;  //преоецека на счете учета ОС
							ПереоценкаАморт = ПереоценкаСтоим - Дельта;  // переоценка амортизации
						Иначе    // если ОС полностью амортизировано, то после переоценки вроде можем начать все заново, хотя момент спорный
							ПереоценкаСтоим = Дельта;
							ПереоценкаАморт = 0;
						КонецЕсли;
					Иначе
						ПереоценкаСтоим = Дельта;
						ПереоценкаАморт = 0;
						// списываем сумму амортизации
						БухИтогиРС.РассчитатьИтоги("Международный", Итоги, "Сумма", "Счет, Субконто1", Дата, Дата, , СчетАмортизации, ПланыВидовХарактеристик.ВидыСубконтоМеждународные.ОсновныеСредства, , , "Организация", Организация);
						Амортизация = БухИтогиРС.ПолучитьИтог("СуммаКонечныйОстатокКт", "Субконто1", ТекСтрокаСписокОС.ОС) - БухИтогиРС.ПолучитьИтог("СуммаКонечныйОстатокДт", "Субконто1", ТекСтрокаСписокОС.ОС);
						Если Амортизация <> 0 Тогда
							// Списываем всю сумму амортизации
							Движение = Движения.Международный.Добавить();
							Движение.Организация = Организация;
							Движение.Период = Дата;
							Движение.СчетДт = СчетАмортизации;
							УстановитьСубконтоПроводки(Движение.СубконтоДт, СчетАмортизации, ВидСубконтоОС, ТекСтрокаСписокОС.ОС);
							Движение.СчетКт = СчетОбъекта;
							УстановитьСубконтоПроводки(Движение.СубконтоКт, СчетОбъекта, ВидСубконтоОС, ТекСтрокаСписокОС.ОС);
							Движение.Сумма = Амортизация;
							Движение.Содержание = "Переоценка ИС - списание амортизации";
							Движение.НомерЖурнала = "Рег";
						КонецЕсли;
						НоваяАмортизация = 0;
					КонецЕсли;
					// проводка по переоценке валовой балансовой стоимости
					Движение = Движения.Международный.Добавить();
					Движение.Организация = Организация;
					Движение.Период = Дата;
					Движение.СчетДт = СчетОбъекта;
					УстановитьСубконтоПроводки(Движение.СубконтоДт, СчетОбъекта, ВидСубконтоОС, ТекСтрокаСписокОС.ОС);
					Движение.СчетКт = ПланыСчетов.Международный.ПрочиеОперационныеДоходы;
					УстановитьСубконтоПроводки(Движение.СубконтоКт, Движение.СчетКт, ВидСубконтоВидыДоходовРасходов, СтатьяПрочихДоходов);
					Движение.Сумма = ПереоценкаСтоим;
					Движение.Содержание = "Переоценка стоимости объекта ИС";
					Движение.НомерЖурнала = "Рег";
					// проводка по переоценке амортизации (если нужна) - в уменьшение доходов
					Если ПереоценкаАморт>0 Тогда
						Движение = Движения.Международный.Добавить();
						Движение.Организация = Организация;
						Движение.Период = Дата;
						Движение.СчетДт = ПланыСчетов.Международный.ПрочиеОперационныеДоходы;
						УстановитьСубконтоПроводки(Движение.СубконтоДт, Движение.СчетДт, ВидСубконтоВидыДоходовРасходов, СтатьяПрочихДоходов);
						Движение.СчетКт = СчетАмортизации;
						УстановитьСубконтоПроводки(Движение.СубконтоКт, Движение.СчетКт, ВидСубконтоОС, ТекСтрокаСписокОС.ОС);
						Движение.Сумма = ПереоценкаАморт;
						Движение.Содержание = "Переоценка амортизации объекта ИС";
						Движение.НомерЖурнала = "Рег";
						НоваяАмортизация = НоваяАмортизация + ПереоценкаАморт;
					КонецЕсли;
				КонецЕсли;
						
			Иначе   // переоценка в сторону понижения
				Если (УбытокОтОбесценения>0) Тогда  // это чистое обесценение - используется другой документ
					Сообщить("("+ТекСтрокаСписокОС.НомерСтроки+") По объекту инвестиционной собственности: '"+ТекСтрокаСписокОС.ОС+"' производится попытка уценения при наличии более раннего обесценения. Рекомендуется провести обесценение, а не переоценку данного объекта.", СтатусСообщения.Важное);
				Иначе  // проводим переоценку
					// сначала списываем стоимость с основного счета - в пределах первоначальной стоимости
					// и если объект учитывается как по МСФО 16 - то применяем учетную политику для ОС
					// а если не учитывался как ОС (т.е. без амортизации) - то все получится автоматом без учета амортизации
					Если СрезСведенийУП.МетодПереоценкиОС = Перечисления.МетодыПереоценкиОС.ПропорциональнаяПереоценкаНакопленнойАмортизации Тогда
						ОстРезерв = ПервоначальнаяСтоимость - СрезСведенийМежд.ПервоначальнаяСтоимость; // прирост стоимости при предыдущих переоценках
						СписВаловаяСтоимость = Мин (ОстРезерв, -Дельта*ПервоначальнаяСтоимость/ОстаточнаяСтоимость); // то, что списываем с валовой стоимости
						СписАморт = ОстРезерв*(1-ОстаточнаяСтоимость/ПервоначальнаяСтоимость);
						// теперь делаем проводку по списанию валовой балансовой стоимости объекта
						Движение = Движения.Международный.Добавить();
						Движение.Организация = Организация;
						Движение.Период = Дата;
						Движение.СчетКт = СчетОбъекта;
						УстановитьСубконтоПроводки(Движение.СубконтоКт, СчетОбъекта, ВидСубконтоОС, ТекСтрокаСписокОС.ОС);
						Движение.СчетДт = ПланыСчетов.Международный.ПрочиеОперационныеРасходы;
						УстановитьСубконтоПроводки(Движение.СубконтоДт, Движение.СчетДт, ВидСубконтоВидыДоходовРасходов, СтатьяПрочихДоходов);
                        Движение.Сумма = СписВаловаяСтоимость;
						Движение.Содержание = "Переоценка (уценка) стоимости объекта ИС";
						Движение.НомерЖурнала = "Рег";
						// и если нужно - по списанию амортизации
						Если СписАморт>0 Тогда
							Движение = Движения.Международный.Добавить();
							Движение.Организация = Организация;
							Движение.Период = Дата;
							Движение.СчетДт = СчетАмортизации;
							УстановитьСубконтоПроводки(Движение.СубконтоДт, СчетОбъекта, ВидСубконтоОС, ТекСтрокаСписокОС.ОС);
							Движение.СчетКт = ПланыСчетов.Международный.ПрочиеОперационныеРасходы;
							УстановитьСубконтоПроводки(Движение.СубконтоКт, Движение.СчетКт, ВидСубконтоВидыДоходовРасходов, СтатьяПрочихДоходов);
	                        Движение.Сумма = СписАморт;
							Движение.Содержание = "Переоценка (уценка) амортизации объекта ИС";
							Движение.НомерЖурнала = "Рег";
							НоваяАмортизация = НоваяАмортизация - СписАморт;
						КонецЕсли;
						СписанаСтоимость = СписВаловаяСтоимость - СписАморт;
					Иначе  // сначала списываем амортизацию, если она есть
						БухИтогиРС.РассчитатьИтоги("Международный", Итоги, "Сумма", "Счет, Субконто1", Дата, Дата, , СчетАмортизации, ПланыВидовХарактеристик.ВидыСубконтоМеждународные.ОсновныеСредства, , , "Организация", Организация);
						Амортизация = БухИтогиРС.ПолучитьИтог("СуммаКонечныйОстатокКт", "Субконто1", ТекСтрокаСписокОС.ОС) - БухИтогиРС.ПолучитьИтог("СуммаКонечныйОстатокДт", "Субконто1", ТекСтрокаСписокОС.ОС);
						Если Амортизация <> 0 Тогда
							// Списываем всю сумму амортизации
							Движение = Движения.Международный.Добавить();
							Движение.Организация = Организация;
							Движение.Период = Дата;
							Движение.СчетДт = СчетАмортизации;
							УстановитьСубконтоПроводки(Движение.СубконтоДт, СчетАмортизации, ВидСубконтоОС, ТекСтрокаСписокОС.ОС);
							Движение.СчетКт = СчетОбъекта;
							УстановитьСубконтоПроводки(Движение.СубконтоКт, СчетОбъекта, ВидСубконтоОС, ТекСтрокаСписокОС.ОС);
							Движение.Сумма = Амортизация;
							Движение.Содержание = "Переоценка ИС - списание амортизации";
							Движение.НомерЖурнала = "Рег";
							НоваяАмортизация = 0;
						КонецЕсли;
						ОстРезерв = ОстаточнаяСтоимость - СрезСведенийМежд.ПервоначальнаяСтоимость;
						Движение = Движения.Международный.Добавить();
						Движение.Организация = Организация;
						Движение.Период = Дата;
						Движение.СчетКт = СчетОбъекта;
						УстановитьСубконтоПроводки(Движение.СубконтоКт, СчетОбъекта, ВидСубконтоОС, ТекСтрокаСписокОС.ОС);
						Движение.СчетДт = ПланыСчетов.Международный.ПрочиеОперационныеРасходы;
						УстановитьСубконтоПроводки(Движение.СубконтоДт, Движение.СчетДт, ВидСубконтоВидыДоходовРасходов, СтатьяПрочихДоходов);
                        Движение.Сумма = ОстРезерв;
						Движение.Содержание = "Переоценка (уценка) стоимости объекта ИС";
						Движение.НомерЖурнала = "Рег";
						СписанаСтоимость = ОстРезерв;
					КонецЕсли;	
					// затем образуем сальдо на счете учета снижения стоимости актива, если это нужно
					Если (СписанаСтоимость+Дельта)<0 И (ЗначениеЗаполнено(СчетСнижСтоим)) Тогда
						Движение = Движения.Международный.Добавить();
						Движение.Организация = Организация;
						Движение.Период = Дата;
						Движение.СчетКт = СчетСнижСтоим;
						УстановитьСубконтоПроводки(Движение.СубконтоКт, СчетОбъекта, ВидСубконтоОС, ТекСтрокаСписокОС.ОС);
						Движение.СчетДт = ПланыСчетов.Международный.ПрочиеОперационныеРасходы;
						УстановитьСубконтоПроводки(Движение.СубконтоДт, Движение.СчетДт, ВидСубконтоВидыДоходовРасходов, СтатьяПрочихДоходов);
                        Движение.Сумма = -Дельта - СписанаСтоимость; // признан убыто сверх списания стоимости;
						Движение.Содержание = "Переоценка (уценка) стоимости объекта ИС - признан убыток";
						Движение.НомерЖурнала = "Рег";
						НовыйУбытокОтОбесценения = -Дельта - СписанаСтоимость;
					КонецЕсли;	
				КонецЕсли;
			КонецЕсли;	
		КонецЕсли;	// ОС или ИС
		
		// теперь записываем новую справедливую стоимость, новую амортизацию и новый убыток от обесценения (а также ликвидационную
		// стоимость)в регистр сведений об объектах ОС для международного учета
		Движение = Движения.ОсновныеСредстваМеждународныйУчет.Добавить();
		Движение.Период = Дата;
		Движение.ОсновноеСредство = ТекСтрокаСписокОС.ОС;
		Движение.Организация = СрезСведенийМежд.Организация;
		Движение.ДатаПринятияКУчету = СрезСведенийМежд.ДатаПринятияКУчету;
		Движение.МестонахождениеОбъекта = СрезСведенийМежд.МестонахождениеОбъекта;
		Движение.МОЛ = СрезСведенийМежд.МОЛ;
		Движение.СчетУчета = СрезСведенийМежд.СчетУчета;    
		Движение.СрокПолезногоИспользования = СрезСведенийМежд.СрокПолезногоИспользования;
		Движение.НачислятьАмортизацию = СрезСведенийМежд.НачислятьАмортизацию;
		Движение.МетодНачисленияАмортизации = СрезСведенийМежд.МетодНачисленияАмортизации;
		Движение.СчетНачисленияАмортизации = СрезСведенийМежд.СчетНачисленияАмортизации;
		Движение.ПервоначальнаяСтоимость = СрезСведенийМежд.ПервоначальнаяСтоимость;
		Движение.ЛиквидационнаяСтоимость = ТекСтрокаСписокОС.ЛиквидационнаяСтоимость; //Пересмотренная 
		Движение.СчетЗатрат = СрезСведенийМежд.СчетЗатрат;   
		Движение.Субконто1 = СрезСведенийМежд.Субконто1;
		Движение.Субконто2 = СрезСведенийМежд.Субконто2;
		Движение.Субконто3 = СрезСведенийМежд.Субконто3;
		Движение.ПредполагаемыйОбъемПродукции = СрезСведенийМежд.ПредполагаемыйОбъемПродукции;
		Движение.СуммаНачисленнойАмортизации = НоваяАмортизация;
		Движение.СправедливаяСтоимость = ТекСтрокаСписокОС.Сумма; // новая справедливая стоимость
		Движение.Состояние = СрезСведенийМежд.Состояние;
		Движение.СчетСниженияСтоимости = СрезСведенийМежд.СчетСниженияСтоимости;
		Движение.УбытокОтОбесценения = НовыйУбытокОтОбесценения;
		
	КонецЦикла;
	
	Отказ = Ложь;
	
КонецПроцедуры

Процедура ПереоценкаНМА(Отказ, СтруктураПараметров)
	
	БухИтогиРС = СтруктураПараметров.БухИтогиРС;
	Итоги = СтруктураПараметров.Итоги;
	
	СрезСведенийУП = СтруктураПараметров.СрезСведенийУП;
	
	ВидСубконтоНМА                 = ПланыВидовХарактеристик.ВидыСубконтоМеждународные.НематериальныеАктивы;
	ВидСубконтоРезервы             = ПланыВидовХарактеристик.ВидыСубконтоМеждународные.Резервы;
	ВидСубконтоВидыДоходовРасходов = ПланыВидовХарактеристик.ВидыСубконтоМеждународные.ВидыДоходовИРасходов;
	
	Для Каждого ТекСтрокаСписокНМА Из СписокНМА Цикл
		// регистр НематериальныеАктивыМеждународныйУчет
		СрезСведенийМежд = РегистрыСведений.НематериальныеАктивыМеждународныйУчет.ПолучитьПоследнее(Дата, Новый Структура("НематериальныйАктив", ТекСтрокаСписокНМА.НМА));
		УбытокОтОбесценения = СрезСведенийМежд.УбытокОтОбесценения;
		
		Движение = Движения.НематериальныеАктивыМеждународныйУчет.Добавить();
		Движение.Период = Дата;
		Движение.НематериальныйАктив = ТекСтрокаСписокНМА.НМА;
		Движение.СчетУчета = СрезСведенийМежд.СчетУчета;
		Движение.СрокПолезногоИспользования = СрезСведенийМежд.СрокПолезногоИспользования;
		Движение.НачислятьАмортизацию = СрезСведенийМежд.НачислятьАмортизацию;
		Движение.МетодНачисленияАмортизации = СрезСведенийМежд.МетодНачисленияАмортизации;
		Движение.СчетНачисленияАмортизации = СрезСведенийМежд.СчетНачисленияАмортизации;
		Движение.СчетЗатрат = СрезСведенийМежд.СчетЗатрат;
		Движение.Субконто1 = СрезСведенийМежд.Субконто1;
		Движение.Субконто2 = СрезСведенийМежд.Субконто2;
		Движение.Субконто3 = СрезСведенийМежд.Субконто3;
		Движение.ПредполагаемыйОбъемПродукции = СрезСведенийМежд.ПредполагаемыйОбъемПродукции;
		Движение.ФактическийОбъемПродукции = СрезСведенийМежд.ФактическийОбъемПродукции;
		Движение.СуммаНачисленнойАмортизации = СрезСведенийМежд.СуммаНачисленнойАмортизации;
		Движение.ПервоначальнаяСтоимость = СрезСведенийМежд.ПервоначальнаяСтоимость;
		Движение.СправедливаяСтоимость = ТекСтрокаСписокНМА.Сумма;
		Движение.ЛиквидационнаяСтоимость = СрезСведенийМежд.ЛиквидационнаяСтоимость;
		Движение.ДатаПринятияКУчету = СрезСведенийМежд.ДатаПринятияКУчету;
		Движение.СчетСниженияСтоимости = СрезСведенийМежд.СчетСниженияСтоимости;
		Движение.КоэффициентУскорения = СрезСведенийМежд.КоэффициентУскорения;
		
		
		СчетОбъекта = СрезСведенийМежд.СчетУчета;
		СчетУчета = СчетОбъекта.Родитель; // общая группа
		СчетАмортизации = СрезСведенийМежд.СчетНачисленияАмортизации;
		
		БухИтогиРС.РассчитатьИтоги("Международный", Итоги, "Сумма", "Счет, Субконто1", Дата, Дата, , СчетУчета, ПланыВидовХарактеристик.ВидыСубконтоМеждународные.НематериальныеАктивы, , , "Организация", Организация);
		ОстаточнаяСтоимость = БухИтогиРС.ПолучитьИтог("СуммаКонечныйОстатокДт", "Субконто1", ТекСтрокаСписокНМА.НМА) - БухИтогиРС.ПолучитьИтог("СуммаКонечныйОстатокКт", "Субконто1", ТекСтрокаСписокНМА.НМА);
		
		БухИтогиРС.РассчитатьИтоги("Международный", Итоги, "Сумма", "Счет, Субконто1", Дата, Дата, , СчетОбъекта, ПланыВидовХарактеристик.ВидыСубконтоМеждународные.НематериальныеАктивы, , , "Организация", Организация);
		ПервоначальнаяСтоимость = БухИтогиРС.ПолучитьИтог("СуммаКонечныйОстатокДт", "Субконто1", ТекСтрокаСписокНМА.НМА) - БухИтогиРС.ПолучитьИтог("СуммаКонечныйОстатокКт", "Субконто1", ТекСтрокаСписокНМА.НМА);
		
		Дельта = ТекСтрокаСписокНМА.Сумма - ОстаточнаяСтоимость;
		Если Дельта = 0 Тогда
			// Стоимость не изменилась, пропускаем этот объект
			Продолжить;
		КонецЕсли;
		
		Если УбытокОтОбесценения > 0 Тогда
			Если Дельта < 0 Тогда
				Сообщить("("+ТекСтрокаСписокНМА.НомерСтроки+") По объекту НМА: '"+ТекСтрокаСписокНМА.НМА+"' производится попытка уценения при наличии более раннего обесценения. Рекомендуется провести обесценение, а не переоценку данного объекта.", СтатусСообщения.Важное);
			КонецЕсли;
			
			СуммаУбытокОтОбесценения = Мин(УбытокОтОбесценения, Дельта);
			Движение.УбытокОтОбесценения = СуммаУбытокОтОбесценения;
			
			Если ЗначениеЗаполнено(СчетОбъекта) Тогда
				Движение = Движения.Международный.Добавить();
				Движение.Период = Дата;
				Движение.СчетДт = СчетОбъекта;
				УстановитьСубконтоПроводки(Движение.СубконтоДт, СчетОбъекта, ВидСубконтоНМА, ТекСтрокаСписокНМА.НМА);
				Движение.СчетКт = ПланыСчетов.Международный.ПрочиеОперационныеДоходы;
				УстановитьСубконтоПроводки(Движение.СубконтоКт, Движение.СчетКт, ВидСубконтоВидыДоходовРасходов, СтатьяПрочихДоходов);
				Движение.Организация = Организация;
				Движение.Сумма = СуммаУбытокОтОбесценения;
				Движение.Содержание = "Убыток от обесценения";
				Движение.НомерЖурнала = "Рег";
			КонецЕсли; 
		Иначе
			СуммаУбытокОтОбесценения = 0;
			Движение.УбытокОтОбесценения = СрезСведенийМежд.УбытокОтОбесценения;
		КонецЕсли;
		
		Если ОстаточнаяСтоимость = 0 Тогда
			Сообщить("Для " + ТекСтрокаСписокНМА.НМА + " не найдена остаточная стоимость!");
			Продолжить;
		КонецЕсли;
		
		Если СрезСведенийУП.МетодПереоценкиОС = Перечисления.МетодыПереоценкиОС.ПропорциональнаяПереоценкаНакопленнойАмортизации Тогда
			Если ЗначениеЗаполнено(СчетОбъекта) Тогда
				Движение = Движения.Международный.Добавить();
				Движение.Период = Дата;
				Если Дельта >0 Тогда
					Движение.СчетДт = СчетОбъекта;
					УстановитьСубконтоПроводки(Движение.СубконтоДт, Движение.СчетДт, ВидСубконтоНМА, ТекСтрокаСписокНМА.НМА);
					Движение.СчетКт = ПланыСчетов.Международный.РезервыНаПереоценкуНМА;
					УстановитьСубконтоПроводки(Движение.СубконтоКт, Движение.СчетКт, ВидСубконтоНМА, ТекСтрокаСписокНМА.НМА);
					УстановитьСубконтоПроводки(Движение.СубконтоКт, Движение.СчетКт, ВидСубконтоРезервы, Справочники.ВидыФинРезервов.РезервыНаПереоценку);
				Иначе
					Движение.СчетДт = ПланыСчетов.Международный.РезервыНаПереоценкуНМА;
					УстановитьСубконтоПроводки(Движение.СубконтоДт, Движение.СчетДт, ВидСубконтоНМА, ТекСтрокаСписокНМА.НМА);
					УстановитьСубконтоПроводки(Движение.СубконтоДт, Движение.СчетДт, ВидСубконтоРезервы, Справочники.ВидыФинРезервов.РезервыНаПереоценку);
					Движение.СчетКт = СчетОбъекта;
					УстановитьСубконтоПроводки(Движение.СубконтоКт, Движение.СчетКт, ВидСубконтоНМА, ТекСтрокаСписокНМА.НМА);
				КонецЕсли;
				Движение.Организация = Организация;
				Движение.Сумма = (Макс(Дельта,-Дельта) - СуммаУбытокОтОбесценения)*ПервоначальнаяСтоимость/ОстаточнаяСтоимость;
				Движение.Содержание = "Переоценка НМА";
				Движение.НомерЖурнала = "Рег";
			КонецЕсли; 
			
			Если ЗначениеЗаполнено(СчетАмортизации) Тогда
				Движение = Движения.Международный.Добавить();
				Движение.Период = Дата;
				Если Дельта >0 Тогда
					Движение.СчетДт = ПланыСчетов.Международный.РезервыНаПереоценкуНМА;
					УстановитьСубконтоПроводки(Движение.СубконтоДт, Движение.СчетДт, ВидСубконтоНМА, ТекСтрокаСписокНМА.НМА);
					УстановитьСубконтоПроводки(Движение.СубконтоДт, Движение.СчетДт, ВидСубконтоРезервы, Справочники.ВидыФинРезервов.РезервыНаПереоценку);
					Движение.СчетКт = СчетАмортизации;
					УстановитьСубконтоПроводки(Движение.СубконтоКт, Движение.СчетКт, ВидСубконтоНМА, ТекСтрокаСписокНМА.НМА);
				Иначе
					Движение.СчетДт = СчетАмортизации;
					УстановитьСубконтоПроводки(Движение.СубконтоДт, Движение.СчетДт, ВидСубконтоНМА, ТекСтрокаСписокНМА.НМА);
					Движение.СчетКт = ПланыСчетов.Международный.РезервыНаПереоценкуНМА;
					УстановитьСубконтоПроводки(Движение.СубконтоКт, Движение.СчетКт, ВидСубконтоНМА, ТекСтрокаСписокНМА.НМА);
					УстановитьСубконтоПроводки(Движение.СубконтоКт, Движение.СчетКт, ВидСубконтоРезервы, Справочники.ВидыФинРезервов.РезервыНаПереоценку);
				КонецЕсли;
				Движение.Организация = Организация;
				Движение.Сумма = (Макс(Дельта,-Дельта)- СуммаУбытокОтОбесценения) *(ПервоначальнаяСтоимость/ОстаточнаяСтоимость - 1);
				Движение.Содержание = "Переоценка НМА";
				Движение.НомерЖурнала = "Рег";
			КонецЕсли; 
			
		ИначеЕсли СрезСведенийУП.МетодПереоценкиОС = Перечисления.МетодыПереоценкиОС.СписаниеАмортизацииПриПереоценки Тогда
			БухИтогиРС.РассчитатьИтоги("Международный", Итоги, "Сумма", "Счет, Субконто1", Дата, Дата, , СчетАмортизации, ПланыВидовХарактеристик.ВидыСубконтоМеждународные.НематериальныеАктивы, , , "Организация", Организация);
			АмортизационнаяСтоимость = БухИтогиРС.ПолучитьИтог("СуммаКонечныйОстатокКт", "Субконто1", ТекСтрокаСписокНМА.НМА) - БухИтогиРС.ПолучитьИтог("СуммаКонечныйОстатокДт", "Субконто1", ТекСтрокаСписокНМА.НМА);
			
			Если АмортизационнаяСтоимость <> 0 Тогда
				// Списываем всю сумму амортизации
				Движение = Движения.Международный.Добавить();
				Движение.Период = Дата;
				Движение.СчетДт = СчетАмортизации;
				УстановитьСубконтоПроводки(Движение.СубконтоДт, Движение.СчетДт, ВидСубконтоНМА, ТекСтрокаСписокНМА.НМА);
				Движение.СчетКт = СчетОбъекта;
				УстановитьСубконтоПроводки(Движение.СубконтоКт, Движение.СчетКт, ВидСубконтоНМА, ТекСтрокаСписокНМА.НМА);
				Движение.Организация = Организация;
				Движение.Сумма = АмортизационнаяСтоимость;
				Движение.Содержание = "Переоценка НМА";
				Движение.НомерЖурнала = "Рег";
			КонецЕсли;
			
			Если Дельта <> 0 Тогда
				Движение = Движения.Международный.Добавить();
				Движение.Период = Дата;
				Если Дельта < 0 Тогда
					Движение.СчетДт = ПланыСчетов.Международный.РезервыНаПереоценкуНМА;
					УстановитьСубконтоПроводки(Движение.СубконтоДт, Движение.СчетДт, ВидСубконтоНМА, ТекСтрокаСписокНМА.НМА);
					УстановитьСубконтоПроводки(Движение.СубконтоДт, Движение.СчетДт, ВидСубконтоРезервы, Справочники.ВидыФинРезервов.РезервыНаПереоценку);
					Движение.СчетКт = СчетОбъекта;
					УстановитьСубконтоПроводки(Движение.СубконтоКт, Движение.СчетКт, ВидСубконтоНМА, ТекСтрокаСписокНМА.НМА);
				Иначе
					Движение.СчетДт = СчетОбъекта;
					УстановитьСубконтоПроводки(Движение.СубконтоДт, Движение.СчетДт, ВидСубконтоНМА, ТекСтрокаСписокНМА.НМА);
					Движение.СчетКт = ПланыСчетов.Международный.РезервыНаПереоценкуНМА;
					УстановитьСубконтоПроводки(Движение.СубконтоКт, Движение.СчетКт, ВидСубконтоНМА, ТекСтрокаСписокНМА.НМА);
					УстановитьСубконтоПроводки(Движение.СубконтоКт, Движение.СчетКт, ВидСубконтоРезервы, Справочники.ВидыФинРезервов.РезервыНаПереоценку);
				КонецЕсли;
				Движение.Организация = Организация;
				Движение.Сумма = Макс(Дельта,-Дельта) - СуммаУбытокОтОбесценения;
				Движение.Содержание = "Переоценка НМА";
				Движение.НомерЖурнала = "Рег";
			КонецЕсли;
		Иначе
			Сообщить("Ошибка: В учетной политике (международный учет) нет данных о методе переоценки ОС и НМА");
			Отказ = Истина;
			Возврат;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ПереоценкаФинАктивов(Отказ, СтруктураПараметров)
	
	БухИтогиРС = СтруктураПараметров.БухИтогиРС;
	Итоги = СтруктураПараметров.Итоги;
	
	СрезСведенийУП = СтруктураПараметров.СрезСведенийУП;
	
	ВидСубконтоКонтрагенты         = ПланыВидовХарактеристик.ВидыСубконтоМеждународные.Контрагенты;
	ВидСубконтоДоговоры            = ПланыВидовХарактеристик.ВидыСубконтоМеждународные.Договоры;
	ВидСубконтоРезервы             = ПланыВидовХарактеристик.ВидыСубконтоМеждународные.Резервы;
	ВидСубконтоВидыДоходовРасходов = ПланыВидовХарактеристик.ВидыСубконтоМеждународные.ВидыДоходовИРасходов;
	
	Для Каждого ТекСтрокаСписокФинансовыеАктивы Из СписокФинансовыеАктивы Цикл
		СчетУчета = ТекСтрокаСписокФинансовыеАктивы.СчетУчета;
		
		Если СчетУчета.ПринадлежитЭлементу(ПланыСчетов.Международный.ТекущиеАктивы) Тогда
			СчетПрочихДоходов = ПланыСчетов.Международный.ПрочиеВнереализационныеДоходы;
			СчетПрочихРасходов = ПланыСчетов.Международный.ПрочиеВнереализационныеРасходы;
		Иначе
			СчетПрочихДоходов = ПланыСчетов.Международный.РезервыНаПереоценкуФА;
			СчетПрочихРасходов = ПланыСчетов.Международный.РезервыНаПереоценкуФА;
		КонецЕсли;
		
		БухИтогиРС.РассчитатьИтоги("Международный", Итоги, "Сумма", "Счет, Субконто1", Дата, Дата, , СчетУчета, ПланыВидовХарактеристик.ВидыСубконтоМеждународные.Договоры, , , "Организация", Организация);
		ОстаточнаяСтоимость = БухИтогиРС.ПолучитьИтог("СуммаКонечныйОстатокДт", "Субконто1", ТекСтрокаСписокФинансовыеАктивы.Договор) - БухИтогиРС.ПолучитьИтог("СуммаКонечныйОстатокКт", "Субконто1", ТекСтрокаСписокФинансовыеАктивы.Договор);
		
		Дельта = ТекСтрокаСписокФинансовыеАктивы.Сумма - ОстаточнаяСтоимость;
		Если Дельта = 0 Тогда
			// Стоимость не изменилась, пропускаем этот объект
			Продолжить;
		КонецЕсли;
		
		Если ОстаточнаяСтоимость = 0 Тогда
			Сообщить("Для " + ТекСтрокаСписокФинансовыеАктивы.Договор + " не найдена остаточная стоимость по счету " + СчетУчета + "!");
			Продолжить;
		КонецЕсли;
		
		Если Дельта <> 0 Тогда
			Движение = Движения.Международный.Добавить();
			Движение.Период = Дата;
			Если Дельта > 0 Тогда
				Движение.СчетДт = СчетУчета;
				УстановитьСубконтоПроводки(Движение.СубконтоДт, СчетУчета, ВидСубконтоКонтрагенты, ТекСтрокаСписокФинансовыеАктивы.Контрагент);
				УстановитьСубконтоПроводки(Движение.СубконтоДт, СчетУчета, ВидСубконтоДоговоры, ТекСтрокаСписокФинансовыеАктивы.Договор);
				Движение.СчетКт = СчетПрочихДоходов;
				УстановитьСубконтоПроводки(Движение.СубконтоКт, СчетПрочихДоходов, ВидСубконтоВидыДоходовРасходов, СтатьяПрочихДоходов);
				УстановитьСубконтоПроводки(Движение.СубконтоКт, СчетПрочихДоходов, ВидСубконтоРезервы, Справочники.ВидыФинРезервов.РезервыНаПереоценку);
				УстановитьСубконтоПроводки(Движение.СубконтоКт, СчетПрочихДоходов, ВидСубконтоКонтрагенты, ТекСтрокаСписокФинансовыеАктивы.Контрагент);
				УстановитьСубконтоПроводки(Движение.СубконтоКт, СчетПрочихДоходов, ВидСубконтоДоговоры, ТекСтрокаСписокФинансовыеАктивы.Договор);
			Иначе
				Движение.СчетДт = СчетПрочихРасходов;
				УстановитьСубконтоПроводки(Движение.СубконтоДт, СчетПрочихРасходов, ВидСубконтоВидыДоходовРасходов, СтатьяПрочихРасходов);
				УстановитьСубконтоПроводки(Движение.СубконтоДт, СчетПрочихРасходов, ВидСубконтоРезервы, Справочники.ВидыФинРезервов.РезервыНаПереоценку);
				УстановитьСубконтоПроводки(Движение.СубконтоДт, СчетПрочихРасходов, ВидСубконтоКонтрагенты, ТекСтрокаСписокФинансовыеАктивы.Контрагент);
				УстановитьСубконтоПроводки(Движение.СубконтоДт, СчетПрочихРасходов, ВидСубконтоДоговоры, ТекСтрокаСписокФинансовыеАктивы.Договор);
				Движение.СчетКт = СчетУчета;
				УстановитьСубконтоПроводки(Движение.СубконтоКт, СчетУчета, ВидСубконтоКонтрагенты, ТекСтрокаСписокФинансовыеАктивы.Контрагент);
				УстановитьСубконтоПроводки(Движение.СубконтоКт, СчетУчета, ВидСубконтоДоговоры, ТекСтрокаСписокФинансовыеАктивы.Договор);
			КонецЕсли;
			Движение.Организация = Организация;
			Движение.Сумма = Макс(Дельта,-Дельта);
			Движение.Содержание = "Переоценка финансовых активов";
			Движение.НомерЖурнала = "Рег";
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, Режим)
	
	
	Если мУдалятьДвижения Тогда
		ОбщегоНазначения.УдалитьДвиженияРегистратора(ЭтотОбъект, Отказ);
	КонецЕсли;

	// Бухгалтерские итоги
	БухИтогиРС = Обработки.БухгалтерскиеИтоги.Создать();
	Итоги = "КонечныйОстатокДт,КонечныйОстатокКт";
	
	СрезСведенийУП = РегистрыСведений.УчетнаяПолитикаМеждународныйУчет.ПолучитьПоследнее(Дата, Новый Структура("Организация", Организация));
	
	СтруктураПараметров = Новый Структура("БухИтогиРС,Итоги,СрезСведенийУП",
											БухИтогиРС,Итоги,СрезСведенийУП);

	Если (СрезСведенийУП.МетодОценкиОсновныхСредств = Перечисления.МетодыОценкиАктивов.ПоСправедливойСтоимости) ИЛИ
		 (СрезСведенийУП.МетодУчетаИнвестиционнойСобственности = Перечисления.МетодыОценкиАктивов.ПоСправедливойСтоимости)  Тогда
		// Переоценка ОС и/или ИС
		ПереоценкаОС(Отказ, СтруктураПараметров);
	Иначе
		Сообщить("В учетной политике по международному учету запрещено переоценивать ОС.", СтатусСообщения.Важное);
	КонецЕсли;
	
	Если СрезСведенийУП.МетодОценкиОсновныхСредств = Перечисления.МетодыОценкиАктивов.ПоСправедливойСтоимости Тогда
		// Переоценка НМА
		ПереоценкаНМА(Отказ, СтруктураПараметров);
	Иначе
		Сообщить("В учетной политике по международному учету запрещено переоценивать НМА.", СтатусСообщения.Важное);
	КонецЕсли;

	// Переоценка фин. активов
	ПереоценкаФинАктивов(Отказ, СтруктураПараметров);
	
	// записываем движения регистров
	Движения.ОсновныеСредстваМеждународныйУчет.Записать();
	Движения.НематериальныеАктивыМеждународныйУчет.Записать();
	Движения.Международный.Записать();
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	Если ОбменДанными.Загрузка  Тогда
		Возврат;
	КонецЕсли;


	 
	мУдалятьДвижения = НЕ ЭтоНовый();
	
КонецПроцедуры // ПередЗаписью