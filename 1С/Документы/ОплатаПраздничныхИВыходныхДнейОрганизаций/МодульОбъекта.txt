//////////////////////////////////////////////////////////////////////////////////
//// ПЕРЕМЕННЫЕ МОДУЛЯ

// Хранит массив движений, которые документ напрямую не делает 
Перем мПроизводныеДвижения;

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

// Заполняет документ работниками, работавшими в праздники
//
Функция ЗаполнитьРаботавшимиВПраздники() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	// ПраздничныеДаты
	//		Список праздников текущего месяца
	// 
	//	Поля:
	//		ДатаКалендаря - дата, общегосударственный праздник
	// 
	// Описание:
	//
	//	выбираем из РегистрСведений.РегламентированныйПроизводственныйКалендарь дни,  
	//	помеченные пользователем как праздники
		
	Запрос.Текст =
	"ВЫБРАТЬ
	|	РегламентированныйПроизводственныйКалендарь.ДатаКалендаря КАК ДатаКалендаря
	|ПОМЕСТИТЬ ВТПраздничныеДаты
	|ИЗ
	|	РегистрСведений.РегламентированныйПроизводственныйКалендарь КАК РегламентированныйПроизводственныйКалендарь
	|ГДЕ
	|	РегламентированныйПроизводственныйКалендарь.ВидДня = ЗНАЧЕНИЕ(Перечисление.ВидыДнейПроизводственногоКалендаря.Праздник)
	|	И РегламентированныйПроизводственныйКалендарь.ДатаКалендаря МЕЖДУ &ДатаНачала И &ДатаОкончания";
	
	Запрос.УстановитьПараметр("ДатаНачала",		НачалоМесяца(ПериодРегистрации));
	Запрос.УстановитьПараметр("ДатаОкончания",	КонецМесяца(ПериодРегистрации));
	Результат = Запрос.Выполнить().Выгрузить();
	
	Если Результат[0].Количество = 0 Тогда // в этом месяце праздничных дней не оказалось
		Возврат Ложь
	КонецЕсли;
	
	Запрос.УстановитьПараметр("ПериодРегистрации",	ПериодРегистрации);
	Запрос.УстановитьПараметр("Организация",		Организация);
	Запрос.УстановитьПараметр("ГоловнаяОрганизация",ОбщегоНазначения.ГоловнаяОрганизация(Организация));
	Запрос.УстановитьПараметр("РегламентВалюта",	Константы.ВалютаРегламентированногоУчета.Получить());
	Запрос.УстановитьПараметр("Год",				Год(ПериодРегистрации));
	
	// СотрудникиПоПраздникам
	//		Таблица работающих по состоянию на каждый праздничный день с их кадровыми данными
	// 
	//	Поля:
	//		ПраздничныйДень - дата, общегосударственный праздник
	//		Сотрудник,
	//		ПодразделениеОрганизации,
	//		ГрафикРаботы,
	//		ВидГрафика,
	//		ДлительностьРабочейНедели
	// 
	// Описание:
	// 
	//	"псевдосрез" последних из РегистрСведений.РаботникиОрганизаций 
	//	для списка праздников текущего месяца
	// 
	// СостояниеСотрудниковПоПраздникам
	//		Таблица состояний работников на каждый праздничный день
	// 
	//	Поля:
	//		ПраздничныйДень - дата, общегосударственный праздник
	//		Сотрудник,
	//		Состояние
	// 
	// Описание:
	//
	//	"псевдосрез" последних из РегистрСведений.СостояниеРаботниковОрганизаций 
	//	для списка праздников текущего месяца
	// 
	// ДанныеИндивидуальныхГрафиковИТабелей
	//		Таблица сотрудников и рабочих дней при табельном учете времени
	// 
	//	Поля:
	//		РабочийДень - дата, отмеченная как рабочий день при табельном учете времени
	//		Сотрудник,
	//		Часы - суммируем все часы рабочего времени
	//      План - строка регистра записана либо инд.графиком либо табелем
	// 
	// Описание:
	//
	//	выбираем данные из рег-ра ГрафикиРаботыПоВидамВремени,	
	//	введенные доками ВводИндивидуальныхГрафиков или Табель
	//
	// ОбщиеГрафикиРаботы
	//		Данные графиков работы
	// 
	//	Поля:
	//		РабочийДень - дата, отмеченная как рабочий день в индивидуальном графике
	//		ГрафикРаботы,
	//		Часы
	// 
	// Описание:
	//
	//	выбираем данные из рег-ра ГрафикиРаботыПоВидамВремени для элементов спр-ка ГрафикиРаботы
	//
	// СводныеИндивидуальныеГрафикиРаботы
	//		Сотрудники, для которых введены сводные данные об их графике
	// 
	//	Поля:
	//		Сотрудник
	// 
	// СводныеТабелиРаботы
	//		Сотрудники, для которых введены сводные данные об их графике
	// 
	//	Поля:
	//		Сотрудник
	// 
	// НормаВремениПоСводнымИндивидуальнымГрафикам
	//		Сотрудники, для которых введены сводные данные об их графике
	// 
	//	Поля:
	//		Сотрудник
	//		ЧасовЗаМесяц - указанное пользователем значение
	// 
	// НормаВремениПоЕжедневнымИндивидуальнымГрафикам
	//		Таблица сотрудников и нормы времени в часах, введенной индивидуальными графиками
	// 
	//	Поля:
	//		Сотрудник,
	//		ЧасовЗаМесяц - суммируем все часы рабочего времени
	// 
	// Описание:
	//
	//	выбираем данные из рег-ра ГрафикиРаботыПоВидамВремени,	
	//	введенные доками ВводИндивидуальныхГрафиков, т.е. по строкам с выставленным флажком План
	// 
	// НормаВремениПоОбщимГрафикам
	//		Таблица сотрудников и нормы времени в часах по общим графикам
	// 
	//	Поля:
	//		Сотрудник,
	//		ЧасовЗаМесяц - суммируем все часы рабочего времени
	// 
	// Описание:
	//
	//	выбираем данные из рег-ра ГрафикиРаботыПоВидамВремени для общих графиков
	// 
	// ПлановыеНачисленияРаботниковОрганизаций
	//		Данные о плановых начислениях работников по состоянию на каждый праздничный день
	// 
	//	Поля:
	//		ПраздничныйДень - дата, общегосударственный праздник
	//		Сотрудник,
	//		Валюта,СпособРасчета,Размер
	// 
	// Описание:
	//
	//	"псевдосрезы" последних из рег-ров ПлановыеНачисленияРаботниковОрганизаций и РазмерТарифныхСтавок 
	//	для списка праздников текущего месяца
	// 
	//  Основной текст запроса
	//  ----------------------
	// 
	//	Поля:
	//		соответствуют реквизитам т.ч. документа
	//	
	// Описание:
	//
	// из всех сотрудников организации отбираются только те, по которым в ИБ присутствуют
	// следующие данные:
	//  или документом Табель введено фактически отработанное время в дни праздников
	//  или введен индивидуальный график, в котором праздничный день помечен как рабочий
	//  или в назначенном работнику по состоянию на дату праздника графике праздничный день помечен как рабочий
	//
	// из выборки исключаются работники, по которым не введено фактически отработанное время
	// в дни праздников, но:
	//  введен сводный индивидуальный график или сводный табель
	//  по данным кадрового учета они отсутствуют на рабочем месте, т.е. болеют, находятся в отпуске и т.п.
	//
	// отработано часов выбирается в следующем порядке:
	//	фактически отработанное время (выборка ФактическиОтработано), 
	//	затем данные индивидуального графика работы (выборка ИндивидуальныеГрафикиРаботы),
	//	затем данные текущего графика работы сотрудника (выборка ОбщиеГрафикиРаботы)
	// размер (часовая тарифная ставка) определяется в зависимости от порядка расчета основного начисления и от используемого 
	//  пользователями алгоритма пересчета оклада: либо по общероссийскому производственному календарю (с использованием
	//  среднемесячного количества часов и длительности рабочей недели графика), либо по графику работника
	//  данные по рабочим и предпраздничным дням выбираются из РегистрСведений.РегламентированныйПроизводственныйКалендарь	
	// в качестве вида расчета вводим доплату за праздничный день
	
	
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Даты.ПраздничныйДень КАК ПраздничныйДень,
	|	НАЧАЛОПЕРИОДА(Даты.ПраздничныйДень, МЕСЯЦ) КАК ТекущийМесяц,
	|	Даты.Сотрудник КАК Сотрудник,
	|	ВЫБОР
	|		КОГДА РаботникиОрганизаций.ПериодЗавершения <= Даты.ПраздничныйДень
	|				И РаботникиОрганизаций.ПериодЗавершения <> ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|			ТОГДА РаботникиОрганизаций.ПодразделениеОрганизацииЗавершения
	|		ИНАЧЕ РаботникиОрганизаций.ПодразделениеОрганизации
	|	КОНЕЦ КАК ПодразделениеОрганизации,
	|	ВЫБОР
	|		КОГДА РаботникиОрганизаций.ПериодЗавершения <= Даты.ПраздничныйДень
	|				И РаботникиОрганизаций.ПериодЗавершения <> ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|			ТОГДА РаботникиОрганизаций.ГрафикРаботыЗавершения
	|		ИНАЧЕ РаботникиОрганизаций.ГрафикРаботы
	|	КОНЕЦ КАК ГрафикРаботы,
	|	ВЫБОР
	|		КОГДА РаботникиОрганизаций.ПериодЗавершения <= Даты.ПраздничныйДень
	|				И РаботникиОрганизаций.ПериодЗавершения <> ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|			ТОГДА ВЫБОР
	|					КОГДА РаботникиОрганизаций.ГрафикРаботыЗавершения.СокращенноеРабочееВремя
	|							И РаботникиОрганизаций.ГрафикРаботыЗавершения.ГрафикПолногоРабочегоВремени <> ЗНАЧЕНИЕ(Справочник.ГрафикиРаботы.ПустаяСсылка)
	|						ТОГДА РаботникиОрганизаций.ГрафикРаботыЗавершения.ГрафикПолногоРабочегоВремени
	|					ИНАЧЕ РаботникиОрганизаций.ГрафикРаботыЗавершения
	|				КОНЕЦ
	|		КОГДА РаботникиОрганизаций.ГрафикРаботы.СокращенноеРабочееВремя
	|				И РаботникиОрганизаций.ГрафикРаботы.ГрафикПолногоРабочегоВремени <> ЗНАЧЕНИЕ(Справочник.ГрафикиРаботы.ПустаяСсылка)
	|			ТОГДА РаботникиОрганизаций.ГрафикРаботы.ГрафикПолногоРабочегоВремени
	|		ИНАЧЕ РаботникиОрганизаций.ГрафикРаботы
	|	КОНЕЦ КАК ГрафикРаботыНорма,
	|	ВЫБОР
	|		КОГДА РаботникиОрганизаций.ПериодЗавершения <= Даты.ПраздничныйДень
	|				И РаботникиОрганизаций.ПериодЗавершения <> ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|			ТОГДА РаботникиОрганизаций.ГрафикРаботыЗавершения.ВидГрафика
	|		ИНАЧЕ РаботникиОрганизаций.ГрафикРаботы.ВидГрафика
	|	КОНЕЦ КАК ВидГрафика,
	|	ЕСТЬNULL(ВЫБОР
	|			КОГДА РаботникиОрганизаций.ПериодЗавершения <= Даты.ПраздничныйДень
	|					И РаботникиОрганизаций.ПериодЗавершения <> ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|				ТОГДА ВЫБОР
	|						КОГДА РаботникиОрганизаций.ГрафикРаботыЗавершения.СокращенноеРабочееВремя
	|								И РаботникиОрганизаций.ГрафикРаботыЗавершения.ГрафикПолногоРабочегоВремени <> ЗНАЧЕНИЕ(Справочник.ГрафикиРаботы.ПустаяСсылка)
	|							ТОГДА РаботникиОрганизаций.ГрафикРаботыЗавершения.ГрафикПолногоРабочегоВремени.ДлительностьРабочейНедели
	|						ИНАЧЕ РаботникиОрганизаций.ГрафикРаботыЗавершения.ДлительностьРабочейНедели
	|					КОНЕЦ
	|			КОГДА РаботникиОрганизаций.ГрафикРаботы.СокращенноеРабочееВремя
	|					И РаботникиОрганизаций.ГрафикРаботы.ГрафикПолногоРабочегоВремени <> ЗНАЧЕНИЕ(Справочник.ГрафикиРаботы.ПустаяСсылка)
	|				ТОГДА РаботникиОрганизаций.ГрафикРаботы.ГрафикПолногоРабочегоВремени.ДлительностьРабочейНедели
	|			ИНАЧЕ РаботникиОрганизаций.ГрафикРаботы.ДлительностьРабочейНедели
	|		КОНЕЦ, 0) КАК ДлительностьРабочейНедели
	|ПОМЕСТИТЬ ВТСотрудникиПоПраздникам
	|ИЗ
	|	(ВЫБРАТЬ
	|		ПраздничныеДаты.ДатаКалендаря КАК ПраздничныйДень,
	|		РаботникиОрганизаций.Сотрудник КАК Сотрудник,
	|		МАКСИМУМ(РаботникиОрганизаций.Период) КАК Период
	|	ИЗ
	|		ВТПраздничныеДаты КАК ПраздничныеДаты
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РаботникиОрганизаций КАК РаботникиОрганизаций
	|			ПО ПраздничныеДаты.ДатаКалендаря >= РаботникиОрганизаций.Период
	|	ГДЕ
	|		РаботникиОрганизаций.Организация = &ГоловнаяОрганизация
	|	
	|	СГРУППИРОВАТЬ ПО
	|		РаботникиОрганизаций.Сотрудник,
	|		ПраздничныеДаты.ДатаКалендаря) КАК Даты
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РаботникиОрганизаций КАК РаботникиОрганизаций
	|		ПО Даты.Период = РаботникиОрганизаций.Период
	|			И Даты.Сотрудник = РаботникиОрганизаций.Сотрудник
	|ГДЕ
	|	РаботникиОрганизаций.Организация = &ГоловнаяОрганизация
	|	И ВЫБОР
	|			КОГДА РаботникиОрганизаций.ПериодЗавершения <= Даты.ПраздничныйДень
	|					И РаботникиОрганизаций.ПериодЗавершения <> ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|				ТОГДА РаботникиОрганизаций.ПричинаИзмененияСостоянияЗавершения
	|			ИНАЧЕ РаботникиОрганизаций.ПричинаИзмененияСостояния
	|		КОНЕЦ <> ЗНАЧЕНИЕ(Перечисление.ПричиныИзмененияСостояния.Увольнение)
	|	И ВЫБОР
	|			КОГДА РаботникиОрганизаций.ПериодЗавершения <= Даты.ПраздничныйДень
	|					И РаботникиОрганизаций.ПериодЗавершения <> ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|				ТОГДА РаботникиОрганизаций.ОбособленноеПодразделениеЗавершения
	|			ИНАЧЕ РаботникиОрганизаций.ОбособленноеПодразделение
	|		КОНЕЦ = &Организация
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Сотрудник,
	|	ПраздничныйДень
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Даты.ПраздничныйДень КАК ПраздничныйДень,
	|	Даты.Сотрудник КАК Сотрудник,
	|	ВЫБОР
	|		КОГДА СостояниеРаботниковОрганизаций.ПериодЗавершения <= Даты.ПраздничныйДень
	|				И СостояниеРаботниковОрганизаций.ПериодЗавершения <> ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|			ТОГДА СостояниеРаботниковОрганизаций.СостояниеЗавершения
	|		ИНАЧЕ СостояниеРаботниковОрганизаций.Состояние
	|	КОНЕЦ КАК Состояние
	|ПОМЕСТИТЬ ВТСостояниеСотрудниковПоПраздникам
	|ИЗ
	|	(ВЫБРАТЬ
	|		СписокРаботников.ПраздничныйДень КАК ПраздничныйДень,
	|		СписокРаботников.Сотрудник КАК Сотрудник,
	|		МАКСИМУМ(СостояниеРаботниковОрганизаций.Период) КАК Период
	|	ИЗ
	|		ВТСотрудникиПоПраздникам КАК СписокРаботников
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СостояниеРаботниковОрганизаций КАК СостояниеРаботниковОрганизаций
	|			ПО СписокРаботников.ПраздничныйДень >= СостояниеРаботниковОрганизаций.Период
	|				И СписокРаботников.Сотрудник = СостояниеРаботниковОрганизаций.Сотрудник
	|	
	|	СГРУППИРОВАТЬ ПО
	|		СписокРаботников.Сотрудник,
	|		СписокРаботников.ПраздничныйДень) КАК Даты
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СостояниеРаботниковОрганизаций КАК СостояниеРаботниковОрганизаций
	|		ПО Даты.Период = СостояниеРаботниковОрганизаций.Период
	|			И Даты.Сотрудник = СостояниеРаботниковОрганизаций.Сотрудник
	|ГДЕ
	|	СостояниеРаботниковОрганизаций.Организация = &ГоловнаяОрганизация
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Сотрудник,
	|	ПраздничныйДень
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	СписокРаботников.ПраздничныйДень КАК ПраздничныйДень,
	|	СписокРаботников.Сотрудник КАК Сотрудник
	|ПОМЕСТИТЬ ВТНеРаботаютПоДаннымОНачислениям
	|ИЗ
	|	ВТСотрудникиПоПраздникам КАК СписокРаботников
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрРасчета.ОсновныеНачисленияРаботниковОрганизаций КАК ОсновныеНачисленияРаботниковОрганизаций
	|		ПО СписокРаботников.Сотрудник = ОсновныеНачисленияРаботниковОрганизаций.Сотрудник
	|			И СписокРаботников.ТекущийМесяц = ОсновныеНачисленияРаботниковОрганизаций.ПериодДействия
	|			И (СписокРаботников.ПраздничныйДень МЕЖДУ ОсновныеНачисленияРаботниковОрганизаций.ПериодДействияНачало И ОсновныеНачисленияРаботниковОрганизаций.ПериодДействияКонец)
	|			И ((НЕ ОсновныеНачисленияРаботниковОрганизаций.Сторно))
	|			И (ОсновныеНачисленияРаботниковОрганизаций.ВидРасчета.ВидВремени = ЗНАЧЕНИЕ(Перечисление.ВидыВремени.ЦелодневноеНеотработанное))
	|ГДЕ
	|	ОсновныеНачисленияРаботниковОрганизаций.Сотрудник ЕСТЬ НЕ NULL 
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Сотрудник
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СписокРаботников.Сотрудник КАК Сотрудник,
	|	СписокРаботников.ПраздничныйДень КАК РабочийДень,
	|	ВЫБОР
	|		КОГДА ГрафикиРаботыПоВидамВремени.План
	|			ТОГДА ГрафикиРаботыПоВидамВремени.ДополнительноеЗначение
	|		ИНАЧЕ ГрафикиРаботыПоВидамВремени.ДополнительноеЗначениеПолное
	|	КОНЕЦ КАК Часы,
	|	ГрафикиРаботыПоВидамВремени.План
	|ПОМЕСТИТЬ ВТДанныеИндивидуальныхГрафиковИТабелей
	|ИЗ
	|	ВТСотрудникиПоПраздникам КАК СписокРаботников
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ГрафикиРаботыПоВидамВремени КАК ГрафикиРаботыПоВидамВремени
	|		ПО СписокРаботников.Сотрудник = ГрафикиРаботыПоВидамВремени.ГрафикРаботы
	|			И СписокРаботников.ТекущийМесяц = ГрафикиРаботыПоВидамВремени.Месяц
	|			И СписокРаботников.ПраздничныйДень = ГрафикиРаботыПоВидамВремени.Дата
	|			И (ГрафикиРаботыПоВидамВремени.ВидУчетаВремени = ЗНАЧЕНИЕ(Перечисление.ВидыУчетаВремени.ПоДням))
	|ГДЕ
	|	ГрафикиРаботыПоВидамВремени.ГрафикРаботы ЕСТЬ НЕ NULL 
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Сотрудник,
	|	РабочийДень
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СписокРаботников.Сотрудник КАК Сотрудник,
	|	СписокРаботников.ПраздничныйДень КАК РабочийДень,
	|	ГрафикиРаботыПоВидамВремени.ДополнительноеЗначение КАК Часы
	|ПОМЕСТИТЬ ВТОбщиеГрафикиРаботы
	|ИЗ
	|	ВТСотрудникиПоПраздникам КАК СписокРаботников
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ГрафикиРаботыПоВидамВремени КАК ГрафикиРаботыПоВидамВремени
	|		ПО СписокРаботников.ГрафикРаботы = ГрафикиРаботыПоВидамВремени.ГрафикРаботы
	|			И СписокРаботников.ТекущийМесяц = ГрафикиРаботыПоВидамВремени.Месяц
	|			И (ГрафикиРаботыПоВидамВремени.План)
	|			И (ГрафикиРаботыПоВидамВремени.ВидУчетаВремени = ЗНАЧЕНИЕ(Перечисление.ВидыУчетаВремени.ПоДням))
	|			И СписокРаботников.ПраздничныйДень = ГрафикиРаботыПоВидамВремени.Дата
	|			И (ГрафикиРаботыПоВидамВремени.ОсновноеЗначение <> 0)
	|ГДЕ
	|	ГрафикиРаботыПоВидамВремени.ГрафикРаботы ЕСТЬ НЕ NULL 
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Сотрудник,
	|	РабочийДень
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	СписокРаботников.Сотрудник КАК Сотрудник
	|ПОМЕСТИТЬ ВТСводныеИндивидуальныеГрафикиРаботы
	|ИЗ
	|	ВТСотрудникиПоПраздникам КАК СписокРаботников
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СводныеИндивидуальныеГрафики КАК СводныеИндивидуальныеГрафики
	|		ПО СписокРаботников.Сотрудник = СводныеИндивидуальныеГрафики.Сотрудник
	|			И СписокРаботников.ТекущийМесяц = СводныеИндивидуальныеГрафики.Месяц
	|ГДЕ
	|	СводныеИндивидуальныеГрафики.Сотрудник ЕСТЬ НЕ NULL 
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Сотрудник
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	СписокРаботников.Сотрудник КАК Сотрудник
	|ПОМЕСТИТЬ ВТСводныеТабелиРаботы
	|ИЗ
	|	ВТСотрудникиПоПраздникам КАК СписокРаботников
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.РабочееВремяРаботниковОрганизаций КАК РабочееВремяРаботниковОрганизаций
	|		ПО СписокРаботников.Сотрудник = РабочееВремяРаботниковОрганизаций.Сотрудник
	|			И (СписокРаботников.ТекущийМесяц = НАЧАЛОПЕРИОДА(РабочееВремяРаботниковОрганизаций.Период, ДЕНЬ))
	|			И (РабочееВремяРаботниковОрганизаций.ВЦеломЗаПериод)
	|			И (РабочееВремяРаботниковОрганизаций.ВидИспользованияРабочегоВремени.РабочееВремя)
	|ГДЕ
	|	РабочееВремяРаботниковОрганизаций.Сотрудник ЕСТЬ НЕ NULL 
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Сотрудник
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	СписокРаботников.Сотрудник КАК Сотрудник,
	|	СводныеИндивидуальныеГрафики.Часов КАК ЧасовЗаМесяц
	|ПОМЕСТИТЬ ВТНормаВремениПоСводнымИндивидуальнымГрафикам
	|ИЗ
	|	ВТСотрудникиПоПраздникам КАК СписокРаботников
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СводныеИндивидуальныеГрафики КАК СводныеИндивидуальныеГрафики
	|		ПО СписокРаботников.Сотрудник = СводныеИндивидуальныеГрафики.Сотрудник
	|			И СписокРаботников.ТекущийМесяц = СводныеИндивидуальныеГрафики.Месяц
	|ГДЕ
	|	СводныеИндивидуальныеГрафики.Сотрудник ЕСТЬ НЕ NULL 
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Сотрудник
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СписокРаботников.Сотрудник КАК Сотрудник,
	|	СУММА(ГрафикиРаботыПоВидамВремени.ДополнительноеЗначение) КАК ЧасовЗаМесяц
	|ПОМЕСТИТЬ ВТНормаВремениПоЕжедневнымИндивидуальнымГрафикам
	|ИЗ
	|	ВТСотрудникиПоПраздникам КАК СписокРаботников
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ГрафикиРаботыПоВидамВремени КАК ГрафикиРаботыПоВидамВремени
	|		ПО СписокРаботников.Сотрудник = ГрафикиРаботыПоВидамВремени.ГрафикРаботы
	|			И СписокРаботников.ТекущийМесяц = ГрафикиРаботыПоВидамВремени.Месяц
	|			И (ГрафикиРаботыПоВидамВремени.План)
	|			И (ГрафикиРаботыПоВидамВремени.ВидУчетаВремени = ЗНАЧЕНИЕ(Перечисление.ВидыУчетаВремени.ПоДням))
	|ГДЕ
	|	ГрафикиРаботыПоВидамВремени.ГрафикРаботы ЕСТЬ НЕ NULL 
	|
	|СГРУППИРОВАТЬ ПО
	|	СписокРаботников.Сотрудник
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Сотрудник
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СписокРаботников.Сотрудник КАК Сотрудник,
	|	СУММА(ГрафикиРаботыПоВидамВремени.ДополнительноеЗначение) КАК ЧасовЗаМесяц
	|ПОМЕСТИТЬ ВТНормаВремениПоОбщимГрафикам
	|ИЗ
	|	(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		СписокРаботников.Сотрудник КАК Сотрудник,
	|		СписокРаботников.ГрафикРаботыНорма КАК ГрафикРаботы,
	|		СписокРаботников.ТекущийМесяц КАК ТекущийМесяц
	|	ИЗ
	|		ВТСотрудникиПоПраздникам КАК СписокРаботников) КАК СписокРаботников
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ГрафикиРаботыПоВидамВремени КАК ГрафикиРаботыПоВидамВремени
	|		ПО СписокРаботников.ГрафикРаботы = ГрафикиРаботыПоВидамВремени.ГрафикРаботы
	|			И СписокРаботников.ТекущийМесяц = ГрафикиРаботыПоВидамВремени.Месяц
	|			И (ГрафикиРаботыПоВидамВремени.План)
	|			И (ГрафикиРаботыПоВидамВремени.ВидУчетаВремени = ЗНАЧЕНИЕ(Перечисление.ВидыУчетаВремени.ПоДням))
	|ГДЕ
	|	ГрафикиРаботыПоВидамВремени.ГрафикРаботы ЕСТЬ НЕ NULL 
	|
	|СГРУППИРОВАТЬ ПО
	|	СписокРаботников.Сотрудник
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Сотрудник
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Даты.ПраздничныйДень КАК ПраздничныйДень,
	|	Даты.Сотрудник КАК Сотрудник,
	|	ВЫБОР
	|		КОГДА ПлановыеНачисленияРаботниковОрганизаций.ПериодЗавершения <= Даты.ПраздничныйДень
	|				И ПлановыеНачисленияРаботниковОрганизаций.ПериодЗавершения <> ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|			ТОГДА ВЫБОР
	|					КОГДА ПлановыеНачисленияРаботниковОрганизаций.ВидРасчетаЗавершения.ТребуетВводаТарифногоРазряда
	|						ТОГДА РазмерТарифныхСтавок.Валюта
	|					ИНАЧЕ ПлановыеНачисленияРаботниковОрганизаций.Валюта1Завершения
	|				КОНЕЦ
	|		ИНАЧЕ ВЫБОР
	|				КОГДА ПлановыеНачисленияРаботниковОрганизаций.ВидРасчета.ТребуетВводаТарифногоРазряда
	|					ТОГДА РазмерТарифныхСтавок.Валюта
	|				ИНАЧЕ ПлановыеНачисленияРаботниковОрганизаций.Валюта1
	|			КОНЕЦ
	|	КОНЕЦ КАК Валюта,
	|	ВЫБОР
	|		КОГДА ПлановыеНачисленияРаботниковОрганизаций.ПериодЗавершения <= Даты.ПраздничныйДень
	|				И ПлановыеНачисленияРаботниковОрганизаций.ПериодЗавершения <> ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|			ТОГДА ПлановыеНачисленияРаботниковОрганизаций.ВидРасчетаЗавершения.СпособРасчета
	|		ИНАЧЕ ПлановыеНачисленияРаботниковОрганизаций.ВидРасчета.СпособРасчета
	|	КОНЕЦ КАК СпособРасчета,
	|	ВЫБОР
	|		КОГДА ПлановыеНачисленияРаботниковОрганизаций.ПериодЗавершения <= Даты.ПраздничныйДень
	|				И ПлановыеНачисленияРаботниковОрганизаций.ПериодЗавершения <> ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|			ТОГДА ВЫБОР
	|					КОГДА ПлановыеНачисленияРаботниковОрганизаций.ВидРасчетаЗавершения.ТребуетВводаТарифногоРазряда
	|						ТОГДА РазмерТарифныхСтавок.Размер
	|					ИНАЧЕ ПлановыеНачисленияРаботниковОрганизаций.Показатель1Завершения
	|				КОНЕЦ
	|		ИНАЧЕ ВЫБОР
	|				КОГДА ПлановыеНачисленияРаботниковОрганизаций.ВидРасчета.ТребуетВводаТарифногоРазряда
	|					ТОГДА РазмерТарифныхСтавок.Размер
	|				ИНАЧЕ ПлановыеНачисленияРаботниковОрганизаций.Показатель1
	|			КОНЕЦ
	|	КОНЕЦ * ВЫБОР
	|		КОГДА ВЫБОР
	|				КОГДА ПлановыеНачисленияРаботниковОрганизаций.ПериодЗавершения <= Даты.ПраздничныйДень
	|						И ПлановыеНачисленияРаботниковОрганизаций.ПериодЗавершения <> ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|					ТОГДА ВЫБОР
	|							КОГДА ПлановыеНачисленияРаботниковОрганизаций.ВидРасчетаЗавершения.ТребуетВводаТарифногоРазряда
	|								ТОГДА РазмерТарифныхСтавок.Валюта
	|							ИНАЧЕ ПлановыеНачисленияРаботниковОрганизаций.Валюта1Завершения
	|						КОНЕЦ
	|				ИНАЧЕ ВЫБОР
	|						КОГДА ПлановыеНачисленияРаботниковОрганизаций.ВидРасчета.ТребуетВводаТарифногоРазряда
	|							ТОГДА РазмерТарифныхСтавок.Валюта
	|						ИНАЧЕ ПлановыеНачисленияРаботниковОрганизаций.Валюта1
	|					КОНЕЦ
	|			КОНЕЦ В (&РегламентВалюта, ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка))
	|			ТОГДА 1
	|		ИНАЧЕ ЕСТЬNULL(Валюты.Курс / Валюты.Кратность, 0)
	|	КОНЕЦ КАК Размер
	|ПОМЕСТИТЬ ВТПлановыеНачисленияРаботниковОрганизаций
	|ИЗ
	|	(ВЫБРАТЬ
	|		СписокРаботников.ПраздничныйДень КАК ПраздничныйДень,
	|		СписокРаботников.Сотрудник КАК Сотрудник,
	|		МАКСИМУМ(ПлановыеНачисленияРаботниковОрганизаций.Период) КАК Период
	|	ИЗ
	|		ВТСотрудникиПоПраздникам КАК СписокРаботников
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПлановыеНачисленияРаботниковОрганизаций КАК ПлановыеНачисленияРаботниковОрганизаций
	|			ПО СписокРаботников.ПраздничныйДень >= ПлановыеНачисленияРаботниковОрганизаций.Период
	|				И СписокРаботников.Сотрудник = ПлановыеНачисленияРаботниковОрганизаций.Сотрудник
	|	ГДЕ
	|		ПлановыеНачисленияРаботниковОрганизаций.ВидРасчетаИзмерение = НЕОПРЕДЕЛЕНО
	|	
	|	СГРУППИРОВАТЬ ПО
	|		СписокРаботников.Сотрудник,
	|		СписокРаботников.ПраздничныйДень) КАК Даты
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПлановыеНачисленияРаботниковОрганизаций КАК ПлановыеНачисленияРаботниковОрганизаций
	|		ПО Даты.Период = ПлановыеНачисленияРаботниковОрганизаций.Период
	|			И Даты.Сотрудник = ПлановыеНачисленияРаботниковОрганизаций.Сотрудник
	|			И (ПлановыеНачисленияРаботниковОрганизаций.ВидРасчетаИзмерение = НЕОПРЕДЕЛЕНО)
	|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|			ПраздничныеДаты.ДатаКалендаря КАК ПраздничныйДень,
	|			РазмерТарифныхСтавок.ТарифныйРазряд КАК ТарифныйРазряд,
	|			МАКСИМУМ(РазмерТарифныхСтавок.Период) КАК ПериодРазряда
	|		ИЗ
	|			ВТПраздничныеДаты КАК ПраздничныеДаты
	|				ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РазмерТарифныхСтавок КАК РазмерТарифныхСтавок
	|				ПО ПраздничныеДаты.ДатаКалендаря >= РазмерТарифныхСтавок.Период
	|		
	|		СГРУППИРОВАТЬ ПО
	|			РазмерТарифныхСтавок.ТарифныйРазряд,
	|			ПраздничныеДаты.ДатаКалендаря) КАК ДатыТарифныхРазрядов
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РазмерТарифныхСтавок КАК РазмерТарифныхСтавок
	|			ПО ДатыТарифныхРазрядов.ПериодРазряда = РазмерТарифныхСтавок.Период
	|				И ДатыТарифныхРазрядов.ТарифныйРазряд = РазмерТарифныхСтавок.ТарифныйРазряд
	|		ПО Даты.ПраздничныйДень = ДатыТарифныхРазрядов.ПраздничныйДень
	|			И (ВЫБОР
	|				КОГДА ПлановыеНачисленияРаботниковОрганизаций.ПериодЗавершения <= Даты.ПраздничныйДень
	|						И ПлановыеНачисленияРаботниковОрганизаций.ПериодЗавершения <> ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|					ТОГДА ВЫБОР
	|							КОГДА ПлановыеНачисленияРаботниковОрганизаций.ВидРасчетаЗавершения.ТребуетВводаТарифногоРазряда
	|								ТОГДА ПлановыеНачисленияРаботниковОрганизаций.ТарифныйРазряд1
	|							ИНАЧЕ NULL
	|						КОНЕЦ
	|				ИНАЧЕ ВЫБОР
	|						КОГДА ПлановыеНачисленияРаботниковОрганизаций.ВидРасчета.ТребуетВводаТарифногоРазряда
	|							ТОГДА ПлановыеНачисленияРаботниковОрганизаций.ТарифныйРазряд1
	|						ИНАЧЕ NULL
	|					КОНЕЦ
	|			КОНЕЦ = ДатыТарифныхРазрядов.ТарифныйРазряд)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалютДляРасчетовСПерсоналом КАК Валюты
	|		ПО (Валюты.Период = &ПериодРегистрации)
	|			И (Валюты.Валюта = ВЫБОР
	|				КОГДА ПлановыеНачисленияРаботниковОрганизаций.ПериодЗавершения <= Даты.ПраздничныйДень
	|						И ПлановыеНачисленияРаботниковОрганизаций.ПериодЗавершения <> ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|					ТОГДА ВЫБОР
	|							КОГДА ПлановыеНачисленияРаботниковОрганизаций.ВидРасчетаЗавершения.ТребуетВводаТарифногоРазряда
	|								ТОГДА РазмерТарифныхСтавок.Валюта
	|							ИНАЧЕ ПлановыеНачисленияРаботниковОрганизаций.Валюта1Завершения
	|						КОНЕЦ
	|				ИНАЧЕ ВЫБОР
	|						КОГДА ПлановыеНачисленияРаботниковОрганизаций.ВидРасчета.ТребуетВводаТарифногоРазряда
	|							ТОГДА РазмерТарифныхСтавок.Валюта
	|						ИНАЧЕ ПлановыеНачисленияРаботниковОрганизаций.Валюта1
	|					КОНЕЦ
	|			КОНЕЦ)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Сотрудник,
	|	ПраздничныйДень
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СУММА(ВЫБОР
	|			КОГДА РегламентированныйПроизводственныйКалендарь.ВидДня = ЗНАЧЕНИЕ(Перечисление.ВидыДнейПроизводственногоКалендаря.Рабочий)
	|				ТОГДА 1
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ЧислоРабочихДней,
	|	СУММА(ВЫБОР
	|			КОГДА РегламентированныйПроизводственныйКалендарь.ВидДня = ЗНАЧЕНИЕ(Перечисление.ВидыДнейПроизводственногоКалендаря.Предпраздничный)
	|				ТОГДА 1
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ЧислоПредпраздничныхДней
	|ПОМЕСТИТЬ ВТДанныеПроизводственногоКалендаря
	|ИЗ
	|	РегистрСведений.РегламентированныйПроизводственныйКалендарь КАК РегламентированныйПроизводственныйКалендарь
	|ГДЕ
	|	РегламентированныйПроизводственныйКалендарь.Год = &Год
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МЕСЯЦ(РегламентированныйПроизводственныйКалендарь.ДатаКалендаря) КАК МесяцКалендаря,
	|	СУММА(ВЫБОР
	|			КОГДА РегламентированныйПроизводственныйКалендарь.ВидДня = ЗНАЧЕНИЕ(Перечисление.ВидыДнейПроизводственногоКалендаря.Рабочий)
	|				ТОГДА 1
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ЧислоРабочихДней,
	|	СУММА(ВЫБОР
	|			КОГДА РегламентированныйПроизводственныйКалендарь.ВидДня = ЗНАЧЕНИЕ(Перечисление.ВидыДнейПроизводственногоКалендаря.Предпраздничный)
	|				ТОГДА 1
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ЧислоПредпраздничныхДней
	|ПОМЕСТИТЬ ВТДанныеПроизводственногоКалендаряЗаМесяц
	|ИЗ
	|	РегистрСведений.РегламентированныйПроизводственныйКалендарь КАК РегламентированныйПроизводственныйКалендарь
	|ГДЕ
	|	РегламентированныйПроизводственныйКалендарь.Год = &Год
	|
	|СГРУППИРОВАТЬ ПО
	|	МЕСЯЦ(РегламентированныйПроизводственныйКалендарь.ДатаКалендаря)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	СотрудникиПоПраздникам.Сотрудник,
	|	СотрудникиПоПраздникам.Сотрудник.Физлицо КАК Физлицо,
	|	СотрудникиПоПраздникам.ПодразделениеОрганизации,
	|	СотрудникиПоПраздникам.ПраздничныйДень КАК ДатаВыхода,
	|	ВЫБОР
	|		КОГДА ФактическиОтработано.Сотрудник ЕСТЬ НЕ NULL 
	|			ТОГДА ФактическиОтработано.Часы
	|		КОГДА ИндивидуальныеГрафикиРаботы.Сотрудник ЕСТЬ НЕ NULL 
	|			ТОГДА ИндивидуальныеГрафикиРаботы.Часы
	|		ИНАЧЕ ОбщиеГрафикиРаботы.Часы
	|	КОНЕЦ КАК ОтработаноЧасов,
	|	ВЫБОР
	|		КОГДА ПлановыеНачисленияРаботниковОрганизаций.СпособРасчета В (ЗНАЧЕНИЕ(Перечисление.СпособыРасчетаОплатыТруда.ПоЧасовойТарифнойСтавке), ЗНАЧЕНИЕ(Перечисление.СпособыРасчетаОплатыТруда.СдельныйЗаработок))
	|			ТОГДА ПлановыеНачисленияРаботниковОрганизаций.Размер
	|		КОГДА ПлановыеНачисленияРаботниковОрганизаций.СпособРасчета В (ЗНАЧЕНИЕ(Перечисление.СпособыРасчетаОплатыТруда.ПоМесячнойТарифнойСтавкеПоЧасам), ЗНАЧЕНИЕ(Перечисление.СпособыРасчетаОплатыТруда.ПоМесячнойТарифнойСтавкеПоДням))
	|			ТОГДА ВЫРАЗИТЬ(ВЫБОР
	|						КОГДА ЕСТЬNULL(ВариантыВстроенныхАлгоритмовРасчетаЗарплаты.ПорядокПересчетаМесячнойСтавкиВЧасовую, ЗНАЧЕНИЕ(Перечисление.ПорядкиПересчетаМесячнойСтавкиВЧасовую.ПоСреднемуКоличествуЧасовВМесяце)) = ЗНАЧЕНИЕ(Перечисление.ПорядкиПересчетаМесячнойСтавкиВЧасовую.ПоСреднемуКоличествуЧасовВМесяце)
	|							ТОГДА ВЫБОР
	|									КОГДА ЕСТЬNULL(ДанныеПроизводственногоКалендаря.ЧислоРабочихДней, 0) = 0
	|										ТОГДА 0
	|									КОГДА СотрудникиПоПраздникам.ДлительностьРабочейНедели = 0
	|										ТОГДА 0
	|									ИНАЧЕ ПлановыеНачисленияРаботниковОрганизаций.Размер / (СотрудникиПоПраздникам.ДлительностьРабочейНедели / 5 * (ЕСТЬNULL(ДанныеПроизводственногоКалендаря.ЧислоРабочихДней, 0) + ЕСТЬNULL(ДанныеПроизводственногоКалендаря.ЧислоПредпраздничныхДней, 0)) - ЕСТЬNULL(ДанныеПроизводственногоКалендаря.ЧислоПредпраздничныхДней, 0)) * 12
	|								КОНЕЦ
	|						КОГДА ЕСТЬNULL(ВариантыВстроенныхАлгоритмовРасчетаЗарплаты.ПорядокПересчетаМесячнойСтавкиВЧасовую, ЗНАЧЕНИЕ(Перечисление.ПорядкиПересчетаМесячнойСтавкиВЧасовую.ПоСреднемуКоличествуЧасовВМесяце)) = ЗНАЧЕНИЕ(Перечисление.ПорядкиПересчетаМесячнойСтавкиВЧасовую.ПоМесячнойНормеПоПроизводственномуКалендарю)
	|							ТОГДА ВЫБОР
	|									КОГДА ЕСТЬNULL(ДанныеПроизводственногоКалендаряЗаМесяц.ЧислоРабочихДней, 0) = 0
	|										ТОГДА 0
	|									КОГДА СотрудникиПоПраздникам.ДлительностьРабочейНедели = 0
	|										ТОГДА 0
	|									ИНАЧЕ ПлановыеНачисленияРаботниковОрганизаций.Размер / (СотрудникиПоПраздникам.ДлительностьРабочейНедели / 5 * (ЕСТЬNULL(ДанныеПроизводственногоКалендаряЗаМесяц.ЧислоРабочихДней, 0) + ЕСТЬNULL(ДанныеПроизводственногоКалендаряЗаМесяц.ЧислоПредпраздничныхДней, 0)) - ЕСТЬNULL(ДанныеПроизводственногоКалендаряЗаМесяц.ЧислоПредпраздничныхДней, 0))
	|								КОНЕЦ
	|						КОГДА ВЫБОР
	|								КОГДА НормаВремениПоСводнымИндивидуальнымГрафикам.Сотрудник ЕСТЬ НЕ NULL 
	|									ТОГДА НормаВремениПоСводнымИндивидуальнымГрафикам.ЧасовЗаМесяц
	|								КОГДА НормаВремениПоЕжедневнымИндивидуальнымГрафикам.Сотрудник ЕСТЬ НЕ NULL 
	|									ТОГДА НормаВремениПоЕжедневнымИндивидуальнымГрафикам.ЧасовЗаМесяц
	|								ИНАЧЕ ЕСТЬNULL(НормаВремениПоОбщимГрафикам.ЧасовЗаМесяц, 0)
	|							КОНЕЦ = 0
	|							ТОГДА 0
	|						ИНАЧЕ ПлановыеНачисленияРаботниковОрганизаций.Размер / ВЫБОР
	|								КОГДА НормаВремениПоСводнымИндивидуальнымГрафикам.Сотрудник ЕСТЬ НЕ NULL 
	|									ТОГДА НормаВремениПоСводнымИндивидуальнымГрафикам.ЧасовЗаМесяц
	|								КОГДА НормаВремениПоЕжедневнымИндивидуальнымГрафикам.Сотрудник ЕСТЬ НЕ NULL 
	|									ТОГДА НормаВремениПоЕжедневнымИндивидуальнымГрафикам.ЧасовЗаМесяц
	|								ИНАЧЕ ЕСТЬNULL(НормаВремениПоОбщимГрафикам.ЧасовЗаМесяц, 0)
	|							КОНЕЦ
	|					КОНЕЦ КАК ЧИСЛО(15, 3))
	|		КОГДА ПлановыеНачисленияРаботниковОрганизаций.СпособРасчета = ЗНАЧЕНИЕ(Перечисление.СпособыРасчетаОплатыТруда.ПоДневнойТарифнойСтавке)
	|			ТОГДА ВЫРАЗИТЬ(ВЫБОР
	|						КОГДА СотрудникиПоПраздникам.ДлительностьРабочейНедели = 0
	|							ТОГДА 0
	|						ИНАЧЕ ПлановыеНачисленияРаботниковОрганизаций.Размер / СотрудникиПоПраздникам.ДлительностьРабочейНедели * ВЫБОР
	|								КОГДА СотрудникиПоПраздникам.ВидГрафика = ЗНАЧЕНИЕ(Перечисление.ВидыРабочихГрафиков.Шестидневка)
	|									ТОГДА 6
	|								ИНАЧЕ 5
	|							КОНЕЦ
	|					КОНЕЦ КАК ЧИСЛО(15, 3))
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК Размер,
	|	ВЫБОР
	|		КОГДА ФактическиОтработано.Сотрудник ЕСТЬ НЕ NULL 
	|			ТОГДА ФактическиОтработано.Часы
	|		КОГДА ИндивидуальныеГрафикиРаботы.Сотрудник ЕСТЬ НЕ NULL 
	|			ТОГДА ИндивидуальныеГрафикиРаботы.Часы
	|		ИНАЧЕ ОбщиеГрафикиРаботы.Часы
	|	КОНЕЦ * ВЫБОР
	|		КОГДА ПлановыеНачисленияРаботниковОрганизаций.СпособРасчета В (ЗНАЧЕНИЕ(Перечисление.СпособыРасчетаОплатыТруда.ПоЧасовойТарифнойСтавке), ЗНАЧЕНИЕ(Перечисление.СпособыРасчетаОплатыТруда.СдельныйЗаработок))
	|			ТОГДА ПлановыеНачисленияРаботниковОрганизаций.Размер
	|		КОГДА ПлановыеНачисленияРаботниковОрганизаций.СпособРасчета В (ЗНАЧЕНИЕ(Перечисление.СпособыРасчетаОплатыТруда.ПоМесячнойТарифнойСтавкеПоЧасам), ЗНАЧЕНИЕ(Перечисление.СпособыРасчетаОплатыТруда.ПоМесячнойТарифнойСтавкеПоДням))
	|			ТОГДА ВЫРАЗИТЬ(ВЫБОР
	|						КОГДА ЕСТЬNULL(ВариантыВстроенныхАлгоритмовРасчетаЗарплаты.ПорядокПересчетаМесячнойСтавкиВЧасовую, ЗНАЧЕНИЕ(Перечисление.ПорядкиПересчетаМесячнойСтавкиВЧасовую.ПоСреднемуКоличествуЧасовВМесяце)) = ЗНАЧЕНИЕ(Перечисление.ПорядкиПересчетаМесячнойСтавкиВЧасовую.ПоСреднемуКоличествуЧасовВМесяце)
	|							ТОГДА ВЫБОР
	|									КОГДА ЕСТЬNULL(ДанныеПроизводственногоКалендаря.ЧислоРабочихДней, 0) = 0
	|										ТОГДА 0
	|									КОГДА СотрудникиПоПраздникам.ДлительностьРабочейНедели = 0
	|										ТОГДА 0
	|									ИНАЧЕ ПлановыеНачисленияРаботниковОрганизаций.Размер / (СотрудникиПоПраздникам.ДлительностьРабочейНедели / 5 * (ЕСТЬNULL(ДанныеПроизводственногоКалендаря.ЧислоРабочихДней, 0) + ЕСТЬNULL(ДанныеПроизводственногоКалендаря.ЧислоПредпраздничныхДней, 0)) - ЕСТЬNULL(ДанныеПроизводственногоКалендаря.ЧислоПредпраздничныхДней, 0)) * 12
	|								КОНЕЦ
	|						КОГДА ЕСТЬNULL(ВариантыВстроенныхАлгоритмовРасчетаЗарплаты.ПорядокПересчетаМесячнойСтавкиВЧасовую, ЗНАЧЕНИЕ(Перечисление.ПорядкиПересчетаМесячнойСтавкиВЧасовую.ПоСреднемуКоличествуЧасовВМесяце)) = ЗНАЧЕНИЕ(Перечисление.ПорядкиПересчетаМесячнойСтавкиВЧасовую.ПоМесячнойНормеПоПроизводственномуКалендарю)
	|							ТОГДА ВЫБОР
	|									КОГДА ЕСТЬNULL(ДанныеПроизводственногоКалендаряЗаМесяц.ЧислоРабочихДней, 0) = 0
	|										ТОГДА 0
	|									КОГДА СотрудникиПоПраздникам.ДлительностьРабочейНедели = 0
	|										ТОГДА 0
	|									ИНАЧЕ ПлановыеНачисленияРаботниковОрганизаций.Размер / (СотрудникиПоПраздникам.ДлительностьРабочейНедели / 5 * (ЕСТЬNULL(ДанныеПроизводственногоКалендаряЗаМесяц.ЧислоРабочихДней, 0) + ЕСТЬNULL(ДанныеПроизводственногоКалендаряЗаМесяц.ЧислоПредпраздничныхДней, 0)) - ЕСТЬNULL(ДанныеПроизводственногоКалендаряЗаМесяц.ЧислоПредпраздничныхДней, 0))
	|								КОНЕЦ
	|						КОГДА ВЫБОР
	|								КОГДА НормаВремениПоСводнымИндивидуальнымГрафикам.Сотрудник ЕСТЬ НЕ NULL 
	|									ТОГДА НормаВремениПоСводнымИндивидуальнымГрафикам.ЧасовЗаМесяц
	|								КОГДА НормаВремениПоЕжедневнымИндивидуальнымГрафикам.Сотрудник ЕСТЬ НЕ NULL 
	|									ТОГДА НормаВремениПоЕжедневнымИндивидуальнымГрафикам.ЧасовЗаМесяц
	|								ИНАЧЕ ЕСТЬNULL(НормаВремениПоОбщимГрафикам.ЧасовЗаМесяц, 0)
	|							КОНЕЦ = 0
	|							ТОГДА 0
	|						ИНАЧЕ ПлановыеНачисленияРаботниковОрганизаций.Размер / ВЫБОР
	|								КОГДА НормаВремениПоСводнымИндивидуальнымГрафикам.Сотрудник ЕСТЬ НЕ NULL 
	|									ТОГДА НормаВремениПоСводнымИндивидуальнымГрафикам.ЧасовЗаМесяц
	|								КОГДА НормаВремениПоЕжедневнымИндивидуальнымГрафикам.Сотрудник ЕСТЬ НЕ NULL 
	|									ТОГДА НормаВремениПоЕжедневнымИндивидуальнымГрафикам.ЧасовЗаМесяц
	|								ИНАЧЕ ЕСТЬNULL(НормаВремениПоОбщимГрафикам.ЧасовЗаМесяц, 0)
	|							КОНЕЦ
	|					КОНЕЦ КАК ЧИСЛО(15, 3))
	|		КОГДА ПлановыеНачисленияРаботниковОрганизаций.СпособРасчета = ЗНАЧЕНИЕ(Перечисление.СпособыРасчетаОплатыТруда.ПоДневнойТарифнойСтавке)
	|			ТОГДА ВЫРАЗИТЬ(ВЫБОР
	|						КОГДА СотрудникиПоПраздникам.ДлительностьРабочейНедели = 0
	|							ТОГДА 0
	|						ИНАЧЕ ПлановыеНачисленияРаботниковОрганизаций.Размер / СотрудникиПоПраздникам.ДлительностьРабочейНедели * ВЫБОР
	|								КОГДА СотрудникиПоПраздникам.ВидГрафика = ЗНАЧЕНИЕ(Перечисление.ВидыРабочихГрафиков.Шестидневка)
	|									ТОГДА 6
	|								ИНАЧЕ 5
	|							КОНЕЦ
	|					КОНЕЦ КАК ЧИСЛО(15, 3))
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК Результат,
	|	ЗНАЧЕНИЕ(ПланВидовРасчета.ОсновныеНачисленияОрганизаций.ДоплатаЗаПраздничныеИВыходные) КАК ВидРасчета
	|ИЗ
	|	ВТСотрудникиПоПраздникам КАК СотрудникиПоПраздникам
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСостояниеСотрудниковПоПраздникам КАК СостояниеСотрудниковПоПраздникам
	|		ПО СотрудникиПоПраздникам.ПраздничныйДень = СостояниеСотрудниковПоПраздникам.ПраздничныйДень
	|			И СотрудникиПоПраздникам.Сотрудник = СостояниеСотрудниковПоПраздникам.Сотрудник
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТПлановыеНачисленияРаботниковОрганизаций КАК ПлановыеНачисленияРаботниковОрганизаций
	|		ПО СотрудникиПоПраздникам.ПраздничныйДень = ПлановыеНачисленияРаботниковОрганизаций.ПраздничныйДень
	|			И СотрудникиПоПраздникам.Сотрудник = ПлановыеНачисленияРаботниковОрганизаций.Сотрудник
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТОбщиеГрафикиРаботы КАК ОбщиеГрафикиРаботы
	|		ПО СотрудникиПоПраздникам.ПраздничныйДень = ОбщиеГрафикиРаботы.РабочийДень
	|			И СотрудникиПоПраздникам.Сотрудник = ОбщиеГрафикиРаботы.Сотрудник
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТДанныеИндивидуальныхГрафиковИТабелей КАК ФактическиОтработано
	|		ПО СотрудникиПоПраздникам.ПраздничныйДень = ФактическиОтработано.РабочийДень
	|			И СотрудникиПоПраздникам.Сотрудник = ФактическиОтработано.Сотрудник
	|			И ((НЕ ФактическиОтработано.План))
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТДанныеИндивидуальныхГрафиковИТабелей КАК ИндивидуальныеГрафикиРаботы
	|		ПО СотрудникиПоПраздникам.ПраздничныйДень = ИндивидуальныеГрафикиРаботы.РабочийДень
	|			И СотрудникиПоПраздникам.Сотрудник = ИндивидуальныеГрафикиРаботы.Сотрудник
	|			И (ИндивидуальныеГрафикиРаботы.План)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСводныеИндивидуальныеГрафикиРаботы КАК СводныеИндивидуальныеГрафикиРаботы
	|		ПО СотрудникиПоПраздникам.Сотрудник = СводныеИндивидуальныеГрафикиРаботы.Сотрудник
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТНеРаботаютПоДаннымОНачислениям КАК НеРаботаютПоДаннымОНачислениям
	|		ПО СотрудникиПоПраздникам.Сотрудник = НеРаботаютПоДаннымОНачислениям.Сотрудник
	|			И СотрудникиПоПраздникам.ПраздничныйДень = НеРаботаютПоДаннымОНачислениям.ПраздничныйДень
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСводныеТабелиРаботы КАК СводныеТабелиРаботы
	|		ПО СотрудникиПоПраздникам.Сотрудник = СводныеТабелиРаботы.Сотрудник
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТДанныеПроизводственногоКалендаря КАК ДанныеПроизводственногоКалендаря
	|		ПО (ИСТИНА)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТДанныеПроизводственногоКалендаряЗаМесяц КАК ДанныеПроизводственногоКалендаряЗаМесяц
	|		ПО (МЕСЯЦ(СотрудникиПоПраздникам.ПраздничныйДень) = ДанныеПроизводственногоКалендаряЗаМесяц.МесяцКалендаря)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТНормаВремениПоСводнымИндивидуальнымГрафикам КАК НормаВремениПоСводнымИндивидуальнымГрафикам
	|		ПО СотрудникиПоПраздникам.Сотрудник = НормаВремениПоСводнымИндивидуальнымГрафикам.Сотрудник
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТНормаВремениПоЕжедневнымИндивидуальнымГрафикам КАК НормаВремениПоЕжедневнымИндивидуальнымГрафикам
	|		ПО СотрудникиПоПраздникам.Сотрудник = НормаВремениПоЕжедневнымИндивидуальнымГрафикам.Сотрудник
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТНормаВремениПоОбщимГрафикам КАК НормаВремениПоОбщимГрафикам
	|		ПО СотрудникиПоПраздникам.Сотрудник = НормаВремениПоОбщимГрафикам.Сотрудник
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ВариантыВстроенныхАлгоритмовРасчетаЗарплаты КАК ВариантыВстроенныхАлгоритмовРасчетаЗарплаты
	|		ПО (ИСТИНА)
	|ГДЕ
	|	ВЫБОР
	|			КОГДА ФактическиОтработано.Сотрудник ЕСТЬ НЕ NULL 
	|				ТОГДА ФактическиОтработано.Сотрудник
	|			КОГДА ИндивидуальныеГрафикиРаботы.Сотрудник ЕСТЬ НЕ NULL 
	|				ТОГДА ИндивидуальныеГрафикиРаботы.Сотрудник
	|			ИНАЧЕ ОбщиеГрафикиРаботы.Сотрудник
	|		КОНЕЦ ЕСТЬ НЕ NULL 
	|	И ВЫБОР
	|			КОГДА СводныеТабелиРаботы.Сотрудник ЕСТЬ НЕ NULL 
	|				ТОГДА ЛОЖЬ
	|			КОГДА ФактическиОтработано.Сотрудник ЕСТЬ НЕ NULL 
	|				ТОГДА ИСТИНА
	|			КОГДА СводныеИндивидуальныеГрафикиРаботы.Сотрудник ЕСТЬ НЕ NULL 
	|				ТОГДА ЛОЖЬ
	|			ИНАЧЕ ИСТИНА
	|		КОНЕЦ
	|	И ВЫБОР
	|			КОГДА НеРаботаютПоДаннымОНачислениям.Сотрудник ЕСТЬ НЕ NULL 
	|				ТОГДА ЗНАЧЕНИЕ(Перечисление.СостоянияРаботникаОрганизации.НеРаботает)
	|			КОГДА ФактическиОтработано.Часы <> 0
	|				ТОГДА ЗНАЧЕНИЕ(Перечисление.СостоянияРаботникаОрганизации.Работает)
	|			КОГДА ФактическиОтработано.Часы = 0
	|				ТОГДА ЗНАЧЕНИЕ(Перечисление.СостоянияРаботникаОрганизации.НеРаботает)
	|			КОГДА ИндивидуальныеГрафикиРаботы.Часы = 0
	|				ТОГДА ЗНАЧЕНИЕ(Перечисление.СостоянияРаботникаОрганизации.НеРаботает)
	|			ИНАЧЕ ЕСТЬNULL(СостояниеСотрудниковПоПраздникам.Состояние, ЗНАЧЕНИЕ(Перечисление.СостоянияРаботникаОрганизации.Работает))
	|		КОНЕЦ = ЗНАЧЕНИЕ(Перечисление.СостоянияРаботникаОрганизации.Работает)";
	
	Начисления.Загрузить(Запрос.Выполнить().Выгрузить());
	
	Возврат Истина
	
КонецФункции //ЗаполнитьРаботавшимиВПраздники()

// Выполняет перерасчет по заданному списку физлиц
// Если передан пустой список физлиц - перерасчет не проводится
// Если в качестве списка физлиц передан не массив, а Неопределено - перерасчет по всем физлицам 
// регистратора по которым требуется перерасчет
//
// Параметры
// 	Физлица - список значений - ссылки на физлиц
// Возвращаемое значение
//  Нет
//
Процедура Перерассчитать() Экспорт
	
	// проверим можно ли что-то делать с документом
	ОписаниеПричиныОтказа = "";
	Если ПроведениеРасчетов.ДокументНельзяИзменятьЗаднимЧислом(Ссылка, ОписаниеПричиныОтказа) Тогда
		ВызватьИсключение(ОписаниеПричиныОтказа);
	КонецЕсли;
	
	Если Не ПроведениеРасчетов.НеобходимостьПерерасчета(Ссылка) Тогда
		Возврат;
	КонецЕсли;
	
	////////////////////////////////////////////////////////////////////////
	// Начисления
	
	НачатьТранзакцию();
	Прочитать();
	Выборка = СформироватьЗапросДляПерерасчета(Ссылка, ПериодРегистрации, , Истина).Выбрать();
	ЗафиксироватьТранзакцию();
	
	// если не перерассчитываем никого - возврат
	Если Выборка.Количество()=0 Тогда
		Возврат;
	КонецЕсли;
	
	Пока Выборка.Следующий() Цикл
		СтрокаНачисления = Начисления.Найти(Выборка.НомерСтроки,"НомерСтроки");
		
		СтрокаНачисления.ПодразделениеОрганизации = Выборка.НовоеПодразделениеОрганизации;
		СтрокаНачисления.Размер = Выборка.НоваяТарифнаяСтавка;
		СтрокаНачисления.Результат = Выборка.НоваяТарифнаяСтавка * СтрокаНачисления.ОтработаноЧасов;
	КонецЦикла;
	
	// записываем документ перед запросом для обновления регистров
	Записать();

	ВыборкаПоШапкеДокумента = СформироватьЗапросПоШапке().Выбрать();
	ВыборкаПоШапкеДокумента.Следующий();
	
	// получим реквизиты табличной части
	РезультатЗапросаПоНачислениям = СформироватьЗапросПоНачисления(ВыборкаПоШапкеДокумента);
	ВыборкаПоНачислениям = РезультатЗапросаПоНачислениям.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);

	Пока ВыборкаПоНачислениям.Следующий() Цикл 
		// Заполним записи в наборах записей регистров
		ДобавитьСтрокуОсновныхНачислений(ВыборкаПоШапкеДокумента, ВыборкаПоНачислениям);
		ДобавитьСтрокуВДвиженияПоРегистрамНакопления(ВыборкаПоШапкеДокумента, ВыборкаПоНачислениям);
	КонецЦикла;

	// записываем движения по регистрам
	Для Каждого НаборЗаписей Из Движения Цикл
		
		// Производные движения пропускаем
		Если мПроизводныеДвижения.Найти(НаборЗаписей) <> Неопределено Тогда
			Продолжить
		КонецЕсли;
		
		Если ТипЗнч(НаборЗаписей) = Тип("РегистрРасчетаНаборЗаписей.ОсновныеНачисленияРаботниковОрганизаций") Тогда
			НаборЗаписей.Записать(Истина, Ложь, Истина, Ложь);
		Иначе
			НаборЗаписей.Записать();
		КонецЕсли;
		
	КонецЦикла;

	////////////////////////////////////////////////////////////////////////
	// Удалим записи перерасчета, по которым выполнен перерасчет
	ПроведениеРасчетов.УдалитьСведенияОПерерасчетеДокумента(Ссылка);
	
КонецПроцедуры  // Перерассчитать()

// Заполняет документ для отобранного пользователем списка работников
//
// Параметры:
//	Запрос - запрос, полученный из настроенного пользователем построителя отчета
//							в обработке ФормированиеСпискаРаботников
//	Реквизиты - структура данных для заполнения, соответствующих реквизитам 
//							 т.ч.документа, пришедшая из обработки ФормированиеСпискаРаботников
//
Процедура Автозаполнение(Запрос = Неопределено, Реквизиты = Неопределено) Экспорт
	
	Если Запрос = Неопределено Или Реквизиты = Неопределено Тогда // не передали необходимых для заполнения данных
		Возврат
	КонецЕсли;
	
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	// создаем временную таблицу ВТСписокРаботников с сотрудниками, отобранными по критериям пользователя 
	// 
	// Поля:
	//   Сотрудник
	//   Физлицо
	//   ФИО
	//   Подразделение
	//   ГрафикРаботы
	//
	Запрос.Выполнить();
	
	Запрос.УстановитьПараметр("РегламентВалюта",	Константы.ВалютаРегламентированногоУчета.Получить());
	Запрос.УстановитьПараметр("ДатаВалютногоУчета",	ПериодРегистрации);
	Запрос.УстановитьПараметр("ПериодРегистрации",	ПериодРегистрации);
	Запрос.УстановитьПараметр("ГоловнаяОрганизация",ОбщегоНазначения.ГоловнаяОрганизация(Организация));
	Запрос.УстановитьПараметр("Год",				Год(Реквизиты.ДатаВыхода));
	Запрос.УстановитьПараметр("ТекущийМесяц",		НачалоМесяца(Реквизиты.ДатаВыхода));
	Запрос.УстановитьПараметр("ДатаВыхода",			Реквизиты.ДатаВыхода);
	Запрос.УстановитьПараметр("ОтработаноЧасов",	Реквизиты.ОтработаноЧасов);
	
	// НормаВремениПоСводнымИндивидуальнымГрафикам
	//		Сотрудники, для которых введены сводные данные об их графике
	// 
	//	Поля:
	//		Сотрудник
	//		ЧасовЗаМесяц - указанное пользователем значение
	// 
		
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	СписокРаботников.Сотрудник КАК Сотрудник,
	|	СводныеИндивидуальныеГрафики.Часов КАК ЧасовЗаМесяц
	|ПОМЕСТИТЬ ВТНормаВремениПоСводнымИндивидуальнымГрафикам
	|ИЗ
	|	ВТСписокРаботников КАК СписокРаботников
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СводныеИндивидуальныеГрафики КАК СводныеИндивидуальныеГрафики
	|		ПО СписокРаботников.Сотрудник = СводныеИндивидуальныеГрафики.Сотрудник
	|			И (СводныеИндивидуальныеГрафики.Месяц = &ТекущийМесяц)
	|ГДЕ
	|	СводныеИндивидуальныеГрафики.Сотрудник ЕСТЬ НЕ NULL 
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Сотрудник";
	Запрос.Выполнить();
	
	// НормаВремениПоЕжедневнымИндивидуальнымГрафикам
	//		Таблица сотрудников и нормы времени в часах, введенной индивидуальными графиками
	// 
	//	Поля:
	//		Сотрудник,
	//		ЧасовЗаМесяц - суммируем все часы рабочего времени
	// 
	// Описание:
	//
	//	выбираем данные из рег-ра ГрафикиРаботыПоВидамВремени,	
	//	введенные доками ВводИндивидуальныхГрафиков, т.е. по строкам с выставленным флажком План
		
	Запрос.Текст =
	"ВЫБРАТЬ
	|	СписокРаботников.Сотрудник КАК Сотрудник,
	|	СУММА(ГрафикиРаботыПоВидамВремени.ДополнительноеЗначение) КАК ЧасовЗаМесяц
	|ПОМЕСТИТЬ ВТНормаВремениПоЕжедневнымИндивидуальнымГрафикам
	|ИЗ
	|	ВТСписокРаботников КАК СписокРаботников
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ГрафикиРаботыПоВидамВремени КАК ГрафикиРаботыПоВидамВремени
	|		ПО СписокРаботников.Сотрудник = ГрафикиРаботыПоВидамВремени.ГрафикРаботы
	|			И (ГрафикиРаботыПоВидамВремени.Месяц = &ТекущийМесяц)
	|			И (ГрафикиРаботыПоВидамВремени.План)
	|			И (ГрафикиРаботыПоВидамВремени.ВидУчетаВремени = ЗНАЧЕНИЕ(Перечисление.ВидыУчетаВремени.ПоДням))
	|ГДЕ
	|	ГрафикиРаботыПоВидамВремени.ГрафикРаботы ЕСТЬ НЕ NULL 
	|
	|СГРУППИРОВАТЬ ПО
	|	СписокРаботников.Сотрудник
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Сотрудник";
	Запрос.Выполнить();
	
	// НормаВремениПоОбщимГрафикам
	//		Таблица сотрудников и нормы времени в часах по общим графикам
	// 
	//	Поля:
	//		Сотрудник,
	//		ЧасовЗаМесяц - суммируем все часы рабочего времени
	// 
	// Описание:
	//
	//	выбираем данные из рег-ра ГрафикиРаботыПоВидамВремени для общих графиков
		
	Запрос.Текст =
	"ВЫБРАТЬ
	|	СписокРаботников.Сотрудник КАК Сотрудник,
	|	СУММА(ГрафикиРаботыПоВидамВремени.ДополнительноеЗначение) КАК ЧасовЗаМесяц
	|ПОМЕСТИТЬ ВТНормаВремениПоОбщимГрафикам
	|ИЗ
	|	ВТСписокРаботников КАК СписокРаботников
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ГрафикиРаботыПоВидамВремени КАК ГрафикиРаботыПоВидамВремени
	|		ПО (ВЫБОР
	|				КОГДА СписокРаботников.ГрафикРаботы.СокращенноеРабочееВремя
	|						И СписокРаботников.ГрафикРаботы.ГрафикПолногоРабочегоВремени <> ЗНАЧЕНИЕ(Справочник.ГрафикиРаботы.ПустаяСсылка)
	|					ТОГДА СписокРаботников.ГрафикРаботы.ГрафикПолногоРабочегоВремени
	|				ИНАЧЕ СписокРаботников.ГрафикРаботы
	|			КОНЕЦ = ГрафикиРаботыПоВидамВремени.ГрафикРаботы)
	|			И (ГрафикиРаботыПоВидамВремени.Месяц = &ТекущийМесяц)
	|			И (ГрафикиРаботыПоВидамВремени.План)
	|			И (ГрафикиРаботыПоВидамВремени.ВидУчетаВремени = ЗНАЧЕНИЕ(Перечисление.ВидыУчетаВремени.ПоДням))
	|ГДЕ
	|	ГрафикиРаботыПоВидамВремени.ГрафикРаботы ЕСТЬ НЕ NULL 
	|
	|СГРУППИРОВАТЬ ПО
	|	СписокРаботников.Сотрудник
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Сотрудник";
	Запрос.Выполнить();
	
	// ДанныеИндивидуальныхГрафиковИТабелей
	//		Таблица сотрудников и рабочих дней при табельном учете времени
	// 
	//	Поля:
	//		РабочийДень - дата, отмеченная как рабочий день при табельном учете времени
	//		Сотрудник,
	//		Часы - суммируем все часы рабочего времени
	//      План - строка регистра записана либо инд.графиком либо табелем
	// 
	// Описание:
	//
	//	выбираем данные из рег-ра ГрафикиРаботыПоВидамВремени,	
	//	введенные доками ВводИндивидуальныхГрафиков или Табель
		
		
	Запрос.Текст =
	"ВЫБРАТЬ
	|	СписокРаботников.Сотрудник КАК Сотрудник,
	|	ГрафикиРаботыПоВидамВремени.План,
	|	ГрафикиРаботыПоВидамВремени.ДополнительноеЗначение КАК Часы
	|ПОМЕСТИТЬ ВТДанныеИндивидуальныхГрафиковИТабелей
	|ИЗ
	|	ВТСписокРаботников КАК СписокРаботников
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ГрафикиРаботыПоВидамВремени КАК ГрафикиРаботыПоВидамВремени
	|		ПО СписокРаботников.Сотрудник = ГрафикиРаботыПоВидамВремени.ГрафикРаботы
	|			И (ГрафикиРаботыПоВидамВремени.Месяц = &ТекущийМесяц)
	|			И (ГрафикиРаботыПоВидамВремени.ВидУчетаВремени = ЗНАЧЕНИЕ(Перечисление.ВидыУчетаВремени.ПоДням))
	|			И (ГрафикиРаботыПоВидамВремени.Дата = &ДатаВыхода)
	|ГДЕ
	|	ГрафикиРаботыПоВидамВремени.ГрафикРаботы ЕСТЬ НЕ NULL 
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Сотрудник";
	Запрос.Выполнить();
	
	// ОбщиеГрафикиРаботы
	//		Данные графиков работы
	// 
	//	Поля:
	//		РабочийДень - дата, отмеченная как рабочий день в индивидуальном графике
	//		ГрафикРаботы,
	//		Часы
	// 
	// Описание:
	//
	//	выбираем данные из рег-ра ГрафикиРаботыПоВидамВремени для элементов спр-ка ГрафикиРаботы
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	СписокРаботников.Сотрудник КАК Сотрудник,
	|	ГрафикиРаботыПоВидамВремени.ДополнительноеЗначение КАК Часы
	|ПОМЕСТИТЬ ВТОбщиеГрафикиРаботы
	|ИЗ
	|	ВТСписокРаботников КАК СписокРаботников
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ГрафикиРаботыПоВидамВремени КАК ГрафикиРаботыПоВидамВремени
	|		ПО (ВЫБОР
	|				КОГДА СписокРаботников.ГрафикРаботы.СокращенноеРабочееВремя
	|						И СписокРаботников.ГрафикРаботы.ГрафикПолногоРабочегоВремени <> ЗНАЧЕНИЕ(Справочник.ГрафикиРаботы.ПустаяСсылка)
	|					ТОГДА СписокРаботников.ГрафикРаботы.ГрафикПолногоРабочегоВремени
	|				ИНАЧЕ СписокРаботников.ГрафикРаботы
	|			КОНЕЦ = ГрафикиРаботыПоВидамВремени.ГрафикРаботы)
	|			И (ГрафикиРаботыПоВидамВремени.Месяц = &ТекущийМесяц)
	|			И (ГрафикиРаботыПоВидамВремени.План)
	|			И (ГрафикиРаботыПоВидамВремени.ВидУчетаВремени = ЗНАЧЕНИЕ(Перечисление.ВидыУчетаВремени.ПоДням))
	|			И (ГрафикиРаботыПоВидамВремени.Дата = &ДатаВыхода)
	|ГДЕ
	|	ГрафикиРаботыПоВидамВремени.ГрафикРаботы ЕСТЬ НЕ NULL 
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Сотрудник";
	Запрос.Выполнить();
	
	// ДанныеПроизводственногоКалендаряЗаМесяц
	//		Данные производственного календаря за месяц
	// 
	//	Поля:
	//		ЧислоРабочихДней,
	//		ЧислоПредпраздничныхДней
	// 
	// Описание:
	//
	//	выбираем данные из рег-ра РегламентированныйПроизводственныйКалендарь за месяц, в который сотрудник работал
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	СУММА(ВЫБОР
	|			КОГДА РегламентированныйПроизводственныйКалендарь.ВидДня = ЗНАЧЕНИЕ(Перечисление.ВидыДнейПроизводственногоКалендаря.Рабочий)
	|				ТОГДА 1
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ЧислоРабочихДней,
	|	СУММА(ВЫБОР
	|			КОГДА РегламентированныйПроизводственныйКалендарь.ВидДня = ЗНАЧЕНИЕ(Перечисление.ВидыДнейПроизводственногоКалендаря.Предпраздничный)
	|				ТОГДА 1
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ЧислоПредпраздничныхДней
	|ПОМЕСТИТЬ ВТДанныеПроизводственногоКалендаряЗаМесяц
	|ИЗ
	|	РегистрСведений.РегламентированныйПроизводственныйКалендарь КАК РегламентированныйПроизводственныйКалендарь
	|ГДЕ
	|	НАЧАЛОПЕРИОДА(РегламентированныйПроизводственныйКалендарь.ДатаКалендаря, МЕСЯЦ) = &ТекущийМесяц";
	Запрос.Выполнить();
	
	// СотрудникиРазмерыНачислений 
	// таблица сотрудников с рассчитанной для каждого часовой тарифной ставкой
	// Поля:
	//   Сотрудник
	//   Физлицо
	//   ФИО
	//   ПодразделениеОрганизации
	//   ГрафикРаботы
	//   ДатаВыхода
	//   ОтработаноЧасов
	//   Размер
	//
	// Описание:
	//	
	// 	1) выбираем из ВТСписокРаботников список сотрудников, присоединяем из рег-ра ПлановыеНачисленияРаботниковОрганизаций
	//		данные об "основном" начислении - способ расчета, размер и валюту (с учетом возможжного использования тарифных разрядов)
	// 	2) рассчитываем часовую тарифную ставку в зависимости от порядка расчета основного начисления и от используемого 
	//      пользователями алгоритма пересчета оклада: либо по общероссийскому производственному календарю (с использованием
	//      среднемесячного количества часов и длительности рабочей недели графика), либо по графику работника
	//      данные по рабочим и предпраздничным дням выбираются из РегистрСведений.РегламентированныйПроизводственныйКалендарь	
	//      

	Запрос.Текст =
	"ВЫБРАТЬ
	|	РаботникиИНачисления.Сотрудник,
	|	РаботникиИНачисления.Физлицо,
	|	РаботникиИНачисления.ФИО,
	|	РаботникиИНачисления.ПодразделениеОрганизации,
	|	РаботникиИНачисления.ГрафикРаботы,
	|	&ТекущийМесяц КАК ТекущийМесяц,
	|	&ДатаВыхода КАК ДатаВыхода,
	|	&ОтработаноЧасов КАК ОтработаноЧасов,
	|	ВЫБОР
	|		КОГДА РаботникиИНачисления.СпособРасчета В (ЗНАЧЕНИЕ(Перечисление.СпособыРасчетаОплатыТруда.ПоЧасовойТарифнойСтавке), ЗНАЧЕНИЕ(Перечисление.СпособыРасчетаОплатыТруда.СдельныйЗаработок))
	|			ТОГДА РаботникиИНачисления.ОкладТариф
	|		КОГДА РаботникиИНачисления.СпособРасчета В (ЗНАЧЕНИЕ(Перечисление.СпособыРасчетаОплатыТруда.ПоМесячнойТарифнойСтавкеПоЧасам), ЗНАЧЕНИЕ(Перечисление.СпособыРасчетаОплатыТруда.ПоМесячнойТарифнойСтавкеПоДням))
	|			ТОГДА ВЫРАЗИТЬ(ВЫБОР
	|						КОГДА ЕСТЬNULL(ВариантыВстроенныхАлгоритмовРасчетаЗарплаты.ПорядокПересчетаМесячнойСтавкиВЧасовую, ЗНАЧЕНИЕ(Перечисление.ПорядкиПересчетаМесячнойСтавкиВЧасовую.ПоСреднемуКоличествуЧасовВМесяце)) = ЗНАЧЕНИЕ(Перечисление.ПорядкиПересчетаМесячнойСтавкиВЧасовую.ПоСреднемуКоличествуЧасовВМесяце)
	|							ТОГДА ВЫБОР
	|									КОГДА ЕСТЬNULL(ДанныеПроизводственногоКалендаря.ЧислоРабочихДней, 0) = 0
	|										ТОГДА 0
	|									КОГДА РаботникиИНачисления.ДлительностьРабочейНедели = 0
	|										ТОГДА 0
	|									ИНАЧЕ РаботникиИНачисления.ОкладТариф / (РаботникиИНачисления.ДлительностьРабочейНедели / 5 * (ЕСТЬNULL(ДанныеПроизводственногоКалендаря.ЧислоРабочихДней, 0) + ЕСТЬNULL(ДанныеПроизводственногоКалендаря.ЧислоПредпраздничныхДней, 0)) - ЕСТЬNULL(ДанныеПроизводственногоКалендаря.ЧислоПредпраздничныхДней, 0)) * 12
	|								КОНЕЦ
	|						КОГДА ЕСТЬNULL(ВариантыВстроенныхАлгоритмовРасчетаЗарплаты.ПорядокПересчетаМесячнойСтавкиВЧасовую, ЗНАЧЕНИЕ(Перечисление.ПорядкиПересчетаМесячнойСтавкиВЧасовую.ПоСреднемуКоличествуЧасовВМесяце)) = ЗНАЧЕНИЕ(Перечисление.ПорядкиПересчетаМесячнойСтавкиВЧасовую.ПоМесячнойНормеПоПроизводственномуКалендарю)
	|							ТОГДА ВЫБОР
	|									КОГДА ЕСТЬNULL(ДанныеПроизводственногоКалендаряЗаМесяц.ЧислоРабочихДней, 0) = 0
	|										ТОГДА 0
	|									КОГДА РаботникиИНачисления.ДлительностьРабочейНедели = 0
	|										ТОГДА 0
	|									ИНАЧЕ РаботникиИНачисления.ОкладТариф / (РаботникиИНачисления.ДлительностьРабочейНедели / 5 * (ЕСТЬNULL(ДанныеПроизводственногоКалендаряЗаМесяц.ЧислоРабочихДней, 0) + ЕСТЬNULL(ДанныеПроизводственногоКалендаряЗаМесяц.ЧислоПредпраздничныхДней, 0)) - ЕСТЬNULL(ДанныеПроизводственногоКалендаряЗаМесяц.ЧислоПредпраздничныхДней, 0))
	|								КОНЕЦ
	|						КОГДА ВЫБОР
	|								КОГДА НормаВремениПоСводнымИндивидуальнымГрафикам.Сотрудник ЕСТЬ НЕ NULL 
	|									ТОГДА НормаВремениПоСводнымИндивидуальнымГрафикам.ЧасовЗаМесяц
	|								КОГДА НормаВремениПоЕжедневнымИндивидуальнымГрафикам.Сотрудник ЕСТЬ НЕ NULL 
	|									ТОГДА НормаВремениПоЕжедневнымИндивидуальнымГрафикам.ЧасовЗаМесяц
	|								ИНАЧЕ ЕСТЬNULL(НормаВремениПоОбщимГрафикам.ЧасовЗаМесяц, 0)
	|							КОНЕЦ = 0
	|							ТОГДА 0
	|						ИНАЧЕ РаботникиИНачисления.ОкладТариф / ВЫБОР
	|								КОГДА НормаВремениПоСводнымИндивидуальнымГрафикам.Сотрудник ЕСТЬ НЕ NULL 
	|									ТОГДА НормаВремениПоСводнымИндивидуальнымГрафикам.ЧасовЗаМесяц
	|								КОГДА НормаВремениПоЕжедневнымИндивидуальнымГрафикам.Сотрудник ЕСТЬ НЕ NULL 
	|									ТОГДА НормаВремениПоЕжедневнымИндивидуальнымГрафикам.ЧасовЗаМесяц
	|								ИНАЧЕ ЕСТЬNULL(НормаВремениПоОбщимГрафикам.ЧасовЗаМесяц, 0)
	|							КОНЕЦ
	|					КОНЕЦ КАК ЧИСЛО(15, 3))
	|		КОГДА РаботникиИНачисления.СпособРасчета = ЗНАЧЕНИЕ(Перечисление.СпособыРасчетаОплатыТруда.ПоДневнойТарифнойСтавке)
	|			ТОГДА ВЫРАЗИТЬ(ВЫБОР
	|						КОГДА РаботникиИНачисления.ДлительностьРабочейНедели = 0
	|							ТОГДА 0
	|						ИНАЧЕ РаботникиИНачисления.ОкладТариф / РаботникиИНачисления.ДлительностьРабочейНедели * ВЫБОР
	|								КОГДА РаботникиИНачисления.ВидГрафика = ЗНАЧЕНИЕ(Перечисление.ВидыРабочихГрафиков.Шестидневка)
	|									ТОГДА 6
	|								ИНАЧЕ 5
	|							КОНЕЦ
	|					КОНЕЦ КАК ЧИСЛО(15, 3))
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК Размер
	|ПОМЕСТИТЬ ВТСотрудникиРазмерыНачислений
	|ИЗ
	|	(ВЫБРАТЬ
	|		СписокРаботников.Сотрудник КАК Сотрудник,
	|		СписокРаботников.ФИО КАК ФИО,
	|		СписокРаботников.Физлицо КАК Физлицо,
	|		СписокРаботников.Подразделение КАК ПодразделениеОрганизации,
	|		СписокРаботников.ГрафикРаботы КАК ГрафикРаботы,
	|		ЕСТЬNULL(ВЫБОР
	|				КОГДА СписокРаботников.ГрафикРаботы.СокращенноеРабочееВремя
	|						И СписокРаботников.ГрафикРаботы.ГрафикПолногоРабочегоВремени <> ЗНАЧЕНИЕ(Справочник.ГрафикиРаботы.ПустаяСсылка)
	|					ТОГДА СписокРаботников.ГрафикРаботы.ГрафикПолногоРабочегоВремени.ДлительностьРабочейНедели
	|				ИНАЧЕ СписокРаботников.ГрафикРаботы.ДлительностьРабочейНедели
	|			КОНЕЦ, 0) КАК ДлительностьРабочейНедели,
	|		СписокРаботников.ГрафикРаботы.ВидГрафика КАК ВидГрафика,
	|		ВЫБОР
	|			КОГДА ПлановыеНачисленияРаботниковОрганизацийСрезПоследних.ПериодЗавершения <= &ДатаВыхода
	|					И ПлановыеНачисленияРаботниковОрганизацийСрезПоследних.ПериодЗавершения <> ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|				ТОГДА ВЫБОР
	|						КОГДА ПлановыеНачисленияРаботниковОрганизацийСрезПоследних.ВидРасчетаЗавершения.ТребуетВводаТарифногоРазряда
	|							ТОГДА РазмерТарифныхСтавокСрезПоследних.Размер
	|						ИНАЧЕ ПлановыеНачисленияРаботниковОрганизацийСрезПоследних.Показатель1Завершения
	|					КОНЕЦ
	|			ИНАЧЕ ВЫБОР
	|					КОГДА ПлановыеНачисленияРаботниковОрганизацийСрезПоследних.ВидРасчета.ТребуетВводаТарифногоРазряда
	|						ТОГДА РазмерТарифныхСтавокСрезПоследних.Размер
	|					ИНАЧЕ ПлановыеНачисленияРаботниковОрганизацийСрезПоследних.Показатель1
	|				КОНЕЦ
	|		КОНЕЦ * ВЫБОР
	|			КОГДА ВЫБОР
	|					КОГДА ПлановыеНачисленияРаботниковОрганизацийСрезПоследних.ПериодЗавершения <= &ДатаВыхода
	|							И ПлановыеНачисленияРаботниковОрганизацийСрезПоследних.ПериодЗавершения <> ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|						ТОГДА ВЫБОР
	|								КОГДА ПлановыеНачисленияРаботниковОрганизацийСрезПоследних.ВидРасчетаЗавершения.ТребуетВводаТарифногоРазряда
	|									ТОГДА РазмерТарифныхСтавокСрезПоследних.Валюта
	|								ИНАЧЕ ПлановыеНачисленияРаботниковОрганизацийСрезПоследних.Валюта1Завершения
	|							КОНЕЦ
	|					ИНАЧЕ ВЫБОР
	|							КОГДА ПлановыеНачисленияРаботниковОрганизацийСрезПоследних.ВидРасчета.ТребуетВводаТарифногоРазряда
	|								ТОГДА РазмерТарифныхСтавокСрезПоследних.Валюта
	|							ИНАЧЕ ПлановыеНачисленияРаботниковОрганизацийСрезПоследних.Валюта1
	|						КОНЕЦ
	|				КОНЕЦ В (&РегламентВалюта, ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка))
	|				ТОГДА 1
	|			ИНАЧЕ ЕСТЬNULL(Валюты.Курс / Валюты.Кратность, 0)
	|		КОНЕЦ КАК ОкладТариф,
	|		ВЫБОР
	|			КОГДА ПлановыеНачисленияРаботниковОрганизацийСрезПоследних.ПериодЗавершения <= &ДатаВыхода
	|					И ПлановыеНачисленияРаботниковОрганизацийСрезПоследних.ПериодЗавершения <> ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|				ТОГДА ПлановыеНачисленияРаботниковОрганизацийСрезПоследних.ВидРасчетаЗавершения.СпособРасчета
	|			ИНАЧЕ ПлановыеНачисленияРаботниковОрганизацийСрезПоследних.ВидРасчета.СпособРасчета
	|		КОНЕЦ КАК СпособРасчета
	|	ИЗ
	|		ВТСписокРаботников КАК СписокРаботников
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПлановыеНачисленияРаботниковОрганизаций.СрезПоследних(
	|					&ДатаВыхода,
	|					ВидРасчетаИзмерение.Ссылка ЕСТЬ NULL 
	|						И Сотрудник В
	|							(ВЫБРАТЬ
	|								ВТСписокРаботников.Сотрудник
	|							ИЗ
	|								ВТСписокРаботников)) КАК ПлановыеНачисленияРаботниковОрганизацийСрезПоследних
	|				ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РазмерТарифныхСтавок.СрезПоследних(&ДатаВыхода, ) КАК РазмерТарифныхСтавокСрезПоследних
	|				ПО (ВЫБОР
	|						КОГДА ПлановыеНачисленияРаботниковОрганизацийСрезПоследних.ПериодЗавершения <= &ДатаВыхода
	|								И ПлановыеНачисленияРаботниковОрганизацийСрезПоследних.ПериодЗавершения <> ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|							ТОГДА ПлановыеНачисленияРаботниковОрганизацийСрезПоследних.ТарифныйРазряд1Завершения
	|						ИНАЧЕ ПлановыеНачисленияРаботниковОрганизацийСрезПоследних.ТарифныйРазряд1
	|					КОНЕЦ = РазмерТарифныхСтавокСрезПоследних.ТарифныйРазряд)
	|			ПО СписокРаботников.Сотрудник = ПлановыеНачисленияРаботниковОрганизацийСрезПоследних.Сотрудник
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалютДляРасчетовСПерсоналом КАК Валюты
	|			ПО (Валюты.Период = &ДатаВалютногоУчета)
	|				И (Валюты.Валюта = ВЫБОР
	|					КОГДА ПлановыеНачисленияРаботниковОрганизацийСрезПоследних.ПериодЗавершения <= &ДатаВыхода
	|							И ПлановыеНачисленияРаботниковОрганизацийСрезПоследних.ПериодЗавершения <> ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|						ТОГДА ВЫБОР
	|								КОГДА ПлановыеНачисленияРаботниковОрганизацийСрезПоследних.ВидРасчетаЗавершения.ТребуетВводаТарифногоРазряда
	|									ТОГДА РазмерТарифныхСтавокСрезПоследних.Валюта
	|								ИНАЧЕ ПлановыеНачисленияРаботниковОрганизацийСрезПоследних.Валюта1Завершения
	|							КОНЕЦ
	|					ИНАЧЕ ВЫБОР
	|							КОГДА ПлановыеНачисленияРаботниковОрганизацийСрезПоследних.ВидРасчета.ТребуетВводаТарифногоРазряда
	|								ТОГДА РазмерТарифныхСтавокСрезПоследних.Валюта
	|							ИНАЧЕ ПлановыеНачисленияРаботниковОрганизацийСрезПоследних.Валюта1
	|						КОНЕЦ
	|				КОНЕЦ)) КАК РаботникиИНачисления
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТНормаВремениПоСводнымИндивидуальнымГрафикам КАК НормаВремениПоСводнымИндивидуальнымГрафикам
	|		ПО РаботникиИНачисления.Сотрудник = НормаВремениПоСводнымИндивидуальнымГрафикам.Сотрудник
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТНормаВремениПоЕжедневнымИндивидуальнымГрафикам КАК НормаВремениПоЕжедневнымИндивидуальнымГрафикам
	|		ПО РаботникиИНачисления.Сотрудник = НормаВремениПоЕжедневнымИндивидуальнымГрафикам.Сотрудник
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТНормаВремениПоОбщимГрафикам КАК НормаВремениПоОбщимГрафикам
	|		ПО РаботникиИНачисления.Сотрудник = НормаВремениПоОбщимГрафикам.Сотрудник
	|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|			СУММА(ВЫБОР
	|					КОГДА РегламентированныйПроизводственныйКалендарь.ВидДня = ЗНАЧЕНИЕ(Перечисление.ВидыДнейПроизводственногоКалендаря.Рабочий)
	|						ТОГДА 1
	|					ИНАЧЕ 0
	|				КОНЕЦ) КАК ЧислоРабочихДней,
	|			СУММА(ВЫБОР
	|					КОГДА РегламентированныйПроизводственныйКалендарь.ВидДня = ЗНАЧЕНИЕ(Перечисление.ВидыДнейПроизводственногоКалендаря.Предпраздничный)
	|						ТОГДА 1
	|					ИНАЧЕ 0
	|				КОНЕЦ) КАК ЧислоПредпраздничныхДней
	|		ИЗ
	|			РегистрСведений.РегламентированныйПроизводственныйКалендарь КАК РегламентированныйПроизводственныйКалендарь
	|		ГДЕ
	|			РегламентированныйПроизводственныйКалендарь.Год = &Год) КАК ДанныеПроизводственногоКалендаря
	|		ПО (ИСТИНА)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТДанныеПроизводственногоКалендаряЗаМесяц КАК ДанныеПроизводственногоКалендаряЗаМесяц
	|		ПО (ИСТИНА)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ВариантыВстроенныхАлгоритмовРасчетаЗарплаты КАК ВариантыВстроенныхАлгоритмовРасчетаЗарплаты
	|		ПО (ИСТИНА)";
	Запрос.Выполнить();
	
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	СписокРаботников.ФИО КАК ФИО,
	|	СписокРаботников.Сотрудник,
	|	СписокРаботников.Физлицо,
	|	СписокРаботников.ПодразделениеОрганизации,
	|	СписокРаботников.ДатаВыхода КАК ДатаВыхода,
	|	СписокРаботников.ОтработаноЧасов КАК ОтработаноЧасов,
	|	СписокРаботников.Размер КАК Размер,
	|	СписокРаботников.ОтработаноЧасов * СписокРаботников.Размер КАК Результат,
	|	ЗНАЧЕНИЕ(ПланВидовРасчета.ОсновныеНачисленияОрганизаций.ДоплатаЗаПраздничныеИВыходные) КАК ВидРасчета
	|ИЗ
	|	ВТСотрудникиРазмерыНачислений КАК СписокРаботников
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	СписокРаботников.ФИО,
	|	СписокРаботников.Сотрудник,
	|	СписокРаботников.Физлицо,
	|	СписокРаботников.ПодразделениеОрганизации,
	|	СписокРаботников.ДатаВыхода,
	|	СписокРаботников.ОтработаноЧасов,
	|	СписокРаботников.Размер,
	|	СписокРаботников.ОтработаноЧасов * СписокРаботников.Размер,
	|	ЗНАЧЕНИЕ(ПланВидовРасчета.ОсновныеНачисленияОрганизаций.ОплатаПраздничныхИВыходных)
	|ИЗ
	|	ВТСотрудникиРазмерыНачислений КАК СписокРаботников
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТОбщиеГрафикиРаботы КАК ОбщиеГрафикиРаботы
	|		ПО СписокРаботников.Сотрудник = ОбщиеГрафикиРаботы.Сотрудник
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТДанныеИндивидуальныхГрафиковИТабелей КАК ДанныеИндивидуальныхГрафиков
	|		ПО СписокРаботников.Сотрудник = ДанныеИндивидуальныхГрафиков.Сотрудник
	|			И (ДанныеИндивидуальныхГрафиков.План)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТДанныеИндивидуальныхГрафиковИТабелей КАК ФактическиОтработано
	|		ПО СписокРаботников.Сотрудник = ФактическиОтработано.Сотрудник
	|			И ((НЕ ФактическиОтработано.План))
	|ГДЕ
	|	ЕСТЬNULL(ФактическиОтработано.Часы, 0) = 0
	|	И ЕСТЬNULL(ДанныеИндивидуальныхГрафиков.Часы, ОбщиеГрафикиРаботы.Часы) = 0
	|
	|УПОРЯДОЧИТЬ ПО
	|	ФИО,
	|	ВидРасчета";
	
	Начисления.Загрузить(Запрос.Выполнить().Выгрузить());
	
КонецПроцедуры //Автозаполнение()

// Заполняет документ по перерассчитываемому документу
//
Процедура ЗаполнитьПоПерерассчитываемомуДокументу(ИсходныйДокумент, Сотрудники = Неопределено) Экспорт

	ПерерассчитываемыйДокумент = ИсходныйДокумент.Ссылка;
 	ЗаполнитьЗначенияСвойств(ЭтотОбъект,ИсходныйДокумент,,
		"Проведен, Номер, Дата, ПометкаУдаления, ПерерассчитываемыйДокумент, ПериодРегистрации, Комментарий, Ответственный"); // кроме указанных

	ПериодПерерассчитываемогоДокумента = ИсходныйДокумент.ПериодРегистрации;
	Выборка = СформироватьЗапросДляПерерасчета(ПерерассчитываемыйДокумент, ПериодПерерассчитываемогоДокумента, Сотрудники).Выбрать();
	
	Пока Выборка.Следующий() Цикл
		// сторно-строка
		НоваяСтрока = Начисления.Добавить();
		НоваяСтрока.Сотрудник					= Выборка.Сотрудник;
		НоваяСтрока.Физлицо						= Выборка.Физлицо;
		НоваяСтрока.ПодразделениеОрганизации	= Выборка.ПодразделениеОрганизации;
		НоваяСтрока.ДатаВыхода					= Выборка.ДатаВыхода;
		НоваяСтрока.ВидРасчета					= Выборка.ВидРасчета;
		НоваяСтрока.Размер						= Выборка.Размер;
		НоваяСтрока.ОтработаноЧасов				= - Выборка.ОтработаноЧасов;
		НоваяСтрока.Результат					= - Выборка.Результат;
		НоваяСтрока.Сторно						= Истина;
		
		// новая рассчитываемая строка
		НоваяСтрока = Начисления.Добавить();
		НоваяСтрока.Сотрудник					= Выборка.Сотрудник;
		НоваяСтрока.Физлицо						= Выборка.Физлицо;
		НоваяСтрока.ПодразделениеОрганизации	= Выборка.НовоеПодразделениеОрганизации;
		НоваяСтрока.ДатаВыхода					= Выборка.ДатаВыхода;
		НоваяСтрока.ВидРасчета					= Выборка.ВидРасчета;
		НоваяСтрока.Размер						= Выборка.НоваяТарифнаяСтавка;
		НоваяСтрока.ОтработаноЧасов				= Выборка.ОтработаноЧасов;
		НоваяСтрока.Результат					= Выборка.НоваяТарифнаяСтавка * Выборка.ОтработаноЧасов;
	КонецЦикла;
	
КонецПроцедуры  // ЗаполнитьПоПерерассчитываемомуДокументу()

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОБЩЕГО НАЗНАЧЕНИЯ

// Формирует запрос для заполнения ТЧ документа по данным перерасчетов
Функция СформироватьЗапросДляПерерасчета(ДокументСсылка, ПериодРегистрацииДокумента, Сотрудники = Неопределено, ПоЗаписямПерерасчета = Ложь)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("ДокументСсылка",		ДокументСсылка);
	Запрос.УстановитьПараметр("РегламентВалюта",	Константы.ВалютаРегламентированногоУчета.Получить());
	Запрос.УстановитьПараметр("ДатаВалютногоУчета",	ПериодРегистрацииДокумента);
	Запрос.УстановитьПараметр("ГоловнаяОрганизация",ОбщегоНазначения.ГоловнаяОрганизация(Организация));
	Запрос.УстановитьПараметр("Сотрудники",			Сотрудники);
	Запрос.УстановитьПараметр("ПоВсемСотрудникам",	Ложь);
	
	Если ЗначениеЗаполнено(Сотрудники) Тогда // этих сотрудников попросил исправить пользователь
		Запрос.Текст =
		"ВЫБРАТЬ
		|	СотрудникиОрганизаций.Ссылка КАК Ссылка
		|ПОМЕСТИТЬ ВТСотрудники
		|ИЗ
		|	Справочник.СотрудникиОрганизаций КАК СотрудникиОрганизаций
		|ГДЕ
		|	СотрудникиОрганизаций.Ссылка В(&Сотрудники)
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Ссылка";	
	ИначеЕсли ПоЗаписямПерерасчета Тогда //  список физлиц для перерасчета получим из таблицы перерасчетов
		Запрос.Текст =
		"ВЫБРАТЬ
		|	СотрудникиОрганизаций.Сотрудник КАК Ссылка
		|ПОМЕСТИТЬ ВТСотрудники
		|ИЗ
		|	Документ.ОплатаПраздничныхИВыходныхДнейОрганизаций.Начисления КАК СотрудникиОрганизаций
		|ГДЕ
		|	СотрудникиОрганизаций.Ссылка = &ДокументСсылка
		|	И СотрудникиОрганизаций.Физлицо В
		|			(ВЫБРАТЬ
		|				Перерасчет.ФизЛицо
		|			ИЗ
		|				РегистрРасчета.ОсновныеНачисленияРаботниковОрганизаций.ПерерасчетОсновныхНачислений КАК Перерасчет
		|			ГДЕ
		|				Перерасчет.ОбъектПерерасчета = &ДокументСсылка)
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Ссылка";
	Иначе 
		Запрос.Текст =
		"ВЫБРАТЬ
		|	НЕОПРЕДЕЛЕНО КАК Ссылка
		|ПОМЕСТИТЬ ВТСотрудники";
		Запрос.УстановитьПараметр("ПоВсемСотрудникам",	Истина);
	КонецЕсли;
	Запрос.Выполнить();
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Начисления.НомерСтроки,
	|	Начисления.Сотрудник КАК Сотрудник,
	|	Начисления.Сотрудник.Физлицо КАК Физлицо,
	|	Начисления.ПодразделениеОрганизации,
	|	Начисления.ДатаВыхода,
	|	НАЧАЛОПЕРИОДА(Начисления.ДатаВыхода, МЕСЯЦ) КАК ТекущийМесяц,
	|	Начисления.ВидРасчета,
	|	Начисления.Размер,
	|	Начисления.ОтработаноЧасов,
	|	Начисления.Результат
	|ПОМЕСТИТЬ ВТНачисления
	|ИЗ
	|	Документ.ОплатаПраздничныхИВыходныхДнейОрганизаций.Начисления КАК Начисления
	|ГДЕ
	|	Начисления.Ссылка = &ДокументСсылка
	|	И (НЕ Начисления.Сторно)
	|	И (&ПоВсемСотрудникам
	|			ИЛИ Начисления.Сотрудник В
	|				(ВЫБРАТЬ
	|					Сотрудники.Ссылка
	|				ИЗ
	|					ВТСотрудники КАК Сотрудники))
	|	И Начисления.Ссылка.Проведен
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Сотрудник";
	Запрос.Выполнить();
	
	// НормаВремениПоСводнымИндивидуальнымГрафикам
	//		Таблица строк документа и нормы времени в часах, введенной сводными индивидуальными графиками
	// 
	//	Поля:
	//		НомерСтроки,
	//		ЧасовЗаМесяц - суммируем все часы рабочего времени
	// 
	// Описание:
	//
	//	выбираем данные из рег-ра СводныеИндивидуальныеГрафики, соответствующие дате из строки документа	
		
		
	Запрос.Текст =
	"ВЫБРАТЬ
	|	СписокРаботников.НомерСтроки КАК НомерСтроки,
	|	СводныеИндивидуальныеГрафики.Часов КАК ЧасовЗаМесяц
	|ПОМЕСТИТЬ ВТНормаВремениПоСводнымИндивидуальнымГрафикам
	|ИЗ
	|	ВТНачисления КАК СписокРаботников
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СводныеИндивидуальныеГрафики КАК СводныеИндивидуальныеГрафики
	|		ПО СписокРаботников.Сотрудник = СводныеИндивидуальныеГрафики.Сотрудник
	|			И СписокРаботников.ТекущийМесяц = СводныеИндивидуальныеГрафики.Месяц
	|ГДЕ
	|	СводныеИндивидуальныеГрафики.Сотрудник ЕСТЬ НЕ NULL 
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	НомерСтроки";
	Запрос.Выполнить();
	
	// НормаВремениПоЕжедневнымИндивидуальнымГрафикам
	//		Таблица строк документа и нормы времени в часах, введенной индивидуальными графиками
	// 
	//	Поля:
	//		НомерСтроки,
	//		ЧасовЗаМесяц - суммируем все часы рабочего времени
	// 
	// Описание:
	//
	//	выбираем данные из рег-ра ГрафикиРаботыПоВидамВремени,	
	//	введенные доками ВводИндивидуальныхГрафиков, т.е. по строкам с выставленным флажком План
		
	Запрос.Текст =
	"ВЫБРАТЬ
	|	СписокРаботников.НомерСтроки КАК НомерСтроки,
	|	СУММА(ГрафикиРаботыПоВидамВремени.ДополнительноеЗначение) КАК ЧасовЗаМесяц
	|ПОМЕСТИТЬ ВТНормаВремениПоЕжедневнымИндивидуальнымГрафикам
	|ИЗ
	|	ВТНачисления КАК СписокРаботников
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ГрафикиРаботыПоВидамВремени КАК ГрафикиРаботыПоВидамВремени
	|		ПО СписокРаботников.Сотрудник = ГрафикиРаботыПоВидамВремени.ГрафикРаботы
	|			И СписокРаботников.ТекущийМесяц = ГрафикиРаботыПоВидамВремени.Месяц
	|			И (ГрафикиРаботыПоВидамВремени.План)
	|			И (ГрафикиРаботыПоВидамВремени.ВидУчетаВремени = ЗНАЧЕНИЕ(Перечисление.ВидыУчетаВремени.ПоДням))
	|ГДЕ
	|	ГрафикиРаботыПоВидамВремени.ГрафикРаботы ЕСТЬ НЕ NULL 
	|
	|СГРУППИРОВАТЬ ПО
	|	СписокРаботников.НомерСтроки
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	НомерСтроки";
	Запрос.Выполнить();
	
	// РаботникиИНачисления
	//		Таблица строк документа с добавленными сведениями о работниках и их начислениях
	// 
	//	Поля:
	//		НомерСтроки,
	//		Сотрудник 
	//		ДатаВыхода 
	//		ОкладТариф - размер основного начисления по состоянию на дату ДатаВыхода 
	//		СпособРасчета - способ расчета основного начисления по состоянию на дату ДатаВыхода 
	//		НовоеПодразделениеОрганизации - подразделение сотрудника по состоянию на дату ДатаВыхода 
	//		ГрафикРаботы - график сотрудника по состоянию на дату ДатаВыхода 
	//		ВидГрафика,
	//		ДлительностьРабочейНедели - реквизиты графика сотрудника по состоянию на дату ДатаВыхода 
	// 
	// Описание:
	//
	//	1. к каждой строке документа приписываем данные из "псевдосрезов" последних по регистрам	
	//	ПлановыеНачисленияРаботниковОрганизаций и РаботникиОрганизаций
	//  2. для определения размер основного начисления по состоянию на дату ДатаВыхода дополнительно
	//  пользуемся "псевдосрезом" последних по регистру РазмерТарифныхСтавок и курсами валют
	//
		
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Начисления.НомерСтроки КАК НомерСтроки,
	|	Начисления.Сотрудник,
	|	Начисления.ДатаВыхода,
	|	Начисления.ТекущийМесяц,
	|	ВЫБОР
	|		КОГДА ОсновноеНачисление.ПериодЗавершения <= Начисления.ДатаВыхода
	|				И ОсновноеНачисление.ПериодЗавершения <> ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|			ТОГДА ВЫБОР
	|					КОГДА ОсновноеНачисление.ВидРасчетаЗавершения.ТребуетВводаТарифногоРазряда
	|						ТОГДА РазмерТарифныхСтавок.Размер
	|					ИНАЧЕ ОсновноеНачисление.Показатель1Завершения
	|				КОНЕЦ
	|		ИНАЧЕ ВЫБОР
	|				КОГДА ОсновноеНачисление.ВидРасчета.ТребуетВводаТарифногоРазряда
	|					ТОГДА РазмерТарифныхСтавок.Размер
	|				ИНАЧЕ ОсновноеНачисление.Показатель1
	|			КОНЕЦ
	|	КОНЕЦ * ВЫБОР
	|		КОГДА ВЫБОР
	|				КОГДА ОсновноеНачисление.ПериодЗавершения <= Начисления.ДатаВыхода
	|						И ОсновноеНачисление.ПериодЗавершения <> ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|					ТОГДА ВЫБОР
	|							КОГДА ОсновноеНачисление.ВидРасчетаЗавершения.ТребуетВводаТарифногоРазряда
	|								ТОГДА РазмерТарифныхСтавок.Валюта
	|							ИНАЧЕ ОсновноеНачисление.Валюта1Завершения
	|						КОНЕЦ
	|				ИНАЧЕ ВЫБОР
	|						КОГДА ОсновноеНачисление.ВидРасчета.ТребуетВводаТарифногоРазряда
	|							ТОГДА РазмерТарифныхСтавок.Валюта
	|						ИНАЧЕ ОсновноеНачисление.Валюта1
	|					КОНЕЦ
	|			КОНЕЦ В (&РегламентВалюта, ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка))
	|			ТОГДА 1
	|		ИНАЧЕ ЕСТЬNULL(Валюты.Курс / Валюты.Кратность, 0)
	|	КОНЕЦ КАК ОкладТариф,
	|	ВЫБОР
	|		КОГДА ОсновноеНачисление.ПериодЗавершения <= Начисления.ДатаВыхода
	|				И ОсновноеНачисление.ПериодЗавершения <> ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|			ТОГДА ОсновноеНачисление.ВидРасчетаЗавершения.СпособРасчета
	|		ИНАЧЕ ОсновноеНачисление.ВидРасчета.СпособРасчета
	|	КОНЕЦ КАК СпособРасчета,
	|	ВЫБОР
	|		КОГДА РаботникиОрганизации.ПериодЗавершения <= Начисления.ДатаВыхода
	|				И РаботникиОрганизации.ПериодЗавершения <> ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|			ТОГДА РаботникиОрганизации.ПодразделениеОрганизацииЗавершения
	|		ИНАЧЕ РаботникиОрганизации.ПодразделениеОрганизации
	|	КОНЕЦ КАК НовоеПодразделениеОрганизации,
	|	ВЫБОР
	|		КОГДА РаботникиОрганизации.ПериодЗавершения <= Начисления.ДатаВыхода
	|				И РаботникиОрганизации.ПериодЗавершения <> ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|			ТОГДА ВЫБОР
	|					КОГДА РаботникиОрганизации.ГрафикРаботыЗавершения.СокращенноеРабочееВремя
	|							И РаботникиОрганизации.ГрафикРаботыЗавершения.ГрафикПолногоРабочегоВремени <> ЗНАЧЕНИЕ(Справочник.ГрафикиРаботы.ПустаяСсылка)
	|						ТОГДА РаботникиОрганизации.ГрафикРаботыЗавершения.ГрафикПолногоРабочегоВремени
	|					ИНАЧЕ РаботникиОрганизации.ГрафикРаботыЗавершения
	|				КОНЕЦ
	|		КОГДА РаботникиОрганизации.ГрафикРаботы.СокращенноеРабочееВремя
	|				И РаботникиОрганизации.ГрафикРаботы.ГрафикПолногоРабочегоВремени <> ЗНАЧЕНИЕ(Справочник.ГрафикиРаботы.ПустаяСсылка)
	|			ТОГДА РаботникиОрганизации.ГрафикРаботы.ГрафикПолногоРабочегоВремени
	|		ИНАЧЕ РаботникиОрганизации.ГрафикРаботы
	|	КОНЕЦ КАК ГрафикРаботы,
	|	ВЫБОР
	|		КОГДА РаботникиОрганизации.ПериодЗавершения <= Начисления.ДатаВыхода
	|				И РаботникиОрганизации.ПериодЗавершения <> ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|			ТОГДА ВЫБОР
	|					КОГДА РаботникиОрганизации.ГрафикРаботыЗавершения.СокращенноеРабочееВремя
	|							И РаботникиОрганизации.ГрафикРаботыЗавершения.ГрафикПолногоРабочегоВремени <> ЗНАЧЕНИЕ(Справочник.ГрафикиРаботы.ПустаяСсылка)
	|						ТОГДА РаботникиОрганизации.ГрафикРаботыЗавершения.ГрафикПолногоРабочегоВремени.ДлительностьРабочейНедели
	|					ИНАЧЕ РаботникиОрганизации.ГрафикРаботыЗавершения.ДлительностьРабочейНедели
	|				КОНЕЦ
	|		КОГДА РаботникиОрганизации.ГрафикРаботы.СокращенноеРабочееВремя
	|				И РаботникиОрганизации.ГрафикРаботы.ГрафикПолногоРабочегоВремени <> ЗНАЧЕНИЕ(Справочник.ГрафикиРаботы.ПустаяСсылка)
	|			ТОГДА РаботникиОрганизации.ГрафикРаботы.ГрафикПолногоРабочегоВремени.ДлительностьРабочейНедели
	|		ИНАЧЕ РаботникиОрганизации.ГрафикРаботы.ДлительностьРабочейНедели
	|	КОНЕЦ КАК ДлительностьРабочейНедели,
	|	ВЫБОР
	|		КОГДА РаботникиОрганизации.ПериодЗавершения <= Начисления.ДатаВыхода
	|				И РаботникиОрганизации.ПериодЗавершения <> ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|			ТОГДА РаботникиОрганизации.ГрафикРаботыЗавершения.ВидГрафика
	|		ИНАЧЕ РаботникиОрганизации.ГрафикРаботы.ВидГрафика
	|	КОНЕЦ КАК ВидГрафика
	|ПОМЕСТИТЬ ВТРаботникиИНачисления
	|ИЗ
	|	ВТНачисления КАК Начисления
	|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|			Начисления.НомерСтроки КАК НомерСтроки,
	|			Начисления.Сотрудник КАК Сотрудник,
	|			МАКСИМУМ(РаботникиОрганизации.Период) КАК Период
	|		ИЗ
	|			ВТНачисления КАК Начисления
	|				ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РаботникиОрганизаций КАК РаботникиОрганизации
	|				ПО Начисления.ДатаВыхода >= РаботникиОрганизации.Период
	|					И Начисления.Сотрудник = РаботникиОрганизации.Сотрудник
	|		
	|		СГРУППИРОВАТЬ ПО
	|			Начисления.НомерСтроки,
	|			Начисления.Сотрудник) КАК ДатыПоследнихНазначений
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РаботникиОрганизаций КАК РаботникиОрганизации
	|			ПО ДатыПоследнихНазначений.Период = РаботникиОрганизации.Период
	|				И ДатыПоследнихНазначений.Сотрудник = РаботникиОрганизации.Сотрудник
	|		ПО Начисления.НомерСтроки = ДатыПоследнихНазначений.НомерСтроки
	|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|			Начисления.НомерСтроки КАК НомерСтроки,
	|			Начисления.Сотрудник КАК Сотрудник,
	|			МАКСИМУМ(ПлановыеНачисления.Период) КАК Период
	|		ИЗ
	|			ВТНачисления КАК Начисления
	|				ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПлановыеНачисленияРаботниковОрганизаций КАК ПлановыеНачисления
	|				ПО Начисления.Сотрудник = ПлановыеНачисления.Сотрудник
	|					И Начисления.ДатаВыхода >= ПлановыеНачисления.Период
	|					И (ПлановыеНачисления.ВидРасчетаИзмерение.Ссылка ЕСТЬ NULL )
	|		
	|		СГРУППИРОВАТЬ ПО
	|			Начисления.НомерСтроки,
	|			Начисления.Сотрудник) КАК ДатыПоследнихПлановыхНачислений
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПлановыеНачисленияРаботниковОрганизаций КАК ОсновноеНачисление
	|			ПО ДатыПоследнихПлановыхНачислений.Сотрудник = ОсновноеНачисление.Сотрудник
	|				И ДатыПоследнихПлановыхНачислений.Период = ОсновноеНачисление.Период
	|				И (ОсновноеНачисление.ВидРасчетаИзмерение.Ссылка ЕСТЬ NULL )
	|		ПО Начисления.НомерСтроки = ДатыПоследнихПлановыхНачислений.НомерСтроки
	|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|			Начисления.НомерСтроки КАК НомерСтроки,
	|			РазмерТарифныхСтавок.ТарифныйРазряд КАК ТарифныйРазряд,
	|			МАКСИМУМ(РазмерТарифныхСтавок.Период) КАК ПериодРазряда
	|		ИЗ
	|			ВТНачисления КАК Начисления
	|				ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РазмерТарифныхСтавок КАК РазмерТарифныхСтавок
	|				ПО Начисления.ДатаВыхода >= РазмерТарифныхСтавок.Период
	|		
	|		СГРУППИРОВАТЬ ПО
	|			РазмерТарифныхСтавок.ТарифныйРазряд,
	|			Начисления.НомерСтроки) КАК ДатыТарифныхРазрядов
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РазмерТарифныхСтавок КАК РазмерТарифныхСтавок
	|			ПО ДатыТарифныхРазрядов.ПериодРазряда = РазмерТарифныхСтавок.Период
	|				И ДатыТарифныхРазрядов.ТарифныйРазряд = РазмерТарифныхСтавок.ТарифныйРазряд
	|		ПО Начисления.НомерСтроки = ДатыТарифныхРазрядов.НомерСтроки
	|			И (ВЫБОР
	|				КОГДА ОсновноеНачисление.ПериодЗавершения <= Начисления.ДатаВыхода
	|						И ОсновноеНачисление.ПериодЗавершения <> ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|					ТОГДА ВЫБОР
	|							КОГДА ОсновноеНачисление.ВидРасчетаЗавершения.ТребуетВводаТарифногоРазряда
	|								ТОГДА ОсновноеНачисление.ТарифныйРазряд1
	|							ИНАЧЕ NULL
	|						КОНЕЦ
	|				ИНАЧЕ ВЫБОР
	|						КОГДА ОсновноеНачисление.ВидРасчета.ТребуетВводаТарифногоРазряда
	|							ТОГДА ОсновноеНачисление.ТарифныйРазряд1
	|						ИНАЧЕ NULL
	|					КОНЕЦ
	|			КОНЕЦ = ДатыТарифныхРазрядов.ТарифныйРазряд)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалютДляРасчетовСПерсоналом КАК Валюты
	|		ПО (Валюты.Валюта = ВЫБОР
	|				КОГДА ОсновноеНачисление.ПериодЗавершения <= Начисления.ДатаВыхода
	|						И ОсновноеНачисление.ПериодЗавершения <> ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|					ТОГДА ВЫБОР
	|							КОГДА ОсновноеНачисление.ВидРасчетаЗавершения.ТребуетВводаТарифногоРазряда
	|								ТОГДА РазмерТарифныхСтавок.Валюта
	|							ИНАЧЕ ОсновноеНачисление.Валюта1Завершения
	|						КОНЕЦ
	|				ИНАЧЕ ВЫБОР
	|						КОГДА ОсновноеНачисление.ВидРасчета.ТребуетВводаТарифногоРазряда
	|							ТОГДА РазмерТарифныхСтавок.Валюта
	|						ИНАЧЕ ОсновноеНачисление.Валюта1
	|					КОНЕЦ
	|			КОНЕЦ)
	|			И (Валюты.Период = &ДатаВалютногоУчета)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	НомерСтроки";
	Запрос.Выполнить();
	
	// НормаВремениПоОбщимГрафикам
	//		Таблица строк документа и нормы времени в часах по общим графикам
	// 
	//	Поля:
	//		Сотрудник,
	//		ЧасовЗаМесяц - суммируем все часы рабочего времени
	// 
	// Описание:
	//
	//	выбираем данные из рег-ра ГрафикиРаботыПоВидамВремени для общих графиков
		
	Запрос.Текст =
	"ВЫБРАТЬ
	|	СписокРаботников.НомерСтроки КАК НомерСтроки,
	|	СУММА(ГрафикиРаботыПоВидамВремени.ДополнительноеЗначение) КАК ЧасовЗаМесяц
	|ПОМЕСТИТЬ ВТНормаВремениПоОбщимГрафикам
	|ИЗ
	|	ВТРаботникиИНачисления КАК СписокРаботников
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ГрафикиРаботыПоВидамВремени КАК ГрафикиРаботыПоВидамВремени
	|		ПО СписокРаботников.ГрафикРаботы = ГрафикиРаботыПоВидамВремени.ГрафикРаботы
	|			И СписокРаботников.ТекущийМесяц = ГрафикиРаботыПоВидамВремени.Месяц
	|			И (ГрафикиРаботыПоВидамВремени.План)
	|			И (ГрафикиРаботыПоВидамВремени.ВидУчетаВремени = ЗНАЧЕНИЕ(Перечисление.ВидыУчетаВремени.ПоДням))
	|ГДЕ
	|	ГрафикиРаботыПоВидамВремени.ГрафикРаботы ЕСТЬ НЕ NULL 
	|
	|СГРУППИРОВАТЬ ПО
	|	СписокРаботников.НомерСтроки
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	НомерСтроки";
	Запрос.Выполнить();
	
	// ДанныеПроизводственногоКалендаряЗаМесяц
	//		Данные производственного календаря за месяц
	// 
	//	Поля:
	//		ЧислоРабочихДней,
	//		ЧислоПредпраздничныхДней
	// 
	// Описание:
	//
	//	выбираем данные из рег-ра РегламентированныйПроизводственныйКалендарь за месяц, в который сотрудник работал
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	МЕСЯЦ(РегламентированныйПроизводственныйКалендарь.ДатаКалендаря) КАК МесяцКалендаря,
	|	СУММА(ВЫБОР
	|			КОГДА РегламентированныйПроизводственныйКалендарь.ВидДня = ЗНАЧЕНИЕ(Перечисление.ВидыДнейПроизводственногоКалендаря.Рабочий)
	|				ТОГДА 1
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ЧислоРабочихДней,
	|	СУММА(ВЫБОР
	|			КОГДА РегламентированныйПроизводственныйКалендарь.ВидДня = ЗНАЧЕНИЕ(Перечисление.ВидыДнейПроизводственногоКалендаря.Предпраздничный)
	|				ТОГДА 1
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ЧислоПредпраздничныхДней
	|ПОМЕСТИТЬ ВТДанныеПроизводственногоКалендаряЗаМесяц
	|ИЗ
	|	(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		ГОД(ОплатаПраздничных.ДатаВыхода) КАК ГодВыхода
	|	ИЗ
	|		ВТНачисления КАК ОплатаПраздничных) КАК ГодаОплаты
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РегламентированныйПроизводственныйКалендарь КАК РегламентированныйПроизводственныйКалендарь
	|		ПО (ГодаОплаты.ГодВыхода = ГОД(РегламентированныйПроизводственныйКалендарь.ДатаКалендаря))
	|
	|СГРУППИРОВАТЬ ПО
	|	МЕСЯЦ(РегламентированныйПроизводственныйКалендарь.ДатаКалендаря)";
	Запрос.Выполнить();
	
	// основной запрос - по собранным данным рассчитываем часовую тарифную ставку в зависимости 
	// от порядка расчета основного начисления и от используемого пользователями алгоритма пересчета оклада: 
	// либо по общероссийскому производственному календарю (с использованием 
	// среднемесячного количества часов и длительности рабочей недели графика), либо по графику работника
	// данные по рабочим и предпраздничным дням выбираются из РегистрСведений.РегламентированныйПроизводственныйКалендарь	
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Начисления.НомерСтроки,
	|	Начисления.Сотрудник,
	|	Начисления.Физлицо,
	|	Начисления.ВидРасчета,
	|	Начисления.ДатаВыхода,
	|	Начисления.Размер,
	|	Начисления.Результат,
	|	Начисления.ПодразделениеОрганизации,
	|	Начисления.ОтработаноЧасов,
	|	РаботникиИНачисления.НовоеПодразделениеОрганизации,
	|	ВЫБОР
	|		КОГДА РаботникиИНачисления.СпособРасчета В (ЗНАЧЕНИЕ(Перечисление.СпособыРасчетаОплатыТруда.ПоЧасовойТарифнойСтавке), ЗНАЧЕНИЕ(Перечисление.СпособыРасчетаОплатыТруда.СдельныйЗаработок))
	|			ТОГДА РаботникиИНачисления.ОкладТариф
	|		КОГДА РаботникиИНачисления.СпособРасчета В (ЗНАЧЕНИЕ(Перечисление.СпособыРасчетаОплатыТруда.ПоМесячнойТарифнойСтавкеПоЧасам), ЗНАЧЕНИЕ(Перечисление.СпособыРасчетаОплатыТруда.ПоМесячнойТарифнойСтавкеПоДням))
	|			ТОГДА ВЫРАЗИТЬ(ВЫБОР
	|						КОГДА ЕСТЬNULL(ВариантыВстроенныхАлгоритмовРасчетаЗарплаты.ПорядокПересчетаМесячнойСтавкиВЧасовую, ЗНАЧЕНИЕ(Перечисление.ПорядкиПересчетаМесячнойСтавкиВЧасовую.ПоСреднемуКоличествуЧасовВМесяце)) = ЗНАЧЕНИЕ(Перечисление.ПорядкиПересчетаМесячнойСтавкиВЧасовую.ПоСреднемуКоличествуЧасовВМесяце)
	|							ТОГДА ВЫБОР
	|									КОГДА ЕСТЬNULL(ДанныеПроизводственногоКалендаря.ЧислоРабочихДней, 0) = 0
	|										ТОГДА 0
	|									КОГДА РаботникиИНачисления.ДлительностьРабочейНедели = 0
	|										ТОГДА 0
	|									ИНАЧЕ РаботникиИНачисления.ОкладТариф / (РаботникиИНачисления.ДлительностьРабочейНедели / 5 * (ЕСТЬNULL(ДанныеПроизводственногоКалендаря.ЧислоРабочихДней, 0) + ЕСТЬNULL(ДанныеПроизводственногоКалендаря.ЧислоПредпраздничныхДней, 0)) - ЕСТЬNULL(ДанныеПроизводственногоКалендаря.ЧислоПредпраздничныхДней, 0)) * 12
	|								КОНЕЦ
	|						КОГДА ЕСТЬNULL(ВариантыВстроенныхАлгоритмовРасчетаЗарплаты.ПорядокПересчетаМесячнойСтавкиВЧасовую, ЗНАЧЕНИЕ(Перечисление.ПорядкиПересчетаМесячнойСтавкиВЧасовую.ПоСреднемуКоличествуЧасовВМесяце)) = ЗНАЧЕНИЕ(Перечисление.ПорядкиПересчетаМесячнойСтавкиВЧасовую.ПоМесячнойНормеПоПроизводственномуКалендарю)
	|							ТОГДА ВЫБОР
	|									КОГДА ЕСТЬNULL(ДанныеПроизводственногоКалендаряЗаМесяц.ЧислоРабочихДней, 0) = 0
	|										ТОГДА 0
	|									КОГДА РаботникиИНачисления.ДлительностьРабочейНедели = 0
	|										ТОГДА 0
	|									ИНАЧЕ РаботникиИНачисления.ОкладТариф / (РаботникиИНачисления.ДлительностьРабочейНедели / 5 * (ЕСТЬNULL(ДанныеПроизводственногоКалендаряЗаМесяц.ЧислоРабочихДней, 0) + ЕСТЬNULL(ДанныеПроизводственногоКалендаряЗаМесяц.ЧислоПредпраздничныхДней, 0)) - ЕСТЬNULL(ДанныеПроизводственногоКалендаряЗаМесяц.ЧислоПредпраздничныхДней, 0))
	|								КОНЕЦ
	|						КОГДА ВЫБОР
	|								КОГДА НормаВремениПоСводнымИндивидуальнымГрафикам.НомерСтроки ЕСТЬ НЕ NULL 
	|									ТОГДА НормаВремениПоСводнымИндивидуальнымГрафикам.ЧасовЗаМесяц
	|								КОГДА НормаВремениПоЕжедневнымИндивидуальнымГрафикам.НомерСтроки ЕСТЬ НЕ NULL 
	|									ТОГДА НормаВремениПоЕжедневнымИндивидуальнымГрафикам.ЧасовЗаМесяц
	|								ИНАЧЕ ЕСТЬNULL(НормаВремениПоОбщимГрафикам.ЧасовЗаМесяц, 0)
	|							КОНЕЦ = 0
	|							ТОГДА 0
	|						ИНАЧЕ РаботникиИНачисления.ОкладТариф / ВЫБОР
	|								КОГДА НормаВремениПоСводнымИндивидуальнымГрафикам.НомерСтроки ЕСТЬ НЕ NULL 
	|									ТОГДА НормаВремениПоСводнымИндивидуальнымГрафикам.ЧасовЗаМесяц
	|								КОГДА НормаВремениПоЕжедневнымИндивидуальнымГрафикам.НомерСтроки ЕСТЬ НЕ NULL 
	|									ТОГДА НормаВремениПоЕжедневнымИндивидуальнымГрафикам.ЧасовЗаМесяц
	|								ИНАЧЕ ЕСТЬNULL(НормаВремениПоОбщимГрафикам.ЧасовЗаМесяц, 0)
	|							КОНЕЦ
	|					КОНЕЦ КАК ЧИСЛО(15, 3))
	|		КОГДА РаботникиИНачисления.СпособРасчета = ЗНАЧЕНИЕ(Перечисление.СпособыРасчетаОплатыТруда.ПоДневнойТарифнойСтавке)
	|			ТОГДА ВЫРАЗИТЬ(ВЫБОР
	|						КОГДА РаботникиИНачисления.ДлительностьРабочейНедели = 0
	|							ТОГДА 0
	|						ИНАЧЕ РаботникиИНачисления.ОкладТариф / РаботникиИНачисления.ДлительностьРабочейНедели * ВЫБОР
	|								КОГДА РаботникиИНачисления.ВидГрафика = ЗНАЧЕНИЕ(Перечисление.ВидыРабочихГрафиков.Шестидневка)
	|									ТОГДА 6
	|								ИНАЧЕ 5
	|							КОНЕЦ
	|					КОНЕЦ КАК ЧИСЛО(15, 3))
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК НоваяТарифнаяСтавка
	|ИЗ
	|	ВТНачисления КАК Начисления
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТРаботникиИНачисления КАК РаботникиИНачисления
	|		ПО Начисления.НомерСтроки = РаботникиИНачисления.НомерСтроки
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТНормаВремениПоСводнымИндивидуальнымГрафикам КАК НормаВремениПоСводнымИндивидуальнымГрафикам
	|		ПО Начисления.НомерСтроки = НормаВремениПоСводнымИндивидуальнымГрафикам.НомерСтроки
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТНормаВремениПоЕжедневнымИндивидуальнымГрафикам КАК НормаВремениПоЕжедневнымИндивидуальнымГрафикам
	|		ПО Начисления.НомерСтроки = НормаВремениПоЕжедневнымИндивидуальнымГрафикам.НомерСтроки
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТНормаВремениПоОбщимГрафикам КАК НормаВремениПоОбщимГрафикам
	|		ПО Начисления.НомерСтроки = НормаВремениПоОбщимГрафикам.НомерСтроки
	|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|			ГодаОплаты.ГодВыхода КАК ГодВыхода,
	|			СУММА(ВЫБОР
	|					КОГДА РегламентированныйПроизводственныйКалендарь.ВидДня = ЗНАЧЕНИЕ(Перечисление.ВидыДнейПроизводственногоКалендаря.Рабочий)
	|						ТОГДА 1
	|					ИНАЧЕ 0
	|				КОНЕЦ) КАК ЧислоРабочихДней,
	|			СУММА(ВЫБОР
	|					КОГДА РегламентированныйПроизводственныйКалендарь.ВидДня = ЗНАЧЕНИЕ(Перечисление.ВидыДнейПроизводственногоКалендаря.Предпраздничный)
	|						ТОГДА 1
	|					ИНАЧЕ 0
	|				КОНЕЦ) КАК ЧислоПредпраздничныхДней
	|		ИЗ
	|			(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|				ГОД(ОплатаПраздничных.ДатаВыхода) КАК ГодВыхода
	|			ИЗ
	|				ВТНачисления КАК ОплатаПраздничных) КАК ГодаОплаты
	|				ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РегламентированныйПроизводственныйКалендарь КАК РегламентированныйПроизводственныйКалендарь
	|				ПО (ГодаОплаты.ГодВыхода = ГОД(РегламентированныйПроизводственныйКалендарь.ДатаКалендаря))
	|		
	|		СГРУППИРОВАТЬ ПО
	|			ГодаОплаты.ГодВыхода) КАК ДанныеПроизводственногоКалендаря
	|		ПО (ГОД(Начисления.ДатаВыхода) = ДанныеПроизводственногоКалендаря.ГодВыхода)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТДанныеПроизводственногоКалендаряЗаМесяц КАК ДанныеПроизводственногоКалендаряЗаМесяц
	|		ПО (МЕСЯЦ(Начисления.ДатаВыхода) = ДанныеПроизводственногоКалендаряЗаМесяц.МесяцКалендаря)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ВариантыВстроенныхАлгоритмовРасчетаЗарплаты КАК ВариантыВстроенныхАлгоритмовРасчетаЗарплаты
	|		ПО (ИСТИНА)";
	
	Возврат Запрос.Выполнить();
	
КонецФункции  // СформироватьЗапросДляПерерасчета()

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ДЛЯ ОБЕСПЕЧЕНИЯ ПРОВЕДЕНИЯ ДОКУМЕНТА

// Формирует запрос по шапке документа
//
// Параметры: 
//	Режим - режим проведения
//
// Возвращаемое значение:
//	Результат запроса
//
Функция СформироватьЗапросПоШапке()

	Запрос = Новый Запрос;

	// Установим параметры запроса
	Запрос.УстановитьПараметр("ДокументСсылка",			Ссылка);
	Запрос.УстановитьПараметр("парамПустаяОрганизация",	Справочники.Организации.ПустаяСсылка());

	Запрос.Текст =
	"ВЫБРАТЬ
	|	ОплатаПраздничныхИВыходныхДнейОрганизаций.Дата,
	|	ОплатаПраздничныхИВыходныхДнейОрганизаций.Организация,
	|	ВЫБОР
	|		КОГДА ОплатаПраздничныхИВыходныхДнейОрганизаций.Организация.ГоловнаяОрганизация = &парамПустаяОрганизация
	|			ТОГДА ОплатаПраздничныхИВыходныхДнейОрганизаций.Организация
	|		ИНАЧЕ ОплатаПраздничныхИВыходныхДнейОрганизаций.Организация.ГоловнаяОрганизация
	|	КОНЕЦ КАК ГоловнаяОрганизация,
	|	ОплатаПраздничныхИВыходныхДнейОрганизаций.Организация КАК ОбособленноеПодразделение,
	|	ОплатаПраздничныхИВыходныхДнейОрганизаций.ПерерассчитываемыйДокумент КАК ПерерассчитываемыйДокумент,
	|	ОплатаПраздничныхИВыходныхДнейОрганизаций.ПерерассчитываемыйДокумент.ПериодРегистрации КАК ПериодПерерасчета,
	|	ОплатаПраздничныхИВыходныхДнейОрганизаций.ПерерассчитываемыйДокумент.Организация КАК ОрганизацияПерерасчета,
	|	ОплатаПраздничныхИВыходныхДнейОрганизаций.ПериодРегистрации,
	|	ОплатаПраздничныхИВыходныхДнейОрганизаций.Ответственный,
	|	ОплатаПраздничныхИВыходныхДнейОрганизаций.Ссылка
	|ИЗ
	|	Документ.ОплатаПраздничныхИВыходныхДнейОрганизаций КАК ОплатаПраздничныхИВыходныхДнейОрганизаций
	|ГДЕ
	|	ОплатаПраздничныхИВыходныхДнейОрганизаций.Ссылка = &ДокументСсылка";

	Возврат Запрос.Выполнить();

КонецФункции // СформироватьЗапросПоШапке()

// Формирует запрос по таблице "Начисления" документа
//
// Параметры:
//	Режим		- режим проведения.
//
// Возвращаемое значение:
//	Результат запроса.
//
Функция СформироватьЗапросПоНачисления(ВыборкаПоШапкеДокумента)


	Запрос = Новый Запрос;
	
	ГоловнаяОрганизация = ВыборкаПоШапкеДокумента.ГоловнаяОрганизация;
	СписокСтруктурныхПодразделений = ОбщегоНазначения.ПолучитьСписокОбособленныхПодразделенийОрганизации(ГоловнаяОрганизация);
	СписокСтруктурныхПодразделений.Добавить(ГоловнаяОрганизация);
	Запрос.УстановитьПараметр("СписокСтруктурныхПодразделений",	СписокСтруктурныхПодразделений);
		
	// Установим параметры запроса
	Запрос.УстановитьПараметр("ДокументСсылка",					Ссылка);
	Запрос.УстановитьПараметр("Организация",					Организация);
	Запрос.УстановитьПараметр("ПустаяДата",						'00010101');
	Запрос.УстановитьПараметр("ГоловнаяОрганизация",			ГоловнаяОрганизация);

	// Описание текста запроса:
	// 1. Выборка "ТЧНачисления":
	//		Выбираются строки документа.
	// 2. Выборка "ПересекающиесяСтроки":
	//		Среди остальных строк документа ищем строки, имеющие пересекающийся период действия
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ТЧНачисления.НомерСтроки КАК НомерСтроки,
	|	ТЧНачисления.Сотрудник,
	|	ТЧНачисления.Сотрудник.Наименование,
	|	ТЧНачисления.Сотрудник.Физлицо КАК ФизЛицо,
	|	ТЧНачисления.ПодразделениеОрганизации,
	|	ТЧНачисления.ДатаВыхода КАК ДатаВыхода,
	|	ЕСТЬNULL(ТЧНачисления.ВидРасчета.ЗачетОтработанногоВремени, 0) КАК ЗачетОтработанногоВремени,
	|	ТЧНачисления.ОтработаноЧасов КАК ОтработаноЧасов,
	|	ТЧНачисления.ВидРасчета КАК ВидРасчета,
	|	ТЧНачисления.ВидРасчета.КодДоходаНДФЛ КАК КодДоходаНДФЛ,
	|	ВЫБОР
	|		КОГДА ТЧНачисления.ВидРасчета.КодДоходаНДФЛ.УчитыватьПоПериодуДействия
	|			ТОГДА ТЧНачисления.ДатаВыхода
	|		ИНАЧЕ ТЧНачисления.Ссылка.ПериодРегистрации
	|	КОНЕЦ КАК МесяцНалоговогоПериода,
	|	ТЧНачисления.Размер,
	|	ТЧНачисления.Результат,
	|	ТЧНачисления.Сторно,
	|	ВЫБОР
	|		КОГДА ТЧНачисления.Сторно
	|			ТОГДА ТЧНачисления.Ссылка.ПерерассчитываемыйДокумент
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК СторнируемыйДокумент,
	|	ВЫБОР
	|		КОГДА ТЧНачисления.Сотрудник.Организация = &ГоловнаяОрганизация
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК ОшибкаНеСоответствиеСотрудникаИОрганизации,
	|	ВЫБОР
	|		КОГДА ГрафикРаботыПоСотруднику.НомерСтроки ЕСТЬ NULL 
	|			ТОГДА ВЫБОР
	|					КОГДА ДанныеПоРаботникуДоНазначения.ПериодЗавершения <= ТЧНачисления.ДатаВыхода
	|							И ДанныеПоРаботникуДоНазначения.ПериодЗавершения <> &ПустаяДата
	|						ТОГДА ДанныеПоРаботникуДоНазначения.ГрафикРаботыЗавершения
	|					ИНАЧЕ ДанныеПоРаботникуДоНазначения.ГрафикРаботы
	|				КОНЕЦ
	|		ИНАЧЕ ТЧНачисления.Сотрудник
	|	КОНЕЦ КАК ГрафикРаботы,
	|	ВЫБОР
	|		КОГДА ДанныеПоРаботникуДоНазначения.ПериодЗавершения <= ТЧНачисления.ДатаВыхода
	|				И ДанныеПоРаботникуДоНазначения.ПериодЗавершения <> &ПустаяДата
	|			ТОГДА ДанныеПоРаботникуДоНазначения.ЗанимаемыхСтавокЗавершения
	|		ИНАЧЕ ДанныеПоРаботникуДоНазначения.ЗанимаемыхСтавок
	|	КОНЕЦ КАК ЗанимаемыхСтавок,
	|	ПересекающиесяСтроки.КонфликтнаяСтрокаНомер,
	|	ВЫБОР
	|		КОГДА (НЕ ТЧНачисления.ПодразделениеОрганизации.Владелец В (&СписокСтруктурныхПодразделений))
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ОшибкаПодразделениеНеПринадлежитОрганизации,
	|	ДатыПоследнихДвиженийРаботников.Период КАК ДатаПоследнегоДвиженияПоРаботнику
	|ИЗ
	|	Документ.ОплатаПраздничныхИВыходныхДнейОрганизаций.Начисления КАК ТЧНачисления
	|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|			ТЧНачисления.НомерСтроки КАК НомерСтроки,
	|			МАКСИМУМ(РаботникиОрганизации.Период) КАК Период
	|		ИЗ
	|			Документ.ОплатаПраздничныхИВыходныхДнейОрганизаций.Начисления КАК ТЧНачисления
	|				ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.РаботникиОрганизаций КАК РаботникиОрганизации
	|				ПО (РаботникиОрганизации.Период <= ТЧНачисления.ДатаВыхода)
	|					И ТЧНачисления.Сотрудник = РаботникиОрганизации.Сотрудник
	|		ГДЕ
	|			ТЧНачисления.Ссылка = &ДокументСсылка
	|		
	|		СГРУППИРОВАТЬ ПО
	|			ТЧНачисления.НомерСтроки) КАК ДатыПоследнихДвиженийРаботников
	|		ПО ТЧНачисления.НомерСтроки = ДатыПоследнихДвиженийРаботников.НомерСтроки
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РаботникиОрганизаций КАК ДанныеПоРаботникуДоНазначения
	|		ПО (ДанныеПоРаботникуДоНазначения.Период = ДатыПоследнихДвиженийРаботников.Период)
	|			И ТЧНачисления.Сотрудник = ДанныеПоРаботникуДоНазначения.Сотрудник
	|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|			ТЧНачисления.НомерСтроки КАК НомерСтроки,
	|			МИНИМУМ(ТЧНачисления2.НомерСтроки) КАК КонфликтнаяСтрокаНомер
	|		ИЗ
	|			Документ.ОплатаПраздничныхИВыходныхДнейОрганизаций.Начисления КАК ТЧНачисления
	|				ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ОплатаПраздничныхИВыходныхДнейОрганизаций.Начисления КАК ТЧНачисления2
	|				ПО (ТЧНачисления2.Ссылка = &ДокументСсылка)
	|					И ТЧНачисления.ВидРасчета = ТЧНачисления2.ВидРасчета
	|					И ТЧНачисления.НомерСтроки > ТЧНачисления2.НомерСтроки
	|					И ТЧНачисления.ДатаВыхода = ТЧНачисления2.ДатаВыхода
	|					И ТЧНачисления.Сотрудник = ТЧНачисления2.Сотрудник
	|		ГДЕ
	|			ТЧНачисления.Ссылка = &ДокументСсылка
	|			И (НЕ ТЧНачисления.Сторно)
	|			И (НЕ ТЧНачисления2.Сторно)
	|		
	|		СГРУППИРОВАТЬ ПО
	|			ТЧНачисления.НомерСтроки) КАК ПересекающиесяСтроки
	|		ПО ТЧНачисления.НомерСтроки = ПересекающиесяСтроки.НомерСтроки
	|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ РАЗЛИЧНЫЕ
	|			СтрокиНачисления.НомерСтроки КАК НомерСтроки
	|		ИЗ
	|			Документ.ОплатаПраздничныхИВыходныхДнейОрганизаций.Начисления КАК СтрокиНачисления
	|				ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ГрафикиРаботыПоВидамВремени КАК ГрафикиРаботыПоВидамВремени
	|				ПО СтрокиНачисления.Сотрудник = ГрафикиРаботыПоВидамВремени.ГрафикРаботы
	|					И (ГрафикиРаботыПоВидамВремени.Месяц = НАЧАЛОПЕРИОДА(СтрокиНачисления.ДатаВыхода, МЕСЯЦ))
	|		ГДЕ
	|			СтрокиНачисления.Ссылка = &ДокументСсылка) КАК ГрафикРаботыПоСотруднику
	|		ПО ТЧНачисления.НомерСтроки = ГрафикРаботыПоСотруднику.НомерСтроки
	|ГДЕ
	|	ТЧНачисления.Ссылка = &ДокументСсылка
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки";

	Запрос.Текст = ТекстЗапроса;

	Возврат Запрос.Выполнить();

КонецФункции // СформироватьЗапросПоНачисления()

// Проверяет правильность заполнения шапки документа.
// Если какой-то из реквизитов шапки, влияющий на проведение не заполнен или
// заполнен не корректно, то выставляется флаг отказа в проведении.
// Проверка выполняется по выборке из результата запроса по шапке,
// все проверяемые реквизиты должны быть включены в выборку по шапке.
//
// Параметры: 
//	ВыборкаПоШапкеДокумента	- выборка из результата запроса по шапке документа,
//	Отказ					- флаг отказа в проведении,
//	Заголовок				- Заголовок для сообщений об ошибках проведения.
//
Процедура ПроверитьЗаполнениеШапки(ВыборкаПоШапкеДокумента, Отказ, Заголовок)

	Если НЕ ЗначениеЗаполнено(ВыборкаПоШапкеДокумента.Организация) Тогда
		ОбщегоНазначения.ВывестиИнформациюОбОшибке(ОбщегоНазначения.ПреобразоватьСтрокуИнтерфейса("Не указана организация!"), Отказ, Заголовок);
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ВыборкаПоШапкеДокумента.ПериодРегистрации) Тогда
		ОбщегоНазначения.ВывестиИнформациюОбОшибке("Не указан период регистрации (месяц)!", Отказ, Заголовок);
	КонецЕсли;
	
	// соответствие периодов документа и перерассчитываемого документа
	Если ВыборкаПоШапкеДокумента.ПериодПерерасчета <> null 
		и ВыборкаПоШапкеДокумента.ПериодРегистрации <= ВыборкаПоШапкеДокумента.ПериодПерерасчета Тогда
		ОбщегоНазначения.ВывестиИнформациюОбОшибке("Период документа должен быть больше периода перерассчитываемого документа!", Отказ, Заголовок);
	КонецЕсли;
	
	// соответствие организаций документа и перерассчитываемого документа
	Если ВыборкаПоШапкеДокумента.ОрганизацияПерерасчета <> null 
		и ВыборкаПоШапкеДокумента.ОбособленноеПодразделение <> ВыборкаПоШапкеДокумента.ОрганизацияПерерасчета Тогда
		ОбщегоНазначения.ВывестиИнформациюОбОшибке(ОбщегоНазначения.ПреобразоватьСтрокуИнтерфейса("Организация, заданная для документа, должна совпадать с организацией перерассчитываемого документа!"), Отказ, Заголовок);
	КонецЕсли;
	
КонецПроцедуры // ПроверитьЗаполнениеШапки()

// Проверяет правильность заполнения реквизитов в строке ТЧ "Начисления" документа.
// Если какой-то из реквизитов, влияющий на проведение не заполнен или
// заполнен не корректно, то выставляется флаг отказа в проведении.
// Проверка выполняется по выборке из результата запроса по строке ТЧ документа,
// все проверяемые реквизиты должны быть включены в выборку.
//
// Параметры: 
//	ВыборкаПоШапкеДокумента		- выборка из результата запроса по шапке документа,
//	ВыборкаПоСтрокамДокумента	- спозиционированная на определеной строке выборка 
//								  из результата запроса по товарам документа, 
//	Отказ 						- флаг отказа в проведении,
//	Заголовок					- Заголовок для сообщений об ошибках проведения.
//
Процедура ПроверитьЗаполнениеСтрокиНачислений(ВыборкаПоШапкеДокумента, ВыборкаПоСтрокамДокумента, Отказ, Заголовок)
	
	СтрокаНачалаСообщенияОбОшибке = "В строке номер """+ СокрЛП(ВыборкаПоСтрокамДокумента.НомерСтроки) +
	""" табл. части ""Начисления"": ";

	// Сотрудник
	НетСотрудника = НЕ ЗначениеЗаполнено(ВыборкаПоСтрокамДокумента.Сотрудник);
	Если НетСотрудника Тогда
		ОбщегоНазначения.ВывестиИнформациюОбОшибке(СтрокаНачалаСообщенияОбОшибке + "не выбран сотрудник!", Отказ, Заголовок);
	КонецЕсли;
	
	// Подразделение
	Если НЕ ЗначениеЗаполнено(ВыборкаПоСтрокамДокумента.ПодразделениеОрганизации) Тогда
		ОбщегоНазначения.ВывестиИнформациюОбОшибке(СтрокаНачалаСообщенияОбОшибке + ОбщегоНазначения.ПреобразоватьСтрокуИнтерфейса("не указано подразделение организации!"), Отказ, Заголовок);
		
	ИначеЕсли ВыборкаПоСтрокамДокумента.ОшибкаПодразделениеНеПринадлежитОрганизации Тогда
		ОбщегоНазначения.ВывестиИнформациюОбОшибке(СтрокаНачалаСообщенияОбОшибке + ОбщегоНазначения.ПреобразоватьСтрокуИнтерфейса("указано подразделение, принадлежащее другой организации!"), Отказ, Заголовок);
		
	КонецЕсли;
	
	// ДатаВыхода
	НетДатаВыхода = НЕ ЗначениеЗаполнено(ВыборкаПоСтрокамДокумента.ДатаВыхода);
	Если НетДатаВыхода Тогда
		ОбщегоНазначения.ВывестиИнформациюОбОшибке(СтрокаНачалаСообщенияОбОшибке + "не указана дата выхода на работу!", Отказ, Заголовок);
	КонецЕсли;
	
	// ВидРасчета
	Если НЕ ЗначениеЗаполнено(ВыборкаПоСтрокамДокумента.ВидРасчета) Тогда
		ОбщегоНазначения.ВывестиИнформациюОбОшибке(СтрокаНачалаСообщенияОбОшибке + "не указан вид оплаты!", Отказ, Заголовок);
	КонецЕсли;
	
	Если НетСотрудника ИЛИ НетДатаВыхода Тогда
		Возврат; // Дальше не проверяем
	КонецЕсли;
	
	// Организация сотрудника должна совпадать с организацией документа
	Если ВыборкаПоСтрокамДокумента.ОшибкаНеСоответствиеСотрудникаИОрганизации Тогда
		ОбщегоНазначения.ВывестиИнформациюОбОшибке(СтрокаНачалаСообщенияОбОшибке + ОбщегоНазначения.ПреобразоватьСтрокуИнтерфейса(" указанный сотрудник оформлен на другую организацию!"), Отказ, Заголовок);
	КонецЕсли;
	
	// Проверка: ранее работник должен быть принят на работу
	Если ВыборкаПоСтрокамДокумента.ЗанимаемыхСтавок = NULL Тогда
		СтрокаПродолжениеСообщенияОбОшибке = "на " + Формат(ВыборкаПоСтрокамДокумента.ДатаВыхода, "ДЛФ=DD") + " сотрудник " + ВыборкаПоСтрокамДокумента.СотрудникНаименование + " еще не принят на работу!";
		ОбщегоНазначения.ВывестиИнформациюОбОшибке(СтрокаНачалаСообщенияОбОшибке + СтрокаПродолжениеСообщенияОбОшибке, Отказ, Заголовок);
		
	ИначеЕсли ВыборкаПоСтрокамДокумента.ЗанимаемыхСтавок = 0 Тогда
		СтрокаПродолжениеСообщенияОбОшибке = "на " + Формат(ВыборкаПоСтрокамДокумента.ДатаВыхода, "ДЛФ=DD") + " сотрудник " + ВыборкаПоСтрокамДокумента.СотрудникНаименование + " уже уволен (с " + Формат(ВыборкаПоСтрокамДокумента.ДатаПоследнегоДвиженияПоРаботнику, "ДЛФ=DD") + ")!";
		ОбщегоНазначения.ВывестиИнформациюОбОшибке(СтрокаНачалаСообщенияОбОшибке + СтрокаПродолжениеСообщенияОбОшибке, Отказ, Заголовок);
		
	КонецЕсли;
	
	// График работы
	Если Не ЗначениеЗаполнено(ВыборкаПоСтрокамДокумента.ГрафикРаботы) Тогда
		ОбщегоНазначения.ВывестиИнформациюОбОшибке(СтрокаНачалаСообщенияОбОшибке + "не указан рабочий график!", Отказ, Заголовок);
	КонецЕсли;
	
	// Проверка: противоречие другой строке документа
	Если ВыборкаПоСтрокамДокумента.КонфликтнаяСтрокаНомер <> NULL Тогда
		СтрокаСообщениеОбОшибке = "указана повторяющаяся строка (см. строку  № " + ВыборкаПоСтрокамДокумента.КонфликтнаяСтрокаНомер + ")!"; 
		ОбщегоНазначения.ВывестиИнформациюОбОшибке(СтрокаНачалаСообщенияОбОшибке + СтрокаСообщениеОбОшибке, Отказ, Заголовок);
	КонецЕсли;

КонецПроцедуры // ПроверитьЗаполнениеСтрокиНачислений()

// По строке выборок из результатов запроса по документу формируем движения по регистру
//
// Параметры: 
//	ВыборкаПоСтрокамДокумента	- спозиционированная на определеной строке выборка 
//								  из результата запроса к ТЧ документа, 
// Возвращаемое значение:
//	Нет.
//
Процедура ДобавитьСтрокуОсновныхНачислений(ВыборкаПоШапкеДокумента, ВыборкаПоСтрокамДокумента)
	
	Движение = Движения.ОсновныеНачисленияРаботниковОрганизаций.Добавить();
	
	// Свойства
	Движение.ПериодРегистрации			= ВыборкаПоШапкеДокумента.ПериодРегистрации;
	Движение.ПериодДействияНачало		= ВыборкаПоСтрокамДокумента.ДатаВыхода;
	Движение.ПериодДействияКонец		= КонецДня(ВыборкаПоСтрокамДокумента.ДатаВыхода);
	Движение.ВидРасчета					= ВыборкаПоСтрокамДокумента.ВидРасчета;
	Движение.Сторно						= ВыборкаПоСтрокамДокумента.Сторно;
	
	// Измерения
	Движение.Сотрудник					= ВыборкаПоСтрокамДокумента.Сотрудник;
	Движение.ФизЛицо					= ВыборкаПоСтрокамДокумента.ФизЛицо;
	Движение.Организация				= ВыборкаПоШапкеДокумента.ГоловнаяОрганизация;
	
	// Ресурсы
	Движение.Результат					= ВыборкаПоСтрокамДокумента.Результат;
	Движение.ОплаченоДнейЧасов		= ВыборкаПоСтрокамДокумента.ОтработаноЧасов;
	
	// ресурсы по отработанному времени
	Если ВыборкаПоСтрокамДокумента.ЗачетОтработанногоВремени Тогда
		Движение.ОтработаноЧасов		= ВыборкаПоСтрокамДокумента.ОтработаноЧасов;
		Движение.ОтработаноДней			= ?(ВыборкаПоСтрокамДокумента.ОтработаноЧасов < 0,-1,1);
	КонецЕсли;
	
	// Реквизиты
	Движение.Показатель1				= ВыборкаПоСтрокамДокумента.Размер;
	Движение.ПодразделениеОрганизации	= ВыборкаПоСтрокамДокумента.ПодразделениеОрганизации;
	Движение.ОбособленноеПодразделение	= ВыборкаПоШапкеДокумента.ОбособленноеПодразделение;
	Движение.ГрафикРаботы				= ВыборкаПоСтрокамДокумента.ГрафикРаботы;
	Движение.ВидУчетаВремени			= Перечисления.ВидыУчетаВремени.ПоЧасам;
	Движение.ДатаНачалаСобытия			= ВыборкаПоСтрокамДокумента.ДатаВыхода;
	Движение.СторнируемыйДокумент		= ВыборкаПоСтрокамДокумента.СторнируемыйДокумент;	
	
КонецПроцедуры // ДобавитьСтрокуОсновныхНачислений()

// По строке выборки результата запроса по документу формируем движения по регистрам
//
// Параметры:
//	ВыборкаПоШапкеДокумента						- выборка из результата запроса по шапке документа
//
// Возвращаемое значение:
//	Нет.
//
Процедура ДобавитьСтрокуВДвиженияПоРегистрамНакопления(ВыборкаПоШапкеДокумента, ВыборкаПоТЧ)
	
	Если ЗначениеЗаполнено(ВыборкаПоТЧ.КодДоходаНДФЛ) Тогда
		
		Движение = Движения.НДФЛСведенияОДоходах.Добавить();
		
		ЗаполнитьЗначенияСвойств(Движение,ВыборкаПоШапкеДокумента,"ОбособленноеПодразделение");
		ЗаполнитьЗначенияСвойств(Движение,ВыборкаПоТЧ); // ФизЛицо,  ПодразделениеОрганизации, ВидРасчета
		
		// Свойства
		Движение.Период						= ВыборкаПоТЧ.МесяцНалоговогоПериода;
		
		// Измерения
		Движение.Организация				= ВыборкаПоШапкеДокумента.ГоловнаяОрганизация;
		Движение.КодДохода					= ВыборкаПоТЧ.КодДоходаНДФЛ;
		Движение.ПериодРегистрации			= НачалоМесяца(ПериодРегистрации);
		
		// Ресурсы
		Движение.СуммаДохода				= ВыборкаПоТЧ.Результат; 
		
		// Реквизиты
		Движение.ИсчисленоИзЗарплаты		= Истина;

	КонецЕсли;
	
	Движение = Движения.ВзаиморасчетыСРаботникамиОрганизаций.Добавить();
	
	// Свойства
	Движение.Период					= КонецМесяца(ПериодРегистрации);
	Движение.ВидДвижения			= ВидДвиженияНакопления.Приход;
	
	// Измерения
	Движение.Организация			= ВыборкаПоШапкеДокумента.Организация;
	Движение.ФизЛицо				= ВыборкаПоТЧ.ФизЛицо;
	Движение.ПериодВзаиморасчетов	= ПериодРегистрации;
	
	// Ресурсы
	Движение.СуммаВзаиморасчетов	= ВыборкаПоТЧ.Результат;
	
	
КонецПроцедуры // ДобавитьСтрокуВДвиженияПоРегистрамНакопления()

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ

Процедура ОбработкаПроведения(Отказ, Режим)

	ОбработкаКомментариев = глЗначениеПеременной("глОбработкаСообщений");
	ОбработкаКомментариев.УдалитьСообщения();
	
	// Заголовок для сообщений об ошибках проведения.
	Заголовок = ОбщегоНазначения.ПредставлениеДокументаПриПроведении(Ссылка);
		
	РезультатЗапросаПоШапке = СформироватьЗапросПоШапке();

	// Получим реквизиты шапки из запроса
	ВыборкаПоШапкеДокумента = РезультатЗапросаПоШапке.Выбрать();

	Если ВыборкаПоШапкеДокумента.Следующий() Тогда

		//Надо позвать проверку заполнения реквизитов шапки
		ПроверитьЗаполнениеШапки(ВыборкаПоШапкеДокумента, Отказ, Заголовок);

		// Движения стоит добавлять, если в проведении еще не отказано (отказ =ложь)
		Если НЕ Отказ Тогда

			Движения.ОсновныеНачисленияРаботниковОрганизаций.мВыполнятьВспомогательныеРасчеты = Истина;
			
			// получим реквизиты табличной части
			РезультатЗапросаПоНачислениям = СформироватьЗапросПоНачисления(ВыборкаПоШапкеДокумента);
			ВыборкаПоНачислениям = РезультатЗапросаПоНачислениям.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);

			Пока ВыборкаПоНачислениям.Следующий() Цикл 

				// проверим очередную строку табличной части
				ПроверитьЗаполнениеСтрокиНачислений(ВыборкаПоШапкеДокумента, ВыборкаПоНачислениям, Отказ, Заголовок);

				Если НЕ Отказ Тогда

					// Заполним записи в наборах записей регистров
					ДобавитьСтрокуОсновныхНачислений(ВыборкаПоШапкеДокумента, ВыборкаПоНачислениям);
					ДобавитьСтрокуВДвиженияПоРегистрамНакопления(ВыборкаПоШапкеДокумента, ВыборкаПоНачислениям);
				КонецЕсли;

			КонецЦикла;

			// выполним удаление перерасчетов исправленного документа
			Если Не Отказ И ЗначениеЗаполнено(ПерерассчитываемыйДокумент) Тогда
				ПроведениеРасчетов.УдалитьСведенияОПерерасчетеДокумента(Ссылка, Истина); // Только по исправленным документам
			КонецЕсли;

		КонецЕсли;

	КонецЕсли;

	ОбработкаКомментариев.ПоказатьСообщения();
	
КонецПроцедуры // ОбработкаПроведения()

Процедура ОбработкаУдаленияПроведения(Отказ)
    Движения.ОсновныеНачисленияРаботниковОрганизаций.мВыполнятьВспомогательныеРасчеты = Истина;
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	МассивТЧ = Новый Массив();
	МассивТЧ.Добавить(Начисления);
	
	КраткийСоставДокумента = ПроцедурыУправленияПерсоналом.ЗаполнитьКраткийСоставДокумента(МассивТЧ);
	
КонецПроцедуры // ПередЗаписью()

////////////////////////////////////////////////////////////////////////////////
// ОПЕРАТОРЫ ОСНОВНОЙ ПРОГРАММЫ

мПроизводныеДвижения = Новый Массив;
мПроизводныеДвижения.Добавить(Движения.ЗарплатаЗаМесяцОрганизаций);