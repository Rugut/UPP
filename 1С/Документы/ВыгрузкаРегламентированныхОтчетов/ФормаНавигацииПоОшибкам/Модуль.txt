Перем ВладелецТС Экспорт;

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

// Процедура формирует необходимый набор параметров и вызывает экспортную
// процедуру формы регламентированного отчета, соответствующего переданной в качестве
// параметра строке ВыбраннаяСтрока. Вызываемая экспортная процедура активизирует
// описываемую ячейку одного из табличных документов, расположенных на форме.
//
// Параметры:
//	ВыбраннаяСтрока - строка табличного поля ТаблицаСообщений формы, ячейку, 
//						соответствующую которой, следует активизировать.
//
Процедура АктивизироватьЯчейку(ВыбраннаяСтрока)
	
	Если ВыбраннаяСтрока = Неопределено Тогда
		Возврат;
	КонецЕсли;
		
	ТекДок = ВыбраннаяСтрока.ОтчетДок;
	Если ТекДок = Неопределено Тогда
		Возврат;
	КонецЕсли;
	ТекДок.ПолучитьФорму().Открыть();
	
	Ячейка = Новый Структура("Раздел, Страница, Строка, Графа, СтрокаПП, ИмяЯчейки, Описание",
				ВыбраннаяСтрока.Раздел, ВыбраннаяСтрока.Страница, ВыбраннаяСтрока.Строка, ВыбраннаяСтрока.Графа, ВыбраннаяСтрока.СтрокаПП, ВыбраннаяСтрока.ИмяЯчейки, ВыбраннаяСтрока.Описание);
	
	Попытка
		РегламентированнаяОтчетность.ФормаРеглОтчета(ТекДок.ИсточникОтчета, ТекДок.ВыбраннаяФорма, , ТекДок.Ссылка).АктивизироватьЯчейку(Ячейка);
	Исключение
	КонецПопытки;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

// Процедура - обработчик события ПриОткрытии формы.
//
Процедура ПриОткрытии()
	
	Для Каждого Стр Из ВладелецТС Цикл
		ДокументВладелец = ТаблицаСообщений.Строки.Найти(Стр.ОтчетДок, "ОтчетДок");
		Если ДокументВладелец = Неопределено Тогда
			ДокументВладелец = ТаблицаСообщений.Строки.Добавить();
			ДокументВладелец.Отчет = Стр.Отчет;
			ДокументВладелец.ОтчетДок = Стр.ОтчетДок;
			ЭлементыФормы.ТаблицаСообщений.Развернуть(ДокументВладелец);
		КонецЕсли;
		НовСтр = ДокументВладелец.Строки.Добавить();
		ЗаполнитьЗначенияСвойств(НовСтр, Стр);
	КонецЦикла;
	НадписьВсегоОшибок = "Всего ошибок: " + Формат(ВладелецТС.Количество(), "ЧГ=")
						+ " в " + Формат(ТаблицаСообщений.Строки.Количество(), "ЧГ=")
						+ ?(ТаблицаСообщений.Строки.Количество() = 1, " отчете", " отчетах");
						
	Для Каждого Стр Из ТаблицаСообщений.Строки Цикл
		Если Стр.Строки.Количество() <> 0 Тогда
			ЭлементыФормы.ТаблицаСообщений.ТекущаяСтрока = Стр.Строки[0];
			Прервать;
		КонецЕсли;
	КонецЦикла;					
	
КонецПроцедуры

// Процедура - обработчик события ПриЗакрытии формы.
//
Процедура ПриЗакрытии()
	
	ТаблицаСообщений.Строки.Очистить();
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ФОРМЫ

// Процедура - обработчик события ПриНажатии кнопки КнопкаВперед.
//
Процедура КнопкаНазадНажатие(Элемент)
	
	ТекЭл = ЭлементыФормы.ТаблицаСообщений.ТекущаяСтрока;
	
	Если ЭлементыФормы.ТаблицаСообщений.ТекущаяСтрока.Уровень() = 1 Тогда
		ТекИнд = ТекЭл.Родитель.Строки.Индекс(ТекЭл);
		Если ТекИнд <> 0 Тогда
			ЭлементыФормы.ТаблицаСообщений.ТекущаяСтрока = ТекЭл.Родитель.Строки.Получить(ТекИнд - 1);
			АктивизироватьЯчейку(ЭлементыФормы.ТаблицаСообщений.ТекущиеДанные);
			Возврат;
		КонецЕсли;
		Эл1Уровня = ТекЭл.Родитель;
	Иначе
		Эл1Уровня = ТекЭл;
	КонецЕсли;
	
	ТекИнд = ТаблицаСообщений.Строки.Индекс(Эл1Уровня) - 1;
	Для Сч = 0 По ТекИнд Цикл
		ТИ = ТекИнд - Сч;
		Если ТаблицаСообщений.Строки.Получить(ТИ).Строки.Количество() = 0 Тогда
			Продолжить;
		КонецЕсли;
		ЭлементыФормы.ТаблицаСообщений.ТекущаяСтрока = ТаблицаСообщений.Строки.Получить(ТИ).Строки.Получить(ТаблицаСообщений.Строки.Получить(ТИ).Строки.Количество() - 1);
	КонецЦикла;
	
	АктивизироватьЯчейку(ЭлементыФормы.ТаблицаСообщений.ТекущиеДанные);
	
КонецПроцедуры

// Процедура - обработчик события ПриНажатии кнопки КнопкаНазад.
//
Процедура КнопкаВпередНажатие(Элемент)
	
	ТекЭл = ЭлементыФормы.ТаблицаСообщений.ТекущаяСтрока;
	
	Если ЭлементыФормы.ТаблицаСообщений.ТекущиеДанные.Уровень() = 1 Тогда
		ТекИнд = ТекЭл.Родитель.Строки.Индекс(ТекЭл);
		Если ТекИнд <> ТекЭл.Родитель.Строки.Количество() - 1 Тогда
			ЭлементыФормы.ТаблицаСообщений.ТекущаяСтрока = ТекЭл.Родитель.Строки.Получить(ТекИнд + 1);
			АктивизироватьЯчейку(ЭлементыФормы.ТаблицаСообщений.ТекущиеДанные);
			Возврат;
		КонецЕсли;
		Эл1Уровня = ТекЭл.Родитель;
		ТекИнд = ТаблицаСообщений.Строки.Индекс(Эл1Уровня) + 1;
	Иначе
		Эл1Уровня = ТекЭл;
		ТекИнд = ТаблицаСообщений.Строки.Индекс(Эл1Уровня);
	КонецЕсли;
	
	Для Сч = ТекИнд По ТаблицаСообщений.Строки.Количество() - 1 Цикл
		Если ТаблицаСообщений.Строки.Получить(ТекИнд).Строки.Количество() <> 0 Тогда
			ЭлементыФормы.ТаблицаСообщений.ТекущаяСтрока = ТаблицаСообщений.Строки.Получить(ТекИнд).Строки.Получить(0);
		КонецЕсли;
	КонецЦикла;
		
	АктивизироватьЯчейку(ЭлементыФормы.ТаблицаСообщений.ТекущиеДанные);
	
КонецПроцедуры

// Процедура - обработчик события Выбор табличного поля ТаблицаСообщений.
//
Процедура ТаблицаСообщенийВыбор(Элемент, ВыбраннаяСтрока, Колонка, СтандартнаяОбработка)
	
	Если ВыбраннаяСтрока.Уровень() <> 0 Тогда
		АктивизироватьЯчейку(ВыбраннаяСтрока);
	КонецЕсли;
	
КонецПроцедуры


Процедура ТаблицаСообщенийПриПолученииДанных(Элемент, ОформленияСтрок)
	
	Для Каждого ОформлениеСтроки Из ОформленияСтрок Цикл
		Если ОформлениеСтроки.ДанныеСтроки.Уровень() = 0 Тогда
			ОформлениеСтроки.Ячейки.Описание.УстановитьТекст("Ошибок в отчете: " + ОформлениеСтроки.ДанныеСтроки.Строки.Количество());
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры