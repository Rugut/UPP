Перем мУдалятьДвижения;

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОКУМЕНТА

#Если Клиент Тогда
// Процедура осуществляет печать документа. Можно направить печать на 
// экран или принтер, а также распечатать необходимое количество копий.
//
//  Название макета печати передается в качестве параметра,
// по переданному названию находим имя макета в соответствии.
//
// Параметры:
//  НазваниеМакета - строка, название макета.
//
Процедура Печать(ИмяМакета, КоличествоЭкземпляров = 1, НаПринтер = Ложь) Экспорт

	Если ЭтоНовый() Тогда
		Предупреждение("Документ можно распечатать только после его записи");
		Возврат;
	ИначеЕсли Не УправлениеДопПравамиПользователей.РазрешитьПечатьНепроведенныхДокументов(Проведен) Тогда
		Предупреждение("Недостаточно полномочий для печати непроведенного документа!");
		Возврат;
	КонецЕсли;

	Если Не РаботаСДиалогами.ПроверитьМодифицированность(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;

	Если ТипЗнч(ИмяМакета) = Тип("ДвоичныеДанные") Тогда

		ТабДокумент = УниверсальныеМеханизмы.НапечататьВнешнююФорму(Ссылка, ИмяМакета);
		
		Если ТабДокумент = Неопределено Тогда
			Возврат
		КонецЕсли; 
		
	КонецЕсли;

	УниверсальныеМеханизмы.НапечататьДокумент(ТабДокумент, КоличествоЭкземпляров, НаПринтер, ОбщегоНазначения.СформироватьЗаголовокДокумента(ЭтотОбъект), Ссылка);

КонецПроцедуры // Печать

// Возвращает доступные варианты печати документа
//
// Возвращаемое значение:
//  Структура, каждая строка которой соответствует одному из вариантов печати
//  
Функция ПолучитьСтруктуруПечатныхФорм() Экспорт
	
	Возврат Новый Структура;

КонецФункции // ПолучитьСтруктуруПечатныхФорм()

#КонецЕсли

Процедура ОбработкаПроведения(Отказ)
	
	Если мУдалятьДвижения Тогда
		ОбщегоНазначения.УдалитьДвиженияРегистратора(ЭтотОбъект, Отказ);
	КонецЕсли;

	Для Каждого ТекСтрокаОсновныеСредства Из ОсновныеСредства Цикл
		СрезСведенийМежд = РегистрыСведений.ОсновныеСредстваМеждународныйУчет.ПолучитьПоследнее((Дата-1), Новый Структура("ОсновноеСредство", ТекСтрокаОсновныеСредства.ОсновноеСредство));

		// регистр ОсновныеСредстваМеждународныйУчет 
		Движение = Движения.ОсновныеСредстваМеждународныйУчет.Добавить();
		Движение.Период = Дата;
		Движение.ОсновноеСредство = ТекСтрокаОсновныеСредства.ОсновноеСредство;
		Движение.Организация = Организация;
		Движение.ДатаПринятияКУчету = СрезСведенийМежд.ДатаПринятияКУчету;
		Движение.МестонахождениеОбъекта = ТекСтрокаОсновныеСредства.МестонахождениеОбъекта;
		Движение.МОЛ = ТекСтрокаОсновныеСредства.МОЛ;
		Движение.СчетУчета = ТекСтрокаОсновныеСредства.СчетУчета;
		Движение.СчетСниженияСтоимости = ТекСтрокаОсновныеСредства.СчетСниженияСтоимости;
		Движение.СрокПолезногоИспользования = ТекСтрокаОсновныеСредства.СрокПолезногоИспользования;
		Движение.НачислятьАмортизацию = ТекСтрокаОсновныеСредства.НачислятьАмортизацию;
		Движение.МетодНачисленияАмортизации = ТекСтрокаОсновныеСредства.МетодНачисленияАмортизации;
		Движение.СчетНачисленияАмортизации = ТекСтрокаОсновныеСредства.СчетНачисленияАмортизации;
		Движение.ПервоначальнаяСтоимость = СрезСведенийМежд.ПервоначальнаяСтоимость;
		Движение.ЛиквидационнаяСтоимость = ТекСтрокаОсновныеСредства.ЛиквидационнаяСтоимость;
		Движение.СчетЗатрат = ТекСтрокаОсновныеСредства.СчетЗатрат;
		Движение.Субконто1 = ТекСтрокаОсновныеСредства.Субконто1;
		Движение.Субконто2 = ТекСтрокаОсновныеСредства.Субконто2;
		Движение.Субконто3 = ТекСтрокаОсновныеСредства.Субконто3;
		Движение.ПредполагаемыйОбъемПродукции = ТекСтрокаОсновныеСредства.ПредполагаемыйОбъемПродукции;
		Движение.СуммаНачисленнойАмортизации = ТекСтрокаОсновныеСредства.СуммаНачисленнойАмортизации;
		Движение.СправедливаяСтоимость = СрезСведенийМежд.СправедливаяСтоимость;
		Движение.Состояние = СрезСведенийМежд.Состояние;
		Движение.КоэффициентУскорения = ТекСтрокаОсновныеСредства.КоэффициентУскорения;
		
		// регистр Международный
		Если НЕ ЗначениеЗаполнено(ТекСтрокаОсновныеСредства.СчетНачисленияАмортизации) Тогда
			ОписаниеОС = "Для объекта ОС '"+Строка(ТекСтрокаОсновныеСредства.ОсновноеСредство)+"' в строке "+ТекСтрокаОсновныеСредства.НомерСтроки;
			Сообщить(ОписаниеОС+" не указан счет начисления амортизации! Документ не проведен!", СтатусСообщения.ОченьВажное);
			Отказ = Истина;
			Возврат;
		КонецЕсли;
		Если НЕ ЗначениеЗаполнено(СрезСведенийМежд.СчетНачисленияАмортизации) Тогда
			ОписаниеОС = "Для объекта ОС '"+Строка(ТекСтрокаОсновныеСредства.ОсновноеСредство)+"' в строке "+ТекСтрокаОсновныеСредства.НомерСтроки;
			ОбщегоНазначения.СообщитьОбОшибке("Ошибка! "+ОписаниеОС+" не определен исходный счет начисления амортизации. Возможно этого объекта нет на балансе.", Отказ);
			Возврат;
		КонецЕсли;

		Движение = Движения.Международный.Добавить();
		Движение.Период = Дата;
		Движение.Организация = Организация;
		Движение.Сумма = ТекСтрокаОсновныеСредства.СуммаНачисленнойАмортизации;
		Движение.НомерЖурнала = "Рег";
		
		Если ТекСтрокаОсновныеСредства.НаправлениеПереводаОСиИС = Перечисления.НаправлениеПереводаОСиИС.ИзИнвестиционнойСобственностиВОсновныеСредства Тогда
			Движение.СчетДт = ТекСтрокаОсновныеСредства.СчетНачисленияАмортизации;
			Движение.СчетКт = СрезСведенийМежд.СчетНачисленияАмортизации;
			Движение.Содержание = "Перевод инвестиционной собственности в основные средства";
			
		ИначеЕсли ТекСтрокаОсновныеСредства.НаправлениеПереводаОСиИС = Перечисления.НаправлениеПереводаОСиИС.ИзОсновныхСредствВИнвестиционнуюСобственность Тогда
			Движение.СчетКт = ТекСтрокаОсновныеСредства.СчетНачисленияАмортизации;
			Движение.СчетДт = СрезСведенийМежд.СчетНачисленияАмортизации;
			Движение.Содержание = "Перевод основных средств в инвестиционную собственность";
			
		Иначе
			Сообщить("Не указано направление перевода! Документ не проведен!", СтатусСообщения.ОченьВажное);
			Отказ = Истина;
			Возврат;
		КонецЕсли;

		Если не Движение.СчетДт.ВидыСубконто.Найти(ПланыВидовХарактеристик.ВидыСубконтоМеждународные.ОсновныеСредства,) = Неопределено Тогда
			Движение.СубконтоДт[ПланыВидовХарактеристик.ВидыСубконтоМеждународные.ОсновныеСредства] = ТекСтрокаОсновныеСредства.ОсновноеСредство;
		КонецЕсли;
		Если не Движение.СчетКт.ВидыСубконто.Найти(ПланыВидовХарактеристик.ВидыСубконтоМеждународные.ОсновныеСредства,) = Неопределено Тогда
			Движение.СубконтоКт[ПланыВидовХарактеристик.ВидыСубконтоМеждународные.ОсновныеСредства] = ТекСтрокаОсновныеСредства.ОсновноеСредство;
		КонецЕсли;
	КонецЦикла;
	
	// записываем движения регистров
	Движения.ОсновныеСредстваМеждународныйУчет.Записать();
	Движения.Международный.Записать();
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	Если ОбменДанными.Загрузка  Тогда
		Возврат;
	КонецЕсли;


	 
	мУдалятьДвижения = НЕ ЭтоНовый();
	
КонецПроцедуры // ПередЗаписью