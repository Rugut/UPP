////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ РЕКВИЗИТОВ ФОРМЫ

// Процедура вызывается при нажатии кнопки "Разместить"
//
Процедура ОсновныеДействияФормыРазместить(Кнопка)

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Номенклатура", ТаблицаНераспределенныхКомплектов.ВыгрузитьКолонку("Номенклатура"));
	Запрос.УстановитьПараметр("ХарактеристикаНоменклатуры", ТаблицаНераспределенныхКомплектов.ВыгрузитьКолонку("ХарактеристикаНоменклатуры"));
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	КомплектующиеНоменклатуры.Номенклатура                  КАК Номенклатура,
	|	КомплектующиеНоменклатуры.ХарактеристикаНоменклатуры    КАК ХарактеристикаНоменклатуры,
	|	КомплектующиеНоменклатуры.Комплектующая                 КАК Комплектующая,
	|	КомплектующиеНоменклатуры.ХарактеристикаКомплектующей   КАК ХарактеристикаКомплектующей,
	|	СУММА(КомплектующиеНоменклатуры.Количество)             КАК Количество,
	|	КомплектующиеНоменклатуры.ЕдиницаИзмерения              КАК ЕдиницаИзмерения
	|ИЗ
	|	РегистрСведений.КомплектующиеНоменклатуры КАК КомплектующиеНоменклатуры
	|ГДЕ
	|	КомплектующиеНоменклатуры.Номенклатура В(&Номенклатура) И
	|	КомплектующиеНоменклатуры.ХарактеристикаНоменклатуры В(&ХарактеристикаНоменклатуры)
	|СГРУППИРОВАТЬ ПО
	|	КомплектующиеНоменклатуры.Номенклатура,
	|	КомплектующиеНоменклатуры.ХарактеристикаНоменклатуры,
	|	КомплектующиеНоменклатуры.Комплектующая,
	|	КомплектующиеНоменклатуры.ХарактеристикаКомплектующей,
	|	КомплектующиеНоменклатуры.ЕдиницаИзмерения
	|";
	Выборка = Запрос.Выполнить().Выбрать();

	ТаблицаКомплектующих = Новый ТаблицаЗначений;
	ТаблицаКомплектующих.Колонки.Добавить("Номенклатура");
	ТаблицаКомплектующих.Колонки.Добавить("ХарактеристикаНоменклатуры");
	ТаблицаКомплектующих.Колонки.Добавить("Количество");
	ТаблицаКомплектующих.Колонки.Добавить("ЕдиницаИзмерения");
	ТаблицаКомплектующих.Колонки.Добавить("Коэффициент");

	Для Каждого СтрокаТаблицы Из ТаблицаНераспределенныхКомплектов Цикл

		Если СтрокаТаблицы.Размещаем > 0 Тогда

			СтруктураПоиска = Новый Структура();
			СтруктураПоиска.Вставить("Номенклатура", СтрокаТаблицы.Номенклатура);
			СтруктураПоиска.Вставить("ХарактеристикаНоменклатуры", СтрокаТаблицы.ХарактеристикаНоменклатуры);

			Выборка.Сбросить();
			Пока Выборка.НайтиСледующий(СтруктураПоиска) Цикл
				НоваяСтрока = ТаблицаКомплектующих.Добавить();
				НоваяСтрока.Номенклатура = Выборка.Комплектующая;
				НоваяСтрока.ХарактеристикаНоменклатуры = Выборка.ХарактеристикаКомплектующей;
				НоваяСтрока.ЕдиницаИзмерения = Выборка.ЕдиницаИзмерения;
				НоваяСтрока.Коэффициент = Выборка.ЕдиницаИзмерения.Коэффициент;
				НоваяСтрока.Количество = Выборка.Количество * СтрокаТаблицы.Размещаем;
			КонецЦикла;
		КонецЕсли;

	КонецЦикла;

	СтруктураВозвращаемыхЗначений = Новый Структура();
	СтруктураВозвращаемыхЗначений.Вставить("Команда", "РазмещениеКомплектующих");
	СтруктураВозвращаемыхЗначений.Вставить("Товары", ТаблицаКомплектующих);
	ОповеститьОВыборе(СтруктураВозвращаемыхЗначений);

КонецПроцедуры // ОсновныеДействияФормыРазместить()

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ РЕКВИЗИТОВ ТАБЛИЦЫ НЕРАСПРЕДЕЛЕННЫХ КОМПЛЕКТОВ

// Процедура - обработчик события "ПриИзменении" реквизита "Размещаем" 
// таблицы нераспределенных комплектов
//
Процедура ТаблицаНераспределенныхКомплектовРазмещаемПриИзменении(Элемент)

	СтрокаТабличнойЧасти = ЭлементыФормы.ТаблицаНераспределенныхКомплектов.ТекущиеДанные;
	Если СтрокаТабличнойЧасти.Размещаем > СтрокаТабличнойЧасти.НеРазмещено Тогда
		Предупреждение("Комплектов для размещения выбрано больше, чем осталось не размещенных!");
	КонецЕсли;

КонецПроцедуры // ТаблицаНераспределенныхКомплектовРазмещаемПриИзменении()

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ДЕЙСТВИЯ КОМАНДНЫХ ПАНЕЛЕЙ ФОРМЫ

// Процедура вызывается при нажатии кнопки "Не размещать ничего"
//
Процедура КоманднаяПанельТаблицыРаспределенияНеРазмещатьНичего(Кнопка)

	Для Каждого Строка Из ТаблицаНераспределенныхКомплектов Цикл
		Строка.Размещаем = 0;
	КонецЦикла;

КонецПроцедуры

// Процедура вызывается при нажатии кнопки "Разместить все"
//
Процедура КоманднаяПанельТаблицыРаспределенияРазместитьВсе(Кнопка)

	Для Каждого Строка Из ТаблицаНераспределенныхКомплектов Цикл
		Строка.Размещаем = Строка.НеРазмещено;
	КонецЦикла;

КонецПроцедуры