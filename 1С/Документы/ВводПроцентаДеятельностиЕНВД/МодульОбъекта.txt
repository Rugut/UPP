Перем мУдалятьДвижения;
////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ДЛЯ ОБЕСПЕЧЕНИЯ ПРОВЕДЕНИЯ ДОКУМЕНТА

// Формирует запрос по шапке документа
//
// Параметры:
//	Режим - режим проведения
//
// Возвращаемое значение:
//	Результат запроса
//
Функция СформироватьЗапросПоШапке(Режим)

	Запрос = Новый Запрос;

	// Установим параметры запроса
	Запрос.УстановитьПараметр("ДокументСсылка" ,		Ссылка);
	Запрос.УстановитьПараметр("парамПустаяОрганизация",	Справочники.Организации.ПустаяСсылка());
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВводПроцентаДеятельностиЕНВД.Дата,
	|	ВводПроцентаДеятельностиЕНВД.Организация,
	|	ВЫБОР
	|		КОГДА ВводПроцентаДеятельностиЕНВД.Организация.ГоловнаяОрганизация = &парамПустаяОрганизация
	|			ТОГДА ВводПроцентаДеятельностиЕНВД.Организация
	|		ИНАЧЕ ВводПроцентаДеятельностиЕНВД.Организация.ГоловнаяОрганизация
	|	КОНЕЦ КАК ГоловнаяОрганизация,
	|	ВводПроцентаДеятельностиЕНВД.Период,
	|	ВводПроцентаДеятельностиЕНВД.Ссылка,
	|	ВводПроцентаДеятельностиЕНВД.ВводДанныхПоПериодам
	|ИЗ
	|	Документ.ВводПроцентаДеятельностиЕНВД КАК ВводПроцентаДеятельностиЕНВД
	|ГДЕ
	|	ВводПроцентаДеятельностиЕНВД.Ссылка = &ДокументСсылка";

	Возврат Запрос.Выполнить();

КонецФункции // СформироватьЗапросПоШапке()

// Формирует запрос по таблице "РаботникиОрганизации" документа
//
// Параметры:
//	Режим - режим проведения
//
// Возвращаемое значение:
//	Результат запроса.
//
Функция СформироватьЗапросПоРаботникиОрганизации(Режим)

	Запрос = Новый Запрос;
	
	// Установим параметры запроса
	Запрос.УстановитьПараметр("ДокументСсылка",			Ссылка);
	Запрос.УстановитьПараметр("Период",					Период);
	Запрос.УстановитьПараметр("ГоловнаяОрганизация",	ОбщегоНазначения.ГоловнаяОрганизация(Организация));
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	СписокРаботников.Сотрудник.Наименование КАК СотрудникНаименование,
	|	СписокРаботников.Сотрудник,
	|	СписокРаботников.ПодпадаетПодЕНВД,
	|	СписокРаботников.СчетДт,
	|	СписокРаботников.Субконто1,
	|	СписокРаботников.Субконто2,
	|	СписокРаботников.Субконто3,
	|	СписокРаботников.СпособРаспределенияЗатрат,
	|	СписокРаботников.НомерСтроки,
	|	ВЫБОР
	|		КОГДА СписокРаботников.Сотрудник.Организация = &ГоловнаяОрганизация
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК ОшибкаНеСоответствиеСотрудникаИОрганизации,
	|	СписокРаботников.КонфликтныйНомерСтроки,
	|	ПРЕДСТАВЛЕНИЕ(СуществующиеДвижения.Регистратор) КАК КонфликтныйДокумент,
	|	СписокРаботников.Период
	|ИЗ
	|	(ВЫБРАТЬ
	|		СписокРаботников.НомерСтроки КАК НомерСтроки,
	|		СписокРаботников.Сотрудник КАК Сотрудник,
	|		СписокРаботников.ПодпадаетПодЕНВД КАК ПодпадаетПодЕНВД,
	|		СписокРаботников.СчетДт КАК СчетДт,
	|		СписокРаботников.Субконто1 КАК Субконто1,
	|		СписокРаботников.Субконто2 КАК Субконто2,
	|		СписокРаботников.Субконто3 КАК Субконто3,
	|		СписокРаботников.СпособРаспределенияЗатрат КАК СпособРаспределенияЗатрат,
	|		МИНИМУМ(ПовторяющиесяСтроки.НомерСтроки) КАК КонфликтныйНомерСтроки,
	|		СписокРаботников.МесяцРегистрации КАК Период
	|	ИЗ
	|		Документ.ВводПроцентаДеятельностиЕНВД.РаботникиОрганизации КАК СписокРаботников
	|			ЛЕВОЕ СОЕДИНЕНИЕ Документ.ВводПроцентаДеятельностиЕНВД.РаботникиОрганизации КАК ПовторяющиесяСтроки
	|			ПО СписокРаботников.Ссылка = ПовторяющиесяСтроки.Ссылка
	|				И СписокРаботников.НомерСтроки < ПовторяющиесяСтроки.НомерСтроки
	|				И СписокРаботников.Сотрудник = ПовторяющиесяСтроки.Сотрудник
	|				И СписокРаботников.МесяцРегистрации = ПовторяющиесяСтроки.МесяцРегистрации
	|	ГДЕ
	|		СписокРаботников.Ссылка = &ДокументСсылка
	|	
	|	СГРУППИРОВАТЬ ПО
	|		СписокРаботников.НомерСтроки,
	|		СписокРаботников.Сотрудник,
	|		СписокРаботников.ПодпадаетПодЕНВД,
	|		СписокРаботников.СчетДт,
	|		СписокРаботников.МесяцРегистрации,
	|		СписокРаботников.Субконто1,
	|		СписокРаботников.Субконто2,
	|		СписокРаботников.Субконто3,
	|		СписокРаботников.СпособРаспределенияЗатрат) КАК СписокРаботников
	|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|			СуществующиеДвижения.Сотрудник КАК Сотрудник,
	|			СуществующиеДвижения.Регистратор КАК Регистратор,
	|			СуществующиеДвижения.ПериодРегистрации КАК ПериодРегистрации
	|		ИЗ
	|			РегистрСведений.ПроцентДеятельностиЕНВДСотрудников КАК СуществующиеДвижения
	|		ГДЕ
	|			СуществующиеДвижения.ПериодРегистрации В
	|					(ВЫБРАТЬ
	|						СписокРаботников.МесяцРегистрации
	|					ИЗ
	|						Документ.ВводПроцентаДеятельностиЕНВД.РаботникиОрганизации КАК СписокРаботников
	|					СГРУППИРОВАТЬ ПО
	|								СписокРаботников.МесяцРегистрации)
	|			И СуществующиеДвижения.Сотрудник В
	|					(ВЫБРАТЬ
	|						СписокРаботников.Сотрудник
	|					ИЗ
	|						Документ.ВводПроцентаДеятельностиЕНВД.РаботникиОрганизации КАК СписокРаботников
	|					СГРУППИРОВАТЬ ПО
	|								СписокРаботников.Сотрудник)) КАК СуществующиеДвижения
	|		ПО СписокРаботников.Сотрудник = СуществующиеДвижения.Сотрудник
	|			И СписокРаботников.Период = СуществующиеДвижения.ПериодРегистрации";
	
	Возврат Запрос.Выполнить();

КонецФункции // СформироватьЗапросПоРаботникиОрганизации()

// Проверяет правильность заполнения шапки документа.
// Если какой-то из реквизитов шапки, влияющий на проведение не заполнен или
// заполнен не корректно, то выставляется флаг отказа в проведении.
// Проверка выполняется по выборке из результата запроса по шапке,
// все проверяемые реквизиты должны быть включены в выборку по шапке.
//
// Параметры: 
//	ВыборкаПоШапкеДокумента	- выборка из результата запроса по шапке документа,
//	Отказ 					- флаг отказа в проведении.
//
Процедура ПроверитьЗаполнениеШапки(ВыборкаПоШапкеДокумента, Отказ, Заголовок)

	Если НЕ ЗначениеЗаполнено(ВыборкаПоШапкеДокумента.Организация) Тогда
		ОбщегоНазначения.ВывестиИнформациюОбОшибке("Не указана организация!", Отказ, Заголовок);
		
	ИначеЕсли ЗначениеЗаполнено(ВыборкаПоШапкеДокумента.Период) Тогда
		НалоговыйУчет.ОбновитьДанныеУчетнойПолитикиПоНалоговомуУчету(глЗначениеПеременной("УчетнаяПолитикаОтраженияЗарплатыВУчете"), КонецМесяца(Период), Организация);
		Если Не глЗначениеПеременной("УчетнаяПолитикаОтраженияЗарплатыВУчете")[КонецМесяца(Период)][Организация].ОрганизацияЯвляетсяПлательщикомЕНВД Тогда
			ОбщегоНазначения.ВывестиИнформациюОбОшибке("Организация не применяет ЕНВД!", Отказ, Заголовок);
		КонецЕсли;
	КонецЕсли;
	
	Если НЕ ВыборкаПоШапкеДокумента.ВводДанныхПоПериодам И Не ЗначениеЗаполнено(ВыборкаПоШапкеДокумента.Период) Тогда
		ОбщегоНазначения.ВывестиИнформациюОбОшибке("Не указан период!", Отказ, Заголовок);
	КонецЕсли;

КонецПроцедуры // ПроверитьЗаполнениеШапки()

// Проверяет правильность заполнения реквизитов в строке ТЧ "РаботникиОрганизации" документа.
// Если какой-то из реквизитов, влияющий на проведение не заполнен или
// заполнен не корректно, то выставляется флаг отказа в проведении.
// Проверка выполняется по выборке из результата запроса по строке ТЧ документа,
// все проверяемые реквизиты должны быть включены в выборку.
//
// Параметры: 
//	ВыборкаПоШапкеДокумента		- выборка из результата запроса по шапке документа,
//	ВыборкаПоСтрокамДокумента	- спозиционированная на определенной строке выборка 
//								  из результата запроса
//	Отказ						- флаг отказа в проведении.
//	Заголовок					- Заголовок для сообщений об ошибках проведения
//
Процедура ПроверитьЗаполнениеСтрокиРаботникаОрганизации(ВыборкаПоШапкеДокумента, ВыборкаПоСтрокамДокумента, Отказ, Заголовок)

	СтрокаНачалаСообщенияОбОшибке = "В строке номер """+ СокрЛП(ВыборкаПоСтрокамДокумента.НомерСтроки) +
									""" табл. части ""сотрудники организации"": ";
	
	// Сотрудник
	Если НЕ ЗначениеЗаполнено(ВыборкаПоСтрокамДокумента.Сотрудник) Тогда
		ОбщегоНазначения.ВывестиИнформациюОбОшибке(СтрокаНачалаСообщенияОбОшибке + "не выбран сотрудник!", Отказ, Заголовок);
	КонецЕсли;
	
	// Организация сотрудника должна совпадать с организацией документа
	Если ВыборкаПоСтрокамДокумента.ОшибкаНеСоответствиеСотрудникаИОрганизации Тогда
		ОбщегоНазначения.ВывестиИнформациюОбОшибке(СтрокаНачалаСообщенияОбОшибке + ОбщегоНазначения.ПреобразоватьСтрокуИнтерфейса("указанный сотрудник оформлен на другую организацию!"), Отказ, Заголовок);
	КонецЕсли;
	
	// месяц регистрации
	Если НЕ ЗначениеЗаполнено(ВыборкаПоСтрокамДокумента.Период) Тогда
		ОбщегоНазначения.ВывестиИнформациюОбОшибке(СтрокаНачалаСообщенияОбОшибке + "не задан месяц регистрации!", Отказ, Заголовок);
	КонецЕсли;

	// Одинаковые строки
	Если ЗначениеЗаполнено(ВыборкаПоСтрокамДокумента.КонфликтныйНомерСтроки) Тогда
		ОбщегоНазначения.ВывестиИнформациюОбОшибке(СтрокаНачалаСообщенияОбОшибке  + "по сотруднику " + ВыборкаПоСтрокамДокумента.СотрудникНаименование + " обнаружено повторное назначение процента деятельности ЕНВД в строке №" + ВыборкаПоСтрокамДокумента.КонфликтныйНомерСтроки + "!", Отказ, Заголовок);
	КонецЕсли;
	
	// Движения в регистре на дату из документа
	Если ЗначениеЗаполнено(ВыборкаПоСтрокамДокумента.КонфликтныйДокумент) Тогда
		Расшифровки = Новый Массив;
		Расшифровки.Добавить(Новый Структура("Представление, Расшифровка", ВыборкаПоСтрокамДокумента.КонфликтныйДокумент, ВыборкаПоСтрокамДокумента.КонфликтныйДокумент));
		ОбщегоНазначения.ВывестиИнформациюОбОшибке(СтрокаНачалаСообщенияОбОшибке + "на период "+ Формат(ВыборкаПоСтрокамДокумента.Период, "ДФ='ММММ гггг'") + " процент деятельности ЕНВД уже зарегистрирован документом ", Отказ, Заголовок, , Расшифровки);
	КонецЕсли;
	
	// Счет ДТ
	Если НЕ ЗначениеЗаполнено(ВыборкаПоСтрокамДокумента.СчетДт) Тогда
		ОбщегоНазначения.ВывестиИнформациюОбОшибке(СтрокаНачалаСообщенияОбОшибке + "не указан счет дебета!", Отказ, Заголовок);
	Иначе
		// Субконто "Статьи затрат" для счета 44
		Если (ВыборкаПоСтрокамДокумента.СчетДт.ПринадлежитЭлементу(ПланыСчетов["Хозрасчетный"].РасходыНаПродажу)) и (НЕ ЗначениеЗаполнено(ВыборкаПоСтрокамДокумента.Субконто1)) Тогда
			ОбщегоНазначения.ВывестиИнформациюОбОшибке(СтрокаНачалаСообщенияОбОшибке + "не заполнено субконто ""Статьи затрат""!", Отказ, Заголовок);
		КонецЕсли	
		
	КонецЕсли;
	
КонецПроцедуры // ПроверитьЗаполнениеСтрокиРаботникаОрганизации()

// По строке выборки результата запроса по документу формируем движения по регистрам
//
// Параметры: 
//	ВыборкаПоШапкеДокумента					- выборка из результата запроса по шапке документа,
//	ВыборкаПоРаботникиОрганизации			- выборка из результата запроса по табличной части,
// Возвращаемое значение:
//	Нет.
//
Процедура ДобавитьСтрокуВДвиженияПоРегистрамСведений(ВыборкаПоШапкеДокумента, ВыборкаПоРаботникиОрганизации)
	
	Движение = Движения.ПроцентДеятельностиЕНВДСотрудников.Добавить();
	// Свойства
	
	// Измерения
	Движение.ПериодРегистрации	= ВыборкаПоРаботникиОрганизации.Период;
	Движение.Сотрудник			= ВыборкаПоРаботникиОрганизации.Сотрудник;
	Движение.Организация		= ВыборкаПоШапкеДокумента.ГоловнаяОрганизация;
	
	// Ресурсы
	Движение.ПодпадаетПодЕНВД	= ВыборкаПоРаботникиОрганизации.ПодпадаетПодЕНВД;
	Движение.СчетДт				= ВыборкаПоРаботникиОрганизации.СчетДт;
	Движение.Субконто1			= ВыборкаПоРаботникиОрганизации.Субконто1;
	Движение.Субконто2			= ВыборкаПоРаботникиОрганизации.Субконто2;
	Движение.Субконто3			= ВыборкаПоРаботникиОрганизации.Субконто3;
	Движение.СпособРаспределенияЗатрат = ВыборкаПоРаботникиОрганизации.СпособРаспределенияЗатрат;
	
КонецПроцедуры // ДобавитьСтрокуВДвиженияПоРегистрамСведений()

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ

Процедура ОбработкаПроведения(Отказ, Режим)
	
	ОбработкаКомментариев = глЗначениеПеременной("глОбработкаСообщений");
	ОбработкаКомментариев.УдалитьСообщения();
	
	Если мУдалятьДвижения Тогда
		ОбщегоНазначения.УдалитьДвиженияРегистратора(ЭтотОбъект, Отказ);
	КонецЕсли;
	
	// Заголовок для сообщений об ошибках проведения.
	Заголовок = ОбщегоНазначения.ПредставлениеДокументаПриПроведении(Ссылка);

	РезультатЗапросаПоШапке = СформироватьЗапросПоШапке(Режим);

	// Получим реквизиты шапки из запроса
	ВыборкаПоШапкеДокумента = РезультатЗапросаПоШапке.Выбрать();

	Если ВыборкаПоШапкеДокумента.Следующий() Тогда

		//Надо позвать проверку заполнения реквизитов шапки
		ПроверитьЗаполнениеШапки(ВыборкаПоШапкеДокумента, Отказ, Заголовок);

		// Движения стоит добавлять, если в проведении еще не отказано (отказ =ложь)
		Если НЕ Отказ Тогда

			// получим реквизиты табличной части
			РезультатЗапросаПоРаботники = СформироватьЗапросПоРаботникиОрганизации(Режим);
			ВыборкаПоРаботникиОрганизации = РезультатЗапросаПоРаботники.Выбрать();

			Пока ВыборкаПоРаботникиОрганизации.Следующий() Цикл 

				// проверим очередную строку табличной части
				ПроверитьЗаполнениеСтрокиРаботникаОрганизации(ВыборкаПоШапкеДокумента, ВыборкаПоРаботникиОрганизации, Отказ, Заголовок);

				Если НЕ Отказ Тогда

					// Заполним записи в наборах записей регистров
					ДобавитьСтрокуВДвиженияПоРегистрамСведений(ВыборкаПоШапкеДокумента, ВыборкаПоРаботникиОрганизации);
					
				КонецЕсли;

			КонецЦикла;
			
		КонецЕсли;

	КонецЕсли;

	ОбработкаКомментариев.ПоказатьСообщения();
	
КонецПроцедуры // ОбработкаПроведения()

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	МассивТЧ = Новый Массив();
	МассивТЧ.Добавить(РаботникиОрганизации);
	
	КраткийСоставДокумента = ПроцедурыУправленияПерсоналом.ЗаполнитьКраткийСоставДокумента(МассивТЧ);
	
	Если Не ВводДанныхПоПериодам Тогда
		//перед записью документа заполним месяц регистрации
		Для каждого СтрокаТЧ Из РаботникиОрганизации Цикл
			СтрокаТЧ.МесяцРегистрации = Период;
		КонецЦикла;
	КонецЕсли;
	
	мУдалятьДвижения = НЕ ЭтоНовый();
	
КонецПроцедуры // ПередЗаписью()


Процедура ОбработкаУдаленияПроведения(Отказ)

	
	ОбщегоНазначения.УдалитьДвиженияРегистратора(ЭтотОбъект, Отказ);

КонецПроцедуры // ОбработкаУдаленияПроведения

Процедура ПриЗаписи(Отказ)
	Если ОбменДанными.Загрузка  Тогда
		Возврат;
	КонецЕсли;

КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОПЕРАТОРЫ ОСНОВНОЙ ПРОГРАММЫ