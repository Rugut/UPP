Перем мУдалятьДвижения;

Перем мВалютаРегламентированногоУчета Экспорт;

Перем мСтруктураПараметровДляПолученияДоговора Экспорт;

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОКУМЕНТА

#Если Клиент Тогда

// Функция формирует табличный документ с печатной формой приходного ордера на поступление денежных документов
//
// Возвращаемое значение:
//  Табличный документ - печатная форма приходного ордера
//
Функция ПечатьПоступлениеДенежныхДокументов()

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ТекущийДокумент", Ссылка);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Док.Номер,
	|	Док.Дата,
	|	Док.ПринятоОт,
	|	Док.Организация,
	|	Док.СуммаДокумента,
	|	Док.ВалютаДокумента
	|ИЗ
	|	Документ.ПоступлениеДенежныхДокументов КАК Док
	|ГДЕ
	|	Док.Ссылка = &ТекущийДокумент";
	Шапка = Запрос.Выполнить().Выбрать();
	Шапка.Следующий();

	ЗапросПоДенежнымДокументам = Новый Запрос();
	ЗапросПоДенежнымДокументам.УстановитьПараметр("ТекущийДокумент", Ссылка);
	ЗапросПоДенежнымДокументам.Текст =
	"ВЫБРАТЬ
	|	ДенежныеДокументы.НомерСтроки КАК НомерСтроки,
	|	ДенежныеДокументы.ДенежныйДокумент КАК ДенежныйДокумент,
	|	ПРЕДСТАВЛЕНИЕ(ДенежныеДокументы.ДенежныйДокумент) КАК ДенежныйДокументПредставление,
	|	ДенежныеДокументы.Количество КАК Количество,
	|	ДенежныеДокументы.Сумма
	|ИЗ
	|	Документ.ПоступлениеДенежныхДокументов.ДенежныеДокументы КАК ДенежныеДокументы
	|ГДЕ
	|	ДенежныеДокументы.Ссылка = &ТекущийДокумент";
	ТаблицаДенежныхДокументов = ЗапросПоДенежнымДокументам.Выполнить().Выгрузить();

	ТабДокумент = Новый ТабличныйДокумент;
	ТабДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_ПоступлениеДенежныхДокументов_ПриходныйОрдер";
	Макет       = ПолучитьМакет("ПриходныйОрдер");

	// Вывести шапку 

	ОбластьМакета = Макет.ПолучитьОбласть("Заголовок");
	ОбластьМакета.Параметры.ТекстЗаголовка = ОбщегоНазначения.СформироватьЗаголовокДокумента(Шапка, "Приходный ордер");
	ТабДокумент.Вывести(ОбластьМакета);

	ОбластьМакета = Макет.ПолучитьОбласть("Организация");
	СведенияОбОрганизации = УправлениеКонтактнойИнформацией.СведенияОЮрФизЛице(Шапка.Организация, Шапка.Дата);
	ОбластьМакета.Параметры.ПредставлениеОрганизации = ФормированиеПечатныхФорм.ОписаниеОрганизации(
		СведенияОбОрганизации, "ПолноеНаименование,");
	ТабДокумент.Вывести(ОбластьМакета);

	ОбластьМакета = Макет.ПолучитьОбласть("Подразделение");
	ОбластьМакета.Параметры.Заполнить(Шапка);
	ТабДокумент.Вывести(ОбластьМакета);
	
	ОбластьМакета = Макет.ПолучитьОбласть("ПринятоОт");
	ОбластьМакета.Параметры.Заполнить(Шапка);
	ТабДокумент.Вывести(ОбластьМакета);

	// Вывести табличную часть
	ОбластьМакета = Макет.ПолучитьОбласть("ШапкаТаблицы");
	ТабДокумент.Вывести(ОбластьМакета);

	ОбластьМакета = Макет.ПолучитьОбласть("Строка");
	Для Каждого СтрокаДенежногоДокумента Из ТаблицаДенежныхДокументов Цикл

		ОбластьМакета.Параметры.Заполнить(СтрокаДенежногоДокумента);
		ТабДокумент.Вывести(ОбластьМакета);

	КонецЦикла;

	// Вывести Итого
	ОбластьМакета = Макет.ПолучитьОбласть("Итого");
	ОбластьМакета.Параметры.Всего = ОбщегоНазначения.ФорматСумм(Шапка.СуммаДокумента);
	ТабДокумент.Вывести(ОбластьМакета);

	// Вывести Сумму прописью
	ОбластьМакета = Макет.ПолучитьОбласть("СуммаПрописью");
	ОбластьМакета.Параметры.ИтоговаяСтрока = "Всего наименований " + ТаблицаДенежныхДокументов.Количество()
		+ ", на сумму " + ОбщегоНазначения.ФорматСумм(Шапка.СуммаДокумента, Шапка.ВалютаДокумента);
	ОбластьМакета.Параметры.СуммаПрописью = 
		ОбщегоНазначения.СформироватьСуммуПрописью(Шапка.СуммаДокумента, Шапка.ВалютаДокумента);
	ТабДокумент.Вывести(ОбластьМакета);

	// Вывести подписи
	ОбластьМакета = Макет.ПолучитьОбласть("Подписи");
	ОбластьМакета.Параметры.Заполнить(Шапка);
	ТабДокумент.Вывести(ОбластьМакета);

	Возврат ТабДокумент;

КонецФункции // ПечатьПоступлениеДенежныхДокументов()

// Процедура осуществляет печать документа. Можно направить печать на 
// экран или принтер, а также распечатать необходмое количество копий.
//
//  Название макета печати передается в качестве параметра,
// по переданному названию находим имя макета в соответствии.
//
// Параметры:
//  НазваниеМакета - строка, название макета.
//
Процедура Печать(ИмяМакета, КоличествоЭкземпляров = 1, НаПринтер = Ложь) Экспорт
	
	Если ЭтоНовый() Тогда
		Предупреждение("Документ можно распечатать только после его записи");
		Возврат;
	ИначеЕсли Не УправлениеДопПравамиПользователей.РазрешитьПечатьНепроведенныхДокументов(Проведен) Тогда
		Предупреждение("Недостаточно полномочий для печати непроведенного документа!");
		Возврат;
	КонецЕсли;

	Если Не РаботаСДиалогами.ПроверитьМодифицированность(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;

	// Получить экземпляр документа на печать
	Если ИмяМакета = "ПриходныйОрдер" тогда
		
		ТабДокумент = ПечатьПоступлениеДенежныхДокументов();
		
	ИначеЕсли ТипЗнч(ИмяМакета) = Тип("ДвоичныеДанные") Тогда

		ТабДокумент = УниверсальныеМеханизмы.НапечататьВнешнююФорму(Ссылка, ИмяМакета);
		
		Если ТабДокумент = Неопределено Тогда
			Возврат
		КонецЕсли;

	КонецЕсли;
	
	УниверсальныеМеханизмы.НапечататьДокумент(
		ТабДокумент, КоличествоЭкземпляров, НаПринтер, 
		ОбщегоНазначения.СформироватьЗаголовокДокумента(ЭтотОбъект, Строка(ВидОперации)), Ссылка);
	
КонецПроцедуры // Печать

#КонецЕсли

// Возвращает доступные варианты печати документа
//
// Возвращаемое значение:
//  Структура, каждая строка которой соответствует одному из вариантов печати
//  
Функция ПолучитьСтруктуруПечатныхФорм() Экспорт
	
	СтруктураПечатныхФорм = Новый Структура;
	СтруктураПечатныхФорм.Вставить("ПриходныйОрдер", "Приходный ордер");
	Возврат СтруктураПечатныхФорм;

КонецФункции // ПолучитьСтруктуруПечатныхФорм()

//////////////////////////////////////////////////////////////////////////////////
//// ПРОЦЕДУРЫ И ФУНКЦИИ ДЛЯ ОБЕСПЕЧЕНИЯ ПРОВЕДЕНИЯ ДОКУМЕНТА

// Выгружает результат запроса в табличную часть, добавляет ей необходимые колонки для проведения.
//
// Параметры: 
//  РезультатЗапросаПоТоварам - результат запроса по табличной части "Товары",
//  СтруктураШапкиДокумента   - структура, содержащая рексвизиты шапки документа и результаты запроса по шапке
//
// Возвращаемое значение:
//  Сформированная таблиица значений.
//
// Проверяет правильность заполнения шапки документа.
// Если какой-то из реквизтов шапки, влияющий на проведение не заполнен или
// заполнен не корректно, то выставляется флаг отказа в проведении.
// Проверяется также правильность заполнения реквизитов ссылочных полей документа.
// Проверка выполняется по объекту и по выборке из результата запроса по шапке.
//
// Параметры: 
//  СтруктураШапкиДокумента - структура, содержащая рексвизиты шапки документа и результаты запроса по шапке,
//  Отказ                   - флаг отказа в проведении,
//  Заголовок               - строка, заголовок сообщения об ошибке проведения.
//
Процедура ПроверитьЗаполнениеШапки(СтруктураШапкиДокумента, Отказ, Заголовок)

	СтруктураОбязательныхПолей = Новый Структура("ВидОперации, Организация, СчетУчетаДенежныхДокументов");

	Если СтруктураШапкиДокумента.ВидОперации = Перечисления.ВидыОперацийПоступлениеДенежныхДокументов.ПоступлениеОтПоставщика Тогда

		СтруктураОбязательныхПолей.Вставить("Контрагент");
		СтруктураОбязательныхПолей.Вставить("ДоговорКонтрагента");
		СтруктураОбязательныхПолей.Вставить("СчетУчетаРасчетовСКонтрагентом");
		
	ИначеЕсли СтруктураШапкиДокумента.ВидОперации = Перечисления.ВидыОперацийПоступлениеДенежныхДокументов.ПоступлениеОтПодотчетногоЛица Тогда

		СтруктураОбязательныхПолей.Вставить("Контрагент", "Не указано подотчетное лицо");
		
	ИначеЕсли СтруктураШапкиДокумента.ВидОперации = Перечисления.ВидыОперацийПоступлениеДенежныхДокументов.ПрочееПоступление Тогда

		СтруктураОбязательныхПолей.Вставить("СчетУчетаРасчетовСКонтрагентом", "Не указан счет кредита");
		
	КонецЕсли;

	// Документ должен принадлежать хотя бы к одному виду учета (управленческий, бухгалтерский, налоговый)
	ОбщегоНазначения.ПроверитьПринадлежностьКВидамУчета(СтруктураШапкиДокумента, Отказ, Заголовок);

	// Теперь вызовем общую процедуру проверки.
	ЗаполнениеДокументов.ПроверитьЗаполнениеШапкиДокумента(ЭтотОбъект, СтруктураОбязательныхПолей, Отказ, Заголовок);

КонецПроцедуры // ПроверитьЗаполнениеШапки()

// Проверяет правильность заполнения строк табличной части "ДенежныеДокументы".
//
// Параметры:
// Параметры: 
//  ТаблицаПоДенежнымДокументам - таблица значений, содержащая данные для проведения и проверки ТЧ ДенежныеДокументы
//  СтруктураШапкиДокумента     - структура, содержащая рексвизиты шапки документа и результаты запроса по шапке
//  Отказ                       - флаг отказа в проведении.
//  Заголовок                   - строка, заголовок сообщения об ошибке проведения.
//
Процедура ПроверитьЗаполнениеТабличнойЧастиДенежныеДокументы(ТаблицаПоДенежнымДокументам, СтруктураШапкиДокумента, Отказ, Заголовок)

	// Укажем, что надо проверить:
	СтруктураОбязательныхПолей = Новый Структура("ДенежныйДокумент, Количество");
	
	// Теперь вызовем общую процедуру проверки.
	ЗаполнениеДокументов.ПроверитьЗаполнениеТабличнойЧасти(ЭтотОбъект, "ДенежныеДокументы", СтруктураОбязательныхПолей, Отказ, Заголовок);

КонецПроцедуры // ПроверитьЗаполнениеТабличнойЧастиДенежныеДокументы()

Процедура ДвиженияПоРегистрамУпр(СтруктураШапкиДокумента, ТаблицаПоДенежнымДокументам,Отказ, Заголовок)
	
	Если ВидОперации = Перечисления.ВидыОперацийПоступлениеДенежныхДокументов.ПрочееПоступление Тогда
		Возврат;
	КонецЕсли;
	Если СтруктураШапкиДокумента.СуммаВзаиморасчетов = 0 Тогда
		Возврат;
	КонецЕсли;
	
	СуммаУпр = МодульВалютногоУчета.ПересчитатьИзВалютыВВалюту(
		СтруктураШапкиДокумента.СуммаВзаиморасчетов, 
		СтруктураШапкиДокумента.ВалютаВзаиморасчетов, СтруктураШапкиДокумента.ВалютаУправленческогоУчета,
		СтруктураШапкиДокумента.КурсВзаиморасчетов, СтруктураШапкиДокумента.КурсВалютыУправленческогоУчета,
		СтруктураШапкиДокумента.КратностьВзаиморасчетов, СтруктураШапкиДокумента.КратностьВалютыУправленческогоУчета);
		
	Если ВидОперации = Перечисления.ВидыОперацийПоступлениеДенежныхДокументов.ПоступлениеОтПоставщика Тогда
			
		// По регистру ВзаиморасчетыСКонтрагентами

		ТаблицаВзаиморасчетыСКонтрагентами = Движения.ВзаиморасчетыСКонтрагентами.ВыгрузитьКолонки();
		
		СтрокаДвижений = ТаблицаВзаиморасчетыСКонтрагентами.Добавить();
		
		СтрокаДвижений.ДоговорКонтрагента  = ДоговорКонтрагента;
		СтрокаДвижений.Контрагент  		   = Контрагент;
		СтрокаДвижений.Организация  	   = Организация;
    	СтрокаДвижений.Сделка              = Неопределено;
		
		СтрокаДвижений.СуммаВзаиморасчетов = СтруктураШапкиДокумента.СуммаВзаиморасчетов;
		СтрокаДвижений.СуммаУпр            = СуммаУпр;
		
		Движения.ВзаиморасчетыСКонтрагентами.мПериод            = Дата;
		Движения.ВзаиморасчетыСКонтрагентами.мТаблицаДвижений   = ТаблицаВзаиморасчетыСКонтрагентами;
		
		Если Не Отказ Тогда
			Движения.ВзаиморасчетыСКонтрагентами.ВыполнитьРасход();
		КонецЕсли;

		// По регистру РасчетыСКонтрагентами
		
		ТаблицаРасчетыСКонтрагентами = Движения.РасчетыСКонтрагентами.ВыгрузитьКолонки();
		
		СтрокаДвижений = ТаблицаРасчетыСКонтрагентами.Добавить();
		
		СтрокаДвижений.РасчетыВозврат      = Перечисления.РасчетыВозврат.Расчеты;
		СтрокаДвижений.ДоговорКонтрагента  = ДоговорКонтрагента;
		СтрокаДвижений.Контрагент  		   = Контрагент;
		СтрокаДвижений.Организация  	   = Организация;
		СтрокаДвижений.Сделка              = Неопределено;
		
		СтрокаДвижений.СуммаВзаиморасчетов = СтруктураШапкиДокумента.СуммаВзаиморасчетов;
		СтрокаДвижений.СуммаУпр            = СуммаУпр;
		
		Движения.РасчетыСКонтрагентами.мПериод            = Дата;
		Движения.РасчетыСКонтрагентами.мТаблицаДвижений   = ТаблицаРасчетыСКонтрагентами;

		Если Не Отказ Тогда
			Движения.РасчетыСКонтрагентами.ВыполнитьРасход();
		КонецЕсли;

	ИначеЕсли ВидОперации = Перечисления.ВидыОперацийПоступлениеДенежныхДокументов.ПоступлениеОтПодотчетногоЛица Тогда
		
		ТаблицаВзаиморасчетыСПодотчетнымиЛицами = Движения.ВзаиморасчетыСПодотчетнымиЛицами.Выгрузить();
			
		СтрокаДвижений = ТаблицаВзаиморасчетыСПодотчетнымиЛицами.Добавить();
		
		СтрокаДвижений.Организация         = Организация;
		СтрокаДвижений.ФизЛицо             = Контрагент;
		СтрокаДвижений.РасчетныйДокумент   = ?(ЗначениеЗаполнено(РасчетныйДокумент), РасчетныйДокумент, Ссылка);
		СтрокаДвижений.Валюта              = СтруктураШапкиДокумента.ВалютаДокумента;
		
		СтрокаДвижений.СуммаВзаиморасчетов = СтруктураШапкиДокумента.СуммаВзаиморасчетов;
		СтрокаДвижений.СуммаУпр            = СуммаУпр;
			
		Движения.ВзаиморасчетыСПодотчетнымиЛицами.мПериод          = Дата;
		Движения.ВзаиморасчетыСПодотчетнымиЛицами.мТаблицаДвижений = ТаблицаВзаиморасчетыСПодотчетнымиЛицами;
		Движения.ВзаиморасчетыСПодотчетнымиЛицами.ВыполнитьРасход();
		
	КонецЕсли;

КонецПроцедуры // ДвиженияПоРегистрамУпр()

Процедура ДвиженияПоРегистрамРегл(СтруктураШапкиДокумента, ТаблицаПоДенежнымДокументам, РежимПроведения, Отказ, Заголовок)
	
	// Проводки по поступлению денежных документов

	Проводки = Движения.Хозрасчетный;
	
	ДатаДока = СтруктураШапкиДокумента.Дата;
	
	СодержаниеПроводок = "Поступление денежных документов "
		+ "по вх.д." + НомерВходящегоДокумента + " от " + Формат(ДатаВходящегоДокумента, "ДЛФ=Д");
	
	Для каждого СтрокаТаблицы Из ТаблицаПоДенежнымДокументам Цикл

		Проводка = Проводки.Добавить();

		Проводка.Период      = ДатаДока;
		Проводка.Организация = СтруктураШапкиДокумента.Организация;
		Проводка.Содержание  = СодержаниеПроводок;
		Проводка.Сумма       = СтрокаТаблицы.СуммаБУ;

		Проводка.СчетДт      = СтруктураШапкиДокумента.СчетУчетаДенежныхДокументов;
		Если Проводка.СчетДт.Количественный Тогда
			Проводка.КоличествоДт = СтрокаТаблицы.Количество;
		КонецЕсли;

		БухгалтерскийУчет.УстановитьСубконто(
			Проводка.СчетДт, Проводка.СубконтоДт, "ДенежныеДокументы", СтрокаТаблицы.ДенежныйДокумент, Истина);

		Если Проводка.СчетДт.Валютный Тогда
			Проводка.ВалютаДт        = СтруктураШапкиДокумента.ВалютаДокумента;
			Проводка.ВалютнаяСуммаДт = СтрокаТаблицы.СуммаВал;
		КонецЕсли;
		
		Если ВидОперации = Перечисления.ВидыОперацийПоступлениеДенежныхДокументов.ПоступлениеОтПоставщика Тогда
			
			Проводка.СчетКт = СтруктураШапкиДокумента.СчетУчетаРасчетовСКонтрагентом;
			БухгалтерскийУчет.УстановитьСубконто(
				Проводка.СчетКт, Проводка.СубконтоКт, "Контрагенты", СтруктураШапкиДокумента.Контрагент, Истина);
			БухгалтерскийУчет.УстановитьСубконто(
				Проводка.СчетКт, Проводка.СубконтоКт, "Договоры", СтруктураШапкиДокумента.ДоговорКонтрагента);
			
		ИначеЕсли ВидОперации = Перечисления.ВидыОперацийПоступлениеДенежныхДокументов.ПоступлениеОтПодотчетногоЛица Тогда

			Если Проводка.СчетДт.Валютный Тогда
				Проводка.СчетКт = ПланыСчетов.Хозрасчетный.РасчетыСПодотчетнымиЛицамиВал;
			Иначе
				Проводка.СчетКт = ПланыСчетов.Хозрасчетный.РасчетыСПодотчетнымиЛицами;
			КонецЕсли;
			БухгалтерскийУчет.УстановитьСубконто(
				Проводка.СчетКт, Проводка.СубконтоКт, "РаботникиОрганизации", СтруктураШапкиДокумента.Контрагент, Истина);
			
		Иначе
			
			Проводка.СчетКт = СтруктураШапкиДокумента.СчетУчетаРасчетовСКонтрагентом;
			БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт, 1, СубконтоКт1);
			БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт, 2, СубконтоКт2);
			БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт, 3, СубконтоКт3);
			
		КонецЕсли;
		
		Если Проводка.СчетКт.Валютный Тогда
			Проводка.ВалютаКт        = СтруктураШапкиДокумента.ВалютаДокумента;
			Проводка.ВалютнаяСуммаКт = СтрокаТаблицы.СуммаВал;
		КонецЕсли;
		
	КонецЦикла;
	
	Если СтруктураШапкиДокумента.ОтражатьВНалоговомУчетеУСН Тогда
		
		НалоговыйУчетУСН.ДвиженияУСН(Ссылка, РежимПроведения);
	ИначеЕсли СтруктураШапкиДокумента.ОтражатьВНалоговомУчетеУСНДоходы Тогда
		
		НалоговыйУчетУСН.ПрочееДДС(ЭтотОбъект);
	КонецЕсли;

КонецПроцедуры // ДвиженияПоРегистрамРегл()

Процедура ДвиженияПоРегистрам(РежимПроведения, СтруктураШапкиДокумента, ТаблицаПоДенежнымДокументам,Отказ, Заголовок)
	
	Если СтруктураШапкиДокумента.ОтражатьВУправленческомУчете Тогда
		ДвиженияПоРегистрамУпр(СтруктураШапкиДокумента, ТаблицаПоДенежнымДокументам,Отказ, Заголовок);
	КонецЕсли;
		
	Если СтруктураШапкиДокумента.ОтражатьВБухгалтерскомУчете Тогда
		ДвиженияПоРегистрамРегл(СтруктураШапкиДокумента, ТаблицаПоДенежнымДокументам, РежимПроведения, Отказ, Заголовок);
	КонецЕсли;
	
КонецПроцедуры // ДвиженияПоРегистрам()

// Процедура формирует структуру шапки документа и дополнительных полей.
//
Процедура ПодготовитьСтруктуруШапкиДокумента(СтруктураШапкиДокумента, Отказ) Экспорт
	
	// Сформируем структуру реквизитов шапки документа
	СтруктураШапкиДокумента = ОбщегоНазначения.СформироватьСтруктуруШапкиДокумента(ЭтотОбъект);
	
	СтруктураШапкиДокумента.Вставить("ОтражатьВУправленческомУчете",   Истина);
	СтруктураШапкиДокумента.Вставить("ОтражатьВБухгалтерскомУчете",    Истина);
	
	ПрименяетсяУСН = НалоговыйУчетУСН.ПрименениеУСН(Организация, Дата);
	СтруктураШапкиДокумента.Вставить("ОтражатьВНалоговомУчете", НЕ ПрименяетсяУСН);
	СтруктураШапкиДокумента.Вставить("ОтражатьВНалоговомУчетеУСНДоходы", 
		ПрименяетсяУСН И НалоговыйУчетУСН.ПрименениеУСНДоходы(Организация, Дата));
	СтруктураШапкиДокумента.Вставить("ОтражатьВНалоговомУчетеУСН",
		ПрименяетсяУСН И НЕ СтруктураШапкиДокумента.ОтражатьВНалоговомУчетеУСНДоходы);
	
	СтруктураШапкиДокумента.Вставить("ВалютаРегламентированногоУчета", мВалютаРегламентированногоУчета);
	СтруктураШапкиДокумента.Вставить("СуммаВзаиморасчетов",            СтруктураШапкиДокумента.СуммаДокумента);
	Если ВидОперации = Перечисления.ВидыОперацийПоступлениеДенежныхДокументов.ПоступлениеОтПодотчетногоЛица Тогда
		СтруктураШапкиДокумента.Вставить("ВалютаВзаиморасчетов", СтруктураШапкиДокумента.ВалютаДокумента);
	КонецЕсли;
	
	ДеревоПолейЗапросаПоШапке = УправлениеЗапасами.СформироватьДеревоПолейЗапросаПоШапке();
	
	УправлениеЗапасами.ДобавитьСтрокуВДеревоПолейЗапросаПоШапке(
		ДеревоПолейЗапросаПоШапке, "Константы", "ВалютаУправленческогоУчета", "ВалютаУправленческогоУчета");
	УправлениеЗапасами.ДобавитьСтрокуВДеревоПолейЗапросаПоШапке(
		ДеревоПолейЗапросаПоШапке, "Константы", "КурсВалютыУправленческогоУчета" , "КурсВалютыУправленческогоУчета");
	
	Если ВидОперации = Перечисления.ВидыОперацийПоступлениеДенежныхДокументов.ПоступлениеОтПоставщика Тогда
		
		УправлениеЗапасами.ДобавитьСтрокуВДеревоПолейЗапросаПоШапке(
			ДеревоПолейЗапросаПоШапке, "ДоговорКонтрагента", "Организация", "ДоговорОрганизация");
		УправлениеЗапасами.ДобавитьСтрокуВДеревоПолейЗапросаПоШапке(
			ДеревоПолейЗапросаПоШапке, "ДоговорКонтрагента", "ВидДоговора", "ВидДоговора");
		УправлениеЗапасами.ДобавитьСтрокуВДеревоПолейЗапросаПоШапке(
			ДеревоПолейЗапросаПоШапке, "ДоговорКонтрагента", "ВалютаВзаиморасчетов", "ВалютаВзаиморасчетов");
		
	КонецЕсли;
	
	// Сформируем запрос на дополнительные параметры, нужные при проведении, по данным шапки документа
	СтруктураШапкиДокумента = УправлениеЗапасами.СформироватьЗапросПоДеревуПолей(
		ЭтотОбъект, ДеревоПолейЗапросаПоШапке, СтруктураШапкиДокумента, мВалютаРегламентированногоУчета);
	
КонецПроцедуры

Процедура ПодготовитьТаблицыДокумента(СтруктураШапкиДокумента, ТаблицаПоДенежнымДокументам) Экспорт
	
	// Получим необходимые данные по табличной части "Денежные документы".
	СтруктураПолей = Новый Структура;
	СтруктураПолей.Вставить("ДенежныйДокумент"	, "ДенежныйДокумент");
	СтруктураПолей.Вставить("Количество"  		, "Количество");
	СтруктураПолей.Вставить("Сумма"       		, "Сумма");

	РезультатЗапроса = УправлениеЗапасами.СформироватьЗапросПоТабличнойЧасти(
		ЭтотОбъект, "ДенежныеДокументы", СтруктураПолей);
		
	ТаблицаПоДенежнымДокументам = РезультатЗапроса.Выгрузить();
	
	БухгалтерскийУчетРасчетовСКонтрагентами.ПодготовкаТаблицыЗначенийДляЦелейПриобретенияИРеализации(
		ТаблицаПоДенежнымДокументам, СтруктураШапкиДокумента, Истина, мВалютаРегламентированногоУчета);

КонецПроцедуры	
	
////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

// Процедура вызывается перед записью документа 
//
Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)

	НалоговыйУчетУСН.ЗаполнитьНастройкуКУДиР(ЭтотОбъект);
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	СуммаДокумента = УчетНДС.ПолучитьСуммуДокументаСНДС(ЭтотОбъект);

	мУдалятьДвижения = НЕ ЭтоНовый();
	
КонецПроцедуры // ПередЗаписью

// Процедура - обработчик события "ОбработкаПроведения".
//
Процедура ОбработкаПроведения(Отказ, РежимПроведения)

	Перем СтруктураШапкиДокумента, ТаблицаПоДенежнымДокументам;
	
	Если мУдалятьДвижения Тогда
		ОбщегоНазначения.УдалитьДвиженияРегистратора(ЭтотОбъект, Отказ, Истина, РежимПроведения);
	КонецЕсли;
	
	ПодготовитьСтруктуруШапкиДокумента(СтруктураШапкиДокумента, Отказ);
	
	Заголовок = ОбщегоНазначения.ПредставлениеДокументаПриПроведении(СтруктураШапкиДокумента);
     
	ПроверитьЗаполнениеШапки(СтруктураШапкиДокумента, Отказ, Заголовок);

	ПодготовитьТаблицыДокумента(СтруктураШапкиДокумента, ТаблицаПоДенежнымДокументам);

	ПроверитьЗаполнениеТабличнойЧастиДенежныеДокументы(
		ТаблицаПоДенежнымДокументам, СтруктураШапкиДокумента, Отказ, Заголовок);

	УправлениеВзаиморасчетами.ПроверкаВозможностиПроведенияВ_БУ_НУ(
		СтруктураШапкиДокумента.ДоговорКонтрагента, СтруктураШапкиДокумента.ВалютаДокумента,
		СтруктураШапкиДокумента.ОтражатьВБухгалтерскомУчете, СтруктураШапкиДокумента.ОтражатьВНалоговомУчете,
		мВалютаРегламентированногоУчета, Ложь, Отказ, Заголовок);

	Если Не Отказ Тогда
		ДвиженияПоРегистрам(РежимПроведения, СтруктураШапкиДокумента, ТаблицаПоДенежнымДокументам, Отказ, Заголовок);
	КонецЕсли;

КонецПроцедуры // ОбработкаПроведения()

мВалютаРегламентированногоУчета = глЗначениеПеременной("ВалютаРегламентированногоУчета");

мСтруктураПараметровДляПолученияДоговора = Новый Структура();
СписокДопустимыхВидовДоговоров = Новый СписокЗначений();
СписокДопустимыхВидовДоговоров.Добавить(Перечисления.ВидыДоговоровКонтрагентов.Прочее);
мСтруктураПараметровДляПолученияДоговора.Вставить("СписокДопустимыхВидовДоговоров",   СписокДопустимыхВидовДоговоров);
мСтруктураПараметровДляПолученияДоговора.Вставить("ВидСравненияВалютыВзаиморасчетов", "=");