Перем мУдалятьДвижения;

// Строки, хранят реквизиты имеющие смысл только для бух. учета и упр. соответственно
// в случае если документ не отражается в каком-то виде учета, делаются невидимыми
Перем мСтрокаРеквизитыБухУчета Экспорт; // (Регл)
Перем мСтрокаРеквизитыУпрУчета Экспорт; // (Упр)
Перем мСтрокаРеквизитыНалУчета Экспорт; // (Регл)

Перем мВалютаРегламентированногоУчета Экспорт;
Перем мВалютаУправленческогоУчета Экспорт;

Перем мУказаниеПроектовВТабличнойЧастиДокументов Экспорт;

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОКУМЕНТА

#Если Клиент Тогда
	
// Функция формирования печатной формы документа "ПрочиеЗатраты"
//
Функция ПечатьПрочиеЗатраты(ИмяМакета)
	
	ТипУчета = ?( ИмяМакета = "ПрочиеЗатратыУпр", "Упр",
			   ?( ИмяМакета = "ПрочиеЗатратыБух", "Бух",
			   ?( ОтражатьВУправленческомУчете, "Упр", "Бух")));
			   
	ТекстЗапроса = "ВЫБРАТЬ
	               |	ПрочиеЗатраты.Ссылка,
	               |	ПрочиеЗатраты.Представление,
	               |	ПрочиеЗатраты.Организация,
	               |	ПрочиеЗатраты.Организация.Представление КАК ПечОрганизация,
	               |	ВЫБОР
	               |		КОГДА &ТипУчета = ""Упр""
	               |			ТОГДА ПрочиеЗатраты.Подразделение
	               |		ИНАЧЕ ПрочиеЗатраты.ПодразделениеОрганизации
	               |	КОНЕЦ КАК Подразделение,
	               |	ВЫБОР
	               |		КОГДА &ТипУчета = ""Упр""
	               |			ТОГДА ПРЕДСТАВЛЕНИЕ(ПрочиеЗатраты.Подразделение)
	               |		ИНАЧЕ ПРЕДСТАВЛЕНИЕ(ПрочиеЗатраты.ПодразделениеОрганизации)
	               |	КОНЕЦ КАК ПечПодразделение,
	               |	ПрочиеЗатраты.Затраты.(
	               |		НомерСтроки КАК НомерСтроки,
	               |		СтатьяЗатрат,
	               |		ПРЕДСТАВЛЕНИЕ(ПрочиеЗатраты.Затраты.СтатьяЗатрат) КАК ПечСтатьяЗатрат,
	               |		ВЫБОР
	               |			КОГДА &ТипУчета = ""Упр""
	               |				ТОГДА ПрочиеЗатраты.Затраты.Сумма
	               |			ИНАЧЕ ПрочиеЗатраты.Затраты.СуммаРегл
	               |		КОНЕЦ КАК Сумма,
	               |		СчетЗатрат КАК Счет,
	               |		ПРЕДСТАВЛЕНИЕ(ПрочиеЗатраты.Затраты.СчетЗатрат) КАК ПечСчет,
	               |		Заказ КАК Заказ,
	               |		ПРЕДСТАВЛЕНИЕ(ПрочиеЗатраты.Затраты.Заказ) КАК ПечЗаказ,
	               |		СпособРаспределенияЗатратНаВыпуск КАК СпособРаспределения,
	               |		ПРЕДСТАВЛЕНИЕ(ПрочиеЗатраты.Затраты.СпособРаспределенияЗатратНаВыпуск) КАК ПечСпособРаспределения,
	               |		НоменклатурнаяГруппа КАК НомГруппа,
	               |		ПРЕДСТАВЛЕНИЕ(ПрочиеЗатраты.Затраты.НоменклатурнаяГруппа) КАК ПечНомГруппа,
	               |		Продукция,
	               |		ХарактеристикаПродукции КАК Характеристика,
	               |		СерияПродукции КАК Серия,
	               |		СтатьяЗатрат.ХарактерЗатрат КАК ХарактерЗатрат,
	               |		ОбъектСтроительства,
	               |		СпособСтроительства
               |	)
	               |ИЗ
	               |	Документ.ПрочиеЗатраты КАК ПрочиеЗатраты
	               |ГДЕ
	               |	ПрочиеЗатраты.Ссылка = &ТекДок
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	НомерСтроки";
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапроса;
	
	Запрос.УстановитьПараметр( "ТекДок",   Ссылка);
	Запрос.УстановитьПараметр( "ТипУчета", ТипУчета);
	
	РезультатЗапроса = Запрос.Выполнить();
	Шапка = РезультатЗапроса.Выбрать();
	Шапка.Следующий();
	
	ТабДок = Новый ТабличныйДокумент;
	ТабДок.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_ПрочиеЗатраты_ПрочиеЗатраты";
	
	Макет  = ПолучитьМакет("ПрочиеЗатраты");
	
	// Вывод заголовка
	Область = Макет.ПолучитьОбласть("Заголовок");
	Область.Параметры.Заголовок = ОбщегоНазначения.СформироватьЗаголовокДокумента( ЭтотОбъект);
	Область.Параметры.Подразделение    = Шапка.Подразделение;
	Область.Параметры.ПечПодразделение = Шапка.ПечПодразделение;
	Область.Параметры.Организация      = Шапка.Организация;
	Область.Параметры.ПечОрганизация   = Шапка.ПечОрганизация;
	
	ТабДок.Вывести(Область);
	
	Если ТипУчета = "Упр" Тогда
		Область = Макет.ПолучитьОбласть("ТабШапкаУпр");
		ТабДок.Вывести(Область);
		Область = Макет.ПолучитьОбласть("ТабСтрокаУпр");
	Иначе
		Область = Макет.ПолучитьОбласть("ТабШапка");
		ТабДок.Вывести(Область);
		Область = Макет.ПолучитьОбласть("ТабСтрока");
	КонецЕсли;
	ТабЧасть = Шапка.Затраты.Выбрать();
	СуммаИтого = 0;
	
	Пока ТабЧасть.Следующий() Цикл
		Область.Параметры.ПечНомер        = ТабЧасть.НомерСтроки;
		
		Область.Параметры.СтатьяЗатрат    = ТабЧасть.СтатьяЗатрат;
		Область.Параметры.ПечСтатьяЗатрат = ТабЧасть.ПечСтатьяЗатрат;
		Область.Параметры.ПечСумма        = ТабЧасть.Сумма;
		
		Область.Параметры.Заказ           = ТабЧасть.Заказ;
		Область.Параметры.ПечЗаказ        = ТабЧасть.ПечЗаказ;
		
		Область.Параметры.ПечАналитика1 = "";
		Область.Параметры.ПечАналитика2 = "";
		Область.Параметры.ПечАналитика3 = "";
		Область.Параметры.Аналитика1    = Неопределено;
		Область.Параметры.Аналитика2    = Неопределено;
		Область.Параметры.Аналитика3    = Неопределено;
			
		Если ТипУчета = "Упр" Тогда
			Область.Параметры.ПечХарактерЗатрат = ТабЧасть.ХарактерЗатрат;
		Иначе
			Область.Параметры.ПечСчет = ТабЧасть.ПечСчет;
			Область.Параметры.Счет    = ТабЧасть.Счет;
		КонецЕсли;
		
		Если ТабЧасть.ХарактерЗатрат = Перечисления.ХарактерЗатрат.ПроизводственныеРасходы Тогда
		
			Область.Параметры.Аналитика1      = ТабЧасть.НомГруппа;
			Область.Параметры.ПечАналитика1   = ТабЧасть.ПечНомГруппа;
			Область.Параметры.Аналитика2      = ТабЧасть.СпособРаспределения;
			Область.Параметры.ПечАналитика2   = ТабЧасть.ПечСпособРаспределения;
		
		ИначеЕсли ТабЧасть.ХарактерЗатрат = Перечисления.ХарактерЗатрат.БракВПроизводстве Тогда
			
			// "Брак в производстве";
			Область.Параметры.Аналитика1      = ТабЧасть.НомГруппа;
			Область.Параметры.ПечАналитика1   = ТабЧасть.ПечНомГруппа;
			Область.Параметры.Аналитика2      = ТабЧасть.Продукция;
			Область.Параметры.ПечАналитика2   = СокрП(ТабЧасть.Продукция) + ФормированиеПечатныхФорм.ПредставлениеСерий(ТабЧасть);
			
		ИначеЕсли ТабЧасть.ХарактерЗатрат = Перечисления.ХарактерЗатрат.ВложенияВоВнеоборотныеАктивы Тогда
			
			// "Строительство объектов основных средств"
			Область.Параметры.ПечАналитика1   = ТабЧасть.ОбъектСтроительства;
			Область.Параметры.Аналитика1      = ТабЧасть.ОбъектСтроительства;
			Область.Параметры.ПечАналитика3   = ТабЧасть.СпособСтроительства;
			Область.Параметры.Аналитика3      = ТабЧасть.СпособСтроительства;
			
		ИначеЕсли ТабЧасть.ХарактерЗатрат = Перечисления.ХарактерЗатрат.ОбщепроизводственныеРасходы
			  ИЛИ ТабЧасть.ХарактерЗатрат = Перечисления.ХарактерЗатрат.ОбщехозяйственныеРасходы
			  ИЛИ ТабЧасть.ХарактерЗатрат = Перечисления.ХарактерЗатрат.КоммерческиеРасходы
			  ИЛИ ТабЧасть.ХарактерЗатрат = Перечисления.ХарактерЗатрат.ИздержкиОбращения Тогда
			  
			Область.Параметры.Аналитика1      = ТабЧасть.НомГруппа;
			Область.Параметры.ПечАналитика1   = ТабЧасть.ПечНомГруппа;
			
		КонецЕсли;
		
		СуммаИтого = СуммаИтого + ТабЧасть.Сумма;
		
		ТабДок.Вывести(Область);
		
	КонецЦикла;
	
	Область = Макет.ПолучитьОбласть("Подвал");
	Область.Параметры.СуммаИтого    = СуммаИтого;
	Область.Параметры.СуммаПрописью = ОбщегоНазначения.СформироватьСуммуПрописью(
		СуммаИтого,
		?(ТипУчета = "Упр",
			мВалютаУправленческогоУчета,
			мВалютаРегламентированногоУчета));
			
	ТабДок.Вывести(Область);
	
	Возврат ТабДок;
	
КонецФункции // ПечатьПрочиеЗатраты()
	
// Процедура осуществляет печать документа. Можно направить печать на 
// экран или принтер, а также распечатать необходимое количество копий.
//
//  Название макета печати передается в качестве параметра,
// по переданному названию находим имя макета в соответствии.
//
// Параметры:
//  НазваниеМакета - строка, название макета.
//
Процедура Печать(ИмяМакета, КоличествоЭкземпляров = 1, НаПринтер = Ложь) Экспорт

	Если ЭтоНовый() Тогда
		Предупреждение("Документ можно распечатать только после его записи");
		Возврат;
	ИначеЕсли Не УправлениеДопПравамиПользователей.РазрешитьПечатьНепроведенныхДокументов(Проведен) Тогда
		Предупреждение("Недостаточно полномочий для печати непроведенного документа!");
		Возврат;
	КонецЕсли;

	Если Не РаботаСДиалогами.ПроверитьМодифицированность(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;

	Если ИмяМакета = "ПрочиеЗатраты" ИЛИ ИмяМакета = "ПрочиеЗатратыУпр" ИЛИ ИмяМакета = "ПрочиеЗатратыБух" Тогда
		
		ТабДокумент = ПечатьПрочиеЗатраты(ИмяМакета);
		
	ИначеЕсли ТипЗнч(ИмяМакета) = Тип("ДвоичныеДанные") Тогда

		ТабДокумент = УниверсальныеМеханизмы.НапечататьВнешнююФорму(Ссылка, ИмяМакета);
		
		Если ТабДокумент = Неопределено Тогда
			Возврат
		КонецЕсли; 
		
	КонецЕсли;

	УниверсальныеМеханизмы.НапечататьДокумент(ТабДокумент, КоличествоЭкземпляров, НаПринтер, ОбщегоНазначения.СформироватьЗаголовокДокумента(ЭтотОбъект), Ссылка);

КонецПроцедуры // Печать

// Возвращает доступные варианты печати документа
//
// Возвращаемое значение:
//  Структура, каждая строка которой соответствует одному из вариантов печати
//  
Функция ПолучитьСтруктуруПечатныхФорм() Экспорт
	
	СтруктПечФорм = Новый Структура;
	Если ОтражатьВУправленческомУчете И ОтражатьВБухгалтерскомУчете Тогда
		СтруктПечФорм.Вставить( "ПрочиеЗатратыУпр",  "Прочие затраты (упр.)");
		СтруктПечФорм.Вставить( "ПрочиеЗатратыБух", "Прочие затраты (регл.)");
	Иначе
		СтруктПечФорм.Вставить( "ПрочиеЗатраты", "Прочие затраты");
	КонецЕсли;
	
	Возврат СтруктПечФорм;

КонецФункции // ПолучитьСтруктуруПечатныхФорм()

#КонецЕсли

// Процедура заполняет структуры именами реквизитов, которые имеют смысл
// только для определенного вида учета
//
Процедура ЗаполнитьСписокРеквизитовЗависимыхОтТиповУчета() Экспорт
	
	ЗаполнитьСписокРеквизитовЗависимыхОтТиповУчетаУпр();
	ЗаполнитьСписокРеквизитовЗависимыхОтТиповУчетаРегл();
	
КонецПроцедуры // ЗаполнитьСписокРеквизитовЗависимыхОтТиповУчета()

// Процедура заполняет структуры именами реквизитов, которые имеют смысл
// только для упр. учета
//
Процедура ЗаполнитьСписокРеквизитовЗависимыхОтТиповУчетаУпр()
	
	мСтрокаРеквизитыУпрУчета = "Подразделение, НадписьПодразделение, Затраты.Сумма";
	
КонецПроцедуры // ЗаполнитьСписокРеквизитовЗависимыхОтТиповУчетаУпр()

// Процедура заполняет структуры именами реквизитов, которые имеют смысл
// только для регл. учета
//
Процедура ЗаполнитьСписокРеквизитовЗависимыхОтТиповУчетаРегл()
	
	мСтрокаРеквизитыБухУчета = "ПодразделениеОрганизации, НадписьПодразделениеОрганизации,
		|Затраты.СчетЗатрат, Затраты.СуммаРегл";
	
	мСтрокаРеквизитыНалУчета = "Затраты.СчетЗатратНУ, Затраты.СуммаНал";
	
КонецПроцедуры // ЗаполнитьСписокРеквизитовЗависимыхОтТиповУчетаРегл()

// Процедура проверяет правильность заполнения реквизитов документа
//
Процедура ПроверкаРеквизитов(Отказ, Заголовок, СтруктураШапкиДокумента) Экспорт
	
	Если ОтражатьВБухгалтерскомУчете Тогда
		РеквизитыШапки = "Организация";
	Иначе
		РеквизитыШапки = "";
	КонецЕсли;
	
	ДополнитьРеквизитыШапкиУпр(РеквизитыШапки);
	ДополнитьРеквизитыШапкиРегл(РеквизитыШапки);
	
	РеквизитыТЧ = "СтатьяЗатрат";
	ДополнитьРеквизитыТабличнойЧастиУпр ( РеквизитыТЧ, СтруктураШапкиДокумента);
	ДополнитьРеквизитыТабличнойЧастиРегл( РеквизитыТЧ, СтруктураШапкиДокумента);
	
	ЗаполнениеДокументов.ПроверитьЗаполнениеШапкиДокумента(ЭтотОбъект, Новый Структура(РеквизитыШапки), Отказ, Заголовок);
	ЗаполнениеДокументов.ПроверитьЗаполнениеТабличнойЧасти(ЭтотОбъект, "Затраты", Новый Структура(РеквизитыТЧ), Отказ, Заголовок);
	
	// Проверим соответствие подразделения и организации.
	УправлениеЗатратами.ПроверитьПодразделениеОрганизации(ЭтотОбъект, Отказ, Заголовок);
	
	// Проверим наличие вида расходов в статье затрат
	СтруктураПолей = Новый Структура;
	СтруктураПолей.Вставить( "СтатьяЗатрат",   "СтатьяЗатрат");
	СтруктураПолей.Вставить( "ХарактерЗатрат", "СтатьяЗатрат.ХарактерЗатрат");
	СтруктураПолей.Вставить( "ВидЗатрат",      "СтатьяЗатрат.ВидЗатрат");
	СтруктураПолей.Вставить( "Сумма",     "Сумма");
	СтруктураПолей.Вставить( "СуммаРегл", "СуммаРегл");
	СтруктураПолей.Вставить( "СуммаНал",  "СуммаНал");
	
	ТаблицаМатериалов = УправлениеЗапасами.СформироватьЗапросПоТабличнойЧасти(ЭтотОбъект, "Затраты", СтруктураПолей).Выгрузить();
	
	//УправлениеПроизводством.ПроверитьСтатьиЗатрат(Затраты, "СтатьяЗатрат", "Нематериальные", Отказ, Заголовок);
	
	//Для Каждого СтрокаТЧ Из Затраты Цикл
	Для Каждого СтрокаТЧ Из ТаблицаМатериалов Цикл
		Если СтрокаТЧ.ХарактерЗатрат = Перечисления.ХарактерЗатрат.ПроизводственныеРасходы
		   И СтрокаТЧ.ВидЗатрат = Перечисления.ВидыЗатрат.Материальные Тогда
			ОбщегоНазначения.СообщитьОбОшибке("Нельзя использовать материальные производственные статьи затрат """ + СтрокаТЧ.СтатьяЗатрат + """ (строка № " + СтрокаТЧ.НомерСтроки + " табличной части ""Затраты"")", Отказ, Заголовок);
		КонецЕсли;
		Если НЕ ЗначениеЗаполнено(СтрокаТЧ.ХарактерЗатрат) Тогда
			ОбщегоНазначения.СообщитьОбОшибке("Не указан Хар-р затрат в статье затрат """ + СтрокаТЧ.СтатьяЗатрат + """ (строка № " + СтрокаТЧ.НомерСтроки + " табличной части ""Затраты"")", Отказ, Заголовок);
		КонецЕсли;
		Если СтруктураШапкиДокумента.ОтражатьВУправленческомУчете И СтруктураШапкиДокумента.ОтражатьВБухгалтерскомУчете Тогда
			Если СтрокаТЧ.Сумма = 0 И СтрокаТЧ.СуммаРегл = 0 Тогда
				Если СтруктураШапкиДокумента.ОтражатьВНалоговомУчете И СтрокаТЧ.СуммаНал = 0 Тогда
					ОбщегоНазначения.СообщитьОбОшибке("Не указана сумма для статьи затрат """ + СтрокаТЧ.СтатьяЗатрат + """ (строка № " + СтрокаТЧ.НомерСтроки + " табличной части ""Затраты"")", Отказ, Заголовок);
				Иначе
					ОбщегоНазначения.СообщитьОбОшибке("Не указана сумма для статьи затрат """ + СтрокаТЧ.СтатьяЗатрат + """ (строка № " + СтрокаТЧ.НомерСтроки + " табличной части ""Затраты"")", Отказ, Заголовок);
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Если СтруктураШапкиДокумента.ОтражатьВБухгалтерскомУчете Тогда
		СписСчетовБух = УправлениеПроизводством.ПолучитьЗатратныеСчета("Бух");
		Если Не СписСчетовБух.НайтиПоЗначению(Счет) = Неопределено Тогда
			ОбщегоНазначения.СообщитьОбОшибке("Нельзя указывать счет " + Счет + " в качестве корреспонденции! (счет бух. учета)", Отказ, Заголовок);
		КонецЕсли;
		Если СтруктураШапкиДокумента.ОтражатьВНалоговомУчете Тогда
			СписСчетовНал = УправлениеПроизводством.ПолучитьЗатратныеСчета("Нал");
			Если Не СписСчетовНал.НайтиПоЗначению(СчетНУ) = Неопределено Тогда
				ОбщегоНазначения.СообщитьОбОшибке("Нельзя указывать счета " + СчетНУ + " в качестве корреспонденции! (счет нал. учета)", Отказ, Заголовок);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры // ПроверкаРеквизитов()

// Процедура дополняет список реквизитов шапки упр. реквизитами
//
Процедура ДополнитьРеквизитыШапкиУпр(Реквизиты)
	
	Если ОтражатьВУправленческомУчете Тогда
		Реквизиты = Реквизиты + ?(ПустаяСтрока(Реквизиты),"",", ") 
		          + "Подразделение";
	КонецЕсли;
			  
КонецПроцедуры // ДополнитьРеквизитыШапкиУпр()

// Процедура дополняет список реквизитов шапки регл. реквизитами
//
Процедура ДополнитьРеквизитыШапкиРегл(Реквизиты)
	
	Если ОтражатьВБухгалтерскомУчете Тогда
		Реквизиты = Реквизиты + ?(ПустаяСтрока(Реквизиты),"",", ") 
		          + "ПодразделениеОрганизации, Счет";
	КонецЕсли;
	
КонецПроцедуры // ДополнитьРеквизитыШапкиРегл()

// Процедура дополняет список реквизитов табл. части упр. реквизитами
//
Процедура ДополнитьРеквизитыТабличнойЧастиУпр(Реквизиты, СтруктураШапкиДокумента)
	
	Если СтруктураШапкиДокумента.ОтражатьВУправленческомУчете
	И НЕ СтруктураШапкиДокумента.ОтражатьВБухгалтерскомУчете
	И НЕ СтруктураШапкиДокумента.ОтражатьВНалоговомУчете Тогда
		Реквизиты = Реквизиты + ?(ПустаяСтрока(Реквизиты),"",", ") + "Сумма";
	КонецЕсли;
			  
КонецПроцедуры // ДополнитьРеквизитыТабличнойЧастиУпр()

// Процедура дополняет список реквизитов табл. части регл. реквизитами
//
Процедура ДополнитьРеквизитыТабличнойЧастиРегл(Реквизиты, СтруктураШапкиДокумента)
	
	Если СтруктураШапкиДокумента.ОтражатьВБухгалтерскомУчете Тогда
		Реквизиты = Реквизиты + ?(ПустаяСтрока(Реквизиты),"",", ") + "СчетЗатрат";
	КонецЕсли;
	Если НЕ СтруктураШапкиДокумента.ОтражатьВУправленческомУчете
		  И СтруктураШапкиДокумента.ОтражатьВБухгалтерскомУчете
	   И НЕ СтруктураШапкиДокумента.ОтражатьВНалоговомУчете Тогда
		Реквизиты = Реквизиты + ?(ПустаяСтрока(Реквизиты),"",", ") + "СуммаРегл";
	КонецЕсли;
			  
	Если СтруктураШапкиДокумента.ОтражатьВНалоговомУчете Тогда
		Реквизиты = Реквизиты + ", СчетЗатратНУ";
	КонецЕсли;
			  
КонецПроцедуры // ДополнитьРеквизитыТабличнойЧастиРегл()

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ДЛЯ ОБЕСПЕЧЕНИЯ ПРОВЕДЕНИЯ ДОКУМЕНТА

// Процедура формирования движений регистров
//
Процедура ФормированиеДвижений(СтруктураШапкиДокумента)
	
	УправлениеЗатратамиДвиженияПоРегистрам.СформироватьДвиженияПоОтражениюЗатрат(
		СтруктураШапкиДокумента,
		Неопределено, // ТаблицаЗатрат,
		Неопределено // ВидОтраженияВУчете
	);
	
	ФормированиеДвиженийРегл(СтруктураШапкиДокумента);
	
КонецПроцедуры // ФормированиеДвижений()

// Процедура формирования движений регл. регистров
//
Процедура ФормированиеДвиженийРегл(СтруктураШапкиДокумента)
	
	Если СтруктураШапкиДокумента.ОтражатьВБухгалтерскомУчете Тогда
		Проводки = Движения.Хозрасчетный;
	КонецЕсли;
	
	Если СтруктураШапкиДокумента.ОтражатьВНалоговомУчете Тогда
		ПроводкиНУ = Движения.Налоговый;
	КонецЕсли;
	
	Для Каждого СтрокаТабличнойЧасти Из Затраты Цикл
		
		Если ОтражатьВБухгалтерскомУчете Тогда

			НоваяПроводка = Проводки.Добавить();
			НоваяПроводка.Активность 	= Истина;
			НоваяПроводка.Период     	= Дата;

			НоваяПроводка.Организация 	= Организация;
			НоваяПроводка.Содержание	= "Прочие затраты";
			НоваяПроводка.Сумма  		= СтрокаТабличнойЧасти.СуммаРегл;
			
			Если ВидОперации = Перечисления.ВидыОперацийПрочиеЗатраты.Отражение Тогда
				
				УправлениеЗатратами.ЗаполнитьСчетИСубконтоУчетаЗатрат(НоваяПроводка, СтрокаТабличнойЧасти, СтруктураШапкиДокумента);
				
				НоваяПроводка.СчетКт = Счет;
				БухгалтерскийУчет.УстановитьСубконто(Счет, НоваяПроводка.СубконтоКт, 1, Субконто1);
				БухгалтерскийУчет.УстановитьСубконто(Счет, НоваяПроводка.СубконтоКт, 2, Субконто2);
				БухгалтерскийУчет.УстановитьСубконто(Счет, НоваяПроводка.СубконтоКт, 3, Субконто3);

			Иначе
				НоваяПроводка.СчетДт = Счет;
				БухгалтерскийУчет.УстановитьСубконто(Счет, НоваяПроводка.СубконтоДт, 1, Субконто1);
				БухгалтерскийУчет.УстановитьСубконто(Счет, НоваяПроводка.СубконтоДт, 2, Субконто2);
				БухгалтерскийУчет.УстановитьСубконто(Счет, НоваяПроводка.СубконтоДт, 3, Субконто3);
				
				УправлениеЗатратами.ЗаполнитьСчетИСубконтоУчетаЗатрат(НоваяПроводка, СтрокаТабличнойЧасти, СтруктураШапкиДокумента, , "Кт");

			КонецЕсли;

		КонецЕсли;

		Если СтруктураШапкиДокумента.ОтражатьВНалоговомУчете Тогда

			Если СтрокаТабличнойЧасти.СуммаНал <> 0 Тогда
				
				НоваяПроводкаНУ = ПроводкиНУ.Добавить();
				НоваяПроводкаНУ.Активность 	= Истина;
				НоваяПроводкаНУ.Период 		= Дата;

				НоваяПроводкаНУ.Организация = Организация;
				НоваяПроводкаНУ.Содержание 	= "Прочие затраты";
				НоваяПроводкаНУ.Сумма 		= СтрокаТабличнойЧасти.СуммаНал;
				
				Если ВидОперации = Перечисления.ВидыОперацийПрочиеЗатраты.Отражение Тогда
					
					УправлениеЗатратами.ЗаполнитьСчетИСубконтоУчетаЗатрат(НоваяПроводкаНУ, СтрокаТабличнойЧасти, СтруктураШапкиДокумента, "Налоговый");
					
					НоваяПроводкаНУ.СчетКт = СчетНУ;
					БухгалтерскийУчет.УстановитьСубконто(СчетНУ, НоваяПроводкаНУ.СубконтоКт, 1, СубконтоНУ1);
					БухгалтерскийУчет.УстановитьСубконто(СчетНУ, НоваяПроводкаНУ.СубконтоКт, 2, СубконтоНУ2);
					БухгалтерскийУчет.УстановитьСубконто(СчетНУ, НоваяПроводкаНУ.СубконтоКт, 3, СубконтоНУ3);
					
				Иначе
					НоваяПроводкаНУ.СчетДт = СчетНУ;
					БухгалтерскийУчет.УстановитьСубконто(СчетНУ, НоваяПроводкаНУ.СубконтоДт, 1, СубконтоНУ1);
					БухгалтерскийУчет.УстановитьСубконто(СчетНУ, НоваяПроводкаНУ.СубконтоДт, 2, СубконтоНУ2);
					БухгалтерскийУчет.УстановитьСубконто(СчетНУ, НоваяПроводкаНУ.СубконтоДт, 3, СубконтоНУ3);
					
					УправлениеЗатратами.ЗаполнитьСчетИСубконтоУчетаЗатрат(НоваяПроводкаНУ, СтрокаТабличнойЧасти, СтруктураШапкиДокумента, "Налоговый", "Кт");
					
				КонецЕсли;
				
				НалоговыйУчет.ВидУчетаПоПБУ18(НоваяПроводкаНУ);
				
				// При списании затрат вид учета дебета определяется по статье затрат списываемых расходов.
				Если СтруктураШапкиДокумента.ВидОперации = Перечисления.ВидыОперацийПрочиеЗатраты.Списание
				   И НоваяПроводкаНУ.ВидУчетаКт = Перечисления.ВидыУчетаПоПБУ18.ПР
				Тогда
					НоваяПроводкаНУ.ВидУчетаДт = Перечисления.ВидыУчетаПоПБУ18.ПР;
				КонецЕсли;
				
				Если Не СтруктураШапкиДокумента.ПоддержкаПБУ18
				   И НоваяПроводкаНУ.ВидУчетаДт = Перечисления.ВидыУчетаПоПБУ18.ПР
				Тогда
					ПроводкиНУ.Удалить(НоваяПроводкаНУ);
				КонецЕсли;
				
			КонецЕсли;
			
			// Сформируем проводку по временной разнице.
			Если СтрокаТабличнойЧасти.СуммаРегл <> СтрокаТабличнойЧасти.СуммаНал И СтруктураШапкиДокумента.ПоддержкаПБУ18 Тогда
				
				НоваяПроводкаНУ = ПроводкиНУ.Добавить();
				НоваяПроводкаНУ.Активность 	= Истина;
				НоваяПроводкаНУ.Период 		= Дата;

				НоваяПроводкаНУ.Организация = Организация;
				НоваяПроводкаНУ.Содержание 	= "Прочие затраты";
				НоваяПроводкаНУ.Сумма 		= СтрокаТабличнойЧасти.СуммаРегл - СтрокаТабличнойЧасти.СуммаНал;
				
				Если ВидОперации = Перечисления.ВидыОперацийПрочиеЗатраты.Отражение Тогда
					
					УправлениеЗатратами.ЗаполнитьСчетИСубконтоУчетаЗатрат(НоваяПроводкаНУ, СтрокаТабличнойЧасти, СтруктураШапкиДокумента, "Налоговый");
					
					НоваяПроводкаНУ.СчетКт = СчетНУ;
					БухгалтерскийУчет.УстановитьСубконто(СчетНУ, НоваяПроводкаНУ.СубконтоКт, 1, СубконтоНУ1);
					БухгалтерскийУчет.УстановитьСубконто(СчетНУ, НоваяПроводкаНУ.СубконтоКт, 2, СубконтоНУ2);
					БухгалтерскийУчет.УстановитьСубконто(СчетНУ, НоваяПроводкаНУ.СубконтоКт, 3, СубконтоНУ3);
					
				Иначе
					НоваяПроводкаНУ.СчетДт = СчетНУ;
					БухгалтерскийУчет.УстановитьСубконто(СчетНУ, НоваяПроводкаНУ.СубконтоДт, 1, СубконтоНУ1);
					БухгалтерскийУчет.УстановитьСубконто(СчетНУ, НоваяПроводкаНУ.СубконтоДт, 2, СубконтоНУ2);
					БухгалтерскийУчет.УстановитьСубконто(СчетНУ, НоваяПроводкаНУ.СубконтоДт, 3, СубконтоНУ3);
					
					УправлениеЗатратами.ЗаполнитьСчетИСубконтоУчетаЗатрат(НоваяПроводкаНУ, СтрокаТабличнойЧасти, СтруктураШапкиДокумента, "Налоговый", "Кт");
					
				КонецЕсли;
				
				НоваяПроводкаНУ.ВидУчетаДт = Перечисления.ВидыУчетаПоПБУ18.ВР;
				НоваяПроводкаНУ.ВидУчетаКт = Перечисления.ВидыУчетаПоПБУ18.ВР;
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры // ФормированиеДвиженийРегл()

// Дополняет полями, нужными для упр. учета.
//
Процедура ДополнитьДеревоПолейЗапросаПоШапкеУпр(ДеревоПолейЗапросаПоШапке)

	УправлениеЗапасами.ДобавитьСтрокуВДеревоПолейЗапросаПоШапке(ДеревоПолейЗапросаПоШапке, "Константы", "ВалютаУправленческогоУчета"    , "ВалютаУправленческогоУчета");
	УправлениеЗапасами.ДобавитьСтрокуВДеревоПолейЗапросаПоШапке(ДеревоПолейЗапросаПоШапке, "Константы", "КурсВалютыУправленческогоУчета", "КурсВалютыУправленческогоУчета");
	
КонецПроцедуры // ДополнитьДеревоПолейЗапросаПоШапкеУпр()

// Дополняет полями, нужными для регл. учета.
//
Процедура ДополнитьДеревоПолейЗапросаПоШапкеРегл(ДеревоПолейЗапросаПоШапке)

КонецПроцедуры // ДополнитьДеревоПолейЗапросаПоШапкеРегл()

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

// Процедура - обработчик события "ОбработкаПроведения"
//
Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	Если мУдалятьДвижения Тогда
		ОбщегоНазначения.УдалитьДвиженияРегистратора(ЭтотОбъект, Отказ);
	КонецЕсли;
	
	// Сформируем структуру реквизитов шапки документа
	СтруктураШапкиДокумента = ОбщегоНазначения.СформироватьСтруктуруШапкиДокумента(ЭтотОбъект);
	
	// Дополним полями, нужными для регл. и упр. учета
	ДеревоПолейЗапросаПоШапке = УправлениеЗапасами.СформироватьДеревоПолейЗапросаПоШапке();
	ДополнитьДеревоПолейЗапросаПоШапкеУпр(ДеревоПолейЗапросаПоШапке);
	ДополнитьДеревоПолейЗапросаПоШапкеРегл(ДеревоПолейЗапросаПоШапке);
	
	УправлениеЗапасами.ДобавитьСтрокуВДеревоПолейЗапросаПоШапке(ДеревоПолейЗапросаПоШапке, "УчетнаяПолитика", "ВедениеУчетаЗатратПоПроектам", "ВедениеУчетаЗатратПоПроектам");
	
	// Сформируем запрос на дополнительные параметры, нужные при проведении, по данным шапки документа
	СтруктураШапкиДокумента = УправлениеЗапасами.СформироватьЗапросПоДеревуПолей(ЭтотОбъект, ДеревоПолейЗапросаПоШапке, СтруктураШапкиДокумента, мВалютаРегламентированногоУчета);
	
	// Заголовок для сообщений об ошибках проведения.
	Заголовок = ОбщегоНазначения.ПредставлениеДокументаПриПроведении(Ссылка);
	
	// Документ должен принадлежать хотя бы к одному виду учета (управленческий, бухгалтерский, налоговый)
	ОбщегоНазначения.ПроверитьПринадлежностьКВидамУчета(СтруктураШапкиДокумента, Отказ, Заголовок);
	
	ПроверкаРеквизитов(Отказ, Заголовок, СтруктураШапкиДокумента);
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	// Формирование движений регистров, бухгалтерских и налоговых проводок.
	ФормированиеДвижений(СтруктураШапкиДокумента);
	
КонецПроцедуры	// ОбработкаПроведения()

// Процедура - обработчик события "ПередЗаписью".
//
Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ мУказаниеПроектовВТабличнойЧастиДокументов Тогда
		УправлениеПроектами.ЗаполнитьПроектВСтрокахТабЧасти(ЭтотОбъект, Затраты);
	КонецЕсли;
	
	мУдалятьДвижения = НЕ ЭтоНовый();
	
КонецПроцедуры // ПередЗаписью

мВалютаРегламентированногоУчета = глЗначениеПеременной("ВалютаРегламентированногоУчета");
мВалютаУправленческогоУчета     = глЗначениеПеременной("ВалютаУправленческогоУчета");

мУказаниеПроектовВТабличнойЧастиДокументов = УправлениеПроектами.УказаниеПроектовВТабличнойЧастиДокументов();