////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ МОДУЛЯ

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

#Если ТолстыйКлиентОбычноеПриложение Тогда

// Процедура осуществляет печать документа. Можно направить печать на 
// экран или принтер, а также распечатать необходимое количество копий.
//
// Название макета печати передается в качестве параметра,
// по переданному названию находим имя макета в соответствии.
//
// Параметры:
//	НазваниеМакета	- строка, название макета.
//
Функция Печать(ИмяМакета, КоличествоЭкземпляров = 1, НаПринтер = Ложь) Экспорт
	
	Если Не РаботаСДиалогами.ПроверитьМодифицированность(ЭтотОбъект) Тогда
		Возврат Неопределено;
	КонецЕсли;

	// Получить экземпляр документа на печать
	Если ИмяМакета = "Справка" Или ИмяМакета = "СправкаРасширенная" Тогда
		ТабДокумент = ПечатьСправки(ИмяМакета);
		Возврат УниверсальныеМеханизмы.НапечататьДокумент(ТабДокумент, КоличествоЭкземпляров, НаПринтер, ОбщегоНазначения.СформироватьЗаголовокДокумента(ЭтотОбъект,Метаданные().Синоним) + " (расчет среднего заработка)",,ТабДокумент);
	КонецЕсли;
	
КонецФункции // Печать()

#КонецЕсли

// Возвращает доступные варианты печати документа
//
// Возвращаемое значение:
//	Структура, каждая строка которой соответствует одному из вариантов печати
//
Функция ПолучитьСтруктуруПечатныхФорм() Экспорт
	
	СтруктураПечатныхФорм = Новый Структура;
	СтруктураПечатныхФорм.Вставить("Справка", "Справка (утвержденная форма)");
	СтруктураПечатныхФорм.Вставить("СправкаРасширенная", "Справка (расширенная форма)");
	
	Возврат СтруктураПечатныхФорм;
	
КонецФункции // ПолучитьСтруктуруПечатныхФорм()

Процедура ЗаполнитьДанныеСтрахователя()

	ЗаполнитьЗначенияСвойств(ЭтотОбъект, ОбщегоНазначения.ПолучитьЗначенияРеквизитов(Организация,"РегистрационныйНомерФСС, ДополнительныйКодФСС, КодПодчиненностиФСС, НаименованиеТерриториальногоОрганаФСС"));
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ОтветственныеЛицаСрезПоследних.Должность КАК ДолжностьРуководителя,
	|	ОтветственныеЛицаСрезПоследних.ОтветственноеЛицо,
	|	ОтветственныеЛицаСрезПоследних.ФизическоеЛицо,
	|	ФизическиеЛица.Комментарий
	|ИЗ
	|	РегистрСведений.ОтветственныеЛицаОрганизаций.СрезПоследних(
	|			&Дата,
	|			ОтветственноеЛицо В (ЗНАЧЕНИЕ(Перечисление.ОтветственныеЛицаОрганизаций.ГлавныйБухгалтер), ЗНАЧЕНИЕ(Перечисление.ОтветственныеЛицаОрганизаций.Руководитель))
	|				И СтруктурнаяЕдиница = &Организация) КАК ОтветственныеЛицаСрезПоследних
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ФизическиеЛица КАК ФизическиеЛица
	|		ПО ОтветственныеЛицаСрезПоследних.ФизическоеЛицо = ФизическиеЛица.Ссылка";
	
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("Дата", Дата);
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
	
		Если Выборка.ОтветственноеЛицо = Перечисления.ОтветственныеЛицаОрганизаций.ГлавныйБухгалтер Тогда
			ГлавныйБухгалтер = Выборка.ФизическоеЛицо
		ИначеЕсли Выборка.ОтветственноеЛицо = Перечисления.ОтветственныеЛицаОрганизаций.Руководитель Тогда
			Руководитель = Выборка.ФизическоеЛицо;
			ДолжностьРуководителя = Выборка.ДолжностьРуководителя;
		КонецЕсли;
	
	КонецЦикла;

	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ВЫРАЗИТЬ(КонтактнаяИнформация.Представление КАК СТРОКА(300)) КАК ТелефонОрганизации
	|ИЗ
	|	РегистрСведений.КонтактнаяИнформация КАК КонтактнаяИнформация
	|ГДЕ
	|	КонтактнаяИнформация.Объект = &Организация
	|	И КонтактнаяИнформация.Тип = ЗНАЧЕНИЕ(Перечисление.ТипыКонтактнойИнформации.Телефон)
	|	И КонтактнаяИнформация.Вид = ЗНАЧЕНИЕ(Справочник.ВидыКонтактнойИнформации.ТелефонОрганизации)";
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		ТелефонОрганизации = СокрЛП(Выборка.ТелефонОрганизации);
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьДанныеСотрудника()

	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СотрудникиОрганизаций.Физлицо,
	|	СотрудникиОрганизаций.ДатаПриемаНаРаботу,
	|	СотрудникиОрганизаций.ДатаУвольнения
	|ПОМЕСТИТЬ ВТФизлицо
	|ИЗ
	|	Справочник.СотрудникиОрганизаций КАК СотрудникиОрганизаций
	|ГДЕ
	|	СотрудникиОрганизаций.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ФизическиеЛица.СтраховойНомерПФР,
	|	ФизическиеЛица.Наименование,
	|	ФИОФизЛицСрезПоследних.Фамилия,
	|	ФИОФизЛицСрезПоследних.Имя,
	|	ФИОФизЛицСрезПоследних.Отчество,
	|	ВЫБОР
	|		КОГДА АдресаФактические.Поле1 ЕСТЬ NULL 
	|			ТОГДА "","" + АдресаРегистрации.Поле1 + "","" + АдресаРегистрации.Поле2 + "","" + АдресаРегистрации.Поле3 + "","" + АдресаРегистрации.Поле4 + "","" + АдресаРегистрации.Поле5 + "","" + АдресаРегистрации.Поле6 + "","" + АдресаРегистрации.Поле7 + "","" + АдресаРегистрации.Поле8 + "","" + АдресаРегистрации.Поле9 + "","" + ВЫБОР
	|					КОГДА АдресаРегистрации.ТипДома В (ЗНАЧЕНИЕ(Перечисление.ТипыДомов.Дом), ЗНАЧЕНИЕ(Перечисление.ТипыДомов.ПустаяСсылка))
	|						ТОГДА ""дом""
	|					ИНАЧЕ ""владение""
	|				КОНЕЦ + "","" + ВЫБОР
	|					КОГДА АдресаРегистрации.ТипКорпуса В (ЗНАЧЕНИЕ(Перечисление.ТипыКорпусов.Корпус), ЗНАЧЕНИЕ(Перечисление.ТипыКорпусов.ПустаяСсылка))
	|						ТОГДА ""корпус""
	|					ИНАЧЕ ""строение""
	|				КОНЕЦ + "","" + ВЫБОР
	|					КОГДА АдресаРегистрации.ТипКвартиры В (ЗНАЧЕНИЕ(Перечисление.ТипыКвартир.Квартира), ЗНАЧЕНИЕ(Перечисление.ТипыКвартир.ПустаяСсылка))
	|						ТОГДА ""кв.""
	|					ИНАЧЕ ""оф.""
	|				КОНЕЦ
	|		ИНАЧЕ "","" + АдресаФактические.Поле1 + "","" + АдресаФактические.Поле2 + "","" + АдресаФактические.Поле3 + "","" + АдресаФактические.Поле4 + "","" + АдресаФактические.Поле5 + "","" + АдресаФактические.Поле6 + "","" + АдресаФактические.Поле7 + "","" + АдресаФактические.Поле8 + "","" + АдресаФактические.Поле9 + "","" + ВЫБОР
	|				КОГДА АдресаФактические.ТипДома В (ЗНАЧЕНИЕ(Перечисление.ТипыДомов.Дом), ЗНАЧЕНИЕ(Перечисление.ТипыДомов.ПустаяСсылка))
	|					ТОГДА ""дом""
	|				ИНАЧЕ ""владение""
	|			КОНЕЦ + "","" + ВЫБОР
	|				КОГДА АдресаФактические.ТипКорпуса В (ЗНАЧЕНИЕ(Перечисление.ТипыКорпусов.Корпус), ЗНАЧЕНИЕ(Перечисление.ТипыКорпусов.ПустаяСсылка))
	|					ТОГДА ""корпус""
	|				ИНАЧЕ ""строение""
	|			КОНЕЦ + "","" + ВЫБОР
	|				КОГДА АдресаФактические.ТипКвартиры В (ЗНАЧЕНИЕ(Перечисление.ТипыКвартир.Квартира), ЗНАЧЕНИЕ(Перечисление.ТипыКвартир.ПустаяСсылка))
	|					ТОГДА ""кв.""
	|				ИНАЧЕ ""оф.""
	|			КОНЕЦ
	|	КОНЕЦ КАК Адрес,
	|	ПаспортныеДанныеФизЛицСрезПоследних.ДокументВид КАК ВидДокумента,
	|	ПаспортныеДанныеФизЛицСрезПоследних.ДокументСерия КАК СерияДокумента,
	|	ПаспортныеДанныеФизЛицСрезПоследних.ДокументНомер КАК НомерДокумента,
	|	ПаспортныеДанныеФизЛицСрезПоследних.ДокументДатаВыдачи КАК ДатаВыдачиДокумента,
	|	ПаспортныеДанныеФизЛицСрезПоследних.ДокументКемВыдан КАК КемВыданДокумент,
	|	Физлица.ДатаПриемаНаРаботу КАК ПериодРаботыС,
	|	ВЫБОР
	|		КОГДА Физлица.ДатаПриемаНаРаботу = ДАТАВРЕМЯ(1, 1, 1)
	|				И Физлица.ДатаУвольнения = ДАТАВРЕМЯ(1, 1, 1)
	|			ТОГДА ДАТАВРЕМЯ(1, 1, 1)
	|		КОГДА Физлица.ДатаУвольнения = ДАТАВРЕМЯ(1, 1, 1)
	|			ТОГДА &Дата
	|		ИНАЧЕ Физлица.ДатаУвольнения
	|	КОНЕЦ КАК ПериодРаботыПо
	|ИЗ
	|	ВТФизлицо КАК Физлица
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ФизическиеЛица КАК ФизическиеЛица
	|		ПО Физлица.Физлицо = ФизическиеЛица.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КонтактнаяИнформация КАК АдресаФактические
	|		ПО Физлица.Физлицо = АдресаФактические.Объект
	|			И (АдресаФактические.Вид = ЗНАЧЕНИЕ(Справочник.ВидыКонтактнойИнформации.ФактАдресФизЛица))
	|			И (АдресаФактические.Тип = ЗНАЧЕНИЕ(Перечисление.ТипыКонтактнойИнформации.Адрес))
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КонтактнаяИнформация КАК АдресаРегистрации
	|		ПО Физлица.Физлицо = АдресаРегистрации.Объект
	|			И (АдресаРегистрации.Вид = ЗНАЧЕНИЕ(Справочник.ВидыКонтактнойИнформации.ЮрАдресФизЛица))
	|			И (АдресаРегистрации.Тип = ЗНАЧЕНИЕ(Перечисление.ТипыКонтактнойИнформации.Адрес))
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПаспортныеДанныеФизЛиц.СрезПоследних(
	|				&Дата,
	|				Физлицо В
	|					(ВЫБРАТЬ
	|						Физлица.Физлицо
	|					ИЗ
	|						ВТФизлицо КАК Физлица)) КАК ПаспортныеДанныеФизЛицСрезПоследних
	|		ПО Физлица.Физлицо = ПаспортныеДанныеФизЛицСрезПоследних.ФизЛицо
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ФИОФизЛиц.СрезПоследних(
	|				&Дата,
	|				Физлицо В
	|					(ВЫБРАТЬ
	|						Физлица.Физлицо
	|					ИЗ
	|						ВТФизлицо КАК Физлица)) КАК ФИОФизЛицСрезПоследних
	|		ПО Физлица.Физлицо = ФИОФизЛицСрезПоследних.ФизЛицо";
	Запрос.УстановитьПараметр("Ссылка", Сотрудник);
	Запрос.УстановитьПараметр("Дата", Дата);
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		ЗаполнитьЗначенияСвойств(ЭтотОбъект,Выборка);
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьДанныеОЗаработке()

	Если ГодС = 0 И ГодПо = 0 Тогда
		Возврат
	КонецЕсли;	

	Если Сотрудник.Пустая() Или Организация.Пустая() Тогда
		Возврат
	КонецЕсли;
	
	МассивЛет = Новый Массив;
	Если ГодС = 0 Тогда
		МассивЛет.Добавить(ГодПо);
	ИначеЕсли ГодПо = 0 Тогда 	
		МассивЛет.Добавить(ГодС);
	Иначе 	
		ГодМежду = Мин(ГодС, ГодПо);
		Пока ГодМежду <= Макс(ГодС, ГодПо) Цикл
			МассивЛет.Добавить(ГодМежду);
			ГодМежду = ГодМежду + 1;	
		КонецЦикла;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("ДанныеОЗаработке",	ПроведениеРасчетов.ЗаработокДляВыплатыПособийСоцСтрахованияС2011года(МассивЛет, ОбщегоНазначения.ПолучитьЗначениеРеквизита(Сотрудник,"Физлицо"), Организация, Ложь, Истина));
	Запрос.УстановитьПараметр("ДатаЗакона213ФЗ",	ПроведениеРасчетов.ДатаЗаменыЕСНСтраховымиВзносами());
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ДанныеОЗаработке.РасчетныйГод,
	|	ВЫБОР
	|		КОГДА НАЧАЛОПЕРИОДА(ДанныеОЗаработке.БазовыйПериодНачало, ГОД) < &ДатаЗакона213ФЗ
	|			ТОГДА &ДатаЗакона213ФЗ
	|		ИНАЧЕ НАЧАЛОПЕРИОДА(ДанныеОЗаработке.БазовыйПериодНачало, ГОД)
	|	КОНЕЦ КАК БазовыйПериодНачало,
	|	ДанныеОЗаработке.ДнейБолезниУходаЗаДетьми,
	|	ДанныеОЗаработке.Заработок
	|ПОМЕСТИТЬ ВТДанныеОЗаработке
	|ИЗ
	|	&ДанныеОЗаработке КАК ДанныеОЗаработке";
	Запрос.Выполнить();
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ДатыРегистра.РасчетныйГод,
	|	ВЫБОР
	|		КОГДА ДатыРегистра.Заработок >= ПредельнаяВеличинаБазыСтраховыхВзносов.Размер
	|			ТОГДА ПредельнаяВеличинаБазыСтраховыхВзносов.Размер
	|		ИНАЧЕ ДатыРегистра.Заработок
	|	КОНЕЦ КАК Заработок,
	|	ДатыРегистра.ДнейБолезниУходаЗаДетьми
	|ИЗ
	|	(ВЫБРАТЬ
	|		МАКСИМУМ(ПредельнаяВеличинаБазыСтраховыхВзносов.Период) КАК Период,
	|		ДанныеОЗаработке.ДнейБолезниУходаЗаДетьми КАК ДнейБолезниУходаЗаДетьми,
	|		ДанныеОЗаработке.РасчетныйГод КАК РасчетныйГод,
	|		ДанныеОЗаработке.Заработок КАК Заработок
	|	ИЗ
	|		ВТДанныеОЗаработке КАК ДанныеОЗаработке
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПредельнаяВеличинаБазыСтраховыхВзносов КАК ПредельнаяВеличинаБазыСтраховыхВзносов
	|			ПО ДанныеОЗаработке.БазовыйПериодНачало >= ПредельнаяВеличинаБазыСтраховыхВзносов.Период
	|	
	|	СГРУППИРОВАТЬ ПО
	|		ДанныеОЗаработке.РасчетныйГод,
	|		ДанныеОЗаработке.ДнейБолезниУходаЗаДетьми,
	|		ДанныеОЗаработке.Заработок) КАК ДатыРегистра
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПредельнаяВеличинаБазыСтраховыхВзносов КАК ПредельнаяВеличинаБазыСтраховыхВзносов
	|		ПО ДатыРегистра.Период = ПредельнаяВеличинаБазыСтраховыхВзносов.Период";
	
	ДанныеОЗаработке.Загрузить(Запрос.Выполнить().Выгрузить());
	
КонецПроцедуры

Процедура Автозаполнение(Режим = Неопределено) Экспорт
	
	Если Режим = "ДанныеОЗаработке" Тогда
		ЗаполнитьДанныеОЗаработке()
	ИначеЕсли Режим = "ДанныеСотрудника" Тогда
		ЗаполнитьДанныеСотрудника()
	ИначеЕсли Режим = "ДанныеСтрахователя" Тогда
		ЗаполнитьДанныеСтрахователя()
	Иначе	
		ЗаполнитьДанныеОЗаработке();
		ЗаполнитьДанныеСтрахователя();
		ЗаполнитьДанныеСотрудника();
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьРеквизитыНовогоДокумента() Экспорт
	
	// Заполнить реквизиты значениями по умолчанию.
	ЗаполнениеДокументовПереопределяемый.ЗаполнитьШапкуДокумента(ЭтотОбъект, глЗначениеПеременной("глТекущийПользователь"));
	
	Если ГодС = 0 и ГодПо = 0 Тогда
		ГодС = Год(Дата) - 2;
		ГодПо = Год(Дата);
		Если ЗначениеЗаполнено(Организация) Тогда
			Автозаполнение("ДанныеСтрахователя");
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры
	
////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОБЩЕГО НАЗНАЧЕНИЯ

#Если ТолстыйКлиентОбычноеПриложение Тогда

// Функция формирует табличный документ с печатной формой справки
//
// Возвращаемое значение:
//	Табличный документ - печатная форма
//
Функция ПечатьСправки(ИмяМакета)
	
	ОбработкаКомментариев = глЗначениеПеременной("глОбработкаСообщений");
	ОбработкаКомментариев.УдалитьСообщения();
	
	// Заголовок для сообщений об ошибках проведения.
	Заголовок = ОбщегоНазначения.ПредставлениеДокументаПриПроведении(Ссылка);
	
	// получаем данные для печати
	ВыборкаДляШапкиИПодвала = СформироватьЗапросДляПечати().Выбрать();
	ВыборкаСтрок = СформироватьЗапросПоДанныеОЗаработке().Выбрать();
	
	Отказ = Ложь;
	
	ТабДокумент = Новый ТабличныйДокумент;
	ТабДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_СправкаОЗаработкеДляРасчетаПособий";
	ТабДокумент.ПолеСлева = 0;
	ТабДокумент.ПолеСправа = 0;
	
	// запоминаем области макета
	Макет = ПолучитьМакет(ИмяМакета);
	ОбластьМакетаШапка	= Макет.ПолучитьОбласть("Шапка"); // Шапка документа.
	ОбластьМакетаПодвал	= Макет.ПолучитьОбласть("Подвал");// Подвал документа
	ОбластьМакета 		= Макет.ПолучитьОбласть("Год"); // область календарного года
	ПустаяОбластьМакета = Макет.ПолучитьОбласть("ПустойГод"); // область календарного года
	
	Если ВыборкаДляШапкиИПодвала.Следующий() Тогда 
		
		ПроверитьЗаполнениеШапки(ВыборкаДляШапкиИПодвала, Отказ, Заголовок);
		
		ОбластьМакетаШапка.Параметры.Заполнить(ВыборкаДляШапкиИПодвала); // Шапка документа.
		ОбластьМакетаШапка.Параметры.НазваниеОрганизации = СокрЛП(ОбластьМакетаШапка.Параметры.НазваниеОрганизации);
		ОбластьМакетаШапка.Параметры.НаименованиеТерриториальногоОрганаФСС = СокрЛП(ОбластьМакетаШапка.Параметры.НаименованиеТерриториальногоОрганаФСС);
		ОбластьМакетаШапка.Параметры.КемВыданДокумент = СокрЛП(ОбластьМакетаШапка.Параметры.КемВыданДокумент);
		ОбластьМакетаШапка.Параметры.ДатаВыдачиДокумента = Формат(ОбластьМакетаШапка.Параметры.ДатаВыдачиДокумента,"ДЛФ=DD");
		ДанныеАдреса = УправлениеКонтактнойИнформацией.ПолучитьСтруктуруАдресаИзСтроки(ВыборкаДляШапкиИПодвала.Адрес);
		Если ДанныеАдреса <> Неопределено Тогда
			ЗаполнитьЗначенияСвойств(ОбластьМакетаШапка.Параметры,ДанныеАдреса);
		КонецЕсли;
		
		ОбластьМакетаПодвал.Параметры.Заполнить(ВыборкаДляШапкиИПодвала); // Для подвала
		
	КонецЕсли;
	
	// Начинаем формировать выходной документ
	ТабДокумент.Вывести(ОбластьМакетаШапка); // Шапка документа.
	
	Валюта = Константы.ВалютаРегламентированногоУчета.Получить();
	ВыведеноСтрок = 0;
	// выводим строки по годам
	Пока ВыборкаСтрок.Следующий() Цикл
		
		ПроверитьЗаполнениеСтрокиДанныеОЗаработке(ВыборкаСтрок, Отказ, Заголовок);
		
		ОбластьМакета.Параметры.РасчетныйГод = ВыборкаСтрок.РасчетныйГод;
		ОбластьМакета.Параметры.Заработок = Формат(Цел(ВыборкаСтрок.Заработок),"ЧДЦ=0; ЧН=-");
		ОбластьМакета.Параметры.ЗаработокКоп = Формат((ВыборкаСтрок.Заработок - Цел(ВыборкаСтрок.Заработок)) * 100,"ЧЦ=2; ЧДЦ=0; ЧН=-");
		ОбластьМакета.Параметры.ЗаработокПрописью = ОбщегоНазначения.СформироватьСуммуПрописью(ВыборкаСтрок.Заработок, Валюта);

		ТабДокумент.Вывести(ОбластьМакета);
		
		ВыведеноСтрок = ВыведеноСтрок + 1;
		
	КонецЦикла;
	
	Если НЕ Отказ Тогда
		
		Пока ВыведеноСтрок < 3 Цикл
			ТабДокумент.Вывести(ПустаяОбластьМакета);
			ВыведеноСтрок = ВыведеноСтрок + 1;
		КонецЦикла;
		
		Если ИмяМакета = "СправкаРасширенная" Тогда
			ТабДокумент.Вывести(Макет.ПолучитьОбласть("Пункт4"));
			ОбластьМакета 		= Макет.ПолучитьОбласть("ГодДни"); // область календарного года
			ПустаяОбластьМакета = Макет.ПолучитьОбласть("ПустойГодДни"); // область календарного года
			ВыборкаСтрок.Сбросить();
			ВыведеноСтрок = 0;
			// выводим строки по годам
			Пока ВыборкаСтрок.Следующий() Цикл
				Если ЗначениеЗаполнено(ВыборкаСтрок.ДнейБолезниУходаЗаДетьми) Тогда
					ОбластьМакета.Параметры.Заполнить(ВыборкаСтрок);
					ТабДокумент.Вывести(ОбластьМакета);
					ВыведеноСтрок = ВыведеноСтрок + 1;
				КонецЕсли;
			КонецЦикла;
			Пока ВыведеноСтрок < 3 Цикл
				ТабДокумент.Вывести(ПустаяОбластьМакета);
				ВыведеноСтрок = ВыведеноСтрок + 1;
			КонецЦикла;
		КонецЕсли;
		
		// выводим предварительно подготовленный Подвал документа.
		ТабДокумент.Вывести(ОбластьМакетаПодвал);
		
		Возврат ТабДокумент;
		
	КонецЕсли;
	
	ОбработкаКомментариев.ПоказатьСообщения();
	
	Возврат Неопределено;
	
КонецФункции // ПечатьСправки()

// Формирует запрос по шапке документа
//
//
// Возвращаемое значение:
//	Результат запроса
//
Функция СформироватьЗапросДляПечати()

	Запрос = Новый Запрос;

	// Установим параметры запроса
	Запрос.УстановитьПараметр("ДокументСсылка", Ссылка);
	Запрос.УстановитьПараметр("Дата", Дата);
	Запрос.УстановитьПараметр("ЭтоЮрЛицо", ОбщегоНазначенияПереопределяемый.ЭтоЮрЛицо(Организация));
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ДокументСправка.Дата,
	|	ДокументСправка.Номер,
	|	ДокументСправка.Организация,
	|	ДокументСправка.Ссылка,
	|	ДокументСправка.Сотрудник,
	|	ДокументСправка.Руководитель,
	|	ДокументСправка.ДолжностьРуководителя,
	|	ДокументСправка.ГлавныйБухгалтер,
	|	ДокументСправка.ТелефонОрганизации,
	|	ДокументСправка.РегистрационныйНомерФСС,
	|	ДокументСправка.ДополнительныйКодФСС,
	|	ДокументСправка.КодПодчиненностиФСС,
	|	ДокументСправка.НаименованиеТерриториальногоОрганаФСС,
	|	ДокументСправка.ПериодРаботыС,
	|	ДокументСправка.ПериодРаботыПо,
	|	ДокументСправка.СтраховойНомерПФР,
	|	ДокументСправка.Фамилия,
	|	ДокументСправка.Имя,
	|	ДокументСправка.Отчество,
	|	ДокументСправка.Адрес,
	|	ДокументСправка.ВидДокумента,
	|	ДокументСправка.СерияДокумента,
	|	ДокументСправка.НомерДокумента,
	|	ДокументСправка.ДатаВыдачиДокумента,
	|	ДокументСправка.КемВыданДокумент,
	|	ВЫБОР
	|		КОГДА ДокументСправка.Сотрудник.Организация = ВЫБОР
	|				КОГДА ДокументСправка.Организация.ГоловнаяОрганизация = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|					ТОГДА ДокументСправка.Организация
	|				ИНАЧЕ ДокументСправка.Организация.ГоловнаяОрганизация
	|			КОНЕЦ
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК ОшибкаНеСоответствиеСотрудникаИОрганизации,
	|	ВЫБОР
	|		КОГДА ДокументСправка.Сотрудник.ВидЗанятости В (ЗНАЧЕНИЕ(Перечисление.ВидыЗанятостиВОрганизации.ВнутреннееСовместительство), ЗНАЧЕНИЕ(Перечисление.ВидыЗанятостиВОрганизации.ПустаяСсылка))
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ОшибкаВидаЗанятости
	|ПОМЕСТИТЬ ВТДанныеДокумента
	|ИЗ
	|	Документ.СправкаОЗаработкеДляРасчетаПособий КАК ДокументСправка
	|ГДЕ
	|	ДокументСправка.Ссылка = &ДокументСсылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ДанныеДокумента.Фамилия + "" "" + ДанныеДокумента.Имя + "" "" + ДанныеДокумента.Отчество КАК ФИО,
	|	ДанныеДокумента.Организация.НаименованиеПолное КАК НазваниеОрганизации,
	|	ДанныеДокумента.Дата,
	|	ДанныеДокумента.Номер,
	|	ДанныеДокумента.Организация,
	|	ДанныеДокумента.Сотрудник,
	|	ДанныеДокумента.Организация.ИНН КАК ИНН,
	|	ДанныеДокумента.Организация.КПП КАК КПП,
	|	ДанныеДокумента.РегистрационныйНомерФСС,
	|	ДанныеДокумента.ДополнительныйКодФСС,
	|	ДанныеДокумента.КодПодчиненностиФСС,
	|	ДанныеДокумента.НаименованиеТерриториальногоОрганаФСС,
	|	ДанныеДокумента.ПериодРаботыС,
	|	ДанныеДокумента.ПериодРаботыПо,
	|	ДанныеДокумента.СтраховойНомерПФР,
	|	ДанныеДокумента.Адрес,
	|	ВЫБОР
	|		КОГДА &ЭтоЮрЛицо
	|			ТОГДА АдресЮрЛица.Представление
	|		КОГДА АдресаФактические.Поле1 ЕСТЬ NULL 
	|			ТОГДА АдресаРегистрации.Представление
	|		ИНАЧЕ АдресаФактические.Представление
	|	КОНЕЦ КАК АдресСтрахователя,
	|	ДанныеДокумента.ВидДокумента.Наименование КАК ВидДокумента,
	|	ДанныеДокумента.СерияДокумента,
	|	ДанныеДокумента.НомерДокумента,
	|	ДанныеДокумента.ДатаВыдачиДокумента,
	|	ДанныеДокумента.КемВыданДокумент,
	|	ЕСТЬNULL(ФИОРуководителя.Фамилия + "" "" + ФИОРуководителя.Имя + "" "" + ФИОРуководителя.Отчество, ДанныеДокумента.Руководитель.Наименование) КАК ФИОРуководителя,
	|	ДанныеДокумента.ДолжностьРуководителя.Наименование КАК Должность,
	|	ЕСТЬNULL(ФИОГлБухгалтера.Фамилия + "" "" + ФИОГлБухгалтера.Имя + "" "" + ФИОГлБухгалтера.Отчество, ДанныеДокумента.ГлавныйБухгалтер.Наименование) КАК ФИОГлБухгалтера,
	|	ДанныеДокумента.ТелефонОрганизации КАК Телефон,
	|	ДанныеДокумента.ОшибкаВидаЗанятости,
	|	ДанныеДокумента.ОшибкаНеСоответствиеСотрудникаИОрганизации
	|ИЗ
	|	ВТДанныеДокумента КАК ДанныеДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ФИОФизЛиц.СрезПоследних(
	|				&Дата,
	|				ФизЛицо В
	|					(ВЫБРАТЬ
	|						ДанныеДокумента.Руководитель
	|					ИЗ
	|						ВТДанныеДокумента КАК ДанныеДокумента)) КАК ФИОРуководителя
	|		ПО ДанныеДокумента.Руководитель = ФИОРуководителя.ФизЛицо
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КонтактнаяИнформация КАК АдресаФактические
	|		ПО ДанныеДокумента.Организация.ИндивидуальныйПредприниматель = АдресаФактические.Объект
	|			И (АдресаФактические.Вид = ЗНАЧЕНИЕ(Справочник.ВидыКонтактнойИнформации.ФактАдресФизЛица))
	|			И (АдресаФактические.Тип = ЗНАЧЕНИЕ(Перечисление.ТипыКонтактнойИнформации.Адрес))
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КонтактнаяИнформация КАК АдресаРегистрации
	|		ПО ДанныеДокумента.Организация.ИндивидуальныйПредприниматель = АдресаРегистрации.Объект
	|			И (АдресаРегистрации.Вид = ЗНАЧЕНИЕ(Справочник.ВидыКонтактнойИнформации.ЮрАдресФизЛица))
	|			И (АдресаРегистрации.Тип = ЗНАЧЕНИЕ(Перечисление.ТипыКонтактнойИнформации.Адрес))
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КонтактнаяИнформация КАК АдресЮрЛица
	|		ПО ДанныеДокумента.Организация = АдресЮрЛица.Объект
	|			И (АдресЮрЛица.Вид = ЗНАЧЕНИЕ(Справочник.ВидыКонтактнойИнформации.ЮрАдресОрганизации))
	|			И (АдресЮрЛица.Тип = ЗНАЧЕНИЕ(Перечисление.ТипыКонтактнойИнформации.Адрес))
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ФИОФизЛиц.СрезПоследних(
	|				&Дата,
	|				ФизЛицо В
	|					(ВЫБРАТЬ
	|						ДанныеДокумента.ГлавныйБухгалтер
	|					ИЗ
	|						ВТДанныеДокумента КАК ДанныеДокумента)) КАК ФИОГлБухгалтера
	|		ПО ДанныеДокумента.ГлавныйБухгалтер = ФИОГлБухгалтера.ФизЛицо";

	Возврат Запрос.Выполнить();

КонецФункции // СформироватьЗапросПоШапке()

// Формирует запрос по табличной части Начисления
//
// Возвращаемое значение:
//	Результат запроса
//
Функция СформироватьЗапросПоДанныеОЗаработке() 

	Запрос = Новый Запрос;

	// Установим параметры запроса
	Запрос.УстановитьПараметр("ДокументСсылка" , Ссылка);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Документ.РасчетныйГод КАК РасчетныйГод,
	|	Документ.Заработок КАК Заработок,
	|	Документ.НомерСтроки,
	|	Документ.ДнейБолезниУходаЗаДетьми
	|ПОМЕСТИТЬ ВТСтрокиДокумента
	|ИЗ
	|	Документ.СправкаОЗаработкеДляРасчетаПособий.ДанныеОЗаработке КАК Документ
	|ГДЕ
	|	Документ.Ссылка = &ДокументСсылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Документ.РасчетныйГод КАК РасчетныйГод,
	|	Документ.Заработок КАК Заработок,
	|	Документ.НомерСтроки,
	|	ПовторныеСтроки.НомерСтроки КАК НомерПовторнойСтроки,
	|	Документ.ДнейБолезниУходаЗаДетьми
	|ИЗ
	|	ВТСтрокиДокумента КАК Документ
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСтрокиДокумента КАК ПовторныеСтроки
	|		ПО Документ.РасчетныйГод = ПовторныеСтроки.РасчетныйГод
	|			И Документ.НомерСтроки < ПовторныеСтроки.НомерСтроки
	|
	|УПОРЯДОЧИТЬ ПО
	|	РасчетныйГод";

	Возврат Запрос.Выполнить();

КонецФункции // СформироватьЗапросПоНачислениям()

// Проверяет правильность заполнения шапки документа.
// Если какой-то из реквизитов шапки, влияющий на проведение, не заполнен или
// заполнен некорректно, то выставляется флаг отказа в проведении.
// Проверка выполняется по выборке из результата запроса по шапке,
// все проверяемые реквизиты должны быть включены в выборку по шапке.
//
// Параметры: 
//	ВыборкаПоШапкеДокумента	- выборка из результата запроса по шапке документа,
//	Отказ 					- флаг отказа в проведении,
//	Заголовок				- Заголовок для сообщений об ошибках проведения.
//
Процедура ПроверитьЗаполнениеШапки(ВыборкаПоШапкеДокумента, Отказ, Заголовок = "")

	//  Организация
	Если Не ЗначениеЗаполнено(ВыборкаПоШапкеДокумента.Организация) Тогда
		ОбщегоНазначения.ВывестиИнформациюОбОшибке(ОбщегоНазначения.ПреобразоватьСтрокуИнтерфейса("Не указана организация!"), Отказ, Заголовок);
	КонецЕсли;
	
	// Сотрудник
	Если Не ЗначениеЗаполнено(ВыборкаПоШапкеДокумента.Сотрудник) Тогда
		ОбщегоНазначения.ВывестиИнформациюОбОшибке("Не выбран сотрудник!", Отказ, Заголовок);
	Иначе
		Если ВыборкаПоШапкеДокумента.ОшибкаНеСоответствиеСотрудникаИОрганизации Тогда
			ОбщегоНазначения.ВывестиИнформациюОбОшибке(ОбщегоНазначения.ПреобразоватьСтрокуИнтерфейса("Указанный сотрудник оформлен на другую организацию!"), Отказ, Заголовок);
		КонецЕсли;
		Если ВыборкаПоШапкеДокумента.ОшибкаВидаЗанятости Тогда
			ОбщегоНазначения.ВывестиИнформациюОбОшибке("Справки выдаются сотрудникам, имевшим основное место работы или работавшим по внешнему совместительству!", Отказ, Заголовок);
		КонецЕсли;
	КонецЕсли;

	Если Не ЗначениеЗаполнено(ВыборкаПоШапкеДокумента.ФИО) Тогда
		ОбщегоНазначения.ВывестиИнформациюОбОшибке("Не указано ФИО сотрудника!", Отказ, Заголовок);
	КонецЕсли;
	
	//  ФСС
	Если Не ЗначениеЗаполнено(ВыборкаПоШапкеДокумента.НаименованиеТерриториальногоОрганаФСС) Тогда
		ОбщегоНазначения.ВывестиИнформациюОбОшибке("Не указано наименование территориального органа ФСС!", Отказ, Заголовок);
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ВыборкаПоШапкеДокумента.РегистрационныйНомерФСС) Тогда
		ОбщегоНазначения.ВывестиИнформациюОбОшибке("Не указан регистрационный номер в ФСС!", Отказ, Заголовок);
	КонецЕсли;
	
КонецПроцедуры // ПроверитьЗаполнениеШапки()

// Проверяет правильность заполнения реквизитов в строке ТЧ "Начисления" документа.
// Если какой-то из реквизитов, влияющий на проведение, не заполнен или
// заполнен некорректно, то выставляется флаг отказа в проведении.
// Проверка выполняется по выборке из результата запроса к ТЧ документа,
// все проверяемые реквизиты должны быть включены в выборку.
//
// Параметры: 
//	ВыборкаПоШапкеДокумента		- выборка из результата запроса по шапке документа,
//	ВыборкаПоСтрокамДокумента	- спозиционированная на определеной строке выборка 
//								  из результата запроса к ТЧ документа, 
//	Отказ						- флаг отказа в проведении,
//	Заголовок					- Заголовок для сообщений об ошибках проведения.
//
Процедура ПроверитьЗаполнениеСтрокиДанныеОЗаработке(ВыборкаПоСтрокамДокумента, Отказ, Заголовок) 

	СтрокаНачалаСообщенияОбОшибке =
		"В строке номер """+ СокрЛП(ВыборкаПоСтрокамДокумента.НомерСтроки) + """ табл. части ""Данные о заработке"": ";
	
	Если НЕ ЗначениеЗаполнено(ВыборкаПоСтрокамДокумента.РасчетныйГод) Тогда
		ОбщегоНазначения.ВывестиИнформациюОбОшибке(СтрокаНачалаСообщенияОбОшибке + "не указан расчетный год!", Отказ, Заголовок);
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ВыборкаПоСтрокамДокумента.Заработок) Тогда
		ОбщегоНазначения.ВывестиИнформациюОбОшибке(СтрокаНачалаСообщенияОбОшибке + "не указан заработок!", Отказ, Заголовок);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ВыборкаПоСтрокамДокумента.НомерПовторнойСтроки) Тогда
		ОбщегоНазначения.ВывестиИнформациюОбОшибке(СтрокаНачалаСообщенияОбОшибке + "указанный расчетный год повторяется в строке " + ВыборкаПоСтрокамДокумента.НомерПовторнойСтроки + "!", Отказ, Заголовок);
	КонецЕсли;
	
КонецПроцедуры // ПроверитьЗаполнениеСтрокиНачисления()

#КонецЕсли

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("СправочникСсылка.СотрудникиОрганизаций") Тогда
		
		Дата = ОбщегоНазначения.ПолучитьРабочуюДату();
		
		ЗаполнитьРеквизитыНовогоДокумента();
		
		Сотрудник	= ДанныеЗаполнения;
		Организация	= ОбщегоНазначения.ПолучитьЗначениеРеквизита(Сотрудник, "ТекущееОбособленноеПодразделение");
		
		Автозаполнение("ДанныеСотрудника");
		АдресВФорме	= УправлениеКонтактнойИнформацией.ПолучитьПредставлениеАдресаПоСтрока(Адрес);
		Если ЗначениеЗаполнено(ГодС) и ЗначениеЗаполнено(ГодПо) Тогда
			Автозаполнение("ДанныеОЗаработке");
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОПЕРАТОРЫ ОСНОВНОЙ ПРОГРАММЫ