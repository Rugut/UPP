Перем НП;
// Переменные настройки диалога
Перем ФлагВидимостиРазмещениеРезервов;
Перем ФлагВидимостиПросроченоДнейОтгрузки;
Перем ФлагВидимостиДатыОтгрузки;
Перем ФлагДоступностиИзмененияНастроек;
Перем ФлагДоступностиИзмененияДляРезервов;

Перем СоответствиеНазначений;

Перем СтруктураДляОтбораПоКатегориям;

Перем СтруктураПредставлениеПолей;

Перем СтараяПричинаЗакрытия;

Перем ДатаНачала;
Перем ДатаОкончания;

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ
// 

// Устанавливает видимость полей ввода в зависимости от настроек в форме.
//
Процедура УстановитьВидимостьЭлементовНастройки()
	
	// Поле Размещение резервов видимо когда Наличие резервов установлено как С резервами
	ЭлементыФормы.ПолеНастройкиРазмещениеРезервов.Видимость = ФлагВидимостиРазмещениеРезервов;
	
	// Поле Дата отгрузки видимо когда Меньше/Равна/Больше
	ЭлементыФормы.ПолеДатаОтгрузки.Видимость = ФлагВидимостиДатыОтгрузки;
	
	// Поле ввода Просрочено дней видимо только когда Просрочен
	ЭлементыФормы.НадписьОтгрузкаДней.Видимость = ФлагВидимостиПросроченоДнейОтгрузки;
	ЭлементыФормы.ПолеПросроченоДнейОтгрузки.Видимость = ФлагВидимостиПросроченоДнейОтгрузки;
	ЭлементыФормы.НадписьОтгрузкаНа.Видимость = ФлагВидимостиПросроченоДнейОтгрузки;
	
КонецПроцедуры // УстановитьВидимостьЭлементовНастройки()

// Устанавливает доступность полей ввода в зависимости от настроек в форме.
//
Процедура УстановитьДоступностьЭлементовНастройки()
	
	ЭлементыФормы.ПолеНастройкиНаличиеРезервов.Доступность = ФлагДоступностиИзмененияДляРезервов;
	ЭлементыФормы.ПолеНастройкиСостояниеОтгрузки.Доступность = ФлагДоступностиИзмененияНастроек;
	ЭлементыФормы.ПолеНастройкиСрокОтгрузкиСравнение.Доступность = ФлагДоступностиИзмененияНастроек;

КонецПроцедуры // УстановитьДоступностьЭлементовНастройки()

// Устанавливает доступность кнопок в зависимости от наличия документов в таблице.
//
Процедура УстановитьДоступностьКнопокПанелиЗаказов()
	
	// При пустом ТабличноеПолеЗаказы не должны быть доступны кнопки действий над заказами.
	Если ТабличноеПолеЗаказы.Количество() > 0 Тогда
		ЭлементыФормы.КоманднаяПанельЗаказы.Кнопки.ДействиеАнализ.Доступность       = Истина;
		ЭлементыФормы.КоманднаяПанельЗаказы.Кнопки.ДействиеВключитьВсе.Доступность  = Истина;
		ЭлементыФормы.КоманднаяПанельЗаказы.Кнопки.ДействиеВыключитьВсе.Доступность = Истина;
	Иначе
		ЭлементыФормы.КоманднаяПанельЗаказы.Кнопки.ДействиеАнализ.Доступность       = Ложь;
		ЭлементыФормы.КоманднаяПанельЗаказы.Кнопки.ДействиеВключитьВсе.Доступность  = Ложь;
		ЭлементыФормы.КоманднаяПанельЗаказы.Кнопки.ДействиеВыключитьВсе.Доступность = Ложь;
	КонецЕсли;
	
КонецПроцедуры // УстановитьДоступностьКнопокПанелиЗаказов()

// Устанавливает список доступных "Видов заполнения" и "Вид заполнения" по умолчанию по "Типу операции".
//
Процедура УстановитьСтруктуруПодменюВидЗаполнения()
	
	// Получим настройки из хранилища
	НастройкиЗаполнения = ДокументОбъект.СпособЗаполнения.Получить();
	
	// Проверим был ли заполнен документ
	Если НастройкиЗаполнения <> Неопределено Тогда
		
		// Было заполнение, попробуем восстановить значения
		ДатаНач = НастройкиЗаполнения.ДатаНач;
		ДатаКон = НастройкиЗаполнения.ДатаКон;
		НаличиеРезервов = НастройкиЗаполнения.НаличиеРезервов;
		РазмещениеРезервов = НастройкиЗаполнения.РазмещениеРезервов;
		
		СостояниеОтгрузки = НастройкиЗаполнения.СостояниеОтгрузки;
		СрокОтгрузкиСравнение = НастройкиЗаполнения.СрокОтгрузкиСравнение;
		ПросроченоДнейОтгрузки = НастройкиЗаполнения.ПросроченоДнейОтгрузки;
		ДатаОтгрузки = НастройкиЗаполнения.ДатаОтгрузки;
		
		ИспользоватьСвойстваИКатегории = НастройкиЗаполнения.ИспользоватьСвойстваИКатегории;
		НастройкиОтбора = НастройкиЗаполнения.НастройкиОтбора;
	
		ВидЗаполненияИзДокумента = НастройкиЗаполнения.ВидЗаполнения;
		
		НастройкиИзДокумента = Истина;
		
	КонецЕсли;
		
	// Вид заполнения по Типу операции документа Закрытия
	Если ВариантОтбораПоВидуОперации = Перечисления.ВидыОперацийЗакрытиеВнутреннихЗаказов.СнятиеРезервов Тогда
		ВидЗаполненияПоУмолчанию = ЗаполнитьСписокВидЗаполненияДляСнятияРезервов();

	ИначеЕсли ВариантОтбораПоВидуОперации = Перечисления.ВидыОперацийЗакрытиеВнутреннихЗаказов.ЗакрытиеЗаказов Тогда
		ВидЗаполненияПоУмолчанию = ЗаполнитьСписокВидЗаполненияДляЗакрытияЗаказов();
		
	КонецЕсли;
	
	Если НастройкиИзДокумента = Истина Тогда
		// Вид заполнения по значению из документа
		Найден = Неопределено;
		Если НЕ(ВидЗаполненияИзДокумента = Неопределено) Тогда
			Найден = ЭлементыФормы.ПолеНастройкиВидЗаполнения.СписокВыбора.НайтиПоЗначению(ВидЗаполненияИзДокумента);
		КонецЕсли;
		
		Если НЕ(Найден = Неопределено) Тогда
			// Значение в документе корректно, берем его
			ВидЗаполнения = ВидЗаполненияИзДокумента;
		Иначе
			Сообщить("Некорректное значение способа заполнения в документе, установлено значение по умолчанию");
			ВидЗаполнения = ВидЗаполненияПоУмолчанию;
		КонецЕсли;
	Иначе	
		ВидЗаполнения = ВидЗаполненияПоУмолчанию;
	КонецЕсли;
	
	ПолеНастройкиВидЗаполненияПриИзменении(ЭлементыФормы.ПолеНастройкиВидЗаполнения);
	
КонецПроцедуры // УстановитьСтруктуруПодменюВидЗаполнения()

// Вызывается при изменении настроек отбора в форме.
//
Процедура ПриИзмененииНастроекПоУмолчанию()

	ПолеНастройкиНаличиеРезервовПриИзменении(ЭлементыФормы.ПолеНастройкиНаличиеРезервов);
	ПолеНастройкиСрокОтгрузкиСравнениеПриИзменении(ЭлементыФормы.ПолеНастройкиСрокОтгрузкиСравнение);
	
	Если (ВидЗаполнения = 1) ИЛИ (ВидЗаполнения = 4) Тогда
		// Для данного вида заполнения доступны для изменения все настройки
		ФлагДоступностиИзмененияНастроек = Истина;
	Иначе			
		// Для данного Вида заполнения основные настройки недоступны для изменения
		ФлагДоступностиИзмененияНастроек = Ложь;
	КонецЕсли;
	
	Если ВидЗаполнения = 1 Тогда
		// Для данного вида заполнения доступны для изменения настройки резервов
		ФлагДоступностиИзмененияДляРезервов	= Истина;
	Иначе
		// Для данного вида заполнения недоступны для изменения настройки резервов
		ФлагДоступностиИзмененияДляРезервов	= Ложь;		
	КонецЕсли;
	
	УстановитьДоступностьЭлементовНастройки();
			
КонецПроцедуры // ПриИзмененииНастроекПоУмолчанию()

// Заполнить Доступные Виды заполнения для Вида операции Закрытие заказов
// и вернуть Вид заполнения по умолчанию для данного Вида операции.
//
// Возвращаемое значение:
//  Число - Вид заполнения по умолчанию для данного Вида операции
//
Функция ЗаполнитьСписокВидЗаполненияДляЗакрытияЗаказов()
	
	ЭлементыФормы.ПолеНастройкиВидЗаполнения.СписокВыбора.Очистить();
	
	// Доступные Виды заполнения для Типа операции Закрытие заказов 
	ЭлементыФормы.ПолеНастройкиВидЗаполнения.СписокВыбора.Добавить(1, "Произвольный отбор");
	ЭлементыФормы.ПолеНастройкиВидЗаполнения.СписокВыбора.Добавить(2, "Устаревшие заказы");		
	ЭлементыФормы.ПолеНастройкиВидЗаполнения.СписокВыбора.Добавить(3, "Исполненные заказы");		

	Возврат 3;

КонецФункции // ЗаполнитьСписокВидЗаполненияДляЗакрытияЗаказов()

// Заполнить Доступные Виды заполнения для Типа операции Снятие резервов
// и вернуть Вид заполнения по умолчанию для данного Вида операции.
//
// Возвращаемое значение:
//  Число - Вид заполнения по умолчанию для данного Вида операции
//
Функция ЗаполнитьСписокВидЗаполненияДляСнятияРезервов()
	
	ЭлементыФормы.ПолеНастройкиВидЗаполнения.СписокВыбора.Очистить();
	
	// Доступные Виды заполнения для Типа операции Снятие резервов 
	ЭлементыФормы.ПолеНастройкиВидЗаполнения.СписокВыбора.Добавить(4, "Все заказы с резервами");
	ЭлементыФормы.ПолеНастройкиВидЗаполнения.СписокВыбора.Добавить(5, "Заказы с устаревшими резервами");

	Возврат 5;

КонецФункции // ЗаполнитьСписокВидЗаполненияДляСнятияРезервов()

// Заполняет установки по умолчанию для отбора по всем возможным условиям.
//
Процедура УстановитьНастройкиПоУмолчаниюДляПроизвольногоОтбора()
	
	// Не важно
	НаличиеРезервов	=1;
	
	// Не важно
	СостояниеОтгрузки = 1;
	
	// Не важно
	СрокОтгрузкиСравнение = 1;
	
	ЗначениеПользователя = ВосстановитьЗначение(ДокументОбъект.Метаданные().Имя + "_ПросроченоДнейОтгрузки_" + Строка(ВидЗаполнения));
	Если НЕ(ЗначениеПользователя = Неопределено) Тогда
		// Восстановим последнее установленное значение данным пользователем
		ПросроченоДнейОтгрузки = ЗначениеПользователя;
	Иначе
		// По умолчанию 1 день
		ПросроченоДнейОтгрузки = 1;
	КонецЕсли;
	
	// По умолчанию Текущая дата
	ДатаОтгрузки = ТекущаяДата();
	
КонецПроцедуры // УстановитьНастройкиПоУмолчаниюДляПроизвольногоОтбора()

// Заполняет установки по умолчанию для отбора "Устаревших" заказов.
//
Процедура УстановитьНастройкиПоУмолчаниюДляУстаревшихЗаказов()

	// Без резервов
	НаличиеРезервов	= 2;
	
	// Не отгружен полностью (неотгружен либо отгружен частично)
	СостояниеОтгрузки = 2;
	
	// Просрочен (более чем на)
	СрокОтгрузкиСравнение = 2;
	
	ЗначениеПользователя = ВосстановитьЗначение(ДокументОбъект.Метаданные().Имя + "_ПросроченоДнейОтгрузки_" + Строка(ВидЗаполнения));
	Если НЕ(ЗначениеПользователя = Неопределено) Тогда
		// Восстановим последнее установленное значение данным пользователем
		ПросроченоДнейОтгрузки = ЗначениеПользователя;
	Иначе
		// Срок давности для устаревших заказов по умолчанию: 90 дней
		ПросроченоДнейОтгрузки = 90;
	КонецЕсли;
	
	// По умолчанию Текущая дата (в отборе не участвуют)
	ДатаОтгрузки = ТекущаяДата();
	
КонецПроцедуры // УстановитьНастройкиПоУмолчаниюДляУстаревшихЗаказов()

// Заполняет установки по умолчанию для отбора "Исполненных" заказов.
//
Процедура УстановитьНастройкиПоУмолчаниюДляИсполненныхЗаказов()

	// Без резервов
	НаличиеРезервов	= 2;
	
	// Отгружен полностью
	СостояниеОтгрузки = 3;
	
	// Срок отгрузки Не важно
	СрокОтгрузкиСравнение = 1;
	
	// По умолчанию 1 день (в отборе не участвуют)
	ПросроченоДнейОтгрузки = 1;
	
	// По умолчанию Текущая дата (в отборе не участвуют)
	ДатаОтгрузки = ТекущаяДата();
	
КонецПроцедуры // УстановитьНастройкиПоУмолчаниюДляИсполненныхЗаказов()

// Заполняет установки по умолчанию для отбора всех заказов с резервами.
//
Процедура УстановитьНастройкиПоУмолчаниюДляВсехЗаказовСРезервами()
	
	// С резервами
	НаличиеРезервов	=3;
	
	// Не важно
	СостояниеОтгрузки = 1;
	
	// Не важно
	СрокОтгрузкиСравнение = 1;
	
	ЗначениеПользователя = ВосстановитьЗначение(ДокументОбъект.Метаданные().Имя + "_ПросроченоДнейОтгрузки_" + Строка(ВидЗаполнения));
	Если НЕ(ЗначениеПользователя = Неопределено) Тогда
		// Восстановим последнее установленное значение данным пользователем
		ПросроченоДнейОтгрузки = ЗначениеПользователя;
	Иначе
		// По умолчанию 1 день
		ПросроченоДнейОтгрузки = 1;
	КонецЕсли;
	
	// По умолчанию Текущая дата
	ДатаОтгрузки = ТекущаяДата();
	
КонецПроцедуры // УстановитьНастройкиПоУмолчаниюДляВсехЗаказовСРезервами()

// Заполняет установки по умолчанию для отбора заказов с "Устаревшими" резервами.
//
Процедура УстановитьНастройкиПоУмолчаниюДляЗаказовСУстаревшимиРезервами()

	// С резервами
	НаличиеРезервов	= 3;
	
	// Не отгружен полностью (неотгружен либо отгружен частично)
	СостояниеОтгрузки = 2;
	
	// Просрочен (более чем на)
	СрокОтгрузкиСравнение = 2;
	
	ЗначениеПользователя = ВосстановитьЗначение(ДокументОбъект.Метаданные().Имя + "_ПросроченоДнейОтгрузки_" + Строка(ВидЗаполнения));
	Если НЕ(ЗначениеПользователя = Неопределено) Тогда
		// Восстановим последнее установленное значение данным пользователем
		ПросроченоДнейОтгрузки = ЗначениеПользователя;
	Иначе
		// Срок давности для резервов по умолчанию: 30 дней
		ПросроченоДнейОтгрузки = 30;
	КонецЕсли;
	
	// По умолчанию Текущая дата (в отборе не участвуют)
	ДатаОтгрузки = ТекущаяДата();
	
КонецПроцедуры // УстановитьНастройкиПоУмолчаниюДляЗаказовСУстаревшимиРезервами()

// Создает запрос и заполняет начальные установки построителя отбора.
//
// Параметры:
//  ПостроительОтчета - ссылка на объект ПостроительОтчета.
//
Процедура ИнициироватьПостроительДляОтбораЗаказов(ПостроительОтчета)

	// Запрос для отбора
	ТекстЗапроса = "
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ //РАЗЛИЧНЫЕ
	|	истина 																	КАК Переносить,
	|	&ПричинаЗакрытияЗаказа 													КАК ПричинаЗакрытияЗаказа,
	|	ВЫБОР КОГДА ЕстьNULL(ВнутренниеЗаказыОстатки.КоличествоОстаток,0)>0 ТОГДА
	|		истина
	|	ИНАЧЕ ложь КОНЕЦ														КАК ЗаказыКоличество,
	|	ВЫБОР КОГДА ЕстьNULL(ТоварыВРезервеНаСкладахОстатки.КоличествоОстаток,0)>0 ТОГДА
	|		истина
	|	ИНАЧЕ ложь КОНЕЦ														КАК РезервыКоличество,
	|	ВЫБОР КОГДА ЕстьNULL(РазмещениеЗаказовПокупателейОстатки.КоличествоОстаток,0)>0 ТОГДА
	|		истина
	|	ИНАЧЕ ложь КОНЕЦ									       				КАК РазмещенияКоличество,
	|	ЕстьNULL(ВнутренниеЗаказыОстатки.КоличествоОстаток,0) 					КАК ЗаказыОстаток,
	|	ДокументыВнутреннийЗаказ.Ссылка,
	|	ДокументыВнутреннийЗаказ.Дата,
	|	ДокументыВнутреннийЗаказ.Номер,
	|	ДокументыВнутреннийЗаказ.Организация,
	|	ДокументыВнутреннийЗаказ.Ответственный,
	|	ДокументыВнутреннийЗаказ.ДатаОтгрузки,
	|	ДокументыВнутреннийЗаказ.ВидЗаказа,
	|	ДокументыВнутреннийЗаказ.Заказчик,
	|	ДокументыВнутреннийЗаказ.Проведен
	|
	|{ВЫБРАТЬ 
	|	ДокументыВнутреннийЗаказ.ВидЗаказа КАК ВидЗаказа,
	|	ДокументыВнутреннийЗаказ.Заказчик.* КАК Заказчик,
	|	ДокументыВнутреннийЗаказ.Организация.* КАК Организация,
	|	ДокументыВнутреннийЗаказ.Ответственный.* КАК Ответственный
	|//СВОЙСТВА
	|}
	|
	|ИЗ
	|	Документ.ВнутреннийЗаказ                                    КАК ДокументыВнутреннийЗаказ
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрНакопления.ВнутренниеЗаказы.Остатки              КАК ВнутренниеЗаказыОстатки
	|		ПО ДокументыВнутреннийЗаказ.Ссылка = ВнутренниеЗаказыОстатки.ВнутреннийЗаказ
	|		
	|   ЛЕВОЕ СОЕДИНЕНИЕ
	|       РегистрНакопления.ТоварыВРезервеНаСкладах.Остатки       КАК ТоварыВРезервеНаСкладахОстатки
	|		ПО ДокументыВнутреннийЗаказ.Ссылка = ТоварыВРезервеНаСкладахОстатки.ДокументРезерва
	|
	|   ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрНакопления.РазмещениеЗаказовПокупателей.Остатки  КАК РазмещениеЗаказовПокупателейОстатки
	|		ПО ДокументыВнутреннийЗаказ.Ссылка = РазмещениеЗаказовПокупателейОстатки.ЗаказПокупателя
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрСведений.ПричиныЗакрытияЗаказов 					КАК ПричиныЗакрытияЗаказов
	|		ПО ДокументыВнутреннийЗаказ.Ссылка = ПричиныЗакрытияЗаказов.Заказ 
	|			И ПричиныЗакрытияЗаказов.Регистратор <> ДокументыВнутреннийЗаказ.Ссылка
	|//СОЕДИНЕНИЯ
	|
	|	{ ГДЕ 
	|	 ДокументыВнутреннийЗаказ.ВидЗаказа КАК ВидЗаказа,
	|	 ДокументыВнутреннийЗаказ.Заказчик.* КАК Заказчик,
	|	 ДокументыВнутреннийЗаказ.Организация.* КАК Организация,
	|	 ДокументыВнутреннийЗаказ.Ответственный.* КАК Ответственный
	|//СВОЙСТВА
	|//КАТЕГОРИИ
	|	}
	|		
	|ГДЕ
	|	(ДокументыВнутреннийЗаказ.Проведен) И
	|	(ПричиныЗакрытияЗаказов.ПричинаЗакрытияЗаказа IS NULL) И
	|	((ДокументыВнутреннийЗаказ.Дата >= &ДатаНач) ИЛИ (&ДатаНач = &ПустаяДата)) И
	|	((ДокументыВнутреннийЗаказ.Дата <= &ДатаКон) ИЛИ (&ДатаКон = &ПустаяДата)) И
	|
	|	(	((&СрокОтгрузкиСравнение = 6) И (ДокументыВнутреннийЗаказ.ДатаОтгрузки = &ПустаяДата)) ИЛИ
	|		((&СрокОтгрузкиСравнение = 5) И (ДокументыВнутреннийЗаказ.ДатаОтгрузки > &ДатаОтгрузки)) ИЛИ
	|		((&СрокОтгрузкиСравнение = 4) И (ДокументыВнутреннийЗаказ.ДатаОтгрузки = &ДатаОтгрузки)) ИЛИ
	|		((&СрокОтгрузкиСравнение = 3) И (ДокументыВнутреннийЗаказ.ДатаОтгрузки < &ДатаОтгрузки)) ИЛИ
	|		((&СрокОтгрузкиСравнение = 2) И (ДокументыВнутреннийЗаказ.ДатаОтгрузки <= &ТребуемаяДатаОтгрузки)) ИЛИ
	|		(&СрокОтгрузкиСравнение = 1)
	|	) И
	|
	|	(	((&СостояниеОтгрузки = 3) И ((ВнутренниеЗаказыОстатки.КоличествоОстаток IS NULL) ИЛИ (ВнутренниеЗаказыОстатки.КоличествоОстаток < 0))) ИЛИ
	|		((&СостояниеОтгрузки = 2) И (ВнутренниеЗаказыОстатки.КоличествоОстаток > 0)) ИЛИ
	|		(&СостояниеОтгрузки = 1)
	|	) И
	|
	|	(	((&НаличиеРезервов = 3) И (&РазмещениеРезервов = 3) И ((ТоварыВРезервеНаСкладахОстатки.КоличествоОстаток IS NULL) ИЛИ (ТоварыВРезервеНаСкладахОстатки.КоличествоОстаток < 0)) И (РазмещениеЗаказовПокупателейОстатки.КоличествоОстаток > 0)) ИЛИ
	|		((&НаличиеРезервов = 3) И (&РазмещениеРезервов = 2) И (ТоварыВРезервеНаСкладахОстатки.КоличествоОстаток > 0) И ((РазмещениеЗаказовПокупателейОстатки.КоличествоОстаток IS NULL) ИЛИ (РазмещениеЗаказовПокупателейОстатки.КоличествоОстаток < 0))) ИЛИ
	|		((&НаличиеРезервов = 3) И (&РазмещениеРезервов = 1) И ((ТоварыВРезервеНаСкладахОстатки.КоличествоОстаток > 0) ИЛИ (РазмещениеЗаказовПокупателейОстатки.КоличествоОстаток > 0))) ИЛИ
	|		((&НаличиеРезервов = 2) И ((ТоварыВРезервеНаСкладахОстатки.КоличествоОстаток IS NULL) ИЛИ (ТоварыВРезервеНаСкладахОстатки.КоличествоОстаток < 0)) И ((РазмещениеЗаказовПокупателейОстатки.КоличествоОстаток IS NULL) ИЛИ (РазмещениеЗаказовПокупателейОстатки.КоличествоОстаток < 0))) ИЛИ
	|		(&НаличиеРезервов = 1)
	|	)
	|
	|УПОРЯДОЧИТЬ ПО
	|	ДокументыВнутреннийЗаказ.Дата, ДокументыВнутреннийЗаказ.Номер
	|";

	СтруктураПредставлениеПолей = Новый Структура;
	
	СтруктураПредставлениеПолей.Вставить("ВидЗаказа",          "Вид заказа");
	СтруктураПредставлениеПолей.Вставить("Заказчик",           "Заказчик");
	СтруктураПредставлениеПолей.Вставить("Организация",        "Организация");
	СтруктураПредставлениеПолей.Вставить("Ответственный",      "Ответственный");
	
	Если ИспользоватьСвойстваИКатегории Тогда

        СоответствиеНазначений = Новый Соответствие;
		
		ТаблицаПолей = Новый ТаблицаЗначений;
		ТаблицаПолей.Колонки.Добавить("ПутьКДанным");  // описание поля запроса поля, для которого добавляются свойства и категории. Используется в условии соединения с регистром сведений, хранящим значения свойств или категорий
		ТаблицаПолей.Колонки.Добавить("Представление");// представление поля, для которого добавляются свойства и категории. 
		ТаблицаПолей.Колонки.Добавить("Назначение");   // назначение свойств/категорий объектов для данного поля
		//ТаблицаПолей.Колонки.Добавить("ТипЗначения");  // тип значения поля, для которого добавляются свойства и категории. Используется, если не установлено назначение
		ТаблицаПолей.Колонки.Добавить("НетКатегорий"); // признак НЕиспользования категорий для объекта
		
		ТекстПоляКатегорий = "";
		ТекстПоляСвойств = "";
		
		ТаблицаПолей.Очистить();
		
		НоваяСтрока = ТаблицаПолей.Добавить();
		НоваяСтрока.ПутьКДанным = "ДокументыВнутреннийЗаказ.Ссылка";
		НоваяСтрока.Представление = "Внутренний заказ";
		НоваяСтрока.Назначение = ПланыВидовХарактеристик.НазначенияСвойствКатегорийОбъектов.Документы;
		
		УправлениеОтчетами.ДобавитьВТекстСвойстваИКатегории(ТаблицаПолей, ТекстЗапроса, СтруктураПредставлениеПолей, СоответствиеНазначений, ПостроительОтчета.Параметры,, ТекстПоляКатегорий, ТекстПоляСвойств,, "//СВОЙСТВА", "//КАТЕГОРИИ", "//СОЕДИНЕНИЯ", , СтруктураДляОтбораПоКатегориям);
		
	КонецЕсли;
	
	Если ПустаяСтрока(ТекстЗапроса) = Ложь Тогда
		ПостроительОтчета.Текст = ТекстЗапроса;	
	КонецЕсли;
	
	УправлениеОтчетами.УстановитьТипыЗначенийСвойствИКатегорийДляОтбора(ПостроительОтчета, ТекстПоляКатегорий, ТекстПоляСвойств, СоответствиеНазначений, СтруктураПредставлениеПолей);
	
	// Заполнить отбор построителя по умолчанию
	Если НЕ(ПостроительОтчета.Отбор.Количество() > 0) Тогда
		Если НастройкиИзДокумента Тогда
			ПостроительОтчета.УстановитьНастройки(НастройкиОтбора);
		Иначе
			ЗаполнитьОтборПостроителя(ПостроительОтчета);
		КонецЕсли;
		
		Поле = ПостроительОтчета.Отбор.Найти("Организация");
		Если Поле = Неопределено Тогда
			Поле = ПостроительОтчета.Отбор.Добавить("Организация", "Организация", "Организация");
		КонецЕсли;
		
		Если НЕ (НастройкиИзДокумента И ДокументОбъект.Организация.Пустая()) Тогда
			Если ДокументОбъект.Организация.Пустая() Тогда
				Поле.Использование = Ложь;
			Иначе		
				Поле.Использование = Истина;
				Поле.Установить(ДокументОбъект.Организация);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	// Заполнить представление полей построителя по именам
	УправлениеОтчетами.ЗаполнитьПредставленияПолей(СтруктураПредставлениеПолей, ПостроительОтчета);
	
КонецПроцедуры // ИнициироватьПостроительДляОтбораЗаказов()

// Заполняет переменные построителя отбора выбранными в форме значениями.
//
// Параметры:
//  ПостроительОтчета - ссылка на объект ПостроительОтчета.
//
Процедура ЗаполнитьПараметрыПостроителя(ПостроительОтчета)
	
	// Заполним параметры построителя
	ПостроительОтчета.Параметры.Вставить("НаличиеРезервов", 		НаличиеРезервов);
	ПостроительОтчета.Параметры.Вставить("РазмещениеРезервов", 		РазмещениеРезервов);
	ПостроительОтчета.Параметры.Вставить("СостояниеОтгрузки", 		СостояниеОтгрузки);
	ПостроительОтчета.Параметры.Вставить("СрокОтгрузкиСравнение", 	СрокОтгрузкиСравнение);
	
	ПостроительОтчета.Параметры.Вставить("ДатаНач", 		ДатаНачала);
	ПостроительОтчета.Параметры.Вставить("ДатаКон", 		ДатаОкончания);
	ПостроительОтчета.Параметры.Вставить("ДатаОтгрузки", 	ДатаОтгрузки);
	ПостроительОтчета.Параметры.Вставить("ТекущаяДата", 	ТекущаяДата());
	ПостроительОтчета.Параметры.Вставить("ПустаяДата",  	Дата('00010101'));
	ПостроительОтчета.Параметры.Вставить("ПричинаЗакрытияЗаказа",ПричинаЗакрытия);
	
	Если ПросроченоДнейОтгрузки = 0 ИЛИ СрокОтгрузкиСравнение <> 2 Тогда
		ТребуемаяДатаОтгрузки = НачалоДня(ТекущаяДата());
	ИначеЕсли Константы.СпособКонтроляДнейЗадолженности.Получить() = Перечисления.СпособыКонтроляДнейЗадолженности.ПоКалендарнымДням Тогда
		ТребуемаяДатаОтгрузки = НачалоДня(ТекущаяДата()-24*60*60*ПросроченоДнейОтгрузки);
	Иначе
		ТребуемаяДатаОтгрузки = ЗаполнениеДокументов.ОпределитьДату(НачалоДня(ТекущаяДата()),-ПросроченоДнейОтгрузки);
		Если ТребуемаяДатаОтгрузки = неопределено Тогда //не удалось определить дату по регл. производственному календарю
			Сообщить("Не заполнен Регламентированный производственный календарь за "+Год(ТекущаяДата())+" год.");
			Сообщить("Дата, ранее которой заказы просрочены, рассчитана по календарным дням.");
			ТребуемаяДатаОтгрузки = НачалоДня(ТекущаяДата()-24*60*60*ПросроченоДнейОтгрузки);
		КонецЕсли;
	КонецЕсли;
	ПостроительОтчета.Параметры.Вставить("ТребуемаяДатаОтгрузки", 	ТребуемаяДатаОтгрузки);

КонецПроцедуры // ЗаполнитьПараметрыПостроителя()

// Заполняет отбор построителя по умолчанию.
//
// Параметры:
//  ПостроительОтчета - ссылка на объект ПостроительОтчета.
//
Процедура ЗаполнитьОтборПостроителя(ПостроительОтчета)
	
	// Заполним доступные поля
	Поле = ПостроительОтчета.Отбор.Добавить("Ответственный", "Ответственный", "Ответственный");
	
	Поле.Установить(ДокументОбъект.Ответственный.Ссылка);
	
	Поле = ПостроительОтчета.Отбор.Добавить("ВидЗаказа", "ВидЗаказа", "Вид заказа");
	Поле.Использование = Ложь;
	Поле = ПостроительОтчета.Отбор.Добавить("Заказчик", "Заказчик", "Заказчик");
	Поле.Использование = Ложь;
	
КонецПроцедуры // ЗаполнитьОтборПостроителя()

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ
//
	
// Обработчик события ПриОткрытии формы.
//
Процедура ПриОткрытии()
	
	// Заполнить структуру подменю Вид заполнения панели формы отбора
	УстановитьСтруктуруПодменюВидЗаполнения();
	
	// Создать и заполнить построитель для отбора
	ИнициироватьПостроительДляОтбораЗаказов(ПостроительОтчета);
	
	// Установить доступность кнопок командной панели таблицы Заказов
	УстановитьДоступностьКнопокПанелиЗаказов();
	
	// Установим конец периода на конец дня даты документа закрытия
	ДатаКон = КонецДня(Дата);
	ДатаКонПриИзменении(ЭлементыФормы.ДатаКон);

КонецПроцедуры // ПриОткрытии()

// Обработчик события ПриИзменении элемента формы ДатаНач.
//
Процедура ДатаНачПриИзменении(Элемент)

	// Установка даты начала периода по умолчанию
	НП.УстановитьПериод(ДатаНач, КонецДня(ДатаКон), Истина);
	
КонецПроцедуры	// ДатаНачПриИзменении()

// Обработчик события ПриИзменении элемента формы ДатаКон.
//
Процедура ДатаКонПриИзменении(Элемент)

	// Установка даты конца периода по умолчанию
	Если КонецДня(ДатаКон) > КонецДня(Дата) Тогда
		// Установим конец периода на конец дня даты документа закрытия
		ДатаКон = КонецДня(Дата);
		Предупреждение("Дата окончания периода не должна быть больше даты документа!",,ЭтаФорма.Заголовок);
	Иначе
		ДатаКон = КонецДня(ДатаКон);
	КонецЕсли;
	
	НП.УстановитьПериод(ДатаНач, ДатаКон, Истина);
	
КонецПроцедуры	// ДатаКонПриИзменении()

// Обработчик события Нажатие кнопки настройки периода.
//
Процедура КнопкаНастройкаПериодаНажатие(Элемент)
	
	// Установка периода отбора
	Если НП.Редактировать() Тогда
		ДатаНач = НП.ПолучитьДатуНачала();
		ДатаКон = НП.ПолучитьДатуОкончания();
	КонецЕсли;

КонецПроцедуры // КнопкаНастройкаПериодаНажатие()

// Обработчик события Выбор кнопки подменю ВидЗаполнения командной панели формы.
// формы. Процедура устанавливает значение реквизита ВидОперации.
//
Процедура ПолеНастройкиВидЗаполненияПриИзменении(Элемент)

	// Установить настройки диалога по виду заполнения
	Если Элемент.Значение = 1 Тогда 
		УстановитьНастройкиПоУмолчаниюДляПроизвольногоОтбора();
	ИначеЕсли Элемент.Значение = 2 Тогда 
		УстановитьНастройкиПоУмолчаниюДляУстаревшихЗаказов();
	ИначеЕсли Элемент.Значение = 3 Тогда 
		УстановитьНастройкиПоУмолчаниюДляИсполненныхЗаказов();
	ИначеЕсли Элемент.Значение = 4 Тогда 
		УстановитьНастройкиПоУмолчаниюДляВсехЗаказовСРезервами();
	ИначеЕсли Элемент.Значение = 5 Тогда 
		УстановитьНастройкиПоУмолчаниюДляЗаказовСУстаревшимиРезервами();
	КонецЕсли;
	
	ПриИзмененииНастроекПоУмолчанию();

КонецПроцедуры // ПолеНастройкиВидЗаполненияПриИзменении()

// Обработчик события ПриИзменении элемента формы ПолеНастройкиНаличиеРезервов.
//
Процедура ПолеНастройкиНаличиеРезервовПриИзменении(Элемент)
	
	// Если с резервами, то доступен отбор по местоположение резервов
	Если Элемент.Значение = 3 Тогда 
		ФлагВидимостиРазмещениеРезервов = Истина;
	Иначе
		ФлагВидимостиРазмещениеРезервов = Ложь;
	КонецЕсли;
	
	УстановитьВидимостьЭлементовНастройки();
	
КонецПроцедуры // ПолеНастройкиНаличиеРезервовПриИзменении()

// Обработчик события ПриИзменении элемента формы ПолеНастройкиСрокОтгрузкиСравнение.
//
Процедура ПолеНастройкиСрокОтгрузкиСравнениеПриИзменении(Элемент)
	
	Если Элемент.Значение = 1 ИЛИ Элемент.Значение = 6 Тогда 
		// По значению Не важно и Не заполнена скроем поля настройки отбора 
		ФлагВидимостиПросроченоДнейОтгрузки = Ложь;
		ФлагВидимостиДатыОтгрузки = Ложь;
	ИначеЕсли Элемент.Значение = 2 Тогда 
		// По значению Просрочен доступен отбор по Количеству дней
		ФлагВидимостиПросроченоДнейОтгрузки = Истина;
		ФлагВидимостиДатыОтгрузки = Ложь;
	Иначе
		// Доступен отбор по Дате отгрузки
		ФлагВидимостиПросроченоДнейОтгрузки = Ложь;
		ФлагВидимостиДатыОтгрузки = Истина;
	КонецЕсли;
	
	УстановитьВидимостьЭлементовНастройки();
			
КонецПроцедуры // ПолеНастройкиСрокОтгрузкиСравнениеПриИзменении()

// Обработчик события ПриИзменении элемента формы ПолеПросроченоДнейОтгрузки.
//
Процедура ПолеПросроченоДнейОтгрузкиПриИзменении(Элемент)

	// Сохраним значение поля данного ВидаЗаполнения
	СохранитьЗначение(ДокументОбъект.Метаданные().Имя + "_ПросроченоДнейОтгрузки_" + Строка(ВидЗаполнения), ПросроченоДнейОтгрузки);

КонецПроцедуры

// Обработчик события Выбор, открывает Документ по двойному щелчку в списке.
//
Процедура ТабличноеПолеЗаказыВыбор(Элемент, ВыбраннаяСтрока, Колонка, СтандартнаяОбработка)

	Если Колонка.Имя <> "Переносить" И Колонка.Имя <> "ПричинаЗакрытияЗаказа" Тогда
		Если ВыбраннаяСтрока.Ссылка <> Неопределено Тогда
			ВыбраннаяСтрока.Ссылка.ПолучитьФорму(, ЭтаФорма).Открыть();
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры // ТабличноеПолеЗаказыВыбор()

// Обработчик события НачалоВыбора элемента формы ТабличноеПолеОтбор.Значение.
//
Процедура ТабличноеПолеОтборЗначениеНачалоВыбора(Элемент, СтандартнаяОбработка)

	Если Найти(НРег(ЭлементыФормы.ТабличноеПолеОтбор.ТекущаяСтрока.Представление), "категории") Тогда
		// Ограничение списка категорий
		Назначение = СоответствиеНазначений.Получить(ЭлементыФормы.ТабличноеПолеОтбор.ТекущаяСтрока.Представление);
		УправлениеОтчетами.ОсуществитьВыборКатегории(Элемент, Назначение, ЭтаФорма, СтандартнаяОбработка);
	ИначеЕсли Найти(НРег(ЭлементыФормы.ТабличноеПолеОтбор.ТекущаяСтрока.Представление), "св-во") Тогда
		Свойство = СоответствиеНазначений.Получить(ЭлементыФормы.ТабличноеПолеОтбор.ТекущаяСтрока.Представление);
		УправлениеОтчетами.ОсуществитьВыборСвойства(Элемент, Свойство, ЭтаФорма, СтандартнаяОбработка);
	КонецЕсли;

КонецПроцедуры

// Обработчик события Нажатие кнопки ВключитьВсе.
//
Процедура КоманднаяПанельЗаказыДействиеВключитьВсе(Кнопка)
	
	// Установим все отметки в таблице ТабличноеПолеЗаказы формы.
	Для Каждого Строка Из ТабличноеПолеЗаказы Цикл
		Строка.Переносить = Истина;
	КонецЦикла;
	
КонецПроцедуры // КоманднаяПанельЗаказыДействиеВключитьВсе()

// Обработчик события Нажатие кнопки ВыключитьВсе.
//
Процедура КоманднаяПанельЗаказыДействиеВыключитьВсе(Кнопка)
	
	// Сбросим все отметки в таблице ТабличноеПолеЗаказы формы.
	Для Каждого Строка Из ТабличноеПолеЗаказы Цикл
		Строка.Переносить = Ложь;
	КонецЦикла;
	
КонецПроцедуры // КоманднаяПанельЗаказыДействиеВыключитьВсе()

// Обработчик события нажатие кнопки Анализ.
// вызывает анализ текущего состояния заказа.
//
Процедура ДействияФормыДействиеАнализ(Кнопка)

	// Открыть форму отчета АнализЗаказа для выбранного таблице документа.
	Если ЭлементыФормы.ТабличноеПолеЗаказы.ТекущаяСтрока <> Неопределено Тогда
		УправлениеЗаказами.СформироватьОтчетАнализЗаказа(ЭлементыФормы.ТабличноеПолеЗаказы.ТекущаяСтрока.Ссылка,ложь, истина);
	КонецЕсли;

КонецПроцедуры // ДействияФормыДействиеАнализ()

// Обработчик события Нажатие кнопки Заполнить.
//
Процедура КоманднаяПанельЗаказыДействиеЗаполнить(Кнопка)
	
	//Произведем отбор заказов по условиям отбора
	Если (ДатаНач > ДатаКон) И (ДатаКон <> Дата('00010101')) Тогда
		Предупреждение(" Дата начала периода не может быть больше даты окончания периода!",,ЭтаФорма.Заголовок);
		Возврат;
	КонецЕсли;
	
	// Заполним Построитель параметрами из полей формы и выполним отбор
	ДатаНачала = НачалоДня(ДатаНач);
	ДатаОкончания = КонецДня(ДатаКон);
	Если НЕ УправлениеОтчетами.ЗадатьПараметрыОтбораПоКатегориям(ПостроительОтчета, СтруктураДляОтбораПоКатегориям) Тогда
		Предупреждение("По одной категории нельзя устанавливать несколько отборов");
		Возврат;
	КонецЕсли;

	ЗаполнитьПараметрыПостроителя(ПостроительОтчета);
	ПостроительОтчета.Выполнить();
	
	// Поместим результат в таблицу значений
	//ТаблицаЗаказы = ПостроительОтчета.Результат.Выгрузить(ОбходРезультатаЗапроса.Прямой);
	
	// Заполним список отобранных заказов
	ТабличноеПолеЗаказы.Очистить();
	ТабличноеПолеЗаказы = ПостроительОтчета.Результат.Выгрузить(ОбходРезультатаЗапроса.Прямой);
	
	УстановитьДоступностьКнопокПанелиЗаказов();

КонецПроцедуры // КоманднаяПанельЗаказыДействиеЗаполнить()

// Обработчик события Нажатие кнопки Перенести.
//
Процедура ОсновныеДействияФормыДействиеПеренести(Кнопка)
	
	// Проверим наличие строк в таблице.
	Если ТабличноеПолеЗаказы.Количество() = 0 Тогда
		Предупреждение("Не выбраны документы!");
		Возврат;
	КонецЕсли;
	
	//Перенесем заказы в табличную часть документа Закрытие заказов
	
	// Выберем отмеченные
	СтруктураОтбора = Новый Структура;
	СтруктураОтбора.Вставить("Переносить", Истина);
	НайденныеСтроки = ТабличноеПолеЗаказы.НайтиСтроки(СтруктураОтбора);
	
	// Если есть отмеченные 
	Если НайденныеСтроки.Количество() > 0 Тогда
		Если ДокументОбъект.Заказы.Количество() > 0 Тогда
			// Если документ не пуст 
			ТекстВопроса = "";
			Если ДокументОбъект.Проведен Тогда
				ТекстВопроса = "Внимание! Документ проведен." + Символы.ПС;
			КонецЕсли;
			ТекстВопроса = ТекстВопроса + "Перед заполнением табличная часть будет очищена. Заполнить?";
			Ответ = Вопрос(ТекстВопроса, РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Да, ЭтаФорма.Заголовок);
			Если Ответ <> КодВозвратаДиалога.Да Тогда
				Возврат;
			КонецЕсли; 
			// Очистим 
			ДокументОбъект.Заказы.Очистить();
		КонецЕсли;
	КонецЕсли;
	
	// Перенесем отмеченные заказы в документ
	Для Каждого Строка Из НайденныеСтроки Цикл 
		Если Строка.Ссылка <> Неопределено Тогда
			СтрокаДокумента = ДокументОбъект.Заказы.Добавить();
			СтрокаДокумента.ВнутреннийЗаказ = Строка.Ссылка;
			Если НЕ(Строка.ПричинаЗакрытияЗаказа.Пустая() = Истина) Тогда
				СтрокаДокумента.ПричинаЗакрытияЗаказа = Строка.ПричинаЗакрытияЗаказа;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	// Сохраним значение всех настроек в поле документа СпособЗаполнения
	НастройкиЗаполнения = Новый Структура();
	
	НастройкиЗаполнения.Вставить("ВидЗаполнения", ВидЗаполнения);
	
	НастройкиЗаполнения.Вставить("ДатаНач", ДатаНач);
	НастройкиЗаполнения.Вставить("ДатаКон", ДатаКон);
	
	НастройкиЗаполнения.Вставить("НаличиеРезервов", НаличиеРезервов);
	НастройкиЗаполнения.Вставить("РазмещениеРезервов", РазмещениеРезервов);
	
	НастройкиЗаполнения.Вставить("СостояниеОтгрузки", СостояниеОтгрузки);
	НастройкиЗаполнения.Вставить("СрокОтгрузкиСравнение", СрокОтгрузкиСравнение);
	НастройкиЗаполнения.Вставить("ПросроченоДнейОтгрузки", ПросроченоДнейОтгрузки);
	НастройкиЗаполнения.Вставить("ДатаОтгрузки", ДатаОтгрузки);
	
	НастройкиЗаполнения.Вставить("ИспользоватьСвойстваИКатегории", ИспользоватьСвойстваИКатегории);
	
	НастройкиЗаполнения.Вставить("НастройкиОтбора", ПостроительОтчета.ПолучитьНастройки());
	
	ДокументОбъект.СпособЗаполнения = Новый ХранилищеЗначения(НастройкиЗаполнения);
	
	ЭтаФорма.Закрыть();

КонецПроцедуры // ОсновныеДействияФормыДействиеПеренести()

// Обработчик события ПриИзменении элемента формы ИспользоватьСвойстваИКатегории.
//
Процедура ИспользоватьСвойстваИКатегорииПриИзменении(Элемент)
	
	// Заполнить построитель для отбора
	ИнициироватьПостроительДляОтбораЗаказов(ПостроительОтчета);
	
КонецПроцедуры

// Обработчик события ПередЗакрытием формы.
//
Процедура ПередЗакрытием(Отказ, СтандартнаяОбработка)
	
	// Отключим стандартную обработку для этой формы
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

// Обработчик события ПриИзменении элемента формы ПолеВводаПричинаЗакрытия.
//
Процедура ПолеВводаПричинаЗакрытияПриИзменении(Элемент)
	
	Если (ПричинаЗакрытия <> СтараяПричинаЗакрытия) И (ТабличноеПолеЗаказы.Количество() > 0) Тогда
		Если ПричинаЗакрытия.Пустая() Тогда
			ТекстВопроса = "Очистить причину закрытия у всех заказов?";
			Ответ = Вопрос(ТекстВопроса, РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Да, ЭтаФорма.Заголовок);
			Если Ответ <> КодВозвратаДиалога.Да Тогда
				Возврат;
			КонецЕсли; 
			// Очистим причину закрытия во всех строках
			Для Каждого Строка Из ТабличноеПолеЗаказы Цикл 
				Если Строка.Ссылка <> Неопределено Тогда
					Строка.ПричинаЗакрытияЗаказа = Справочники.ПричиныЗакрытияЗаказов.ПустаяСсылка();
				КонецЕсли;
			КонецЦикла;
		Иначе
			ТекстВопроса = "Изменить причину закрытия у всех заказов?";
			Ответ = Вопрос(ТекстВопроса, РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Да, ЭтаФорма.Заголовок);
			Если Ответ <> КодВозвратаДиалога.Да Тогда
				Возврат;
			КонецЕсли; 
			// Заполним причину закрытия во все строки
			Для Каждого Строка Из ТабличноеПолеЗаказы Цикл 
				Если Строка.Ссылка <> Неопределено Тогда
					Строка.ПричинаЗакрытияЗаказа = ПричинаЗакрытия;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
		
КонецПроцедуры

// Обработчик события НачалоВыбора элемента формы ПолеВводаПричинаЗакрытия.
// 
Процедура ПолеВводаПричинаЗакрытияНачалоВыбора(Элемент, СтандартнаяОбработка)

	// Сохраним значение
	СтараяПричинаЗакрытия = ПричинаЗакрытия;
	
КонецПроцедуры

// Обработчик события Очистка элемента формы ПолеВводаПричинаЗакрытия.
// 
Процедура ПолеВводаПричинаЗакрытияОчистка(Элемент, СтандартнаяОбработка)
	
	// Сохраним значение
	СтараяПричинаЗакрытия = ПричинаЗакрытия;
	
КонецПроцедуры


НП = Новый НастройкаПериода;