// Хранит дерево кнопок подменю заполнение ТЧ
Перем мКнопкиЗаполненияТЧ;

// Хранит доступлный список действий документа
Перем мСписокДействий;

// Хранит текущую дату документа - для проверки перехода документа в другой период установки номера
Перем мТекущаяДатаДокумента;

// Хранит признак модификации таблиц регистров, которая выполняется перед открытием
Перем мТаблицыРегистровМодифицированыПередОткрытием;

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

Процедура ДействиеЗаполнитьСвободныеОстатки(Кнопка)

	Если Движения.СвободныеОстатки.Количество() <> 0 Тогда
		
		ТекстВопроса = "Перед заполнением табличная часть будет очищена. Заполнить?";
		Ответ = Вопрос(ТекстВопроса, РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Да,);
		Если Ответ <> КодВозвратаДиалога.Да Тогда
			Возврат;
		КонецЕсли;
		
		Движения.СвободныеОстатки.Очистить();
		
	КонецЕсли; 
	
	Для каждого СтрокаТаблицыРегистров Из ТаблицаРегистровНакопления Цикл
		Если СтрокаТаблицыРегистров.Имя = "ТоварыНаСкладах"
			ИЛИ СтрокаТаблицыРегистров.Имя = "ТоварыВРознице"
			ИЛИ СтрокаТаблицыРегистров.Имя = "ТоварыВРезервеНаСкладах"
			ИЛИ СтрокаТаблицыРегистров.Имя = "ТоварыКПередачеСоСкладов" Тогда
			
			НаборБазовыйРегистр = Движения[СтрокаТаблицыРегистров.Имя];
			
			Если НаборБазовыйРегистр.Количество() = 0 Тогда
				Продолжить;;
			КонецЕсли;
		
			НаборБазовыйРегистр[0].Период = Дата;

			ВидРегистраОснования = Перечисления.ВидыРегистровОснованийРегистраСвободныеОстатки[СтрокаТаблицыРегистров.Имя];
			ОбщегоНазначения.ЗаполнитьСвободныеОстаткиПоДаннымБазовогоРегистра(Движения.СвободныеОстатки, НаборБазовыйРегистр, ВидРегистраОснования);
		КонецЕсли; 
	КонецЦикла; 	
	
КонецПроцедуры //

Процедура ДобавитьКнопкуЗаполненияСвободныхОстатков(КоманднаяПанельРегистра)

	КоманднаяПанельРегистра.Кнопки.Добавить("РазделительЗаполнитьСвободныеОстатки");
	Кнопка = КоманднаяПанельРегистра.Кнопки.Добавить("ДействиеЗаполнитьСвободныеОстатки", ТипКнопкиКоманднойПанели.Действие, "Заполнить", Новый Действие("ДействиеЗаполнитьСвободныеОстатки"));
	Кнопка.Подсказка = "Заполнить регистр по данным базовых регистров";
	
КонецПроцедуры //

Процедура ПроверитьВозможностьЗаписи(ТаблицаРегистров, ВидРегистра)
	
	МассивУдаляемыхСтрок = Новый Массив;
	
	Для каждого СтрокаТаблицыРегистров Из ТаблицаРегистров Цикл
		Если Движения.Найти(СтрокаТаблицыРегистров.Имя) = Неопределено Тогда
			ОбщегоНазначения.Сообщение("Документ не является регистратором """ + ВидРегистра + "." + СтрокаТаблицыРегистров.Имя + """. Регистр удален из состава регистров.",,, Ссылка); 
			МассивУдаляемыхСтрок.Добавить(СтрокаТаблицыРегистров);
			Продолжить;
		КонецЕсли;
	КонецЦикла; 
	
	Для каждого СтрокаТаблицыРегистров Из МассивУдаляемыхСтрок Цикл
		ТаблицаРегистров.Удалить(СтрокаТаблицыРегистров);
	КонецЦикла; 
	
	Если МассивУдаляемыхСтрок.Количество() <> 0 Тогда
		мТаблицыРегистровМодифицированыПередОткрытием = Истина;
	КонецЕсли; 
	
КонецПроцедуры // 

Процедура СортироватьНаборЗаписей(ТабличноеПолеНаборЗаписей, ИмяКолонки, Порядок = "Возр");
	
	Если ПустаяСтрока(ИмяКолонки) Тогда
		Возврат;
	КонецЕсли; 
	
	КопияНаборЗаписей = ТабличноеПолеНаборЗаписей.Значение.Выгрузить();
	КопияНаборЗаписей.Сортировать(ИмяКолонки + " " + Порядок);
	ТабличноеПолеНаборЗаписей.Значение.Загрузить(КопияНаборЗаписей);
	
КонецПроцедуры // СортироватьНаборЗаписей
 
// Процедура устанавливает подменю "Заполнить" в командных панелях ТЧ документа при необходимости
//
Процедура УстановитьКнопкиПодменюЗаполненияТЧ();
	
	мКнопкиЗаполненияТЧ = УниверсальныеМеханизмы.ПолучитьДеревоКнопокЗаполненияТабличныхЧастей(Ссылка,Новый Действие("НажатиеНаДополнительнуюКнопкуЗаполненияТЧ"));
	
	СоответствиеТЧ = Новый Соответствие;
	
	УниверсальныеМеханизмы.СформироватьПодменюЗаполненияТЧПоДеревуКнопок(мКнопкиЗаполненияТЧ,СоответствиеТЧ);
	
КонецПроцедуры

// Процедура создает и настраивает табличные поля на страницах панели ПанельРегистровНакопления
//
Процедура НастроитьЗакладкиПанелиРегистровНакопления(Отказ)
	
	ПроверитьВозможностьЗаписи(ТаблицаРегистровНакопления, "РегистрНакопления");
	
	Если ТаблицаРегистровНакопления.Количество() = 0 Тогда
		
		ЭлементыФормы.ПанельРегистровНакопления.Страницы.Подсказка.Видимость = Истина;
		Сч = 0;
		Пока Сч < ЭлементыФормы.ПанельРегистровНакопления.Страницы.Количество() Цикл
			Если ЭлементыФормы.ПанельРегистровНакопления.Страницы[Сч].Имя<>"Подсказка" Тогда
				ЭлементыФормы.ПанельРегистровНакопления.Страницы.Удалить(Сч);
			Иначе
				Сч = Сч + 1;
			КонецЕсли;
		КонецЦикла;
		ЭлементыФормы.ПанельРегистровНакопления.ОтображениеЗакладок = ОтображениеЗакладок.НеИспользовать;
		
	Иначе
		// Добавим новые страницы
		Для каждого СтрокаТаблицыРегистров Из ТаблицаРегистровНакопления Цикл
			Найдена = Ложь;
			Для каждого СтраницаПанели Из ЭлементыФормы.ПанельРегистровНакопления.Страницы Цикл
				Если СтраницаПанели.Имя = СтрокаТаблицыРегистров.Имя Тогда
					Найдена = Истина;
					Прервать;
				КонецЕсли;
			КонецЦикла; 
			Если Найдена Тогда
				Продолжить;
			КонецЕсли; 
			
			СтраницаПанели = ЭлементыФормы.ПанельРегистровНакопления.Страницы.Вставить(ТаблицаРегистровНакопления.Индекс(СтрокаТаблицыРегистров), СтрокаТаблицыРегистров.Имя, СтрокаТаблицыРегистров.Представление);
			
			ЭлементыФормы.ПанельРегистровНакопления.ТекущаяСтраница = СтраницаПанели;
			
			// Расположим на странице командную панель
			КоманднаяПанельРегистра = ЭлементыФормы.Добавить(Тип("КоманднаяПанель"), ("КоманднаяПанель" + СтрокаТаблицыРегистров.Имя), Истина, ЭлементыФормы.ПанельРегистровНакопления);
			КоманднаяПанельРегистра.Верх = 6;
			КоманднаяПанельРегистра.Лево = 6;
			КоманднаяПанельРегистра.Ширина = ЭлементыФормы.ПанельРегистровНакопления.Ширина - 12 - 2;
			КоманднаяПанельРегистра.Высота = 24;
			
			КоманднаяПанельРегистра.УстановитьПривязку(ГраницаЭлементаУправления.Верх,ЭлементыФормы.ПанельРегистровНакопления,ГраницаЭлементаУправления.Верх);
			КоманднаяПанельРегистра.УстановитьПривязку(ГраницаЭлементаУправления.Низ,КоманднаяПанельРегистра,ГраницаЭлементаУправления.Верх);
			КоманднаяПанельРегистра.УстановитьПривязку(ГраницаЭлементаУправления.Право,ЭлементыФормы.ПанельРегистровНакопления,ГраницаЭлементаУправления.Право);
			КоманднаяПанельРегистра.УстановитьПривязку(ГраницаЭлементаУправления.Лево,ЭлементыФормы.ПанельРегистровНакопления,ГраницаЭлементаУправления.Лево);
			
			// Расположим на странице табличное поле
			ПолеРегистра = ЭлементыФормы.Добавить(Тип("ТабличноеПоле"), СтрокаТаблицыРегистров.Имя, Истина, ЭлементыФормы.ПанельРегистровНакопления);
			ПолеРегистра.Данные = "ДокументОбъект.Движения." + СтрокаТаблицыРегистров.Имя;
			
			Если НЕ ЭтоНовый() и НЕ Модифицированность() Тогда
				Попытка
				
					ПолеРегистра.Значение.Прочитать();
				
				Исключение
					Предупреждение("Нарушение прав доступа");
					Отказ = Истина;
					Возврат; 
				КонецПопытки; 
			КонецЕсли; 
			
			ПолеРегистра.Верх = 30;
			ПолеРегистра.Лево = 6;
			ПолеРегистра.Ширина = ЭлементыФормы.ПанельРегистровНакопления.Ширина - 12 - 2;
			ПолеРегистра.Высота = ЭлементыФормы.ПанельРегистровНакопления.Высота - 24 - 12 - 20;
			ПолеРегистра.ТолькоПросмотр = Ложь;
			ПолеРегистра.ИзменятьПорядокСтрок = Истина;
			ПолеРегистра.ИзменятьСоставСтрок = Истина;
			ПолеРегистра.РежимВыделения = РежимВыделенияТабличногоПоля.Множественный;
			
			ПолеРегистра.УстановитьПривязку(ГраницаЭлементаУправления.Верх,ЭлементыФормы.ПанельРегистровНакопления,ГраницаЭлементаУправления.Верх);
			ПолеРегистра.УстановитьПривязку(ГраницаЭлементаУправления.Низ,ЭлементыФормы.ПанельРегистровНакопления,ГраницаЭлементаУправления.Низ);
			ПолеРегистра.УстановитьПривязку(ГраницаЭлементаУправления.Право,ЭлементыФормы.ПанельРегистровНакопления,ГраницаЭлементаУправления.Право);
			ПолеРегистра.УстановитьПривязку(ГраницаЭлементаУправления.Лево,ЭлементыФормы.ПанельРегистровНакопления,ГраницаЭлементаУправления.Лево);
			
			ПолеРегистра.СоздатьКолонки();
			
			Если Метаданные.РегистрыНакопления[СтрокаТаблицыРегистров.Имя].ВидРегистра = Метаданные.СвойстваОбъектов.ВидРегистраНакопления.Остатки Тогда
				КолонкаВидДвижения = ПолеРегистра.Колонки.Вставить(1, "Вид движения");
				КолонкаВидДвижения.Имя = "ВидДвиженияРегистраНакопления";
				КолонкаВидДвижения.УстановитьЭлементУправления(Тип("ПолеВвода"));
				КолонкаВидДвижения.Данные = "ВидДвижения";
				КолонкаВидДвижения.ЭлементУправления.КнопкаВыбора = Истина;
				КолонкаВидДвижения.ЭлементУправления.ВыбиратьТип = Ложь;
			КонецЕсли; 
			
			ПолеРегистра.Колонки.Регистратор.Видимость = Ложь;
			ПолеРегистра.Колонки.Период.Видимость      = Ложь;
			ПолеРегистра.Колонки.НомерСтроки.Видимость = Ложь;
			
			КоманднаяПанельРегистра.ИсточникДействий = ПолеРегистра;
			КоманднаяПанельРегистра.АвтоЗаполнение = Истина;
			
			КоманднаяПанельРегистра.Кнопки.Добавить("Разделитель1" + СтрокаТаблицыРегистров.Имя);
			
			Кнопка = КоманднаяПанельРегистра.Кнопки.Добавить(СтрокаТаблицыРегистров.Имя + "СортироватьПоВозрастанию", ТипКнопкиКоманднойПанели.Действие, "Сортировать по возрастанию", Новый Действие("СортироватьНаборЗаписейПоВозрастанию"));
			Кнопка.Отображение = ОтображениеКнопкиКоманднойПанели.Картинка;
			Кнопка.Картинка    = БиблиотекаКартинок.СортироватьСписокПоВозрастанию;
			Кнопка.Подсказка   = "Упорядочить по возрастанию";
			
			Кнопка = КоманднаяПанельРегистра.Кнопки.Добавить(СтрокаТаблицыРегистров.Имя + "СортироватьПоУбыванию", ТипКнопкиКоманднойПанели.Действие, "Сортировать по убыванию" , Новый Действие("СортироватьНаборЗаписейПоУбыванию"));
			Кнопка.Отображение = ОтображениеКнопкиКоманднойПанели.Картинка;
			Кнопка.Картинка    = БиблиотекаКартинок.СортироватьСписокПоУбыванию;
			Кнопка.Подсказка   = "Упорядочить по убыванию";  
			
			КоманднаяПанельРегистра.Кнопки.Добавить("Разделитель" + СтрокаТаблицыРегистров.Имя);
			
			Кнопка = КоманднаяПанельРегистра.Кнопки.Добавить(СтрокаТаблицыРегистров.Имя, ТипКнопкиКоманднойПанели.Действие, , Новый Действие("ПереключитьАктивность"));
			Кнопка.Отображение = ОтображениеКнопкиКоманднойПанели.Картинка;
			Кнопка.Картинка    = БиблиотекаКартинок.ПереключитьАктивность;
			Кнопка.Подсказка   = "Переключить активность";
			
			Если СтрокаТаблицыРегистров.Имя = "СвободныеОстатки" Тогда
				ДобавитьКнопкуЗаполненияСвободныхОстатков(КоманднаяПанельРегистра);
			КонецЕсли; 
			
		КонецЦикла; 
		
		// Удалим лишние страницы
		Индекс = 0;
		Пока Индекс <= ЭлементыФормы.ПанельРегистровНакопления.Страницы.Количество() - 2 Цикл
			
			СтраницаПанели = ЭлементыФормы.ПанельРегистровНакопления.Страницы[Индекс];
			
			Если ТаблицаРегистровНакопления.Найти(СтраницаПанели.Имя, "Имя") = Неопределено Тогда
				ЭлементыФормы.ПанельРегистровНакопления.Страницы.Удалить(Индекс);
				Продолжить;
			КонецЕсли;
			
			Индекс = Индекс + 1;
		
		КонецЦикла; 
		
		Если ЭлементыФормы.ПанельРегистровНакопления.Страницы.Количество() > 1 Тогда
			
		КонецЕсли; 
		ЭлементыФормы.ПанельРегистровНакопления.ОтображениеЗакладок = ОтображениеЗакладок.Сверху;
		ЭлементыФормы.ПанельРегистровНакопления.Страницы.Подсказка.Видимость = Ложь;
		ЭлементыФормы.ПанельРегистровНакопления.ТекущаяСтраница = ЭлементыФормы.ПанельРегистровНакопления.Страницы[0];
		
	КонецЕсли; 
	
КонецПроцедуры

// Процедура создает и настраивает табличные поля на страницах панели ПанельРегистровСведений
//
Процедура НастроитьЗакладкиПанелиРегистровСведений(Отказ)
	
	ПроверитьВозможностьЗаписи(ТаблицаРегистровСведений, "РегистрСведений");
	
	Если ТаблицаРегистровСведений.Количество() = 0 Тогда
		
		ЭлементыФормы.ПанельРегистровСведений.Страницы.Подсказка.Видимость = Истина;
		Сч = 0;
		Пока Сч < ЭлементыФормы.ПанельРегистровСведений.Страницы.Количество() Цикл
			Если ЭлементыФормы.ПанельРегистровСведений.Страницы[Сч].Имя<>"Подсказка" Тогда
				ЭлементыФормы.ПанельРегистровСведений.Страницы.Удалить(Сч);
			Иначе
				Сч = Сч + 1;
			КонецЕсли;
		КонецЦикла;
		ЭлементыФормы.ПанельРегистровСведений.ОтображениеЗакладок = ОтображениеЗакладок.НеИспользовать;
		
	Иначе
		// Добавим новые страницы
		ЭлементыФормы.ПанельРегистровСведений.ОтображениеЗакладок = ОтображениеЗакладок.Сверху;
		Для каждого СтрокаТаблицыРегистров Из ТаблицаРегистровСведений Цикл
			Найдена = Ложь;
			Для каждого СтраницаПанели Из ЭлементыФормы.ПанельРегистровСведений.Страницы Цикл
				Если СтраницаПанели.Имя = СтрокаТаблицыРегистров.Имя Тогда
					Найдена = Истина;
					Прервать;
				КонецЕсли;
			КонецЦикла; 
			Если Найдена Тогда
				Продолжить;
			КонецЕсли; 
			
			СтраницаПанели = ЭлементыФормы.ПанельРегистровСведений.Страницы.Вставить(ТаблицаРегистровСведений.Индекс(СтрокаТаблицыРегистров), СтрокаТаблицыРегистров.Имя, СтрокаТаблицыРегистров.Представление);
			
			ЭлементыФормы.ПанельРегистровСведений.ТекущаяСтраница = СтраницаПанели;
			
			// Расположим на странице командную панель
			КоманднаяПанельРегистра = ЭлементыФормы.Добавить(Тип("КоманднаяПанель"), ("КоманднаяПанель" + СтрокаТаблицыРегистров.Имя), Истина, ЭлементыФормы.ПанельРегистровСведений);
			КоманднаяПанельРегистра.Верх = 6;
			КоманднаяПанельРегистра.Лево = 6;
			КоманднаяПанельРегистра.Ширина = ЭлементыФормы.ПанельРегистровСведений.Ширина - 12 - 2;
			КоманднаяПанельРегистра.Высота = 24;
			
			КоманднаяПанельРегистра.УстановитьПривязку(ГраницаЭлементаУправления.Верх,ЭлементыФормы.ПанельРегистровСведений,ГраницаЭлементаУправления.Верх);
			КоманднаяПанельРегистра.УстановитьПривязку(ГраницаЭлементаУправления.Низ,КоманднаяПанельРегистра,ГраницаЭлементаУправления.Верх);
			КоманднаяПанельРегистра.УстановитьПривязку(ГраницаЭлементаУправления.Право,ЭлементыФормы.ПанельРегистровСведений,ГраницаЭлементаУправления.Право);
			КоманднаяПанельРегистра.УстановитьПривязку(ГраницаЭлементаУправления.Лево,ЭлементыФормы.ПанельРегистровСведений,ГраницаЭлементаУправления.Лево);
			
			// Расположим на странице табличное поле
			ПолеРегистра = ЭлементыФормы.Добавить(Тип("ТабличноеПоле"), СтрокаТаблицыРегистров.Имя, Истина, ЭлементыФормы.ПанельРегистровСведений);
			ПолеРегистра.Данные = "ДокументОбъект.Движения." + СтрокаТаблицыРегистров.Имя;
			
			Если НЕ ЭтоНовый() и НЕ Модифицированность() Тогда
				Попытка
				
					ПолеРегистра.Значение.Прочитать();
				
				Исключение
					Предупреждение("Нарушение прав доступа");
					Отказ = Истина;
					Возврат; 
				КонецПопытки; 
			КонецЕсли; 
			
			ПолеРегистра.Верх = 30;
			ПолеРегистра.Лево = 6;
			ПолеРегистра.Ширина = ЭлементыФормы.ПанельРегистровСведений.Ширина - 12 - 2;
			ПолеРегистра.Высота = ЭлементыФормы.ПанельРегистровСведений.Высота - 24 - 12 - 20;
			ПолеРегистра.ТолькоПросмотр = Ложь;
			ПолеРегистра.ИзменятьПорядокСтрок = Истина;
			ПолеРегистра.ИзменятьСоставСтрок = Истина;
			ПолеРегистра.РежимВыделения = РежимВыделенияТабличногоПоля.Множественный;
			
			ПолеРегистра.УстановитьПривязку(ГраницаЭлементаУправления.Верх,ЭлементыФормы.ПанельРегистровСведений,ГраницаЭлементаУправления.Верх);
			ПолеРегистра.УстановитьПривязку(ГраницаЭлементаУправления.Низ,ЭлементыФормы.ПанельРегистровСведений,ГраницаЭлементаУправления.Низ);
			ПолеРегистра.УстановитьПривязку(ГраницаЭлементаУправления.Право,ЭлементыФормы.ПанельРегистровСведений,ГраницаЭлементаУправления.Право);
			ПолеРегистра.УстановитьПривязку(ГраницаЭлементаУправления.Лево,ЭлементыФормы.ПанельРегистровСведений,ГраницаЭлементаУправления.Лево);
			
			ПолеРегистра.СоздатьКолонки();
			
			ПолеРегистра.Колонки.Регистратор.Видимость = Ложь;
			ПолеРегистра.Колонки.НомерСтроки.Видимость = Ложь;
			
			КоманднаяПанельРегистра.ИсточникДействий = ПолеРегистра;
			КоманднаяПанельРегистра.АвтоЗаполнение = Истина;
			
			КоманднаяПанельРегистра.Кнопки.Добавить("Разделитель1" + СтрокаТаблицыРегистров.Имя);
			
			Кнопка = КоманднаяПанельРегистра.Кнопки.Добавить(СтрокаТаблицыРегистров.Имя + "СортироватьПоВозрастанию", ТипКнопкиКоманднойПанели.Действие, "Сортировать по возрастанию", Новый Действие("СортироватьНаборЗаписейПоВозрастанию"));
			Кнопка.Отображение = ОтображениеКнопкиКоманднойПанели.Картинка;
			Кнопка.Картинка    = БиблиотекаКартинок.СортироватьСписокПоВозрастанию;
			Кнопка.Подсказка   = "Упорядочить по возрастанию";
			
			Кнопка = КоманднаяПанельРегистра.Кнопки.Добавить(СтрокаТаблицыРегистров.Имя + "СортироватьПоУбыванию", ТипКнопкиКоманднойПанели.Действие, "Сортировать по убыванию" , Новый Действие("СортироватьНаборЗаписейПоУбыванию"));
			Кнопка.Отображение = ОтображениеКнопкиКоманднойПанели.Картинка;
			Кнопка.Картинка    = БиблиотекаКартинок.СортироватьСписокПоУбыванию;
			Кнопка.Подсказка   = "Упорядочить по убыванию";  
			
			КоманднаяПанельРегистра.Кнопки.Добавить("Разделитель" + СтрокаТаблицыРегистров.Имя);
			
			Кнопка = КоманднаяПанельРегистра.Кнопки.Добавить(СтрокаТаблицыРегистров.Имя, ТипКнопкиКоманднойПанели.Действие, , Новый Действие("ПереключитьАктивность"));
			Кнопка.Отображение = ОтображениеКнопкиКоманднойПанели.Картинка;
			Кнопка.Картинка    = БиблиотекаКартинок.ПереключитьАктивность;
			Кнопка.Подсказка   = "Переключить активность";
			
		КонецЦикла; 
		
		// Удалим лишние страницы
		Индекс = 0;
		Пока Индекс <= ЭлементыФормы.ПанельРегистровСведений.Страницы.Количество() - 2 Цикл
			
			СтраницаПанели = ЭлементыФормы.ПанельРегистровСведений.Страницы[Индекс];
			
			Если ТаблицаРегистровСведений.Найти(СтраницаПанели.Имя, "Имя") = Неопределено Тогда
				ЭлементыФормы.ПанельРегистровСведений.Страницы.Удалить(Индекс);
				Продолжить;
			КонецЕсли;
			
			Индекс = Индекс + 1;
		
		КонецЦикла; 
		
		ЭлементыФормы.ПанельРегистровСведений.Страницы.Подсказка.Видимость = Ложь;
		ЭлементыФормы.ПанельРегистровСведений.ТекущаяСтраница = ЭлементыФормы.ПанельРегистровСведений.Страницы[0];
		
	КонецЕсли; 
	
КонецПроцедуры

// Процедура создает и настраивает табличные поля на страницах панели ПанельРегистровБухгалтерии
//
Процедура НастроитьЗакладкиПанелиРегистровБухгалтерии()
	
	Если ТаблицаРегистровБухгалтерии.Количество() = 0 Тогда
		
		Для Каждого СтраницаПанели ИЗ ЭлементыФормы.ПанельРегистровБухгалтерии.Страницы Цикл
			СтраницаПанели.Видимость = Ложь;
		КонецЦикла;
		ЭлементыФормы.ПанельРегистровБухгалтерии.Страницы.Подсказка.Видимость = Истина;
		ЭлементыФормы.ПанельРегистровБухгалтерии.ОтображениеЗакладок = ОтображениеЗакладок.НеИспользовать;
		
	Иначе
		ЭлементыФормы.ПанельРегистровБухгалтерии.ОтображениеЗакладок = ОтображениеЗакладок.Сверху;
		Для каждого СтрокаТаблицыРегистров Из ТаблицаРегистровБухгалтерии Цикл
			ЭлементыФормы.ПанельРегистровБухгалтерии.Страницы[СтрокаТаблицыРегистров.Имя].Видимость = Истина;
		КонецЦикла;
		
		Индекс = 0;
		Пока Индекс <= ЭлементыФормы.ПанельРегистровБухгалтерии.Страницы.Количество() - 2 Цикл
			
			СтраницаПанели = ЭлементыФормы.ПанельРегистровБухгалтерии.Страницы[Индекс];
			
			Если ТаблицаРегистровБухгалтерии.Найти(СтраницаПанели.Имя, "Имя") = Неопределено Тогда
				ЭлементыФормы.ПанельРегистровБухгалтерии.Страницы[СтраницаПанели.Имя].Видимость = Ложь;
			КонецЕсли;
			
			Индекс = Индекс + 1;
		
		КонецЦикла; 
		
		ЭлементыФормы.ПанельРегистровБухгалтерии.Страницы.Подсказка.Видимость = Ложь;
		ЭлементыФормы.ПанельРегистровБухгалтерии.ТекущаяСтраница = ЭлементыФормы.ПанельРегистровБухгалтерии.Страницы[0];
		
	КонецЕсли; 
	
КонецПроцедуры

Процедура НастроитьЗакладкиПанелейРегистров(Отказ)
	
	НастроитьЗакладкиПанелиРегистровНакопления(Отказ);
	НастроитьЗакладкиПанелиРегистровСведений(Отказ);
	НастроитьЗакладкиПанелиРегистровБухгалтерии();
	
КонецПроцедуры // НастроитьЗакладкиПанелейРегистров() 

Процедура ПереключитьАктивность(Кнопка)

	ТекущаяАктивность = Ложь;
	Если ЭлементыФормы[Кнопка.Имя].Значение.Количество() > 0 Тогда
		ТекущаяАктивность = ЭлементыФормы[Кнопка.Имя].Значение[0].Активность;
	КонецЕсли;
	ЭлементыФормы[Кнопка.Имя].Значение.УстановитьАктивность(НЕ ТекущаяАктивность);

КонецПроцедуры

// Процедура заполняет список возможных действий табличной части Выполняемые действия
//
Процедура ОбновитьСписокДействий()
	
	СписокДействий = Новый СписокЗначений;
	СписокДействий.Добавить("Сторно движений документа");
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ВнешниеОбработкиПринадлежность.Ссылка,
	|	ВнешниеОбработкиПринадлежность.Ссылка.Наименование КАК Наименование
	|ИЗ
	|	Справочник.ВнешниеОбработки.Принадлежность КАК ВнешниеОбработкиПринадлежность
	|ГДЕ
	|	(НЕ ВнешниеОбработкиПринадлежность.Ссылка.ПометкаУдаления)
	|	И ВнешниеОбработкиПринадлежность.МетаданныеОбъекта = &МетаданныеОбъекта
	|	И ВнешниеОбработкиПринадлежность.Ссылка.ВидОбработки = &ВидОбработки
	|
	|УПОРЯДОЧИТЬ ПО
	|	Наименование";
	
	Запрос.УстановитьПараметр("ВидОбработки", Перечисления.ВидыДополнительныхВнешнихОбработок.ЗаполнениеТабличныхЧастей);
	Запрос.УстановитьПараметр("МетаданныеОбъекта", "Документ.КорректировкаЗаписейРегистров");
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		СписокДействий.Добавить(Выборка.Ссылка, Выборка.Наименование);
	КонецЦикла;
	
	мСписокДействий = СписокДействий;
	
КонецПроцедуры

// Пересчет валютной суммы в основную по курсу на указанную дату
//
Функция ПересчетСуммыПоКурсу(ВалютнаяСумма, Валюта, Дата) Экспорт

	Запись = РегистрыСведений.КурсыВалют.ПолучитьПоследнее(Дата, Новый Структура("Валюта", Валюта));
	Сумма  = ВалютнаяСумма * Запись.Курс;

	Если Запись.Кратность <> 0 Тогда
		Сумма = Сумма / ?(Запись.Кратность = 0, 1, Запись.Кратность);
	КонецЕсли;

	Возврат Сумма;

КонецФункции // ПересчетСуммыПоКурсу()

// Заполнение параметров формы настройки спсика регистров перед открытием
//
Процедура ИнициализацияФормыНастройки(ФормаНастройки,ИмяТекущейСтраницы = "")
	
	ФормаНастройки.СписокРегистровНакопления.Очистить();
	ФормаНастройки.СписокРегистровСведений.Очистить();
	ФормаНастройки.СписокРегистровБухгалтерии.Очистить();
	
	Для каждого Набор Из Движения Цикл
		
		МетаданныеНабора = Набор.Метаданные();
		Если НЕ ПравоДоступа("Изменение", МетаданныеНабора) Тогда
			Продолжить;
		КонецЕсли;
		
		Если Метаданные.РегистрыНакопления.Содержит(МетаданныеНабора) Тогда
			НовыйЭлементСписка = ФормаНастройки.СписокРегистровНакопления.Добавить(МетаданныеНабора.Имя, МетаданныеНабора.Синоним);
			Если МетаданныеНабора.ВидРегистра = Метаданные.СвойстваОбъектов.ВидРегистраНакопления.Остатки Тогда
				НовыйЭлементСписка.Картинка = БиблиотекаКартинок.РегистрНакопления_Остатки;
			Иначе
				НовыйЭлементСписка.Картинка = БиблиотекаКартинок.РегистрНакопления_Обороты;
			КонецЕсли;
			Если ТаблицаРегистровНакопления.Найти(МетаданныеНабора.Имя, "Имя") <> Неопределено Тогда
				НовыйЭлементСписка.Пометка = Истина;
			КонецЕсли; 
			
		ИначеЕсли Метаданные.РегистрыСведений.Содержит(МетаданныеНабора) Тогда
			НовыйЭлементСписка = ФормаНастройки.СписокРегистровСведений  .Добавить(МетаданныеНабора.Имя, МетаданныеНабора.Синоним);
			НовыйЭлементСписка.Картинка = БиблиотекаКартинок.РегистрСведений;
			Если ТаблицаРегистровСведений.Найти(МетаданныеНабора.Имя, "Имя") <> Неопределено Тогда
				НовыйЭлементСписка.Пометка = Истина;
			КонецЕсли; 
		ИначеЕсли Метаданные.РегистрыБухгалтерии.Содержит(МетаданныеНабора) Тогда
			НовыйЭлементСписка = ФормаНастройки.СписокРегистровБухгалтерии.Добавить(МетаданныеНабора.Имя, МетаданныеНабора.Синоним);
			НовыйЭлементСписка.Картинка = БиблиотекаКартинок.РегистрБухгалтерии;
			Если ТаблицаРегистровБухгалтерии.Найти(МетаданныеНабора.Имя, "Имя") <> Неопределено Тогда
				НовыйЭлементСписка.Пометка = Истина;
			КонецЕсли; 
		Иначе
			Продолжить;
		КонецЕсли;
		
	КонецЦикла;
	
	ФормаНастройки.ТЗРегистровНакопления  = ТаблицаРегистровНакопления.Выгрузить();
	ФормаНастройки.ТЗРегистровСведений    = ТаблицаРегистровСведений.Выгрузить();
	ФормаНастройки.ТЗРегистровБухгалтерии = ТаблицаРегистровБухгалтерии.Выгрузить();
	
	ФормаНастройки.ИмяТекущейСтраницы = ИмяТекущейСтраницы;
	
	ФормаНастройки.СписокРегистровНакопления.СортироватьПоПредставлению();
	ФормаНастройки.СписокРегистровСведений.СортироватьПоПредставлению();
	ФормаНастройки.СписокРегистровБухгалтерии.СортироватьПоПредставлению();
	
КонецПроцедуры

// Обработка результатов возвращаемых формой настройки списока регистров
//
Процедура ОбработкаНастройки(ФормаНастройки)
	
	//////////////////////
	// Регистры накопления
	//////////////////////
	
	// Сначала удалим те, у которых сняли флажки
	Индекс = 0;
	Пока Индекс <= ТаблицаРегистровНакопления.Количество() - 1 Цикл
		
		СтрокаТаблицы = ТаблицаРегистровНакопления[Индекс];
			
		ЭлементСписка = ФормаНастройки.СписокРегистровНакопления.НайтиПоЗначению(СтрокаТаблицы.Имя);
		Если ЭлементСписка = Неопределено Тогда
			Движения[СтрокаТаблицы.Имя].Очистить();
			ТаблицаРегистровНакопления.Удалить(СтрокаТаблицы);
			Продолжить;
		КонецЕсли; 
		Если НЕ ЭлементСписка.Пометка Тогда
			Движения[СтрокаТаблицы.Имя].Очистить();
			ТаблицаРегистровНакопления.Удалить(СтрокаТаблицы);
			Продолжить;
		КонецЕсли;
			
		Индекс = Индекс + 1;
		
	КонецЦикла;
		
	// Теперь добавим новые
	Для каждого ЭлементСписка Из ФормаНастройки.СписокРегистровНакопления Цикл
		
		Если НЕ ЭлементСписка.Пометка Тогда
			Продолжить;
		КонецЕсли;
			
		Если ТаблицаРегистровНакопления.Найти(ЭлементСписка.Значение, "Имя") <> Неопределено Тогда
			Продолжить;
		КонецЕсли;
			
		НоваяСтрока = ТаблицаРегистровНакопления.Добавить();
		НоваяСтрока.Имя = ЭлементСписка.Значение;
		НоваяСтрока.Представление = ЭлементСписка.Представление;
		
	КонецЦикла; 
		
	ТаблицаРегистровНакопления.Сортировать("Имя ВОЗР");
	
	//////////////////////
	// Регистры сведений
	//////////////////////
	
	// Сначала удалим те, у которых сняли флажки
	Индекс = 0;
	Пока Индекс <= ТаблицаРегистровСведений.Количество() - 1 Цикл
		
		СтрокаТаблицы = ТаблицаРегистровСведений[Индекс];
			
		ЭлементСписка = ФормаНастройки.СписокРегистровСведений.НайтиПоЗначению(СтрокаТаблицы.Имя);
		Если ЭлементСписка = Неопределено Тогда
			Движения[СтрокаТаблицы.Имя].Очистить();
			ТаблицаРегистровСведений.Удалить(СтрокаТаблицы);
			Продолжить;
		КонецЕсли; 
		Если НЕ ЭлементСписка.Пометка Тогда
			Движения[СтрокаТаблицы.Имя].Очистить();
			ТаблицаРегистровСведений.Удалить(СтрокаТаблицы);
			Продолжить;
		КонецЕсли;
			
		Индекс = Индекс + 1;
		
	КонецЦикла;
		
	// Теперь добавим новые
	Для каждого ЭлементСписка Из ФормаНастройки.СписокРегистровСведений Цикл
		
		Если НЕ ЭлементСписка.Пометка Тогда
			Продолжить;
		КонецЕсли;
			
		Если ТаблицаРегистровСведений.Найти(ЭлементСписка.Значение, "Имя") <> Неопределено Тогда
			Продолжить;
		КонецЕсли;
			
		НоваяСтрока = ТаблицаРегистровСведений.Добавить();
		НоваяСтрока.Имя = ЭлементСписка.Значение;
		НоваяСтрока.Представление = ЭлементСписка.Представление;
		
	КонецЦикла; 
		
	ТаблицаРегистровСведений.Сортировать("Имя ВОЗР");
	
	//////////////////////
	// Регистры бухгалтерии
	//////////////////////
	
	// Сначала удалим те, у которых сняли флажки
	Индекс = 0;
	Пока Индекс <= ТаблицаРегистровБухгалтерии.Количество() - 1 Цикл
		
		СтрокаТаблицы = ТаблицаРегистровБухгалтерии[Индекс];
			
		ЭлементСписка = ФормаНастройки.СписокРегистровБухгалтерии.НайтиПоЗначению(СтрокаТаблицы.Имя);
		Если ЭлементСписка = Неопределено Тогда
			Движения[СтрокаТаблицы.Имя].Очистить();
			ТаблицаРегистровБухгалтерии.Удалить(СтрокаТаблицы);
			Продолжить;
		КонецЕсли; 
		Если НЕ ЭлементСписка.Пометка Тогда
			Движения[СтрокаТаблицы.Имя].Очистить();
			ТаблицаРегистровБухгалтерии.Удалить(СтрокаТаблицы);
			Продолжить;
		КонецЕсли;
			
		Индекс = Индекс + 1;
		
	КонецЦикла;
		
	// Теперь добавим новые
	Для каждого ЭлементСписка Из ФормаНастройки.СписокРегистровБухгалтерии Цикл
		
		Если НЕ ЭлементСписка.Пометка Тогда
			Продолжить;
		КонецЕсли;
			
		Если ТаблицаРегистровБухгалтерии.Найти(ЭлементСписка.Значение, "Имя") <> Неопределено Тогда
			Продолжить;
		КонецЕсли;
			
		НоваяСтрока = ТаблицаРегистровБухгалтерии.Добавить();
		НоваяСтрока.Имя = ЭлементСписка.Значение;
		НоваяСтрока.Представление = ЭлементСписка.Представление;
		
	КонецЦикла; 
		
	ТаблицаРегистровБухгалтерии.Сортировать("Имя ВОЗР");
	
	НастроитьЗакладкиПанелейРегистров(Ложь);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ УПРАВЛЕНИЯ КОМАНДНОЙ ПАНЕЛИ ФОРМЫ

Процедура ДействияФормыРедактироватьНомер(Кнопка)
	МеханизмНумерацииОбъектов.ИзменениеВозможностиРедактированияНомера(ЭтотОбъект.Метаданные(), ЭтаФорма, ЭлементыФормы.ДействияФормы.Кнопки.Подменю, ЭлементыФормы.Номер);
КонецПроцедуры
// Обработчик события элемента ДействияФормы.Настройка.
// .
Процедура ДействияФормыНастройка(Кнопка)
	
	ФормаНастройки = ЭтотОбъект.ПолучитьФорму("ФормаНастройки", ЭтаФорма);
	
	ИнициализацияФормыНастройки(ФормаНастройки);
	
	Если Не ФормаНастройки.ОткрытьМодально() = Истина Тогда
		Возврат;
	КонецЕсли; 
	
	ОбработкаНастройки(ФормаНастройки);
	
	ОбновлениеОтображения();
	
КонецПроцедуры

// Процедура вызывается при выборе пункта подменю "Движения документа по регистрам" меню "Перейти".
// командной панели формы. Процедура отрабатывает печать движений документа по регистрам.
//
Процедура ДействияФормыДвиженияДокументаПоРегистрам(Кнопка)

	РаботаСДиалогами.НапечататьДвиженияДокумента(Ссылка);

КонецПроцедуры // ДействияФормыДвиженияДокументаПоРегистрам()


////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ФОРМЫ

// Процедура - обработчик события "ПриИзменении" поля ввода даты документа.
//
Процедура ДатаПриИзменении(Элемент)
	
	РаботаСДиалогами.ПроверитьНомерДокумента(ЭтотОбъект, мТекущаяДатаДокумента);
	МеханизмНумерацииОбъектов.ОбновитьПодсказкуКодНомерОбъекта(ЭтотОбъект.Метаданные(), ЭлементыФормы.ДействияФормы.Кнопки.Подменю, ЭлементыФормы.Номер);

	мТекущаяДатаДокумента = Дата; // запомним текущую дату документа для контроля номера документа
	
КонецПроцедуры

// Процедура - обработчик события "ПриИзменении" флажка ИспользоватьЗаполнениеДвижений
//
Процедура ИспользоватьЗаполнениеДвиженийПриИзменении(Элемент)
	
	Если ИспользоватьЗаполнениеДвижений Тогда
		ЭлементыФормы.ПанельЗаполнениеДвижений.ТекущаяСтраница = ЭлементыФормы.ПанельЗаполнениеДвижений.Страницы.ТаблицаЗаполнениеДвижений;
	Иначе
		Если ЗаполнениеДвижений.Количество()>0 Тогда
			Ответ = Вопрос("Табличная часть ""Заполнение движений"" будет очищена.",РежимДиалогаВопрос.ОКОтмена);
			Если Ответ = КодВозвратаДиалога.Отмена Тогда
				ИспользоватьЗаполнениеДвижений = Истина;
				Возврат;
			КонецЕсли;
			
			ЗаполнениеДвижений.Очистить();
		КонецЕсли;
		ЭлементыФормы.ПанельЗаполнениеДвижений.ТекущаяСтраница = ЭлементыФормы.ПанельЗаполнениеДвижений.Страницы.ПодсказкаЗаполнениеДвижений;
	КонецЕсли;
	
КонецПроцедуры

// Процедура - обработчик события "Нажатие" гиперссылки НастройкаСоставаРегистровСведений
//
Процедура ГиперссылкаНастройкаСоставаРегистровСведенийНажатие(Элемент)
	
	ФормаНастройки = ЭтотОбъект.ПолучитьФорму("ФормаНастройки", ЭтаФорма);
	
	ИнициализацияФормыНастройки(ФормаНастройки,"Сведений");
	
	Если Не ФормаНастройки.ОткрытьМодально() = Истина Тогда
		Возврат;
	КонецЕсли; 
	
	ОбработкаНастройки(ФормаНастройки);
	
	ОбновлениеОтображения();
	
КонецПроцедуры

// Процедура - обработчик события "Нажатие" гиперссылки НастройкаСоставаРегистровНакопления
//
Процедура ГиперссылкаНастройкаСоставаРегистровНакопленияНажатие(Элемент)

	ФормаНастройки = ЭтотОбъект.ПолучитьФорму("ФормаНастройки", ЭтаФорма);
	
	ИнициализацияФормыНастройки(ФормаНастройки,"Накопления");
	
	Если Не ФормаНастройки.ОткрытьМодально() = Истина Тогда
		Возврат;
	КонецЕсли; 
	
	ОбработкаНастройки(ФормаНастройки);
	
	ОбновлениеОтображения();
	
КонецПроцедуры

// Процедура - обработчик события "Нажатие" гиперссылки НастройкаСоставаРегистровБухгалтерии
//
Процедура ГиперссылкаНастройкаСоставаРегистровБухгалтерииНажатие(Элемент)
	
	ФормаНастройки = ЭтотОбъект.ПолучитьФорму("ФормаНастройки", ЭтаФорма);
	
	ИнициализацияФормыНастройки(ФормаНастройки,"Бухгалтерии");
	
	Если Не ФормаНастройки.ОткрытьМодально() = Истина Тогда
		Возврат;
	КонецЕсли; 
	
	ОбработкаНастройки(ФормаНастройки);
	
	ОбновлениеОтображения();
	
КонецПроцедуры


////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ РЕКВИЗИТОВ ТЧ ВЫПОНЯЕМЫЕ ДЕЙСТВИЯ

// Процедура - обработчик события "НачалоВыбора" поля ввода "Действие"
// в строке табличной части "Выполняемые действия".
//
Процедура ЗаполнениеДвиженийДействиеНачалоВыбора(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ВыбранноеДействие = мСписокДействий.ВыбратьЭлемент("Выберите действие");
	Если ВыбранноеДействие <> неопределено Тогда
		Элемент.Значение = ВыбранноеДействие.Значение;
	КонецЕсли;
	
КонецПроцедуры


////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ РЕКВИЗИТОВ ТЧ ПроводкиБУ и ПроводкиНУ

// Процедура - обработчик события "ПриИзменении" поля Организация
//
Процедура ТабличноеПолеДвиженияБУОрганизацияПриИзменении(Элемент)
	
	ТипБС = Тип("СправочникСсылка.БанковскиеСчета");
	ТипПО = Тип("СправочникСсылка.ПодразделенияОрганизаций");

	Проводка = ЭлементыФормы.ТабличноеПолеДвиженияБУ.ТекущиеДанные;

	Если ЗначениеЗаполнено(Проводка.СубконтоДт.БанковскиеСчета) Тогда
		Проводка.СубконтоДт.БанковскиеСчета = Новый(ТипБС);
	КонецЕсли;

	Если ЗначениеЗаполнено(Проводка.СубконтоДт.Подразделения) Тогда
		Проводка.СубконтоДт.Подразделения = Новый(ТипПО);
	КонецЕсли;

	Если ЗначениеЗаполнено(Проводка.СубконтоКт.БанковскиеСчета) Тогда
		Проводка.СубконтоКт.БанковскиеСчета = Новый(ТипБС);
	КонецЕсли;

	Если ЗначениеЗаполнено(Проводка.СубконтоКт.Подразделения) Тогда
		Проводка.СубконтоКт.Подразделения = Новый(ТипПО);
	КонецЕсли;
	
КонецПроцедуры

// Процедура - обработчик события "Нажатия" кнопки ЗАполнитьБУ по НУ
//
Процедура КоманднаяПанельБУЗаполнитьНУ(Кнопка)

	Если ТаблицаРегистровБухгалтерии.Найти("Налоговый", "Имя") = Неопределено Тогда
		НоваяСтрока = ТаблицаРегистровБухгалтерии.Добавить();
		НоваяСтрока.Имя = "Налоговый";
		НоваяСтрока.Представление = "Журнал проводок (налоговый учет)";
		ЭлементыФормы.ПанельРегистровБухгалтерии.Страницы.Налоговый.Видимость = Истина;
	КонецЕсли;
	
	Если Движения.Хозрасчетный.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;

	Если ЭлементыФормы.ТабличноеПолеДвиженияНУ.Значение.Количество() > 0 Тогда
		Ответ = Вопрос("Имеющиеся данные на закладке ""Налоговый учет"" будут удалены и заполнены по данным бухгалтерского учета
						|Продолжить?", 
						РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Нет,
						"Заполнение данных налогового учета");

		Если Ответ = КодВозвратаДиалога.Да Тогда
			Движения.Налоговый.Очистить();
		Иначе
			Возврат;
		КонецЕсли;
		
	КонецЕсли;

    НалоговыйУчет.ЗаполнитьДанныеНалоговогоУчетаПоБухгалтерскомуУчету(Движения.Налоговый, Движения.Хозрасчетный, Дата);

КонецПроцедуры // КоманднаяПанельБУЗаполнитьНУ()

// Процедура - обработчик события "ПриНачалеРедактирования" элемента ТабличноеПолеДвиженияБУ, ТабличноеПолеДвиженияНУ,
//			ТабличноеПолеДвиженияМУ, ТабличноеПолеДвиженияБюджетирование
Процедура ТабличноеПолеРегистраБухгалтерииПриНачалеРедактирования(Элемент, НоваяСтрока)

	Строка = Элемент.ТекущаяСтрока;

	Строка.Период      = Дата;

КонецПроцедуры

// Процедура - обработчик события "ОбработкаВыбора" поля СчетДт
//
Процедура ТабличноеПолеДвиженияБУСчетДтОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)

	СтандартнаяОбработка = БухгалтерскийУчет.СчетМожноИспользоватьВПроводках(ВыбранноеЗначение);

КонецПроцедуры

// Процедура - обработчик события "ОбработкаВыбора" поля СчетКт
//
Процедура ТабличноеПолеДвиженияБУСчетКтОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)

	СтандартнаяОбработка = БухгалтерскийУчет.СчетМожноИспользоватьВПроводках(ВыбранноеЗначение);

КонецПроцедуры

// Процедура - обработчик события "ПриИзменении" поля ВалютаДт
//
Процедура ТабличноеПолеДвиженияБУВалютаДтПриИзменении(Элемент)

	ТД       = ЭлементыФормы.ТабличноеПолеДвиженияБУ.ТекущиеДанные;
	ТД.Сумма = ПересчетСуммыПоКурсу(ТД.ВалютнаяСуммаДт, ТД.ВалютаДт, Дата);

КонецПроцедуры

// Процедура - обработчик события "ПриИзменении" поля ВалютаКт
//
Процедура ТабличноеПолеДвиженияБУВалютаКтПриИзменении(Элемент)

	ТД = ЭлементыФормы.ТабличноеПолеДвиженияБУ.ТекущиеДанные;

	Если НЕ ТД.СчетДт.Валютный Тогда
		ТД.Сумма = ПересчетСуммыПоКурсу(ТД.ВалютнаяСуммаКт, ТД.ВалютаКт, Дата);
	КонецЕсли;

КонецПроцедуры

// Процедура - обработчик события "НачалоВыбора" поля СубконтоДт1
//
Процедура ТабличноеПолеДвиженияБУСубконтоДт1НачалоВыбора(Элемент, СтандартнаяОбработка)

	Проводка = ЭлементыФормы.ТабличноеПолеДвиженияБУ.ТекущиеДанные;
	БухгалтерскийУчет.ОбработатьВыборПервогоСубконто(Элемент, СтандартнаяОбработка, Проводка.Организация);

КонецПроцедуры

// Процедура - обработчик события "НачалоВыбора" поля СубконтоКт1
//
Процедура ТабличноеПолеДвиженияБУСубконтоКт1НачалоВыбора(Элемент, СтандартнаяОбработка)

	Проводка = ЭлементыФормы.ТабличноеПолеДвиженияБУ.ТекущиеДанные;
	БухгалтерскийУчет.ОбработатьВыборПервогоСубконто(Элемент, СтандартнаяОбработка, Проводка.Организация);

КонецПроцедуры


// Процедура - обработчик события "ОбработкаВыбора" поля СчетДт
//
Процедура ТабличноеПолеДвиженияНУСчетДтОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)

	СтандартнаяОбработка = БухгалтерскийУчет.СчетМожноИспользоватьВПроводках(ВыбранноеЗначение);
	
КонецПроцедуры

// Процедура - обработчик события "ПриИзменении" поля СчетДт
//
Процедура ТабличноеПолеДвиженияНУСчетДтПриИзменении(Элемент)
	Строка = ЭлементыФормы.ТабличноеПолеДвиженияНУ.ТекущиеДанные;
	Если (ЗначениеЗаполнено(Строка.СчетДт)) И НЕ ЗначениеЗаполнено(Строка.ВидУчетаДт) Тогда
		Строка.ВидУчетаДт = Перечисления.ВидыУчетаПоПБУ18.НУ;
	ИначеЕсли НЕ ЗначениеЗаполнено(Строка.СчетДт) Тогда
		Строка.ВидУчетаДт = Перечисления.ВидыУчетаПоПБУ18.ПустаяСсылка();
	КонецЕсли;
КонецПроцедуры

// Процедура - обработчик события "ОбработкаВыбора" поля СчетКт
//
Процедура ТабличноеПолеДвиженияНУСчетКтОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)

	СтандартнаяОбработка = БухгалтерскийУчет.СчетМожноИспользоватьВПроводках(ВыбранноеЗначение);
	
КонецПроцедуры

// Процедура - обработчик события "ПриИзменении" поля СчетКт
//
Процедура ТабличноеПолеДвиженияНУСчетКтПриИзменении(Элемент)
	Строка = ЭлементыФормы.ТабличноеПолеДвиженияНУ.ТекущиеДанные;
	Если (ЗначениеЗаполнено(Строка.СчетКт)) И НЕ ЗначениеЗаполнено(Строка.ВидУчетаКт) Тогда
		Строка.ВидУчетаКт = Перечисления.ВидыУчетаПоПБУ18.НУ;
	ИначеЕсли НЕ ЗначениеЗаполнено(Строка.СчетКт) Тогда
		Строка.ВидУчетаКт = Перечисления.ВидыУчетаПоПБУ18.ПустаяСсылка();
	КонецЕсли;
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ РЕКВИЗИТОВ ТЧ ПроводкиМУ и Бюджетирование

// Процедура - обработчик события "НачалоВыбора" поля СубконтоДт1
//
Процедура ТабличноеПолеДвиженияМУСубконтоДтНачалоВыбора(Элемент, СтандартнаяОбработка)
	
	Проводка = ЭлементыФормы.ТабличноеПолеДвиженияМУ.ТекущиеДанные;
	БухгалтерскийУчет.ОбработатьВыборПервогоСубконто(Элемент, СтандартнаяОбработка, Проводка.Организация);
	
КонецПроцедуры

// Процедура - обработчик события "НачалоВыбора" поля СубконтоКт1
//
Процедура ТабличноеПолеДвиженияМУСубконтоКтНачалоВыбора(Элемент, СтандартнаяОбработка)
	
	Проводка = ЭлементыФормы.ТабличноеПолеДвиженияМУ.ТекущиеДанные;
	БухгалтерскийУчет.ОбработатьВыборПервогоСубконто(Элемент, СтандартнаяОбработка, Проводка.Организация);
	
КонецПроцедуры



////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ КОМАНДНЫХ ПАНЕЛЕЙ РЕГИСТРОВ НАКОПЛЕНИЯ И СВЕДЕНИЙ

// Процедура - обработчик нажатия на кнопку "СортироватьНаборЗаписейПоВозрастанию"
//
Процедура СортироватьНаборЗаписейПоВозрастанию(Кнопка)
	
	ТабличноеПолеНаборЗаписей = ЭлементыФормы[Лев(Кнопка.Имя, Найти(Кнопка.Имя, "СортироватьПоВозрастанию") - 1)];
	СортироватьНаборЗаписей(ТабличноеПолеНаборЗаписей, ТабличноеПолеНаборЗаписей.ТекущаяКолонка.Данные);
	
КонецПроцедуры // СортироватьНаборЗаписейПоВозрастанию
 
// Процедура - обработчик нажатия на кнопку "СортироватьНаборЗаписейПоУбыванию"
//
Процедура СортироватьНаборЗаписейПоУбыванию(Кнопка)

	ТабличноеПолеНаборЗаписей = ЭлементыФормы[Лев(Кнопка.Имя, Найти(Кнопка.Имя, "СортироватьПоУбыванию") - 1)];
	СортироватьНаборЗаписей(ТабличноеПолеНаборЗаписей, ТабличноеПолеНаборЗаписей.ТекущаяКолонка.Данные, "Убыв");
	
КонецПроцедуры // СортироватьНаборЗаписейПоУбыванию

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

// Процедура - обработчик события "ПередОткрытием" формы.
//
Процедура ПередОткрытием(Отказ, СтандартнаяОбработка)
	Если ЗапретОткрытияДокумента Тогда
		Отказ = истина;
		Возврат;
	КонецЕсли;
	

	// Установка кнопок заполнение ТЧ
	УстановитьКнопкиПодменюЗаполненияТЧ();
	
	мТаблицыРегистровМодифицированыПередОткрытием = Ложь;
	НастроитьЗакладкиПанелейРегистров(Отказ);
	
КонецПроцедуры // ПередОткрытием()

// Обработчик события ПриОткрытии формы.
//
Процедура ПриОткрытии()
	
	
	Если НЕ ЭтоНовый() Тогда 
		НастройкаПравДоступа.ОпределитьДоступностьВозможностьИзмененияДокументаПоДатеЗапрета(ДокументОбъект, ЭтаФорма);
	КонецЕсли;
	
	Если ЭтоНовый() Тогда

		// Заполнить реквизиты значениями по умолчанию.
		ЗаполнениеДокументов.ЗаполнитьШапкуДокумента(ЭтотОбъект);

	КонецЕсли;
	
	Если не ИспользоватьЗаполнениеДвижений Тогда
		Если ТаблицаРегистровБухгалтерии.Количество()>0 Тогда
			ЭлементыФормы.ПанельРегистров.ТекущаяСтраница = ЭлементыФормы.ПанельРегистров.Страницы.РегистрыБухгалтерии;
		ИначеЕсли ТаблицаРегистровНакопления.Количество()>0 Тогда
			ЭлементыФормы.ПанельРегистров.ТекущаяСтраница = ЭлементыФормы.ПанельРегистров.Страницы.РегистрыНакопления;
		ИначеЕсли ТаблицаРегистровСведений.Количество()>0 Тогда
			ЭлементыФормы.ПанельРегистров.ТекущаяСтраница = ЭлементыФормы.ПанельРегистров.Страницы.РегистрыСведений;
		КонецЕсли;
	Иначе
		ЭлементыФормы.ПанельЗаполнениеДвижений.ТекущаяСтраница = ЭлементыФормы.ПанельЗаполнениеДвижений.Страницы.ТаблицаЗаполнениеДвижений;
	КонецЕсли;
	
	ОбновитьСписокДействий();
	
	// Запомнить текущие значения реквизитов формы.
	мТекущаяДатаДокумента = Дата;
	
	МеханизмНумерацииОбъектов.ДобавитьВМенюДействияКнопкуРедактированияНомера(ЭлементыФормы.ДействияФормы.Кнопки.Подменю);
	МеханизмНумерацииОбъектов.УстановитьДоступностьПоляВводаНомера(Метаданные(), ЭтаФорма, ЭлементыФормы.ДействияФормы.Кнопки.Подменю,ЭлементыФормы.Номер);
	
	РаботаСДиалогами.УстановитьЗаголовокФормыДокумента(, ЭтотОбъект, ЭтаФорма);
	
	// Установить активный реквизит.
	РаботаСДиалогами.АктивизироватьРеквизитВФорме(ЭтотОбъект, ЭтаФорма);
	
	// Создать кнопки печати
	ФормированиеПечатныхФорм.СоздатьКнопкиПечати(ЭтотОбъект, ЭтаФорма);
	
	Если НЕ ТолькоПросмотр И мТаблицыРегистровМодифицированыПередОткрытием Тогда
		Модифицированность = Истина;
	КонецЕсли; 
	
КонецПроцедуры

// Обработчик события ПередЗаписью формы.
//
Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	ПроверитьВозможностьЗаписи(ТаблицаРегистровНакопления,  "РегистрНакопления");
	ПроверитьВозможностьЗаписи(ТаблицаРегистровСведений,    "РегистрСведений");
	ПроверитьВозможностьЗаписи(ТаблицаРегистровБухгалтерии, "РегистрБухгалтерии");
	
	Для Каждого РегистрБухгалтерии из ТаблицаРегистровБухгалтерии Цикл
		НомерСтроки = 0;
		Для каждого Проводка из Движения[РегистрБухгалтерии.Имя] Цикл
			НомерСтроки = НомерСтроки +1;
			СтрокаНачалаСообщенияОбОшибке = "В строке номер """+ СокрЛП(НомерСтроки) +
			                               """ табличной части """+РегистрБухгалтерии.Представление+""": ";

			Если РегистрБухгалтерии.Имя = "Бюджетирование" Тогда
				Если НЕ ЗначениеЗаполнено(Проводка.Сценарий) Тогда
					СтрокаСообщения = "Не заполнено значение реквизита ""Сценарий""!";
					ОбщегоНазначения.СообщитьОбОшибке(СтрокаНачалаСообщенияОбОшибке + СтрокаСообщения, Отказ);
				КонецЕсли;
			Иначе
				Если НЕ ЗначениеЗаполнено(Проводка.Организация) Тогда
					СтрокаСообщения = "Не заполнено значение реквизита ""Организация""!";
					ОбщегоНазначения.СообщитьОбОшибке(СтрокаНачалаСообщенияОбОшибке + СтрокаСообщения, Отказ);
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	Для каждого СтрокаТаблицы Из ТаблицаРегистровНакопления Цикл
		Для каждого Запись Из Движения[СтрокаТаблицы.Имя] Цикл
			Запись.Период = Дата;
		КонецЦикла; 
	КонецЦикла; 
	
	Для Каждого Проводка Из Движения.Хозрасчетный Цикл

		Проводка.Период      = Дата;

	КонецЦикла;

	Для Каждого Проводка Из Движения.Налоговый Цикл

		Если (НЕ ЗначениеЗаполнено(Проводка.ВидУчетаДт)) И
			(ЗначениеЗаполнено(Проводка.СчетДт)) Тогда
			Предупреждение("В проводке налогового учета не указан вид учета. Документ не записан.");
			Отказ = Истина;
			Возврат;
		КонецЕсли;

		Если (НЕ ЗначениеЗаполнено(Проводка.ВидУчетаКт)) И
			(ЗначениеЗаполнено(Проводка.СчетКт)) Тогда
			Предупреждение("В проводке налогового учета не указан вид учета. Документ не записан.");
			Отказ = Истина;
			Возврат;
		КонецЕсли;

		Проводка.Период      = Дата;

	КонецЦикла;
	Если Движения.Найти("Международный") <> Неопределено Тогда
		Для Каждого Проводка Из Движения.Международный Цикл
			НомерСтроки = НомерСтроки + 1;
			Проводка.Период = Дата;

			Если НЕ ЗначениеЗаполнено(Проводка.СчетДт) Тогда
				Сообщить("В строке № " + НомерСтроки + " не заполнен счет дебета.", СтатусСообщения.ОченьВажное);
				Отказ = Истина;
			КонецЕсли;
			Если НЕ ЗначениеЗаполнено(Проводка.СчетКт) Тогда
				Сообщить("В строке № " + НомерСтроки + " не заполнен счет кредита.", СтатусСообщения.ОченьВажное);
				Отказ = Истина;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	Если Движения.Найти("Бюджетирование") <> Неопределено Тогда
		Для Каждого Проводка Из Движения.Бюджетирование Цикл
			НомерСтроки = НомерСтроки + 1;
			Проводка.Период = Дата;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ ОБРАБОТКИ СВОЙСТВ И КАТЕГОРИЙ

// Процедура выполняет открытие формы работы со свойствами документа
//
Процедура ДействияФормыДействиеОткрытьСвойства(Кнопка)

	РаботаСДиалогами.ОткрытьСвойстваДокумента(ЭтотОбъект, ЭтаФорма);

КонецПроцедуры

// Процедура выполняет открытие формы работы с категориями документа
//
Процедура ДействияФормыДействиеОткрытьКатегории(Кнопка)

	РаботаСДиалогами.ОткрытьКатегорииДокумента(ЭтотОбъект, ЭтаФорма);

КонецПроцедуры

Процедура ОбновлениеОтображения()

	ЭлементыФормы.КоманднаяПанельБУ.Кнопки.ПереключитьАктивность.Доступность = НЕ ПометкаУдаления;
	ЭлементыФормы.КоманднаяПанельНУ.Кнопки.ПереключитьАктивность.Доступность = НЕ ПометкаУдаления;
	
	Если ЗаполнениеДвижений.Количество()>0 Тогда
		ЭлементыФормы.ПанельРегистров.Страницы.ЗаполнениеДвижений.КартинкаЗаголовка = БиблиотекаКартинок.Выполнить;
	Иначе
		ЭлементыФормы.ПанельРегистров.Страницы.ЗаполнениеДвижений.КартинкаЗаголовка = Новый Картинка;
	КонецЕсли;

	Если ТаблицаРегистровБухгалтерии.Количество()>0 Тогда
		ЭлементыФормы.ПанельРегистров.Страницы.РегистрыБухгалтерии.КартинкаЗаголовка = БиблиотекаКартинок.ЖурналПроводок;
	Иначе
		ЭлементыФормы.ПанельРегистров.Страницы.РегистрыБухгалтерии.КартинкаЗаголовка = Новый Картинка;
	КонецЕсли;
	
	Если ТаблицаРегистровНакопления.Количество()>0 Тогда
		ЭлементыФормы.ПанельРегистров.Страницы.РегистрыНакопления.КартинкаЗаголовка = БиблиотекаКартинок.РегистрНакопления;
	Иначе
		ЭлементыФормы.ПанельРегистров.Страницы.РегистрыНакопления.КартинкаЗаголовка = Новый Картинка;
	КонецЕсли;
	
	Если ТаблицаРегистровСведений.Количество()>0 Тогда
		ЭлементыФормы.ПанельРегистров.Страницы.РегистрыСведений.КартинкаЗаголовка = БиблиотекаКартинок.РегистрСведений;
	Иначе
		ЭлементыФормы.ПанельРегистров.Страницы.РегистрыСведений.КартинкаЗаголовка = Новый Картинка;
	КонецЕсли;
	
КонецПроцедуры

// Процедура вызова структуры подчиненности документа
Процедура ДействияФормыСтруктураПодчиненностиДокумента(Кнопка)
	РаботаСДиалогами.ПоказатьСтруктуруПодчиненностиДокумента(Ссылка);
КонецПроцедуры

// Процедура - обработчик нажатия на любую из дополнительных кнопок по заполнению ТЧ
//
Процедура НажатиеНаДополнительнуюКнопкуЗаполненияТЧ(Кнопка)
	
	УниверсальныеМеханизмы.ОбработатьНажатиеНаДополнительнуюКнопкуЗаполненияТЧ(мКнопкиЗаполненияТЧ.Строки.Найти(Кнопка.Имя,"Имя",Истина),ЭтотОбъект);
	
КонецПроцедуры

// Процедура - обработчик нажатия на кнопку "Печать".
// Открывает форму выбора печатных форм объекта.
//
Процедура ОсновныеДействияФормыПечать(Кнопка)
	
	УниверсальныеМеханизмы.ОткрытьФормуВыбораПечатныхФормОбъекта(ЭтотОбъект, ЭтаФорма);
	
КонецПроцедуры // ОсновныеДействияФормыПечать()

// Процедура - обработчик нажатия на кнопку "Печать по умолчанию"
//
Процедура ОсновныеДействияФормыПечатьПоУмолчанию(Кнопка)

	УниверсальныеМеханизмы.НапечататьДокументПоУмолчанию(ЭтотОбъект);

КонецПроцедуры

// Процедура - обработчик нажатия на кнопку "Заполнить движения"
// командно панели ТЧ "Выполняемые действия"
// Открывает форму выбора печатных форм объекта.
//
Процедура КоманднаяПанельЗаполнениеДвиженийЗаполнитьДвижения(Кнопка)
	
	Если ЗаполнениеДвижений.Количество() = 0 Тогда
		Возврат;
	КонецЕсли; 
	
	ВыполнитьДействияДокумента();
	НастроитьЗакладкиПанелейРегистров(Ложь);

КонецПроцедуры

// Процедура - обработчик события ПриЗаписи
//
Процедура ПослеЗаписи()
	
	РаботаСДиалогами.УстановитьЗаголовокФормыДокумента(, ЭтотОбъект, ЭтаФорма);
	МеханизмНумерацииОбъектов.ОбновитьПодсказкуКодНомерОбъекта(ЭтотОбъект.Метаданные(), ЭлементыФормы.ДействияФормы.Кнопки.Подменю, ЭлементыФормы.Номер);
	
КонецПроцедуры