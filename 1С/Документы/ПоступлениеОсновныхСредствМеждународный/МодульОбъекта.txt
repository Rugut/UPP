Перем мУдалятьДвижения;

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОКУМЕНТА

#Если Клиент Тогда
// Процедура осуществляет печать документа. Можно направить печать на 
// экран или принтер, а также распечатать необходимое количество копий.
//
//  Название макета печати передается в качестве параметра,
// по переданному названию находим имя макета в соответствии.
//
// Параметры:
//  НазваниеМакета - строка, название макета.
//
Процедура Печать(ИмяМакета, КоличествоЭкземпляров = 1, НаПринтер = Ложь) Экспорт

	Если ЭтоНовый() Тогда
		Предупреждение("Документ можно распечатать только после его записи");
		Возврат;
	ИначеЕсли Не УправлениеДопПравамиПользователей.РазрешитьПечатьНепроведенныхДокументов(Проведен) Тогда
		Предупреждение("Недостаточно полномочий для печати непроведенного документа!");
		Возврат;
	КонецЕсли;

	Если Не РаботаСДиалогами.ПроверитьМодифицированность(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;

	Если ТипЗнч(ИмяМакета) = Тип("ДвоичныеДанные") Тогда

		ТабДокумент = УниверсальныеМеханизмы.НапечататьВнешнююФорму(Ссылка, ИмяМакета);
		
		Если ТабДокумент = Неопределено Тогда
			Возврат;
		КонецЕсли; 
		
	КонецЕсли;

	УниверсальныеМеханизмы.НапечататьДокумент(ТабДокумент, КоличествоЭкземпляров, НаПринтер, ОбщегоНазначения.СформироватьЗаголовокДокумента(ЭтотОбъект), Ссылка);

КонецПроцедуры // Печать

// Возвращает доступные варианты печати документа
//
// Возвращаемое значение:
//  Структура, каждая строка которой соответствует одному из вариантов печати
//  
Функция ПолучитьСтруктуруПечатныхФорм() Экспорт
	
	Возврат Новый Структура;

КонецФункции // ПолучитьСтруктуруПечатныхФорм()

#КонецЕсли

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

Процедура ОбработкаПроведения(Отказ)
	
	Если мУдалятьДвижения Тогда
		ОбщегоНазначения.УдалитьДвиженияРегистратора(ЭтотОбъект, Отказ);
	КонецЕсли;

	Для Каждого ТекСтрокаОсновныеСредства Из ОсновныеСредства Цикл
		Если ТекСтрокаОсновныеСредства.УчитыватьКакОС Тогда
			Если НЕ ЗначениеЗаполнено(ТекСтрокаОсновныеСредства.СчетСниженияСтоимости) Тогда
				Сообщить("Не заполнен реквизит: Счет снижения стоимости");
				Отказ = Истина;
			КонецЕсли;
			// регистр ОсновныеСредстваМеждународныйУчет 
			Движение = Движения.ОсновныеСредстваМеждународныйУчет.Добавить();
			Движение.Период = ТекСтрокаОсновныеСредства.ДатаПринятияКУчету;
			Движение.ОсновноеСредство = ТекСтрокаОсновныеСредства.ОсновноеСредство;
			Движение.Организация = Организация;
			Движение.ДатаПринятияКУчету = ТекСтрокаОсновныеСредства.ДатаПринятияКУчету;
			Движение.МестонахождениеОбъекта = ТекСтрокаОсновныеСредства.МестонахождениеОбъекта;
			Движение.МОЛ = ТекСтрокаОсновныеСредства.МОЛ;
			Если НЕ ЗначениеЗаполнено(ТекСтрокаОсновныеСредства.СчетУчетаНовый) Тогда
				Движение.СчетУчета = ТекСтрокаОсновныеСредства.СчетУчета;
			Иначе
				Движение.СчетУчета = ТекСтрокаОсновныеСредства.СчетУчетаНовый;
			КонецЕсли;
			Движение.СрокПолезногоИспользования = ТекСтрокаОсновныеСредства.СрокПолезногоИспользования;
			Движение.НачислятьАмортизацию = ТекСтрокаОсновныеСредства.НачислятьАмортизацию;
			Движение.МетодНачисленияАмортизации = ТекСтрокаОсновныеСредства.МетодНачисленияАмортизации;
			Движение.СчетНачисленияАмортизации = ТекСтрокаОсновныеСредства.СчетНачисленияАмортизации;
			Движение.ПервоначальнаяСтоимость = ТекСтрокаОсновныеСредства.ПервоначальнаяСтоимость;
			Движение.ЛиквидационнаяСтоимость = ТекСтрокаОсновныеСредства.ЛиквидационнаяСтоимость;
			Движение.СчетЗатрат = ТекСтрокаОсновныеСредства.СчетЗатрат;
			Движение.Субконто1 = ТекСтрокаОсновныеСредства.Субконто1;
			Движение.Субконто2 = ТекСтрокаОсновныеСредства.Субконто2;
			Движение.Субконто3 = ТекСтрокаОсновныеСредства.Субконто3;
			Движение.ПредполагаемыйОбъемПродукции = ТекСтрокаОсновныеСредства.ПредполагаемыйОбъемПродукции;
			Движение.СуммаНачисленнойАмортизации = ТекСтрокаОсновныеСредства.СуммаНачисленнойАмортизации;
			Движение.Состояние = Перечисления.ВидыСостоянийОС.ВведеноВЭксплуатацию;
			Движение.КоэффициентУскорения = ТекСтрокаОсновныеСредства.КоэффициентУскорения;
			Движение.СчетСниженияСтоимости = ТекСтрокаОсновныеСредства.СчетСниженияСтоимости;
			Движение.СправедливаяСтоимость = ТекСтрокаОсновныеСредства.ПервоначальнаяСтоимость;
		КонецЕсли;
		
		Если (ТекСтрокаОсновныеСредства.Сумма <> 0) и (ТекСтрокаОсновныеСредства.СчетУчетаНовый <> ТекСтрокаОсновныеСредства.СчетУчета) и (ЗначениеЗаполнено(ТекСтрокаОсновныеСредства.СчетУчетаНовый)) и (ЗначениеЗаполнено(ТекСтрокаОсновныеСредства.СчетУчета)) и (ЗначениеЗаполнено(ТекСтрокаОсновныеСредства.КоррСчет)) Тогда // переводим на другой счет
			// регистр Международный 
			Движение = Движения.Международный.Добавить();
			Движение.Период = ТекСтрокаОсновныеСредства.ДатаПринятияКУчету;
			Движение.СчетДт = ТекСтрокаОсновныеСредства.СчетУчета;
			Движение.СчетКт = ТекСтрокаОсновныеСредства.КоррСчет;
			Движение.Организация = Организация;
			Движение.Сумма = -ТекСтрокаОсновныеСредства.Сумма;
			Движение.Содержание = "Сторно";
			Движение.НомерЖурнала = "Рег";
			
			Если не Движение.СчетДт.ВидыСубконто.Найти(ПланыВидовХарактеристик.ВидыСубконтоМеждународные.ОсновныеСредства,) = Неопределено Тогда
				Движение.СубконтоДт[ПланыВидовХарактеристик.ВидыСубконтоМеждународные.ОсновныеСредства] = ТекСтрокаОсновныеСредства.ОсновноеСредство;
			КонецЕсли;
			
			Для Ном = 1 по Движение.СчетКт.ВидыСубконто.Количество() Цикл
				Движение.СубконтоКт[Движение.СчетКт.ВидыСубконто[Ном-1].ВидСубконто] = ТекСтрокаОсновныеСредства["Субконто" + Ном + "Кт"];
			КонецЦикла;

			Движение = Движения.Международный.Добавить();
			Движение.Период = ТекСтрокаОсновныеСредства.ДатаПринятияКУчету;
			Движение.СчетДт = ТекСтрокаОсновныеСредства.СчетУчетаНовый;
			Движение.СчетКт = ТекСтрокаОсновныеСредства.КоррСчет;
			Движение.Организация = Организация;
			Движение.Сумма = ТекСтрокаОсновныеСредства.Сумма;
			Движение.Содержание = "Поступление";
			Движение.НомерЖурнала = "Рег";
			
			Для Ном = 1 по Движение.СчетДт.ВидыСубконто.Количество() Цикл
				Движение.СубконтоДт[Движение.СчетДт.ВидыСубконто[Ном-1].ВидСубконто] = ТекСтрокаОсновныеСредства["Субконто" + Ном + "Нов"];
			КонецЦикла;
			
			Для Ном = 1 по Движение.СчетКт.ВидыСубконто.Количество() Цикл
				Движение.СубконтоКт[Движение.СчетКт.ВидыСубконто[Ном-1].ВидСубконто] = ТекСтрокаОсновныеСредства["Субконто" + Ном + "Кт"];
			КонецЦикла;
			
			// регистр Международный (СТАРЫЙ КОД - К УДАЛЕНИЮ)
			//Движение = Движения.Международный.Добавить();
			//Движение.Период = ТекСтрокаОсновныеСредства.ДатаПринятияКУчету;
			//Движение.СчетДт = ТекСтрокаОсновныеСредства.СчетУчетаНовый;
			//Движение.СчетКт = ТекСтрокаОсновныеСредства.СчетУчета;
			//Движение.Организация = Организация;
			//Движение.Сумма = ТекСтрокаОсновныеСредства.Сумма;
			//Движение.Содержание = "Поступление ОС";
			//Движение.НомерЖурнала = "Рег";
			//
			//Для Ном = 1 по Движение.СчетДт.ВидыСубконто.Количество() Цикл
			//	Движение.СубконтоДт[Движение.СчетДт.ВидыСубконто[Ном-1].ВидСубконто] = ТекСтрокаОсновныеСредства["Субконто" + Ном + "Нов"];
			//КонецЦикла;
			//
			//Если не Движение.СчетДт.ВидыСубконто.Найти(ПланыВидовХарактеристик.ВидыСубконтоМеждународные.ОсновныеСредства,) = Неопределено Тогда
			//	Движение.СубконтоДт[ПланыВидовХарактеристик.ВидыСубконтоМеждународные.ОсновныеСредства] = ТекСтрокаОсновныеСредства.ОсновноеСредство;
			//КонецЕсли;
			//Если не Движение.СчетКт.ВидыСубконто.Найти(ПланыВидовХарактеристик.ВидыСубконтоМеждународные.ОсновныеСредства,) = Неопределено Тогда
			//	Движение.СубконтоКт[ПланыВидовХарактеристик.ВидыСубконтоМеждународные.ОсновныеСредства] = ТекСтрокаОсновныеСредства.ОсновноеСредство;
			//КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	// записываем движения регистров
	Движения.ОсновныеСредстваМеждународныйУчет.Записать();
	Движения.Международный.Записать();
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	Если ОбменДанными.Загрузка  Тогда
		Возврат;
	КонецЕсли;


	 
	мУдалятьДвижения = НЕ ЭтоНовый();
	
КонецПроцедуры // ПередЗаписью