Перем мУдалятьДвижения Экспорт;

Перем мВестиУчетНДС Экспорт;

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОКУМЕНТА

#Если Клиент Тогда
// Процедура осуществляет печать документа. Можно направить печать на 
// экран или принтер, а также распечатать необходимое количество копий.
//
//  Название макета печати передается в качестве параметра,
// по переданному названию находим имя макета в соответствии.
//
// Параметры:
//  НазваниеМакета - строка, название макета.
//
Процедура Печать(ИмяМакета, КоличествоЭкземпляров = 1, НаПринтер = Ложь) Экспорт

	Если ЭтоНовый() Тогда
		Предупреждение("Документ можно распечатать только после его записи");
		Возврат;
	ИначеЕсли Не УправлениеДопПравамиПользователей.РазрешитьПечатьНепроведенныхДокументов(Проведен) Тогда
		Предупреждение("Недостаточно полномочий для печати непроведенного документа!");
		Возврат;
	КонецЕсли;

	Если Не РаботаСДиалогами.ПроверитьМодифицированность(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;

	Если ТипЗнч(ИмяМакета) = Тип("ДвоичныеДанные") Тогда

		ТабДокумент = УниверсальныеМеханизмы.НапечататьВнешнююФорму(Ссылка, ИмяМакета);

		Если ТабДокумент = Неопределено Тогда
			Возврат

		КонецЕсли; 
		
	КонецЕсли;

	УниверсальныеМеханизмы.НапечататьДокумент(ТабДокумент, КоличествоЭкземпляров, НаПринтер, ОбщегоНазначения.СформироватьЗаголовокДокумента(ЭтотОбъект), Ссылка);

КонецПроцедуры // Печать()

// Возвращает доступные варианты печати документа
//
// Возвращаемое значение:
//  Структура, каждая строка которой соответствует одному из вариантов печати
//  
Функция ПолучитьСтруктуруПечатныхФорм() Экспорт

	Возврат Новый Структура;

КонецФункции // ПолучитьСтруктуруПечатныхФорм()

#КонецЕсли

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ДЛЯ ОБЕСПЕЧЕНИЯ АВТОЗАПОЛНЕНИЯ СТРОК ДОКУМЕНТА

// Процедура вызывается при нажатии на кнопку "Запонить" в диалоге документа
// Реализует алгоритм автоматического заполнения табличной части.
//
Процедура ЗаполнитьДокумент(ОшибкаЗаполнения = Ложь, Сообщать = Истина, СтрокаСообщения = "", ОтменитьПроведение = Ложь, УдалятьНезаполненные = Ложь) Экспорт
	
	Если Проведен Тогда
		Если ОтменитьПроведение Тогда
			Записать(РежимЗаписиДокумента.ОтменаПроведения);
		Иначе
			ОшибкаЗаполнения = Истина;
			СтрокаСообщения = " перед заполнением требуется отменить проведение документа";
			Если Сообщать Тогда
				ОбщегоНазначения.СообщитьОбОшибке("Документ не заполнен:" + СтрокаСообщения, , Строка(Ссылка));
			КонецЕсли; 
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	УчетнаяПолитикаНУ = ОбщегоНазначения.ПолучитьПараметрыУчетнойПолитикиРегл(Дата, Организация);
	
	ЗаполнитьРазделРеализация(?(НЕ ЗначениеЗаполнено(УчетнаяПолитикаНУ), Ложь, УчетнаяПолитикаНУ.НДСИспользованиеОплатПокупателя_Приоритет0), , УдалятьНезаполненные);
	
	Если Реализация0 Тогда
		Авансы.Очистить();
		НачисленКУплате.Очистить();
		Восстановлен.Очистить();
		НеОтражаетсяВКниге.Очистить();
		ВосстановленПоАвансам.Очистить();
	Иначе
		ЗаполнитьРазделАвансы(, УдалятьНезаполненные);
		ЗаполнитьРазделНачисленКУплате(, УдалятьНезаполненные);
		ЗаполнитьРазделНеОтражаетсяВКниге(, УдалятьНезаполненные);
		ЗаполнитьРазделВосстановлен(, УдалятьНезаполненные);
		ЗаполнитьРазделВосстановленПоАвансам(, УдалятьНезаполненные);
	КонецЕсли;
	
	Если Не (Реализация.Количество() > 0 
		Или Авансы.Количество() >0
		Или НачисленКУплате.Количество() >0
		Или Восстановлен.Количество() >0
		Или НеОтражаетсяВКниге.Количество() >0
		Или ВосстановленПоАвансам.Количество() >0
        ) Тогда
		ОшибкаЗаполнения = Истина;
		СтрокаСообщения = СтрокаСообщения+Символы.ПС+" - не обнаружены записи к отражению в книге продаж"
	КонецЕсли;	

   Если ОшибкаЗаполнения Тогда
		Если Сообщать Тогда
			ОбщегоНазначения.СообщитьОбОшибке("Документ не заполнен:" + СтрокаСообщения, , Строка(Ссылка));
		КонецЕсли; 
		Возврат;
	КонецЕсли; 
	
КонецПроцедуры // ЗаполнитьСтрокиДокумента()

// Заполняет раздел "Реализация"
Процедура ЗаполнитьРазделРеализация(НДСИспользованиеОплатПокупателя_Приоритет0, ОтменитьПроведение = Ложь, УдалятьНезаполненные = Ложь) Экспорт
	
	Если Проведен Тогда
		Если ОтменитьПроведение Тогда
			Записать(РежимЗаписиДокумента.ОтменаПроведения);
		Иначе
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	ТаблицаРезультатов = Реализация.ВыгрузитьКолонки();
	
	Если Реализация0 Тогда
		Дерево_НДСНачисленный = ЗаполнитьНДСНачисленныйПоДаннымРегистраНДСРеализация0();
	Иначе	
		Дерево_НДСНачисленный = ЗаполнитьНДСНачисленныйПоДаннымРегистраНДСНачисленный();
	КонецЕсли; 
	
	Если Дерево_НДСНачисленный.Строки.Количество()=0 Тогда
		// Дальнейшая обработка не требуется, не обнаружен НДС, который может быть принят к вычету.
		Реализация.Очистить();
		Возврат;
	КонецЕсли;

	СписокСчетовФактур = ОбщегоНазначения.УдалитьПовторяющиесяЭлементыМассива(Дерево_НДСНачисленный.Строки.ВыгрузитьКолонку("СчетФактура"),Истина);
	
	РаспределенныеОплаты = ПолучитьДанныеОРаспределенныхОплатахПоСпискуСФ(СписокСчетовФактур);
	
	Если Не Реализация0 = НДСИспользованиеОплатПокупателя_Приоритет0 Тогда
		// В учетная политике приоритет в распределении оплат установлен по НДС 0%, отрабатывается НДС по прочим ставкам 
		// или наоборот - приоритет по прочим ставкам, отрабатывается 0%.
		// В этом случае суммы сначала распределяются на начисления, которые не отражаются данным документом,
		// лишь затем на начисления, которые могут быть отражены данным документом.
		Если Реализация0 Тогда
			ДеревоНДС_ВычетЗаблокирован = ЗаполнитьНДСНачисленныйПоДаннымРегистраНДСНачисленный(СписокСчетовФактур);
		Иначе	
			ДеревоНДС_ВычетЗаблокирован = ЗаполнитьНДСНачисленныйПоДаннымРегистраНДСРеализация0(Истина, СписокСчетовФактур);
		КонецЕсли; 
		
		РаспределитьОплатыПоДеревуСФ(ДеревоНДС_ВычетЗаблокирован, ТаблицаРезультатов,СписокСчетовФактур,РаспределенныеОплаты, Ложь);
	КонецЕсли; 
	
	РаспределитьОплатыПоДеревуСФ(Дерево_НДСНачисленный, ТаблицаРезультатов,СписокСчетовФактур,РаспределенныеОплаты, Истина);

	// Удаление строк, в которых есть незаполненные обязательные поля
	Если УдалятьНезаполненные Тогда
		
		ОбязательныеПоля = "ВидЦенности, СчетФактура, СтавкаНДС";
		Если Реализация0 Тогда
			ОбязательныеПоля = ОбязательныеПоля + ", Состояние"; 
		КонецЕсли; 
		ОбработкаТабличныхЧастейСервер.УдалитьНезаполненныеСтроки(ТаблицаРезультатов, ОбязательныеПоля);
		
	КонецЕсли;
	
	Реализация.Загрузить(ТаблицаРезультатов);

КонецПроцедуры // ЗаполнитьОсновнойРаздел()

// Вызывается из процедуры ЗаполнитьРазделРеализация.
// Заполняет ТЧ Реализация по данным регистра НДС начисленный (за вычетом реализации 0%)
Функция ЗаполнитьНДСНачисленныйПоДаннымРегистраНДСНачисленный(СписокСчетовФактур = неопределено)

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	РеестрНачислений.СчетФактура КАК СчетФактура,
		|	РеестрНачислений.СчетФактураДата КАК СчетФактураДата,
		|	ВЫБОР
		|		КОГДА РеестрНачислений.СчетФактураДата >= &Начало2006Года
		|			ТОГДА &МоментОпределения_ПоОтгрузке
		|		ИНАЧЕ ЕСТЬNULL(УчетнаяПолитикаНалоговыйУчет.МоментОпределенияНалоговойБазыНДС, &МоментОпределения_ПоОтгрузке)
		|	КОНЕЦ КАК МоментОпределенияНалоговойБазыНДС,
		|	РеестрНачислений.ВидЦенности,
		|	РеестрНачислений.СтавкаНДС,
		|	РеестрНачислений.Покупатель,
		|	РеестрНачислений.СуммаБезНДС КАК СуммаБезНДС,
		|	РеестрНачислений.СуммаНДС КАК СуммаНДС,
		|	РеестрНачислений.СуммаСНДС КАК СуммаСНДС,
		|	РеестрНачислений.ДоговорКонтрагента КАК ДоговорКонтрагента
		|ИЗ
		|	(ВЫБРАТЬ
		|		НДСНачисленныйОстатки.СчетФактура КАК СчетФактура,
		|		ЕСТЬNULL(НДСНачисленныйОстатки.СчетФактура.Дата, &КонецПериода) КАК СчетФактураДата,
		|		НДСНачисленныйОстатки.ВидЦенности КАК ВидЦенности,
		|		НДСНачисленныйОстатки.СтавкаНДС КАК СтавкаНДС,
		|		НДСНачисленныйОстатки.Покупатель КАК Покупатель,
		|		СУММА(ЕСТЬNULL(НДСНачисленныйОстатки.СуммаБезНДСОстаток, 0) - ЕСТЬNULL(НДСРеализация0Остатки.СуммаБезНДСОстаток, 0)) КАК СуммаБезНДС,
		|		СУММА(ЕСТЬNULL(НДСНачисленныйОстатки.НДСОстаток, 0) - ЕСТЬNULL(НДСРеализация0Остатки.НДСОстаток, 0)) КАК СуммаНДС,
		|		СУММА(ЕСТЬNULL(НДСНачисленныйОстатки.СуммаБезНДСОстаток, 0) - ЕСТЬNULL(НДСРеализация0Остатки.СуммаБезНДСОстаток, 0) + ЕСТЬNULL(НДСНачисленныйОстатки.НДСОстаток, 0) - ЕСТЬNULL(НДСРеализация0Остатки.НДСОстаток, 0)) КАК СуммаСНДС,
		|		ВЫБОР
		|			КОГДА НДСНачисленныйОстатки.ВидЦенности = ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.НалоговыйАгентКомитент)
		|				ТОГДА НДСНачисленныйОстатки.ДоговорКонтрагента
		|			ИНАЧЕ НЕОПРЕДЕЛЕНО
		|		КОНЕЦ КАК ДоговорКонтрагента
		|	ИЗ
		|		РегистрНакопления.НДСНачисленный.Остатки(
		|				&КонецПериодаГраница,
		|				Организация = &Организация
		|					И ВЫБОР
		|						КОГДА &ОтборПоСпискуСФ
		|							ТОГДА СчетФактура В (&СписокСчетовФактур)
		|						ИНАЧЕ ИСТИНА
		|					КОНЕЦ
		|					И (НЕ ВидНачисления В (&ИсключаемыеВидыНачисления))) КАК НДСНачисленныйОстатки
		|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.НДСРеализация0.Остатки(&КонецПериодаГраница, Организация = &Организация) КАК НДСРеализация0Остатки
		|			ПО НДСНачисленныйОстатки.СчетФактура = НДСРеализация0Остатки.СчетФактура
		|				И НДСНачисленныйОстатки.ВидЦенности = НДСРеализация0Остатки.ВидЦенности
		|				И НДСНачисленныйОстатки.СтавкаНДС = НДСРеализация0Остатки.СтавкаНДС
		|				И НДСНачисленныйОстатки.Покупатель = НДСРеализация0Остатки.Покупатель
		|	
		|	СГРУППИРОВАТЬ ПО
		|		НДСНачисленныйОстатки.ВидЦенности,
		|		НДСНачисленныйОстатки.СчетФактура,
		|		НДСНачисленныйОстатки.СтавкаНДС,
		|		НДСНачисленныйОстатки.Покупатель,
		|		ЕСТЬNULL(НДСНачисленныйОстатки.СчетФактура.Дата, &КонецПериода),
		|		ВЫБОР
		|			КОГДА НДСНачисленныйОстатки.ВидЦенности = ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.НалоговыйАгентКомитент)
		|				ТОГДА НДСНачисленныйОстатки.ДоговорКонтрагента
		|			ИНАЧЕ НЕОПРЕДЕЛЕНО
		|		КОНЕЦ) КАК РеестрНачислений
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.УчетнаяПолитикаОрганизаций КАК УчетнаяПолитикаНалоговыйУчет
		|		ПО (УчетнаяПолитикаНалоговыйУчет.Организация = &Организация)
		|			И (ВЫБОР
		|				КОГДА &ОтборПоСпискуСФ
		|					ТОГДА РеестрНачислений.СчетФактура В (&СписокСчетовФактур)
		|				ИНАЧЕ ИСТИНА
		|			КОНЕЦ)
		|			И (УчетнаяПолитикаНалоговыйУчет.Период В
		|				(ВЫБРАТЬ
		|					МАКСИМУМ(УчетнаяПолитикаНалоговыйУчет.Период) КАК Период
		|				ИЗ
		|					РегистрСведений.УчетнаяПолитикаОрганизаций КАК УчетнаяПолитикаНалоговыйУчет
		|				ГДЕ
		|					РеестрНачислений.СчетФактураДата >= УчетнаяПолитикаНалоговыйУчет.Период
		|					И УчетнаяПолитикаНалоговыйУчет.Организация = &Организация))
		|ГДЕ
		|	(НЕ РеестрНачислений.СуммаСНДС = 0)
		|
		|УПОРЯДОЧИТЬ ПО
		|	СчетФактураДата,
		|	СчетФактура
		|ИТОГИ
		|	МИНИМУМ(СчетФактураДата),
		|	СУММА(СуммаБезНДС),
		|	СУММА(СуммаНДС),
		|	СУММА(СуммаСНДС)
		|ПО
		|	СчетФактура";

	Запрос.УстановитьПараметр("Организация",  Организация);
	Запрос.УстановитьПараметр("КонецПериода", КонецДня(Дата));
	Запрос.УстановитьПараметр("КонецПериодаГраница", Новый Граница(КонецДня(Дата),ВидГраницы.Включая));

	Запрос.УстановитьПараметр("Начало2006Года", '20060101');
	Запрос.УстановитьПараметр("МоментОпределения_ПоОтгрузке", Перечисления.МоментыОпределенияНалоговойБазыНДС.ПоОтгрузке);

	// Исключаемые из анализа виды ценностей
	ИсключаемыеВидыНачисления = Новый СписокЗначений;
	ИсключаемыеВидыНачисления.Добавить(Перечисления.НДСВидНачисления.РеализацияЕНВД);		// не отражается в книге продаж
	
	ИсключаемыеВидыНачисления.Добавить(Перечисления.НДСВидНачисления.НДСНачисленКУплате);	// Отражается отдельно
	ИсключаемыеВидыНачисления.Добавить(Перечисления.НДСВидНачисления.НДСсАвансов);			// Отражается отдельно
	ИсключаемыеВидыНачисления.Добавить(Перечисления.НДСВидНачисления.НДСВосстановлен);		// Отражается отдельно
	
	Запрос.УстановитьПараметр("ИсключаемыеВидыНачисления", ИсключаемыеВидыНачисления);

	Запрос.УстановитьПараметр("ОтборПоСпискуСФ", (не СписокСчетовФактур = Неопределено));
	Запрос.УстановитьПараметр("СписокСчетовФактур",СписокСчетовФактур);
	
	Возврат Запрос.Выполнить().Выгрузить(ОбходРезультатаЗапроса.ПоГруппировкам);
	
КонецФункции // ЗаполнитьНДСКВычетуПоДаннымРегистраНДСНачисленный()

// Вызывается из процедуры ЗаполнитьРазделРеализация.
// Заполняет ТЧ Реализация по данным регистра НДСПредъявленныйРеализация0
Функция ЗаполнитьНДСНачисленныйПоДаннымРегистраНДСРеализация0(ВключитьОжиданиеПодтверждения = ложь, СписокСчетовФактур = Неопределено)

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	НДСРеализация0Остатки.Организация,
		|	НДСРеализация0Остатки.СчетФактура КАК СчетФактура,
		|	ЕСТЬNULL(НДСРеализация0Остатки.СчетФактура.Дата, &КонецПериода) КАК СчетФактураДата,
		|	ВЫБОР
		|		КОГДА ЕСТЬNULL(НДСРеализация0Остатки.СчетФактура.Дата, &КонецПериода) >= &Начало2006Года
		|			ТОГДА &МоментОпределения_ПоОтгрузке
		|		ИНАЧЕ ЕСТЬNULL(УчетнаяПолитикаНалоговыйУчет.МоментОпределенияНалоговойБазыНДС, &МоментОпределения_ПоОтгрузке)
		|	КОНЕЦ КАК МоментОпределенияНалоговойБазыНДС,
		|	НДСРеализация0Остатки.ВидЦенности,
		|	НДСРеализация0Остатки.СтавкаНДС,
		|	НДСРеализация0Остатки.СуммаБезНДСОстаток КАК СуммаБезНДС,
		|	НДСРеализация0Остатки.НДСОстаток КАК СуммаНДС,
		|	НДСРеализация0Остатки.СуммаБезНДСОстаток + НДСРеализация0Остатки.НДСОстаток КАК СуммаСНДС,
		|	НДСРеализация0Остатки.Состояние,
		|	НДСНачисленныйОстатки.Покупатель,
		|	НДСНачисленныйОстатки.СуммаБезНДСОстаток + НДСНачисленныйОстатки.НДСОстаток КАК СуммаСНДС_НачисленоВсего,
		|	ВЫБОР
		|		КОГДА НДСНачисленныйОстатки.СуммаБезНДСОстаток + НДСНачисленныйОстатки.НДСОстаток < НДСРеализация0Остатки.СуммаБезНДСОстаток + НДСРеализация0Остатки.НДСОстаток
		|				И (НЕ НДСРеализация0Остатки.СуммаБезНДСОстаток + НДСРеализация0Остатки.НДСОстаток < 0)
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК ТребуетсяКорректировка,
		|	ВЫБОР
		|		КОГДА НДСНачисленныйОстатки.ВидЦенности = ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.НалоговыйАгентКомитент)
		|			ТОГДА НДСНачисленныйОстатки.ДоговорКонтрагента
		|		ИНАЧЕ НЕОПРЕДЕЛЕНО
		|	КОНЕЦ КАК ДоговорКонтрагента
		|ИЗ
		|	РегистрНакопления.НДСРеализация0.Остатки(
		|		&КонецПериодаГраница,
		|		Организация = &Организация
		|		    И Состояние В (&ОтрабатываемыеСостояния)) КАК НДСРеализация0Остатки
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.УчетнаяПолитикаОрганизаций КАК УчетнаяПолитикаНалоговыйУчет
		|		ПО УчетнаяПолитикаНалоговыйУчет.Организация = НДСРеализация0Остатки.Организация
		|			И (УчетнаяПолитикаНалоговыйУчет.Период В
		|				(ВЫБРАТЬ
		|					МАКСИМУМ(УчетнаяПолитикаНалоговыйУчет.Период) КАК Период
		|				ИЗ
		|					РегистрСведений.УчетнаяПолитикаОрганизаций КАК УчетнаяПолитикаНалоговыйУчет
		|				ГДЕ
		|					ЕСТЬNULL(НДСРеализация0Остатки.СчетФактура.Дата, &КонецПериода) >= УчетнаяПолитикаНалоговыйУчет.Период
		|					И УчетнаяПолитикаНалоговыйУчет.Организация = НДСРеализация0Остатки.Организация))
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.НДСНачисленный.Остатки(&КонецПериодаГраница, Организация = &Организация) КАК НДСНачисленныйОстатки
		|		ПО НДСРеализация0Остатки.СчетФактура = НДСНачисленныйОстатки.СчетФактура
		|			И НДСРеализация0Остатки.ВидЦенности = НДСНачисленныйОстатки.ВидЦенности
		|			И НДСРеализация0Остатки.СтавкаНДС = НДСНачисленныйОстатки.СтавкаНДС
		|ГДЕ
		|	(НДСРеализация0Остатки.СуммаБезНДСОстаток > 0
		|			ИЛИ НДСРеализация0Остатки.НДСОстаток > 0)
		|
		|УПОРЯДОЧИТЬ ПО
		|	СчетФактураДата
		|ИТОГИ
		|	СУММА(СуммаБезНДС),
		|	СУММА(СуммаНДС),
		|	СУММА(СуммаСНДС),
		|	МАКСИМУМ(ТребуетсяКорректировка)
		|ПО
		|	СчетФактура";

	Запрос.УстановитьПараметр("Организация",  Организация);
	Запрос.УстановитьПараметр("КонецПериода", КонецДня(Дата));
	Запрос.УстановитьПараметр("КонецПериодаГраница", Новый Граница(КонецДня(Дата),ВидГраницы.Включая));

	Запрос.УстановитьПараметр("Начало2006Года", '20060101');
	Запрос.УстановитьПараметр("МоментОпределения_ПоОтгрузке", Перечисления.МоментыОпределенияНалоговойБазыНДС.ПоОтгрузке);

	ОтрабатываемыеСостояния = Новый СписокЗначений;
	ОтрабатываемыеСостояния.Добавить(Перечисления.НДССостоянияРеализация0.ПодтвержденаРеализация0);
	ОтрабатываемыеСостояния.Добавить(Перечисления.НДССостоянияРеализация0.НеПодтвержденаРеализация0);
	Если ВключитьОжиданиеПодтверждения Тогда
		ОтрабатываемыеСостояния.Добавить(Перечисления.НДССостоянияРеализация0.ОжидаетсяПодтверждение);
	КонецЕсли; 
	
	Запрос.УстановитьПараметр("ОтрабатываемыеСостояния",ОтрабатываемыеСостояния);
	Если не СписокСчетовФактур = Неопределено Тогда
	   Запрос.Текст = СтрЗаменить(Запрос.Текст,"Организация = &Организация"," 
			|		Организация = &Организация
			|		    И СчетФактура В (&СписокСчетовФактур)
			|");
		Запрос.УстановитьПараметр("СписокСчетовФактур",СписокСчетовФактур);
	КонецЕсли; 
	
	РезультатЗапроса = Запрос.Выполнить().Выгрузить(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	СФДляКорректировки = РезультатЗапроса.Строки.НайтиСтроки(Новый Структура("ТребуетсяКорректировка", Истина));
	Для каждого СтрокаСФ Из СФДляКорректировки Цикл
		Для каждого СтрокаРасшифровки Из СтрокаСФ.Строки Цикл
			Если СтрокаРасшифровки.ТребуетсяКорректировка Тогда
				СтрокаРасшифровки.СуммаНДС = (СтрокаРасшифровки.СуммаНДС*СтрокаРасшифровки.СуммаСНДС_НачисленоВсего/СтрокаРасшифровки.СуммаСНДС);
				СтрокаРасшифровки.СуммаСНДС = СтрокаРасшифровки.СуммаСНДС_НачисленоВсего;
				СтрокаРасшифровки.СуммаБезНДС = СтрокаРасшифровки.СуммаСНДС - СтрокаРасшифровки.СуммаНДС;
			КонецЕсли; 
		КонецЦикла; 
		
		СтрокаСФ.СуммаНДС = СтрокаСФ.Строки.Итог("СуммаНДС");
		СтрокаСФ.СуммаСНДС = СтрокаСФ.Строки.Итог("СуммаСНДС");
		СтрокаСФ.СуммаБезНДС = СтрокаСФ.Строки.Итог("СуммаБезНДС");
	КонецЦикла; 
	
	Возврат РезультатЗапроса;

КонецФункции // ЗаполнитьНДСКВычетуПоДаннымРегистраНДСРеализация0()

// Заполняет раздел "Авансы"
//
Процедура ЗаполнитьРазделАвансы(ОтменитьПроведение = Ложь, УдалятьНезаполненные = Ложь) Экспорт
	
	Если Проведен Тогда
		Если ОтменитьПроведение Тогда
			Записать(РежимЗаписиДокумента.ОтменаПроведения);
		Иначе
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	ТаблицаРезультатов = Авансы.ВыгрузитьКолонки();
	
	Таблица_НДСcАвансов = ЗаполнитьНДСНачисленныйДаннымиПоАвансам();
	
	Если Таблица_НДСcАвансов.Количество()=0 Тогда
		// Дальнейшая обработка не требуется, не обнаружен НДС, который может быть отражен в книге.
		Авансы.Очистить();
		Возврат;
	КонецЕсли;
	
	ОбщегоНазначения.ЗагрузитьВТаблицуЗначений(Таблица_НДСcАвансов,ТаблицаРезультатов);

	// Удаление строк, в которых есть незаполненные обязательные поля
	Если УдалятьНезаполненные Тогда
		ОбработкаТабличныхЧастейСервер.УдалитьНезаполненныеСтроки(ТаблицаРезультатов, "ВидЦенности, СчетФактура, СтавкаНДС, ДоговорКонтрагента");
	КонецЕсли;
	
	Авансы.Загрузить(ТаблицаРезультатов);

КонецПроцедуры // ЗаполнитьОсновнойРаздел()

// Вызывается из процедуры ЗаполнитьРазделАвансы.
// Заполняет ТЧ Реализация по данным регистра НДС начисленный (по НДС с авансов)
Функция ЗаполнитьНДСНачисленныйДаннымиПоАвансам()

	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	НДСНачисленныйОстатки.СчетФактура КАК СчетФактура,
	|	НДСНачисленныйОстатки.ВидЦенности,
	|	НДСНачисленныйОстатки.СтавкаНДС,
	|	НДСНачисленныйОстатки.Покупатель,
	|	НДСНачисленныйОстатки.СчетФактура.Дата КАК СчетФактураДата,
	|	НДСНачисленныйОстатки.СуммаБезНДСОстаток КАК СуммаБезНДС,
	|	НДСНачисленныйОстатки.НДСОстаток КАК НДС,
	|	&Событие_НДСсАвансов КАК Событие,
	|	НДСНачисленныйОстатки.ДоговорКонтрагента
	|ИЗ
	|	РегистрНакопления.НДСНачисленный.Остатки(
	|			&КонецПериодаГраница,
	|			Организация = &Организация
	|				И ВидНачисления = &НДСВидНачисления_НДСсАвансов) КАК НДСНачисленныйОстатки
	|ГДЕ
	|	(НЕ(НДСНачисленныйОстатки.СуммаБезНДСОстаток = 0
	|				И НДСНачисленныйОстатки.НДСОстаток = 0))
	|
	|УПОРЯДОЧИТЬ ПО
	|	СчетФактураДата,
	|	СчетФактура";

	Запрос.УстановитьПараметр("Организация",  Организация);
	Запрос.УстановитьПараметр("КонецПериодаГраница", Новый Граница(КонецДня(Дата),ВидГраницы.Включая));
	Запрос.УстановитьПараметр("НДСВидНачисления_НДСсАвансов", Перечисления.НДСВидНачисления.НДСсАвансов);
	Запрос.УстановитьПараметр("Событие_НДСсАвансов", Перечисления.СобытияПоНДСПродажи.ПолученАванс);

	Возврат Запрос.Выполнить().Выгрузить(ОбходРезультатаЗапроса.Прямой);
	
КонецФункции // ЗаполнитьНДСКВычетуПоДаннымРегистраНДСНачисленный()

// Заполняет раздел "НачисленКУплате"
//
Процедура ЗаполнитьРазделНачисленКУплате(ОтменитьПроведение = Ложь, УдалятьНезаполненные = Ложь) Экспорт
	
	Если Проведен Тогда
		Если ОтменитьПроведение Тогда
			Записать(РежимЗаписиДокумента.ОтменаПроведения);
		Иначе
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	ТаблицаРезультатов = НачисленКУплате.ВыгрузитьКолонки();
	
	Таблица_НДСНачисленКУплате = ЗаполнитьНДСНачисленныйДаннымиПоНДСНачисленномуКУплате();
	
	Если Таблица_НДСНачисленКУплате.Количество()=0 Тогда
		// Дальнейшая обработка не требуется, не обнаружен НДС, который может быть отражен в книге.
		НачисленКУплате.Очистить();
		Возврат;
	КонецЕсли;
	
	ОбщегоНазначения.ЗагрузитьВТаблицуЗначений(Таблица_НДСНачисленКУплате,ТаблицаРезультатов);

	// Удаление строк, в которых есть незаполненные обязательные поля
	Если УдалятьНезаполненные Тогда
		ОбработкаТабличныхЧастейСервер.УдалитьНезаполненныеСтроки(ТаблицаРезультатов, "ВидЦенности, СчетФактура, СтавкаНДС");
	КонецЕсли;
	
	НачисленКУплате.Загрузить(ТаблицаРезультатов);

КонецПроцедуры // ЗаполнитьОсновнойРаздел()

// Заполняет раздел "Восстановлен"
//
Процедура ЗаполнитьРазделВосстановлен(ОтменитьПроведение = Ложь, УдалятьНезаполненные = Ложь) Экспорт
	
	Если Проведен Тогда
		Если ОтменитьПроведение Тогда
			Записать(РежимЗаписиДокумента.ОтменаПроведения);
		Иначе
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	ТаблицаРезультатов = Восстановлен.ВыгрузитьКолонки();
	
	Таблица_Восстановлен = ЗаполнитьНДСНачисленныйДаннымиПоНДСВосстановленному();
	
	Если Таблица_Восстановлен.Количество()=0 Тогда
		// Дальнейшая обработка не требуется, не обнаружен НДС, который может быть отражен в книге.
		Восстановлен.Очистить();
		Возврат;
	КонецЕсли;
	
	ОбщегоНазначения.ЗагрузитьВТаблицуЗначений(Таблица_Восстановлен, ТаблицаРезультатов);

	// Удаление строк, в которых есть незаполненные обязательные поля
	Если УдалятьНезаполненные Тогда
		ОбработкаТабличныхЧастейСервер.УдалитьНезаполненныеСтроки(ТаблицаРезультатов, "ВидЦенности, СчетФактура, СтавкаНДС, СчетУчетаНДС");
	КонецЕсли;
	
	Восстановлен.Загрузить(ТаблицаРезультатов);

КонецПроцедуры // ЗаполнитьОсновнойРаздел()

// Заполняет раздел "Восстановлен по авансам"
Процедура ЗаполнитьРазделВосстановленПоАвансам(ОтменитьПроведение = Ложь, УдалятьНезаполненные = Ложь) Экспорт
	
	Если Проведен Тогда
		Если ОтменитьПроведение Тогда
			Записать(РежимЗаписиДокумента.ОтменаПроведения);
		Иначе
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	ТаблицаРезультатов = ВосстановленПоАвансам.ВыгрузитьКолонки();
	
	Таблица_Восстановлен = ЗаполнитьНДСНачисленныйДаннымиПоНДСВосстановленномуПоАвансам();
	
	Если Таблица_Восстановлен.Количество()=0 Тогда
		// Дальнейшая обработка не требуется, не обнаружен НДС, который может быть отражен в книге.
		ВосстановленПоАвансам.Очистить();
		Возврат;
	КонецЕсли;
	
	ОбщегоНазначения.ЗагрузитьВТаблицуЗначений(Таблица_Восстановлен, ТаблицаРезультатов);

	// Удаление строк, в которых есть незаполненные обязательные поля
	Если УдалятьНезаполненные Тогда
		ОбработкаТабличныхЧастейСервер.УдалитьНезаполненныеСтроки(ТаблицаРезультатов, "Поставщик, ДоговорКонтрагента, СчетФактура, СтавкаНДС");
	КонецЕсли;
	
	ВосстановленПоАвансам.Загрузить(ТаблицаРезультатов);

КонецПроцедуры // ЗаполнитьОсновнойРаздел()

// Вызывается из процедуры ЗаполнитьРазделНачисленКУплате.
// Заполняет ТЧ Реализация по данным регистра НДС начисленный (по НДС начисленному к уплате)
Функция ЗаполнитьНДСНачисленныйДаннымиПоНДСНачисленномуКУплате()

	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	НДСНачисленныйОстатки.СчетФактура КАК СчетФактура,
	|	НДСНачисленныйОстатки.ВидЦенности,
	|	НДСНачисленныйОстатки.СтавкаНДС,
	|	НДСНачисленныйОстатки.Покупатель,
	|	НДСНачисленныйОстатки.ДоговорКонтрагента,
	|	НДСНачисленныйОстатки.СчетФактура.Дата КАК СчетФактураДата,
	|	НДСНачисленныйОстатки.СуммаБезНДСОстаток КАК СуммаБезНДС,
	|	НДСНачисленныйОстатки.НДСОстаток КАК НДС,
	|	ВЫБОР
	|		КОГДА НДСНачисленныйОстатки.СуммаБезНДСОстаток < 0
	|				ИЛИ НДСНачисленныйОстатки.НДСОстаток < 0
	|			ТОГДА &Событие_Скорректирован
	|		ИНАЧЕ &Событие_НДСНачисленКУплате
	|	КОНЕЦ КАК Событие,
	|	ВЫБОР
	|		КОГДА НДСНачисленныйОстатки.ДоговорКонтрагента.УчетАгентскогоНДС
	|				И НДСНачисленныйОстатки.ДоговорКонтрагента.НалоговыйАгентПоОплате
	|			ТОГДА ВЫБОР
	|					КОГДА НДСНачисленныйОстатки.СчетФактура ССЫЛКА Документ.ПлатежноеПоручениеИсходящее
	|							ИЛИ НДСНачисленныйОстатки.СчетФактура ССЫЛКА Документ.ПлатежныйОрдерСписаниеДенежныхСредств
	|							ИЛИ НДСНачисленныйОстатки.СчетФактура ССЫЛКА Документ.ПлатежноеТребованиеПолученное
	|							ИЛИ НДСНачисленныйОстатки.СчетФактура ССЫЛКА Документ.РасходныйКассовыйОрдер
	|						ТОГДА НДСНачисленныйОстатки.СчетФактура
	|					ИНАЧЕ НЕОПРЕДЕЛЕНО
	|				КОНЕЦ
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК ДокументОплаты,
	|	ВЫБОР
	|		КОГДА НДСНачисленныйОстатки.ДоговорКонтрагента.УчетАгентскогоНДС
	|				И НДСНачисленныйОстатки.ДоговорКонтрагента.НалоговыйАгентПоОплате
	|			ТОГДА ВЫБОР
	|					КОГДА НДСНачисленныйОстатки.СчетФактура ССЫЛКА Документ.ПлатежноеПоручениеИсходящее
	|							ИЛИ НДСНачисленныйОстатки.СчетФактура ССЫЛКА Документ.ПлатежныйОрдерСписаниеДенежныхСредств
	|							ИЛИ НДСНачисленныйОстатки.СчетФактура ССЫЛКА Документ.ПлатежноеТребованиеПолученное
	|							ИЛИ НДСНачисленныйОстатки.СчетФактура ССЫЛКА Документ.РасходныйКассовыйОрдер
	|						ТОГДА НДСНачисленныйОстатки.СчетФактура.Дата
	|					ИНАЧЕ ДАТАВРЕМЯ(1, 1, 1)
	|				КОНЕЦ
	|		ИНАЧЕ ДАТАВРЕМЯ(1, 1, 1)
	|	КОНЕЦ КАК ДатаОплаты
	|ИЗ
	|	РегистрНакопления.НДСНачисленный.Остатки(
	|			&КонецПериодаГраница,
	|			Организация = &Организация
	|				И ВидНачисления = &НДСВидНачисления_НДСНачисленКУплате) КАК НДСНачисленныйОстатки
	|ГДЕ
	|	(НЕ(НДСНачисленныйОстатки.СуммаБезНДСОстаток = 0
	|				И НДСНачисленныйОстатки.НДСОстаток = 0))
	|
	|УПОРЯДОЧИТЬ ПО
	|	СчетФактураДата,
	|	СчетФактура";

	Запрос.УстановитьПараметр("Организация",  Организация);
	Запрос.УстановитьПараметр("КонецПериодаГраница", Новый Граница(КонецДня(Дата),ВидГраницы.Включая));
	Запрос.УстановитьПараметр("НДСВидНачисления_НДСНачисленКУплате", Перечисления.НДСВидНачисления.НДСНачисленКУплате);
	Запрос.УстановитьПараметр("Событие_НДСНачисленКУплате", 		 Перечисления.СобытияПоНДСПродажи.НДСНачисленКУплате);
	Запрос.УстановитьПараметр("Событие_Скорректирован", 			 Перечисления.СобытияПоНДСПродажи.НДССкорректирован);

	Возврат Запрос.Выполнить().Выгрузить(ОбходРезультатаЗапроса.Прямой);
	
КонецФункции // ЗаполнитьНДСКВычетуПоДаннымРегистраНДСНачисленный()

// Вызывается из процедуры ЗаполнитьРазделВосстановлен.
// Заполняет ТЧ Реализация по данным регистра НДС начисленный (по НДС начисленному к уплате)
Функция ЗаполнитьНДСНачисленныйДаннымиПоНДСВосстановленному()

	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	НДСНачисленныйОстатки.СчетФактура КАК СчетФактура,
	|	НДСНачисленныйОстатки.ВидЦенности,
	|	НДСНачисленныйОстатки.СтавкаНДС,
	|	НДСНачисленныйОстатки.Покупатель,
	|	НДСНачисленныйОстатки.ИсправленныйСчетФактура,
	|	НДСНачисленныйОстатки.СчетФактура.Дата КАК СчетФактураДата,
	|	НДСНачисленныйОстатки.СуммаБезНДСОстаток КАК СуммаБезНДС,
	|	НДСНачисленныйОстатки.НДСОстаток КАК НДС,
	|	НДСНачисленныйОстатки.СуммаБезНДСОстаток + НДСНачисленныйОстатки.НДСОстаток КАК СуммаСНДС
	|ИЗ
	|	РегистрНакопления.НДСНачисленный.Остатки(
	|			&КонецПериодаГраница,
	|			Организация = &Организация
	|				И ВидНачисления = &НДСВидНачисления) КАК НДСНачисленныйОстатки
	|ГДЕ
	|	(НЕ(НДСНачисленныйОстатки.СуммаБезНДСОстаток = 0
	|				И НДСНачисленныйОстатки.НДСОстаток = 0))
	|
	|УПОРЯДОЧИТЬ ПО
	|	СчетФактураДата,
	|	СчетФактура";

	Запрос.УстановитьПараметр("Организация",  Организация);
	Запрос.УстановитьПараметр("КонецПериодаГраница", Новый Граница(КонецДня(Дата),ВидГраницы.Включая));
	Запрос.УстановитьПараметр("НДСВидНачисления", 	 Перечисления.НДСВидНачисления.НДСВосстановлен);
	
	НДСкВосстановлению = Запрос.Выполнить().Выгрузить(ОбходРезультатаЗапроса.Прямой);
	НДСкВосстановлению.Колонки.Добавить("СчетУчетаНДС", новый описаниеТипов("ПланСчетовСсылка.Хозрасчетный"));
	
	Если НДСкВосстановлению.Количество() = 0 Тогда
		Возврат НДСкВосстановлению;
	КонецЕсли; 
	
	//Определение счетов учета НДС для формирования проводки по восстановлению
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	НДСНачисленный.СчетФактура КАК СчетФактура,
	|	НДСНачисленный.ВидЦенности,
	|	НДСНачисленный.СтавкаНДС,
	|	НДСНачисленный.ИсправленныйСчетФактура,
	|	НДСНачисленный.СчетУчетаНДС,
	|	СУММА((НДСНачисленный.СуммаБезНДС + НДСНачисленный.НДС) * ВЫБОР
	|			КОГДА НДСНачисленный.ВидДвижения = &ДвижениеПриход
	|				ТОГДА 1
	|			ИНАЧЕ -1
	|		КОНЕЦ) КАК СуммаСНДС
	|ИЗ
	|	РегистрНакопления.НДСНачисленный КАК НДСНачисленный
	|ГДЕ
	|	НДСНачисленный.Организация = &Организация
	|	И НДСНачисленный.СчетФактура В(&СписокСчетовФактур)
	|	И НДСНачисленный.ВидНачисления = &НДСВидНачисления
	|	И НДСНачисленный.ВидЦенности В(&СписокВидовЦенностей)
	|	И НДСНачисленный.СтавкаНДС В(&СписокСтавокНДС)
	|	И НДСНачисленный.Период <= &КонецПериода
	|	И (НЕ НДСНачисленный.СчетУчетаНДС = &ПустойСчет)
	|
	|СГРУППИРОВАТЬ ПО
	|	НДСНачисленный.Организация,
	|	НДСНачисленный.ВидЦенности,
	|	НДСНачисленный.СчетУчетаНДС,
	|	НДСНачисленный.СчетФактура,
	|	НДСНачисленный.СтавкаНДС,
	|	НДСНачисленный.ИсправленныйСчетФактура
	|
	|ИМЕЮЩИЕ
	|	СУММА((НДСНачисленный.СуммаБезНДС + НДСНачисленный.НДС) * ВЫБОР
	|			КОГДА НДСНачисленный.ВидДвижения = &ДвижениеПриход
	|				ТОГДА 1
	|			ИНАЧЕ -1
	|		КОНЕЦ) > 0
	|ИТОГИ ПО
	|	СчетФактура";
	
	Запрос.УстановитьПараметр("КонецПериода"	, КонецДня(Дата));
	Запрос.УстановитьПараметр("ПустаяДата"		, '00010101');
	Запрос.УстановитьПараметр("ПустойСчет"		, ПланыСчетов.Хозрасчетный.ПустаяСсылка());
	Запрос.УстановитьПараметр("Организация"		, Организация);
	Запрос.УстановитьПараметр("НДСВидНачисления", Перечисления.НДСВидНачисления.НДСВосстановлен);
	
	Запрос.УстановитьПараметр("СписокСчетовФактур", ОбщегоНазначения.УдалитьПовторяющиесяЭлементыМассива(НДСкВосстановлению.ВыгрузитьКолонку("СчетФактура"),Истина));
	Запрос.УстановитьПараметр("СписокВидовЦенностей", ОбщегоНазначения.УдалитьПовторяющиесяЭлементыМассива(НДСкВосстановлению.ВыгрузитьКолонку("ВидЦенности"),Истина));
	Запрос.УстановитьПараметр("СписокСтавокНДС"	, ОбщегоНазначения.УдалитьПовторяющиесяЭлементыМассива(НДСкВосстановлению.ВыгрузитьКолонку("СтавкаНДС"),Истина));
	
	Запрос.УстановитьПараметр("ДвижениеПриход", ВидДвиженияНакопления.Приход);
	
	НДСкВосстановлениюВРазрезеСчетов = Запрос.Выполнить().Выгрузить(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	СтрокиКУдалению = Новый Массив();
	КоличествоБазовыхСтрок = НДСкВосстановлению.Количество();
	
	Для НомерСтрокиВосстановления =1 по КоличествоБазовыхСтрок Цикл
		СтрокаВосстановления = НДСкВосстановлению[НомерСтрокиВосстановления-1];
		СтрокаСФВРазезеСчетов = НДСкВосстановлениюВРазрезеСчетов.Строки.Найти(СтрокаВосстановления.СчетФактура,"СчетФактура");
		Если СтрокаСФВРазезеСчетов = Неопределено Тогда
			Продолжить;
		КонецЕсли; 
		
		РасшифровкаСФвРазрезеСчетов = СтрокаСФВРазезеСчетов.Строки.НайтиСтроки(Новый Структура("ВидЦенности,СтавкаНДС",СтрокаВосстановления.ВидЦенности,СтрокаВосстановления.СтавкаНДС));
		
		Если РасшифровкаСФвРазрезеСчетов.Количество() =0 Тогда
			Продолжить;
		КонецЕсли; 
		
		СуммаСНДСПоСтроке = СтрокаВосстановления.СуммаСНДС;
		Для каждого СтрокаРасшифровки Из РасшифровкаСФвРазрезеСчетов Цикл
			СуммаПокрытия = 0;
			Если не СуммаСНДСПоСтроке*СтрокаРасшифровки.СуммаСНДС>0 Тогда
				Продолжить;
			КонецЕсли; 
			
			СуммаПокрытия = ?(СуммаСНДСПоСтроке>0, Мин(СуммаСНДСПоСтроке,СтрокаРасшифровки.СуммаСНДС), Макс(СуммаСНДСПоСтроке,СтрокаРасшифровки.СуммаСНДС));
			
			//Добавление новой строки по восстановлению в разрезе СФ
			НоваяСтрокаВосстановления = НДСкВосстановлению.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрокаВосстановления, СтрокаВосстановления);
			НоваяСтрокаВосстановления.СуммаСНДС = СуммаПокрытия;
			НоваяСтрокаВосстановления.СуммаБезНДС = Окр(НоваяСтрокаВосстановления.СуммаБезНДС*СуммаПокрытия/СуммаСНДСПоСтроке,2);
			НоваяСтрокаВосстановления.НДС = НоваяСтрокаВосстановления.СуммаСНДС - НоваяСтрокаВосстановления.СуммаБезНДС;
			НоваяСтрокаВосстановления.СчетУчетаНДС = СтрокаРасшифровки.СчетУчетаНДС;
			
			СуммаСНДСПоСтроке = СуммаСНДСПоСтроке - СуммаПокрытия;
			
			//Уменьшение суммы по строке восстановления без указания счета
			СтрокаВосстановления.СуммаСНДС = СтрокаВосстановления.СуммаСНДС - НоваяСтрокаВосстановления.СуммаСНДС;
			СтрокаВосстановления.СуммаБезНДС = СтрокаВосстановления.СуммаБезНДС - НоваяСтрокаВосстановления.СуммаБезНДС;
			СтрокаВосстановления.НДС = СтрокаВосстановления.НДС - НоваяСтрокаВосстановления.НДС;
			
			//Уменьшение остатка в таблице расшифровки 
			СтрокаРасшифровки.СуммаСНДС = СтрокаРасшифровки.СуммаСНДС - НоваяСтрокаВосстановления.СуммаСНДС;
			
			Если СуммаСНДСПоСтроке = 0 Тогда
				СтрокиКУдалению.Добавить(СтрокаВосстановления);
				Прервать;
			КонецЕсли; 
			
		КонецЦикла; 
		
		Если не СуммаСНДСПоСтроке = 0 Тогда
			СтрокиКУдалению.Добавить(СтрокаВосстановления);
			НоваяСтрокаВосстановления = НДСкВосстановлению.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрокаВосстановления, СтрокаВосстановления);
		КонецЕсли; 
	КонецЦикла;
	
	Для каждого СтрокаКУдалению Из СтрокиКУдалению Цикл
		НДСкВосстановлению.Удалить(СтрокаКУдалению);
	КонецЦикла; 

	Возврат НДСкВосстановлению;
		
КонецФункции // ЗаполнитьНДСКВычетуПоДаннымРегистраНДСНачисленный()

// Вызывается из процедуры ЗаполнитьРазделВосстановленПоАвансам.
// Заполняет ТЧ Реализация по данным регистра НДС начисленный (по НДС начисленному к уплате)
Функция ЗаполнитьНДСНачисленныйДаннымиПоНДСВосстановленномуПоАвансам()

	ТаблицаВосстановлен = ВосстановленПоАвансам .ВыгрузитьКолонки();
	
	// Восстановление НДС по авансам выданным
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("Организация",  Организация);
	Запрос.УстановитьПараметр("КонецПериодаГраница", Новый Граница(КонецДня(Дата),ВидГраницы.Включая));
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	НДСсАвансовОстатки.Организация,
	|	НДСсАвансовОстатки.СчетФактура,
	|	НДСсАвансовОстатки.Покупатель КАК Поставщик,
	|	НДСсАвансовОстатки.ДоговорКонтрагента,
	|	НДСсАвансовОстатки.СтавкаНДС,
	|	НДСсАвансовОстатки.СуммаБезНДСОстаток - ЕСТЬNULL(НДСПредъявленныйОстатки.СуммаБезНДСОстаток, 0) КАК СуммаБезНДС,
	|	НДСсАвансовОстатки.НДСОстаток - ЕСТЬNULL(НДСПредъявленныйОстатки.НДСОстаток, 0) КАК НДС,
	|	НДСсАвансовОстатки.ВидЦенности,
	|	НДСсАвансовОстатки.ИсправленныйСчетФактура
	|ИЗ
	|	РегистрНакопления.НДСсАвансов.Остатки(
	|			&КонецПериодаГраница,
	|			Организация = &Организация
	|				И ВидЦенности = ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.АвансыВыданные)) КАК НДСсАвансовОстатки
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.НДСПредъявленный.Остатки(
	|				&КонецПериодаГраница,
	|				Организация = &Организация
	|					И ВидЦенности = ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.АвансыВыданные)) КАК НДСПредъявленныйОстатки
	|		ПО НДСсАвансовОстатки.Организация = НДСПредъявленныйОстатки.Организация
	|			И НДСсАвансовОстатки.СчетФактура = НДСПредъявленныйОстатки.СчетФактура
	|			И НДСсАвансовОстатки.Покупатель = НДСПредъявленныйОстатки.Поставщик
	|			И НДСсАвансовОстатки.ДоговорКонтрагента = НДСПредъявленныйОстатки.ДоговорКонтрагента
	|			И НДСсАвансовОстатки.СтавкаНДС = НДСПредъявленныйОстатки.СтавкаНДС
	|			И НДСсАвансовОстатки.ВидЦенности = НДСПредъявленныйОстатки.ВидЦенности
	|ГДЕ
	|	НДСсАвансовОстатки.СуммаБезНДСОстаток - ЕСТЬNULL(НДСПредъявленныйОстатки.СуммаБезНДСОстаток, 0) > 0
	|	И НДСсАвансовОстатки.НДСОстаток - ЕСТЬNULL(НДСПредъявленныйОстатки.НДСОстаток, 0) > 0";
				   
				   
	Результат = Запрос.Выполнить();
	
	Если Результат.Пустой() Тогда
		Возврат ТаблицаВосстановлен;
	КонецЕсли;

	ТаблицаНДССАвансов = Результат.Выгрузить();
	
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("ТаблицаНДССАвансов", ТаблицаНДССАвансов);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ТаблицаНДССАвансов.СчетФактура КАК СчетФактура
	|ПОМЕСТИТЬ ТаблицаНДССАвансов
	|ИЗ
	|	&ТаблицаНДССАвансов КАК ТаблицаНДССАвансов
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РасчетыПоПриобретениюВУсловныхЕдиницахОрганизацииОстатки.Контрагент КАК Поставщик,
	|	РасчетыПоПриобретениюВУсловныхЕдиницахОрганизацииОстатки.ДоговорКонтрагента,
	|	РасчетыПоПриобретениюВУсловныхЕдиницахОрганизацииОстатки.Документ КАК СчетФактура,
	|	РасчетыПоПриобретениюВУсловныхЕдиницахОрганизацииОстатки.СуммаРегОстаток КАК Сумма
	|ИЗ
	|	РегистрНакопления.РасчетыПоПриобретениюВУсловныхЕдиницахОрганизации.Остатки(
	|			&КонецПериодаГраница,
	|			Организация = &Организация
	|				И Документ В
	|					(ВЫБРАТЬ
	|						ТаблицаНДССАвансов.СчетФактура
	|					ИЗ
	|						ТаблицаНДССАвансов КАК ТаблицаНДССАвансов)) КАК РасчетыПоПриобретениюВУсловныхЕдиницахОрганизацииОстатки
	|ГДЕ
	|	РасчетыПоПриобретениюВУсловныхЕдиницахОрганизацииОстатки.СуммаРегОстаток > 0";
				   
	ТаблицаНезачтенныхАвансов = Запрос.Выполнить().Выгрузить();
	
	СтруктураОтбора = Новый Структура("Поставщик, ДоговорКонтрагента, СчетФактура");
	МассивРаспределения = Новый Массив;
	
	Для Каждого СтрокаТаблицы Из ТаблицаНезачтенныхАвансов Цикл
		
		Если СтрокаТаблицы.Сумма = 0 Тогда
			Продолжить;
		КонецЕсли;
		
		ЗаполнитьЗначенияСвойств(СтруктураОтбора, СтрокаТаблицы);
		СтрокиОтбора = ТаблицаНДССАвансов.НайтиСтроки(СтруктураОтбора);
		
		Для Каждого СтрокаОтбора Из СтрокиОтбора Цикл
			
			Если (СтрокаОтбора.СуммаБезНДС = 0
				И СтрокаОтбора.НДС = 0) Или СтрокаТаблицы.Сумма = 0 Тогда
				Продолжить;
			КонецЕсли;
			
			СуммаРаспределения = Мин(СтрокаТаблицы.Сумма, СтрокаОтбора.СуммаБезНДС + СтрокаОтбора.НДС);
			СтрокаТаблицы.Сумма = СтрокаТаблицы.Сумма - СуммаРаспределения;
			МассивРаспределения.Очистить();
			
			МассивРаспределения.Добавить(СтрокаОтбора.СуммаБезНДС);
			МассивРаспределения.Добавить(СтрокаОтбора.НДС);
			
			ИтогРаспределения = ОбщегоНазначения.РаспределитьПропорционально(СуммаРаспределения, МассивРаспределения);
			
			СтрокаОтбора.СуммаБезНДС = СтрокаОтбора.СуммаБезНДС - ИтогРаспределения[0];
			СтрокаОтбора.НДС = СтрокаОтбора.НДС - ИтогРаспределения[1];
			
		КонецЦикла;
		
	КонецЦикла;
	
	СтрокиКУдалению = Новый Массив;
	Для	Каждого СтрокаТаблицыНДССАвансов Из ТаблицаНДССАвансов Цикл
		Если СтрокаТаблицыНДССАвансов.СуммаБезНДС = 0
			И СтрокаТаблицыНДССАвансов.НДС = 0 Тогда
			СтрокиКУдалению.Добавить(СтрокаТаблицыНДССАвансов);
		КонецЕсли;
	КонецЦикла;
	
	Для Каждого СтрокаКУдалению Из СтрокиКУдалению Цикл
		ТаблицаНДССАвансов.Удалить(СтрокаКУдалению);
	КонецЦикла;
	
	ОбщегоНазначения.ЗагрузитьВТаблицуЗначений(ТаблицаНДССАвансов, ТаблицаВосстановлен);
	
	Возврат ТаблицаВосстановлен;
		
КонецФункции // ЗаполнитьНДСКВычетуПоДаннымРегистраНДСНачисленный()

// Заполняет раздел "НеОтражаетсяВКниге"
Процедура ЗаполнитьРазделНеОтражаетсяВКниге(ОтменитьПроведение = Ложь, УдалятьНезаполненные = Ложь) Экспорт
	
	ТаблицаРезультатов = НеОтражаетсяВКниге.ВыгрузитьКолонки();
	ТаблицаРезультатов.Колонки.Добавить("ЗаписьДополнительногоЛиста",новый ОписаниеТипов("Булево"));
	ТаблицаРезультатов.Колонки.Добавить("КорректируемыйПериод");
	
	Дерево_НДСНачисленный = ЗаполнитьНДСНачисленныйПоДаннымРегистраНДСНачисленный_Неотражаемые();
	
	Если Дерево_НДСНачисленный.Строки.Количество()=0 Тогда
		// Дальнейшая обработка не требуется, не обнаружен НДС, который может быть принят к вычету.
		НеОтражаетсяВКниге.Очистить();
		Возврат;
	КонецЕсли;

	СписокСчетовФактур = ОбщегоНазначения.УдалитьПовторяющиесяЭлементыМассива(Дерево_НДСНачисленный.Строки.ВыгрузитьКолонку("СчетФактура"),Истина);
	
	РаспределенныеОплаты = ПолучитьДанныеОРаспределенныхОплатахПоСпискуСФ(СписокСчетовФактур);
	
	// Заблокировать оплаты, зарегистрированные по счетам-фактурам,
	// которые могут быть использованы для погашения отражаемых в книге начислений.
	ДеревоНДС_ВычетЗаблокирован = ЗаполнитьНДСНачисленныйПоДаннымРегистраНДСНачисленный(СписокСчетовФактур);
	ДеревоНДС_ВычетЗаблокирован0 = ЗаполнитьНДСНачисленныйПоДаннымРегистраНДСРеализация0(Истина, СписокСчетовФактур);
	
	Если ДеревоНДС_ВычетЗаблокирован.Строки.Количество()>0 Тогда
		РаспределитьОплатыПоДеревуСФ(ДеревоНДС_ВычетЗаблокирован, ТаблицаРезультатов,СписокСчетовФактур,РаспределенныеОплаты, Ложь);
	КонецЕсли; 
	
	Если ДеревоНДС_ВычетЗаблокирован0.Строки.Количество()>0 Тогда
		РаспределитьОплатыПоДеревуСФ(ДеревоНДС_ВычетЗаблокирован0, ТаблицаРезультатов,СписокСчетовФактур,РаспределенныеОплаты, Ложь);
	КонецЕсли; 
	
	// Отражаем только оплаченные записи вне азвисимости от учетной политики - необходимо погасить запись в регистре начисления и в регистре оплат.
	РаспределитьОплатыПоДеревуСФ(Дерево_НДСНачисленный, ТаблицаРезультатов,СписокСчетовФактур,РаспределенныеОплаты, Истина, Истина);

	НеОтражаетсяВКниге.Загрузить(ТаблицаРезультатов);

КонецПроцедуры // ЗаполнитьОсновнойРаздел()

// Вызывается из процедуры ЗаполнитьРазделНеОтражаетсяВКниге.
// Заполняет ТЧ НеОтражаетсяВКниге по данным регистра НДС начисленный (по реализации ЕНВД)
Функция ЗаполнитьНДСНачисленныйПоДаннымРегистраНДСНачисленный_Неотражаемые()

	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	НДСНачисленныйОстатки.СчетФактура КАК СчетФактура,
	|	НДСНачисленныйОстатки.ВидЦенности,
	|	НДСНачисленныйОстатки.СтавкаНДС,
	|	НДСНачисленныйОстатки.Покупатель,
	|	ЕСТЬNULL(НДСНачисленныйОстатки.СчетФактура.Дата, &КонецПериода) КАК СчетФактураДата,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(НДСНачисленныйОстатки.СчетФактура.Дата, &КонецПериода) >= &Начало2006Года
	|			ТОГДА &МоментОпределения_ПоОтгрузке
	|		ИНАЧЕ ЕСТЬNULL(УчетнаяПолитикаНалоговыйУчет.МоментОпределенияНалоговойБазыНДС, &МоментОпределения_ПоОтгрузке)
	|	КОНЕЦ КАК МоментОпределенияНалоговойБазыНДС,
	|	НДСНачисленныйОстатки.СуммаБезНДСОстаток КАК СуммаБезНДС,
	|	НДСНачисленныйОстатки.НДСОстаток КАК СуммаНДС,
	|	НДСНачисленныйОстатки.СуммаБезНДСОстаток + НДСНачисленныйОстатки.НДСОстаток КАК СуммаСНДС,
	|	ВЫБОР
	|		КОГДА НДСНачисленныйОстатки.СуммаБезНДСОстаток < 0
	|				ИЛИ НДСНачисленныйОстатки.НДСОстаток < 0
	|			ТОГДА &Событие_Скорректирован
	|		ИНАЧЕ &Событие_НДСНачисленКУплате
	|	КОНЕЦ КАК Событие,
	|	НДСНачисленныйОстатки.ВидНачисления
	|ИЗ
	|	РегистрНакопления.НДСНачисленный.Остатки(
	|		&КонецПериодаГраница,
	|		Организация = &Организация
	|		    И ВидНачисления В (&НДСВидыНачисления_Неотражаемые)) КАК НДСНачисленныйОстатки
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.УчетнаяПолитикаОрганизаций КАК УчетнаяПолитикаНалоговыйУчет
	|		ПО УчетнаяПолитикаНалоговыйУчет.Организация = НДСНачисленныйОстатки.Организация
	|			И (УчетнаяПолитикаНалоговыйУчет.Период В
	|				(ВЫБРАТЬ
	|					МАКСИМУМ(УчетнаяПолитикаНалоговыйУчет.Период) КАК Период
	|				ИЗ
	|					РегистрСведений.УчетнаяПолитикаОрганизаций КАК УчетнаяПолитикаНалоговыйУчет
	|				ГДЕ
	|					ЕСТЬNULL(НДСНачисленныйОстатки.СчетФактура.Дата, &КонецПериода) >= УчетнаяПолитикаНалоговыйУчет.Период
	|					И
	|					УчетнаяПолитикаНалоговыйУчет.Организация = НДСНачисленныйОстатки.Организация))
	|ГДЕ
	|	(НЕ(НДСНачисленныйОстатки.СуммаБезНДСОстаток = 0
	|				И НДСНачисленныйОстатки.НДСОстаток = 0))
	|
	|УПОРЯДОЧИТЬ ПО
	|	СчетФактураДата,
	|	СчетФактура
	|ИТОГИ
	|	СУММА(СуммаБезНДС),
	|	СУММА(СуммаНДС),
	|	СУММА(СуммаСНДС)
	|ПО
	|	СчетФактура";

	Запрос.УстановитьПараметр("Организация",  Организация);
	Запрос.УстановитьПараметр("КонецПериода", КонецДня(Дата));
	Запрос.УстановитьПараметр("КонецПериодаГраница", Новый Граница(КонецДня(Дата),ВидГраницы.Включая));

	Запрос.УстановитьПараметр("Начало2006Года", '20060101');
	Запрос.УстановитьПараметр("МоментОпределения_ПоОтгрузке", Перечисления.МоментыОпределенияНалоговойБазыНДС.ПоОтгрузке);
	
	НДСВидыНачисления_Неотражаемые = новый массив();
	НДСВидыНачисления_Неотражаемые.Добавить(Перечисления.НДСВидНачисления.РеализацияЕНВД);
	
	Запрос.УстановитьПараметр("НДСВидыНачисления_Неотражаемые", 	 НДСВидыНачисления_Неотражаемые);
	Запрос.УстановитьПараметр("Событие_НДСНачисленКУплате", 		 Перечисления.СобытияПоНДСПродажи.Реализация);
	Запрос.УстановитьПараметр("Событие_Скорректирован", 			 Перечисления.СобытияПоНДСПродажи.НДССкорректирован);

	Возврат Запрос.Выполнить().Выгрузить(ОбходРезультатаЗапроса.ПоГруппировкам);
	
КонецФункции // ЗаполнитьНДСКВычетуПоДаннымРегистраНДСНачисленный()

// Процедура вызывается из ЗаполнитьРазделРеализация.
// По списку счетов-фактур определяет суммы не использованных ранее распределенных оплат.
Функция ПолучитьДанныеОРаспределенныхОплатахПоСпискуСФ(СписокСчетовФактур)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	РасчетыПоРеализацииВУсловныхЕдиницахОрганизацииОбороты.Организация,
	|	РасчетыПоРеализацииВУсловныхЕдиницахОрганизацииОбороты.Период,
	|	РасчетыПоРеализацииВУсловныхЕдиницахОрганизацииОбороты.Регистратор,
	|	РасчетыПоРеализацииВУсловныхЕдиницахОрганизацииОбороты.Документ,
	|	РасчетыПоРеализацииВУсловныхЕдиницахОрганизацииОбороты.ДатаОплаты,
	|	РасчетыПоРеализацииВУсловныхЕдиницахОрганизацииОбороты.СуммаРегРасход,
	|	РасчетыПоРеализацииВУсловныхЕдиницахОрганизацииОбороты.СуммаРегПриход,
	|	ВЫБОР
	|		КОГДА РасчетыПоРеализацииВУсловныхЕдиницахОрганизацииОбороты.Регистратор В (&СписокСчетовФактур)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ЭтоДокументРеализации
	|ПОМЕСТИТЬ РасчетыПоРеализации
	|ИЗ
	|	РегистрНакопления.РасчетыПоРеализацииВУсловныхЕдиницахОрганизации.Обороты(, &ДатаГраница, Регистратор, Организация = &Организация) КАК РасчетыПоРеализацииВУсловныхЕдиницахОрганизацииОбороты
	|ГДЕ
	|	(РасчетыПоРеализацииВУсловныхЕдиницахОрганизацииОбороты.Регистратор В (&СписокСчетовФактур)
	|			ИЛИ РасчетыПоРеализацииВУсловныхЕдиницахОрганизацииОбороты.Документ В (&СписокСчетовФактур))
	|	И (НЕ РасчетыПоРеализацииВУсловныхЕдиницахОрганизацииОбороты.Регистратор = РасчетыПоРеализацииВУсловныхЕдиницахОрганизацииОбороты.Документ)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РасчетыПоРеализации.Организация,
	|	ВЫБОР
	|		КОГДА РасчетыПоРеализации.ЭтоДокументРеализации
	|			ТОГДА РасчетыПоРеализации.Регистратор
	|		ИНАЧЕ РасчетыПоРеализации.Документ
	|	КОНЕЦ КАК СчетФактура,
	|	ВЫБОР
	|		КОГДА РасчетыПоРеализации.ЭтоДокументРеализации
	|			ТОГДА РасчетыПоРеализации.Документ
	|		ИНАЧЕ РасчетыПоРеализации.Регистратор
	|	КОНЕЦ КАК ДокументОплаты,
	|	ВЫБОР
	|		КОГДА РасчетыПоРеализации.ЭтоДокументРеализации
	|			ТОГДА РасчетыПоРеализации.ДатаОплаты
	|		ИНАЧЕ РасчетыПоРеализации.Период
	|	КОНЕЦ КАК ДатаОплаты,
	|	ВЫБОР
	|		КОГДА РасчетыПоРеализации.ЭтоДокументРеализации
	|			ТОГДА РасчетыПоРеализации.СуммаРегРасход
	|		ИНАЧЕ РасчетыПоРеализации.СуммаРегПриход
	|	КОНЕЦ КАК РаспределеннаяОплата,
	|	ЛОЖЬ КАК РасчетыСБюджетом
	|ИЗ
	|	РасчетыПоРеализации КАК РасчетыПоРеализации
	|
	|УПОРЯДОЧИТЬ ПО
	|	ДатаОплаты";
                                                              
	Запрос.УстановитьПараметр("ДатаГраница", Новый Граница(КонецДня(Дата), ВидГраницы.Включая));
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("СписокСчетовФактур", СписокСчетовФактур);
	
	РаспределенныеОплаты = Запрос.Выполнить().Выгрузить(ОбходРезультатаЗапроса.Прямой);
	РаспределенныеОплаты.Колонки.Добавить("QueryId", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(6)));

	Если РаспределенныеОплаты.Количество()>0 Тогда
		QueryId = Новый Массив(РаспределенныеОплаты.Количество());
		Для Счетчик=0 По РаспределенныеОплаты.Количество()-1 Цикл
			QueryId[Счетчик] = Счетчик;
		КонецЦикла; 
		РаспределенныеОплаты.ЗагрузитьКолонку(QueryId,"QueryId");
	КонецЕсли;
	
	Возврат РаспределенныеОплаты;	

КонецФункции // ПолучитьДанныеОРаспределенныхОплатахПоСпискуСФ()
 
// Процедура вызывается из тела процедуры "ЗаполнитьРазделРеализация".
// В процессе работы процедуры общая сумма НДС, которая может быть признана
// в качестве вычета распределяется по конкретным событиям оплаты, для того
// чтобы сопоставить факт признания вычета по НДС и факт оплаты поставщику, связанный
// с этим признанием.
Процедура РаспределитьОплатыПоДеревуСФ(Дерево_НДСНачисленный, ТаблицаРезультатов, СписокСчетовФактур, РаспределенныеОплаты, ОтражатьВРеестре = Истина, ОтражатьВидНачисления = Ложь )
	
	НДСНалоговыйПериод = Неопределено;
	
	Построитель_РаспределенныеОплаты = Новый построительЗапроса();
	Построитель_РаспределенныеОплаты.ИсточникДанных = Новый ОписаниеИсточникаДанных(РаспределенныеОплаты);
	
	// Подготовка структуры отбора
	Отбор = Построитель_РаспределенныеОплаты.Отбор;
	Отбор.Добавить("СчетФактура");
	Отбор.СчетФактура.Использование = Истина;
	Отбор.Добавить("РаспределеннаяОплата");
	Отбор.РаспределеннаяОплата.ВидСравнения = ВидСравнения.Больше;
	Отбор.РаспределеннаяОплата.Значение = 0;
	Отбор.РаспределеннаяОплата.Использование = Истина;
	
	Построитель_РаспределенныеОплаты.Порядок.Добавить("ДатаОплаты");
	
	ТаблицаОплат = Новый ТаблицаЗначений();
	ТаблицаОплат.Колонки.Добавить("ДокументОплаты");
	ТаблицаОплат.Колонки.Добавить("ДатаОплаты",Новый ОписаниеТипов("Дата", , , Новый КвалификаторыДаты(ЧастиДаты.ДатаВремя)));
	ТаблицаОплат.Колонки.Добавить("СуммаОплаты",Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15,2)));
	
	ТаблицаИсточникаПостроителя = Построитель_РаспределенныеОплаты.ИсточникДанных.ИсточникДанных;
	МоментОпределения_ПоОтгрузке = Перечисления.МоментыОпределенияНалоговойБазыНДС.ПоОтгрузке;
	
	Для каждого СтрокаСФ Из Дерево_НДСНачисленный.Строки Цикл
		
		ТаблицаОплат.Очистить();
		
		Если УчетНДС.ДляСчетаФактурыНеТребуетсяОплата(СтрокаСФ.СчетФактура) Тогда
			НаличиеОплатыНеТребуется = Истина;
		Иначе
			НаличиеОплатыНеТребуется = (СтрокаСФ.Строки[0].МоментОпределенияНалоговойБазыНДС = МоментОпределения_ПоОтгрузке) Или Дата >= '20080101';
			Отбор = Построитель_РаспределенныеОплаты.Отбор;
			Отбор.СчетФактура.Значение = СтрокаСФ.СчетФактура;
			Отбор.РаспределеннаяОплата.ВидСравнения = ?(СтрокаСФ.СуммаСНДС>0,ВидСравнения.Больше,ВидСравнения.Меньше);
			
			Построитель_РаспределенныеОплаты.Выполнить();
			Если Построитель_РаспределенныеОплаты.Результат.Пустой() и не НаличиеОплатыНеТребуется и СтрокаСФ.СуммаСНДС	>= 0 Тогда
				// Оплата не обнаружена
				Продолжить;
			КонецЕсли; 
			
			ВыборкаОплат = Построитель_РаспределенныеОплаты.Результат.Выгрузить(ОбходРезультатаЗапроса.Прямой);
				
			СуммаКПогашению = СтрокаСФ.СуммаСНДС;

			Для каждого СтрокаРаспределеннойОплаты Из ВыборкаОплат Цикл
				СтрокаОплаты = ТаблицаОплат.Добавить();
				СтрокаОплаты.ДокументОплаты = СтрокаРаспределеннойОплаты.ДокументОплаты;
				СтрокаОплаты.ДатаОплаты = СтрокаРаспределеннойОплаты.ДатаОплаты;
				Если СтрокаСФ.СуммаСНДС>0 Тогда
					СтрокаОплаты.СуммаОплаты = Мин(СуммаКПогашению,СтрокаРаспределеннойОплаты.РаспределеннаяОплата);
				Иначе
					СтрокаОплаты.СуммаОплаты = Макс(СуммаКПогашению,СтрокаРаспределеннойОплаты.РаспределеннаяОплата);
				КонецЕсли; 
				
				СуммаКПогашению = СуммаКПогашению - СтрокаОплаты.СуммаОплаты;
				ТаблицаИсточникаПостроителя[СтрокаРаспределеннойОплаты.QueryId].РаспределеннаяОплата = ТаблицаИсточникаПостроителя[СтрокаРаспределеннойОплаты.QueryId].РаспределеннаяОплата - СтрокаОплаты.СуммаОплаты;
				
				Если СуммаКПогашению = 0 Тогда
					Прервать;
				КонецЕсли;
			КонецЦикла; 
		КонецЕсли;
		Если ТаблицаОплат.Количество() = 0  Тогда
			Если НаличиеОплатыНеТребуется или СтрокаСФ.СуммаСНДС < 0 Тогда
				СтрокаОплаты = ТаблицаОплат.Добавить();
				СтрокаОплаты.СуммаОплаты = СтрокаСФ.СуммаСНДС;
			Иначе
				Продолжить;	
			КонецЕсли;
		ИначеЕсли ТаблицаОплат.Итог("СуммаОплаты")<> СтрокаСФ.СуммаСНДС и НаличиеОплатыНеТребуется тогда
			СтрокаОплаты = ТаблицаОплат.Добавить();
			СтрокаОплаты.СуммаОплаты = СтрокаСФ.СуммаСНДС - ТаблицаОплат.Итог("СуммаОплаты");
		ИначеЕсли СтрокаСФ.СуммаСНДС < 0 тогда
			СтрокаОплаты = ТаблицаОплат.Добавить();
			СтрокаОплаты.СуммаОплаты = СтрокаСФ.СуммаСНДС - ТаблицаОплат.Итог("СуммаОплаты");
		КонецЕсли; 
			
		Если не ОтражатьВРеестре тогда
			// Оплаты заблокированы, дальнейшая обработка не требуется
			Продолжить;
		КонецЕсли;
			
		Для каждого СтрокаОплаты Из ТаблицаОплат Цикл
			
			Если СтрокаСФ.Строки.Итог("СуммаСНДС") = 0 Тогда
				Прервать;
			КонецЕсли;
			
			МассивБазиса =  СтрокаСФ.Строки.ВыгрузитьКолонку("СуммаСНДС");
			Массив_БезНДС = СтрокаСФ.Строки.ВыгрузитьКолонку("СуммаБезНДС");
			
			МассивРезультата = ОбщегоНазначения.РаспределитьПропорционально(СтрокаОплаты.СуммаОплаты,МассивБазиса);
			КРаспределениюБезНДС = Окр(СтрокаОплаты.СуммаОплаты * СтрокаСФ.Строки.Итог("СуммаБезНДС")/СтрокаСФ.Строки.Итог("СуммаСНДС"),2);
			
			Если КРаспределениюБезНДС<>0 Тогда
				МассивРезультата_БезНДС = ОбщегоНазначения.РаспределитьПропорционально(КРаспределениюБезНДС,Массив_БезНДС);
			КонецЕсли; 
			
			НомерСтроки = 0;
			Для каждого СтрокаЗадолжености Из СтрокаСФ.Строки Цикл
				Если МассивРезультата[НомерСтроки] = 0 и ?(КРаспределениюБезНДС=0,Истина, МассивРезультата_БезНДС[НомерСтроки] =0) Тогда
					НомерСтроки = НомерСтроки + 1;
					Продолжить;
				КонецЕсли; 
				СтрокаРезультата = ТаблицаРезультатов.Добавить();
				ЗаполнитьЗначенияСвойств(СтрокаРезультата, СтрокаЗадолжености);
				СтрокаРезультата.СчетФактура	= СтрокаЗадолжености.СчетФактура;
				СтрокаРезультата.Покупатель		= СтрокаЗадолжености.Покупатель;
				СтрокаРезультата.ВидЦенности	= СтрокаЗадолжености.ВидЦенности;
				СтрокаРезультата.СтавкаНДС		= СтрокаЗадолжености.СтавкаНДС;
				
				СтрокаРезультата.ДокументОплаты	= СтрокаОплаты.ДокументОплаты;
				Если Реализация0 Тогда
					СтрокаРезультата.Состояние	= СтрокаЗадолжености.Состояние;
				КонецЕсли; 
				
				СтрокаРезультата.ДатаОплаты		= СтрокаОплаты.ДатаОплаты;
				
				СтрокаРезультата.СуммаБезНДС	= ?(КРаспределениюБезНДС=0,0, МассивРезультата_БезНДС[НомерСтроки]);
				СтрокаРезультата.НДС			= МассивРезультата[НомерСтроки]- СтрокаРезультата.СуммаБезНДС; //?(СтрокаСФ.ПорядокОплаты = 1,0,);
				
				СтрокаЗадолжености.СуммаБезНДС = СтрокаЗадолжености.СуммаБезНДС - СтрокаРезультата.СуммаБезНДС;
				СтрокаЗадолжености.СуммаНДС = СтрокаЗадолжености.СуммаНДС - СтрокаРезультата.НДС;
				СтрокаЗадолжености.СуммаСНДС = СтрокаЗадолжености.СуммаСНДС - (СтрокаРезультата.СуммаБезНДС + СтрокаРезультата.НДС);
				Если Реализация0 Тогда
					Если СтрокаЗадолжености.Состояние = Перечисления.НДССостоянияРеализация0.ПодтвержденаРеализация0 Тогда
						СтрокаРезультата.Событие = Перечисления.СобытияПоНДСПродажи.ПодтвержденаСтавка0;
					ИначеЕсли СтрокаЗадолжености.Состояние = Перечисления.НДССостоянияРеализация0.НеПодтвержденаРеализация0 Тогда
						СтрокаРезультата.Событие = Перечисления.СобытияПоНДСПродажи.НеПодтвержденаСтавка0;
					КонецЕсли; 
				Иначе
					Если НаличиеОплатыНеТребуется Тогда
						СтрокаРезультата.Событие = Перечисления.СобытияПоНДСПродажи.Реализация;
					Иначе
						СтрокаРезультата.Событие = Перечисления.СобытияПоНДСПродажи.Оплата;
					КонецЕсли;
				КонецЕсли;
				
				Если (Реализация0 И СтрокаРезультата.Состояние = Перечисления.НДССостоянияРеализация0.НеПодтвержденаРеализация0) Тогда
					Если Дата >= '20060530' Тогда
						Если НДСНалоговыйПериод = Неопределено Тогда
							НДСНалоговыйПериод = УчетНДС.ПолучитьУПНДСНалоговыйПериод(Организация, Дата);
						КонецЕсли;
						СтрокаРезультата.ЗаписьДополнительногоЛиста = Истина;
						СтрокаРезультата.КорректируемыйПериод = СтрокаЗадолжености.СчетФактураДата;
						Если ?(НДСНалоговыйПериод = Перечисления.Периодичность.Месяц, 
								КонецМесяца(СтрокаРезультата.КорректируемыйПериод) = КонецМесяца(Дата),
								КонецКвартала(СтрокаРезультата.КорректируемыйПериод) = КонецКвартала(Дата)) Тогда
							СтрокаРезультата.ЗаписьДополнительногоЛиста = Ложь;
							СтрокаРезультата.КорректируемыйПериод = '00010101';
						КонецЕсли;
					КонецЕсли;
				КонецЕсли;
				
				Если ОтражатьВидНачисления Тогда
					СтрокаРезультата.ВидНачисления	= СтрокаЗадолжености.ВидНачисления;
				КонецЕсли; 
				НомерСтроки = НомерСтроки+1;
			КонецЦикла; 
			
		КонецЦикла; 
			
    КонецЦикла; 
	
	РаспределенныеОплаты = ТаблицаИсточникаПостроителя.Скопировать();
	
КонецПроцедуры // РаспределитьОплатыПоДеревуСФ()
 
////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ДЛЯ ОБЕСПЕЧЕНИЯ ПРОВЕДЕНИЯ ДОКУМЕНТА

// Проверяет правильность заполнения шапки документа.
// Если какой-то из реквизитов шапки, влияющий на проведение, не заполнен или
// заполнен не корректно, то выставляется флаг отказа в проведении.
// Проверяется также правильность заполнения реквизитов ссылочных полей документа.
// Проверка выполняется по объекту и по выборке из результата запроса по шапке.
//
// Параметры: 
//  СтруктураШапкиДокумента - структура, содержащая рексвизиты шапки документа и результаты запроса по шапке,
//  Отказ                   - флаг отказа в проведении,
//  Заголовок               - строка, заголовок сообщения об ошибке проведения.
//
Процедура ПроверитьЗаполнениеШапки(Отказ, Заголовок)

	// Укажем, что надо проверить:
	СтруктураОбязательныхПолей = Новый Структура("Организация");
	
	// Теперь вызовем общую процедуру проверки.
	ЗаполнениеДокументов.ПроверитьЗаполнениеШапкиДокумента(ЭтотОбъект, СтруктураОбязательныхПолей, Отказ, Заголовок);
	
КонецПроцедуры // ПроверитьЗаполнениеШапки()

// Выгружает результат запроса в табличную часть, добавляет ей необходимые колонки для проведения.
//
//
Функция ПодготовитьТаблицуЗаписей(СтруктураШапкиДокумента)

	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("Ссылка", СтруктураШапкиДокумента.Ссылка);
	Запрос.УстановитьПараметр("ПустаяДата", '00010101');
	Запрос.УстановитьПараметр("Начало2008", '20080101');
	Запрос.УстановитьПараметр("ВидНачисления_Реализация0", Перечисления.НДСВидНачисления.Реализация0);
	
	// Виды ценностей расчеты по которым не отражаются в расчетах с покупателями
	ВидаЦенностиБезОплаты = Новый Массив();
	ВидаЦенностиБезОплаты.Добавить(Перечисления.ВидыЦенностей.НалоговыйАгентАренда);
	ВидаЦенностиБезОплаты.Добавить(Перечисления.ВидыЦенностей.НалоговыйАгентРеализацияИмущества);
	ВидаЦенностиБезОплаты.Добавить(Перечисления.ВидыЦенностей.НалоговыйАгентИностранцы);
	ВидаЦенностиБезОплаты.Добавить(Перечисления.ВидыЦенностей.ВнутреннееПотребление);
	ВидаЦенностиБезОплаты.Добавить(Перечисления.ВидыЦенностей.СМРСобственнымиСилами);
	ВидаЦенностиБезОплаты.Добавить(Перечисления.ВидыЦенностей.АвансыПолученные);
	ВидаЦенностиБезОплаты.Добавить(Перечисления.ВидыЦенностей.АвансыПолученные0);
	
	Запрос.УстановитьПараметр("ВидаЦенностиБезОплаты", ВидаЦенностиБезОплаты);
	
	Если СтруктураШапкиДокумента.Реализация0 Тогда
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ТаблицаЗаписей.Ссылка.Организация,
		|	ТаблицаЗаписей.ВидЦенности,
		|	ТаблицаЗаписей.Покупатель,
		|	ТаблицаЗаписей.СчетФактура,
		|	ЕСТЬNULL(ТаблицаЗаписей.СчетФактура.Дата, &ПустаяДата) КАК СчетФактураДата,
		|	ТаблицаЗаписей.СтавкаНДС,
		|	ТаблицаЗаписей.СуммаБезНДС,
		|	ТаблицаЗаписей.НДС,
		|	ТаблицаЗаписей.ДокументОплаты,
		|	ТаблицаЗаписей.ЗаписьДополнительногоЛиста,
		|	ВЫБОР
		|		КОГДА ТаблицаЗаписей.ЗаписьДополнительногоЛиста = ИСТИНА
		|			ТОГДА ТаблицаЗаписей.КорректируемыйПериод
		|		ИНАЧЕ &ПустаяДата
		|	КОНЕЦ КАК КорректируемыйПериод,
		|	ВЫБОР
		|		КОГДА ТаблицаЗаписей.ЗаписьДополнительногоЛиста = ИСТИНА
		|				И ТаблицаЗаписей.Ссылка.ФормироватьСторнирующиеЗаписиДопЛистовВручную = ИСТИНА
		|			ТОГДА ТаблицаЗаписей.СторнирующаяЗаписьДопЛиста
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК СторнирующаяЗаписьДопЛиста,
		|	ВЫБОР
		|		КОГДА ТаблицаЗаписей.ДатаОплаты = &ПустаяДата
		|			ТОГДА ТаблицаЗаписей.ДокументОплаты.Дата
		|		ИНАЧЕ ТаблицаЗаписей.ДатаОплаты
		|	КОНЕЦ КАК ДатаОплаты,
		|	ТаблицаЗаписей.Событие,
		|	&ВидНачисления_Реализация0 КАК ВидНачисления,
		|	ИСТИНА КАК ОтражатьВКниге,
		|	ТаблицаЗаписей.Состояние,
		|	УчетнаяПолитикаНалоговыйУчет.МоментОпределенияНалоговойБазыНДС,
		|	""По реализации"" КАК НазваниеТаблицы,
		|	ТаблицаЗаписей.НомерСтроки,
		|	ВЫБОР
		|		КОГДА ТаблицаЗаписей.ВидЦенности В (&ВидаЦенностиБезОплаты)
		|			ТОГДА ЛОЖЬ
		|		ИНАЧЕ ИСТИНА
		|	КОНЕЦ КАК ВлияетНаРасчеты,
		|	ВЫБОР
		|		КОГДА ТаблицаЗаписей.КорректируемыйПериод < &Начало2008
		|			ТОГДА УчетнаяПолитикаНалоговыйУчетКорректируемогоПериода.НДСНалоговыйПериод
		|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.Периодичность.Квартал)
		|	КОНЕЦ КАК НДСНалоговыйПериодДополнительногоЛиста,
		|	ВЫБОР
		|		КОГДА ТаблицаЗаписей.ВидЦенности = ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.НалоговыйАгентКомитент)
		|			ТОГДА ТаблицаЗаписей.ДоговорКонтрагента
		|		ИНАЧЕ НЕОПРЕДЕЛЕНО
		|	КОНЕЦ КАК ДоговорКонтрагента
		|ИЗ
		|	Документ.ФормированиеЗаписейКнигиПродаж.Реализация КАК ТаблицаЗаписей
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.УчетнаяПолитикаОрганизаций КАК УчетнаяПолитикаНалоговыйУчет
		|		ПО ТаблицаЗаписей.Ссылка.Организация = УчетнаяПолитикаНалоговыйУчет.Организация
		|			И (УчетнаяПолитикаНалоговыйУчет.Период В
		|				(ВЫБРАТЬ
		|					МАКСИМУМ(УчетнаяПолитикаНалоговыйУчет.Период)
		|				ИЗ
		|					РегистрСведений.УчетнаяПолитикаОрганизаций КАК УчетнаяПолитикаНалоговыйУчет
		|				ГДЕ
		|					ТаблицаЗаписей.СчетФактура.Дата >= УчетнаяПолитикаНалоговыйУчет.Период
		|					И ТаблицаЗаписей.Ссылка.Организация = УчетнаяПолитикаНалоговыйУчет.Организация))
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.УчетнаяПолитикаОрганизаций КАК УчетнаяПолитикаНалоговыйУчетКорректируемогоПериода
		|		ПО (ТаблицаЗаписей.ЗаписьДополнительногоЛиста = ИСТИНА)
		|			И ТаблицаЗаписей.Ссылка.Организация = УчетнаяПолитикаНалоговыйУчетКорректируемогоПериода.Организация
		|			И (УчетнаяПолитикаНалоговыйУчетКорректируемогоПериода.Период В
		|				(ВЫБРАТЬ
		|					МАКСИМУМ(УчетнаяПолитикаНалоговыйУчет.Период)
		|				ИЗ
		|					РегистрСведений.УчетнаяПолитикаОрганизаций КАК УчетнаяПолитикаНалоговыйУчет
		|				ГДЕ
		|					ТаблицаЗаписей.КорректируемыйПериод >= УчетнаяПолитикаНалоговыйУчет.Период
		|					И ТаблицаЗаписей.Ссылка.Организация = УчетнаяПолитикаНалоговыйУчет.Организация))
		|ГДЕ
		|	ТаблицаЗаписей.Ссылка = &Ссылка";
		
	иначе
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ТаблицаЗаписей.Ссылка.Организация,
		|	ТаблицаЗаписей.ВидЦенности,
		|	ТаблицаЗаписей.Покупатель,
		|	ТаблицаЗаписей.СчетФактура,
		|	ЕСТЬNULL(ТаблицаЗаписей.СчетФактура.Дата, &ПустаяДата) КАК СчетФактураДата,
		|	ТаблицаЗаписей.СтавкаНДС,
		|	ТаблицаЗаписей.СуммаБезНДС,
		|	ТаблицаЗаписей.НДС,
		|	ТаблицаЗаписей.ДокументОплаты,
		|	ТаблицаЗаписей.ЗаписьДополнительногоЛиста,
		|	ВЫБОР
		|		КОГДА ТаблицаЗаписей.ЗаписьДополнительногоЛиста = ИСТИНА
		|			ТОГДА ТаблицаЗаписей.КорректируемыйПериод
		|		ИНАЧЕ &ПустаяДата
		|	КОНЕЦ КАК КорректируемыйПериод,
		|	ВЫБОР
		|		КОГДА ТаблицаЗаписей.ЗаписьДополнительногоЛиста = ИСТИНА
		|				И ТаблицаЗаписей.Ссылка.ФормироватьСторнирующиеЗаписиДопЛистовВручную = ИСТИНА
		|			ТОГДА ТаблицаЗаписей.СторнирующаяЗаписьДопЛиста
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК СторнирующаяЗаписьДопЛиста,
		|	ВЫБОР
		|		КОГДА ТаблицаЗаписей.ДатаОплаты = &ПустаяДата
		|			ТОГДА ТаблицаЗаписей.ДокументОплаты.Дата
		|		ИНАЧЕ ТаблицаЗаписей.ДатаОплаты
		|	КОНЕЦ КАК ДатаОплаты,
		|	ТаблицаЗаписей.Событие,
		|	ВЫБОР
		|		КОГДА ТаблицаЗаписей.СтавкаНДС = &СтавкаНДС_БезНДС
		|			ТОГДА &ВидНачисления_РеализацияБезНДС
		|		КОГДА ТаблицаЗаписей.СтавкаНДС = &СтавкаНДС_0
		|			ТОГДА &ВидНачисления_Реализация0
		|		ИНАЧЕ &ВидНачисления_РеализацияСНДС
		|	КОНЕЦ КАК ВидНачисления,
		|	ИСТИНА КАК ОтражатьВКниге,
		|	УчетнаяПолитикаНалоговыйУчет.МоментОпределенияНалоговойБазыНДС,
		|	""По реализации"" КАК НазваниеТаблицы,
		|	ТаблицаЗаписей.НомерСтроки,
		|	ВЫБОР
		|		КОГДА ТаблицаЗаписей.ВидЦенности В (&ВидаЦенностиБезОплаты)
		|			ТОГДА ЛОЖЬ
		|		ИНАЧЕ ИСТИНА
		|	КОНЕЦ КАК ВлияетНаРасчеты,
		|	ВЫБОР
		|		КОГДА ТаблицаЗаписей.ВидЦенности = ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.НалоговыйАгентКомитент)
		|			ТОГДА ТаблицаЗаписей.ДоговорКонтрагента
		|		ИНАЧЕ НЕОПРЕДЕЛЕНО
		|	КОНЕЦ КАК ДоговорКонтрагента,
		|	NULL КАК СчетУчетаНДС,
		|	ВЫБОР
		|		КОГДА ТаблицаЗаписей.КорректируемыйПериод < &Начало2008
		|			ТОГДА УчетнаяПолитикаНалоговыйУчетКорректируемогоПериода.НДСНалоговыйПериод
		|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.Периодичность.Квартал)
		|	КОНЕЦ КАК НДСНалоговыйПериодДополнительногоЛиста,
		|	NULL КАК ИсправленныйСчетФактура
		|ИЗ
		|	Документ.ФормированиеЗаписейКнигиПродаж.Реализация КАК ТаблицаЗаписей
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.УчетнаяПолитикаОрганизаций КАК УчетнаяПолитикаНалоговыйУчет
		|		ПО ТаблицаЗаписей.Ссылка.Организация = УчетнаяПолитикаНалоговыйУчет.Организация
		|			И (УчетнаяПолитикаНалоговыйУчет.Период В
		|				(ВЫБРАТЬ
		|					МАКСИМУМ(УчетнаяПолитикаНалоговыйУчет.Период)
		|				ИЗ
		|					РегистрСведений.УчетнаяПолитикаОрганизаций КАК УчетнаяПолитикаНалоговыйУчет
		|				ГДЕ
		|					ТаблицаЗаписей.СчетФактура.Дата >= УчетнаяПолитикаНалоговыйУчет.Период
		|					И ТаблицаЗаписей.Ссылка.Организация = УчетнаяПолитикаНалоговыйУчет.Организация))
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.УчетнаяПолитикаОрганизаций КАК УчетнаяПолитикаНалоговыйУчетКорректируемогоПериода
		|		ПО (ТаблицаЗаписей.ЗаписьДополнительногоЛиста = ИСТИНА)
		|			И ТаблицаЗаписей.Ссылка.Организация = УчетнаяПолитикаНалоговыйУчетКорректируемогоПериода.Организация
		|			И (УчетнаяПолитикаНалоговыйУчетКорректируемогоПериода.Период В
		|				(ВЫБРАТЬ
		|					МАКСИМУМ(УчетнаяПолитикаНалоговыйУчет.Период)
		|				ИЗ
		|					РегистрСведений.УчетнаяПолитикаОрганизаций КАК УчетнаяПолитикаНалоговыйУчет
		|				ГДЕ
		|					ТаблицаЗаписей.КорректируемыйПериод >= УчетнаяПолитикаНалоговыйУчет.Период
		|					И ТаблицаЗаписей.Ссылка.Организация = УчетнаяПолитикаНалоговыйУчет.Организация))
		|ГДЕ
		|	ТаблицаЗаписей.Ссылка = &Ссылка
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ТаблицаЗаписей.Ссылка.Организация,
		|	ТаблицаЗаписей.ВидЦенности,
		|	ТаблицаЗаписей.Покупатель,
		|	ТаблицаЗаписей.СчетФактура,
		|	ЕСТЬNULL(ТаблицаЗаписей.СчетФактура.Дата, &ПустаяДата),
		|	ТаблицаЗаписей.СтавкаНДС,
		|	ТаблицаЗаписей.СуммаБезНДС,
		|	ТаблицаЗаписей.НДС,
		|	ВЫБОР
		|		КОГДА ТаблицаЗаписей.СчетФактура ССЫЛКА Документ.СчетФактураВыданный
		|			ТОГДА ТаблицаЗаписей.СчетФактура.ДокументОснование
		|		ИНАЧЕ ТаблицаЗаписей.СчетФактура
		|	КОНЕЦ,
		|	ТаблицаЗаписей.ЗаписьДополнительногоЛиста,
		|	ВЫБОР
		|		КОГДА ТаблицаЗаписей.ЗаписьДополнительногоЛиста = ИСТИНА
		|			ТОГДА ТаблицаЗаписей.КорректируемыйПериод
		|		ИНАЧЕ &ПустаяДата
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА ТаблицаЗаписей.ЗаписьДополнительногоЛиста = ИСТИНА
		|				И ТаблицаЗаписей.Ссылка.ФормироватьСторнирующиеЗаписиДопЛистовВручную = ИСТИНА
		|			ТОГДА ТаблицаЗаписей.СторнирующаяЗаписьДопЛиста
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ,
		|	ТаблицаЗаписей.СчетФактура.Дата,
		|	ТаблицаЗаписей.Событие,
		|	&ВидНачисления_Авансы,
		|	ИСТИНА,
		|	НЕОПРЕДЕЛЕНО,
		|	""С авансов"",
		|	ТаблицаЗаписей.НомерСтроки,
		|	ЛОЖЬ,
		|	ТаблицаЗаписей.ДоговорКонтрагента,
		|	NULL,
		|	NULL,
		|	NULL
		|ИЗ
		|	Документ.ФормированиеЗаписейКнигиПродаж.Авансы КАК ТаблицаЗаписей
		|ГДЕ
		|	ТаблицаЗаписей.Ссылка = &Ссылка
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ТаблицаЗаписей.Ссылка.Организация,
		|	ТаблицаЗаписей.ВидЦенности,
		|	ТаблицаЗаписей.Покупатель,
		|	ТаблицаЗаписей.СчетФактура,
		|	ЕСТЬNULL(ТаблицаЗаписей.СчетФактура.Дата, &ПустаяДата),
		|	ТаблицаЗаписей.СтавкаНДС,
		|	ТаблицаЗаписей.СуммаБезНДС,
		|	ТаблицаЗаписей.НДС,
		|	ТаблицаЗаписей.ДокументОплаты,
		|	ЛОЖЬ,
		|	НЕОПРЕДЕЛЕНО,
		|	ЛОЖЬ,
		|	ВЫБОР
		|		КОГДА ТаблицаЗаписей.ДатаОплаты = &ПустаяДата
		|			ТОГДА ТаблицаЗаписей.ДокументОплаты.Дата
		|		ИНАЧЕ ТаблицаЗаписей.ДатаОплаты
		|	КОНЕЦ,
		|	ТаблицаЗаписей.Событие,
		|	&ВидНачисления_НачисленКУплате,
		|	ИСТИНА,
		|	НЕОПРЕДЕЛЕНО,
		|	""Начисленный к уплате"",
		|	ТаблицаЗаписей.НомерСтроки,
		|	ЛОЖЬ,
		|	ТаблицаЗаписей.ДоговорКонтрагента,
		|	NULL,
		|	NULL,
		|	NULL
		|ИЗ
		|	Документ.ФормированиеЗаписейКнигиПродаж.НачисленКУплате КАК ТаблицаЗаписей
		|ГДЕ
		|	ТаблицаЗаписей.Ссылка = &Ссылка
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ТаблицаЗаписей.Ссылка.Организация,
		|	ТаблицаЗаписей.ВидЦенности,
		|	ТаблицаЗаписей.Покупатель,
		|	ТаблицаЗаписей.СчетФактура,
		|	ЕСТЬNULL(ТаблицаЗаписей.СчетФактура.Дата, &ПустаяДата),
		|	ТаблицаЗаписей.СтавкаНДС,
		|	ТаблицаЗаписей.СуммаБезНДС,
		|	ТаблицаЗаписей.НДС,
		|	ТаблицаЗаписей.ДокументОплаты,
		|	ЛОЖЬ,
		|	НЕОПРЕДЕЛЕНО,
		|	ЛОЖЬ,
		|	ВЫБОР
		|		КОГДА ТаблицаЗаписей.ДатаОплаты = &ПустаяДата
		|			ТОГДА ТаблицаЗаписей.ДокументОплаты.Дата
		|		ИНАЧЕ ТаблицаЗаписей.ДатаОплаты
		|	КОНЕЦ,
		|	ТаблицаЗаписей.Событие,
		|	ТаблицаЗаписей.ВидНачисления,
		|	ЛОЖЬ,
		|	УчетнаяПолитикаНалоговыйУчет.МоментОпределенияНалоговойБазыНДС,
		|	""Не отражается в книге"",
		|	ТаблицаЗаписей.НомерСтроки,
		|	ВЫБОР
		|		КОГДА ТаблицаЗаписей.ВидЦенности В (&ВидаЦенностиБезОплаты)
		|			ТОГДА ЛОЖЬ
		|		ИНАЧЕ ИСТИНА
		|	КОНЕЦ,
		|	NULL,
		|	NULL,
		|	NULL,
		|	NULL
		|ИЗ
		|	Документ.ФормированиеЗаписейКнигиПродаж.НеОтражаетсяВКниге КАК ТаблицаЗаписей
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.УчетнаяПолитикаОрганизаций КАК УчетнаяПолитикаНалоговыйУчет
		|		ПО ТаблицаЗаписей.Ссылка.Организация = УчетнаяПолитикаНалоговыйУчет.Организация
		|			И (УчетнаяПолитикаНалоговыйУчет.Период В
		|				(ВЫБРАТЬ
		|					МАКСИМУМ(УчетнаяПолитикаНалоговыйУчет.Период)
		|				ИЗ
		|					РегистрСведений.УчетнаяПолитикаОрганизаций КАК УчетнаяПолитикаНалоговыйУчет
		|				ГДЕ
		|					ТаблицаЗаписей.СчетФактура.Дата >= УчетнаяПолитикаНалоговыйУчет.Период
		|					И ТаблицаЗаписей.Ссылка.Организация = УчетнаяПолитикаНалоговыйУчет.Организация))
		|ГДЕ
		|	ТаблицаЗаписей.Ссылка = &Ссылка
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ТаблицаЗаписей.Ссылка.Организация,
		|	ТаблицаЗаписей.ВидЦенности,
		|	ТаблицаЗаписей.Покупатель,
		|	ТаблицаЗаписей.СчетФактура,
		|	&ПустаяДата,
		|	ТаблицаЗаписей.СтавкаНДС,
		|	ТаблицаЗаписей.СуммаБезНДС,
		|	ТаблицаЗаписей.НДС,
		|	НЕОПРЕДЕЛЕНО,
		|	ЛОЖЬ,
		|	НЕОПРЕДЕЛЕНО,
		|	ЛОЖЬ,
		|	&ПустаяДата,
		|	&Событие_НДСВосстановлен,
		|	&ВидНачисления_Восстановлен,
		|	ИСТИНА,
		|	НЕОПРЕДЕЛЕНО,
		|	""Восстановление по другим операциям"",
		|	ТаблицаЗаписей.НомерСтроки,
		|	ЛОЖЬ,
		|	NULL,
		|	ТаблицаЗаписей.СчетУчетаНДС,
		|	NULL,
		|	ТаблицаЗаписей.ИсправленныйСчетФактура
		|ИЗ
		|	Документ.ФормированиеЗаписейКнигиПродаж.Восстановлен КАК ТаблицаЗаписей
		|ГДЕ
		|	ТаблицаЗаписей.Ссылка = &Ссылка";
		
		Запрос.УстановитьПараметр("СтавкаНДС_БезНДС", Перечисления.СтавкиНДС.БезНДС);
		Запрос.УстановитьПараметр("СтавкаНДС_0", Перечисления.СтавкиНДС.НДС0);		
		Запрос.УстановитьПараметр("ВидНачисления_РеализацияБезНДС", Перечисления.НДСВидНачисления.РеализацияБезНДС);
		Запрос.УстановитьПараметр("ВидНачисления_РеализацияСНДС",	Перечисления.НДСВидНачисления.РеализацияСНДС);
		Запрос.УстановитьПараметр("ВидНачисления_Авансы", 			Перечисления.НДСВидНачисления.НДСсАвансов);
		Запрос.УстановитьПараметр("ВидНачисления_НачисленКУплате",	Перечисления.НДСВидНачисления.НДСНачисленКУплате);
		Запрос.УстановитьПараметр("ВидНачисления_Восстановлен",		Перечисления.НДСВидНачисления.НДСВосстановлен);
		Запрос.УстановитьПараметр("Событие_НДСВосстановлен",		Перечисления.СобытияПоНДСПродажи.ВосстановлениеНДС);
		Запрос.УстановитьПараметр("ПустаяДата", '00010101000000');
		
	КонецЕсли; 
	
	Результат = Запрос.Выполнить();
	
	ТаблицаЗаписей = Результат.Выгрузить();
	ТаблицаЗаписей.Колонки.Добавить("ДатаСобытия", Новый ОписаниеТипов("Дата", , , Новый КвалификаторыДаты(ЧастиДаты.ДатаВремя)));
	
	Для каждого СтрокаТаблицы из ТаблицаЗаписей Цикл
		Если НЕ ЗначениеЗаполнено(СтрокаТаблицы.ДатаОплаты) Тогда
			СтрокаТаблицы.ДатаОплаты = '00010101';
		КонецЕсли; 
		
		Если СтрокаТаблицы.ЗаписьДополнительногоЛиста Тогда
			СтрокаТаблицы.ДатаСобытия = СтруктураШапкиДокумента.Дата;
		Иначе
			СтрокаТаблицы.ДатаСобытия = Макс(СтрокаТаблицы.ДатаОплаты,СтрокаТаблицы.СчетФактураДата);
		КонецЕсли;
	КонецЦикла; 
	
	Возврат ТаблицаЗаписей;

КонецФункции // ПодготовитьТаблицуЗаписей()

Функция ПодготовитьТаблицуПоВосстановлениюПоАвансам(РезультатЗапросаПоВосстановлениюПоАвансам, СтруктураШапкиДокумента)
	
	ТаблицаПоВосстановлениюПоАвансам = РезультатЗапросаПоВосстановлениюПоАвансам.Выгрузить();
	
	ТаблицаПоВосстановлениюПоАвансам.Колонки.Добавить("ВидЦенности", Новый ОписаниеТипов("ПеречислениеСсылка.ВидыЦенностей"));
	ТаблицаПоВосстановлениюПоАвансам.ЗаполнитьЗначения(Перечисления.ВидыЦенностей.АвансыВыданные, "ВидЦенности");
	
	ТаблицаПоВосстановлениюПоАвансам.Колонки.Добавить("ВидНачисления", Новый ОписаниеТипов("ПеречислениеСсылка.НДСВидНачисления"));
	ТаблицаПоВосстановлениюПоАвансам.ЗаполнитьЗначения(Перечисления.НДСВидНачисления.НДСВосстановлен, "ВидНачисления");
	
	ТаблицаПоВосстановлениюПоАвансам.Колонки.Добавить("Событие", Новый ОписаниеТипов("ПеречислениеСсылка.СобытияПоНДСПродажи"));
	ТаблицаПоВосстановлениюПоАвансам.ЗаполнитьЗначения(Перечисления.СобытияПоНДСПродажи.ВосстановлениеНДС, "Событие");
	
	ТаблицаПоВосстановлениюПоАвансам.Колонки.Добавить("ДатаСобытия", Новый ОписаниеТипов("Дата", , , Новый КвалификаторыДаты(ЧастиДаты.Дата)));
	ТаблицаПоВосстановлениюПоАвансам.ЗаполнитьЗначения(СтруктураШапкиДокумента.Дата, "ДатаСобытия");
	
	ТаблицаПоВосстановлениюПоАвансам.Колонки.Добавить("НДСНалоговыйПериодДополнительногоЛиста", Новый ОписаниеТипов("ПеречислениеСсылка.Периодичность"));
	ТаблицаПоВосстановлениюПоАвансам.ЗаполнитьЗначения(Перечисления.Периодичность.Квартал, "НДСНалоговыйПериодДополнительногоЛиста");
	
	ТаблицаПоВосстановлениюПоАвансам.Колонки.Добавить("ДатаОплаты", Новый ОписаниеТипов("Дата", , , Новый КвалификаторыДаты(ЧастиДаты.Дата)));
	ТаблицаПоВосстановлениюПоАвансам.ЗаполнитьЗначения(СтруктураШапкиДокумента.Дата, "ДатаОплаты");
	
	ТаблицаПоВосстановлениюПоАвансам.Колонки.Добавить("ДокументОплаты", Новый ОписаниеТипов(ТаблицаПоВосстановлениюПоАвансам.Колонки.СчетФактура.ТипЗначения));
	
	Для Каждого СтрокаТаблицы Из ТаблицаПоВосстановлениюПоАвансам Цикл
		
		Если Не СтрокаТаблицы.ЗаписьДополнительногоЛиста Тогда
			СтрокаТаблицы.КорректируемыйПериод = '00010101000000';
		КонецЕсли;
		
		СтрокаТаблицы.ДатаОплаты = СтрокаТаблицы.ДатаСчетаФактуры;
		СтрокаТаблицы.ДокументОплаты = СтрокаТаблицы.СчетФактура;
		
	КонецЦикла; 
	
	Возврат ТаблицаПоВосстановлениюПоАвансам;
	
КонецФункции // ПодготовитьТаблицуПоОплатам()

// Проверяет правильность заполнения строк табличной части.
//
//
Процедура ПроверитьЗаполнениеТабличныхЧастей(СтруктураШапкиДокумента, ТаблицаПоНачислению, ТаблицаПоВосстановлениюПоАвансам, Отказ, Заголовок)

	// Укажем, что надо проверить:
	СтруктураОбязательныхПолей = Новый Структура("ВидЦенности, СчетФактура, СтавкаНДС"); 
	
	Если СтруктураШапкиДокумента.Реализация0 Тогда
		СтруктураОбязательныхПолей.Вставить("Состояние"); 
	КонецЕсли; 
	// Теперь вызовем общую процедуру проверки.
	ЗаполнениеДокументов.ПроверитьЗаполнениеТабличнойЧасти(ЭтотОбъект, "Реализация", СтруктураОбязательныхПолей, Отказ, Заголовок);
	

	Если НЕ СтруктураШапкиДокумента.Реализация0 Тогда
		
		// Для НДС начисленного к уплате может быть не щаполнен покупатель по СМР пдрядным и внутреннему потреблению
		
		// Теперь вызовем общую процедуру проверки.
		ЗаполнениеДокументов.ПроверитьЗаполнениеТабличнойЧасти(ЭтотОбъект, "НачисленКУплате", СтруктураОбязательныхПолей, Отказ, Заголовок);
		
		//СтруктураОбязательныхПолей.Вставить("ДокументОплаты");
		// Теперь вызовем общую процедуру проверки.
		// Для НДС начисленного к уплате может быть не щаполнен покупатель по СМР пдрядным и внутреннему потреблению
		ЗаполнениеДокументов.ПроверитьЗаполнениеТабличнойЧасти(ЭтотОбъект, "НеОтражаетсяВКниге", СтруктураОбязательныхПолей, Отказ, Заголовок);
		
		СтруктураОбязательныхПолей.Вставить("СчетУчетаНДС");
		
		// Для НДС восстановленного должен быть заполнен счет учета НДС для формирования проводки
		ЗаполнениеДокументов.ПроверитьЗаполнениеТабличнойЧасти(ЭтотОбъект, "Восстановлен", СтруктураОбязательныхПолей, Отказ, Заголовок);
		
		СтруктураОбязательныхПолей.Удалить("СчетУчетаНДС");
		СтруктураОбязательныхПолей.Удалить("ВидЦенности");
		СтруктураОбязательныхПолей.Вставить("Поставщик");
		СтруктураОбязательныхПолей.Вставить("ДоговорКонтрагента");
		// Для НДС восстановленного должен быть заполнен счет учета НДС для формирования проводки
		ЗаполнениеДокументов.ПроверитьЗаполнениеТабличнойЧасти(ЭтотОбъект, "ВосстановленПоАвансам", СтруктураОбязательныхПолей, Отказ, Заголовок);
		
	КонецЕсли;                                          
	
	// Проверка полей на логическое соответствие
	//СтрокаСообщенияДатаОплаты = "Не заполнены документ оплаты и дата оплаты. В книге покупок по данной записи в качестве даты оплаты будет установлена дата текущего документа!";
	СтрокаСообщенияПокупатель = "Не заполнено значение реквизита ""Покупатель""!";
	СтрокаСообщенияПоставщик = "Не заполнено значение реквизита ""Поставщик""!";
	СтрокаСообщенияКорПериод = "Не заполнено значение реквизита ""Корректируемый период""!";
	СтрокаСообщенияДоговор = "Не заполнено значение реквизита ""Договор контрагента""!";
	Для каждого СтрокаТаблицы из ТаблицаПоНачислению Цикл
		Если  СтрокаТаблицы.ВидЦенности = Перечисления.ВидыЦенностей.ВнутреннееПотребление
			или СтрокаТаблицы.ВидЦенности = Перечисления.ВидыЦенностей.СМРСобственнымиСилами Тогда
			// Покупателем является сама оргнизация
			СтрокаТаблицы.Покупатель = Неопределено;
		ИначеЕсли ТипЗнч(СтрокаТаблицы.СчетФактура) = Тип("ДокументСсылка.ОтчетОРозничныхПродажах") 
			Или (ТипЗнч(СтрокаТаблицы.СчетФактура) = Тип("ДокументСсылка.ПриходныйКассовыйОрдер") 
			И НЕ (
				СтрокаТаблицы.ВидЦенности = Перечисления.ВидыЦенностей.АвансыПолученные 
				или СтрокаТаблицы.ВидЦенности = Перечисления.ВидыЦенностей.АвансыПолученные0 
				или СтрокаТаблицы.ВидЦенности = Перечисления.ВидыЦенностей.СуммыСвязанныеСРасчетамиПоОплате
				))
			тогда
			// По рознице покупатель не указывается
			СтрокаТаблицы.Покупатель = Неопределено;
		ИначеЕсли НЕ ЗначениеЗаполнено(СтрокаТаблицы.Покупатель) тогда
			СтрокаНачалаСообщенияОбОшибке = "В строке номер """ + СокрЛП(СтрокаТаблицы.НомерСтроки) + """ табличной части """ + СтрокаТаблицы.НазваниеТаблицы + """: ";
			Если СтрокаТаблицы.НазваниеТаблицы = "Восстановление по другим операциям" Тогда
				ОбщегоНазначения.СообщитьОбОшибке(СтрокаНачалаСообщенияОбОшибке + СтрокаСообщенияПоставщик,Отказ,Заголовок);
			Иначе
				ОбщегоНазначения.СообщитьОбОшибке(СтрокаНачалаСообщенияОбОшибке + СтрокаСообщенияПокупатель,Отказ,Заголовок);
			КонецЕсли;
		КонецЕсли; 
		
		Если (СтрокаТаблицы.ВидЦенности = Перечисления.ВидыЦенностей.АвансыПолученные
			И ТипЗнч(СтрокаТаблицы.СчетФактура) <> Тип("ДокументСсылка.СчетФактураВыданный")) 
			И НЕ ЗначениеЗаполнено(СтрокаТаблицы.ДоговорКонтрагента) Тогда
			СтрокаНачалаСообщенияОбОшибке = "В строке номер """ + СокрЛП(СтрокаТаблицы.НомерСтроки) + """ табличной части """ + СтрокаТаблицы.НазваниеТаблицы + """: ";
			ОбщегоНазначения.СообщитьОбОшибке(СтрокаНачалаСообщенияОбОшибке + СтрокаСообщенияДоговор, Отказ, Заголовок);
		КонецЕсли;
		
		Если СтрокаТаблицы.ЗаписьДополнительногоЛиста и НЕ ЗначениеЗаполнено(СтрокаТаблицы.КорректируемыйПериод) тогда
			СтрокаНачалаСообщенияОбОшибке = "В строке номер """+ СокрЛП(СтрокаТаблицы.НомерСтроки) + """ табличной части """ + СтрокаТаблицы.НазваниеТаблицы + """: ";
			ОбщегоНазначения.СообщитьОбОшибке(СтрокаНачалаСообщенияОбОшибке + СтрокаСообщенияКорПериод,Отказ,Заголовок);		
		КонецЕсли;
		
	КонецЦикла; 

	Для Каждого СтрокаТаблицы Из ТаблицаПоВосстановлениюПоАвансам Цикл
		Если СтрокаТаблицы.ЗаписьДополнительногоЛиста 
			И Не ЗначениеЗаполнено(СтрокаТаблицы.КорректируемыйПериод) тогда
			СтрокаНачалаСообщенияОбОшибке = "В строке номер """ + СокрЛП(СтрокаТаблицы.НомерСтроки) + """ табличной части """ + ВосстановленПоАвансам + """: ";
			ОбщегоНазначения.СообщитьОбОшибке(СтрокаНачалаСообщенияОбОшибке + СтрокаСообщенияКорПериод, Отказ ,Заголовок);		
		КонецЕсли;
	КонецЦикла; 
		
КонецПроцедуры // ПроверитьЗаполнениеТабличнойЧастиТовары()

// По результату запроса по шапке документа формируем движения по регистрам.
//
//
Процедура ДвиженияПоРегистрам(СтруктураШапкиДокумента, ТаблицаПоНачислению, ТаблицаПоВосстановлениюПоАвансам, Отказ, Заголовок)
	
	ДвиженияПоРегистрамНачисления(СтруктураШапкиДокумента, ТаблицаПоНачислению, Отказ, Заголовок);
	ДвиженияПоРегистрамВосстановлениеПоАвансам(СтруктураШапкиДокумента, ТаблицаПоВосстановлениюПоАвансам, Отказ, Заголовок);
	
КонецПроцедуры // ДвиженияПоРегистрам()

Процедура ДвиженияПоРегистрамНачисления(СтруктураШапкиДокумента, ТаблицаПоНачислению, Отказ, Заголовок)
	
	Если ТаблицаПоНачислению.Количество()=0 Тогда
		Возврат;
	КонецЕсли; 
	
	Если мВестиУчетНДС Тогда
		// Отражение по регистру "НДС начисленный"
		ТаблицаДвижений_НДСНачисленный = Движения.НДСНачисленный.ВыгрузитьКолонки();

		ОбщегоНазначения.ЗагрузитьВТаблицуЗначений(ТаблицаПоНачислению,ТаблицаДвижений_НДСНачисленный);
		ТаблицаДвижений_НДСНачисленный.Свернуть(
			"Период,Активность,Организация,СчетФактура,ВидЦенности,СтавкаНДС,Покупатель,ДатаСобытия,Событие,ВидНачисления,ДоговорКонтрагента,СчетУчетаНДС,ИсправленныйСчетФактура",
			"СуммаБезНДС,НДС");
		
		Движения.НДСНачисленный.мПериод = СтруктураШапкиДокумента.Дата;
		Движения.НДСНачисленный.мТаблицаДвижений = ТаблицаДвижений_НДСНачисленный;
		Движения.НДСНачисленный.ВыполнитьРасход();
		
	КонецЕсли;
	
	// Отражение по регистру НДСЗаписиКнигиПродаж
	ТаблицаДвижений_НДСЗаписиКнигиПродаж = Движения.НДСЗаписиКнигиПродаж.ВыгрузитьКолонки();

	ТаблицаДвижений_НДСЗаписиКнигиПродаж.Колонки.Добавить("ОтражатьВКниге",Новый ОписаниеТипов("Булево"));
	ТаблицаДвижений_НДСЗаписиКнигиПродаж.Колонки.Добавить("НДСНалоговыйПериодДополнительногоЛиста");
	Если СтруктураШапкиДокумента.Реализация0 Тогда
		ТаблицаДвижений_НДСЗаписиКнигиПродаж.Колонки.Добавить("Состояние",Новый ОписаниеТипов("ПеречислениеСсылка.НДССостоянияРеализация0"));
		ТаблицаДвижений_НДСЗаписиКнигиПродаж.Колонки.Добавить("СчетФактураДата",Новый ОписаниеТипов("Дата", , , Новый КвалификаторыДаты(ЧастиДаты.ДатаВремя)));
	КонецЕсли;
	
	ОбщегоНазначения.ЗагрузитьВТаблицуЗначений(ТаблицаПоНачислению,ТаблицаДвижений_НДСЗаписиКнигиПродаж);
	
	СтрокиКУдалению = Новый Массив();
	Для каждого СтрокаЗаписи Из ТаблицаДвижений_НДСЗаписиКнигиПродаж Цикл
		Если не СтрокаЗаписи.ОтражатьВКниге Тогда
			СтрокиКУдалению.Добавить(СтрокаЗаписи);
		КонецЕсли; 
		
		Если СтруктураШапкиДокумента.Реализация0 и СтрокаЗаписи.Состояние = Перечисления.НДССостоянияРеализация0.НеПодтвержденаРеализация0 тогда
			Если СтрокаЗаписи.ЗаписьДополнительногоЛиста = Ложь Тогда
				СтрокаЗаписи.Период = Макс(СтрокаЗаписи.ДатаОплаты, СтрокаЗаписи.СчетФактураДата);
			Иначе				
				СтрокаЗаписи.Период = СтруктураШапкиДокумента.Дата;
			КонецЕсли; 
			СтрокаЗаписи.Событие = Перечисления.СобытияПоНДСПродажи.НеПодтвержденаСтавка0;
		КонецЕсли;
	КонецЦикла; 
	Для каждого СтрокаКУдалению Из СтрокиКУдалению Цикл
	    ТаблицаДвижений_НДСЗаписиКнигиПродаж.Удалить(СтрокаКУдалению);
	КонецЦикла; 
	
	ТаблицаДвижений_НДСЗаписиКнигиПродаж.Колонки.Удалить(ТаблицаДвижений_НДСЗаписиКнигиПродаж.Колонки.ОтражатьВКниге);
	
	ДополнитьСторнирующимиЗаписямиДопЛистов(СтруктураШапкиДокумента, ТаблицаДвижений_НДСЗаписиКнигиПродаж);
	
	ТаблицаДвижений_НДСЗаписиКнигиПродаж.Колонки.Удалить(ТаблицаДвижений_НДСЗаписиКнигиПродаж.Колонки.НДСНалоговыйПериодДополнительногоЛиста);
	
	Если СтруктураШапкиДокумента.Реализация0 Тогда
		ТаблицаДвижений_НДСЗаписиКнигиПродаж.Колонки.Удалить(ТаблицаДвижений_НДСЗаписиКнигиПродаж.Колонки.Состояние);
		ТаблицаДвижений_НДСЗаписиКнигиПродаж.Колонки.Удалить(ТаблицаДвижений_НДСЗаписиКнигиПродаж.Колонки.СчетФактураДата);
	КонецЕсли;
	
	ТаблицаДвижений_НДСЗаписиКнигиПродаж.Свернуть(
		"Период,Активность,Организация,СчетФактура,ДокументОплаты,ДатаОплаты,СтавкаНДС,Покупатель,ДатаСобытия,Событие,ВидЦенности,ЗаписьДополнительногоЛиста,
		|КорректируемыйПериод,СторнирующаяЗаписьДопЛиста,ДоговорКонтрагента,ИсправленныйСчетФактура",
		"СуммаБезНДС,НДС");
	
	Движения.НДСЗаписиКнигиПродаж.мПериод 			= СтруктураШапкиДокумента.Дата;
	Движения.НДСЗаписиКнигиПродаж.мТаблицаДвижений = ТаблицаДвижений_НДСЗаписиКнигиПродаж;
	Движения.НДСЗаписиКнигиПродаж.ДобавитьДвижение(Ложь);
	
	Если мВестиУчетНДС Тогда
		Если СтруктураШапкиДокумента.Реализация0 Тогда
			// Отражение по регистру НДСРеализация0
			ТаблицаДвижений_НДСРеализация0 = Движения.НДСРеализация0.ВыгрузитьКолонки();

			ОбщегоНазначения.ЗагрузитьВТаблицуЗначений(ТаблицаПоНачислению,ТаблицаДвижений_НДСРеализация0);
			ТаблицаДвижений_НДСРеализация0.ЗаполнитьЗначения(ВидДвиженияНакопления.Расход, "ВидДвижения");
			
			Движения.НДСРеализация0.мПериод 		 = СтруктураШапкиДокумента.Дата;
			Движения.НДСРеализация0.мТаблицаДвижений = ТаблицаДвижений_НДСРеализация0;
			Движения.НДСРеализация0.ДобавитьДвижение(Ложь);

		КонецЕслИ;
	КонецЕсли;
	
	ВидыНачисления_Реализация = Новый СписокЗначений;
	ВидыНачисления_Реализация.Добавить(Перечисления.НДСВидНачисления.РеализацияСНДС);
	ВидыНачисления_Реализация.Добавить(Перечисления.НДСВидНачисления.Реализация0);
	ВидыНачисления_Реализация.Добавить(Перечисления.НДСВидНачисления.РеализацияБезНДС);
	ВидыНачисления_Реализация.Добавить(Перечисления.НДСВидНачисления.РеализацияЕНВД);
	
	// Сфоормировать проводи по записям книги продаж
	Для каждого ТекСтрокаВычета Из ТаблицаПоНачислению Цикл
			
		Если Не ТекСтрокаВычета.ОтражатьВКниге Тогда
			Продолжить;
		ИначеЕсли ТекСтрокаВычета.НДС = 0 Тогда
			Продолжить;
		КонецЕсли;
		
		Если не ВидыНачисления_Реализация.НайтиПоЗначению(ТекСтрокаВычета.ВидНачисления) = Неопределено Тогда
			Если ТекСтрокаВычета.СчетФактураДата >= '20060101' Тогда 
				// C 2006 года учетная политика всегда по отгрузке
				Продолжить;
			ИначеЕсли не ТекСтрокаВычета.МоментОпределенияНалоговойБазыНДС = Перечисления.МоментыОпределенияНалоговойБазыНДС.ПоОплате тогда
				Продолжить;
			ИначеЕсли ТипЗнч(ТекСтрокаВычета.СчетФактура) = Тип("ДокументСсылка.ОтчетОРозничныхпродажах")
				Или ТипЗнч(ТекСтрокаВычета.СчетФактура) = Тип("ДокументСсылка.ПриходныйКассовыйОрдер") тогда
				Продолжить;
			КонецЕсли; 
					
			Движение = Движения.Хозрасчетный.Добавить();
			Движение.Период = СтруктураШапкиДокумента.Дата;
			Движение.Организация =  СтруктураШапкиДокумента.Организация;
			Движение.Сумма = ТекСтрокаВычета.НДС;
			Движение.Содержание = "НДС ";
			Движение.НомерЖурнала = "НДС";
					
			Движение.СчетДт = ПланыСчетов.Хозрасчетный.РасчетыПоНДСотложенномуДляУплатыВБюджет; //76.Н
			БухгалтерскийУчет.УстановитьСубконто(Движение.СчетДт, Движение.СубконтоДт, "Контрагенты", ТекСтрокаВычета.Покупатель);
			БухгалтерскийУчет.УстановитьСубконто(Движение.СчетДт, Движение.СубконтоДт, "СФВыданные", ТекСтрокаВычета.СчетФактура);
					
			Движение.СчетКт = ПланыСчетов.Хозрасчетный.НДС; //68.02
			БухгалтерскийУчет.УстановитьСубконто(Движение.СчетКт, Движение.СубконтоКт, "ВидыПлатежейВГосБюджет", Перечисления.ВидыПлатежейВГосБюджет.Налог);
		ИначеЕсли ТекСтрокаВычета.ВидНачисления = Перечисления.НДСВидНачисления.НДСВосстановлен Тогда
			// Проводка по восстановлению НДС на 19 счет 
			Движение = Движения.Хозрасчетный.Добавить();
			Движение.Период = СтруктураШапкиДокумента.Дата;
			Движение.Организация =  СтруктураШапкиДокумента.Организация;
			Движение.Сумма = ТекСтрокаВычета.НДС;
			Движение.Содержание = "Восстановление НДС";
			Движение.НомерЖурнала = "НДС";
					
			Движение.СчетДт = ТекСтрокаВычета.СчетУчетаНДС; //19.ХХ
			БухгалтерскийУчет.УстановитьСубконто(Движение.СчетДт, Движение.СубконтоДт, "Контрагенты", ТекСтрокаВычета.Покупатель);
			БухгалтерскийУчет.УстановитьСубконто(Движение.СчетДт, Движение.СубконтоДт, "СФполученные", ТекСтрокаВычета.СчетФактура);
					
			Движение.СчетКт = ПланыСчетов.Хозрасчетный.НДС; //68.02
			БухгалтерскийУчет.УстановитьСубконто(Движение.СчетКт, Движение.СубконтоКт, "ВидыПлатежейВГосБюджет", Перечисления.ВидыПлатежейВГосБюджет.Налог);
				
		КонецЕсли; 			
       
	КонецЦикла;
	
КонецПроцедуры

Процедура ДвиженияПоРегистрамВосстановлениеПоАвансам(СтруктураШапкиДокумента, ТаблицаПоВосстановлениюПоАвансам, Отказ, Заголовок)
	
	Если ТаблицаПоВосстановлениюПоАвансам.Количество()=0 Тогда
		Возврат;
	КонецЕсли; 
	
	// Отражение по регистру НДСПредъявленный
	ТаблицаДвижений_НДСсАвансов = Движения.НДСсАвансов.ВыгрузитьКолонки();
	ОбщегоНазначения.ЗагрузитьВТаблицуЗначений(ТаблицаПоВосстановлениюПоАвансам, ТаблицаДвижений_НДСсАвансов);
	ТаблицаДвижений_НДСсАвансов.Свернуть(
		"Период, Активность, Организация, СчетФактура, ВидЦенности, СтавкаНДС, Покупатель, ДоговорКонтрагента, ДатаСобытия, ИсправленныйСчетФактура",
		"СуммаБезНДС, НДС");
		
	Движения.НДСсАвансов.мПериод = СтруктураШапкиДокумента.Дата;
	Движения.НДСсАвансов.мТаблицаДвижений = ТаблицаДвижений_НДСсАвансов;
	Движения.НДСсАвансов.ВыполнитьРасход();
		
	// Отражение по регистру НДСЗаписиКнигиПокупок
	ТаблицаДвижений_НДСЗаписиКнигиПродаж = Движения.НДСЗаписиКнигиПродаж.ВыгрузитьКолонки();
	
	ТаблицаДвижений_НДСЗаписиКнигиПродаж.Колонки.Добавить("НДСНалоговыйПериодДополнительногоЛиста");
	
	ОбщегоНазначения.ЗагрузитьВТаблицуЗначений(ТаблицаПоВосстановлениюПоАвансам, ТаблицаДвижений_НДСЗаписиКнигиПродаж);
	                                                                                                                                                                                                                                           
	ДополнитьСторнирующимиЗаписямиДопЛистов(СтруктураШапкиДокумента, ТаблицаДвижений_НДСЗаписиКнигиПродаж);
	ТаблицаДвижений_НДСЗаписиКнигиПродаж.Колонки.Удалить(ТаблицаДвижений_НДСЗаписиКнигиПродаж.Колонки.НДСНалоговыйПериодДополнительногоЛиста);
	
	ТаблицаДвижений_НДСЗаписиКнигиПродаж.Свернуть(
		"Период,Активность,Организация,СчетФактура,ДокументОплаты, ДатаОплаты,СтавкаНДС,Покупатель,ДатаСобытия,Событие,ВидЦенности,ЗаписьДополнительногоЛиста,
		|КорректируемыйПериод,СторнирующаяЗаписьДопЛиста, ДоговорКонтрагента, ИсправленныйСчетФактура",
		"СуммаБезНДС,НДС");
		
	Движения.НДСЗаписиКнигиПродаж.мПериод 			= СтруктураШапкиДокумента.Дата;
	Движения.НДСЗаписиКнигиПродаж.мТаблицаДвижений = ТаблицаДвижений_НДСЗаписиКнигиПродаж;
	Движения.НДСЗаписиКнигиПродаж.ДобавитьДвижение(Ложь);
	
	// Сформировать проводки по записям книги покупок
	Для каждого СтрокаТаблицы Из ТаблицаПоВосстановлениюПоАвансам Цикл
		
		Если СтрокаТаблицы.НДС = 0 Тогда
			Продолжить;
		КонецЕсли; 
		
		Движение 			  = Движения.Хозрасчетный.Добавить();
		Движение.Период       = СтруктураШапкиДокумента.Дата;
		Движение.Организация  = СтруктураШапкиДокумента.Организация;
		Движение.Сумма 		  = СтрокаТаблицы.НДС;
		Движение.Содержание   = "Восстановление НДС с выданного аванса";
		Движение.НомерЖурнала = "НДС";
		
		Движение.СчетДт = ПланыСчетов.Хозрасчетный.НДСпоАвансамИПредоплатамВыданным; // 76.ВА

		БухгалтерскийУчет.УстановитьСубконто(Движение.СчетДт, Движение.СубконтоДт, "Контрагенты", СтрокаТаблицы.Покупатель);
		БухгалтерскийУчет.УстановитьСубконто(Движение.СчетДт, Движение.СубконтоДт, "СФПолученные", СтрокаТаблицы.СчетФактура);
		
		Движение.СчетКт = ПланыСчетов.Хозрасчетный.НДС; // 68.02
		БухгалтерскийУчет.УстановитьСубконто(Движение.СчетКт, Движение.СубконтоКт, "ВидыПлатежейВГосБюджет", Перечисления.ВидыПлатежейВГосБюджет.Налог);
		
	КонецЦикла; 
	
	
КонецПроцедуры

Процедура ДополнитьСторнирующимиЗаписямиДопЛистов(СтруктураШапкиДокумента, ТаблицаДвижений_НДСЗаписиКнигиПродаж)
	
	Если СтруктураШапкиДокумента.ФормироватьСторнирующиеЗаписиДопЛистовВручную Тогда
		Возврат;
	КонецЕсли;
	
	ТаблицаДвижений_НДСЗаписиКнигиПродаж.Индексы.Добавить("ЗаписьДополнительногоЛиста");
	Если ТаблицаДвижений_НДСЗаписиКнигиПродаж.Найти(Истина, "ЗаписьДополнительногоЛиста") = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ТаблицаДвижений_НДСЗаписиКнигиПродаж.Колонки.Найти("СторнирующаяЗаписьДопЛиста") = Неопределено Тогда
		ТаблицаДвижений_НДСЗаписиКнигиПродаж.Колонки.Добавить("СторнирующаяЗаписьДопЛиста", Новый ОписаниеТипов("Булево"));
	КонецЕсли; 

	СвернутаяТаблицаЗаписейДопЛиста = ТаблицаДвижений_НДСЗаписиКнигиПродаж.Скопировать();
	Для каждого ТекущаяСтрока Из СвернутаяТаблицаЗаписейДопЛиста Цикл
		// Авансы отрабатываются с учетом ставки НДС (на каждую ставку отдельный счет-фактура)
		Если НЕ (ТекущаяСтрока.ВидЦенности = Перечисления.ВидыЦенностей.АвансыПолученные
			ИЛИ ТекущаяСтрока.ВидЦенности = Перечисления.ВидыЦенностей.АвансыПолученные0) Тогда
			ТекущаяСтрока.СтавкаНДС = Неопределено;
		КонецЕсли; 
		// Авансы и налоговый агент (комитент) отрабатываются с учетом договора (на каждуй
		// договор отдельный счет-фактура или отдельное лист декларации)
		Если НЕ (ТекущаяСтрока.ВидЦенности = Перечисления.ВидыЦенностей.АвансыПолученные
			ИЛИ ТекущаяСтрока.ВидЦенности = Перечисления.ВидыЦенностей.АвансыПолученные0
			ИЛИ ТекущаяСтрока.ВидЦенности = Перечисления.ВидыЦенностей.НалоговыйАгентКомитент) Тогда
			// Авансы отрабатываются с учетом ставки НДС (на каждую ставку отдельный счет-фактура)
			ТекущаяСтрока.ДоговорКонтрагента = Неопределено;
		КонецЕсли; 
	КонецЦикла; 
	
	СвернутаяТаблицаЗаписейДопЛиста.Свернуть("СчетФактура,СтавкаНДС,ДоговорКонтрагента,ЗаписьДополнительногоЛиста,КорректируемыйПериод","СуммаБезНДС,НДС");
	
	СвернутаяТаблицаЗаписейДопЛиста.Индексы.Добавить("ЗаписьДополнительногоЛиста");
	СтрокиДопЛистов = СвернутаяТаблицаЗаписейДопЛиста.НайтиСтроки(Новый Структура("ЗаписьДополнительногоЛиста", Истина));
	
	КэшПараметровУП = Новый Соответствие;
	ПараметрыУПКорректируемогоПериода = Неопределено;
	
	Запрос = Новый Запрос();
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("СтрокиДопЛистов", СвернутаяТаблицаЗаписейДопЛиста);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СтрокиДопЛистов.СчетФактура КАК СчетФактура,
	|	СтрокиДопЛистов.КорректируемыйПериод КАК КорректируемыйПериод
	|ПОМЕСТИТЬ ВТСтрокиДопЛистов
	|ИЗ
	|	&СтрокиДопЛистов КАК СтрокиДопЛистов
	|ГДЕ
	|	СтрокиДопЛистов.ЗаписьДополнительногоЛиста = ИСТИНА
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	СчетФактура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	ВТСтрокиДопЛистов.КорректируемыйПериод
	|ИЗ
	|	ВТСтрокиДопЛистов КАК ВТСтрокиДопЛистов
	|
	|УПОРЯДОЧИТЬ ПО
	|	ВТСтрокиДопЛистов.КорректируемыйПериод";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		НДСНалоговыйПериодДополнительногоЛиста = УчетНДС.ПолучитьУПНДСНалоговыйПериод(СтруктураШапкиДокумента.Организация, Выборка.КорректируемыйПериод, КэшПараметровУП[НачалоМесяца(Выборка.КорректируемыйПериод)]);	
		НачалоНалоговогоПериодаКорректировки = ?(НДСНалоговыйПериодДополнительногоЛиста = Перечисления.Периодичность.Квартал, НачалоКвартала(Выборка.КорректируемыйПериод), НачалоМесяца(Выборка.КорректируемыйПериод));
	Иначе
		Возврат;
	КонецЕсли;	
		
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	НДСЗаписиКнигиПродажОбороты.Организация,
	|	НДСЗаписиКнигиПродажОбороты.Покупатель,
	|	НДСЗаписиКнигиПродажОбороты.СчетФактура,
	|	НДСЗаписиКнигиПродажОбороты.ВидЦенности,
	|	НДСЗаписиКнигиПродажОбороты.СтавкаНДС,
	|	НДСЗаписиКнигиПродажОбороты.ДатаОплаты,
	|	НДСЗаписиКнигиПродажОбороты.ДокументОплаты,
	|	НДСЗаписиКнигиПродажОбороты.Событие,
	|	НДСЗаписиКнигиПродажОбороты.СуммаБезНДСОборот * -1 КАК СуммаБезНДССторно,
	|	НДСЗаписиКнигиПродажОбороты.НДСОборот * -1 КАК НДССторно,
	|	НДСЗаписиКнигиПродажОбороты.СуммаБезНДСОборот КАК СуммаБезНДС,
	|	НДСЗаписиКнигиПродажОбороты.НДСОборот КАК НДС,
	|	НДСЗаписиКнигиПродажОбороты.ДоговорКонтрагента,
	|	НДСЗаписиКнигиПродажОбороты.ЗаписьДополнительногоЛиста,
	|	НДСЗаписиКнигиПродажОбороты.КорректируемыйПериод,
	|	НДСЗаписиКнигиПродажОбороты.Период
	|ИЗ
	|	РегистрНакопления.НДСЗаписиКнигиПродаж.Обороты(
	|			&НачалоПериода,
	|			&КонецПериодаГраница,
	|			Квартал,
	|			СчетФактура В
	|				(ВЫБРАТЬ
	|					ВТСтрокиДопЛистов.СчетФактура
	|				ИЗ
	|					ВТСтрокиДопЛистов КАК ВТСтрокиДопЛистов)) КАК НДСЗаписиКнигиПродажОбороты";
	
	Запрос.УстановитьПараметр("НачалоПериода", НачалоНалоговогоПериодаКорректировки);
	Запрос.УстановитьПараметр("КонецПериодаГраница", Новый Граница(КонецДня(СтруктураШапкиДокумента.Дата), ВидГраницы.Включая));
		
	ВыборкаДляПоиска = Запрос.Выполнить().Выбрать();
	
	ОтработанныеСФпоПериодам = Новый ТаблицаЗначений();
	ОтработанныеСФпоПериодам.Колонки.Добавить("СчетФактура");
	ОтработанныеСФпоПериодам.Колонки.Добавить("НалоговыйПериод");
	ОтработанныеСФпоПериодам.Колонки.Добавить("СтавкаНДС");
	ОтработанныеСФпоПериодам.Колонки.Добавить("ДоговорКонтрагента");
	ОтработанныеСФпоПериодам.Индексы.Добавить("СчетФактура,НалоговыйПериод");
		
	Для каждого СтрокаДопЛиста Из СтрокиДопЛистов Цикл
		
		НДСНалоговыйПериодДополнительногоЛиста = УчетНДС.ПолучитьУПНДСНалоговыйПериод(СтруктураШапкиДокумента.Организация, СтрокаДопЛиста.КорректируемыйПериод, КэшПараметровУП[НачалоМесяца(СтрокаДопЛиста.КорректируемыйПериод)]);
		НачалоНалоговогоПериодаКорректировки = ?(НДСНалоговыйПериодДополнительногоЛиста = Перечисления.Периодичность.Квартал, НачалоКвартала(СтрокаДопЛиста.КорректируемыйПериод),НачалоМесяца(СтрокаДопЛиста.КорректируемыйПериод));
		КонецНалоговогоПериодаКорректировки = ?(НДСНалоговыйПериодДополнительногоЛиста = Перечисления.Периодичность.Квартал, КонецКвартала(СтрокаДопЛиста.КорректируемыйПериод), КонецМесяца(СтрокаДопЛиста.КорректируемыйПериод));
		
		ОтборОтработанныхЗаписей = Новый Структура("СчетФактура, НалоговыйПериод", СтрокаДопЛиста.СчетФактура, НачалоНалоговогоПериодаКорректировки);
		Если ЗначениеЗаполнено(СтрокаДопЛиста.СтавкаНДС) Тогда
			ОтборОтработанныхЗаписей.Вставить("СтавкаНДС", СтрокаДопЛиста.СтавкаНДС);
		КонецЕсли; 
		Если ЗначениеЗаполнено(СтрокаДопЛиста.ДоговорКонтрагента) Тогда
			ОтборОтработанныхЗаписей.Вставить("ДоговорКонтрагента", СтрокаДопЛиста.ДоговорКонтрагента);
		КонецЕсли; 
				
		Если НЕ (ОтработанныеСФпоПериодам.НайтиСтроки(ОтборОтработанныхЗаписей).Количество() = 0) Тогда
			// СФ по данному налоговому периоду уже отработан
			Продолжить;
		Иначе
			НоваяЗапись = ОтработанныеСФпоПериодам.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяЗапись, ОтборОтработанныхЗаписей);
		КонецЕсли; 
				
		
		// Структура поиска для отбора записей
		СтруктураПоиска = Новый Структура();
		СтруктураПоиска.Вставить("СчетФактура", СтрокаДопЛиста.СчетФактура);
		Если ЗначениеЗаполнено(СтрокаДопЛиста.СтавкаНДС) Тогда
			СтруктураПоиска.Вставить("СтавкаНДС", СтрокаДопЛиста.СтавкаНДС);
		КонецЕсли;
		Если ЗначениеЗаполнено(СтрокаДопЛиста.ДоговорКонтрагента) Тогда
			СтруктураПоиска.Вставить("ДоговорКонтрагента", СтрокаДопЛиста.ДоговорКонтрагента);
		КонецЕсли;
		
		Пока ВыборкаДляПоиска.НайтиСледующий(СтруктураПоиска) Цикл
			Если (ВыборкаДляПоиска.ЗаписьДополнительногоЛиста 
				И ВыборкаДляПоиска.КорректируемыйПериод >= НачалоНалоговогоПериодаКорректировки 
				И ВыборкаДляПоиска.КорректируемыйПериод <= КонецНалоговогоПериодаКорректировки)
				ИЛИ (НЕ ВыборкаДляПоиска.ЗаписьДополнительногоЛиста
			    И ВыборкаДляПоиска.Период = НачалоНалоговогоПериодаКорректировки) Тогда
				
				СтрокаТаблицыДвижений = ТаблицаДвижений_НДСЗаписиКнигиПродаж.Добавить();
				
				ЗаполнитьЗначенияСвойств(СтрокаТаблицыДвижений, ВыборкаДляПоиска);
				
				СтрокаТаблицыДвижений.ЗаписьДополнительногоЛиста = Истина;
				СтрокаТаблицыДвижений.СторнирующаяЗаписьДопЛиста = Истина;
								
				СтрокаТаблицыДвижений.СуммаБезНДС = ВыборкаДляПоиска.СуммаБезНДССторно; 
				СтрокаТаблицыДвижений.НДС         = ВыборкаДляПоиска.НДССторно;
				
				СтрокаТаблицыДвижений.КорректируемыйПериод = СтрокаДопЛиста.КорректируемыйПериод;
				СтрокаТаблицыДвижений.ДатаСобытия 		   = СтруктураШапкиДокумента.Дата;
																				
				СтрокаТаблицыДвижений = ТаблицаДвижений_НДСЗаписиКнигиПродаж.Добавить();
				
				ЗаполнитьЗначенияСвойств(СтрокаТаблицыДвижений, ВыборкаДляПоиска);
									
				СтрокаТаблицыДвижений.ЗаписьДополнительногоЛиста = Истина;
				СтрокаТаблицыДвижений.СторнирующаяЗаписьДопЛиста = Ложь;
				
				СтрокаТаблицыДвижений.СуммаБезНДС = ВыборкаДляПоиска.СуммаБезНДС; 
				СтрокаТаблицыДвижений.НДС         = ВыборкаДляПоиска.НДС;
				
				СтрокаТаблицыДвижений.КорректируемыйПериод = СтрокаДопЛиста.КорректируемыйПериод;
				СтрокаТаблицыДвижений.ДатаСобытия 		   = СтруктураШапкиДокумента.Дата;
											
			КонецЕсли;	 
		КонецЦикла;	
	КонецЦикла;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

Процедура ОбработкаПроведения(Отказ, Режим) Экспорт
	
	Если мУдалятьДвижения Тогда
		ОбщегоНазначения.УдалитьДвиженияРегистратора(ЭтотОбъект, Отказ);
	КонецЕсли;

	// Заголовок для сообщений об ошибках проведения.
	Заголовок = ОбщегоНазначения.ПредставлениеДокументаПриПроведении(Ссылка);

	// Сформируем запрос на дополнительные параметры, нужные при проведении, по данным шапки документа
	СтруктураШапкиДокумента = ОбщегоНазначения.СформироватьСтруктуруШапкиДокумента(ЭтотОбъект);
	
	// Проверим правильность заполнения шапки документа
	ПроверитьЗаполнениеШапки(Отказ, Заголовок);
	
	ТаблицаПоНачислению = ПодготовитьТаблицуЗаписей(СтруктураШапкиДокумента);
	
	// Подготовим данные необходимые для проведения и проверки заполнения табличной части по восстановлению по авансам.
	СтруктураПолей = Новый Структура;
	СтруктураПолей.Вставить("Организация",		 "Ссылка.Организация");
	СтруктураПолей.Вставить("Покупатель",		 "Поставщик");
	СтруктураПолей.Вставить("ДоговорКонтрагента","ДоговорКонтрагента");
	СтруктураПолей.Вставить("СчетФактура",	 	 "СчетФактура");
	СтруктураПолей.Вставить("ДатаСчетаФактуры",	 "СчетФактура.Дата");
	СтруктураПолей.Вставить("СтавкаНДС",		 "СтавкаНДС");
	СтруктураПолей.Вставить("СуммаБезНДС",		 "СуммаБезНДС");
	СтруктураПолей.Вставить("НДС",				 "НДС");
	СтруктураПолей.Вставить("ЗаписьДополнительногоЛиста",	"ЗаписьДополнительногоЛиста");
	СтруктураПолей.Вставить("КорректируемыйПериод",			"КорректируемыйПериод");
	СтруктураПолей.Вставить("ИсправленныйСчетФактура",		"ИсправленныйСчетФактура");
		
	РезультатЗапросаПоВосстановлениюПоАвансам = УправлениеЗапасами.СформироватьЗапросПоТабличнойЧасти(ЭтотОбъект, "ВосстановленПоАвансам", СтруктураПолей);
	ТаблицаПоВосстановлениюПоАвансам = ПодготовитьТаблицуПоВосстановлениюПоАвансам(РезультатЗапросаПоВосстановлениюПоАвансам, СтруктураШапкиДокумента);
	
	ПроверитьЗаполнениеТабличныхЧастей(СтруктураШапкиДокумента, ТаблицаПоНачислению, ТаблицаПоВосстановлениюПоАвансам, Отказ, Заголовок);
	
	мВестиУчетНДС = УчетНДС.ПроводитьПоРазделуУчетаНДС(Дата);
	
	Если Не Отказ Тогда
		ДвиженияПоРегистрам(СтруктураШапкиДокумента, ТаблицаПоНачислению, ТаблицаПоВосстановлениюПоАвансам, Отказ, Заголовок);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	Если ОбменДанными.Загрузка  Тогда
		Возврат;
	КонецЕсли;


	 
	мУдалятьДвижения = НЕ ЭтоНовый();
	
КонецПроцедуры // ПередЗаписью