Перем НП;
// Переменные настройки диалога
Перем ФлагВидимостиРазмещениеРезервов;
Перем ФлагВидимостиПросроченоДнейОплаты;
Перем ФлагВидимостиДатыОплаты;
Перем ФлагВидимостиПросроченоДнейОтгрузки;
Перем ФлагВидимостиДатыОтгрузки;
Перем ФлагДоступностиИзмененияНастроек;
Перем ФлагДоступностиИзмененияДляРезервов;

Перем СоответствиеНазначений;

Перем СтруктураДляОтбораПоКатегориям;

Перем СтруктураПредставлениеПолей;

Перем СтараяПричинаЗакрытия;

Перем ДатаНачала;
Перем ДатаОкончания;

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ
// 

// Устанавливает видимость полей ввода в зависимости от настроек в форме.
//
Процедура УстановитьВидимостьЭлементовНастройки()
	
	// Поле Размещение резервов видимо когда Наличие резервов установлено как С резервами
	ЭлементыФормы.ПолеНастройкиРазмещениеРезервов.Видимость = ФлагВидимостиРазмещениеРезервов;
	
	// Поле Дата отгрузки видимо когда Меньше/Равна/Больше
	ЭлементыФормы.ПолеДатаОтгрузки.Видимость = ФлагВидимостиДатыОтгрузки;
	
	// Поле ввода Просрочено дней видимо только когда Просрочен
	ЭлементыФормы.НадписьОтгрузкаДней.Видимость = ФлагВидимостиПросроченоДнейОтгрузки;
	ЭлементыФормы.ПолеПросроченоДнейОтгрузки.Видимость = ФлагВидимостиПросроченоДнейОтгрузки;
	ЭлементыФормы.НадписьОтгрузкаНа.Видимость = ФлагВидимостиПросроченоДнейОтгрузки;
	
	// Поле Дата оплаты видимо когда Меньше/Равна/Больше
	ЭлементыФормы.ПолеДатаОплаты.Видимость = ФлагВидимостиДатыОплаты;
	
	// Поле ввода Просрочено дней видимо только когда Просрочен
	ЭлементыФормы.НадписьОплатаДней.Видимость = ФлагВидимостиПросроченоДнейОплаты;
	ЭлементыФормы.ПолеПросроченоДнейОплаты.Видимость = ФлагВидимостиПросроченоДнейОплаты;
	ЭлементыФормы.НадписьОплатаНа.Видимость = ФлагВидимостиПросроченоДнейОплаты;
	
КонецПроцедуры // УстановитьВидимостьЭлементовНастройки()

// Устанавливает доступность полей ввода в зависимости от настроек в форме.
//
Процедура УстановитьДоступностьЭлементовНастройки()
	
	ЭлементыФормы.ПолеНастройкиНаличиеРезервов.Доступность = ФлагДоступностиИзмененияДляРезервов;
	ЭлементыФормы.ПолеНастройкиСостояниеОплаты.Доступность = ФлагДоступностиИзмененияНастроек;
	ЭлементыФормы.ПолеНастройкиСрокОплатыСравнение.Доступность = ФлагДоступностиИзмененияНастроек;
	ЭлементыФормы.ПолеНастройкиСостояниеОтгрузки.Доступность = ФлагДоступностиИзмененияНастроек;
	ЭлементыФормы.ПолеНастройкиСрокОтгрузкиСравнение.Доступность = ФлагДоступностиИзмененияНастроек;

КонецПроцедуры // УстановитьДоступностьЭлементовНастройки()

// Устанавливает доступность кнопок в зависимости от наличия документов в таблице.
//
Процедура УстановитьДоступностьКнопокПанелиЗаказов()
	
	// При пустом ТабличноеПолеЗаказы не должны быть доступны кнопки действий над заказами.
	Если ТабличноеПолеЗаказы.Количество() > 0 Тогда
		ЭлементыФормы.КоманднаяПанельЗаказы.Кнопки.ДействиеАнализ.Доступность       = Истина;
		ЭлементыФормы.КоманднаяПанельЗаказы.Кнопки.ДействиеВключитьВсе.Доступность  = Истина;
		ЭлементыФормы.КоманднаяПанельЗаказы.Кнопки.ДействиеВыключитьВсе.Доступность = Истина;
	Иначе
		ЭлементыФормы.КоманднаяПанельЗаказы.Кнопки.ДействиеАнализ.Доступность       = Ложь;
		ЭлементыФормы.КоманднаяПанельЗаказы.Кнопки.ДействиеВключитьВсе.Доступность  = Ложь;
		ЭлементыФормы.КоманднаяПанельЗаказы.Кнопки.ДействиеВыключитьВсе.Доступность = Ложь;
	КонецЕсли;
	
КонецПроцедуры // УстановитьДоступностьКнопокПанелиЗаказов()

// Устанавливает список доступных "Видов заполнения" и "Вид заполнения" по умолчанию по "Типу операции".
//
Процедура УстановитьСтруктуруПодменюВидЗаполнения()
	
	// Получим настройки из хранилища
	НастройкиЗаполнения = ДокументОбъект.СпособЗаполнения.Получить();
	
	// Проверим был ли заполнен документ
	Если НастройкиЗаполнения <> Неопределено Тогда
		
		// Было заполнение, попробуем восстановить значения
		ДатаНач = НастройкиЗаполнения.ДатаНач;
		ДатаКон = НастройкиЗаполнения.ДатаКон;
		НаличиеРезервов = НастройкиЗаполнения.НаличиеРезервов;
		РазмещениеРезервов = НастройкиЗаполнения.РазмещениеРезервов;
		
		СостояниеОтгрузки = НастройкиЗаполнения.СостояниеОтгрузки;
		СрокОтгрузкиСравнение = НастройкиЗаполнения.СрокОтгрузкиСравнение;
		ПросроченоДнейОтгрузки = НастройкиЗаполнения.ПросроченоДнейОтгрузки;
		ДатаОтгрузки = НастройкиЗаполнения.ДатаОтгрузки;
		
		СостояниеОплаты = НастройкиЗаполнения.СостояниеОплаты;
		СрокОплатыСравнение = НастройкиЗаполнения.СрокОплатыСравнение;
		ПросроченоДнейОплаты = НастройкиЗаполнения.ПросроченоДнейОплаты;
		ДатаОплаты = НастройкиЗаполнения.ДатаОплаты;
		
		ИспользоватьСвойстваИКатегории = НастройкиЗаполнения.ИспользоватьСвойстваИКатегории;
		НастройкиОтбора = НастройкиЗаполнения.НастройкиОтбора;
	
		ПричинаЗакрытия = НастройкиЗаполнения.ПричинаЗакрытия;
	
		ВидЗаполненияИзДокумента = НастройкиЗаполнения.ВидЗаполнения;
		
		НастройкиИзДокумента = Истина;
		
	КонецЕсли;
		
	// Установить Вид заполнения по Типу операции документа Закрытия
	Если ВариантОтбораПоВидуОперации = Перечисления.ВидыОперацийЗакрытиеЗаказовПокупателей.СнятиеРезервов Тогда
		ВидЗаполненияПоУмолчанию = ЗаполнитьСписокВидЗаполненияДляСнятияРезервов();
		
	ИначеЕсли ВариантОтбораПоВидуОперации = Перечисления.ВидыОперацийЗакрытиеЗаказовПокупателей.ЗакрытиеЗаказов Тогда
		ВидЗаполненияПоУмолчанию = ЗаполнитьСписокВидЗаполненияДляЗакрытияЗаказов();
		
	КонецЕсли;
	
	Если НастройкиИзДокумента = Истина Тогда
		// Вид заполнения по значению из документа
		Найден = Неопределено;
		Если НЕ(ВидЗаполненияИзДокумента = Неопределено) Тогда
			Найден = ЭлементыФормы.ПолеНастройкиВидЗаполнения.СписокВыбора.НайтиПоЗначению(ВидЗаполненияИзДокумента);
		КонецЕсли;
		
		Если НЕ(Найден = Неопределено) Тогда
			// Значение в документе корректно, берем его
			ВидЗаполнения = ВидЗаполненияИзДокумента;
		Иначе
			Сообщить("Некорректное значение способа заполнения в документе, установлено значение по умолчанию");
			ВидЗаполнения = ВидЗаполненияПоУмолчанию;
		КонецЕсли;
	Иначе	
		ВидЗаполнения = ВидЗаполненияПоУмолчанию;
	КонецЕсли;
	
	ПолеНастройкиВидЗаполненияПриИзменении(ЭлементыФормы.ПолеНастройкиВидЗаполнения);
	
КонецПроцедуры // УстановитьСтруктуруПодменюВидЗаполнения()

// Вызывается при изменении настроек отбора в форме.
//
Процедура ПриИзмененииНастроекПоУмолчанию()

	ПолеНастройкиНаличиеРезервовПриИзменении(ЭлементыФормы.ПолеНастройкиНаличиеРезервов);
	ПолеНастройкиСрокОплатыСравнениеПриИзменении(ЭлементыФормы.ПолеНастройкиСрокОплатыСравнение);
	ПолеНастройкиСрокОтгрузкиСравнениеПриИзменении(ЭлементыФормы.ПолеНастройкиСрокОтгрузкиСравнение);
	
	Если (ВидЗаполнения = 1) ИЛИ (ВидЗаполнения = 4) Тогда
		// Для данного вида заполнения доступны для изменения все настройки
		ФлагДоступностиИзмененияНастроек = Истина;
	Иначе
		// Для данного Вида заполнения основные настройки недоступны для изменения
		ФлагДоступностиИзмененияНастроек = Ложь;
	КонецЕсли;
	
	Если ВидЗаполнения = 1 Тогда
		// Для данного вида заполнения доступны для изменения настройки резервов
		ФлагДоступностиИзмененияДляРезервов = Истина;
	Иначе
		// Для данного вида заполнения недоступны для изменения настройки резервов
		ФлагДоступностиИзмененияДляРезервов = Ложь;
	КонецЕсли;
	
	УстановитьДоступностьЭлементовНастройки();

КонецПроцедуры // ПриИзмененииНастроекПоУмолчанию()

// Заполнить Доступные Виды заполнения для Типа операции Закрытие заказов
// и вернуть Вид заполнения по умолчанию для данного Вида операции.
//
// Возвращаемое значение:
//  Число - Вид заполнения по умолчанию для данного Вида операции
//
Функция ЗаполнитьСписокВидЗаполненияДляЗакрытияЗаказов()
	
	ЭлементыФормы.ПолеНастройкиВидЗаполнения.СписокВыбора.Очистить();
	
	// Доступные Виды заполнения для Типа операции Закрытие заказов 
	ЭлементыФормы.ПолеНастройкиВидЗаполнения.СписокВыбора.Добавить(1, "Произвольный отбор");
	ЭлементыФормы.ПолеНастройкиВидЗаполнения.СписокВыбора.Добавить(2, "Устаревшие заказы");
	ЭлементыФормы.ПолеНастройкиВидЗаполнения.СписокВыбора.Добавить(3, "Исполненные заказы");

	Возврат 3;

КонецФункции // ЗаполнитьСписокВидЗаполненияДляЗакрытияЗаказов()

// Заполнить Доступные Виды заполнения для Типа операции Снятие резервов
// и вернуть Вид заполнения по умолчанию для данного Вида операции.
//
// Возвращаемое значение:
//  Число - Вид заполнения по умолчанию для данного Вида операции
//
Функция ЗаполнитьСписокВидЗаполненияДляСнятияРезервов()
	
	ЭлементыФормы.ПолеНастройкиВидЗаполнения.СписокВыбора.Очистить();
	
	// Доступные Виды заполнения для Типа операции Снятие резервов 
	ЭлементыФормы.ПолеНастройкиВидЗаполнения.СписокВыбора.Добавить(4, "Все заказы с резервами");
	ЭлементыФормы.ПолеНастройкиВидЗаполнения.СписокВыбора.Добавить(5, "Неоплаченные в срок заказы");
	
	Возврат 5;

КонецФункции // ЗаполнитьСписокВидЗаполненияДляСнятияРезервов()

// Заполняет установки по умолчанию для отбора по всем возможным условиям.
//
Процедура УстановитьНастройкиПоУмолчаниюДляПроизвольногоОтбора()
	
	// Не важно
	НаличиеРезервов =1;
	
	// Не важно
	СостояниеОплаты = 1;
	
	// Не важно
	СостояниеОтгрузки = 1;
	
	// Не важно
	СрокОплатыСравнение = 1;
	// Не важно
	СрокОтгрузкиСравнение = 1;
	
	ЗначениеПользователя = ВосстановитьЗначение(ДокументОбъект.Метаданные().Имя + "_ПросроченоДнейОплаты_" + Строка(ВидЗаполнения));
	Если НЕ(ЗначениеПользователя = Неопределено) Тогда
		// Восстановим последнее установленное значение данным пользователем
		ПросроченоДнейОплаты = ЗначениеПользователя;
	Иначе
		// По умолчанию 1 день
		ПросроченоДнейОплаты = 1;
	КонецЕсли;
	
	ЗначениеПользователя = ВосстановитьЗначение(ДокументОбъект.Метаданные().Имя + "_ПросроченоДнейОтгрузки_" + Строка(ВидЗаполнения));
	Если НЕ(ЗначениеПользователя = Неопределено) Тогда
		// Восстановим последнее установленное значение данным пользователем
		ПросроченоДнейОтгрузки = ЗначениеПользователя;
	Иначе
		// По умолчанию 1 день
		ПросроченоДнейОтгрузки = 1;
	КонецЕсли;
	
	// По умолчанию Текущая дата
	ДатаОплаты = ТекущаяДата();
	ДатаОтгрузки = ТекущаяДата();
	
КонецПроцедуры // УстановитьНастройкиПоУмолчаниюДляПроизвольногоОтбора()

// Заполняет установки по умолчанию для отбора "Устаревших" заказов.
//
Процедура УстановитьНастройкиПоУмолчаниюДляУстаревшихЗаказов()

	// Без резервов
	НаличиеРезервов = 2;
	
	// Не оплачен полностью (неоплачен либо оплачен частично)
	СостояниеОплаты = 2;
	
	// Не отгружен полностью (неотгружен либо отгружен частично)
	СостояниеОтгрузки = 2;
	
	// Просрочен (более чем на)
	СрокОплатыСравнение = 2;
	// Просрочен (более чем на)
	СрокОтгрузкиСравнение = 2;
	
	ЗначениеПользователя = ВосстановитьЗначение(ДокументОбъект.Метаданные().Имя + "_ПросроченоДнейОплаты_" + Строка(ВидЗаполнения));
	Если НЕ(ЗначениеПользователя = Неопределено) Тогда
		// Восстановим последнее установленное значение данным пользователем
		ПросроченоДнейОплаты = ЗначениеПользователя;
	Иначе
		// Срок давности для устаревших заказов по умолчанию: 90 дней
		ПросроченоДнейОплаты = 90;
	КонецЕсли;
	
	ЗначениеПользователя = ВосстановитьЗначение(ДокументОбъект.Метаданные().Имя + "_ПросроченоДнейОтгрузки_" + Строка(ВидЗаполнения));
	Если НЕ(ЗначениеПользователя = Неопределено) Тогда
		// Восстановим последнее установленное значение данным пользователем
		ПросроченоДнейОтгрузки = ЗначениеПользователя;
	Иначе
		// Срок давности для устаревших заказов по умолчанию: 90 дней
		ПросроченоДнейОтгрузки = 90;
	КонецЕсли;
	
	// По умолчанию Текущая дата (в отборе не участвуют)
	ДатаОплаты = ТекущаяДата();
	ДатаОтгрузки = ТекущаяДата();
	
КонецПроцедуры // УстановитьНастройкиПоУмолчаниюДляУстаревшихЗаказов()

// Заполняет установки по умолчанию для отбора "Исполненных" заказов.
//
Процедура УстановитьНастройкиПоУмолчаниюДляИсполненныхЗаказов()

	// Без резервов
	НаличиеРезервов = 2;
	
	// Оплачен полностью
	СостояниеОплаты = 3;
	
	// Отгружен полностью
	СостояниеОтгрузки = 3;
	
	// Срок оплаты Не важно
	СрокОплатыСравнение = 1;
	// Срок отгрузки Не важно
	СрокОтгрузкиСравнение = 1;
	
	// По умолчанию 1 день (в отборе не участвуют)
	ПросроченоДнейОплаты = 1;
	ПросроченоДнейОтгрузки = 1;
	
	// По умолчанию Текущая дата (в отборе не участвуют)
	ДатаОплаты = ТекущаяДата();
	ДатаОтгрузки = ТекущаяДата();
	
КонецПроцедуры // УстановитьНастройкиПоУмолчаниюДляИсполненныхЗаказов()

// Заполняет установки по умолчанию для отбора всех заказов с резервами.
//
Процедура УстановитьНастройкиПоУмолчаниюДляВсехЗаказовСРезервами()
	
	// С резервами
	НаличиеРезервов = 3;
	
	// Не важно
	СостояниеОплаты = 1;
	
	// Не важно
	СостояниеОтгрузки = 1;
	
	// Просрочен (более чем на)
	СрокОплатыСравнение = 1;
	// Просрочен (более чем на)
	СрокОтгрузкиСравнение = 1;
	
	ЗначениеПользователя = ВосстановитьЗначение(ДокументОбъект.Метаданные().Имя + "_ПросроченоДнейОплаты_" + Строка(ВидЗаполнения));
	Если НЕ(ЗначениеПользователя = Неопределено) Тогда
		// Восстановим последнее установленное значение данным пользователем
		ПросроченоДнейОплаты = ЗначениеПользователя;
	Иначе
		// По умолчанию отбираем все просроченные хотя бы на 1 день
		ПросроченоДнейОплаты = 1;
	КонецЕсли;
	
	ЗначениеПользователя = ВосстановитьЗначение(ДокументОбъект.Метаданные().Имя + "_ПросроченоДнейОтгрузки_" + Строка(ВидЗаполнения));
	Если НЕ(ЗначениеПользователя = Неопределено) Тогда
		// Восстановим последнее установленное значение данным пользователем
		ПросроченоДнейОтгрузки = ЗначениеПользователя;
	Иначе
		// По умолчанию отбираем все просроченные хотя бы на 1 день
		ПросроченоДнейОтгрузки = 1;
	КонецЕсли;
	
	// По умолчанию Текущая дата (в отборе не участвуют)
	ДатаОплаты = ТекущаяДата();
	ДатаОтгрузки = ТекущаяДата();
	
КонецПроцедуры // УстановитьНастройкиПоУмолчаниюДляВсехЗаказовСРезервами()

// Заполняет установки по умолчанию для отбора "Неоплаченных" заказов.
//
Процедура УстановитьНастройкиПоУмолчаниюДляНеоплаченныхЗаказов()
	
	// С резервами
	НаличиеРезервов = 3;
	
	// Не оплачен полностью (неоплачен либо оплачен частично)
	СостояниеОплаты = 2;
	
	// Не важно
	СостояниеОтгрузки = 1;
	
	// Просрочен (более чем на)
	СрокОплатыСравнение = 2;
	// Не важно
	СрокОтгрузкиСравнение = 1;
	
	ЗначениеПользователя = ВосстановитьЗначение(ДокументОбъект.Метаданные().Имя + "_ПросроченоДнейОплаты_" + Строка(ВидЗаполнения));
	Если НЕ(ЗначениеПользователя = Неопределено) Тогда
		// Восстановим последнее установленное значение данным пользователем
		ПросроченоДнейОплаты = ЗначениеПользователя;
	Иначе
		// По умолчанию отбираем все просроченные хотя бы на 1 день
		ПросроченоДнейОплаты = 1;
	КонецЕсли;
	
	ЗначениеПользователя = ВосстановитьЗначение(ДокументОбъект.Метаданные().Имя + "_ПросроченоДнейОтгрузки_" + Строка(ВидЗаполнения));
	Если НЕ(ЗначениеПользователя = Неопределено) Тогда
		// Восстановим последнее установленное значение данным пользователем
		ПросроченоДнейОтгрузки = ЗначениеПользователя;
	Иначе
		// По умолчанию отбираем все просроченные хотя бы на 1 день
		ПросроченоДнейОтгрузки = 1; // В отборе не участвует
	КонецЕсли;
	
	// По умолчанию Текущая дата (в отборе не участвуют)
	ДатаОплаты = ТекущаяДата();
	ДатаОтгрузки = ТекущаяДата();
	
КонецПроцедуры // УстановитьНастройкиПоУмолчаниюДляНеоплаченныхЗаказов()

// Создает запрос и заполняет начальные установки построителя отбора для заказов.
//
// Параметры:
//  ПостроительОтчета - ссылка на объект ПостроительОтчета.
//
Процедура ИнициироватьПостроительДляОтбораЗаказов(ПостроительОтчета)

	// Запрос для отбора
	ТекстЗапроса = "
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ //РАЗЛИЧНЫЕ
	|	истина 																	КАК Переносить,
	|	&ПричинаЗакрытияЗаказа 													КАК ПричинаЗакрытияЗаказа,
	|	ВЫБОР КОГДА ЕстьNULL(ЗаказыПокупателейОстатки.КоличествоОстаток,0)>0 ТОГДА
	|		истина ИНАЧЕ ложь КОНЕЦ 											КАК ЗаказыКоличество,
	|	ВЫБОР КОГДА ЕстьNULL(ТоварыВРезервеНаСкладахОстатки.КоличествоОстаток,0)>0 ТОГДА
	|		истина ИНАЧЕ ложь КОНЕЦ 											КАК РезервыКоличество,
	|	ВЫБОР КОГДА ЕстьNULL(РазмещениеЗаказовПокупателейОстатки.КоличествоОстаток,0)>0 ТОГДА
	|		истина ИНАЧЕ ложь КОНЕЦ 											КАК РазмещенияКоличество,
	|	ЕстьNULL(РасчетыСКонтрагентамиОстатки.СуммаВзаиморасчетовОстаток,0)     КАК РасчетыОстаток,
	|	ЕстьNULL(ЗаказыПокупателейОстатки.СуммаВзаиморасчетовОстаток,0)         КАК ЗаказыОстаток,
	|	ДокументыЗаказПокупателя.Ссылка,
	|	ДокументыЗаказПокупателя.Дата,
	|	ДокументыЗаказПокупателя.Номер,
	|	ДокументыЗаказПокупателя.ВидОперации,
	|	ДокументыЗаказПокупателя.Контрагент,
	|	ДокументыЗаказПокупателя.ДоговорКонтрагента,
	|	ДокументыЗаказПокупателя.КонтактноеЛицоКонтрагента,
	|	ДокументыЗаказПокупателя.Организация,
	|	ДокументыЗаказПокупателя.Подразделение,
	|	ДокументыЗаказПокупателя.СкладГруппа,
	|	ДокументыЗаказПокупателя.Ответственный,
	|	ДокументыЗаказПокупателя.СуммаДокумента,
	|	ДокументыЗаказПокупателя.ВалютаДокумента,
	|	ДокументыЗаказПокупателя.ТипЦен,
	|	ДокументыЗаказПокупателя.ДатаОплаты,
	|	ДокументыЗаказПокупателя.ДатаОтгрузки,
	|	ДокументыЗаказПокупателя.УсловиеПродаж,
	|	ДокументыЗаказПокупателя.Грузополучатель,
	|	ДокументыЗаказПокупателя.ДисконтнаяКарта,
	|	ДокументыЗаказПокупателя.ОтражатьВБухгалтерскомУчете,
	|	ДокументыЗаказПокупателя.ОтражатьВНалоговомУчете,
	|	ДокументыЗаказПокупателя.Проведен
	|
	|{ВЫБРАТЬ 
	|	ДокументыЗаказПокупателя.ВидОперации КАК ВидОперации,
	|	ДокументыЗаказПокупателя.Контрагент.* КАК Контрагент,
	|	ДокументыЗаказПокупателя.ДоговорКонтрагента.* КАК ДоговорКонтрагента,
	|	ДокументыЗаказПокупателя.Организация.* КАК Организация,
	|	ДокументыЗаказПокупателя.КонтактноеЛицоКонтрагента.* КАК КонтактноеЛицоКонтрагента,
	|	ДокументыЗаказПокупателя.Подразделение.* КАК Подразделение,
	|	ДокументыЗаказПокупателя.СкладГруппа.* КАК СкладГруппа,
	|	ДокументыЗаказПокупателя.Ответственный.* КАК Ответственный,
	|	ДокументыЗаказПокупателя.СуммаДокумента КАК СуммаДокумента,
	|	ДокументыЗаказПокупателя.ТипЦен.* КАК ТипЦен,
	|	ДокументыЗаказПокупателя.ВалютаДокумента КАК ВалютаДокумента,
	|	ДокументыЗаказПокупателя.УсловиеПродаж.* КАК УсловиеПродаж,
	|	ДокументыЗаказПокупателя.Грузополучатель.* КАК Грузополучатель,
	|	ДокументыЗаказПокупателя.ДисконтнаяКарта.* КАК ДисконтнаяКарта,
	|	ДокументыЗаказПокупателя.ОтражатьВБухгалтерскомУчете КАК ОтражатьВБухгалтерскомУчете,
	|	ДокументыЗаказПокупателя.ОтражатьВНалоговомУчете КАК ОтражатьВНалоговомУчете
	|//СВОЙСТВА
	|}
	|
	|ИЗ
	|	Документ.ЗаказПокупателя                                    КАК ДокументыЗаказПокупателя
	|		
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрНакопления.ЗаказыПокупателей.Остатки             КАК ЗаказыПокупателейОстатки
	|		ПО ДокументыЗаказПокупателя.Ссылка = ЗаказыПокупателейОстатки.ЗаказПокупателя
	|		
	|	ЛЕВОЕ СОЕДИНЕНИЕ 
	|		РегистрНакопления.РасчетыСКонтрагентами.Остатки         КАК РасчетыСКонтрагентамиОстатки
	|		ПО ДокументыЗаказПокупателя.Ссылка = РасчетыСКонтрагентамиОстатки.Сделка
	|
	|   ЛЕВОЕ СОЕДИНЕНИЕ
	|       РегистрНакопления.ТоварыВРезервеНаСкладах.Остатки       КАК ТоварыВРезервеНаСкладахОстатки
	|		ПО ДокументыЗаказПокупателя.Ссылка = ТоварыВРезервеНаСкладахОстатки.ДокументРезерва
	|
	|   ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрНакопления.РазмещениеЗаказовПокупателей.Остатки  КАК РазмещениеЗаказовПокупателейОстатки
	|		ПО ДокументыЗаказПокупателя.Ссылка = РазмещениеЗаказовПокупателейОстатки.ЗаказПокупателя
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрСведений.ПричиныЗакрытияЗаказов 					КАК ПричиныЗакрытияЗаказов
	|		ПО ДокументыЗаказПокупателя.Ссылка = ПричиныЗакрытияЗаказов.Заказ 
	|			И ПричиныЗакрытияЗаказов.Регистратор <> ДокументыЗаказПокупателя.Ссылка
	|//СОЕДИНЕНИЯ
	|
	|	{ ГДЕ 
	|	 ДокументыЗаказПокупателя.ВидОперации КАК ВидОперации,
	|	 ДокументыЗаказПокупателя.Контрагент.* КАК Контрагент,
	|	 ДокументыЗаказПокупателя.ДоговорКонтрагента.* КАК ДоговорКонтрагента,
	|	 ДокументыЗаказПокупателя.Организация.* КАК Организация,
	|	 ДокументыЗаказПокупателя.КонтактноеЛицоКонтрагента.* КАК КонтактноеЛицоКонтрагента,
	|	 ДокументыЗаказПокупателя.СкладГруппа.* КАК СкладГруппа,
	|	 ДокументыЗаказПокупателя.Подразделение.* КАК Подразделение,
	|	 ДокументыЗаказПокупателя.Ответственный.* КАК Ответственный,
	|	 ДокументыЗаказПокупателя.СуммаДокумента КАК СуммаДокумента,
	|	 ДокументыЗаказПокупателя.ТипЦен.* КАК ТипЦен,
	|	 ДокументыЗаказПокупателя.ВалютаДокумента КАК ВалютаДокумента,
	|	 ДокументыЗаказПокупателя.УсловиеПродаж.* КАК УсловиеПродаж,
	|	 ДокументыЗаказПокупателя.Грузополучатель.* КАК Грузополучатель,
	|	 ДокументыЗаказПокупателя.ДисконтнаяКарта.* КАК ДисконтнаяКарта,
	|	 ДокументыЗаказПокупателя.ОтражатьВБухгалтерскомУчете КАК ОтражатьВБухгалтерскомУчете,
	|	 ДокументыЗаказПокупателя.ОтражатьВНалоговомУчете КАК ОтражатьВНалоговомУчете
	|//СВОЙСТВА
	|//КАТЕГОРИИ
	|	}
	|
	|ГДЕ
	|	(ДокументыЗаказПокупателя.Проведен) И
	|	(ПричиныЗакрытияЗаказов.ПричинаЗакрытияЗаказа IS NULL) И
	|	((ДокументыЗаказПокупателя.Дата >= &ДатаНач) ИЛИ (&ДатаНач = &ПустаяДата)) И
	|	((ДокументыЗаказПокупателя.Дата <= &ДатаКон) ИЛИ (&ДатаКон = &ПустаяДата)) И
	|
	|	(	((&СрокОплатыСравнение = 6) И (ДокументыЗаказПокупателя.ДатаОплаты = &ПустаяДата)) ИЛИ
	|		((&СрокОплатыСравнение = 5) И (ДокументыЗаказПокупателя.ДатаОплаты > &ДатаОплаты)) ИЛИ
	|		((&СрокОплатыСравнение = 4) И (ДокументыЗаказПокупателя.ДатаОплаты = &ДатаОплаты)) ИЛИ
	|		((&СрокОплатыСравнение = 3) И (ДокументыЗаказПокупателя.ДатаОплаты < &ДатаОплаты)) ИЛИ
	|		((&СрокОплатыСравнение = 2) И (ДокументыЗаказПокупателя.ДатаОплаты <= &ТребуемаяДатаОплаты)) ИЛИ
	|		(&СрокОплатыСравнение = 1)
	|	) И
	|
	|	(	((&СрокОтгрузкиСравнение = 6) И (ДокументыЗаказПокупателя.ДатаОтгрузки = &ПустаяДата)) ИЛИ
	|		((&СрокОтгрузкиСравнение = 5) И (ДокументыЗаказПокупателя.ДатаОтгрузки > &ДатаОтгрузки)) ИЛИ
	|		((&СрокОтгрузкиСравнение = 4) И (ДокументыЗаказПокупателя.ДатаОтгрузки = &ДатаОтгрузки)) ИЛИ
	|		((&СрокОтгрузкиСравнение = 3) И (ДокументыЗаказПокупателя.ДатаОтгрузки < &ДатаОтгрузки)) ИЛИ
	|		((&СрокОтгрузкиСравнение = 2) И (ДокументыЗаказПокупателя.ДатаОтгрузки <= &ТребуемаяДатаОтгрузки)) ИЛИ
	|		(&СрокОтгрузкиСравнение = 1)
	|	) И
	|
	|	(	((&СостояниеОплаты = 3) И (ДокументыЗаказПокупателя.СуммаДокумента > 0) И ((РасчетыСКонтрагентамиОстатки.СуммаВзаиморасчетовОстаток IS NULL) ИЛИ (РасчетыСКонтрагентамиОстатки.СуммаВзаиморасчетовОстаток < 0))) ИЛИ
	|		((&СостояниеОплаты = 2) И (ДокументыЗаказПокупателя.СуммаДокумента > 0) И (РасчетыСКонтрагентамиОстатки.СуммаВзаиморасчетовОстаток > 0)) ИЛИ
	|		(&СостояниеОплаты = 1)
	|	) И
	|
	|	(	((&СостояниеОтгрузки = 3) И (ДокументыЗаказПокупателя.СуммаДокумента > 0) И ((ЗаказыПокупателейОстатки.СуммаВзаиморасчетовОстаток IS NULL) ИЛИ (ЗаказыПокупателейОстатки.СуммаВзаиморасчетовОстаток < 0)) И ((ЗаказыПокупателейОстатки.КоличествоОстаток IS NULL) ИЛИ (ЗаказыПокупателейОстатки.КоличествоОстаток < 0))) ИЛИ
	|		((&СостояниеОтгрузки = 2) И (ДокументыЗаказПокупателя.СуммаДокумента > 0) И ((ЗаказыПокупателейОстатки.СуммаВзаиморасчетовОстаток > 0) ИЛИ (ЗаказыПокупателейОстатки.КоличествоОстаток > 0))) ИЛИ
	|		(&СостояниеОтгрузки = 1)
	|	) И
	|
	|	(	((&НаличиеРезервов = 3) И (&РазмещениеРезервов = 3) И ((ТоварыВРезервеНаСкладахОстатки.КоличествоОстаток IS NULL) ИЛИ (ТоварыВРезервеНаСкладахОстатки.КоличествоОстаток < 0)) И (РазмещениеЗаказовПокупателейОстатки.КоличествоОстаток > 0)) ИЛИ
	|		((&НаличиеРезервов = 3) И (&РазмещениеРезервов = 2) И (ТоварыВРезервеНаСкладахОстатки.КоличествоОстаток > 0) И ((РазмещениеЗаказовПокупателейОстатки.КоличествоОстаток IS NULL) ИЛИ (РазмещениеЗаказовПокупателейОстатки.КоличествоОстаток < 0))) ИЛИ
	|		((&НаличиеРезервов = 3) И (&РазмещениеРезервов = 1) И ((ТоварыВРезервеНаСкладахОстатки.КоличествоОстаток > 0) ИЛИ (РазмещениеЗаказовПокупателейОстатки.КоличествоОстаток > 0))) ИЛИ
	|		((&НаличиеРезервов = 2) И ((ТоварыВРезервеНаСкладахОстатки.КоличествоОстаток IS NULL) ИЛИ (ТоварыВРезервеНаСкладахОстатки.КоличествоОстаток < 0)) И ((РазмещениеЗаказовПокупателейОстатки.КоличествоОстаток IS NULL) ИЛИ (РазмещениеЗаказовПокупателейОстатки.КоличествоОстаток < 0))) ИЛИ
	|		(&НаличиеРезервов = 1)
	|	)
	|
	|УПОРЯДОЧИТЬ ПО
	|	ДокументыЗаказПокупателя.Дата, ДокументыЗаказПокупателя.Номер
	|";

	СтруктураПредставлениеПолей = Новый Структура;
	
	СтруктураПредставлениеПолей.Вставить("ВидОперации",                 "Вид операции");
	СтруктураПредставлениеПолей.Вставить("Контрагент",                  "Контрагент");
	СтруктураПредставлениеПолей.Вставить("ДоговорКонтрагента",          "Договор");
	СтруктураПредставлениеПолей.Вставить("Организация",                 "Организация");
	СтруктураПредставлениеПолей.Вставить("КонтактноеЛицоКонтрагента",   "Контактное лицо");
	СтруктураПредставлениеПолей.Вставить("Подразделение",               "Подразделение");
	СтруктураПредставлениеПолей.Вставить("СкладГруппа",                 "Склад/группа");
	СтруктураПредставлениеПолей.Вставить("Ответственный",               "Ответственный");
	СтруктураПредставлениеПолей.Вставить("СуммаДокумента",              "Сумма документа");
	СтруктураПредставлениеПолей.Вставить("ТипЦен",                      "Тип цен");
	СтруктураПредставлениеПолей.Вставить("ВалютаДокумента",             "Валюта документа");
	СтруктураПредставлениеПолей.Вставить("УсловиеПродаж",               "Условие продаж");
	СтруктураПредставлениеПолей.Вставить("Грузополучатель",             "Грузополучатель");
	СтруктураПредставлениеПолей.Вставить("ДисконтнаяКарта",             "Дисконтная карта");
	СтруктураПредставлениеПолей.Вставить("ОтражатьВБухгалтерскомУчете", "Отражать в БУ");
	СтруктураПредставлениеПолей.Вставить("ОтражатьВНалоговомУчете",     "Отражать в НУ");
	СтруктураПредставлениеПолей.Вставить("АвтоРезервирование",          "Авторезервирование");
	СтруктураПредставлениеПолей.Вставить("АвтоРазмещение",              "Авторазмещение");
	
	Если ИспользоватьСвойстваИКатегории Тогда

        СоответствиеНазначений = Новый Соответствие;
		
		ТаблицаПолей = Новый ТаблицаЗначений;
		ТаблицаПолей.Колонки.Добавить("ПутьКДанным");  // описание поля запроса поля, для которого добавляются свойства и категории. Используется в условии соединения с регистром сведений, хранящим значения свойств или категорий
		ТаблицаПолей.Колонки.Добавить("Представление");// представление поля, для которого добавляются свойства и категории. 
		ТаблицаПолей.Колонки.Добавить("Назначение");   // назначение свойств/категорий объектов для данного поля
		//ТаблицаПолей.Колонки.Добавить("ТипЗначения");  // тип значения поля, для которого добавляются свойства и категории. Используется, если не установлено назначение
		ТаблицаПолей.Колонки.Добавить("НетКатегорий"); // признак НЕиспользования категорий для объекта
		
		ТекстПоляКатегорий = "";
		ТекстПоляСвойств = "";
		
		ТаблицаПолей.Очистить();
		
		НоваяСтрока = ТаблицаПолей.Добавить();
		НоваяСтрока.ПутьКДанным = "ДокументыЗаказПокупателя.Контрагент";
		НоваяСтрока.Представление = "Контрагент";
		НоваяСтрока.Назначение = ПланыВидовХарактеристик.НазначенияСвойствКатегорийОбъектов.Справочник_Контрагенты;

		НоваяСтрока = ТаблицаПолей.Добавить();
		НоваяСтрока.ПутьКДанным = "ДокументыЗаказПокупателя.Контрагент";
		НоваяСтрока.Представление = "Грузополучатель";
		НоваяСтрока.Назначение = ПланыВидовХарактеристик.НазначенияСвойствКатегорийОбъектов.Справочник_Контрагенты;

		НоваяСтрока = ТаблицаПолей.Добавить();
		НоваяСтрока.ПутьКДанным = "ДокументыЗаказПокупателя.КонтактноеЛицоКонтрагента";
		НоваяСтрока.Представление = "КонтактноеЛицоКонтрагента";
		НоваяСтрока.Назначение = ПланыВидовХарактеристик.НазначенияСвойствКатегорийОбъектов.Справочник_КонтактныеЛицаКонтрагентов;
		
		НоваяСтрока = ТаблицаПолей.Добавить();
		НоваяСтрока.ПутьКДанным = "ДокументыЗаказПокупателя.Ссылка";
		НоваяСтрока.Представление = "Заказ покупателя";
		НоваяСтрока.Назначение = ПланыВидовХарактеристик.НазначенияСвойствКатегорийОбъектов.Документы;

		УправлениеОтчетами.ДобавитьВТекстСвойстваИКатегории(ТаблицаПолей, ТекстЗапроса, СтруктураПредставлениеПолей, СоответствиеНазначений, ПостроительОтчета.Параметры,, ТекстПоляКатегорий, ТекстПоляСвойств,, "//СВОЙСТВА", "//КАТЕГОРИИ", "//СОЕДИНЕНИЯ", , СтруктураДляОтбораПоКатегориям);
		
	КонецЕсли;
	
	Если ПустаяСтрока(ТекстЗапроса) = Ложь Тогда
		ПостроительОтчета.Текст = ТекстЗапроса;	
	КонецЕсли;
	
	УправлениеОтчетами.УстановитьТипыЗначенийСвойствИКатегорийДляОтбора(ПостроительОтчета, ТекстПоляКатегорий, ТекстПоляСвойств, СоответствиеНазначений, СтруктураПредставлениеПолей);
	
	// Заполнить отбор построителя по умолчанию
	Если НЕ(ПостроительОтчета.Отбор.Количество() > 0) Тогда
		Если НастройкиИзДокумента Тогда
			ПостроительОтчета.УстановитьНастройки(НастройкиОтбора);
		Иначе
			ЗаполнитьОтборПостроителя(ПостроительОтчета);
		КонецЕсли;
		
		Поле = ПостроительОтчета.Отбор.Найти("Организация");
		Если Поле = Неопределено Тогда
			Поле = ПостроительОтчета.Отбор.Добавить("Организация", "Организация", "Организация");
		КонецЕсли;
		
		Если НЕ (НастройкиИзДокумента И ДокументОбъект.Организация.Пустая()) Тогда
			Если ДокументОбъект.Организация.Пустая() Тогда
				Поле.Использование = Ложь;
			Иначе		
				Поле.Использование = Истина;
				Поле.Установить(ДокументОбъект.Организация);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	// Заполнить представление полей построителя по именам
	УправлениеОтчетами.ЗаполнитьПредставленияПолей(СтруктураПредставлениеПолей, ПостроительОтчета);
	
КонецПроцедуры // ИнициироватьПостроительДляОтбораЗаказов()

// Заполняет переменные построителя выбранными в форме значениями.
//
// Параметры:
//  ПостроительОтчета - ссылка на объект ПостроительОтчета.
//
Процедура ЗаполнитьПараметрыПостроителя(ПостроительОтчета)
	
	// Заполним параметры построителя
	ПостроительОтчета.Параметры.Вставить("НаличиеРезервов", 		НаличиеРезервов);
	ПостроительОтчета.Параметры.Вставить("РазмещениеРезервов", 		РазмещениеРезервов);
	ПостроительОтчета.Параметры.Вставить("СостояниеОплаты", 		СостояниеОплаты);
	ПостроительОтчета.Параметры.Вставить("СостояниеОтгрузки", 		СостояниеОтгрузки);
	ПостроительОтчета.Параметры.Вставить("СрокОплатыСравнение", 	СрокОплатыСравнение);
	ПостроительОтчета.Параметры.Вставить("СрокОтгрузкиСравнение", 	СрокОтгрузкиСравнение);
	
	ПостроительОтчета.Параметры.Вставить("ДатаНач", 		ДатаНачала);
	ПостроительОтчета.Параметры.Вставить("ДатаКон", 		ДатаОкончания);
	ПостроительОтчета.Параметры.Вставить("ДатаОплаты", 		ДатаОплаты);
	ПостроительОтчета.Параметры.Вставить("ДатаОтгрузки", 	ДатаОтгрузки);
	ПостроительОтчета.Параметры.Вставить("ТекущаяДата", 	ТекущаяДата());
	ПостроительОтчета.Параметры.Вставить("ПустаяДата",  	Дата('00010101'));
	ПостроительОтчета.Параметры.Вставить("ПричинаЗакрытияЗаказа",ПричинаЗакрытия);

	СпособКонтроляДнейЗадолженности = неопределено;
	флСообщилиНеЗаполненКалендарь = ложь;
	
	//Заполнение параметра ТребуемаяДатаОтгрузки
	СпособКонтроляДнейЗадолженности = неопределено;

	Если  СрокОтгрузкиСравнение <> 2 или ПросроченоДнейОтгрузки=0 Тогда
		ТребуемаяДатаОтгрузки = НачалоДня(ТекущаяДата());
	Иначе
		СпособКонтроляДнейЗадолженности = Константы.СпособКонтроляДнейЗадолженности.Получить();
		Если СпособКонтроляДнейЗадолженности = Перечисления.СпособыКонтроляДнейЗадолженности.ПоКалендарнымДням Тогда
			ТребуемаяДатаОтгрузки = НачалоДня(ТекущаяДата()-24*60*60*ПросроченоДнейОтгрузки);
		Иначе
			ТребуемаяДатаОтгрузки = ЗаполнениеДокументов.ОпределитьДату(НачалоДня(ТекущаяДата()),-ПросроченоДнейОтгрузки);
			Если ТребуемаяДатаОтгрузки=неопределено Тогда
				Сообщить("Не заполнен Регламентированный производственный календарь за "+Год(ТекущаяДата())+" год.");
				Сообщить("Дата, ранее которой заказы просрочены, рассчитана по календарным дням.");
				ТребуемаяДатаОтгрузки = НачалоДня(ТекущаяДата()-24*60*60*ПросроченоДнейОтгрузки);
				флСообщилиНеЗаполненКалендарь = истина;
			КонецЕсли;
		КонецЕсли;  //Если СпособКонтроляДнейЗадолженности = Перечисления.СпособыКонтроляДнейЗадолженности.ПоКалендарнымДням Тогда
	КонецЕсли; //Если  СрокОтгрузкиСравнение <> 2 или ПросроченоДнейОтгрузки=0 Тогда
	
	//Заполнение параметра ТребуемаяДатаОплаты
	Если  СрокОплатыСравнение <> 2 или ПросроченоДнейОплаты=0 Тогда
		ТребуемаяДатаОплаты = НачалоДня(ТекущаяДата());
	Иначе
		Если СпособКонтроляДнейЗадолженности=неопределено Тогда
			СпособКонтроляДнейЗадолженности = Константы.СпособКонтроляДнейЗадолженности.Получить();
		КонецЕсли;
		Если СпособКонтроляДнейЗадолженности = Перечисления.СпособыКонтроляДнейЗадолженности.ПоКалендарнымДням Тогда
			ТребуемаяДатаОплаты = НачалоДня(ТекущаяДата()-24*60*60*ПросроченоДнейОплаты);
		Иначе
			ТребуемаяДатаОплаты = ЗаполнениеДокументов.ОпределитьДату(НачалоДня(ТекущаяДата()),-ПросроченоДнейОплаты);
			Если  ТребуемаяДатаОплаты = неопределено Тогда
				ТребуемаяДатаОплаты = НачалоДня(ТекущаяДата()-24*60*60*ПросроченоДнейОплаты);
				Если не флСообщилиНеЗаполненКалендарь Тогда
					Сообщить("Не заполнен Регламентированный производственный календарь за "+Год(ТекущаяДата())+" год.");
					Сообщить("Дата, ранее которой заказы просрочены, рассчитана по календарным дням.");
				КонецЕсли;
			КонецЕсли;
		КонецЕсли; //Если СпособКонтроляДнейЗадолженности = Перечисления.СпособыКонтроляДнейЗадолженности.ПоКалендарнымДням Тогда
	КонецЕсли; //Если  СрокОплатыСравнение <> 2 или ПросроченоДнейОплаты=0 Тогда

	ПостроительОтчета.Параметры.Вставить("ТребуемаяДатаОплаты", 	ТребуемаяДатаОплаты);
	ПостроительОтчета.Параметры.Вставить("ТребуемаяДатаОтгрузки", 	ТребуемаяДатаОтгрузки);
КонецПроцедуры // ЗаполнитьПараметрыПостроителя()

// Заполняет отбор построителя по умолчанию.
//
// Параметры:
//  ПостроительОтчета - ссылка на объект ПостроительОтчета.
//
Процедура ЗаполнитьОтборПостроителя(ПостроительОтчета)
	
	// Добавим поля отбора по умолчанию
	Поле = ПостроительОтчета.Отбор.Добавить("Ответственный", "Ответственный", "Ответственный");
	Поле.Установить(ДокументОбъект.Ответственный.Ссылка);
	
	Поле = ПостроительОтчета.Отбор.Добавить("ВидОперации", "ВидОперации", "Вид операции");
	Поле.Установить(Перечисления.ВидыОперацийЗаказПокупателя.ПродажаКомиссия);
	
	Поле = ПостроительОтчета.Отбор.Добавить("Контрагент", "Контрагент", "Контрагент");
	Поле.Использование = Ложь;
	Поле = ПостроительОтчета.Отбор.Добавить("ДоговорКонтрагента", "ДоговорКонтрагента", "Договор");
	Поле.Использование = Ложь;
	Поле = ПостроительОтчета.Отбор.Добавить("СкладГруппа", "СкладГруппа", "Склад/группа");
	Поле.Использование = Ложь;
	
КонецПроцедуры // ЗаполнитьОтборПостроителя()

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ
//
	
// Обработчик события ПриОткрытии формы.
//
Процедура ПриОткрытии()
	
	// Заполнить структуру подменю Вид заполнения панели формы отбора
	УстановитьСтруктуруПодменюВидЗаполнения();
	
	// Создать и заполнить построитель для отбора
	ИнициироватьПостроительДляОтбораЗаказов(ПостроительОтчета);
	
	// Установить доступность кнопок командной панели таблицы Заказов
	УстановитьДоступностьКнопокПанелиЗаказов();
	
	// Установим конец периода на конец дня даты документа закрытия
	ДатаКон = КонецДня(Дата);
	ДатаКонПриИзменении(ЭлементыФормы.ДатаКон);

КонецПроцедуры // ПриОткрытии()

// Обработчик события ПриИзменении элемента формы ДатаНач.
//
Процедура ДатаНачПриИзменении(Элемент)

	// Установка даты начала периода по умолчанию
	НП.УстановитьПериод(ДатаНач, КонецДня(ДатаКон), Истина);
	
КонецПроцедуры	// ДатаНачПриИзменении()

// Обработчик события ПриИзменении элемента формы ДатаКон.
//
Процедура ДатаКонПриИзменении(Элемент)

	// Установка даты конца периода по умолчанию
	Если КонецДня(ДатаКон) > КонецДня(Дата) Тогда
		// Установим конец периода на конец дня даты документа закрытия
		ДатаКон = КонецДня(Дата);
		Предупреждение("Дата окончания периода не должна быть больше даты документа!",,ЭтаФорма.Заголовок);
	Иначе
		ДатаКон = КонецДня(ДатаКон);
	КонецЕсли;
	
	НП.УстановитьПериод(ДатаНач, ДатаКон, Истина);
	
КонецПроцедуры	// ДатаКонПриИзменении()

// Обработчик события Нажатие кнопки настройки периода.
//
Процедура КнопкаНастройкаПериодаНажатие(Элемент)
	
	// Установка периода отбора
	Если НП.Редактировать() Тогда
		ДатаНач = НП.ПолучитьДатуНачала();
		ДатаКон = НП.ПолучитьДатуОкончания();
	КонецЕсли;

КонецПроцедуры // КнопкаНастройкаПериодаНажатие()

// Обработчик события Выбор кнопки подменю ВидЗаполнения командной панели формы.
// формы. Процедура устанавливает значение реквизита ВидОперации.
//
Процедура ПолеНастройкиВидЗаполненияПриИзменении(Элемент)

	// Установить настройки диалога по виду заполнения
	Если Элемент.Значение = 1 Тогда 
		УстановитьНастройкиПоУмолчаниюДляПроизвольногоОтбора();
	ИначеЕсли Элемент.Значение = 2 Тогда 
		УстановитьНастройкиПоУмолчаниюДляУстаревшихЗаказов();
	ИначеЕсли Элемент.Значение = 3 Тогда 
		УстановитьНастройкиПоУмолчаниюДляИсполненныхЗаказов();
	ИначеЕсли Элемент.Значение = 4 Тогда 
		УстановитьНастройкиПоУмолчаниюДляВсехЗаказовСРезервами();
	ИначеЕсли Элемент.Значение = 5 Тогда 
		УстановитьНастройкиПоУмолчаниюДляНеоплаченныхЗаказов();
	КонецЕсли;
	
	ПриИзмененииНастроекПоУмолчанию();
	
КонецПроцедуры // ПолеНастройкиВидЗаполненияПриИзменении()

// Обработчик события ПриИзменении элемента формы ПолеНастройкиНаличиеРезервов.
//
Процедура ПолеНастройкиНаличиеРезервовПриИзменении(Элемент)
	
	// Если с резервами, то доступен отбор по местоположение резервов
	Если Элемент.Значение = 3 Тогда 
		ФлагВидимостиРазмещениеРезервов = Истина;
	Иначе
		ФлагВидимостиРазмещениеРезервов = Ложь;
	КонецЕсли;
	
	УстановитьВидимостьЭлементовНастройки();
	
КонецПроцедуры // ПолеНастройкиНаличиеРезервовПриИзменении()

// Обработчик события ПриИзменении элемента формы ПолеНастройкиСрокОплатыСравнение.
//
Процедура ПолеНастройкиСрокОплатыСравнениеПриИзменении(Элемент)
	
	Если Элемент.Значение = 1 ИЛИ Элемент.Значение = 6 Тогда 
		// По значению Не важно и Не заполнена скроем поля настройки отбора 
		ФлагВидимостиПросроченоДнейОплаты = Ложь;
		ФлагВидимостиДатыОплаты = Ложь;
	ИначеЕсли Элемент.Значение = 2 Тогда 
		// По значению Просрочен доступен отбор по Количеству дней
		ФлагВидимостиПросроченоДнейОплаты = Истина;
		ФлагВидимостиДатыОплаты = Ложь;
	Иначе
		// Доступен отбор по Дате оплаты
		ФлагВидимостиПросроченоДнейОплаты = Ложь;
		ФлагВидимостиДатыОплаты = Истина;
	КонецЕсли;
	
	УстановитьВидимостьЭлементовНастройки();
			
КонецПроцедуры // ПолеНастройкиСрокОплатыСравнениеПриИзменении()

// Обработчик события ПриИзменении элемента формы ПолеНастройкиСрокОтгрузкиСравнение.
//
Процедура ПолеНастройкиСрокОтгрузкиСравнениеПриИзменении(Элемент)
	
	Если Элемент.Значение = 1 ИЛИ Элемент.Значение = 6 Тогда 
		// По значению Не важно и Не заполнена скроем поля настройки отбора 
		ФлагВидимостиПросроченоДнейОтгрузки = Ложь;
		ФлагВидимостиДатыОтгрузки = Ложь;
	ИначеЕсли Элемент.Значение = 2 Тогда 
		// По значению Просрочен доступен отбор по Количеству дней
		ФлагВидимостиПросроченоДнейОтгрузки = Истина;
		ФлагВидимостиДатыОтгрузки = Ложь;
	Иначе
		// Доступен отбор по Дате отгрузки
		ФлагВидимостиПросроченоДнейОтгрузки = Ложь;
		ФлагВидимостиДатыОтгрузки = Истина;
	КонецЕсли;
	
	УстановитьВидимостьЭлементовНастройки();
			
КонецПроцедуры // ПолеНастройкиСрокОтгрузкиСравнениеПриИзменении()

// Обработчик события ПриИзменении элемента формы ПолеПросроченоДнейОтгрузки.
//
Процедура ПолеПросроченоДнейОтгрузкиПриИзменении(Элемент)

	// Сохраним значение поля данного ВидаЗаполнения
	СохранитьЗначение(ДокументОбъект.Метаданные().Имя + "_ПросроченоДнейОтгрузки_" + Строка(ВидЗаполнения), ПросроченоДнейОтгрузки);

КонецПроцедуры

// Обработчик события ПриИзменении элемента формы ПолеПросроченоДнейОплаты.
//
Процедура ПолеПросроченоДнейОплатыПриИзменении(Элемент)
	
	// Сохраним значение поля для данного ВидаЗаполнения
	СохранитьЗначение(ДокументОбъект.Метаданные().Имя + "_ПросроченоДнейОплаты_" + Строка(ВидЗаполнения), ПросроченоДнейОплаты);
	
КонецПроцедуры

// Обработчик события Выбор, открывает Документ по двойному щелчку в списке.
//
Процедура ТабличноеПолеЗаказыВыбор(Элемент, ВыбраннаяСтрока, Колонка, СтандартнаяОбработка)

	Если (Колонка.Имя <> "Переносить") И (Колонка.Имя <> "ПричинаЗакрытияЗаказа") Тогда
		Если ВыбраннаяСтрока.Ссылка <> Неопределено Тогда
			ВыбраннаяСтрока.Ссылка.ПолучитьФорму(, ЭтаФорма).Открыть();
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры // ТабличноеПолеЗаказыВыбор()

// Обработчик события НачалоВыбора элемента формы ТабличноеПолеОтбор.Значение.
//
Процедура ТабличноеПолеОтборЗначениеНачалоВыбора(Элемент, СтандартнаяОбработка)

	Если Найти(НРег(ЭлементыФормы.ТабличноеПолеОтбор.ТекущаяСтрока.Представление), "категории") Тогда
		// Ограничение списка категорий
		Назначение = СоответствиеНазначений.Получить(ЭлементыФормы.ТабличноеПолеОтбор.ТекущаяСтрока.Представление);
		УправлениеОтчетами.ОсуществитьВыборКатегории(Элемент, Назначение, ЭтаФорма, СтандартнаяОбработка);
	ИначеЕсли Найти(НРег(ЭлементыФормы.ТабличноеПолеОтбор.ТекущаяСтрока.Представление), "св-во") Тогда
		Свойство = СоответствиеНазначений.Получить(ЭлементыФормы.ТабличноеПолеОтбор.ТекущаяСтрока.Представление);
		УправлениеОтчетами.ОсуществитьВыборСвойства(Элемент, Свойство, ЭтаФорма, СтандартнаяОбработка);
	КонецЕсли;

КонецПроцедуры

// Обработчик события Нажатие кнопки ВключитьВсе.
//
Процедура КоманднаяПанельЗаказыДействиеВключитьВсе(Кнопка)
	
	// Установим все отметки в таблице ТабличноеПолеЗаказы формы.
	Для Каждого Строка Из ТабличноеПолеЗаказы Цикл
		Строка.Переносить = Истина;
	КонецЦикла;
	
КонецПроцедуры // КоманднаяПанельЗаказыДействиеВключитьВсе()

// Обработчик события Нажатие кнопки ВыключитьВсе.
//
Процедура КоманднаяПанельЗаказыДействиеВыключитьВсе(Кнопка)
	
	// Сбросим все отметки в таблице ТабличноеПолеЗаказы формы.
	Для Каждого Строка Из ТабличноеПолеЗаказы Цикл
		Строка.Переносить = Ложь;
	КонецЦикла;
	
КонецПроцедуры // КоманднаяПанельЗаказыДействиеВыключитьВсе()

// Обработчик события нажатие кнопки Анализ.
// вызывает анализ текущего состояния заказа.
//
Процедура ДействияФормыДействиеАнализ(Кнопка)

	// Открыть форму отчета АнализЗаказа для выбранного таблице документа.
	Если ЭлементыФормы.ТабличноеПолеЗаказы.ТекущаяСтрока <> Неопределено Тогда
		УправлениеЗаказами.СформироватьОтчетАнализЗаказа(ЭлементыФормы.ТабличноеПолеЗаказы.ТекущаяСтрока.Ссылка,ложь, истина);
	КонецЕсли;

КонецПроцедуры // ДействияФормыДействиеАнализ()

// Обработчик события Нажатие кнопки Заполнить.
//
Процедура КоманднаяПанельЗаказыДействиеЗаполнить(Кнопка)

	//Произведем отбор заказов по условиям отбора
	Если (ДатаНач > ДатаКон) И (ДатаКон <> Дата('00010101')) Тогда
		Предупреждение("Дата начала периода не может быть больше даты окончания периода!",,ЭтаФорма.Заголовок);
		Возврат;
	КонецЕсли;
	
	// Заполним Построитель параметрами из полей формы и выполним отбор
	ДатаНачала = НачалоДня(ДатаНач);
	ДатаОкончания = КонецДня(ДатаКон);
	
	Если НЕ УправлениеОтчетами.ЗадатьПараметрыОтбораПоКатегориям(ПостроительОтчета, СтруктураДляОтбораПоКатегориям) Тогда
		Предупреждение("По одной категории нельзя устанавливать несколько отборов");
		Возврат;
	КонецЕсли;

	
	ЗаполнитьПараметрыПостроителя(ПостроительОтчета);
	ПостроительОтчета.Выполнить();
	
	// Заполним список отобранных заказов
	ТабличноеПолеЗаказы.Очистить();
	ТабличноеПолеЗаказы = ПостроительОтчета.Результат.Выгрузить(ОбходРезультатаЗапроса.Прямой);
	
	УстановитьДоступностьКнопокПанелиЗаказов();

КонецПроцедуры // КоманднаяПанельЗаказыДействиеЗаполнить()

// Обработчик события Нажатие кнопки Перенести.
//
Процедура ОсновныеДействияФормыДействиеПеренести(Кнопка)
	
	// Проверим наличие строк в таблице.
	Если ТабличноеПолеЗаказы.Количество() = 0 Тогда
		Предупреждение("Не выбраны документы!");
		Возврат;
	КонецЕсли;
	
	//Перенесем заказы в табличную часть документа Закрытие заказов
	
	// Выберем отмеченные
	СтруктураОтбора = Новый Структура;
	СтруктураОтбора.Вставить("Переносить", Истина);
	НайденныеСтроки = ТабличноеПолеЗаказы.НайтиСтроки(СтруктураОтбора);
	
	// Если есть отмеченные 
	Если НайденныеСтроки.Количество() > 0 Тогда
		Если ДокументОбъект.Заказы.Количество() > 0 Тогда
			// Если документ не пуст 
			ТекстВопроса = "";
			Если ДокументОбъект.Проведен Тогда
				ТекстВопроса = "Внимание! Документ проведен." + Символы.ПС;
			КонецЕсли;
			ТекстВопроса = ТекстВопроса + "Перед заполнением табличная часть будет очищена. Заполнить?";
			Ответ = Вопрос(ТекстВопроса, РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Да, ЭтаФорма.Заголовок);
			Если Ответ <> КодВозвратаДиалога.Да Тогда
				Возврат;
			КонецЕсли; 
			// Очистим 
			ДокументОбъект.Заказы.Очистить();
		КонецЕсли;
	КонецЕсли;
	
	// Перенесем отмеченные заказы в документ
	Для Каждого Строка Из НайденныеСтроки Цикл 
		Если Строка.Ссылка <> Неопределено Тогда
			СтрокаДокумента = ДокументОбъект.Заказы.Добавить();
			СтрокаДокумента.ЗаказПокупателя = Строка.Ссылка;
			Если НЕ(Строка.ПричинаЗакрытияЗаказа.Пустая() = Истина) Тогда
				СтрокаДокумента.ПричинаЗакрытияЗаказа = Строка.ПричинаЗакрытияЗаказа;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	// Сохраним значение всех настроек в поле документа СпособЗаполнения
	НастройкиЗаполнения = Новый Структура();
	
	НастройкиЗаполнения.Вставить("ВидЗаполнения", ВидЗаполнения);
	
	НастройкиЗаполнения.Вставить("ДатаНач", ДатаНач);
	НастройкиЗаполнения.Вставить("ДатаКон", ДатаКон);
	
	НастройкиЗаполнения.Вставить("НаличиеРезервов", НаличиеРезервов);
	НастройкиЗаполнения.Вставить("РазмещениеРезервов", РазмещениеРезервов);
	
	НастройкиЗаполнения.Вставить("СостояниеОтгрузки", СостояниеОтгрузки);
	НастройкиЗаполнения.Вставить("СрокОтгрузкиСравнение", СрокОтгрузкиСравнение);
	НастройкиЗаполнения.Вставить("ПросроченоДнейОтгрузки", ПросроченоДнейОтгрузки);
	НастройкиЗаполнения.Вставить("ДатаОтгрузки", ДатаОтгрузки);
	
	НастройкиЗаполнения.Вставить("СостояниеОплаты", СостояниеОплаты);
	НастройкиЗаполнения.Вставить("СрокОплатыСравнение", СрокОплатыСравнение);
	НастройкиЗаполнения.Вставить("ПросроченоДнейОплаты", ПросроченоДнейОплаты);
	НастройкиЗаполнения.Вставить("ДатаОплаты", ДатаОплаты);
	
	НастройкиЗаполнения.Вставить("ИспользоватьСвойстваИКатегории", ИспользоватьСвойстваИКатегории);
	
	НастройкиЗаполнения.Вставить("ПричинаЗакрытия", ПричинаЗакрытия);
	
	НастройкиЗаполнения.Вставить("НастройкиОтбора", ПостроительОтчета.ПолучитьНастройки());
	
	ДокументОбъект.СпособЗаполнения = Новый ХранилищеЗначения(НастройкиЗаполнения);
	
	ЭтаФорма.Закрыть();

КонецПроцедуры // ОсновныеДействияФормыДействиеПеренести()

// Обработчик события ПриИзменении элемента формы ИспользоватьСвойстваИКатегории.
//
Процедура ИспользоватьСвойстваИКатегорииПриИзменении(Элемент)
	
	// Заполнить построитель для отбора
	ИнициироватьПостроительДляОтбораЗаказов(ПостроительОтчета);
	
КонецПроцедуры

// Обработчик события ПередЗакрытием формы.
//
Процедура ПередЗакрытием(Отказ, СтандартнаяОбработка)
	
	// Отключим стандартную обработку для этой формы
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

// Обработчик события ПриИзменении элемента формы ПолеВводаПричинаЗакрытия.
//
Процедура ПолеВводаПричинаЗакрытияПриИзменении(Элемент)
	
	Если (ПричинаЗакрытия <> СтараяПричинаЗакрытия) И (ТабличноеПолеЗаказы.Количество() > 0) Тогда
		Если ПричинаЗакрытия.Пустая() Тогда
			ТекстВопроса = "Очистить причину закрытия у всех заказов?";
			Ответ = Вопрос(ТекстВопроса, РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Да, ЭтаФорма.Заголовок);
			Если Ответ <> КодВозвратаДиалога.Да Тогда
				Возврат;
			КонецЕсли; 
			// Очистим причину закрытия во всех строках
			Для Каждого Строка Из ТабличноеПолеЗаказы Цикл 
				Если Строка.Ссылка <> Неопределено Тогда
					Строка.ПричинаЗакрытияЗаказа = Справочники.ПричиныЗакрытияЗаказов.ПустаяСсылка();
				КонецЕсли;
			КонецЦикла;
		Иначе
			ТекстВопроса = "Изменить причину закрытия у всех заказов?";
			Ответ = Вопрос(ТекстВопроса, РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Да, ЭтаФорма.Заголовок);
			Если Ответ <> КодВозвратаДиалога.Да Тогда
				Возврат;
			КонецЕсли; 
			// Заполним причину закрытия во все строки
			Для Каждого Строка Из ТабличноеПолеЗаказы Цикл 
				Если Строка.Ссылка <> Неопределено Тогда
					Строка.ПричинаЗакрытияЗаказа = ПричинаЗакрытия;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
		
КонецПроцедуры

// Обработчик события НачалоВыбора элемента формы ПолеВводаПричинаЗакрытия.
// 
Процедура ПолеВводаПричинаЗакрытияНачалоВыбора(Элемент, СтандартнаяОбработка)

	// Сохраним значение
	СтараяПричинаЗакрытия = ПричинаЗакрытия;
	
КонецПроцедуры

// Обработчик события Очистка элемента формы ПолеВводаПричинаЗакрытия.
// 
Процедура ПолеВводаПричинаЗакрытияОчистка(Элемент, СтандартнаяОбработка)
	
	// Сохраним значение
	СтараяПричинаЗакрытия = ПричинаЗакрытия;
	
КонецПроцедуры


НП = Новый НастройкаПериода;