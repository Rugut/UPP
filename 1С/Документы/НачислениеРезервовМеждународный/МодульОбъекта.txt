Перем мУдалятьДвижения;

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОКУМЕНТА

#Если Клиент Тогда
// Процедура осуществляет печать документа. Можно направить печать на 
// экран или принтер, а также распечатать необходимое количество копий.
//
//  Название макета печати передается в качестве параметра,
// по переданному названию находим имя макета в соответствии.
//
// Параметры:
//  НазваниеМакета - строка, название макета.
//
Процедура Печать(ИмяМакета, КоличествоЭкземпляров = 1, НаПринтер = Ложь) Экспорт

	Если ЭтоНовый() Тогда
		Предупреждение("Документ можно распечатать только после его записи");
		Возврат;
	ИначеЕсли Не УправлениеДопПравамиПользователей.РазрешитьПечатьНепроведенныхДокументов(Проведен) Тогда
		Предупреждение("Недостаточно полномочий для печати непроведенного документа!");
		Возврат;
	КонецЕсли;

	Если Не РаботаСДиалогами.ПроверитьМодифицированность(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;

	Если ТипЗнч(ИмяМакета) = Тип("ДвоичныеДанные") Тогда

		ТабДокумент = УниверсальныеМеханизмы.НапечататьВнешнююФорму(Ссылка, ИмяМакета);
		
		Если ТабДокумент = Неопределено Тогда
			Возврат
		КонецЕсли; 
		
	КонецЕсли;

	УниверсальныеМеханизмы.НапечататьДокумент(ТабДокумент, КоличествоЭкземпляров, НаПринтер, ОбщегоНазначения.СформироватьЗаголовокДокумента(ЭтотОбъект), Ссылка);

КонецПроцедуры // Печать

#КонецЕсли

// Возвращает доступные варианты печати документа
//
// Возвращаемое значение:
//  Структура, каждая строка которой соответствует одному из вариантов печати
//  
Функция ПолучитьСтруктуруПечатныхФорм() Экспорт
	
	Возврат Новый Структура;

КонецФункции // ПолучитьСтруктуруПечатныхФорм()


Процедура ОбработкаПроведения(Отказ, Режим)
	
	Если мУдалятьДвижения Тогда
		ОбщегоНазначения.УдалитьДвиженияРегистратора(ЭтотОбъект, Отказ);
	КонецЕсли;

	Для Каждого ТекСтрокаРезервы Из Резервы Цикл
		Движение = Движения.Международный.Добавить();
		Движение.Период = Дата;
		Движение.СчетДт = ТекСтрокаРезервы.СчетОтнесенияРасходов;
		Движение.СчетКт = ТекСтрокаРезервы.ВидРезерва.СчетРезерва;
		Движение.Организация = Организация;
		Движение.Сумма = ТекСтрокаРезервы.Сумма;
		Движение.Содержание = "Начисление резервов";
		Движение.НомерЖурнала = "Рег";
		
		Если (ЗначениеЗаполнено(ТекСтрокаРезервы.Валюта)) и (ЗначениеЗаполнено(ТекСтрокаРезервы.ВалютнаяСумма)) Тогда
			Если Движение.СчетДт.Валютный Тогда
				Движение.ВалютаДт = ТекСтрокаРезервы.Валюта;
				Движение.ВалютнаяСуммаДт = ТекСтрокаРезервы.ВалютнаяСумма;
			КонецЕсли;
			Если Движение.СчетКт.Валютный Тогда
				Движение.ВалютаКт = ТекСтрокаРезервы.Валюта;
				Движение.ВалютнаяСуммаКт = ТекСтрокаРезервы.ВалютнаяСумма;
			КонецЕсли;
		КонецЕсли;
		
		Для Ном = 1 по Движение.СчетДт.ВидыСубконто.Количество() Цикл
			Движение.СубконтоДт[Движение.СчетДт.ВидыСубконто[Ном-1].ВидСубконто] = ТекСтрокаРезервы["Субконто" + Ном];
		КонецЦикла;
		
		Если не Движение.СчетДт.ВидыСубконто.Найти(ПланыВидовХарактеристик.ВидыСубконтоМеждународные.Резервы,) = Неопределено Тогда
			Движение.СубконтоДт[ПланыВидовХарактеристик.ВидыСубконтоМеждународные.Резервы] = ТекСтрокаРезервы.ВидРезерва;
		КонецЕсли;
		Если не Движение.СчетКт.ВидыСубконто.Найти(ПланыВидовХарактеристик.ВидыСубконтоМеждународные.Резервы,) = Неопределено Тогда
			Движение.СубконтоКт[ПланыВидовХарактеристик.ВидыСубконтоМеждународные.Резервы] = ТекСтрокаРезервы.ВидРезерва;
		КонецЕсли;
	КонецЦикла;

	Движения.Международный.Записать();
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	Если ОбменДанными.Загрузка  Тогда
		Возврат;
	КонецЕсли;


	 
	мУдалятьДвижения = НЕ ЭтоНовый();
	
КонецПроцедуры // ПередЗаписью