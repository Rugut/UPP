Перем мВалютаРегламентированногоУчета Экспорт;

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОКУМЕНТА

#Если Клиент Тогда
// Формирует печатную форму реестра счетов
//
// Параметры:
//  ТабДок - Табличный документ
//
Функция ПечатьДокумента() Экспорт

	ТабДок = Новый ТабличныйДокумент;
	ТабДок.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_РеестрСчетов_РеестрСчетов";

	ФорматДаты = "ДФ='дд ММ гггг'";

	Макет      = ПолучитьМакет("РеестрСчетов");
	Область    = Макет.ПолучитьОбласть("Шапка");
	Область.Параметры.НаименованиеНомер    = "РЕЕСТР СЧЕТОВ № " + ОбщегоНазначения.ПолучитьНомерНаПечать(ЭтотОбъект);
	
	Область.Параметры.БанкБикИсполняющий="" + СокрЛП(ИсполняющийБанк.Наименование) + 
					?(НЕ ПустаяСтрока(ИсполняющийБанк.Город),", " + СокрЛП(ИсполняющийБанк.Город),"") + ", БИК " + СокрЛП(ИсполняющийБанк.Код);	
	Область.Параметры.ДатаДокумента        = Формат(Дата,ФорматДаты);
	Область.Параметры.НомерАккредитива     = НомерАккредитива;
	Область.Параметры.ДатаАккредитива      = Формат(ДатаАккредитива,ФорматДаты);
	Область.Параметры.НомерСчетаПолучателя = ВернутьРасчетныйСчет(СчетОрганизации);
	Область.Параметры.БанкБикПолучателя    = СформироватьСтрокуБанкБик(СчетОрганизации);
	Область.Параметры.Получатель           = СформироватьСтрокуКорреспондента(СчетОрганизации);
	Область.Параметры.БанкБикПлательщика   = СформироватьСтрокуБанкБик(СчетКонтрагента);
	Область.Параметры.Плательщик           = СформироватьСтрокуКорреспондента(СчетКонтрагента);
	Область.Параметры.СуммаПрописью        = ФорматироватьСуммуПрописи(СуммаАккредитива);
	ТабДок.Вывести(Область);

	Для каждого СтрокаТабЧ Из Реестр Цикл
		Область    = Макет.ПолучитьОбласть("Строка");
		Область.Параметры.НомерСтроки       = Реестр.Индекс(СтрокаТабЧ)+1;
		Область.Параметры.пДатаОтгрузки     = Формат(СтрокаТабЧ.ДатаОтгрузки,ФорматДаты);
		Область.Параметры.пВидТранспорта    = СокрЛП(СтрокаТабЧ.ВидТранспорта);
		Область.Параметры.пНомераДокументов = СокрЛП(СтрокаТабЧ.НомерДокумента);
		Область.Параметры.пСумма            = СтрокаТабЧ.Сумма;
		ТабДок.Вывести(Область);
	КонецЦикла;

	Область = Макет.ПолучитьОбласть("Подвал");
	ТабДок.Вывести(Область);
	Возврат ТабДок;

КонецФункции // Печать()

// Процедура осуществляет печать документа. Можно направить печать на 
// экран или принтер, а также распечатать необходимое количество копий.
//
//  Название макета печати передается в качестве параметра,
// по переданному названию находим имя макета в соответствии.
//
// Параметры:
//  НазваниеМакета - строка, название макета.
//
Процедура Печать(ИмяМакета, КоличествоЭкземпляров = 1, НаПринтер = Ложь) Экспорт

	Если ЭтоНовый() Тогда
		Предупреждение("Документ можно распечатать только после его записи");
		Возврат;
	КонецЕсли;

	Если Не РаботаСДиалогами.ПроверитьМодифицированность(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;

	Если ИмяМакета = "РеестрСчетов" Тогда
		
		Отказ = Ложь;
		Заголовок = СокрЛП(Ссылка);
		ЗаполнениеДокументов.ПроверитьЗаполнениеШапкиДокумента(ЭтотОбъект, СтруктураОбязательныхПолей(), Отказ, Заголовок);
		ЗаполнениеДокументов.ПроверитьЗаполнениеТабличнойЧасти(ЭтотОбъект, "Реестр", СтруктураОбязательныхПолейТабличнойЧасти(), Отказ, Заголовок);
		
		Если НЕ Отказ Тогда
			ТабДокумент = ПечатьДокумента();
		КонецЕсли;
		
	ИначеЕсли ТипЗнч(ИмяМакета) = Тип("ДвоичныеДанные") Тогда

		ТабДокумент = УниверсальныеМеханизмы.НапечататьВнешнююФорму(Ссылка, ИмяМакета);
		
		Если ТабДокумент = Неопределено Тогда
			Возврат
		КонецЕсли; 
		
	КонецЕсли;

	УниверсальныеМеханизмы.НапечататьДокумент(ТабДокумент, КоличествоЭкземпляров, НаПринтер, ОбщегоНазначения.СформироватьЗаголовокДокумента(ЭтотОбъект), Ссылка);

КонецПроцедуры // Печать

// Возвращает доступные варианты печати документа
//
// Возвращаемое значение:
//  Структура, каждая строка которой соответствует одному из вариантов печати
//  
Функция ПолучитьСтруктуруПечатныхФорм() Экспорт
	
	Возврат Новый Структура("РеестрСчетов", "Реестр счетов");

КонецФункции // ПолучитьСтруктуруПечатныхФорм()

#КонецЕсли

// Форматирует сумму прописью документа
//
// Параметры: 
//  СуммаДок - число - Сумма, которую надо прописать
//
// Возвращаемое значение:
//  Строку прописью
//
Функция ФорматироватьСуммуПрописи(СуммаДок)

	Результат = СуммаДок;
	ФорматСтрока  = "Л=ru_RU; ДП=Ложь";
	ПарамПредмета = "рубль, рубля, рублей, м, копейка, копейки, копеек, ж";
	Результат = ЧислоПрописью(Результат,ФорматСтрока,ПарамПредмета);

	Возврат Результат;

КонецФункции // ФорматироватьСуммуПрописи()

// Функция формирует наименование корреспондента для печати
Функция СформироватьНаименованиеКорреспондентов(Корресп)
	Если ТипЗнч(Корресп) = Тип("СправочникСсылка.ФизическиеЛица") Тогда
		Если ПустаяСтрока(Корресп.Фамилия) и ПустаяСтрока(Корресп.Имя) и ПустаяСтрока(Корресп.Отчество) Тогда
			Результат = СокрЛП(Корресп.Наименование);
		Иначе	
			Результат = СокрЛП(Корресп.Фамилия) + " " + СокрЛП(Корресп.Имя) + " " + СокрЛП(Корресп.Отчество);
		КонецЕсли;	
	Иначе	
		Результат = ?(НЕ ПустаяСтрока(Корресп.НаименованиеПолное),СокрЛП(Корресп.НаименованиеПолное),СокрЛП(Корресп.Наименование));
	КонецЕсли;	
	
	Возврат Результат;
КонецФункции //	СформироватьНаименованиеКорреспондентов()

// Формирует строку корреспондента
//
// Параметры: 
//  БанкСчет - Банковский счет. 
//
// Возвращаемое значение:
//  сформированную строку корреспондента
//
Функция СформироватьСтрокуКорреспондента(БанкСчет)

	Если НЕ БанкСчет.Пустая() Тогда
		БанкДляРасчетов = БанкСчет.БанкДляРасчетов;
		ВладБанковскогоСчета = БанкСчет.Владелец;
		Результат = ?(ПустаяСтрока(ВладБанковскогоСчета.ИНН),"","ИНН " + ВладБанковскогоСчета.ИНН + " ") + 
					СформироватьНаименованиеКорреспондентов(ВладБанковскогоСчета) +
					?(БанкДляРасчетов.Пустая(),"", 
					" р/с "+СокрЛП(БанкСчет.НомерСчета) +" в "+СокрЛП(БанкСчет.Банк.Наименование) +
					" " + ?(НЕ ПустаяСтрока(БанкСчет.Банк.Город), " " + СокрЛП(БанкСчет.Банк.Город),""));
	Иначе
		Результат = "";			
	КонецЕсли;				
					
	Возврат Результат;

КонецФункции // СформироватьСтрокуКорреспондента()

// Формирует строку банка и его БИК
//
// Параметры:
//  БанкСчет - Банковский счет. 
//
// Возвращаемое значение:
//   Строку, сформированную из банка и БИКа
//
Функция СформироватьСтрокуБанкБик(БанкСчет)

	Если НЕ БанкСчет.Пустая() Тогда
	    БанкДляРасчетов = БанкСчет.БанкДляРасчетов;
	    Результат = ?(БанкДляРасчетов.Пустая(),
					"" + СокрЛП(БанкСчет.Банк.Наименование) + 
					?(НЕ ПустаяСтрока(БанкСчет.Банк.Город),", " + СокрЛП(БанкСчет.Банк.Город),"") + ", БИК " + СокрЛП(БанкСчет.Банк.Код),
					"" + СокрЛП(БанкДляРасчетов.Наименование) + 
					?(НЕ ПустаяСтрока(БанкДляРасчетов.Город),", " + СокрЛП(БанкДляРасчетов.Город),"") + ", БИК "+СокрЛП(БанкДляРасчетов.Код));
	Иначе
		Результат = "";			
	КонецЕсли;				
	Возврат Результат;

КонецФункции // СформироватьСтрокуБанкБик()

// Определяет номер расчетного счета по
// переданному банковскому счету
//
// Параметры:
//  БанкСчет - Банковский счет. 
//
// Возвращаемое значение:
//   Номер расчетного счета
//
Функция ВернутьРасчетныйСчет(СчетКонтрагента)
	
	БанкДляРасчетов = СчетКонтрагента.БанкДляРасчетов;
	Результат       = ?(БанкДляРасчетов.Пустая(), СчетКонтрагента.НомерСчета, СчетКонтрагента.Банк.КоррСчет);

	Возврат Результат;
	
КонецФункции // ВернутьРасчетныйСчет()

// Возвращает структуру полей, обязательных к заполнению, в шапке документа
//
// Параметры:
//  Нет
//
// Возвращаемое значение:
//   Структура полей
//
Функция СтруктураОбязательныхПолей()

	СтруктураПолей= Новый Структура("Организация,СчетОрганизации,Контрагент,СчетКонтрагента");

	Возврат СтруктураПолей;

КонецФункции // СтруктураОбязательныхПолейРасчетыУпр()

// Возвращает структуру полей, обязательных к заполнению, в табличной части документа
//
// Параметры:
//  Нет
//
// Возвращаемое значение:
//   Структура полей
//
Функция СтруктураОбязательныхПолейТабличнойЧасти()

	СтруктураПолей= Новый Структура("ДатаОтгрузки,ВидТранспорта,НомерДокумента,Сумма");

	Возврат СтруктураПолей;

КонецФункции // СтруктураОбязательныхПолейРасчетыУпр()

Процедура ОбработкаЗаполнения(Основание)
	
	Если ТипЗнч(Основание) = Тип("ДокументСсылка.АккредитивПереданный") Тогда
		// Заполнение шапки
		Комментарий = Основание.Комментарий;
		Контрагент = Основание.Контрагент;
		Организация = Основание.Организация;
		Ответственный = Основание.Ответственный;
		ДокументОснование = Основание.Ссылка;
		СчетКонтрагента = Основание.СчетКонтрагента;
		СчетОрганизации = Основание.СчетОрганизации;
		
		НомерАккредитива=ОбщегоНазначения.ПолучитьНомерНаПечать(Основание);
		ДатаАккредитива=Основание.Дата;
		СуммаАккредитива=Основание.СуммаДокумента;
		
	КонецЕсли;
	
КонецПроцедуры

// Процедура - обработчик события "ПриКопировании" объекта.
//
Процедура ПриКопировании(ОбъектКопирования)
	
	ДокументОснование = Неопределено;

КонецПроцедуры


мВалютаРегламентированногоУчета = глЗначениеПеременной("ВалютаРегламентированногоУчета");