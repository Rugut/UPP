Перем мКолонкиНоменклатурныеЦеновыеГруппы;


Перем мВидОперации;

// Хранит дерево кнопок подменю заполнение ТЧ
Перем мКнопкиЗаполненияТЧ;



////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОБЩЕГО НАЗНАЧЕНИЯ

// Процедура устанавливает подменю "Заполнить" в командных панелях ТЧ документа при необходимости
//
Процедура УстановитьКнопкиПодменюЗаполненияТЧ();
	
	мКнопкиЗаполненияТЧ = УниверсальныеМеханизмы.ПолучитьДеревоКнопокЗаполненияТабличныхЧастей(Ссылка,Новый Действие("НажатиеНаДополнительнуюКнопкуЗаполненияТЧ"));
	
	СоответствиеТЧ = Новый Соответствие;
	СоответствиеТЧ.Вставить(ЭлементыФормы.НоменклатурныеЦеновыеГруппы,ЭлементыФормы.КоманднаяПанельСпискаНоменклатура);
	
	УниверсальныеМеханизмы.СформироватьПодменюЗаполненияТЧПоДеревуКнопок(мКнопкиЗаполненияТЧ,СоответствиеТЧ);
	
КонецПроцедуры


// Возвращает тип группы в соответствии с видом операции.
// Если указан параметр ТипГруппыОписание,
// то в него возвращается описание типа.
//
Функция ПолучитьТипГруппы(ТипГруппыОписание = Неопределено)

	Если НЕ ЗначениеЗаполнено(ВидОперации) Тогда
		ВидОперации = Перечисления.ВидыОперацийУстановкаТиповЦенПоГруппамНоменклатурыДляПокупателей[0];
	КонецЕсли;

	Если ВидОперации = Перечисления.ВидыОперацийУстановкаТиповЦенПоГруппамНоменклатурыДляПокупателей.ПоНоменклатурнымГруппам Тогда
		ТипГруппы = Тип("СправочникСсылка.НоменклатурныеГруппы");
		ТипГруппыОписание = Новый ОписаниеТипов("СправочникСсылка.НоменклатурныеГруппы");
	ИначеЕсли ВидОперации = Перечисления.ВидыОперацийУстановкаТиповЦенПоГруппамНоменклатурыДляПокупателей.ПоЦеновымГруппам Тогда
		ТипГруппы = Тип("СправочникСсылка.ЦеновыеГруппы");
		ТипГруппыОписание = Новый ОписаниеТипов("СправочникСсылка.ЦеновыеГруппы");
	КонецЕсли;

	Возврат ТипГруппы;

КонецФункции //ПолучитьТипГруппы()

// Устанавливает тип для реквизита.
// Параметры:
//  Элемент - элемент, тип которого надо установить.
//  ТипЭлемента - устанавливаемый тип.
Процедура УстановитьТипЭлемента(Элемент, ТипЭлемента)

	Если ТипЗнч(Элемент) <> ТипЭлемента Тогда
		Элемент = ОбщегоНазначения.ПустоеЗначениеТипа(ТипЭлемента);
	КонецЕсли;

КонецПроцедуры // УстановитьТипЭлемента()

// Устанавливает тип реквизита НоменклатурнаяЦеноваяГруппа.
// Для существующих строк, тип, которых не совпадает
// с новым типом, значение группы очищается.
//
Процедура ОграничитьТипГрупп()

	Перем ТипГруппыОписание;

	ТипГруппы = ПолучитьТипГруппы(ТипГруппыОписание);
	
	ЭлементыФормы.НоменклатурныеЦеновыеГруппы.Колонки.НоменклатурнаяЦеноваяГруппа.ЭлементУправления.ОграничениеТипа = ТипГруппыОписание;
	ЭлементыФормы.НоменклатурныеЦеновыеГруппы.Колонки.НоменклатурнаяЦеноваяГруппа.ТекстШапки = ?(ТипГруппы = Тип("СправочникСсылка.НоменклатурныеГруппы"), "Номенклатурная группа", "Ценовая группа");
	ЭлементыФормы.СписокНоменклатурныхЦеновыхГруппИСоответствующихТиповЦен.Заголовок = "Список " + ?(ТипГруппы = Тип("СправочникСсылка.НоменклатурныеГруппы"), "номенклатурных ", "ценовых ") + "групп и соответствующих типов цен";

КонецПроцедуры // ОграничитьТипГрупп()

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

// Процедура - обработчик события "ПередОткрытием" формы.
//
Процедура ПередОткрытием(Отказ, СтандартнаяОбработка)
	
	// Установка кнопок заполнение ТЧ
	УстановитьКнопкиПодменюЗаполненияТЧ();
	
	
КонецПроцедуры // ПередОткрытием()

// Процедура - обработчик события "ПриОткрытии" формы.
//
Процедура ПриОткрытии()

	
	Если НЕ ЭтоНовый() Тогда 
		НастройкаПравДоступа.ОпределитьДоступностьВозможностьИзмененияДокументаПоДатеЗапрета(ДокументОбъект, ЭтаФорма);
	КонецЕсли;
	
	Если ЭтоНовый() Тогда
		ДатаНачала = Дата;
		Ответственный = глЗначениеПеременной("глТекущийПользователь");

		//По умолчанию будем устанавливать вид операции первый заданный в конфигураторе.
		ВидОперации = Перечисления.ВидыОперацийУстановкаТиповЦенПоГруппамНоменклатурыДляПокупателей[0];
	КонецЕсли;

	мВидОперации = ВидОперации;

	СтруктураКолонок = Новый Структура();

	// Установить колонки, видимостью которых пользователь управлять не может.
	СтруктураКолонок.Вставить("НоменклатурнаяЦеноваяГруппа");
	СтруктураКолонок.Вставить("ТипЦен");

	ОбработкаТабличныхЧастей.УстановитьИзменятьВидимостьКолонокТабЧасти(ЭлементыФормы.НоменклатурныеЦеновыеГруппы.Колонки, СтруктураКолонок);
	
	// Заполняем подменю, вызываемое нажатием кнопки "Операция" командной панели
	// формы, значениями перечисления "Вид операции" данного вида документа.
	// В качестве обработки выбора вида операции назначается процедура
	// ДействияФормыДействиеУстановитьОперацию модуля формы.
	РаботаСДиалогами.УстановитьПодменюВыбораВидаОперации(ЭлементыФормы.ДействияФормы.Кнопки.ПодменюВидаОперации,
										ВидОперации.Метаданные().ЗначенияПеречисления,
										Новый Действие("ДействияФормыДействиеУстановитьОперацию"));


	// Вывести в заголовке формы вид операции.
	РаботаСДиалогами.УстановитьЗаголовокФормыДокумента(Строка(ВидОперации), ЭтотОбъект, ЭтаФорма);

	ОграничитьТипГрупп();

	МеханизмНумерацииОбъектов.ДобавитьВМенюДействияКнопкуРедактированияНомера(ЭлементыФормы.ДействияФормы.Кнопки.Подменю1);
	МеханизмНумерацииОбъектов.УстановитьДоступностьПоляВводаНомера(Метаданные(), ЭтаФорма, ЭлементыФормы.ДействияФормы.Кнопки.Подменю1,ЭлементыФормы.Номер);
	
	// Создать кнопки печати
	ФормированиеПечатныхФорм.СоздатьКнопкиПечати(ЭтотОбъект, ЭтаФорма);
	
	// Установить активный реквизит.
	РаботаСДиалогами.АктивизироватьРеквизитВФорме(ЭтотОбъект, ЭтаФорма);

	
КонецПроцедуры

// Процедура - обработчик события "ПослеЗаписи" формы.
//
Процедура ПослеЗаписи()


	РаботаСДиалогами.УстановитьЗаголовокФормыДокумента(Строка(ВидОперации), ЭтотОбъект, ЭтаФорма);
	МеханизмНумерацииОбъектов.ОбновитьПодсказкуКодНомерОбъекта(ЭтотОбъект.Метаданные(), ЭлементыФормы.ДействияФормы.Кнопки.Подменю1, ЭлементыФормы.Номер);

КонецПроцедуры // ПослеЗаписи()

// Процедура вызывает сервисный механизм для подбора в табличную часть.
//
// Параметры:
//  ТабличнаяЧасть - табличная часть, в которую осуществляется подбор.
//
Процедура ДействиеПодбор(ТабличнаяЧасть)

	Если ТабличнаяЧасть = НоменклатурныеЦеновыеГруппы Тогда
		Если ВидОперации = Перечисления.ВидыОперацийУстановкаТиповЦенПоГруппамНоменклатурыДляПокупателей.ПоНоменклатурнымГруппам Тогда
			ФормаПодбора = Справочники.НоменклатурныеГруппы.ПолучитьФорму("ФормаВыбора", ЭтаФорма);
		ИначеЕсли ВидОперации = Перечисления.ВидыОперацийУстановкаТиповЦенПоГруппамНоменклатурыДляПокупателей.ПоЦеновымГруппам Тогда
			ФормаПодбора = Справочники.ЦеновыеГруппы.ПолучитьФорму("ФормаВыбора", ЭтаФорма);
		КонецЕсли;

		ФормаПодбора.РежимВыбора = Истина;
		ФормаПодбора.ЗакрыватьПриВыборе = Ложь;
		ФормаПодбора.Открыть();
	КонецЕсли;

КонецПроцедуры // ДействиеПодбор()

// Процедура - обработчик события "ОбработкаВыбора" формы.
//
Процедура ОбработкаВыбора(ЗначениеВыбора, Источник)

	Если ТипЗнч(ЗначениеВыбора) = Тип("СправочникСсылка.ЦеновыеГруппы")
	 Или ТипЗнч(ЗначениеВыбора) = Тип("СправочникСсылка.НоменклатурныеГруппы") Тогда
		ОбработкаПодбора(НоменклатурныеЦеновыеГруппы, ЗначениеВыбора);
	КонецЕсли;

КонецПроцедуры //ОбработкаВыбора()

// Производит заполнение документа переданными из формы подбора данными.
//
// Параметры:
// ТабличнаяЧасть - ТЧ, в которую осуществляется подбор.
//  ЗначениеВыбора    - структура, содержащая параметры подбора.
//
Процедура ОбработкаПодбора(ТабличнаяЧасть, ЗначениеВыбора)Экспорт

	Если ТабличнаяЧасть = НоменклатурныеЦеновыеГруппы Тогда
		Если НЕ ЗначениеВыбора.ЭтоГруппа Тогда

			// Ищем выбранную позицию в таблице подобранных номенклатурных/ценовых групп.
			// Если не найдем - добавим новую строку.
			СтруктураОтбора = Новый Структура();
			СтруктураОтбора.Вставить("НоменклатурнаяЦеноваяГруппа", ЗначениеВыбора);

			СтрокаТабличнойЧасти = ОбработкаТабличныхЧастей.НайтиСтрокуТабЧасти(НоменклатурныеЦеновыеГруппы, СтруктураОтбора);
			Если СтрокаТабличнойЧасти = Неопределено Тогда

				// Надо добавить.
				НоваяСтрока = НоменклатурныеЦеновыеГруппы.Добавить();
				НоваяСтрока.НоменклатурнаяЦеноваяГруппа = ЗначениеВыбора;
			Иначе

				//Позиционируем.
				ЭлементыФормы.НоменклатурныеЦеновыеГруппы.ТекущаяСтрока = СтрокаТабличнойЧасти;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры // ОбработкаПодбора()

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ФОРМЫ

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ ОБРАБОТКИ СВОЙСТВ И КАТЕГОРИЙ

Процедура ДействияФормыРедактироватьНомер(Кнопка)
	МеханизмНумерацииОбъектов.ИзменениеВозможностиРедактированияНомера(ЭтотОбъект.Метаданные(), ЭтаФорма, ЭлементыФормы.ДействияФормы.Кнопки.Подменю1, ЭлементыФормы.Номер);
КонецПроцедуры
// Процедура выполняет открытие формы работы со свойствами документа.
//
Процедура ДействияФормыДействиеОткрытьСвойства(Кнопка)

	РаботаСДиалогами.ОткрытьСвойстваДокумента(ЭтотОбъект, ЭтаФорма);

КонецПроцедуры //ДействияФормыДействиеОткрытьСвойства()

// Процедура выполняет открытие формы работы с категориями документа.
//
Процедура ДействияФормыДействиеОткрытьКатегории(Кнопка)

	РаботаСДиалогами.ОткрытьКатегорииДокумента(ЭтотОбъект, ЭтаФорма);

КонецПроцедуры //ДействияФормыДействиеОткрытьКатегории()

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ДЕЙСТВИЯ КОМАНДНЫХ ПАНЕЛЕЙ ФОРМЫ

// Процедура вызывается при выборе пункта подменю "ПодменюВидаОперации" командной панели
// формы. Процедура устанавливает значение реквизита ВидОперации.
//
Процедура ДействияФормыДействиеУстановитьОперацию(Кнопка)

	Если Кнопка <> Неопределено Тогда // найти новое значение вида операции
		ВидОперации = Перечисления.ВидыОперацийУстановкаТиповЦенПоГруппамНоменклатурыДляПокупателей[Кнопка.Имя];
	КонецЕсли;

	Если мВидОперации <> ВидОперации Тогда
		Если НоменклатурныеЦеновыеГруппы.Количество() <> 0 Тогда
			Ответ = Вопрос("При изменении вида операции табличная часть будет очищена." +Символы.ПС + "Продолжить?", РежимДиалогаВопрос.ДаНет);

			Если Ответ = КодВозвратаДиалога.Да Тогда
				НоменклатурныеЦеновыеГруппы.Очистить();
				мВидОперации = ВидОперации;
			Иначе
				ВидОперации = мВидОперации;

				Возврат;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;

	// Отобразить в заголовке формы вид операции.
	РаботаСДиалогами.УстановитьЗаголовокФормыДокумента(Строка(ВидОперации), ЭтотОбъект, ЭтаФорма);

	ОграничитьТипГрупп();

КонецПроцедуры // ДействияФормыДействиеУстановитьОперацию()

// Процедура вызывается при выборе пункта подменю "Движения документа по регистрам" меню "Перейти".
// командной панели формы. Процедура отрабатывает печать движений документа по регистрам.
//
Процедура ДействияФормыДвиженияДокументаПоРегистрам(Кнопка)

	РаботаСДиалогами.НапечататьДвиженияДокумента(Ссылка);

КонецПроцедуры // ДействияФормыДвиженияДокументаПоРегистрам()

// Процедура вызова структуры подчиненности документа.
//
Процедура ДействияФормыСтруктураПодчиненностиДокумента(Кнопка)

	РаботаСДиалогами.ПоказатьСтруктуруПодчиненностиДокумента(Ссылка);

КонецПроцедуры //ДействияФормыСтруктураПодчиненностиДокумента()

// Процедура вызывается при начале редактирования строки табличной части.
//
Процедура НоменклатурныеЦеновыеГруппыПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)

	УстановитьТипЭлемента(ЭлементыФормы.НоменклатурныеЦеновыеГруппы.ТекущиеДанные.НоменклатурнаяЦеноваяГруппа, ПолучитьТипГруппы());

КонецПроцедуры // НоменклатурныеЦеновыеГруппыПриНачалеРедактирования()

// Процедура - обработчик события "Нажатие" кнопки "Подбор" в командной панели.
//
Процедура КоманднаяПанельСпискаНоменклатураПодбор(Кнопка)

	ДействиеПодбор(НоменклатурныеЦеновыеГруппы);

КонецПроцедуры //КоманднаяПанельСпискаНоменклатураПодбор()

// Процедура - обработчик нажатия на любую из дополнительных кнопок по заполнению ТЧ
//
Процедура НажатиеНаДополнительнуюКнопкуЗаполненияТЧ(Кнопка)
	
	УниверсальныеМеханизмы.ОбработатьНажатиеНаДополнительнуюКнопкуЗаполненияТЧ(мКнопкиЗаполненияТЧ.Строки.Найти(Кнопка.Имя,"Имя",Истина),ЭтотОбъект);
	
КонецПроцедуры

// Процедура - обработчик нажатия на кнопку "Печать".
// Открывает форму выбора печатных форм объекта.
//
Процедура ОсновныеДействияФормыПечать(Кнопка)
	
	УниверсальныеМеханизмы.ОткрытьФормуВыбораПечатныхФормОбъекта(ЭтотОбъект, ЭтаФорма);
	
КонецПроцедуры // ОсновныеДействияФормыПечать() 

// Процедура - обработчик нажатия на кнопку "Печать по умолчанию"
//
Процедура ОсновныеДействияФормыПечатьПоУмолчанию(Кнопка)

	УниверсальныеМеханизмы.НапечататьДокументПоУмолчанию(ЭтотОбъект);

КонецПроцедуры
 


////////////////////////////////////////////////////////////////////////////////
// ОПЕРАТОРЫ ОСНОВНОЙ ПРОГРАММЫ

мКолонкиНоменклатурныеЦеновыеГруппы = ЭлементыФормы.НоменклатурныеЦеновыеГруппы.Колонки;