////////////////////////////////////////////////////////////////////////////////
// ВСПОМОГАТЕЛЬНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ
//

#Если ТолстыйКлиентОбычноеПриложение Тогда

// Формирует печатную форму по документу
//
// Параметры
//  нет
//
// Возвращаемое значение:
//   сформированный табличный документ
//
Функция ПечатьКарточки()

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("Физлицо", Физлицо);
	Запрос.УстановитьПараметр("Дата", Дата);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ЕСТЬNULL(ФИОФизлица.Фамилия + "" "" + ФИОФизлица.Имя + "" "" + ФИОФизлица.Отчество, ИсполнительныйЛист.Физлицо.Наименование) КАК ФИОРаботника,
	|	ИсполнительныйЛист.Дата,
	|	ИсполнительныйЛист.Номер,
	|	ИсполнительныйЛист.Размер,
	|	ИсполнительныйЛист.Предел,
	|	ВЫБОР
	|		КОГДА ИсполнительныйЛист.СпособРасчетаУдержания = ЗНАЧЕНИЕ(Перечисление.СпособыРасчетаУдержанийПоИЛ.ПроцентомОтЗаработка)
	|				ИЛИ ИсполнительныйЛист.СпособРасчетаУдержания = ЗНАЧЕНИЕ(Перечисление.СпособыРасчетаУдержанийПоИЛ.ПроцентомОтЗаработкаСУчетомБольничныхЛистов)
	|			ТОГДА "" %""
	|		КОГДА ИсполнительныйЛист.СпособРасчетаУдержания = ЗНАЧЕНИЕ(Перечисление.СпособыРасчетаУдержанийПоИЛ.ФиксированнойСуммой)
	|			ТОГДА "" руб.""
	|		КОГДА ИсполнительныйЛист.СпособРасчетаУдержания = ЗНАЧЕНИЕ(Перечисление.СпособыРасчетаУдержанийПоИЛ.КратноВеличинеПрожиточногоМинимума)
	|			ТОГДА "" прож.миним.""
	|		ИНАЧЕ "" руб.""
	|	КОНЕЦ КАК ПорядокРасчета,
	|	ИсполнительныйЛист.ВидИсполнительногоДокумента,
	|	ИсполнительныйЛист.РеквизитыИсполнительногоДокумента,
	|	ВЫРАЗИТЬ(ИсполнительныйЛист.Получатель.НаименованиеПолное КАК СТРОКА(300)) КАК Получатель,
	|	ЕСТЬNULL(ФактАдресФизЛица.Представление, ""<адрес не указан>"") КАК ФактАдресФизЛица,
	|	ЕСТЬNULL(ФактическийАдресПолучателя.Представление, ""<адрес не указан>"") КАК ФактическийАдресПолучателя
	|ИЗ
	|	Документ.ИсполнительныйЛист КАК ИсполнительныйЛист
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ФИОФизЛиц.СрезПоследних(&Дата, Физлицо = &ФизЛицо) КАК ФИОФизлица
	|		ПО (ИСТИНА)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КонтактнаяИнформация КАК ФактическийАдресПолучателя
	|		ПО ИсполнительныйЛист.Получатель = ФактическийАдресПолучателя.Объект
	|			И (ФактическийАдресПолучателя.Тип = ЗНАЧЕНИЕ(Перечисление.ТипыКонтактнойИнформации.Адрес))
	|			И (ФактическийАдресПолучателя.Вид = ЗНАЧЕНИЕ(Справочник.ВидыКонтактнойИнформации.ФактАдресКонтрагента))
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КонтактнаяИнформация КАК ФактАдресФизЛица
	|		ПО ИсполнительныйЛист.Физлицо = ФактАдресФизЛица.Объект
	|			И (ФактАдресФизЛица.Тип = ЗНАЧЕНИЕ(Перечисление.ТипыКонтактнойИнформации.Адрес))
	|			И (ФактАдресФизЛица.Вид = ЗНАЧЕНИЕ(Справочник.ВидыКонтактнойИнформации.ФактАдресФизЛица))
	|ГДЕ
	|	ИсполнительныйЛист.Ссылка = &Ссылка";
	
	ТабДокумент = Новый ТабличныйДокумент;
	ТабДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Портрет;
	ТабДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_ИсполнительныйЛист_КарточкаУчета";
	
	Макет = ПолучитьМакет("Карточка");
	СекцияКарточка = Макет.ПолучитьОбласть("Карточка");
	Выборка = Запрос.Выполнить().Выбрать();
	Выборка.Следующий();
    СекцияКарточка.Параметры.Заполнить(Выборка);
	СекцияКарточка.Параметры.Дата = Формат(СекцияКарточка.Параметры.Дата,"ДФ='ММММ гггг ""г.""'");
	СекцияКарточка.Параметры.Получатель = СокрЛП(СекцияКарточка.Параметры.Получатель);
	ТабДокумент.Вывести(СекцияКарточка);
	
	Возврат	ТабДокумент
	
КонецФункции // ПечатьКарточки()

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

// Процедура осуществляет печать документа. Можно направить печать на 
// экран или принтер, а также распечатать необходимое количество копий.
//
//  Название макета печати передается в качестве параметра,
// по переданному названию находим имя макета в соответствии.
//
// Параметры:
//  НазваниеМакета - строка, название макета.
//
Функция Печать(ИмяМакета, КоличествоЭкземпляров = 1, НаПринтер = Ложь) Экспорт
	
	// Получить экземпляр документа на печать
	Если ИмяМакета = "Карточка" Тогда
		Возврат УниверсальныеМеханизмы.НапечататьДокумент(ПечатьКарточки(), КоличествоЭкземпляров, НаПринтер, ОбщегоНазначения.СформироватьЗаголовокДокумента(ЭтотОбъект,Метаданные().Синоним) + " (карточка учета)");
	КонецЕсли;
	
КонецФункции // Печать

#КонецЕсли

// Возвращает доступные варианты печати документа
//
// Возвращаемое значение:
//  Структура, каждая строка которой соответствует одному из вариантов печати
//  
Функция ПолучитьСтруктуруПечатныхФорм() Экспорт
	
	Возврат Новый Структура("Карточка", "Карточка учета");
	
КонецФункции // ПолучитьСтруктуруПечатныхФорм()

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ДЛЯ ОБЕСПЕЧЕНИЯ ПРОВЕДЕНИЯ ДОКУМЕНТА

// Формирует запрос по шапке документа
//
// Параметры: 
//  Режим - режим проведения
//
// Возвращаемое значение:
//  Результат запроса
//
Функция СформироватьЗапросПоШапке(Режим)

	Запрос = Новый Запрос;

	// Установим параметры запроса
	Запрос.УстановитьПараметр("ДокументСсылка", Ссылка);
	Запрос.УстановитьПараметр("ИЛВПрожиточныхМинимумах", ОбщегоНазначенияЗК.ОсобыйЭлемент(ПланыВидовРасчета.УдержанияОрганизаций, "ИЛВПрожиточныхМинимумах"));
	Запрос.УстановитьПараметр("БанковскиеИздержкиПоИЛ", ОбщегоНазначенияЗК.ОсобыйЭлемент(ПланыВидовРасчета.УдержанияОрганизаций, "БанковскиеИздержкиПоИЛ"));

	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ИсполнительныйЛист.Дата,
	|	ИсполнительныйЛист.Организация,
	|	ВЫБОР
	|		КОГДА ИсполнительныйЛист.Организация.ГоловнаяОрганизация = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|			ТОГДА ИсполнительныйЛист.Организация
	|		ИНАЧЕ ИсполнительныйЛист.Организация.ГоловнаяОрганизация
	|	КОНЕЦ КАК ГоловнаяОрганизация,
	|	ИсполнительныйЛист.Физлицо,
	|	ИсполнительныйЛист.ДатаДействия,
	|	ИсполнительныйЛист.ДатаОкончания,
	|	ИсполнительныйЛист.Размер,
	|	ИсполнительныйЛист.Предел,
	|	ИсполнительныйЛист.СпособРасчетаУдержания,
	|	ИсполнительныйЛист.Получатель,
	|	ИсполнительныйЛист.Ссылка,
	|	ИсполнительныйЛист.ПроцентПочтовогоСбора,
	|	ИсполнительныйЛист.СпособПеречисленияПоИсполнительномуЛисту,
	|	ИсполнительныйЛист.ТарифБанкаНаДенежныеПереводы,
	|	ИсполнительныйЛист.ПочтовыйСборРассчитыватьПроцентом,
	|	ИсполнительныйЛист.Тариф,
	|	ИсполнительныйЛист.ПрожиточныйМинимум,
	|	ВЫБОР
	|		КОГДА ИсполнительныйЛист.СпособРасчетаУдержания = ЗНАЧЕНИЕ(Перечисление.СпособыРасчетаУдержанийПоИЛ.ПроцентомОтЗаработкаСУчетомБольничныхЛистов)
	|				И ИсполнительныйЛист.Предел = 0
	|			ТОГДА ЗНАЧЕНИЕ(ПланВидовРасчета.УдержанияОрганизаций.ИЛПроцентом)
	|		КОГДА ИсполнительныйЛист.СпособРасчетаУдержания = ЗНАЧЕНИЕ(Перечисление.СпособыРасчетаУдержанийПоИЛ.ПроцентомОтЗаработкаСУчетомБольничныхЛистов)
	|				И ИсполнительныйЛист.Предел > 0
	|			ТОГДА ЗНАЧЕНИЕ(ПланВидовРасчета.УдержанияОрганизаций.ИЛПроцентомДоПредела)
	|		КОГДА ИсполнительныйЛист.СпособРасчетаУдержания = ЗНАЧЕНИЕ(Перечисление.СпособыРасчетаУдержанийПоИЛ.ПроцентомОтЗаработка)
	|				И ИсполнительныйЛист.Предел = 0
	|			ТОГДА ЗНАЧЕНИЕ(ПланВидовРасчета.УдержанияОрганизаций.ИЛПроцентомБезБЛ)
	|		КОГДА ИсполнительныйЛист.СпособРасчетаУдержания = ЗНАЧЕНИЕ(Перечисление.СпособыРасчетаУдержанийПоИЛ.ПроцентомОтЗаработка)
	|				И ИсполнительныйЛист.Предел > 0
	|			ТОГДА ЗНАЧЕНИЕ(ПланВидовРасчета.УдержанияОрганизаций.ИЛПроцентомДоПределаБезБЛ)
	|		КОГДА ИсполнительныйЛист.СпособРасчетаУдержания = ЗНАЧЕНИЕ(Перечисление.СпособыРасчетаУдержанийПоИЛ.ФиксированнойСуммой)
	|				И ИсполнительныйЛист.Предел = 0
	|			ТОГДА ЗНАЧЕНИЕ(ПланВидовРасчета.УдержанияОрганизаций.ИЛФиксированнойСуммой)
	|		КОГДА ИсполнительныйЛист.СпособРасчетаУдержания = ЗНАЧЕНИЕ(Перечисление.СпособыРасчетаУдержанийПоИЛ.ФиксированнойСуммой)
	|				И ИсполнительныйЛист.Предел > 0
	|			ТОГДА ЗНАЧЕНИЕ(ПланВидовРасчета.УдержанияОрганизаций.ИЛФиксированнойСуммойДоПредела)
	|		КОГДА ИсполнительныйЛист.СпособРасчетаУдержания = ЗНАЧЕНИЕ(Перечисление.СпособыРасчетаУдержанийПоИЛ.КратноВеличинеПрожиточногоМинимума)
	|			ТОГДА &ИЛВПрожиточныхМинимумах
	|	КОНЕЦ КАК ВидРасчетаУдержания,
	|	ВЫБОР
	|		КОГДА ИсполнительныйЛист.СпособПеречисленияПоИсполнительномуЛисту = ЗНАЧЕНИЕ(Перечисление.СпособыПеречисленийПоИсполнительномуЛисту.БанковскийПеревод)
	|			ТОГДА &БанковскиеИздержкиПоИЛ
	|		ИНАЧЕ ЗНАЧЕНИЕ(ПланВидовРасчета.УдержанияОрганизаций.ПочтовыйСборПоИЛ)
	|	КОНЕЦ КАК ВидРасчетаИздержек,
	|	ВЫБОР
	|		КОГДА ИсполнительныйЛист.СпособПеречисленияПоИсполнительномуЛисту = ЗНАЧЕНИЕ(Перечисление.СпособыПеречисленийПоИсполнительномуЛисту.ПочтовыйПеревод)
	|				И ИсполнительныйЛист.ПочтовыйСборРассчитыватьПроцентом
	|			ТОГДА ИсполнительныйЛист.ПроцентПочтовогоСбора
	|		КОГДА ИсполнительныйЛист.СпособПеречисленияПоИсполнительномуЛисту = ЗНАЧЕНИЕ(Перечисление.СпособыПеречисленийПоИсполнительномуЛисту.ПочтовыйПеревод)
	|				И (НЕ ИсполнительныйЛист.ПочтовыйСборРассчитыватьПроцентом)
	|			ТОГДА ИсполнительныйЛист.Тариф
	|		КОГДА ИсполнительныйЛист.СпособПеречисленияПоИсполнительномуЛисту = ЗНАЧЕНИЕ(Перечисление.СпособыПеречисленийПоИсполнительномуЛисту.БанковскийПеревод)
	|			ТОГДА ИсполнительныйЛист.ТарифБанкаНаДенежныеПереводы
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ПорядокИсчисленияИздержек
	|ИЗ
	|	Документ.ИсполнительныйЛист КАК ИсполнительныйЛист
	|ГДЕ
	|	ИсполнительныйЛист.Ссылка = &ДокументСсылка";

	Возврат Запрос.Выполнить();

КонецФункции // СформироватьЗапросПоШапке()

// Проверяет правильность заполнения шапки документа.
// Если какой-то из реквизитов шапки, влияющий на проведение не заполнен или
// заполнен не корректно, то выставляется флаг отказа в проведении.
// Проверка выполняется по выборке из результата запроса по шапке,
// все проверяемые реквизиты должны быть включены в выборку по шапке.
//
// Параметры: 
//  ВыборкаПоШапкеДокумента	- выборка из результата запроса по шапке документа,
//  Отказ 					- флаг отказа в проведении.
//	Заголовок				- Заголовок для сообщений об ошибках проведения
//
Процедура ПроверитьЗаполнениеШапки(ВыборкаПоШапкеДокумента, Отказ, Заголовок)

	Если НЕ ЗначениеЗаполнено(ВыборкаПоШапкеДокумента.Организация) Тогда
		ОбщегоНазначения.ВывестиИнформациюОбОшибке(ОбщегоНазначения.ПреобразоватьСтрокуИнтерфейса("Не указана организация!"), Отказ, Заголовок);
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ВыборкаПоШапкеДокумента.Физлицо) Тогда
		ОбщегоНазначения.ВывестиИнформациюОбОшибке("Не указан сотрудник!", Отказ, Заголовок);
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ВыборкаПоШапкеДокумента.ДатаДействия) Тогда
		ОбщегоНазначения.ВывестиИнформациюОбОшибке("Не указана дата действия удержания!", Отказ, Заголовок);
	КонецЕсли;
	
	// Получатель
	Если НЕ ЗначениеЗаполнено(ВыборкаПоШапкеДокумента.Получатель) Тогда
		ОбщегоНазначения.ВывестиИнформациюОбОшибке("Не указан получатель!", Отказ, Заголовок);
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ВыборкаПоШапкеДокумента.СпособРасчетаУдержания) Тогда
		ОбщегоНазначения.ВывестиИнформациюОбОшибке("Не указан способ расчета сумм удержания!", Отказ, Заголовок);
	ИначеЕсли ВыборкаПоШапкеДокумента.СпособРасчетаУдержания = Перечисления.СпособыРасчетаУдержанийПоИЛ.КратноВеличинеПрожиточногоМинимума И Не ЗначениеЗаполнено(ВыборкаПоШапкеДокумента.ПрожиточныйМинимум) Тогда
		ОбщегоНазначения.ВывестиИнформациюОбОшибке("Не указан прожиточный минимум для расчета сумм удержания!", Отказ, Заголовок);
	КонецЕсли;
	
	//Проверка почтового сбора
	Если ВыборкаПоШапкеДокумента.СпособПеречисленияПоИсполнительномуЛисту = Перечисления.СпособыПеречисленийПоИсполнительномуЛисту.ПочтовыйПеревод Тогда
		Если ВыборкаПоШапкеДокумента.ПочтовыйСборРассчитыватьПроцентом И ВыборкаПоШапкеДокумента.ПроцентПочтовогоСбора = 0 Тогда
			ОбщегоНазначения.ВывестиИнформациюОбОшибке(ОбщегоНазначения.ПреобразоватьСтрокуИнтерфейса("Не задан процент почтового сбора!"), Отказ, Заголовок);
		КонецЕсли;
		Если НЕ ВыборкаПоШапкеДокумента.ПочтовыйСборРассчитыватьПроцентом И НЕ ЗначениеЗаполнено(ВыборкаПоШапкеДокумента.Тариф) Тогда
			ОбщегоНазначения.ВывестиИнформациюОбОшибке(ОбщегоНазначения.ПреобразоватьСтрокуИнтерфейса("Не задан тариф почтового сбора!"), Отказ, Заголовок);
		КонецЕсли;
	ИначеЕсли ВыборкаПоШапкеДокумента.СпособПеречисленияПоИсполнительномуЛисту = Перечисления.СпособыПеречисленийПоИсполнительномуЛисту.БанковскийПеревод Тогда
		Если НЕ ЗначениеЗаполнено(ВыборкаПоШапкеДокумента.ТарифБанкаНаДенежныеПереводы) Тогда
			ОбщегоНазначения.ВывестиИнформациюОбОшибке(ОбщегоНазначения.ПреобразоватьСтрокуИнтерфейса("Не задан тариф банка на денежные переводы!"), Отказ, Заголовок);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры // ПроверитьЗаполнениеШапки()

// По строке выборки результата запроса по документу формируем движения по регистрам
//
// Параметры: 
//	ВыборкаПоШапкеДокумента		- выборка из результата запроса по шапке документа,
//
// Возвращаемое значение:
//	Нет.
//
Процедура ДобавитьСтрокуВДвиженияПоРегистрамСведений(ВыборкаПоШапкеДокумента)

	// Движения регистра ПлановыеУдержанияРаботниковОрганизаций
	
	Движение = Движения.ПлановыеУдержанияРаботниковОрганизаций.Добавить();
	// свойства
	Движение.Период					= ВыборкаПоШапкеДокумента.ДатаДействия;

	// измерения
	Движение.Организация			= ВыборкаПоШапкеДокумента.ГоловнаяОрганизация;
	Движение.ФизЛицо				= ВыборкаПоШапкеДокумента.ФизЛицо;
	Движение.ВидРасчета				= ВыборкаПоШапкеДокумента.ВидРасчетаУдержания;
	Движение.ДокументОснование		= ВыборкаПоШапкеДокумента.Ссылка;
	
	// ресурсы
	Движение.Действие				= Перечисления.ВидыДействияСНачислением.Начать;
	Движение.Показатель1			= ВыборкаПоШапкеДокумента.Размер;
	
	Если ЗначениеЗаполнено(ВыборкаПоШапкеДокумента.ДатаОкончания) Тогда
		Движение.ПериодЗавершения	= ВыборкаПоШапкеДокумента.ДатаОкончания + 86400;
		Движение.ДействиеЗавершения	= Перечисления.ВидыДействияСНачислением.Прекратить;
		// не заполняются
		//Показатель1Завершения
	КонецЕсли;
	
	// реквизиты
	Движение.Получатель				= ВыборкаПоШапкеДокумента.Получатель;
	Если ВыборкаПоШапкеДокумента.СпособРасчетаУдержания = Перечисления.СпособыРасчетаУдержанийПоИЛ.КратноВеличинеПрожиточногоМинимума Тогда // в долях прожиточных минимумов
		Движение.ПрожиточныйМинимум = ВыборкаПоШапкеДокумента.ПрожиточныйМинимум;
	КонецЕсли;
	Если ВыборкаПоШапкеДокумента.СпособПеречисленияПоИсполнительномуЛисту <> Перечисления.СпособыПеречисленийПоИсполнительномуЛисту.ЧерезКассуПлатежнымПоручением Тогда
		Движение.ПорядокИсчисленияИздержек	= ВыборкаПоШапкеДокумента.ПорядокИсчисленияИздержек;
	КонецЕсли;
	
	// строка издержек
	Если ВыборкаПоШапкеДокумента.СпособПеречисленияПоИсполнительномуЛисту <> Перечисления.СпособыПеречисленийПоИсполнительномуЛисту.ЧерезКассуПлатежнымПоручением Тогда
		
		Движение = Движения.ПлановыеУдержанияРаботниковОрганизаций.Добавить();
		// свойства
		Движение.Период					= ВыборкаПоШапкеДокумента.ДатаДействия;

		// измерения
		Движение.Организация			= ВыборкаПоШапкеДокумента.ГоловнаяОрганизация;
		Движение.ФизЛицо				= ВыборкаПоШапкеДокумента.ФизЛицо;
		Движение.ДокументОснование		= ВыборкаПоШапкеДокумента.Ссылка;
  		Движение.ВидРасчета				= ВыборкаПоШапкеДокумента.ВидРасчетаИздержек;
		
		// ресурсы
		Движение.Действие				= Перечисления.ВидыДействияСНачислением.Начать;
		Если ВыборкаПоШапкеДокумента.СпособПеречисленияПоИсполнительномуЛисту = Перечисления.СпособыПеречисленийПоИсполнительномуЛисту.ПочтовыйПеревод И ВыборкаПоШапкеДокумента.ПочтовыйСборРассчитыватьПроцентом Тогда
			Движение.Показатель1			= ВыборкаПоШапкеДокумента.ПроцентПочтовогоСбора;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ВыборкаПоШапкеДокумента.ДатаОкончания) Тогда
			Движение.ПериодЗавершения	= ВыборкаПоШапкеДокумента.ДатаОкончания + 86400;
			Движение.ДействиеЗавершения	= Перечисления.ВидыДействияСНачислением.Прекратить;
			// не заполняются
			//Показатель1Завершения
		КонецЕсли;
		
		// реквизиты
		Движение.Получатель				= ВыборкаПоШапкеДокумента.Получатель;
		Движение.ПорядокИсчисленияИздержек	= ВыборкаПоШапкеДокумента.ПорядокИсчисленияИздержек;
		
	КонецЕсли;
	
КонецПроцедуры // ДобавитьСтрокуВДвиженияПоРегистрамСведений

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

Процедура ОбработкаПроведения(Отказ, Режим)
	
	// Если документ перенесен - движения не делаем
	Если ДанныеПрошлойВерсии Тогда
		Возврат;
	КонецЕсли;
	
	ОбработкаКомментариев = глЗначениеПеременной("глОбработкаСообщений");
	ОбработкаКомментариев.УдалитьСообщения();
	
	// Заголовок для сообщений об ошибках проведения.
	Заголовок = ОбщегоНазначения.ПредставлениеДокументаПриПроведении(Ссылка);
	
	РезультатЗапросаПоШапке = СформироватьЗапросПоШапке(Режим);

	// Получим реквизиты шапки из запроса
	ВыборкаПоШапкеДокумента = РезультатЗапросаПоШапке.Выбрать();

	Если ВыборкаПоШапкеДокумента.Следующий() Тогда

		//Надо позвать проверку заполнения реквизитов шапки
		ПроверитьЗаполнениеШапки(ВыборкаПоШапкеДокумента, Отказ, Заголовок);

		// Движения стоит добавлять, если в проведении еще не отказано (отказ =ложь)
		Если НЕ Отказ Тогда

			Если НЕ Отказ Тогда
				// Заполним записи в наборах записей регистров
				ДобавитьСтрокуВДвиженияПоРегистрамСведений(ВыборкаПоШапкеДокумента);
			КонецЕсли;
			
		КонецЕсли;

	КонецЕсли;

	ОбработкаКомментариев.ПоказатьСообщения();
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если СпособРасчетаУдержания = Перечисления.СпособыРасчетаУдержанийПоИЛ.КратноВеличинеПрожиточногоМинимума Тогда
		Предел = 0
	КонецЕсли;
	
КонецПроцедуры