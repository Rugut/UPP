////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

// Обработчик события ПриОткрытии формы.
//
Процедура ПриОткрытии()

	Макет = Справочники.ДокументыУдостоверяющиеЛичность.ПолучитьМакет("КлассификаторИМНС");

	Макет.Параметры.Расшифровка = Истина; // чтобы работала расшифровка

	ТабличныйДокумент = ЭлементыФормы.ПолеТабличногоДокумента;
	
	ТабличныйДокумент.Очистить();
	ТабличныйДокумент.Вывести(Макет);

	ТабличныйДокумент.ФиксацияСверху      = 3;

	ТабличныйДокумент.ОтображатьЗаголовки = Ложь;
	ТабличныйДокумент.ОтображатьСетку     = Ложь;
	ТабличныйДокумент.ТолькоПросмотр      = Истина;

КонецПроцедуры

// Обработчик события ОбработкаРасшифровки элемента ПолеТабличногоДокумента.
//
Процедура ПолеТабличногоДокументаОбработкаРасшифровки(Элемент, Расшифровка, СтандартнаяОбработка)

	СтандартнаяОбработка = Ложь;

	// Получение значений полей выбранной строки.
	ТабличныйДокумент = ЭлементыФормы.ПолеТабличногоДокумента;
	ТекущаяОбласть    = ТабличныйДокумент.ТекущаяОбласть;

	ОбластьКодИМНС      = ТабличныйДокумент.Области.КодИМНС;
	ОбластьКодПФР       = ТабличныйДокумент.Области.КодПФР;
	ОбластьНаименование = ТабличныйДокумент.Области.Наименование;
    ОбластьПримечание   = ТабличныйДокумент.Области.Примечание;
	
	Если ТекущаяОбласть.Низ = ТекущаяОбласть.Верх Тогда
	
		КодИМНС      = ТабличныйДокумент.Область(ТекущаяОбласть.Верх, ОбластьКодИМНС.        Лево, ТекущаяОбласть.Низ, ОбластьКодИМНС.        Право).Текст;
		КодПФР       = ТабличныйДокумент.Область(ТекущаяОбласть.Верх, ОбластьКодПФР.        Лево, ТекущаяОбласть.Низ, ОбластьКодПФР.        Право).Текст;
		Наименование = ТабличныйДокумент.Область(ТекущаяОбласть.Верх, ОбластьНаименование.Лево, ТекущаяОбласть.Низ, ОбластьНаименование.Право).Текст;
	    Примечание   = ТабличныйДокумент.Область(ТекущаяОбласть.Верх, ОбластьПримечание. Лево, ТекущаяОбласть.Низ, ОбластьПримечание. Право).Текст;
		
		//Проверка наличия выбранного элемента.
		Ссылка = Справочники.ДокументыУдостоверяющиеЛичность.НайтиПоНаименованию(Наименование);
		
		Если НЕ Ссылка.Пустая() Тогда
			
			Вопрос = "В справочнике ""Документы, удостоверяющие личность"" уже существует элемент с наименованием """ + Наименование + """! Открыть существующий?";
			Ответ  = Вопрос(Вопрос, РежимДиалогаВопрос.ДаНетОтмена, , КодВозвратаДиалога.Отмена, );
			
			Если Ответ = КодВозвратаДиалога.Да Тогда
				
				Ссылка.ПолучитьФорму( , ВладелецФормы, ).Открыть();
				Возврат;

			ИначеЕсли Ответ = КодВозвратаДиалога.Отмена Тогда
				Возврат;
			КонецЕсли;
			
		КонецЕсли;

		// Создание нового элемента справочника.
		ФормаНовогоЭлемента = Справочники.ДокументыУдостоверяющиеЛичность.ПолучитьФормуНовогоЭлемента(, ВладелецФормы, );
		ФормаНовогоЭлемента.Наименование = Наименование;
		ФормаНовогоЭлемента.КодИМНС      = КодИМНС;
		ФормаНовогоЭлемента.КодПФР       = КодПФР;
		ФормаНовогоЭлемента.Открыть();
		
	Иначе
		
		Форма = ПолучитьОбщуюФорму("ФормаПодбораИзКлассификатора", ЭтаФорма);
		Если Форма.Открыта() Тогда
			Форма.СписокВыбора.Очистить();
		Иначе			
			СтруктураКолонок = Новый Структура();
			СтруктураКолонок.Вставить("КодИМНС", Новый Структура("Заголовок, Ширина", "Код ИМНС", 5));
			СтруктураКолонок.Вставить("КодПФР", Новый Структура("Заголовок, Ширина", "Код ПФР"));	
			СтруктураКолонок.Вставить("Наименование", Новый Структура("Заголовок, Ширина", "Наименование документа"));
			СтруктураКолонок.Вставить("Видимость", Новый Структура("Заголовок, Ширина", ""));	
			
			Форма.ТипСправочника = "ДокументыУдостоверяющиеЛичность";
			Форма.НастроитьФорму(СтруктураКолонок);
		КонецЕсли;	
		
		СписокВыбора =  Форма.СписокВыбора;		
		
		Для ТекущаяСтрока = ТекущаяОбласть.Верх по ТекущаяОбласть.Низ Цикл
			
			КодИМНС      = ТабличныйДокумент.Область(ТекущаяСтрока, ОбластьКодИМНС.Лево, 	  ТекущаяСтрока, ОбластьКодИМНС.Право).Текст;
			КодПФР       = ТабличныйДокумент.Область(ТекущаяСтрока, ОбластьКодПФР.Лево, 	  ТекущаяСтрока, ОбластьКодПФР.Право).Текст;
			Наименование = ТабличныйДокумент.Область(ТекущаяСтрока, ОбластьНаименование.Лево, ТекущаяСтрока, ОбластьНаименование.Право).Текст;

			
			ЭтоЧисло = Истина;
			Попытка
				КодКакЧисло = Число(КодИМНС);
			Исключение
				ЭтоЧисло = Ложь;
			КонецПопытки;
			
			Если ЭтоЧисло Тогда
				СтрокаПодбора = СписокВыбора.Добавить();
				СтрокаПодбора.КодИМНС = КодИМНС;				
				СтрокаПодбора.Наименование = Наименование;
				СтрокаПодбора.КодПФР = КодПФР;
				СтрокаПодбора.Видимость = Справочники[Форма.ТипСправочника].НайтиПоНаименованию(Наименование).Пустая();
				СтрокаПодбора.Переносить = СтрокаПодбора.Видимость;
			КонецЕсли;
			
		КонецЦикла;
		
		Форма.Открыть();
	КонецЕсли;
	
КонецПроцедуры