//Процедура выполняет первоначальное заполнение настроек 
//- при вводе нового элемента справочника
//- при изменении варианта настройки
Процедура ЗаполнитьСписокРегламентныхОпераций(ОбъектКопирования = Неопределено, Использовать = Истина) Экспорт
	
	ТаблицаРеглОпераций = РегламентныеОперации.Выгрузить();
	ТаблицаРеглОпераций.Очистить();
	ТаблицаРеглОпераций.Колонки.Добавить("Порядок");
	
	ОбрабатываемыеБизнесПроцессы = Новый Массив();
	Если БизнесПроцессЗакрытиеМесяца.ИспользоватьБизнесПроцессУСН(,ЭтотОбъект) Тогда
		ОбрабатываемыеБизнесПроцессы.Добавить("ЗакрытиеМесяцаУСНДоходыМинусРасходы");
	Иначе
		ОбрабатываемыеБизнесПроцессы.Добавить("ЗакрытиеМесяца");
		Если ВариантНастройки = 0 Тогда
			ОбрабатываемыеБизнесПроцессы.Добавить("РасчетНДС");
		КонецЕсли;
	КонецЕсли;
	
	ТочкиМаршрута = БизнесПроцессЗакрытиеМесяца.ПолучитьТочкиМаршрутаЗакрытияМесяцаИРасчетаНДС(ОбрабатываемыеБизнесПроцессы);
	
	Макет = ПолучитьМакет("МакетНастроекПоУмолчанию");
	ОбластьСписокНастроек = Макет.Области.Найти("Список");
	
	Если ОбластьСписокНастроек <> Неопределено Тогда
		
		ОбластьСписокНастроек = Макет.ПолучитьОбласть("Список");
		
		Для Каждого ТочкаМаршрута ИЗ ТочкиМаршрута Цикл
			
			ОбластьТочкаМаршрута = ОбластьСписокНастроек.НайтиТекст(ТочкаМаршрута.Имя,,,Истина,Истина,,Истина);
			Если ОбластьТочкаМаршрута = Неопределено Тогда
				Продолжить;
			КонецЕсли;
			
			ИмяОбласти_Начало = Лев(ОбластьТочкаМаршрута.Имя,СтрДлина(ОбластьТочкаМаршрута.Имя)-1);
			УУ = Булево(Число(СокрЛП(ОбластьСписокНастроек.Область(ИмяОбласти_Начало+"2").Текст)));
			БУ = Булево(Число(СокрЛП(ОбластьСписокНастроек.Область(ИмяОбласти_Начало+"3").Текст)));
			НУ = Булево(Число(СокрЛП(ОбластьСписокНастроек.Область(ИмяОбласти_Начало+"4").Текст)));

			НоваяСтрока = ТаблицаРеглОпераций.Добавить();
			НоваяСтрока.РегламентнаяОперация = ТочкаМаршрута;
			НоваяСтрока.ОтражатьВУправленческомУчете = УУ;
			НоваяСтрока.ОтражатьВБухгалтерскомУчете = БУ;
			НоваяСтрока.ОтражатьВНалоговомУчете = НУ;
			НоваяСтрока.Использовать = Использовать;
			НоваяСтрока.Порядок = ОбластьТочкаМаршрута.Верх;
			
			Если ОбъектКопирования <> Неопределено Тогда
				
				СтрокаОбъектаКопирования = Неопределено;
				
				Для Каждого ТекСтрокаОбъектаКопирования ИЗ ОбъектКопирования.РегламентныеОперации Цикл
					Если ЗначениеЗаполнено(ТекСтрокаОбъектаКопирования.РегламентнаяОперация) И 
						ТекСтрокаОбъектаКопирования.РегламентнаяОперация.Имя = ТочкаМаршрута.Имя Тогда
						СтрокаОбъектаКопирования = ТекСтрокаОбъектаКопирования;
						Прервать;
					КонецЕсли;
				КонецЦикла;
				
				Если СтрокаОбъектаКопирования <> Неопределено Тогда
					НоваяСтрока.Использовать 	= СтрокаОбъектаКопирования.Использовать;
					НоваяСтрока.Ответственный 	= СтрокаОбъектаКопирования.Ответственный;
				КонецЕсли;
				
			КонецЕсли;
			
			//Особый случай: регламентная операция "Проведение по партиям" в режиме "УСН (доходы - расходы)" нужна независимо от применения РА
			Если ВариантНастройки = 2 И НРег(ТочкаМаршрута.Имя) = НРег("ПровестиПоПартиям") Тогда
				НоваяСтрока.Использовать = Истина;
			КонецЕсли;
			
		КонецЦикла;
		
		ТаблицаРеглОпераций.Сортировать("Порядок");
		РегламентныеОперации.Очистить();
		РегламентныеОперации.Загрузить(ТаблицаРеглОпераций);
		ОтключитьНедоступныеРегламентныеОперации();
		
	КонецЕсли; 
	
КонецПроцедуры

//Процедура отключает регламентные операции, которые являются недоступными
//
Процедура ОтключитьНедоступныеРегламентныеОперации(МассивНедоступныхРегламентныхОпераций = Неопределено) Экспорт
	
	Если МассивНедоступныхРегламентныхОпераций = Неопределено Тогда
		МассивНедоступныхРегламентныхОпераций = БизнесПроцессЗакрытиеМесяца.ЗаполнитьМассивНедоступныхРегламентныхОпераций(?(ЗначениеЗаполнено(ДатаНачалаДействияНастройки),ДатаНачалаДействияНастройки,ТекущаяДата()),,ВариантНастройки);
	КонецЕсли;
	Если МассивНедоступныхРегламентныхОпераций.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого СтрокаТЧ из РегламентныеОперации Цикл
		Если НЕ СтрокаТЧ.Использовать Тогда
			Продолжить;
		КонецЕсли;
		Если БизнесПроцессЗакрытиеМесяца.РегламентнаяОперацияНедоступна(СтрокаТЧ.РегламентнаяОперация, МассивНедоступныхРегламентныхОпераций) Тогда
			СтрокаТЧ.Использовать = Ложь;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры


Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка ИЛИ ЭтоГруппа Тогда
		Возврат;
	КонецЕсли;
	
	Если ДатаНачалаДействияНастройки = Дата('00010101000000') Тогда
		ОбщегоНазначения.СообщитьОбОшибке("Не указана дата начала действия настройки", Отказ);
	КонецЕсли;
	Если НЕ (ОтражатьВУправленческомУчете ИЛИ ОтражатьВБухгалтерскомУчете ИЛИ ОтражатьВНалоговомУчете) Тогда
		ОбщегоНазначения.СообщитьОбОшибке("Не установлены признаки отражения в учете", Отказ);
	КонецЕсли;
	
	Строка = РегламентныеОперации.Найти(Истина, "Использовать");
	Если Строка = Неопределено Тогда
		ОбщегоНазначения.СообщитьОбОшибке("Не включена ни одна регламентная операция", Отказ);
	КонецЕсли;
		
КонецПроцедуры