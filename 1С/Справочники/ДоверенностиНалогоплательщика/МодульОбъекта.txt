Процедура ПередЗаписью(Отказ)
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;

	Если ПометкаУдаления ИЛИ Ссылка.ПометкаУдаления Тогда
		Возврат;
	КонецЕсли;
	
	Наименование = "Доверенность" + ?(ЗначениеЗаполнено(НомерДовер), " №" + НомерДовер, "") + " от " + Формат(ДатаВыдачи, "ДФ='dd.MM.yyyy ""г.""'");
	
	Заголовок = "Запись доверенности налогоплательщика (" + Владелец + ") " + Наименование + ", код " + Код;
	
	// Проверка заполнения обязательных реквизитов
	Если НЕ ЗначениеЗаполнено(Владелец) Тогда
		ОбщегоНазначения.СообщитьОбОшибке("Не выбрана организация налогоплательщика", Отказ, Заголовок);
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ДатаВыдачи) Тогда
		ОбщегоНазначения.СообщитьОбОшибке("Не указана дата выдачи доверенности", Отказ, Заголовок);
	КонецЕсли;
	
	Если ПолномочияПредставителя.Количество() = 0 Тогда
		ОбщегоНазначения.СообщитьОбОшибке("Не заполнены сведения о полномочиях представителя", Отказ, Заголовок);
	КонецЕсли;
КонецПроцедуры