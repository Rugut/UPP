Процедура ВыполнитьОбработку()

	НаборСтарыхНастроек = РегистрыСведений.ЗначенияДополнительныхПравПользователя.СоздатьНаборЗаписей();
	
	Набор = РегистрыСведений.ЗначенияДополнительныхПравПользователя.СоздатьНаборЗаписей();
	
	Запрос = Новый Запрос;
	
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	&Пользователь КАК Пользователь,
		|	ЗначенияДополнительныхПравПользователя.Право КАК Право,
		|	ЗначенияДополнительныхПравПользователя.Значение КАК Значение
		|ИЗ
		|	РегистрСведений.ЗначенияДополнительныхПравПользователя КАК ЗначенияДополнительныхПравПользователя
		|ГДЕ
		|	ЗначенияДополнительныхПравПользователя.Пользователь = &ПользовательГруппа";
		
	МассивВыбранныхДопПрав = СтарыеДопПрава.НайтиСтроки(Новый Структура("Пометка", Истина));
	Для каждого ЭлКоллекции Из МассивВыбранныхДопПрав Цикл
		
		Если ЭлКоллекции.Профиль = Неопределено 
			ИЛИ ЭлКоллекции.Профиль.Пустая() Тогда
			
			Продолжить;
		КонецЕсли;
		
		Запрос.УстановитьПараметр("ПользовательГруппа", ЭлКоллекции.ПользовательГруппа);
		Запрос.УстановитьПараметр("Пользователь", ЭлКоллекции.Профиль);
		
		Набор.Загрузить(Запрос.Выполнить().Выгрузить());
		Набор.Записать();
		
		Если ТипЗнч(ЭлКоллекции.ПользовательГруппа) = Тип("СправочникСсылка.Пользователи") Тогда
			ПредставлениеНастройки = "настройка пользователя";
		Иначе
			ПредставлениеНастройки = "настройка группы пользователей";
		КонецЕсли; 
		
		ОбщегоНазначения.Сообщение("Перенесена " + ПредставлениеНастройки + " """ + ЭлКоллекции.ПользовательГруппа + """ в профиль """ + ЭлКоллекции.Профиль + """");
		
	КонецЦикла; 
	
КонецПроцедуры // ВыполнитьОбработку

Процедура КнопкаВыполнитьНажатие(Кнопка)
	
	ВыполнитьОбработку();
	
КонецПроцедуры

Процедура ПриОткрытии()
	
	Запрос = Новый Запрос;
	
	ТекстЗапроса = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ЛОЖЬ КАК Пометка,
		|	ЗначенияДополнительныхПравПользователя.Пользователь КАК ПользовательГруппа,
		|	НЕОПРЕДЕЛЕНО КАК Профиль
		|ИЗ
		|	РегистрСведений.ЗначенияДополнительныхПравПользователя КАК ЗначенияДополнительныхПравПользователя
		|ГДЕ
		|	(ЗначенияДополнительныхПравПользователя.Пользователь ССЫЛКА Справочник.Пользователи
		|			ИЛИ ЗначенияДополнительныхПравПользователя.Пользователь ССЫЛКА Справочник.ГруппыПользователей)
		
		// Не переносим настройки пользователя, если ему назначен профиль
   		|	И ВЫБОР
		|			КОГДА ЗначенияДополнительныхПравПользователя.Пользователь ССЫЛКА Справочник.Пользователи
		|				ТОГДА ВЫРАЗИТЬ(ЗначенияДополнительныхПравПользователя.Пользователь КАК Справочник.Пользователи).ПрофильПолномочийПользователя
		|			ИНАЧЕ &ПустойПрофиль
		|		КОНЕЦ = &ПустойПрофиль
		|
		|УПОРЯДОЧИТЬ ПО
		|	ПользовательГруппа";
		
			

	 
	Запрос.Текст = ТекстЗапроса;
	
	Запрос.УстановитьПараметр("ПустойПрофиль", Справочники.ПрофилиПолномочийПользователей.ПустаяСсылка());	
	
	СтарыеДопПрава = Запрос.Выполнить().Выгрузить();
	
КонецПроцедуры

Процедура СтарыеДопПраваПередОкончаниемРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования, Отказ)
	
	ТекущиеДанные = ЭлементыФормы.СтарыеДопПрава.ТекущиеДанные;
	
	Если ТекущиеДанные.Профиль = Неопределено 
		ИЛИ ТекущиеДанные.Профиль.Пустая() Тогда
		
		Возврат;
	КонецЕсли;
	
	МассивСтрок = СтарыеДопПрава.НайтиСтроки(Новый Структура("Профиль", ТекущиеДанные.Профиль));
	
	Если МассивСтрок.Количество() > 1 Тогда
		Предупреждение("Значение """ + ТекущиеДанные.Профиль + """ уже используется!");
		Отказ = Истина;
	КонецЕсли; 
	
	ТекущиеДанные.Пометка = Истина;
	
КонецПроцедуры

Процедура ПометитьВыбранныеСтроки(Пометка)

	Для каждого ЭлКоллекции Из ЭлементыФормы.СтарыеДопПрава.ВыделенныеСтроки Цикл
		ЭлКоллекции.Пометка = Пометка;
	КонецЦикла; 
	
КонецПроцедуры // ПометитьВыбранныеСтроки

Процедура ДействияФормыДействиеУстановитьФлажки(Кнопка)
	
	ПометитьВыбранныеСтроки(Истина);
	
КонецПроцедуры

Процедура ДействияФормыДействиеСнятьФлажки(Кнопка)
	
	ПометитьВыбранныеСтроки(Ложь);
	
КонецПроцедуры