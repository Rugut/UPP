////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОБЩЕГО НАЗНАЧЕНИЯ

// Процедура сохраняет значение реквизита Хранилище (двоичные данные) на диск.
// При необходимости открывает файл с использованием ассоциированного с ним приложения.
//
// Параметры:
//  ИмяВнешнегоФайла - путь, имя файла дле сохранения на диске
//  Запустить - булево, признак запускать ассоциированное с файлом приложение.
//
Процедура СохранитьЗапуститьФайл(ИмяВнешнегоФайла, Запустить = Ложь)
	
	// Если не указано имя файла прервать выполнение процедуры
	Если ПустаяСтрока(ИмяВнешнегоФайла) Тогда
		Предупреждение("Не указано имя файла!");
		Возврат;
	КонецЕсли;
	
	// Сохранить файл на диске
	ВнешнийФайл = Хранилище.Получить();
	Попытка
		ВнешнийФайл.Записать(ИмяВнешнегоФайла);
	Исключение
		Сообщить("Ошибка при сохранении файла: " + ОписаниеОшибки(), СтатусСообщения.Важное);
		Возврат;
	КонецПопытки;
	
	// Запустить ассоциированное с файлом приложение. Открыть файл.
	Если Запустить Тогда
		
		Файл = Новый Файл(ИмяВнешнегоФайла);
		
		Если Файл.Существует() Тогда
			Попытка
				ЗапуститьПриложение("explorer " + ИмяВнешнегоФайла);
			Исключение
				Сообщить("Ошибка при запуске файла: " + ОписаниеОшибки(), СтатусСообщения.Важное);
				Возврат;
			КонецПопытки;
		Иначе
			Предупреждение("Не удалось найти файл " + ИмяВнешнегоФайла + " на диске!");
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры // СохранитьЗапуститьФайл()

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ФОРМЫ

// Процедура - обработчик события "НачалоВыбора" элемента формы ИмяФайла.
// Открывает диалог выбора файла. Присваивает двоичные данные реквизиту Хранилище.
//
Процедура ИмяФайлаНачалоВыбора(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	// Выбрать файл на диске
	ДиалогВыбораФайла = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	
	ДиалогВыбораФайла.Фильтр                  = "Все файлы|*.*";
	ДиалогВыбораФайла.Заголовок               = "Выберите файл";
	ДиалогВыбораФайла.ПредварительныйПросмотр = Ложь;
	ДиалогВыбораФайла.Расширение              = "*";
	ДиалогВыбораФайла.ИндексФильтра           = 0;
	
	// Присвоить двоичные данные реквизиту Хранилище
	Если ДиалогВыбораФайла.Выбрать() Тогда
		
		ИмяФайла    = СтрЗаменить(ДиалогВыбораФайла.ПолноеИмяФайла, ДиалогВыбораФайла.Каталог, "");
		ВнешнийФайл = Новый ДвоичныеДанные(ДиалогВыбораФайла.ПолноеИмяФайла);
		Хранилище   = Новый ХранилищеЗначения(ВнешнийФайл);
		
	Иначе
		Возврат;
	КонецЕсли;
	
КонецПроцедуры // ИмяФайлаНачалоВыбора()

// Процедура - обработчик события "Открытие"  элемента формы ИмяФайла.
// Сохраняет двоичные данные на диске. Запускает ассоциированное с файлом приложение.
//
Процедура ИмяФайлаОткрытие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	// Получить расширение файла.
	РасширениеВременногоФайла = Прав(ИмяФайла, 4);
	
	Если Лев(РасширениеВременногоФайла, 1) = "." Тогда
		// Получить имя временного файла. Установить расширение временного файла.
		РасширениеВременногоФайла = Прав(ИмяФайла, 3);
		ИмяВременногоФайла = ПолучитьИмяВременногоФайла(РасширениеВременногоФайла);
	Иначе
		// Получить имя временного файла с расширением по умолчанию.
		ИмяВременногоФайла = ПолучитьИмяВременногоФайла();
	КонецЕсли;
	
	// Сохранить файл на диске. Запустить приложение.
	СохранитьЗапуститьФайл(ИмяВременногоФайла, Истина);
	
КонецПроцедуры // ИмяФайлаОткрытие()

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ДЕЙСТВИЯ КОМАНДНЫХ ПАНЕЛЕЙ ФОРМЫ

// Процедура - обработчик действия "СохранитьФайлНаДиск" командной панели ДействияФормы.
// Открывает диалог сохранения файла. Сохраняет двоичные данные на диске.
//
Процедура ДействияФормыСохранитьФайлНаДиск(Кнопка)
	
	// Открыть диалог сохранения файла
	ДиалогВыбораФайла = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Сохранение);
	
	ДиалогВыбораФайла.Заголовок = "Укажите имя файла";
	ДиалогВыбораФайла.ПредварительныйПросмотр = Ложь;
	ДиалогВыбораФайла.Расширение = "*";
	ДиалогВыбораФайла.ПолноеИмяФайла = ИмяФайла;
	
	// Сохранить файл на диске
	Если ДиалогВыбораФайла.Выбрать() Тогда
		ИмяВнешнегоФайла = ДиалогВыбораФайла.ПолноеИмяФайла;
		СохранитьЗапуститьФайл(ИмяВнешнегоФайла);
	Иначе
		Возврат;
	КонецЕсли;
	
КонецПроцедуры // ДействияФормыСохранитьФайлНаДиск()