////////////////////////////////////////////////////////////////////////////////
// ОБЩИЕ ПРОЦЕДУРЫ И ФУНКЦИИ

// Выполняет проверку заполненности реквизитов.
//
// Параметры
//	Заголовок - заголовок сообщения об ошибке
//
// Возвращаемое значение
//	Истина  - все проверяемые реквизиты заполнены
//	Ложь	- не все проверяемые реквизиты заполнены
Функция РеквизитыЗаполнены(Знач Заголовок) Экспорт
	
	Если ЭтоГруппа Тогда
		Возврат Истина;
	КонецЕсли;
	
	Отказ = Ложь;
	
	// Должны быть заполнены обязательные реквизиты
	СтруктураОбязательныхПолей = Новый Структура();
	СтруктураОбязательныхПолей.Вставить("ВидРегулярногоДокумента", "Не указан вид формируемых документов");
	СтруктураОбязательныхПолей.Вставить("ПериодДетализации",	   "Не указан период детализации формируемых документов");
	СтруктураОбязательныхПолей.Вставить("Организация",			   "Не указана организация, по которой формируются документы");
	СтруктураОбязательныхПолей.Вставить("Склад",				   "Не указан склад, по которому формируются документы");
	ЗаполнениеДокументов.ПроверитьЗаполнениеШапкиДокумента(ЭтотОбъект, СтруктураОбязательныхПолей, Отказ, Заголовок);
	
	// Документ должен принадлежать хотя бы к одному виду учета (управленческий, бухгалтерский, налоговый)
	СтруктураШапкиДокумента = Новый Структура("ОтражатьВУправленческомУчете,ОтражатьВБухгалтерскомУчете,ОтражатьВНалоговомУчете");
	ЗаполнитьЗначенияСвойств(СтруктураШапкиДокумента, ЭтотОбъект);
	ОбщегоНазначения.ПроверитьПринадлежностьКВидамУчета(СтруктураШапкиДокумента, Отказ, Заголовок);
	
	Возврат НЕ Отказ;
		
КонецФункции

// Подготавливает заголовок сообщений об ошибках при записи
//
// Возвращаемое значение
//  Строка, заголовок сообщений
Функция ЗаголовокПриЗаписи() Экспорт
	
	Возврат "Запись настройки формирования документов """ + Наименование + """";
	
КонецФункции

// Возвращает имя объекта метаданных для создания регл. задания
//
// Возвращаемое значение
//	Строка  - имя объекта метаданных
Функция ИмяРегламентногоЗадания() Экспорт
	Возврат "РегламентноеФормированиеДокументов";
КонецФункции	

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ОБЪЕКТА

// Обработчик события ПередЗаписью
// Выполняются проверки - можно записать элемент или нет.
// При необходимости создается регламентное задание
Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Или ЭтоГруппа Тогда
		Возврат;
	КонецЕсли;
	
	// Не во всех документах указываются материалы
	// Если не указываются материалы, то не имеет смысл формировать документ без выпуска
	Если НЕ РегламентноеФормированиеДокументов.РегламентФормированияДокументовВыпуска_ЗаполнятьМатериалы(ВидРегулярногоДокумента) Тогда
		ОтразитьВыпуск    = Истина;
		ОтразитьМатериалы = Ложь;
	КонецЕсли;
	
КонецПроцедуры