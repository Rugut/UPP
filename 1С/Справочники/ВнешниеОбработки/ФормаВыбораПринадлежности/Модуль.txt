Функция ПолучитьСписокВыбранныхОбъектов()
	
	СписокОбъектов = Новый СписокЗначений;
	
	Для Каждого Документ Из СписокДокументов Цикл
		
		Если Документ.Пометка Тогда
			СписокОбъектов.Добавить(Документ.Значение);
		КонецЕсли;
		
	КонецЦикла;
	
	Для Каждого Справочник Из СписокСправочников Цикл
		
		Если Справочник.Пометка Тогда
			СписокОбъектов.Добавить(Справочник.Значение);
		КонецЕсли;
		
	КонецЦикла;
	
	Для Каждого Отчет Из СписокОтчетов Цикл
		
		Если Отчет.Пометка Тогда
			СписокОбъектов.Добавить(Отчет.Значение);
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат СписокОбъектов;
	
КонецФункции

Процедура ЗаполнитьСписокОбъектов()
	
	Если УжеВыбранные = Неопределено  Тогда
		УжеВыбранные = Новый Соответствие;
	КонецЕсли;
	
	Если ВидОбработки = Перечисления.ВидыДополнительныхВнешнихОбработок.ВнешняяОбработкаПодключаемаяКОтчету Тогда
		
		Для Каждого МетаОтчет Из Метаданные.Отчеты Цикл

			ЕстьРеквизитУниверсальныйОтчет = Ложь;
			Для каждого МетаОтчетРеквизиты Из МетаОтчет.Реквизиты Цикл
				Если МетаОтчетРеквизиты.Тип.СодержитТип(Тип("ОтчетОбъект.УниверсальныйОтчет")) Тогда
					ЕстьРеквизитУниверсальныйОтчет = Истина;
					Прервать;
				КонецЕсли;
			КонецЦикла;
			
			Если ЕстьРеквизитУниверсальныйОтчет И УжеВыбранные[МетаОтчет] = Неопределено Тогда
				СписокОтчетов.Добавить(МетаОтчет, ?(МетаОтчет.Синоним="", МетаОтчет.Имя, МетаОтчет.Синоним));
			КонецЕсли;
			
		КонецЦикла;
		
	Иначе	
		
		Для Каждого Документ Из Метаданные.Документы Цикл
			
			Если УжеВыбранные[Документ] = Неопределено Тогда
				СписокДокументов.Добавить(Документ, ?(Документ.Синоним="", Документ.Имя, Документ.Синоним));
			КонецЕсли;
			
		КонецЦикла;
		
		ДоступныеСправочники = Новый Массив;
		ДоступныеСправочники.Добавить(Метаданные.Справочники.Контрагенты);
		ДоступныеСправочники.Добавить(Метаданные.Справочники.НастройкиЗакрытияМесяца);
		ДоступныеСправочники.Добавить(Метаданные.Справочники.Номенклатура);
		ДоступныеСправочники.Добавить(Метаданные.Справочники.ОсновныеСредства);
		ДоступныеСправочники.Добавить(Метаданные.Справочники.ПрофилиПолномочийПользователей);
		ДоступныеСправочники.Добавить(Метаданные.Справочники.НастройкиДопроведенияДокументов);
		ДоступныеСправочники.Добавить(Метаданные.Справочники.СпецификацииНоменклатуры);
		ДоступныеСправочники.Добавить(Метаданные.Справочники.СотрудникиОрганизаций);
		ДоступныеСправочники.Добавить(Метаданные.Справочники.ФизическиеЛица);
		Для Каждого Справочник Из ДоступныеСправочники Цикл
			
			Если УжеВыбранные[Справочник] = Неопределено Тогда
				СписокСправочников.Добавить(Справочник, ?(Справочник.Синоним="", Справочник.Имя, Справочник.Синоним));
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли; 
	
КонецПроцедуры

Процедура ПередОткрытием(Отказ, СтандартнаяОбработка)
	
	Если НЕ РежимПодбора Тогда
		ЭлементыФормы.СписокДокументов.Колонки.Значение.ДанныеФлажка = "";
		ЭлементыФормы.СписокСправочников.Колонки.Значение.ДанныеФлажка = "";
		ЭлементыФормы.СписокОтчетов.Колонки.Значение.ДанныеФлажка = "";
	КонецЕсли;
	
	ЗаполнитьСписокОбъектов();
	
	Если ВидОбработки = Перечисления.ВидыДополнительныхВнешнихОбработок.ВнешняяОбработкаПодключаемаяКОтчету Тогда
		ЭлементыФормы.Панель1.Страницы.Документы.Видимость = Ложь;
		ЭлементыФормы.Панель1.Страницы.Справочники.Видимость = Ложь;
	Иначе
		ЭлементыФормы.Панель1.Страницы.Отчеты.Видимость = Ложь;
	КонецЕсли;
	
	Если ВидОбработки = Перечисления.ВидыДополнительныхВнешнихОбработок.ЗаполнениеТабличныхЧастей ИЛИ ВидОбработки = Перечисления.ВидыДополнительныхВнешнихОбработок.ВнешняяОбработкаПодключаемаяКОтчету Тогда
		Заголовок = "Принадлежность внешней обработки";
	КонецЕсли;
 
КонецПроцедуры

Процедура ОсновныеДействияФормыКнопкаОК(Кнопка)
	
	Если НЕ РежимПодбора Тогда
		Если ЭлементыФормы.Панель1.ТекущаяСтраница = ЭлементыФормы.Панель1.Страницы.Документы Тогда
			ОповеститьОВыборе(ЭлементыФормы.СписокДокументов.ТекущаяСтрока.Значение);
		ИначеЕсли ЭлементыФормы.Панель1.ТекущаяСтраница = ЭлементыФормы.Панель1.Страницы.Справочники Тогда
			ОповеститьОВыборе(ЭлементыФормы.СписокСправочников.ТекущаяСтрока.Значение);
		ИначеЕсли ЭлементыФормы.Панель1.ТекущаяСтраница = ЭлементыФормы.Панель1.Страницы.Отчеты Тогда
			ОповеститьОВыборе(ЭлементыФормы.СписокОтчетов.ТекущаяСтрока.Значение);			
		КонецЕсли; 
	Иначе
		ОповеститьОВыборе(ПолучитьСписокВыбранныхОбъектов());
	КонецЕсли;
	
КонецПроцедуры

Процедура СписокДокументовВыбор(Элемент, ВыбраннаяСтрока, Колонка, СтандартнаяОбработка)
	
	Если РежимПодбора Тогда
		ВыбраннаяСтрока.Пометка = НЕ ВыбраннаяСтрока.Пометка;
	Иначе
		ОповеститьОВыборе(ЭлементыФормы.СписокДокументов.ТекущаяСтрока.Значение);
	КонецЕсли;
	
КонецПроцедуры

Процедура СписокСправочниковВыбор(Элемент, ВыбраннаяСтрока, Колонка, СтандартнаяОбработка)
	
	Если РежимПодбора Тогда
		ВыбраннаяСтрока.Пометка = НЕ ВыбраннаяСтрока.Пометка;
	Иначе
		ОповеститьОВыборе(ЭлементыФормы.СписокСправочников.ТекущаяСтрока.Значение);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриПовторномОткрытии(СтандартнаяОбработка)
	
	ЗаполнитьСписокОбъектов();
	
КонецПроцедуры

Процедура СписокОтчетовВыбор(Элемент, ВыбраннаяСтрока, Колонка, СтандартнаяОбработка)
	
	Если РежимПодбора Тогда
		ВыбраннаяСтрока.Пометка = НЕ ВыбраннаяСтрока.Пометка;
	Иначе
		ОповеститьОВыборе(ЭлементыФормы.СписокОтчетов.ТекущаяСтрока.Значение);
	КонецЕсли;

КонецПроцедуры