Процедура СправочникСписокВыбор(Элемент, ВыбраннаяСтрока, Колонка, СтандартнаяОбработка)
	
	Если Не ВыбраннаяСтрока.ЭтоГруппа Тогда
		
		ЭтоОбработка = ?(ВыбраннаяСтрока.ВидОбработки = Перечисления.ВидыДополнительныхВнешнихОбработок.Обработка, Истина, Ложь);
		ЭтоОтчет = ?(ВыбраннаяСтрока.ВидОбработки = Перечисления.ВидыДополнительныхВнешнихОбработок.Отчет, Истина, Ложь);
		
		Если НЕ (ЭтоОбработка ИЛИ ЭтоОтчет) Тогда
			Возврат;
		КонецЕсли;
		
		Попытка
			
			ИмяФайла = ПолучитьИмяВременногоФайла();
			ДвоичныеДанные = ВыбраннаяСтрока.ХранилищеВнешнейОбработки.Получить();
			ДвоичныеДанные.Записать(ИмяФайла);
			
			Если ЭтоОбработка Тогда
				
				Форма = ВнешниеОбработки.ПолучитьФорму(ИмяФайла);
				
			Иначе
				
				Форма = ВнешниеОтчеты.ПолучитьФорму(ИмяФайла);
				
			КонецЕсли;
			
			Если Не Форма = Неопределено Тогда
				
				Форма.Открыть();
				
			Иначе
				
				Если ЭтоОбработка Тогда
					ВнешняяОбработка = ВнешниеОбработки.Создать(ИмяФайла);
				Иначе
					ВнешнийОтчет = ВнешниеОтчеты.Создать(ИмяФайла);
					Форма = ВнешнийОтчет.ПолучитьФорму();
					Если Форма <> Неопределено Тогда
						Форма.Открыть();
					КонецЕсли;
				КонецЕсли;
				
			КонецЕсли;
			
		Исключение
			
			Если ЭтоОбработка Тогда
				
				Предупреждение("Выбранный файл не является внешней обработкой.
							   |Либо, данная обработка не предназначена для
							   |запуска в этой конфигурации.");
			Иначе
				Предупреждение("Выбранный файл не является внешним отчетом.
							   |Либо, данный отчет не предназначена для
							   |запуска в этой конфигурации.");
			КонецЕсли;
			
		КонецПопытки;
		
		СтандартнаяОбработка = Ложь;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриОткрытии()
	
	Если Отбор.ВидОбработки.Использование Тогда
		Если КлючУникальности = Перечисления.ВидыДополнительныхВнешнихОбработок.ПечатнаяФорма Тогда
			Заголовок = "Дополнительные внешние печатные формы";
		ИначеЕсли КлючУникальности = Перечисления.ВидыДополнительныхВнешнихОбработок.ЗаполнениеТабличныхЧастей Тогда
			Заголовок = "Дополнительные внешние обработки по заполнению табличных частей";
		ИначеЕсли КлючУникальности = Перечисления.ВидыДополнительныхВнешнихОбработок.Обработка Тогда
			Заголовок = "Дополнительные внешние обработки";
		КонецЕсли; 
	КонецЕсли; 
	
КонецПроцедуры

Процедура СправочникСписокПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, ЭтоГруппа)
	
	Если Не ЭтоГруппа И Не Копирование Тогда
		
		Форма = Справочники.ВнешниеОбработки.ПолучитьФормуНовогоЭлемента(,ЭлементыФормы.СправочникСписок);
		Форма.Родитель = Родитель;
		
		Если Отбор.ВидОбработки.Использование и Отбор.ВидОбработки.ВидСравнения = ВидСравнения.ВСписке Тогда
			Форма.ЭлементыФормы.ВидОбработки.ДоступныеЗначения = Отбор.ВидОбработки.Значение;
		ИначеЕсли Отбор.ВидОбработки.Использование и Отбор.ВидОбработки.ВидСравнения = ВидСравнения.Равно Тогда
			Форма.ВидОбработки = Отбор.ВидОбработки.Значение;
		КонецЕсли;
		
		Форма.Открыть();
		Отказ = Истина;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ДействияФормыПраваДоступаПользователейКТекущемуЭлементу(Кнопка)
	
	Если Не ЭлементыФормы.СправочникСписок.ТекущиеДанные = Неопределено Тогда
		НастройкаПравДоступа.РедактироватьПраваДоступа(ЭлементыФормы.СправочникСписок.ТекущиеДанные.Ссылка);
	КонецЕсли;
	
КонецПроцедуры

Процедура ДействияФормыПраваДоступаПользователейКоВсемуСправочнику(Кнопка)
	
	НастройкаПравДоступа.РедактироватьПраваДоступа(Справочники.ВнешниеОбработки.ПустаяСсылка());
	
КонецПроцедуры

Процедура ПередОткрытием(Отказ, СтандартнаяОбработка)
	
	// При необходимости удалим кнопку Права
	РаботаСДиалогами.УдалитьКнопкуПрава(ЭлементыФормы.ДействияФормы.Кнопки);
	
КонецПроцедуры