////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

Процедура ПриОткрытии()
	
	// Установка заголовка формы и удаление ненужных кнопок панели
	ИмяОбъекта = Сред(Отбор.НастраиваемыйОбъект.Значение, Найти(Отбор.НастраиваемыйОбъект.Значение, ".") + 1);
	Если ВладелецФормы <> Неопределено И ТипЗнч(ВладелецФормы) = Тип("Форма") Тогда
		СинонимОбъекта = ВладелецФормы.Заголовок;
	ИначеЕсли Метаданные.Отчеты.Найти(ИмяОбъекта) <> Неопределено Тогда
		СинонимОбъекта = Метаданные.Отчеты.Найти(ИмяОбъекта).Синоним;
	Иначе
		СинонимОбъекта = "";
	КонецЕсли;
	Если РежимСохраненияНастройки Тогда
		ЭтаФорма.Заголовок = "Сохранение варианта отчета"; //"" + СинонимОбъекта + """"
		ЭлементыФормы.ДействияФормы.Кнопки.Удалить(ЭлементыФормы.ДействияФормы.Кнопки.ВыбратьНастройку);
		ЭтаФорма.ТекущийЭлемент = ЭлементыФормы.Наименование;
	Иначе
		ЭтаФорма.Заголовок = "Список вариантов отчета"; //""" + СинонимОбъекта + """"
		ЭлементыФормы.ДействияФормы.Кнопки.Удалить(ЭлементыФормы.ДействияФормы.Кнопки.Сохранить);
		ЭлементыФормы.ПанельВводаНовойНастройки.Свертка = РежимСверткиЭлементаУправления.Верх;
		ЭтаФорма.ТекущийЭлемент = ЭлементыФормы.СправочникСписокСохраненныеНастройки;
		ЭлементыФормы.СправочникСписокСохраненныеНастройки.ИзменятьСоставСтрок = Истина;
	КонецЕсли;
	
	Отбор.ПометкаУдаления.Установить(ложь);
	Отбор.ТипНастройки.Установить(Перечисления.ТипыНастроек.НастройкиОтчета);
	
	// Установка доступности отбора и порядка
	СохранениеНастроек.УстановитьДоступностьОтбора(ЭлементыФормы.СправочникСписокСохраненныеНастройки, "ЭтоГруппа, НастраиваемыйОбъект, ТипНастройки, ПометкаУдаления, Ссылка");
	//СохранениеНастроек.УстановитьДоступностьПорядка(ЭлементыФормы.СправочникСписокСохраненныеНастройки);
	
	// Настройка вида списка
	СохранениеНастроек.НастроитьВидСписка(ЭлементыФормы.СправочникСписокСохраненныеНастройки, глЗначениеПеременной("глТекущийПользователь"));
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ СПИСКА

Процедура СправочникСписокСохраненныеНастройкиВыборЗначения(Элемент, СтандартнаяОбработка, Значение)
	
	Если Значение = Справочники.СохраненныеНастройки.ПустаяСсылка() ИЛИ Значение.ЭтоГруппа Тогда
		СтандартнаяОбработка = Ложь;
		Возврат;
	КонецЕсли;
	
	Если РежимСохраненияНастройки Тогда
		СохранитьНастройку();
		СтандартнаяОбработка = Ложь;
		Возврат;
	КонецЕсли;
	
	Если ТипЗнч(ВладелецФормы) = Тип("Форма") Тогда
		ВладелецФормы.СохраненнаяНастройка = Значение;
		ВладелецФормы.ПрименитьНастройку();
		Модифицированность = Ложь;
	КонецЕсли;
	
КонецПроцедуры

Процедура СправочникСписокСохраненныеНастройкиПриАктивизацииСтроки(Элемент)
	
	// Обновим поле наименование
	ТекущаяСтрока = ЭлементыФормы.СправочникСписокСохраненныеНастройки.ТекущиеДанные;
	Если ПараметрТекущаяСтрока = Справочники.СохраненныеНастройки.ПустаяСсылка() И ПустаяСтрока(Наименование) Тогда
		Наименование = "Новый вариант отчета";
	ИначеЕсли ТекущаяСтрока <> Неопределено Тогда
		Наименование = ТекущаяСтрока.Наименование;
	КонецЕсли;
	
КонецПроцедуры

Процедура СохранитьНастройку()
	
	ТекущаяСтрока = ЭлементыФормы.СправочникСписокСохраненныеНастройки.ТекущаяСтрока;
	Если ТекущаяСтрока <> Неопределено И Наименование = ТекущаяСтрока.Наименование Тогда
		Если ВладелецФормы.СохраненнаяНастройка = ТекущаяСтрока Тогда
			// Выбрали текущую настройку. ничего не делаем
		Иначе
			Ответ = Вопрос("Заменить существующий вариант отчета """ + ТекущаяСтрока + """?", РежимДиалогаВопрос.ДаНет,, КодВозвратаДиалога.Нет);
			Если Ответ = КодВозвратаДиалога.Да Тогда
				// Сохранение существующей настройки
				ВладелецФормы.СохраненнаяНастройка = ТекущаяСтрока;
			Иначе
				Возврат;
			КонецЕсли;
		КонецЕсли;
	Иначе
		// Создание новой настройки
		НовыйЭлемент = Справочники.СохраненныеНастройки.СоздатьЭлемент();
		НовыйЭлемент.Наименование = Наименование;
		НовыйЭлемент.НастраиваемыйОбъект = Отбор.НастраиваемыйОбъект.Значение;
		//НовыйЭлемент.Владелец = глЗначениеПеременной("глТекущийПользователь");
		НовыйЭлемент.ТипНастройки = Перечисления.ТипыНастроек.НастройкиОтчета;
		НовыйПользователь = НовыйЭлемент.Пользователи.Добавить();
		НовыйПользователь.Пользователь = глЗначениеПеременной("глТекущийПользователь");
		НовыйПользователь.ПравоИзменения = Истина;
		Отказ = Ложь;
		Сообщение = "";
		НовыйЭлемент.ПроверитьОбязательныеРеквизиты(НовыйЭлемент, Отказ, Сообщение);
		Если Отказ Тогда
			Сообщить("Элемент не может быть записан! " + Сообщение, СтатусСообщения.Важное);
			Возврат;
		КонецЕсли;
		НовыйЭлемент.Записать();
		// Сохранение
		ВладелецФормы.СохраненнаяНастройка = НовыйЭлемент.Ссылка;
	КонецЕсли;
	ВладелецФормы.СохранитьНастройку();
	ВладелецФормы.Модифицированность = Ложь;
	Если Не ТиповыеОтчеты.ЭтоСтараяВерсияОтчета(ВладелецФормы.ЭтотОбъект) Тогда
		ВладелецФормы.РежимРедактированияНастройки = Ложь;
		ВладелецФормы.ЭтоОтработкаРасшифровки = Ложь;
	КонецЕсли;
	
	Закрыть();
	
КонецПроцедуры

Процедура ДействияФормыСохранить(Кнопка)
	
	СохранитьНастройку();
	
КонецПроцедуры

Процедура СправочникСписокСохраненныеНастройкиПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, ЭтоГруппа)
	
	Отказ = Истина;
	
КонецПроцедуры

Процедура СправочникСписокСохраненныеНастройкиПослеУдаления(Элемент)
	
	ОтработкаУдаленияВарианта();
	
КонецПроцедуры

Процедура СправочникСписокСохраненныеНастройкиПередУстановкойПометкиУдаления(Элемент, Отказ)
	ОтработкаУдаленияВарианта();
КонецПроцедуры

Процедура ОтработкаУдаленияВарианта()
	Если ТипЗнч(ВладелецФормы) = Тип("Форма") Тогда
		ВладелецФормы.СохраненнаяНастройка = Справочники.СохраненныеНастройки.ПустаяСсылка();
		ВладелецФормы.ПрименитьНастройку();
	КонецЕсли;
КонецПроцедуры


Процедура СправочникСписокСохраненныеНастройкиПриПолученииДанных(Элемент, ОформленияСтрок)
	Для каждого ОформленияСтроки из ОформленияСтрок Цикл
		
		Если ОформленияСтроки.ДанныеСтроки <> Неопределено тогда
			СписокПользователей = "";
			ПервыйПроход = истина;
			Для каждого СтрокаПользователь из ОформленияСтроки.ДанныеСтроки.Ссылка.Пользователи Цикл
				СписокПользователей = СписокПользователей + ?(ПервыйПроход, "", ", ") +СтрокаПользователь.Пользователь;
				ПервыйПроход = Ложь;
			КонецЦикла;
			ОформленияСтроки.Ячейки.Пользователи.Текст = СписокПользователей;
			ОформленияСтроки.Ячейки.Пользователи.ОтображатьТекст = истина;
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

Процедура СправочникСписокСохраненныеНастройкиПередНачаломИзменения(Элемент, Отказ)
	Отказ = истина;
КонецПроцедуры