// Процедура - обработчик нажатия кнопки "Файлы"
//
Процедура ДействияФормыРедактироватьКод(Кнопка)
	МеханизмНумерацииОбъектов.ИзменениеВозможностиРедактированияНомера(Метаданные.Справочники.ДоговорыКонтрагентов, ЭлементыФормы.СправочникСписок, ЭлементыФормы.ДействияФормы.Кнопки.Подменю, ЭлементыФормы.СправочникСписок.Колонки.Код);
КонецПроцедуры
Процедура ДействияФормыФайлы(Кнопка)

	Если НЕ ЭлементыФормы.СправочникСписок.ТекущаяСтрока = Неопределено Тогда
		ФормаВыб = Справочники.ХранилищеДополнительнойИнформации.ПолучитьФорму("ФормаСписка",ЭтаФорма);
		ФормаВыб.Отбор.Объект.Значение = ЭлементыФормы.СправочникСписок.ТекущаяСтрока.Ссылка;
		ФормаВыб.Отбор.Объект.Использование = Истина;
		ФормаВыб.Открыть();
	КонецЕсли;

КонецПроцедуры

// Процедура - обработчик события "ПриОткрытии" формы
//
Процедура ПриОткрытии()

	Если РежимВыбора И НЕ ЗначениеЗаполнено(Отбор.Владелец.Значение) Тогда
		ФормаВыбораВладельца = Справочники.Контрагенты.ПолучитьФормуВыбора();
		ФормаВыбораВладельца.Заголовок = "Выберите контрагента, договор которого Вы хотите выбрать.";
		Контрагент = ФормаВыбораВладельца.ОткрытьМодально();
		Если ТипЗнч(Контрагент) = ТипЗнч(Отбор.Владелец.Значение) Тогда
			Отбор.Владелец.Значение = Контрагент;
		КонецЕсли;
		Отбор.Владелец.Использование = ЗначениеЗаполнено(Отбор.Владелец.Значение);
	КонецЕсли;
	
	Если Отбор.Владелец.Использование И Отбор.Владелец.ВидСравнения = ВидСравнения.Равно Тогда
		ЭлементыФормы.СправочникСписок.Колонки.Владелец.Видимость = Ложь;
	КонецЕсли; 
	
	РаботаСДиалогами.УстановитьВидимостьПроекта(, ЭлементыФормы, "СправочникСписок.ОсновнойПроект");

	МеханизмНумерацииОбъектов.ДобавитьВМенюДействияКнопкуРедактированияКода(ЭлементыФормы.ДействияФормы.Кнопки.Подменю);
	МеханизмНумерацииОбъектов.УстановитьДоступностьПоляВводаНомера(Метаданные.Справочники.ДоговорыКонтрагентов, ЭлементыФормы.СправочникСписок, ЭлементыФормы.ДействияФормы.Кнопки.Подменю, ЭлементыФормы.СправочникСписок.Колонки.Код);
КонецПроцедуры

Процедура СправочникСписокПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, ЭтоГруппа)

	МассивПолейОтбора = Новый Массив;
	МассивПолейОтбора.Добавить("Организация");
	МассивПолейОтбора.Добавить("ВидДоговора");
	МассивПолейОтбора.Добавить("ВедениеВзаиморасчетов");
	МассивПолейОтбора.Добавить("ВалютаВзаиморасчетов");

	Если НЕ Копирование и Не ЭтоГруппа Тогда

		Для Каждого ИмяПоляОтбора Из МассивПолейОтбора Цикл
			ОтборФормы = СправочникСписок.Отбор[ИмяПоляОтбора];
			Если ОтборФормы.Использование
			   И (ОтборФормы.ВидСравнения = ВидСравнения.Равно Или ОтборФормы.ВидСравнения = ВидСравнения.ВСписке) Тогда
				Отказ = Истина;
				Прервать;
			КонецЕсли;
		КонецЦикла;

		Если Отказ = Истина Тогда

			НовыйОбъект = Справочники.ДоговорыКонтрагентов.СоздатьЭлемент();

			Для Каждого ИмяПоляОтбора Из МассивПолейОтбора Цикл
				ОтборФормы = СправочникСписок.Отбор[ИмяПоляОтбора];
				Если ОтборФормы.Использование Тогда
					Если ОтборФормы.ВидСравнения = ВидСравнения.Равно Тогда
						НовыйОбъект[ИмяПоляОтбора] = ОтборФормы.Значение;
					ИначеЕсли ОтборФормы.ВидСравнения = ВидСравнения.ВСписке И ОтборФормы.Значение.Количество() > 0 Тогда
						НовыйОбъект[ИмяПоляОтбора] = ОтборФормы.Значение[0];
					КонецЕсли;
				КонецЕсли
			КОнецЦикла;

			// Надо установить владельца, если он задан
			Если СправочникСписок.Отбор.Владелец.Использование
			   И СправочникСписок.Отбор.Владелец.ВидСравнения = ВидСравнения.Равно Тогда
				НовыйОбъект.Владелец = СправочникСписок.Отбор.Владелец.Значение;
			КонецЕсли;

			// Надо установить родителя
			НовыйОбъект.Родитель = Родитель;

			НовыйОбъект.ПолучитьФорму(,ЭлементыФормы.СправочникСписок).Открыть();

		КонецЕсли;

	КонецЕсли;

КонецПроцедуры // СправочникСписокПередНачаломДобавления()