///////////////////////////////////////////////////////////////////////////////
//// ОБРАБОТЧИКИ СОБЫТИЙ

// Процедура - обработчик события "Перед записью".
//
// Параметры:
//  Отказ - <Булево>
//        - Признак отказа от записи элемента. Если в теле
//          процедуры-обработчика установить данному параметру
//          значение Истина, запись элемента выполнена не будет.
//          Значение по умолчанию: Ложь.
//
Процедура ПередЗаписью(Отказ)

	Если Не ОбменДанными.Загрузка Тогда

		Ошибки = "";
		Если ПустаяСтрока(Модель) Тогда
			Ошибки = " - Не указана модель торгового оборудования.";
		КонецЕсли;

		Если НЕ ЗначениеЗаполнено(ОбработкаОбслуживания) Тогда
			Ошибки = Ошибки + ?(ПустаяСтрока(Ошибки),
			                    "",
			                    "
			                    |")
			                + " - Не указана обработка обслуживания.";
		КонецЕсли;

		Если Не ЭтоНовый() И Ссылка.ОбработкаОбслуживания <> ОбработкаОбслуживания И ЕстьСсылкиНаЗаписьСправочника() Тогда
			Ошибки = Ошибки + ?(ПустаяСтрока(Ошибки),
			                    "",
			                    "
			                    |")
			                + " - Для данной модели уже существует подключенное торговое оборудование.
			                  |   Изменение обработки обслуживания невозможно.";
		КонецЕсли;

		Если Не ПустаяСтрока(Ошибки) Тогда
			ОбщегоНазначения.СообщитьОбОшибке("При попытке записи элемента с кодом """ + Код + """ были обнаружены ошибки:
			                 |" + Ошибки);
			Отказ = Истина;
		КонецЕсли;

	КонецЕсли;

КонецПроцедуры

// Функция проверяет, имеют ли в регистре сведений "Торговое оборудование" ссылки на данный элемент справочника, и возвращает ответ.
//
// Параметры
//
// Возвращаемое значение:
//  <Булево>  - ответ на вопрос "Есть ли ссылки на текущую запись справочника?"
//
Функция ЕстьСсылкиНаЗаписьСправочника()

	Запрос = Новый Запрос("
	|ВЫБРАТЬ
	|	РегТО.Модель,
	|	РегТО.Вид
	|ИЗ
	|	РегистрСведений.ТорговоеОборудование КАК РегТО
	|ГДЕ
	|	РегТО.Модель = &СправочникТО
	|");

	Запрос.УстановитьПараметр("СправочникТО", Ссылка);

	Возврат Запрос.Выполнить().Выгрузить().Количество() <> 0;

КонецФункции // ПолучитьОбработки()