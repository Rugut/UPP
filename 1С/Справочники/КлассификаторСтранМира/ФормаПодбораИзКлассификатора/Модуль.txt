////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

// Обработчик события ПриОткрытии формы.
//
Процедура ПриОткрытии()
	
	Макет = Справочники.КлассификаторСтранМира.ПолучитьМакет("КлассификаторСтранМира");
	
	Макет.Параметры.Расшифровка = Истина; // чтобы работала расшифровка
	
	ТабличныйДокумент = ЭлементыФормы.ПолеТабличногоДокумента;
	
	ТабличныйДокумент.Вывести(Макет);
	
	ТабличныйДокумент.ФиксацияСверху      = ТабличныйДокумент.Области.ОбластьРасшифровки.Верх - 1;
	
	ТабличныйДокумент.ОтображатьЗаголовки = Ложь;
	ТабличныйДокумент.ОтображатьСетку     = Ложь;
	ТабличныйДокумент.ТолькоПросмотр      = Истина;
	
КонецПроцедуры

// Обработчик события ОбработкаРасшифровки элемента ПолеТабличногоДокумента.
//
Процедура ПолеТабличногоДокументаОбработкаРасшифровки(Элемент, Расшифровка, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	// Получение значений полей выбранной строки.
	
	ТабличныйДокумент = ЭлементыФормы.ПолеТабличногоДокумента;
	ТекущаяОбласть    = ТабличныйДокумент.ТекущаяОбласть;
	
	ОбластьКодЧисловой         = ТабличныйДокумент.Области.КодЧисловой;
	ОбластьНаименованиеКраткое = ТабличныйДокумент.Области.НаименованиеКраткое;
	ОбластьНаименованиеПолное  = ТабличныйДокумент.Области.НаименованиеПолное;
	ОбластьКодАльфа2           = ТабличныйДокумент.Области.КодАльфа2;
	
	Если ТекущаяОбласть.Низ = ТекущаяОбласть.Верх Тогда
		
		КодЧисловой         = ТабличныйДокумент.Область(ТекущаяОбласть.Верх, ОбластьКодЧисловой.        Лево, ТекущаяОбласть.Низ, ОбластьКодЧисловой.        Право).Текст;
		НаименованиеКраткое = ТабличныйДокумент.Область(ТекущаяОбласть.Верх, ОбластьНаименованиеКраткое.Лево, ТекущаяОбласть.Низ, ОбластьНаименованиеКраткое.Право).Текст;
		НаименованиеПолное  = ТабличныйДокумент.Область(ТекущаяОбласть.Верх, ОбластьНаименованиеПолное. Лево, ТекущаяОбласть.Низ, ОбластьНаименованиеПолное. Право).Текст;
		КодАльфа2           = ТабличныйДокумент.Область(ТекущаяОбласть.Верх, ОбластьКодАльфа2.          Лево, ТекущаяОбласть.Низ, ОбластьКодАльфа2.          Право).Текст;
		
		// Проверка наличия выбранного элемента.
		Ссылка = Справочники.КлассификаторСтранМира.НайтиПоКоду(КодЧисловой);
		
		Если НЕ Ссылка.Пустая() Тогда
			Вопрос = "В справочнике ""Классификатор стран мира"" уже существует элемент с кодом """ + КодЧисловой + """! Открыть существующий?";
			Ответ  = Вопрос(Вопрос, РежимДиалогаВопрос.ДаНетОтмена, , КодВозвратаДиалога.Отмена, );
			
			Если      Ответ = КодВозвратаДиалога.Да Тогда
				Ссылка.ПолучитьФорму( , ВладелецФормы, ).Открыть();
				Возврат;
				
			ИначеЕсли Ответ = КодВозвратаДиалога.Отмена Тогда
				Возврат;
			КонецЕсли;
		КонецЕсли;
		
		// Создание нового элемента справочника.
		ФормаНовогоЭлемента = Справочники.КлассификаторСтранМира.ПолучитьФормуНовогоЭлемента(, ВладелецФормы, );
		
		ФормаНовогоЭлемента.Код                = КодЧисловой;
		ФормаНовогоЭлемента.Наименование       = НаименованиеКраткое;
		ФормаНовогоЭлемента.НаименованиеПолное = НаименованиеПолное;
		ФормаНовогоЭлемента.КодАльфа2          = КодАльфа2;
		
		ФормаНовогоЭлемента.Открыть();
		
	Иначе
		
		Форма = ПолучитьОбщуюФорму("ФормаПодбораИзКлассификатора", ЭтаФорма);
		Если Форма.Открыта() Тогда
			Форма.СписокВыбора.Очистить();
		Иначе			
			СтуктураКолонок = Новый Структура();
			СтуктураКолонок.Вставить("Код", Новый Структура("Заголовок, Ширина", "Код", 5));
			СтуктураКолонок.Вставить("Наименование", Новый Структура("Заголовок, Ширина", "Наименование"));
			СтуктураКолонок.Вставить("НаименованиеПолное", Новый Структура("Заголовок, Ширина", "Полное наименование"));
			СтуктураКолонок.Вставить("КодАльфа2", Новый Структура("Заголовок, Ширина", "Код Альфа2"));
			СтуктураКолонок.Вставить("Видимость", Новый Структура("Заголовок, Ширина", ""));	
			Форма.ТипСправочника = "КлассификаторСтранМира";
			Форма.НастроитьФорму(СтуктураКолонок);
		КонецЕсли;	
		
		СписокВыбора =  Форма.СписокВыбора;		
		
		Для ТекущаяСтрока = ТекущаяОбласть.Верх по ТекущаяОбласть.Низ Цикл
			
			КодЧисловой         = ТабличныйДокумент.Область(ТекущаяСтрока, ОбластьКодЧисловой.        Лево, ТекущаяСтрока, ОбластьКодЧисловой.        Право).Текст;
			НаименованиеКраткое = ТабличныйДокумент.Область(ТекущаяСтрока, ОбластьНаименованиеКраткое.Лево, ТекущаяСтрока, ОбластьНаименованиеКраткое.Право).Текст;
			НаименованиеПолное  = ТабличныйДокумент.Область(ТекущаяСтрока, ОбластьНаименованиеПолное. Лево, ТекущаяСтрока, ОбластьНаименованиеПолное. Право).Текст;				
			КодАльфа2           = ТабличныйДокумент.Область(ТекущаяСтрока, ОбластьКодАльфа2.          Лево, ТекущаяСтрока, ОбластьКодАльфа2.          Право).Текст;
			
			ЭтоЧисло = Истина;
			Попытка
				КодКакЧисло = Число(КодЧисловой);
			Исключение
				ЭтоЧисло = Ложь;
			КонецПопытки;
			
			ЕСли ЭтоЧисло Тогда
				СтрокаПодбора = СписокВыбора.Добавить();
				СтрокаПодбора.Код = КодЧисловой;				
				СтрокаПодбора.Наименование = СтрПолучитьСтроку(НаименованиеКраткое, 1);
				СтрокаПодбора.НаименованиеПолное = СтрПолучитьСтроку(НаименованиеПолное, 1);
				СтрокаПодбора.КодАльфа2 = СтрПолучитьСтроку(КодАльфа2, 1);
				СтрокаПодбора.Видимость = Справочники.КлассификаторСтранМира.НайтиПоКоду(КодЧисловой).Пустая();
				СтрокаПодбора.Переносить = СтрокаПодбора.Видимость;
			КонецЕсли;
			
		КонецЦикла;
		
		Форма.Открыть();
	КонецЕсли;
	
КонецПроцедуры