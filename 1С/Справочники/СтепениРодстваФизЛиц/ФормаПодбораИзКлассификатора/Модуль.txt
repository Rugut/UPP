////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

// Обработчик события ПриОткрытии формы.
//
Процедура ПриОткрытии()

	Макет = Справочники.СтепениРодстваФизЛиц.ПолучитьМакет("КлассификаторРодства");

	Макет.Параметры.Расшифровка = Истина; // чтобы работала расшифровка

	ТабличныйДокумент = ЭлементыФормы.ПолеТабличногоДокумента;
	
	ТабличныйДокумент.Очистить();
	ТабличныйДокумент.Вывести(Макет);

	ТабличныйДокумент.ФиксацияСверху      = 3;

	ТабличныйДокумент.ОтображатьЗаголовки = Ложь;
	ТабличныйДокумент.ОтображатьСетку     = Ложь;
	ТабличныйДокумент.ТолькоПросмотр      = Истина;

КонецПроцедуры

// Обработчик события ОбработкаРасшифровки элемента ПолеТабличногоДокумента.
//
Процедура ПолеТабличногоДокументаОбработкаРасшифровки(Элемент, Расшифровка, СтандартнаяОбработка)

	СтандартнаяОбработка = Ложь;

	// Получение значений полей выбранной строки.
	ТабличныйДокумент = ЭлементыФормы.ПолеТабличногоДокумента;
	ТекущаяОбласть    = ТабличныйДокумент.ТекущаяОбласть;

	ОбластьКод          = ТабличныйДокумент.Области.Код;
	ОбластьНаименование = ТабличныйДокумент.Области.Наименование;
	
	Если ТекущаяОбласть.Низ = ТекущаяОбласть.Верх Тогда
		
		Код          = ТабличныйДокумент.Область(ТекущаяОбласть.Верх, ОбластьКод.         Лево, ТекущаяОбласть.Низ, ОбластьКод.         Право).Текст;
		Наименование = ТабличныйДокумент.Область(ТекущаяОбласть.Верх, ОбластьНаименование.Лево, ТекущаяОбласть.Низ, ОбластьНаименование.Право).Текст;
		
		// Проверка наличия выбранного элемента.
		Ссылка = Справочники.СтепениРодстваФизЛиц.НайтиПоНаименованию(Наименование);

		Если НЕ Ссылка.Пустая() Тогда
			
			Вопрос = "В справочнике ""Степени родства физических лиц"" уже существует элемент с наименованием """ + Наименование + """! Открыть существующий?";
			Ответ  = Вопрос(Вопрос, РежимДиалогаВопрос.ДаНетОтмена, , КодВозвратаДиалога.Отмена, );
			
			Если      Ответ = КодВозвратаДиалога.Да Тогда
				
				Ссылка.ПолучитьФорму( , ВладелецФормы, ).Открыть();
				Возврат;

			ИначеЕсли Ответ = КодВозвратаДиалога.Отмена Тогда
				Возврат;
			КонецЕсли;
			
		КонецЕсли;

		// Создание нового элемента справочника.
		ФормаНовогоЭлемента = Справочники.СтепениРодстваФизЛиц.ПолучитьФормуНовогоЭлемента(, ВладелецФормы, );
		ФормаНовогоЭлемента.Наименование = Наименование;
		ФормаНовогоЭлемента.Код	      	 = Код;
		ФормаНовогоЭлемента.Открыть();
		
	Иначе
		
		Форма = ПолучитьОбщуюФорму("ФормаПодбораИзКлассификатора", ЭтаФорма);
		Если Форма.Открыта() Тогда
			Форма.СписокВыбора.Очистить();
		Иначе			
			СтруктураКолонок = Новый Структура();
			СтруктураКолонок.Вставить("Код", Новый Структура("Заголовок, Ширина", "Код", 5));
			СтруктураКолонок.Вставить("Наименование", Новый Структура("Заголовок, Ширина", "Наименование"));
			СтруктураКолонок.Вставить("Видимость", Новый Структура("Заголовок, Ширина", ""));	
			Форма.ТипСправочника = "СтепениРодстваФизЛиц";
			Форма.НастроитьФорму(СтруктураКолонок);
		КонецЕсли;	
		
		СписокВыбора =  Форма.СписокВыбора;		
		
		Для ТекущаяСтрока = ТекущаяОбласть.Верх по ТекущаяОбласть.Низ Цикл
			
			Код          = ТабличныйДокумент.Область(ТекущаяСтрока, ОбластьКод.         Лево, ТекущаяСтрока, ОбластьКод.         Право).Текст;
			Наименование = ТабличныйДокумент.Область(ТекущаяСтрока, ОбластьНаименование.Лево, ТекущаяСтрока, ОбластьНаименование.Право).Текст;
			
			ЭтоЧисло = Истина;
			Попытка
				КодКакЧисло = Число(Код);
			Исключение
				ЭтоЧисло = Ложь;
			КонецПопытки;
			
			ЕСли ЭтоЧисло Тогда
				СтрокаПодбора = СписокВыбора.Добавить();
				СтрокаПодбора.Код = Код;				
				СтрокаПодбора.Наименование = Наименование;
				СтрокаПодбора.Видимость = Справочники.СтепениРодстваФизЛиц.НайтиПоКоду(Код).Пустая();
				СтрокаПодбора.Переносить = СтрокаПодбора.Видимость;
			КонецЕсли;
			
		КонецЦикла;
		
		Форма.Открыть();		
		
	КонецЕсли;
	
КонецПроцедуры