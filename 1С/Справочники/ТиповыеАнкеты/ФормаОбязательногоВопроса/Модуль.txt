////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОБЩЕГО НАЗНАЧЕНИЯ

Процедура УстановитьДоступность()
	
	Если	Обязательный = Перечисления.ОбязательностьЗаполненияОтветаНаВопрос.НеОбязателенКЗаполнению ИЛИ
			Обязательный = Перечисления.ОбязательностьЗаполненияОтветаНаВопрос.УсловноНеЗаполнять Тогда
		ЭлементыФормы.Надпись1.Заголовок = "Ответ на вопрос """ + ВопросИсходный + """ не обязателен к заполнению при условии, что на вопрос ";
	Иначе
		ЭлементыФормы.Надпись1.Заголовок = "Ответ на вопрос """ + ВопросИсходный + """ обязателен к заполнению при условии, что на вопрос ";
	КонецЕсли;
	
	ЭлементыФормы.Надпись3.Заголовок = "Ответ на вопрос """ + ВопросИсходный + """ ...";
	
	Обязательность = Перечисления.ОбязательностьЗаполненияОтветаНаВопрос;
	
	ЭлементыДоступны = (Обязательный <> Обязательность.НеОбязателенКЗаполнению) И (Обязательный <> Обязательность.ОбязателенКЗаполнению);
	
	ЭлементыФормы.Надпись1.Доступность		= ЭлементыДоступны;
	ЭлементыФормы.Надпись2.Доступность		= ЭлементыДоступны;
	ЭлементыФормы.ВопросУсловия.Доступность	= ЭлементыДоступны;
	ЭлементыФормы.УсловиеОтвета.Доступность	= ЭлементыДоступны;
	ЭлементыФормы.Ответ.Доступность			= ЭлементыДоступны;
	
КонецПроцедуры

Процедура УстановитьВидимость()
	
	УсловияОтвета = Перечисления.ВидыУсловийОбязательныхОтветов;
	
	ЭлементыФормы.СписокЗначенийОтвета.Видимость	= (УсловиеОтвета = УсловияОтвета.ВСписке) ИЛИ (УсловиеОтвета = УсловияОтвета.НеВСписке);
	ЭлементыФормы.Ответ.Видимость					= (УсловиеОтвета <> УсловияОтвета.ПустоеЗначение) И (УсловиеОтвета <> УсловияОтвета.ЛюбоеЗначение) И (УсловиеОтвета <> УсловияОтвета.ВСписке) И (УсловиеОтвета <> УсловияОтвета.НеВСписке);
	
КонецПроцедуры

Функция ПолучитьСписокУсловий(ТипОтвета)
	
	СписокУсловий = Новый СписокЗначений;
	Если ТипЗнч(ТипОтвета) = Тип("Строка") Тогда
		СписокУсловий.Добавить(Перечисления.ВидыУсловийОбязательныхОтветов.ПустоеЗначение);
		СписокУсловий.Добавить(Перечисления.ВидыУсловийОбязательныхОтветов.ЛюбоеЗначение);
		
	ИначеЕсли ТипЗнч(ТипОтвета) = Тип("Число") ИЛИ ТипЗнч(ТипОтвета) = Тип("Дата") Тогда
		СписокУсловий.Добавить(Перечисления.ВидыУсловийОбязательныхОтветов.Больше);
		СписокУсловий.Добавить(Перечисления.ВидыУсловийОбязательныхОтветов.БольшеИлиРавно);
		СписокУсловий.Добавить(Перечисления.ВидыУсловийОбязательныхОтветов.Меньше);
		СписокУсловий.Добавить(Перечисления.ВидыУсловийОбязательныхОтветов.МеньшеИлиРавно);
		
	ИначеЕсли ТипЗнч(ТипОтвета) = Тип("Булево") Тогда
		СписокУсловий.Добавить(Перечисления.ВидыУсловийОбязательныхОтветов.Равно);
		СписокУсловий.Добавить(Перечисления.ВидыУсловийОбязательныхОтветов.НеРавно);
		
	Иначе // иначе это либо справочник, либо вариант ответа
		СписокУсловий.Добавить(Перечисления.ВидыУсловийОбязательныхОтветов.ПустоеЗначение);
		СписокУсловий.Добавить(Перечисления.ВидыУсловийОбязательныхОтветов.ЛюбоеЗначение);
		СписокУсловий.Добавить(Перечисления.ВидыУсловийОбязательныхОтветов.Равно);
		СписокУсловий.Добавить(Перечисления.ВидыУсловийОбязательныхОтветов.НеРавно);
		СписокУсловий.Добавить(Перечисления.ВидыУсловийОбязательныхОтветов.ВСписке);
		СписокУсловий.Добавить(Перечисления.ВидыУсловийОбязательныхОтветов.НеВСписке);
		
	КонецЕсли;
	
	Возврат СписокУсловий;
	
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

Процедура ПриОткрытии()
	
	Если НЕ ЗначениеЗаполнено(Обязательный) тогда
		Обязательный = Перечисления.ОбязательностьЗаполненияОтветаНаВопрос.НеОбязателенКЗаполнению;
	КонецЕсли;
	
	ЭлементыФормы.УсловиеОтвета.ДоступныеЗначения = ПолучитьСписокУсловий(ВопросУсловия.ТипЗначения);
	
	УстановитьДоступность();
	УстановитьВидимость();
	
	ЭлементыФормы.СписокЗначенийОтвета.ОтборСтрок.ВопросВладелец.Установить(ВопросИсходный);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

Процедура ОбязательныйПриИзменении(Элемент)
	
	УстановитьДоступность();
	
КонецПроцедуры

Процедура ВопросУсловияНачалоВыбора(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;

	ВыбранноеЗначение	 = СписокВопросовДляВыбора.ВыбратьЭлемент("Выберите вопрос", Элемент.Значение);
	Если ВыбранноеЗначение <> Неопределено тогда
		Элемент.Значение = ВыбранноеЗначение.Значение;
		Ответ = Элемент.Значение.ТипЗначения.ПривестиЗначение();
		Если Элемент.Значение.ТипЗначения.СодержитТип(Тип("СправочникСсылка.ВариантыОтветовОпросов")) Тогда
			ЭлементыФормы.Ответ.ВыборПоВладельцу = Элемент.Значение;
		КонецЕсли;
		ЭлементыФормы.УсловиеОтвета.ДоступныеЗначения = ПолучитьСписокУсловий(ВопросУсловия.ТипЗначения);
		ЭлементыФормы.СписокЗначенийОтвета.ОтборСтрок.ВопросВладелец.Значение 		= ВопросИсходный;
		ЭлементыФормы.СписокЗначенийОтвета.ОтборСтрок.ВопросВладелец.Использование 	= Истина;
		УсловиеОтвета = Перечисления.ВидыУсловийОбязательныхОтветов.ПустаяСсылка();
		УстановитьВидимость();
	КонецЕсли;
	
КонецПроцедуры

Процедура УсловиеОтветаПриИзменении(Элемент)
	
	УстановитьВидимость();
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ДЕЙСТВИЯ КОМАНДНЫХ ПАНЕЛЕЙ ФОРМЫ

Процедура ОсновныеДействияФормыОК(Кнопка)
	
	Закрыть("ОК");
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ТАБЛИЧНОГО ПОЛЯ СписокЗначенийОтвета

Процедура СписокЗначенийОтветаПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	Если НоваяСтрока тогда
		Элемент.ТекущиеДанные.ЗначениеСписка = ВопросУсловия.ТипЗначения.ПривестиЗначение();
		Элемент.ТекущиеДанные.ВопросВладелец = ВопросИсходный;
	КонецЕсли;
	
КонецПроцедуры

Процедура СписокЗначенийОтветаПередНачаломДобавления(Элемент, Отказ, Копирование)
	
	Если НЕ ЗначениеЗаполнено(ВопросУсловия) тогда
		Сообщить("Выберите вопрос условия");
		Отказ = Истина;
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОПЕРАТОРЫ ОСНОВНОЙ ПРОГРАММЫ