Перем мДеревоРегистров Экспорт;
 Перем мПостроительОтчета Экспорт;

// Процедура предназначена для заполнения дерева таблиц регистров, которые
// могут служить источниками данных.
//
Процедура ЗаполнитьДеревоРегистров() Экспорт

	НовыйИсточник=мДеревоРегистров.Строки.Добавить();
	НовыйИсточник.ОписаниеПоля="Разделы управленческого учета";

	Для Каждого МетаданныеРегистр Из Метаданные.РегистрыНакопления Цикл

		Если МетаданныеРегистр.Ресурсы.Количество()=0 Тогда
			Продолжить;
		КонецЕсли;
		
		РегистрИсточник=НовыйИсточник.Строки.Добавить();
		РегистрИсточник.Регистр="РегистрНакопления."+МетаданныеРегистр.Имя;
		РегистрИсточник.ОписаниеПоля=МетаданныеРегистр.Представление();
		
		РегистрИсточникТаблица=РегистрИсточник.Строки.Добавить();
		РегистрИсточникТаблица.Регистр="РегистрНакопления."+МетаданныеРегистр.Имя;
		РегистрИсточникТаблица.ПредставлениеПоля=МетаданныеРегистр.Представление()+": движения регистра";
		РегистрИсточникТаблица.ОписаниеПоля="Движения";
		
		Если МетаданныеРегистр.ВидРегистра = Метаданные.СвойстваОбъектов.ВидРегистраНакопления.Остатки Тогда
			
			РегистрИсточникТаблицаПриход=РегистрИсточникТаблица.Строки.Добавить();
			РегистрИсточникТаблицаПриход.Регистр="РегистрНакопления."+МетаданныеРегистр.Имя;
			РегистрИсточникТаблицаПриход.ПредставлениеПоля=МетаданныеРегистр.Представление()+" движения: приход";
			РегистрИсточникТаблицаПриход.ОписаниеПоля="движения: приход";		
			РегистрИсточникТаблицаПриход.ИмяПоля=МетаданныеРегистр.Имя+"ДвиженияПриход";
			
			РегистрИсточникТаблицаРасход=РегистрИсточникТаблица.Строки.Добавить();
			РегистрИсточникТаблицаРасход.Регистр="РегистрНакопления."+МетаданныеРегистр.Имя;
			РегистрИсточникТаблицаРасход.ПредставлениеПоля=МетаданныеРегистр.Представление()+" движения: расход";
			РегистрИсточникТаблицаРасход.ОписаниеПоля="движения: расход";		
			РегистрИсточникТаблицаРасход.ИмяПоля=МетаданныеРегистр.Имя+"ДвиженияРасход";
			
		Иначе
			
			РегистрИсточникТаблицаДвижение=РегистрИсточникТаблица.Строки.Добавить();
			РегистрИсточникТаблицаДвижение.Регистр="РегистрНакопления."+МетаданныеРегистр.Имя;
			РегистрИсточникТаблицаДвижение.ПредставлениеПоля=МетаданныеРегистр.Представление()+" движения: оборот";
			РегистрИсточникТаблицаДвижение.ОписаниеПоля="движения: оборот";		
			РегистрИсточникТаблицаДвижение.ИмяПоля=МетаданныеРегистр.Имя+"ДвиженияОборот";

		КонецЕсли;
		
		Если МетаданныеРегистр.ВидРегистра = Метаданные.СвойстваОбъектов.ВидРегистраНакопления.Остатки Тогда

			РегистрИсточникТаблица=РегистрИсточник.Строки.Добавить();
			РегистрИсточникТаблица.Регистр="РегистрНакопления."+МетаданныеРегистр.Имя
											+".Остатки(&МоментВремени,)";
			РегистрИсточникТаблица.ПредставлениеПоля=МетаданныеРегистр.Представление()+": остатки";
			РегистрИсточникТаблица.ОписаниеПоля="Остатки";
			РегистрИсточникТаблица.ИмяПоля=МетаданныеРегистр.Имя+"Остатки";

			РегистрИсточникТаблица=РегистрИсточник.Строки.Добавить();
			РегистрИсточникТаблица.Регистр="РегистрНакопления."+МетаданныеРегистр.Имя
								            +".Обороты(&НачалоПериода,&КонецПериода,День,)";
			РегистрИсточникТаблица.ПредставлениеПоля=МетаданныеРегистр.Представление()+": обороты";
			РегистрИсточникТаблица.ОписаниеПоля="Обороты";
			РегистрИсточникТаблица.ИмяПоля=МетаданныеРегистр.Имя+"Обороты";

			РегистрИсточникТаблица=РегистрИсточник.Строки.Добавить();
			РегистрИсточникТаблица.Регистр="РегистрНакопления."+МетаданныеРегистр.Имя
            								+".ОстаткиИОбороты(&НачалоПериода,&КонецПериода,День,,)";
			РегистрИсточникТаблица.ПредставлениеПоля=МетаданныеРегистр.Представление()+": остатки и обороты";
			РегистрИсточникТаблица.ОписаниеПоля="Остатки и обороты";
			РегистрИсточникТаблица.ИмяПоля=МетаданныеРегистр.Имя+"ОстаткиИОбороты";

		Иначе

			РегистрИсточникТаблица=РегистрИсточник.Строки.Добавить();
			РегистрИсточникТаблица.Регистр="РегистрНакопления."+МетаданныеРегистр.Имя
             								+".Обороты(&НачалоПериода,&КонецПериода,День,)";
			РегистрИсточникТаблица.ПредставлениеПоля=МетаданныеРегистр.Представление()+": обороты";
			РегистрИсточникТаблица.ОписаниеПоля="Обороты";
			РегистрИсточникТаблица.ИмяПоля=МетаданныеРегистр.Имя+"Обороты";

		КонецЕсли;

	КонецЦикла;

	НовыйИсточник=мДеревоРегистров.Строки.Добавить();
	НовыйИсточник.ОписаниеПоля="Справочная информация";

	Для Каждого МетаданныеРегистр Из Метаданные.РегистрыСведений Цикл

        ЧисловыхРесурсов=0;

		Для каждого Ресурс Из МетаданныеРегистр.Ресурсы Цикл	

			ТипыРесурса=Ресурс.Тип.Типы();

			Если ТипыРесурса.Количество() = 1 И ТипыРесурса[0]=Тип("Число") Тогда
				ЧисловыхРесурсов=ЧисловыхРесурсов+1;
			КонецЕсли;
		
		КонецЦикла;                                 
		Если ЧисловыхРесурсов=0 Тогда
			Продолжить;
		КонецЕсли;

		РегистрИсточник=НовыйИсточник.Строки.Добавить();

		Если Строка(МетаданныеРегистр.ПериодичностьРегистраСведений)="Непериодический" Тогда
			РегистрИсточник.Регистр="РегистрСведений."+МетаданныеРегистр.Имя;
		Иначе
			РегистрИсточник.Регистр="РегистрСведений."+МетаданныеРегистр.Имя+".СрезПоследних(&МоментВремени,)";
        КонецЕсли;
		
		РегистрИсточник.ПредставлениеПоля=МетаданныеРегистр.Представление();
		РегистрИсточник.ОписаниеПоля=МетаданныеРегистр.Представление();
		РегистрИсточник.ИмяПоля=МетаданныеРегистр.Имя;

	КонецЦикла;	

	НовыйИсточник=мДеревоРегистров.Строки.Добавить();
	НовыйИсточник.ОписаниеПоля="Остатки и обороты по планам счетов";

	Для Каждого МетаданныеРегистр Из Метаданные.РегистрыБухгалтерии Цикл

        Если МетаданныеРегистр.Ресурсы.Количество()=0 Тогда
			Продолжить;
		КонецЕсли;

		РегистрИсточник=НовыйИсточник.Строки.Добавить();
		РегистрИсточник.Регистр="РегистрБухгалтерии."+МетаданныеРегистр.Имя;
		РегистрИсточник.ОписаниеПоля=МетаданныеРегистр.Представление();

		РегистрИсточникТаблица=РегистрИсточник.Строки.Добавить();
		РегистрИсточникТаблица.Регистр="РегистрБухгалтерии."+МетаданныеРегистр.Имя
										+".Обороты(&НачалоПериода,&КонецПериода,День,,,,,)";
		РегистрИсточникТаблица.ПредставлениеПоля=МетаданныеРегистр.Представление()+": обороты";
		РегистрИсточникТаблица.ОписаниеПоля="Обороты";
		РегистрИсточникТаблица.ИмяПоля=МетаданныеРегистр.Имя+"Обороты";
		
		Если МетаданныеРегистр.Корреспонденция Тогда
			
			РегистрИсточникТаблица=РегистрИсточник.Строки.Добавить();
			РегистрИсточникТаблица.Регистр="РегистрБухгалтерии."+МетаданныеРегистр.Имя
			+".ОборотыДтКт(&НачалоПериода,&КонецПериода,День,,,,,)";
			РегистрИсточникТаблица.ПредставлениеПоля=МетаданныеРегистр.Представление()+": обороты с корреспонденцией";
			РегистрИсточникТаблица.ОписаниеПоля="Обороты с корреспонденцией";
			РегистрИсточникТаблица.ИмяПоля=МетаданныеРегистр.Имя+"ОборотыДтКт";
			
		КонецЕсли;
		
		РегистрИсточникТаблица=РегистрИсточник.Строки.Добавить();
		РегистрИсточникТаблица.Регистр="РегистрБухгалтерии."+МетаданныеРегистр.Имя
										+".ДвиженияССубконто(&НачалоПериода,&КонецПериода,)";
		РегистрИсточникТаблица.ПредставлениеПоля=МетаданныеРегистр.Представление()+": движения с субконто";
		РегистрИсточникТаблица.ОписаниеПоля="Движения с субконто";
		РегистрИсточникТаблица.ИмяПоля=МетаданныеРегистр.Имя+"ДвиженияССубконто";

		РегистрИсточникТаблица=РегистрИсточник.Строки.Добавить();
		РегистрИсточникТаблица.Регистр="РегистрБухгалтерии."+МетаданныеРегистр.Имя
										+".Остатки(&МоментВремени,,,) ";
		РегистрИсточникТаблица.ПредставлениеПоля=МетаданныеРегистр.Представление()+": остатки";
		РегистрИсточникТаблица.ОписаниеПоля="Остатки";
		РегистрИсточникТаблица.ИмяПоля=МетаданныеРегистр.Имя+"Остатки";

		РегистрИсточникТаблица=РегистрИсточник.Строки.Добавить();
		РегистрИсточникТаблица.Регистр="РегистрБухгалтерии."+МетаданныеРегистр.Имя
        								+".ОстаткиИОбороты(&НачалоПериода,&КонецПериода,День,,,,) ";
		РегистрИсточникТаблица.ПредставлениеПоля=МетаданныеРегистр.Представление()+": остатки и обороты";
		РегистрИсточникТаблица.ОписаниеПоля="Остатки и обороты";
		РегистрИсточникТаблица.ИмяПоля=МетаданныеРегистр.Имя+"ОстаткиИОбороты";

	КонецЦикла;

	НовыйИсточник=мДеревоРегистров.Строки.Добавить();
	НовыйИсточник.ОписаниеПоля="Отборы, определенные пользователем";
	
	ЗапросИсточникТаблица=НовыйИсточник.Строки.Добавить();
	ЗапросИсточникТаблица.Регистр="Запрос.НДСПоПланамПродаж";
	ЗапросИсточникТаблица.ПредставлениеПоля="НДС по планам продаж";
	ЗапросИсточникТаблица.ОписаниеПоля="НДС по планам продаж";
	ЗапросИсточникТаблица.ИмяПоля="НДСпоПланамПродаж";


КонецПроцедуры // ЗаполнитьДеревоРегистров()


////////////////////////////////////////////////////////////////////////////////
// ОПЕРАТОРЫ ОСНОВНОЙ ПРОГРАММЫ
// 

мДеревоРегистров = Новый ДеревоЗначений;
мДеревоРегистров.Колонки.Добавить("Регистр");
мДеревоРегистров.Колонки.Добавить("ОписаниеПоля");
мДеревоРегистров.Колонки.Добавить("ИмяПоля");
мДеревоРегистров.Колонки.Добавить("ПредставлениеПоля");