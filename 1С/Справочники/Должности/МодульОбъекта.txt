#Если ТолстыйКлиентОбычноеПриложение Тогда
	
Функция Печать(НазваниеОтчета = Неопределено ,Результат = Неопределено) Экспорт
	
	Если Результат = Неопределено тогда
		Результат = Новый ТабличныйДокумент;
	КонецЕсли;
	
	Результат.ТолькоПросмотр  = Истина;
	Результат.ОтображатьСетку = Ложь;
	Результат.ОтображатьЗаголовки = Ложь;
	
	
	СхемаКомпоновкиДанных = ПолучитьМакет("ОсновнаяСхемаКомпоновкиДанных");
	
	// Получить настройки схемы компоновки данных
	Настройки = СхемаКомпоновкиДанных.НастройкиПоУмолчанию;
	ТиповыеОтчеты.ДобавитьОтбор(Настройки.Отбор, "Должность", Ссылка);
	
	// очистим результирующий табличный документ
	//Результат.АвтоМасштаб = Истина;
	Результат.Очистить();
	
	// получим макет компоновки данных
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
	МакетКомпоновки   = КомпоновщикМакета.Выполнить(СхемаКомпоновкиДанных, Настройки, );
	
	// создадим и инициализируем процессор компоновки данных
	ПроцессорКомпоновки = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновки.Инициализировать(МакетКомпоновки, , );
	
	// выведем отчет в табличный документ "Результат"
	ВывестиРезультатВТабличныйДокумент(ПроцессорКомпоновки, Результат);
	
	Возврат Результат;

	
КонецФункции

Процедура ВывестиРезультатВТабличныйДокумент(ПроцессорКомпоновкиДанных, ТабличныйДокумент) Экспорт
	
	// Создадим и инициализируем процессор вывода результата
	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент;
	ПроцессорВывода.УстановитьДокумент(ТабличныйДокумент);

	// Обозначим начало вывода
	ПроцессорВывода.НачатьВывод();
	Состояние(НСТР("ru='Если Вы хотите прервать вывод отчета, нажмите Ctrl+Break'"));
	
	// Основной цикл вывода отчета
	Пока Истина Цикл

		// Получим следующий элемент результата компоновки
		ЭлементРезультата = ПроцессорКомпоновкиДанных.Следующий();
		
		Если ЭлементРезультата = Неопределено Тогда
			
			// Следующий элемент не получен - заканчиваем цикл вывода
			Прервать;
			
		Иначе
			
			// Элемент получен - выведем его при помощи процессора вывода
			ПроцессорВывода.ВывестиЭлемент(ЭлементРезультата);
			
		КонецЕсли;
		
		ОбработкаПрерыванияПользователя();
		
	КонецЦикла;
	
	ПроцессорВывода.ЗакончитьВывод();
	
КонецПроцедуры

#КонецЕсли