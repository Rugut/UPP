Перем МаксСекунд, ЧислоСекунд, ЧислоПопыток;
Перем ПовторныйЗапрос;

Процедура ПередОткрытием(Отказ, СтандартнаяОбработка)
	Если ТипЗнч(ОтчетСсылка) = Тип("ДокументСсылка.РегламентированныйОтчет") Тогда
		Заголовок = РегламентированнаяОтчетность.ПредставлениеДокументаРеглОтч(ОтчетСсылка);
	Иначе
		Заголовок = ОтчетСсылка.Наименование;
	КонецЕсли;
	ЭлементыФормы.НадписьОтчетПредставление.Заголовок = Заголовок;
	ЭлементыФормы.ДатаОтправки.Заголовок = Формат(ДатаОтправки, "ДЛФ=DDT");
	ПовторныйЗапрос = Ложь;
КонецПроцедуры

Процедура ПриОткрытии()
	ЧислоПопыток = 0;
	//изменить
	МаксСекунд = 60;
	ЧислоСекунд = МаксСекунд;
	ОбновитьПанельЗапроса();
	ПодключитьОбработчикОжидания("ОжиданиеЗапроса", 1);
КонецПроцедуры

Процедура ОжиданиеЗапроса()
	Если ЧислоСекунд < 1 Тогда
		РезультатОтправки = РегламентированнаяОтчетность.ПолучитьРезультатОтправкиФСС(ИдентификаторОтправкиНаСервере);
		ОбработатьРезультат(РезультатОтправки);
	Иначе
		ЧислоСекунд = ЧислоСекунд - 1;
		ОбновитьПанельЗапроса();
	КонецЕсли;
КонецПроцедуры

Процедура ОбработатьРезультат(РезультатОтправки)
	Попытка
		Заблокировать();
	Исключение
		Предупреждение("Не удалось заблокировать объект """ + ЭтотОбъект + """!
		|" + ОписаниеОшибки(), 60);
		ОтключитьОбработчикОжидания("ОжиданиеЗапроса");
		Возврат;
	КонецПопытки;
	
	Если РезультатОтправки <> Неопределено Тогда
		ДатаПолученияРезультата = РезультатОтправки.ДатаПолученияРезультата;
		СтатусОтправки = РезультатОтправки.СтатусОтправки;
		Протокол = Новый ХранилищеЗначения(РезультатОтправки.Протокол);
		Если РезультатОтправки.Квитанция <> Неопределено Тогда
			Квитанция = Новый ХранилищеЗначения(РезультатОтправки.Квитанция);
		КонецЕсли;
	КонецЕсли;
	
	Записать();
	Разблокировать();
	
	Если СтатусОтправки = Перечисления.СтатусыОтправки.Отправлен Тогда
		ЧислоСекунд = МаксСекунд;
		Если ЧислоПопыток = 3 Тогда
			ОтключитьОбработчикОжидания("ОжиданиеЗапроса");
			Активизировать();
			ПоказатьПротоколОбработки();
		Иначе
			ЧислоПопыток = ЧислоПопыток + 1;
			ПовторныйЗапрос = Истина;	
		КонецЕсли;
		
		ОбновитьПанельЗапроса();
		Возврат;
	Иначе
		ОтключитьОбработчикОжидания("ОжиданиеЗапроса");
		Активизировать();
		ПоказатьПротоколОбработки()
	КонецЕсли;
	
	ОбновитьФорму();
	
КонецПроцедуры

Процедура ОбновитьПанельЗапроса()
	
	Если ЧислоПопыток = 3 Тогда
		ЭлементыФормы.НадписьОсталосьСекунд.Заголовок = "Результатов отправки отчета получить не удалось.";	
		ЭлементыФормы.Индикатор.Видимость = Ложь;
	Иначе
		Если НЕ ПовторныйЗапрос Тогда
			ЭлементыФормы.НадписьОсталосьСекунд.Заголовок = "Отчет отправлен.
			|Результаты отправки будут получены через некоторое время (" + Строка(ЧислоСекунд) +" с)";
		Иначе
			ЭлементыФормы.НадписьОсталосьСекунд.Заголовок = "Результатов отправки отчета получить не удалось. 
			|Через некоторое время будет предпринята ещё одна попытка (" + Строка(ЧислоСекунд) +" с)";
		КонецЕсли;														
	КонецЕсли;
	
	
	ЭлементыФормы.Индикатор.Значение = МаксСекунд - ЧислоСекунд;	
КонецПроцедуры

Процедура ОбновитьФорму()
	Если СтатусОтправки <> Перечисления.СтатусыОтправки.Отправлен Тогда
		ПанельЗапроса = ЭлементыФормы.ПанельЗапроса;
		НадписьРезультат = ЭлементыФормы.НадписьРезультат;
		
		ЦветШрифта = Новый Цвет(255, 0, 0);
		ЗаголовокРезультата = "Ошибка механизма отправки";
			
		ПанельЗапроса.ТекущаяСтраница = ПанельЗапроса.Страницы.СтраницаРезультата;
		Если СтатусОтправки = Перечисления.СтатусыОтправки.НеПринят Тогда
			ЦветШрифта = Новый Цвет(255, 0, 0);
			ЗаголовокРезультата = "Отчет не принят, получен протокол ошибок";
		ИначеЕсли СтатусОтправки = Перечисления.СтатусыОтправки.Сдан Тогда
			ЦветШрифта = Новый Цвет(0, 179, 16);			
			ЗаголовокРезультата = "Отчет сдан, получена квитанция";
		КонецЕсли;	
		
		НадписьРезультат.ЦветТекста = ЦветШрифта;
		НадписьРезультат.Заголовок = ЗаголовокРезультата;
	КонецЕсли;
КонецПроцедуры

Процедура НадписьРезультатНажатие(Элемент)
	// покажем протокол
	ПоказатьПротоколОбработки();
КонецПроцедуры

Процедура ПоказатьПротоколОбработки()
	ЗаголовокПротокола = "Протокол обработки";
	РегламентированнаяОтчетность.ПоказатьПротоколОбработки(Протокол.Получить(), ЗаголовокПротокола);
КонецПроцедуры
 
Процедура НадписьОтчетПредставлениеНажатие(Элемент)
	ОткрытьЗначение(ОтчетСсылка);
КонецПроцедуры

Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	Если Имясобытия = "Изменение статуса отправки регламентированного отчета" И ЗначениеЗаполнено(Параметр) И Источник = ОтчетСсылка Тогда
		Прочитать();
		Если СтатусОтправки <> Перечисления.СтатусыОтправки.Отправлен Тогда
			ОтключитьОбработчикОжидания("ОжиданиеЗапроса");	
		КонецЕсли;
		ОбновитьФорму();
	КонецЕсли;
КонецПроцедуры