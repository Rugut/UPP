////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

// Обработчик события ПриОткрытии формы.
//
Процедура ПриОткрытии()

	Макет = Справочники.ОснованияУвольненияИзОрганизации.ПолучитьМакет("СписокОснованийУвольнения");

	Макет.Параметры.Расшифровка = Истина; // чтобы работала расшифровка

	ТабличныйДокумент = ЭлементыФормы.ПолеТабличногоДокумента;
	
	ТабличныйДокумент.Очистить();
	ТабличныйДокумент.Вывести(Макет);

	ТабличныйДокумент.ФиксацияСверху      = 3;

	ТабличныйДокумент.ОтображатьЗаголовки = Ложь;
	ТабличныйДокумент.ОтображатьСетку     = Ложь;
	ТабличныйДокумент.ТолькоПросмотр      = Истина;

КонецПроцедуры

// Обработчик события ОбработкаРасшифровки элемента ПолеТабличногоДокумента.
//
Процедура ПолеТабличногоДокументаОбработкаРасшифровки(Элемент, Расшифровка, СтандартнаяОбработка)

	СтандартнаяОбработка = Ложь;

	// Получение значений полей выбранной строки.
	ТабличныйДокумент = ЭлементыФормы.ПолеТабличногоДокумента;
	ТекущаяОбласть    = ТабличныйДокумент.ТекущаяОбласть;

	ОбластьКод          = ТабличныйДокумент.Области.Код;
	ОбластьНаименование = ТабличныйДокумент.Области.Наименование;
	ОбластьСтатья	    = ТабличныйДокумент.Области.Статья;
	
	Если ТекущаяОбласть.Низ = ТекущаяОбласть.Верх Тогда
		
		Наименование = ТабличныйДокумент.Область(ТекущаяОбласть.Верх, ОбластьНаименование.Лево, ТекущаяОбласть.Низ, ОбластьНаименование.Право).Текст;
		Статья       = ТабличныйДокумент.Область(ТекущаяОбласть.Верх, ОбластьСтатья.Лево, ТекущаяОбласть.Низ, ОбластьСтатья.Право).Текст;
		
		// Проверка наличия выбранного элемента.
		Ссылка = Справочники.ОснованияУвольненияИзОрганизации.НайтиПоНаименованию(Статья + ". " + Наименование);
		
		Если НЕ Ссылка.Пустая() Тогда
			
			Вопрос = "В справочнике " + ОбщегоНазначения.ПреобразоватьСтрокуИнтерфейса("""Основания увольнения из организации""") + " уже существует элемент с наименованием """ + Наименование + """! Открыть существующий?";
			Ответ  = Вопрос(Вопрос, РежимДиалогаВопрос.ДаНетОтмена, , КодВозвратаДиалога.Отмена, );
			
			Если      Ответ = КодВозвратаДиалога.Да Тогда
				
				Ссылка.ПолучитьФорму( , ВладелецФормы, ).Открыть();
				Возврат;
				
			ИначеЕсли Ответ = КодВозвратаДиалога.Отмена Тогда
				Возврат;
			КонецЕсли;
			
		КонецЕсли;
		
		// Создание нового элемента справочника.
		ФормаНовогоЭлемента = Справочники.ОснованияУвольненияИзОрганизации.ПолучитьФормуНовогоЭлемента(, ВладелецФормы, );
		ФормаНовогоЭлемента.Наименование	= Статья;
		ФормаНовогоЭлемента.ТекстОснования	= Наименование;
		ФормаНовогоЭлемента.Открыть();
		
	Иначе
		
		Форма = ПолучитьОбщуюФорму("ФормаПодбораИзКлассификатора", ЭтаФорма);
		Если Форма.Открыта() Тогда
			Форма.СписокВыбора.Очистить();
		Иначе
			СтруктураКолонок = Новый Структура();
			СтруктураКолонок.Вставить("Наименование", Новый Структура("Заголовок, Ширина", "Наименование"));
			СтруктураКолонок.Вставить("Видимость", Новый Структура("Заголовок, Ширина", ""));
			Форма.ТипСправочника = "ОснованияУвольненияИзОрганизации";
			Форма.НастроитьФорму(СтруктураКолонок);
		КонецЕсли;
		
		СписокВыбора =  Форма.СписокВыбора;
		
		Для ТекущаяСтрока = ТекущаяОбласть.Верх по ТекущаяОбласть.Низ Цикл
			
			Код				= ТабличныйДокумент.Область(ТекущаяСтрока, ОбластьКод.			Лево, ТекущаяСтрока, ОбластьКод.			Право).Текст;
			Наименование	= ТабличныйДокумент.Область(ТекущаяСтрока, ОбластьНаименование.	Лево, ТекущаяСтрока, ОбластьНаименование.	Право).Текст;
			Статья			= ТабличныйДокумент.Область(ТекущаяСтрока, ОбластьСтатья.		Лево, ТекущаяСтрока, ОбластьСтатья.			Право).Текст;
			
			Если ЗначениеЗаполнено(Код) И ОбщегоНазначения.ТолькоЦифрыВСтроке(Код,,Ложь) Тогда
				СтрокаПодбора = СписокВыбора.Добавить();
				СтрокаПодбора.Наименование	= Статья + ". " + Наименование;
				СтрокаПодбора.Видимость		= Справочники.ОснованияУвольненияИзОрганизации.НайтиПоНаименованию(Статья + ". " + Наименование).Пустая();
				СтрокаПодбора.Переносить	= СтрокаПодбора.Видимость;
			КонецЕсли;
			
		КонецЦикла;
		
		Форма.Открыть();
	КонецЕсли;
	
КонецПроцедуры