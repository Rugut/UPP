Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	флЭтоПБОЮЛ = РегламентированнаяОтчетность.ЭтоПБОЮЛ(Владелец);
	
	Если ПустаяСтрока(Код) Тогда
		ОбщегоНазначения.СообщитьОбОшибке("Не задан код налогового органа.", Отказ);
	ИначеЕсли СтрДлина(СокрЛП(Код)) <> 4 Тогда
		ОбщегоНазначения.СообщитьОбОшибке("Код налогового органа должен состоять из 4 символов.", Отказ);
	КонецЕсли;
	
	Если НЕ флЭтоПБОЮЛ Тогда
		Если ПустаяСтрока(КПП) Тогда
			ОбщегоНазначения.СообщитьОбОшибке("Не заполнен КПП.", Отказ);
		ИначеЕсли СтрДлина(СокрЛП(КПП)) <> 9 Тогда
			ОбщегоНазначения.СообщитьОбОшибке("КПП должен состоять из 9 символов.", Отказ);
		КонецЕсли;
	Иначе
	    //для ПБОЮЛ очищаем КПП
		КПП = "";
	КонецЕсли;
	
	СуществующаяЗапись = РегламентированнаяОтчетность.ПолучитьПоКодамРегистрациюВИФНС(Владелец, Код, КПП);
	
	Если СуществующаяЗапись <> Неопределено И СуществующаяЗапись <> Ссылка Тогда
		ОбщегоНазначения.СообщитьОбОшибке("Для данной организации уже существует запись с указанными кодом налогового органа" 
		+ ?(флЭтоПБОЮЛ, ".", " и КПП."), Отказ);
	КонецЕсли; 
		
	Если ПустаяСтрока(НаименованиеИФНС) Тогда
		ОбщегоНазначения.СообщитьОбОшибке("Не заполнено полное наименование налогового органа.", Отказ);
	Конецесли;

	Если ЗначениеЗаполнено(Представитель) И ПустаяСтрока(ДокументПредставителя) Тогда
		ОбщегоНазначения.СообщитьОбОшибке("Не задан документ, подтверждающий полномочия представителя.", Отказ);
	КонецЕсли;
		
	Если НЕ Отказ Тогда
		Если ПустаяСтрока(Наименование) Тогда
			Наименование = НаименованиеИФНС;
		КонецЕсли;
		Если НЕ ЗначениеЗаполнено(Представитель) Тогда
			ДокументПредставителя = "";
			УполномоченноеЛицоПредставителя = "";
		ИначеЕсли Метаданные.Справочники.Найти("ФизическиеЛица") <> Неопределено И ТипЗнч(Представитель) = Тип("СправочникСсылка.ФизическиеЛица") Тогда
			УполномоченноеЛицоПредставителя = "";
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры