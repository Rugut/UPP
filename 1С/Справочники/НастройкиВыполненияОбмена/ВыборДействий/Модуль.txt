Процедура ОсновныеДействияФормыКнопкаОтмена(Кнопка)
	
	Закрыть(Ложь);
		
КонецПроцедуры

Функция ПолучитьИмяЭлементаПеречисления(ЗначениеПеречисления)
	
	ИмяЭлемента = Строка(ЗначениеПеречисления);
	Для каждого ЭлементПеречисления Из Метаданные.Перечисления[ЗначениеПеречисления.Метаданные().Имя].ЗначенияПеречисления Цикл
		Если ЭлементПеречисления.Синоним = Строка(ЗначениеПеречисления) Тогда
			ИмяЭлемента = ЭлементПеречисления.Имя;
			Прервать;
		КонецЕсли; 
	КонецЦикла;
	
	Возврат ИмяЭлемента;
	
КонецФункции // ПолучитьПериодичностьДляЗапросаПоЗначениюПеречисления()

Процедура ОсновныеДействияФормыКнопкаОК(Кнопка)
	
	СтруктураСохранения = Новый Структура();	
	
	Для Каждого СтрокаТаблицы Из ТаблицаДействий Цикл
		
		СтруктураСохранения.Вставить(ПолучитьИмяЭлементаПеречисления(СтрокаТаблицы.Действие), СтрокаТаблицы.Флаг);
		
	КонецЦикла;
	
	СохранитьЗначение("Справочники.НастройкиВыполненияОбмена.ВыборДействий.СтруктураВыбранныхЭлементов", СтруктураСохранения);
	
	Закрыть(Истина);
	
КонецПроцедуры

Процедура ПередОткрытием(Отказ, СтандартнаяОбработка)
	
	СтруктураВыбранныхЭлементов = ВосстановитьЗначение("Справочники.НастройкиВыполненияОбмена.ВыборДействий.СтруктураВыбранныхЭлементов");
		
	Если СтруктураВыбранныхЭлементов = Неопределено Тогда
		СтруктураВыбранныхЭлементов = Новый Структура("ЗагрузкаДанных,ВыгрузкаДанных", Истина, Истина);
	КонецЕсли;
	
	Для Каждого СтрокаПеречисления Из Метаданные.Перечисления.ДействиеПриОбмене.ЗначенияПеречисления Цикл
		
		СтрокаТаблицы = ТаблицаДействий.Добавить();
		СтрокаТаблицы.Действие = Перечисления.ДействиеПриОбмене[СтрокаПеречисления.Имя];
		
		ЗначениеФлага = Ложь;
		Если СтруктураВыбранныхЭлементов.Свойство(СтрокаПеречисления.Имя, ЗначениеФлага) Тогда
			
			СтрокаТаблицы.Флаг = ЗначениеФлага;
			
		Иначе
			
			СтрокаТаблицы.Флаг = Ложь;
			
		КонецЕсли;		
				
	КонецЦикла;	
	
КонецПроцедуры

Процедура ТаблицаДействийПриВыводеСтроки(Элемент, ОформлениеСтроки, ДанныеСтроки)
	
	Если ДанныеСтроки.Действие = Перечисления.ДействиеПриОбмене.ВыгрузкаДанных Тогда
		ОформлениеСтроки.Ячейки.Действие.Картинка = БиблиотекаКартинок.ЗаписатьИзменения;
	ИначеЕсли ДанныеСтроки.Действие = Перечисления.ДействиеПриОбмене.ЗагрузкаДанных Тогда
		ОформлениеСтроки.Ячейки.Действие.Картинка = БиблиотекаКартинок.ПрочитатьИзменения;
	ИначеЕсли ДанныеСтроки.Действие = Перечисления.ДействиеПриОбмене.ОтложенныеДвижения Тогда
		ОформлениеСтроки.Ячейки.Действие.Картинка = БиблиотекаКартинок.Провести;
	КонецЕсли;
	
	ОформлениеСтроки.Ячейки.Действие.ОтображатьКартинку = Истина;
	
КонецПроцедуры