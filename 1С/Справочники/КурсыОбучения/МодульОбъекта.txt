////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОКУМЕНТА

#Если ТолстыйКлиентОбычноеПриложение Тогда
	
// Печать курса обучения
//  
// Параметры
//  Нет
//
Функция Печать() Экспорт
	
	ТабДокумент = Новый ТабличныйДокумент;
	ТабДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_КурсОбучения";
	Макет = ПолучитьМакет("Макет");
	
	ОбластьМакета = Макет.ПолучитьОбласть("Шапка");
	ОбластьМакета.Параметры.Заполнить(ЭтотОбъект);
	
	Если НЕ ЗначениеЗаполнено(ВидДокументаОбОбразовании) тогда	
		ОбластьМакета.Параметры.ВидДокументаОбОбразовании = "документы не выдаются"
	КонецЕсли;
	
	ТабДокумент.Вывести(ОбластьМакета);
	
	// группа компетенций 
	ТабДокумент.НачатьГруппуСтрок("Компетенции",Истина);
	ТабДокумент.Вывести(Макет.ПолучитьОбласть("ШапкаСтрокКомпетенций"));
	
	ОбластьМакета = Макет.ПолучитьОбласть("СтрокаКомпетенции");
	Для Каждого СтрокаТЧ из ИзучаемыеКомпетенции Цикл
		ОбластьМакета.Параметры.Компетенция  = СтрокаТЧ.Компетенция;
		ОбластьМакета.Параметры.Длительность = СтрокаТЧ.Вес * ДлительностьКурса / 100;
		ТабДокумент.Вывести(ОбластьМакета);
	КонецЦикла;
	
	ТабДокумент.ЗакончитьГруппуСтрок();
	
	// группа форм обучения
	// Эти части имеет смысл выводить, если есть хотя бы одно занятие.
	Если ЗанятияКурса.Количество() > 0 Тогда
		
		ТабДокумент.Вывести(Макет.ПолучитьОбласть("СоставПоФормеОбучения"));
		ТабДокумент.НачатьГруппуСтрок("Формы",Истина);	
		
		ТабДокумент.Вывести(Макет.ПолучитьОбласть("ШапкаСоставПоФормеОбучения"));
		
		Запрос = Новый Запрос();
		Запрос.УстановитьПараметр("Ссылка",Ссылка);
		
		Запрос.Текст =  
		"ВЫБРАТЬ
		|	СУММА(КурсыОбученияЗанятияКурса.Занятие.ДлительностьЗанятия) КАК Длительность,
		|	ЕСТЬNULL(КурсыОбученияЗанятияКурса.Занятие.ВидЗанятия.Наименование, ""форма обучения не указана"") КАК ФормаОбучения
		|ИЗ
		|	Справочник.КурсыОбучения.ЗанятияКурса КАК КурсыОбученияЗанятияКурса
		|ГДЕ
		|	КурсыОбученияЗанятияКурса.Ссылка = &Ссылка
		|
		|СГРУППИРОВАТЬ ПО
		|	КурсыОбученияЗанятияКурса.Занятие.ВидЗанятия";
		
		ТабФормОбучения = Запрос.Выполнить().Выгрузить();
		
		ОбластьМакета = Макет.ПолучитьОбласть("СтрокаФормыОбучения");
		Для Каждого СтрокаТЧ из ТабФормОбучения Цикл
			ОбластьМакета.Параметры.Заполнить(СтрокаТЧ);
			ТабДокумент.Вывести(ОбластьМакета);
		КонецЦикла;
		ТабДокумент.ЗакончитьГруппуСтрок();
		
		// занятия
		ТабДокумент.Вывести(Макет.ПолучитьОбласть("ШапкаЗанятия"));
		
		Запрос.Текст =  
		"ВЫБРАТЬ
		|	КурсыОбученияЗанятияКурса.Занятие.Наименование КАК Занятие,
		|	КурсыОбученияЗанятияКурса.Занятие.ДлительностьЗанятия КАК Длительность,
		|	ЕСТЬNULL(КурсыОбученияЗанятияКурса.Занятие.ВидЗанятия.Наименование, ""форма обучения не указана"") КАК ФормаОбучения
		|ИЗ
		|	Справочник.КурсыОбучения.ЗанятияКурса КАК КурсыОбученияЗанятияКурса
		|ГДЕ
		|	КурсыОбученияЗанятияКурса.Ссылка = &Ссылка
		|
		|УПОРЯДОЧИТЬ ПО
		|	КурсыОбученияЗанятияКурса.НомерСтроки";
		ТабЗанятияКурса = Запрос.Выполнить().Выгрузить();
		
		ОбластьМакета = Макет.ПолучитьОбласть("СтрокаЗанятия");
		Для Каждого СтрокаТЧ из ТабЗанятияКурса Цикл
			ОбластьМакета.Параметры.Заполнить(СтрокаТЧ);
			ТабДокумент.Вывести(ОбластьМакета);    
		КонецЦикла;
		
	КонецЕсли; // Вывод если есть занятия
	
	Возврат УниверсальныеМеханизмы.НапечататьДокумент(ТабДокумент,,, "Данные по: " + Наименование);
	
КонецФункции

#КонецЕсли