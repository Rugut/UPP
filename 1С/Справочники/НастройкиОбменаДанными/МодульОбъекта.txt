// при записи
Процедура ПриЗаписи(Отказ) 
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли; 
	
	// надо проверить наличие одной записи в регистре сведений
	НаборЗаписейПараметров = РегистрыСведений.ПараметрыОбменаДанными.СоздатьНаборЗаписей();
		
	НаборЗаписейПараметров.Отбор.НастройкаОбменаДанными.Установить(Ссылка);	
			
	НаборЗаписейПараметров.Прочитать();
		
	Если НаборЗаписейПараметров.Количество() = 1 Тогда
		// запись есть, ничего делать не надо
		Возврат;
	КонецЕсли;
	
	// добавляем только одну запись и все
	ЗаписьДатПараметров = НаборЗаписейПараметров.Добавить();
	ЗаписьДатПараметров.НастройкаОбменаДанными = Ссылка;
		
	Попытка
		НаборЗаписейПараметров.Записать();
	Исключение
		ОбщегоНазначения.СообщитьОбОшибке("Ошибка при записи изменений в настройку обмена """ + Ссылка + """ : " + ОписаниеОшибки(), Отказ);
	КонецПопытки;	
				
КонецПроцедуры

Функция ПолучитьДлинуСтрокиХранилища(Хранилище) Экспорт
	
	ТекстПравилаОбмена = Хранилище.Получить();
	РазмерСтроки = СтрДлина(ТекстПравилаОбмена);
	Возврат РазмерСтроки;
	
КонецФункции

// Перед записью
Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
		
	БазоваяПоставка = (Найти(ВРег(Метаданные.Имя), "БАЗОВАЯ") > 0);
	
	Если ПустаяСтрока(ВерсияПлатформыИнформационнойБазыДляПодключения) Тогда
		ВерсияПлатформыИнформационнойБазыДляПодключения = "V81";		
	КонецЕсли;	
	
	Если НЕ ЗначениеЗаполнено(ТипНастройки) Тогда
		ОбщегоНазначения.СообщитьОбОшибке("Не заполнено поле ""Тип настройки""", Отказ);
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(УзелИнформационнойБазы) Тогда
		ОбщегоНазначения.СообщитьОбОшибке("Не заполнено поле ""Узел""", Отказ);
	КонецЕсли;
	
	Если БазоваяПоставка
		И ПроцедурыОбменаДанными.ОпределитьПоУзлуОбменаЭтоРИБ(УзелИнформационнойБазы) Тогда
		
		ОбщегоНазначения.СообщитьОбОшибке("В базовой версии недоступен обмен через распределенную информационную базу", Отказ);	
		
	КонецЕсли;
	
	ЭтотУзелОбмена = ПроцедурыОбменаДанными.ПолучитьТекущийУзелИБ(УзелИнформационнойБазы);
	Если ЭтотУзелОбмена = УзелИнформационнойБазы Тогда
		
		ОбщегоНазначения.СообщитьОбОшибке("В качестве узла обмена выбран узел, соответствующий текущей информационной базе.", Отказ);
				
	КонецЕсли;
	
	Если OnLineОбмен Тогда
		
		РазмерОсновногоХранилищаПравил = ПолучитьДлинуСтрокиХранилища(ПравилаОбмена);
		
		Если (ПроизводитьПриемСообщений
			ИЛИ ПроизводитьОтправкуСообщений)
			И РазмерОсновногоХранилищаПравил = 0 Тогда
			
			ОбщегоНазначения.СообщитьОбОшибке("Не заданы правила обмена данными для выгрузки в базу обмена", Отказ);		
			
		КонецЕсли;			
		
		Если ТипНастройки = Перечисления.ТипыАвтоматическогоОбменаДанными.ОбменЧерезComСоединение Тогда
						
			Если БазоваяПоставка Тогда
				
				ОбщегоНазначения.СообщитьОбОшибке("В базовой версии недоступен Режим обмена через COM - соединение", Отказ);	
				
			КонецЕсли;			
			
			Если ПроизводитьПриемСообщений Тогда
			
				РазмерХранилищаПравилЗагрузки = ПолучитьДлинуСтрокиХранилища(ПравилаОбменаДляПриемника);
				
				Если РазмерХранилищаПравилЗагрузки = 0 Тогда
					
					ОбщегоНазначения.СообщитьОбОшибке("Не заданы правила для выгрузки из базы обмена в текущую базу", Отказ);		
					
				КонецЕсли;
				
			КонецЕсли;
			
			Если ТипИнформационнойБазыДляПодключения Тогда
				
				Если НЕ ЗначениеЗаполнено(КаталогИнформационнойБазыДляПодключения) Тогда
					ОбщегоНазначения.СообщитьОбОшибке("Не заполнено поле ""Каталог информационной базы для подключения""", Отказ);
				КонецЕсли;
				
			Иначе
				
				Если НЕ ЗначениеЗаполнено(ИмяСервераИнформационнойБазыДляПодключения) Тогда
					ОбщегоНазначения.СообщитьОбОшибке("Не заполнено поле ""Сервер 1С:Предприятия""", Отказ);
				КонецЕсли;
				
				Если НЕ ЗначениеЗаполнено(ИмяИнформационнойБазыНаСервереДляПодключения) Тогда
					ОбщегоНазначения.СообщитьОбОшибке("Не заполнено поле ""Имя информационной базы""", Отказ);
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЕсли;
		
	Иначе
		
		Если ТипНастройки = Перечисления.ТипыАвтоматическогоОбменаДанными.ОбменЧерезComСоединение Тогда
		
			ОбщегоНазначения.СообщитьОбОшибке("Режим обмена через COM - соединение возможно только для обмена по правилам обмена", Отказ);		
			
		КонецЕсли;
		
	КонецЕсли;	
				
	// в зависимости от типа настройки очищаем настройки остальных типов
	Если ТипНастройки = Перечисления.ТипыАвтоматическогоОбменаДанными.ОбменЧерезФайловыйРесурс Тогда
		
		КаталогОбменаИнформацией = СокрЛП(КаталогОбменаИнформацией);
						
	ИначеЕсли ТипНастройки = Перечисления.ТипыАвтоматическогоОбменаДанными.ОбменЧерезFTPРесурс Тогда
		
		Если ПустаяСтрока(ПользовательFTPСоединения) Тогда
			ПарольFTPСоединения = "";	
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(ПортFTPСоединения) Тогда
			ПортFTPСоединения = 21;	
		КонецЕсли;
					
	КонецЕсли;
	
	Если НЕ ПроизводитьПриемСообщений
		ИЛИ НЕ ПроизводитьОтправкуСообщений Тогда
		
		ПроизводитьОтправкуТолькоПриУспешномПриеме = Ложь;	
		
	КонецЕсли;
		
	Если Не Отказ Тогда 
		ТипУзлаИнформационнойБазы = Новый(ТипЗнч(УзелИнформационнойБазы));
	КонецЕсли;
			
КонецПроцедуры