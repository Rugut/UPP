Перем УдалятьЗаписиНастройкиПрав;

Перем УдалятьНастройкиПравБезПредупреждений Экспорт;

Перем ПроверятьНаследуемыеПрава;


////////////////////////////////////////////////////////////////////////////////
// ОБЩИЕ ПРОЦЕДУРЫ

Процедура ЗаписатьУнаследованныеПраваДляВсехГрупп()

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ТекущаяГруппа", Ссылка);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	НастройкиПравДоступаПользователей.ОбъектДоступа,
	|	&ТекущаяГруппа КАК Пользователь,
	|	НастройкиПравДоступаПользователей.ВидОбъектаДоступа,
	|	НастройкиПравДоступаПользователей.ОбластьДанных,
	|	НастройкиПравДоступаПользователей.ВладелецПравДоступа,
	|	ИСТИНА КАК НаследованаОтВсеПользователи,
	|	НастройкиПравДоступаПользователей.Чтение,
	|	НастройкиПравДоступаПользователей.Запись,
	|	НастройкиПравДоступаПользователей.ВидНаследованияПравДоступаИерархическихСправочников
	|ИЗ
	|	РегистрСведений.НастройкиПравДоступаПользователей КАК НастройкиПравДоступаПользователей
	|ГДЕ
	|	НастройкиПравДоступаПользователей.Пользователь = ЗНАЧЕНИЕ(Справочник.ГруппыПользователей.ПустаяСсылка)";

	РезультатЗапроса = Запрос.Выполнить();
	Если НЕ РезультатЗапроса.Пустой() Тогда
		Набор = РегистрыСведений.НастройкиПравДоступаПользователей.СоздатьНаборЗаписей();
		Набор.Отбор.Пользователь.Установить(Ссылка);
		Выборка = РезультатЗапроса.Выбрать();
		Пока Выборка.Следующий() Цикл
			ЗаполнитьЗначенияСвойств(Набор.Добавить(), Выборка);
		КонецЦикла;
		Набор.Записать();
	КонецЕсли;
	
КонецПроцедуры


////////////////////////////////////////////////////////////////////////////////
// СОБЫТИЯ ОБЪЕКТА

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ПроверятьНаследуемыеПрава = ЭтоНовый();
	
	УдалитьПриНеобходимостиНастройкиПравДоступа();
	
КонецПроцедуры

Процедура УдалитьПриНеобходимостиНастройкиПравДоступа()
	
	Если ЭтоНовый() Тогда
		Возврат;
	КонецЕсли; 
	
	Если ПометкаУдаления Тогда
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ ПЕРВЫЕ 1
		               |	1 КАК Поле1
		               |ИЗ
		               |	Справочник.ГруппыПользователей КАК ГруппыПользователей
		               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.НастройкиПравДоступаПользователей КАК НастройкиПравДоступа
		               |		ПО НастройкиПравДоступа.Пользователь = ГруппыПользователей.Ссылка
		               |ГДЕ
		               |	ГруппыПользователей.Ссылка = &Ссылка
		               |	И (НЕ ГруппыПользователей.ПометкаУдаления)";
					   
		Запрос.УстановитьПараметр("Ссылка", Ссылка);
		
		РезультатЗапроса = Запрос.Выполнить();
		
		Если НЕ РезультатЗапроса.Пустой() Тогда
			
			Если НЕ УдалятьНастройкиПравБезПредупреждений Тогда
				#Если Клиент Тогда
					ОтветНаВопрос = Вопрос("Удалить настройки прав доступа связанные с группой """ + Наименование + """ (без возможности их восстановления)?", РежимДиалогаВопрос.ДаНет);
					УдалятьЗаписиНастроек = (ОтветНаВопрос = КодВозвратаДиалога.Да);				
				#Иначе
					УдалятьЗаписиНастроек = Истина;
				#КонецЕсли			
			Иначе
				УдалятьЗаписиНастроек = Истина;
			КонецЕсли;
			
			Если УдалятьЗаписиНастроек Тогда
				Набор = РегистрыСведений.НастройкиПравДоступаПользователей.СоздатьНаборЗаписей();
				Набор.Отбор.Пользователь.Установить(Ссылка);
				Набор.Записать();			
			КонецЕсли;			
		КонецЕсли;
		
	Иначе
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ ПЕРВЫЕ 1 1 ИЗ Справочник.ГруппыПользователей ГДЕ Ссылка = &Ссылка И ПометкаУдаления";
		Запрос.УстановитьПараметр("Ссылка", Ссылка);
		Если НЕ Запрос.Выполнить().Пустой() Тогда
			ЗаписатьУнаследованныеПраваДляВсехГрупп();
		КонецЕсли;		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ПроверятьНаследуемыеПрава Тогда
		ЗаписатьУнаследованныеПраваДляВсехГрупп();		
	КонецЕсли;
	
КонецПроцедуры

УдалятьНастройкиПравБезПредупреждений = Ложь;
ПроверятьНаследуемыеПрава = Ложь;