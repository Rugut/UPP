// Определяет вид актива для РБП.
//
// Параметры:
//  СчетСписания   - ПланСчетовСсылка - ссылка на счет, по которому определяется 
//                  вид актива (обязательный параметр).
//  
Функция ОпределитьВидАктиваРБП(СчетСписания) Экспорт
	
	Если Не ЗначениеЗаполнено(СчетСписания) Тогда
		Возврат "";
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА Хозрасчетный.Ссылка В ИЕРАРХИИ (&Счет01)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыАктивовДляРБП.ОсновныеСредства)
	|		ИНАЧЕ ВЫБОР
	|				КОГДА Хозрасчетный.Ссылка В ИЕРАРХИИ (&Счет08)
	|					ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыАктивовДляРБП.ПрочиеВнеоборотныеАктивы)
	|				ИНАЧЕ ВЫБОР
	|						КОГДА Хозрасчетный.Ссылка В ИЕРАРХИИ (&СчетЗатрат)
	|							ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыАктивовДляРБП.Запасы)
	|						ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ВидыАктивовДляРБП.ПрочиеОборотныеАктивы)
	|					КОНЕЦ
	|			КОНЕЦ
	|	КОНЕЦ КАК ВидАктива
	|ИЗ
	|	ПланСчетов.Хозрасчетный КАК Хозрасчетный
	|ГДЕ
	|	Хозрасчетный.Ссылка = &ТекущийСчет";
	
	Запрос.УстановитьПараметр("ТекущийСчет", СчетСписания);
	Запрос.УстановитьПараметр("Счет01", ПланыСчетов.Хозрасчетный.ОсновныеСредства);
	Запрос.УстановитьПараметр("Счет08", ПланыСчетов.Хозрасчетный.ВложенияВоВнеоборотныеАктивы);
	Массив = Новый Массив;
	Массив.Добавить(ПланыСчетов.Хозрасчетный.ОсновноеПроизводство_);
	Массив.Добавить(ПланыСчетов.Хозрасчетный.ВспомогательныеПроизводства);
	Массив.Добавить(ПланыСчетов.Хозрасчетный.ОбщепроизводственныеРасходы);
	Массив.Добавить(ПланыСчетов.Хозрасчетный.ОбщехозяйственныеРасходы);
	Массив.Добавить(ПланыСчетов.Хозрасчетный.БракВПроизводстве);
	Массив.Добавить(ПланыСчетов.Хозрасчетный.ВспомогательныеПроизводства);
	Массив.Добавить(ПланыСчетов.Хозрасчетный.ОбслуживающиеПроизводства);
	Массив.Добавить(ПланыСчетов.Хозрасчетный.РасходыНаПродажу);
	
	Запрос.УстановитьПараметр("СчетЗатрат", Массив);
	
	Результат = Запрос.Выполнить().Выбрать();
	Результат.Следующий();
	
	Возврат Результат.ВидАктива;
	
КонецФункции

// Процедура устанавливает новый реквизит "Вид актива" справочника РБП
//
// Параметры:
//  нет
//
Процедура УстановитьВидАктиваДляРБП() Экспорт
	
	РБП = Справочники.РасходыБудущихПериодов;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ХозрасчетныйОбороты.Субконто1 КАК РБПна76
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Обороты(&ДатаНачала, &ДатаОкончания, , Счет В ИЕРАРХИИ (&Счет76), &ВидСубконтоРБП, (ВЫРАЗИТЬ(Субконто1 КАК Справочник.РасходыБудущихПериодов)) <> ЗНАЧЕНИЕ(Справочник.РасходыБудущихПериодов.ПустаяСсылка), , ) КАК ХозрасчетныйОбороты";
	
	Запрос.УстановитьПараметр("ВидСубконтоРБП", ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.РасходыБудущихПериодов);
	Запрос.УстановитьПараметр("ДатаНачала",    '20081231000000');
	Запрос.УстановитьПараметр("ДатаОкончания", '20111231235959');
	Запрос.УстановитьПараметр("Счет76",        ПланыСчетов.Хозрасчетный.ПлатежиПоДобровольномуСтрахованиюРаботников);
	
	Результат = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = Результат.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		РБПОбъект = ВыборкаДетальныеЗаписи.РБПна76.ПолучитьОбъект();
		РБПОбъект.ВидАктива = Перечисления.ВидыАктивовДляРБП.ДебиторскаяЗадолженность;
		
		Попытка
			РБПОбъект.Записать();
		Исключение
			ШаблонОшибки = НСтр("ru = 'Не удалось установить вид актива для статьи РБП %1, рекомендуется установить самостоятельно значение ""Дебиторская задолженность""'");
			ТекстОшибки  = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				ШаблонОшибки, СокрЛП(РБПОбъект.Наименование));
			ОбщегоНазначения.СообщитьОбОшибке(ТекстОшибки);
		КонецПопытки;
	КонецЦикла;
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	РасходыБудущихПериодов.Ссылка КАК РБПНа97
	|ИЗ
	|	Справочник.РасходыБудущихПериодов КАК РасходыБудущихПериодов
	|ГДЕ
	|	(НЕ РасходыБудущихПериодов.ЭтоГруппа)
	|	И (НЕ РасходыБудущихПериодов.ВидАктива = ЗНАЧЕНИЕ(Перечисление.ВидыАктивовДляРБП.ДебиторскаяЗадолженность))";
	
	Результат = Запрос.Выполнить();
	ВыборкаДетальныеЗаписи = Результат.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		РБПОбъект = ВыборкаДетальныеЗаписи.РБПНа97.ПолучитьОбъект();
		РБПОбъект.ВидАктива = Перечисления.ВидыАктивовДляРБП.ПрочиеОборотныеАктивы;
		Попытка
			РБПОбъект.Записать();
		Исключение
			ШаблонОшибки = НСтр("ru = 'Не удалось установить вид актива для статьи РБП %1, рекомендуется установить самостоятельно значение ""Прочие оборотные активы""'");
			ТекстОшибки  = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				ШаблонОшибки, СокрЛП(РБПОбъект.Наименование));
			ОбщегоНазначения.СообщитьОбОшибке(ТекстОшибки);
		КонецПопытки;
	КонецЦикла;
	
КонецПроцедуры