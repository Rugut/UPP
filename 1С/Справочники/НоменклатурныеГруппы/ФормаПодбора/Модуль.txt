// Процедура - обработчик события ПриЗакрытии
//
Процедура ПриЗакрытии()
	
	Параметры = Новый Структура;
	Параметры.Вставить("ЗапрашиватьКоличество", ЗапрашиватьКоличество);
	
	СохранитьЗначение("НоменклатурныеГруппы_ФормаПодбора_Параметры", Параметры);
	
КонецПроцедуры // ПриЗакрытии()

// Процедура - обработчик события ПриОткрытии
//
Процедура ПриОткрытии()
	
	Параметры = ВосстановитьЗначение("НоменклатурныеГруппы_ФормаПодбора_Параметры");
	
	Если Параметры <> Неопределено Тогда
		
		Если Параметры.Свойство("ЗапрашиватьКоличество") Тогда
			
			ЗапрашиватьКоличество = Параметры.ЗапрашиватьКоличество;
			
		Иначе
			
			ЗапрашиватьКоличество = Истина;
			
		КонецЕсли;
		
	Иначе
		
		ЗапрашиватьКоличество = Истина;
		
	КонецЕсли;
	
КонецПроцедуры // ПриЗакрытии()

// Процедура - обработчик события ВыборЗначения
//
Процедура СправочникСписокВыборЗначения(Элемент, СтандартнаяОбработка, Значение)
	
	Перем Команда;
	
	Если ПараметрВыборГруппИЭлементов = ИспользованиеГруппИЭлементов.Элементы И Значение.ЭтоГруппа Тогда
		
		Возврат;
		
	ИначеЕсли ПараметрВыборГруппИЭлементов = ИспользованиеГруппИЭлементов.Группы И НЕ Значение.ЭтоГруппа Тогда
		
		Возврат;
		
	Иначе
		
		СтандартнаяОбработка = Ложь;
		
	КонецЕсли;
	
	Параметры = Новый Структура;
	Параметры.Вставить("НоменклатурнаяГруппа", Значение);
	
	Если ТипЗнч(СтруктураИсходныхПараметров) = Тип("Структура") Тогда
		
		СтруктураИсходныхПараметров.Свойство("Команда", Команда);
		Параметры.Вставить("Команда", Команда);
		
	КонецЕсли;
	
	Количество = 1;
	
	Если ЗапрашиватьКоличество Тогда
		
		Если Не ВвестиЧисло(Количество, "Укажите количество", 15, 3) Тогда
			
			Возврат;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Параметры.Вставить("Количество", Количество);
	ОповеститьОВыборе(Параметры);
	
КонецПроцедуры // СправочникСписокВыборЗначения()