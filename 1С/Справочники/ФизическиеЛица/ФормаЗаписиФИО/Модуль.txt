////////////////////////////////////////////////////////////////////////////////
// ВСПОМОГАТЕЛЬНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

Процедура ЗаполнитьПоляИзНабора()
	
	Если НаборЗаписей.Количество() = 0 тогда
		НаборЗаписей.Добавить();
		ЗаписьИзНабора 			= НаборЗаписей[0];
		ЗаписьИзНабора.Период  	= ПериодПриОткрытии;
	КонецЕсли;
	
	ЗаписьИзНабора 				= НаборЗаписей[0];
	Фамилия						= ЗаписьИзНабора.Фамилия;
	Имя							= ЗаписьИзНабора.Имя;
	Отчество					= ЗаписьИзНабора.Отчество;
	Период 						= ?(ЗначениеЗаполнено(ЗаписьИзНабора.Период), ЗаписьИзНабора.Период, '19800101');
	
	ФамилияПриОткрытии	= Фамилия;
	ИмяПриОткрытии		= Имя;
	ОтчествоПриОткрытии = Отчество;
	ПериодПриОткрытии	= Период;
	
	ФизЛицо	= ?(Физлицо.Пустая(), ЗаписьИзНабора.Физлицо, Физлицо);
	
	Если ФизЛицо.Пустая() Тогда
		Кнопки = ЭлементыФормы.ОсновныеДействияФормы.Кнопки;
		Кнопки.Удалить(Кнопки.КнопкаИстория);
		Кнопки.Удалить(Кнопки.РазделительИстория);
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

Процедура ПриОткрытии()
	
	ЗаполнитьПоляИзНабора();
	
КонецПроцедуры

Процедура ПодтвердитьИзменения(Кнопка)
	
	ЗаписьИзНабора 			= НаборЗаписей[0];
	
	Если ФамилияПриОткрытии <> Фамилия ИЛИ 
		 ИмяПриОткрытии 	<> Имя ИЛИ 
		 ОтчествоПриОткрытии <> Отчество ИЛИ 
		 Период <> ПериодПриОткрытии Тогда
		 
		ЗаписьИзНабора.Физлицо	= Физлицо;
		 
		ЗаписьИзНабора.Период   = Период;
		ЗаписьИзНабора.Фамилия 	= Фамилия;
		ЗаписьИзНабора.Имя 		= Имя;
		ЗаписьИзНабора.Отчество	= Отчество;
		
		Оповестить("ОбновитьФорму", Новый Структура("ИмяЭлемента, ПолучательОповещения","ФИО", ВладелецФормы), ОбщегоНазначенияЗК.ОпределитьОповещаемоеФизЛицо(ФизЛицо, ВладелецФормы));
		
	КонецЕсли; 
	
	Закрыть();
		
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ФОРМЫ

Процедура КнопкаИсторияНажатие(Элемент)
	
	ФормаРегистра = РегистрыСведений.ФИОФизЛиц.ПолучитьФормуСписка();
	ФормаРегистра.РегистрСведенийСписок.Отбор.ФизЛицо.Значение = ФизЛицо;
	ФормаРегистра.РегистрСведенийСписок.Отбор.ФизЛицо.ВидСравнения = ВидСравнения.Равно;
	ФормаРегистра.РегистрСведенийСписок.Отбор.ФизЛицо.Использование = Истина;
	ФормаРегистра.ЭлементыФормы.РегистрСведенийСписок.НачальноеОтображениеСписка = НачальноеОтображениеСписка.Конец;
	
	ФормаРегистра.Открыть();
	
КонецПроцедуры