Функция ПохожиеФизлица(Фамилия, Имя = "", Отчество = "", ДатаРождения = Неопределено, ИНН = Неопределено, СтраховойНомерПФР = Неопределено) Экспорт
	
	НаименованиеФизлица = НаименованиеФизлица(Фамилия, Имя, Отчество);
	
	НаименованиеФизлица = СтрЗаменить(НаименованиеФизлица, "~", "~~");
	НаименованиеФизлица = СтрЗаменить(НаименованиеФизлица, "%", "~%");
	НаименованиеФизлица = СтрЗаменить(НаименованиеФизлица, "_", "~_");
	НаименованиеФизлица = СтрЗаменить(НаименованиеФизлица, "[", "~[");
	НаименованиеФизлица = СтрЗаменить(НаименованиеФизлица, "-", "~-");
	НаименованиеФизлица = НаименованиеФизлица+"%";

	
	НетДатыРождения = Не ЗначениеЗаполнено(ДатаРождения);
	НетИНН = Не (ЗначениеЗаполнено(ИНН) или РегламентированнаяОтчетность.ИННСоответствуетТребованиям(ИНН, Перечисления.ЮрФизЛицо.ФизЛицо));
	НетСтраховогоНомераПФР = Не (ЗначениеЗаполнено(СтрЗаменить(Лев(СтраховойНомерПФР,11),"-","")) или РегламентированнаяОтчетность.СтраховойНомерПФРСоответствуетТребованиям(СтраховойНомерПФР));
	
	Запрос = Новый Запрос;
	
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ФизическиеЛица.Ссылка
	|ИЗ
	|	Справочник.ФизическиеЛица КАК ФизическиеЛица
	|ГДЕ
	|	(НЕ ФизическиеЛица.ЭтоГруппа)
	|	И (&парамНетДатыРождения
	|			ИЛИ &парамДатаРождения = ФизическиеЛица.ДатаРождения)
	|	И (&парамНетИНН
	|			ИЛИ &парамИНН = ФизическиеЛица.ИНН)
	|	И (&парамНетСтраховогоНомераПФР
	|			ИЛИ &парамСтраховойНомерПФР = ФизическиеЛица.СтраховойНомерПФР)
	|	И ФизическиеЛица.Наименование ПОДОБНО &парамНаименование СПЕЦСИМВОЛ ""~""";
	
	Запрос.УстановитьПараметр("парамНаименование", НаименованиеФизлица);
	Запрос.УстановитьПараметр("парамНетДатыРождения", НетДатыРождения);
	Запрос.УстановитьПараметр("парамДатаРождения", ДатаРождения);
	Запрос.УстановитьПараметр("парамНетИНН", НетИНН);
	Запрос.УстановитьПараметр("парамИНН", ИНН);
	Запрос.УстановитьПараметр("парамНетСтраховогоНомераПФР", НетСтраховогоНомераПФР);
	Запрос.УстановитьПараметр("парамСтраховойНомерПФР", СтраховойНомерПФР);
	
	МассивФизлиц = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
	СписокФизлиц = Новый СписокЗначений;
	СписокФизлиц.ЗагрузитьЗначения(МассивФизлиц);
	
	Возврат СписокФизлиц;

КонецФункции // ПохожиеФизлица

Функция НаименованиеФизлица(Фамилия, Имя, Отчество) Экспорт

	Возврат СОКРЛП(?(ПустаяСтрока(Фамилия),"",СОКРЛП(Фамилия))
				+ ?(ПустаяСтрока(Имя),""," " + СОКРЛП(Имя))
				+ ?(ПустаяСтрока(Отчество),""," " + СОКРЛП(Отчество)));

КонецФункции // НаименованиеФизлица

////////////////////////////////////////////////////////////////////////////////
// Процедуры печати

Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт	
	
	ФизическиеЛицаПереопределяемый.Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода);
	
КонецПроцедуры	

Функция ПечатьСведенийОФизлице(Ссылка, ОбъектыПечати = Неопределено, НазваниеОтчета = Неопределено, Результат = Неопределено) Экспорт
	
	Возврат ФизическиеЛицаПереопределяемый.ПечатьСведенийОФизлице(Ссылка, ОбъектыПечати, НазваниеОтчета, Результат);
	
КонецФункции