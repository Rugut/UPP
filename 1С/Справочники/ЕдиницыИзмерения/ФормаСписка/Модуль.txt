////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ
Процедура ДействияФормыРедактироватьКод(Кнопка)
	МеханизмНумерацииОбъектов.ИзменениеВозможностиРедактированияНомера(Метаданные.Справочники.ЕдиницыИзмерения, ЭлементыФормы.СправочникСписок, ЭлементыФормы.ДействияФормы.Кнопки.Подменю, ЭлементыФормы.СправочникСписок.Колонки.Код);
КонецПроцедуры

// Обработчик события ПриОткрытии формы.
//
Процедура ПриОткрытии()

	ЕдиницаИзмеренияВеса = Константы.ЕдиницаИзмеренияВеса.Получить();

	Если НЕ ЗначениеЗаполнено(ЕдиницаИзмеренияВеса) Тогда
		ЭлементыФормы.СправочникСписок.Колонки.Вес.ТекстШапки = "Вес (не назначена единица измерения)";
	Иначе
		ЭлементыФормы.СправочникСписок.Колонки.Вес.ТекстШапки = "Вес (" + ЕдиницаИзмеренияВеса.Наименование +")";
	КонецЕсли;

	ЕдиницаИзмеренияОбъема = Константы.ЕдиницаИзмеренияОбъема.Получить();

	Если НЕ ЗначениеЗаполнено(ЕдиницаИзмеренияОбъема) Тогда
		ЭлементыФормы.СправочникСписок.Колонки.Объем.ТекстШапки = "Объем (не назначена единица измерения)";
	Иначе
		ЭлементыФормы.СправочникСписок.Колонки.Объем.ТекстШапки = "Объем (" + ЕдиницаИзмеренияОбъема.Наименование +")";
	КонецЕсли;
	Если ПараметрОтборПоВладельцу<>неопределено И ТипЗнч(ПараметрОтборПоВладельцу)<> тип("СправочникСсылка.Номенклатура") Тогда
		ЭлементыФормы.СправочникСписок.Колонки.ПорогОкругления.Видимость = ложь;
		ЭлементыФормы.СправочникСписок.Колонки.ПредупреждатьОНецелыхМестах.Видимость = ложь;
	КонецЕсли;
	
	МеханизмНумерацииОбъектов.ДобавитьВМенюДействияКнопкуРедактированияКода(ЭлементыФормы.ДействияФормы.Кнопки.Подменю);
	МеханизмНумерацииОбъектов.УстановитьДоступностьПоляВводаНомера(Метаданные.Справочники.ЕдиницыИзмерения, ЭлементыФормы.СправочникСписок, ЭлементыФормы.ДействияФормы.Кнопки.Подменю, ЭлементыФормы.СправочникСписок.Колонки.Код);
	
	// Запретим удалять колонку из данных списка, потому что обращение к ней выполняется при получении данных или при выводе строки
	СправочникСписок.Колонки.Добавить("ЕдиницаПоКлассификатору");
	
КонецПроцедуры

// Обработчик события ПриНачалеРедактирования элемента СправочникСписок.
//
Процедура СправочникСписокПриНачалеРедактирования(Элемент, НоваяСтрока)

	Если НоваяСтрока Тогда

		Если Элемент.ТекущиеДанные.Коэффициент = 0 Тогда
			Элемент.ТекущиеДанные.Коэффициент = 1;
		КонецЕсли;

		Если НЕ ЗначениеЗаполнено(Элемент.ТекущиеДанные.ЕдиницаПоКлассификатору) Тогда
			Элемент.ТекущиеДанные.ЕдиницаПоКлассификатору = УправлениеПользователями.ПолучитьЗначениеПоУмолчанию(глЗначениеПеременной("глТекущийПользователь"), "ОсновнаяЕдиницаПоКлассификатору");
		КонецЕсли;

	КонецЕсли;

КонецПроцедуры

// Обработчик события ПриИзменении элемента СправочникСписок.ЕдиницаПоКлассификатору.
//
Процедура СправочникСписокЕдиницаПоКлассификаторуПриИзменении(Элемент)

	ТекущиеДанные = ЭлементыФормы.СправочникСписок.ТекущиеДанные;

	Если ПустаяСтрока(ТекущиеДанные.Наименование) Тогда
		ТекущиеДанные.Наименование = ТекущиеДанные.ЕдиницаПоКлассификатору.Наименование;
	КонецЕсли;

КонецПроцедуры

 // Обработчик события ПриВыводеСтроки элемента СправочникСписок.
//
Процедура СправочникСписокПриВыводеСтроки(Элемент, ОформлениеСтроки, ДанныеСтроки)
	Если ЭлементыФормы.СправочникСписок.Колонки.ПорогОкругления.Видимость или ЭлементыФормы.СправочникСписок.Колонки.ПредупреждатьОНецелыхМестах.Видимость Тогда
    	ОформлениеСтроки.Ячейки.ПорогОкругления.ТолькоПросмотр = истина;
		ОформлениеСтроки.Ячейки.ПредупреждатьОНецелыхМестах.ТолькоПросмотр = истина;
	КонецЕсли;
	
	Если ДанныеСтроки.Владелец = Неопределено Тогда
		ОформлениеСтроки.Ячейки.Коэффициент.ТолькоПросмотр = Ложь;
	Иначе
		ОформлениеСтроки.Ячейки.Коэффициент.ТолькоПросмотр = (ДанныеСтроки.Ссылка = ДанныеСтроки.Владелец.ЕдиницаХраненияОстатков);
		Если (ЭлементыФормы.СправочникСписок.Колонки.ПорогОкругления.Видимость 
			или ЭлементыФормы.СправочникСписок.Колонки.ПредупреждатьОНецелыхМестах.Видимость) 
			И ТипЗнч(ДанныеСтроки.Владелец)=тип("СправочникСсылка.Номенклатура") 
			И ДанныеСтроки.Владелец.БазоваяЕдиницаИзмерения <> ДанныеСтроки.ЕдиницаПоКлассификатору Тогда
			//Колонки доступны для изменения если владелец - номенклатура, и единица не является базовой единицей
			ОформлениеСтроки.Ячейки.ПорогОкругления.ТолькоПросмотр = ложь;
			ОформлениеСтроки.Ячейки.ПредупреждатьОНецелыхМестах.ТолькоПросмотр = ложь;
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры