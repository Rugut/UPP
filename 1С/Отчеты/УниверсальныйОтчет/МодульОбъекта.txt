#Если Клиент Тогда
	
Перем мРазмещениеПоложениеВыбранныхПолей Экспорт;

Перем мСтруктураПредставлениеПолей Экспорт;
Перем мСоответствиеСвойствКатегорийИИзмерений Экспорт;
Перем мСоответствиеКонтактнойИнформацииИИзмерений Экспорт;
Перем мСоответствиеДанныхОФизлицеИИзмерений Экспорт;
Перем мСоответствиеПолейИИзмерений Экспорт;
Перем мСоответствиеНазначений Экспорт;
Перем мСоответствиеСвязьПолей;
Перем мСоответствиеТиповИнтервальныхПолей Экспорт;

Перем мСтруктураФорматаПолей Экспорт;

Перем мСтруктураПериодов Экспорт;

Перем мТаблицаПолей;
Перем мТаблицаПолейКонтактнаяИнформация;
Перем мТаблицаПолейДанныеОФизлице Экспорт;

Перем мНастройкаПериода Экспорт;
Перем мРежимВводаПериода Экспорт; // (-1) - не выбирать период, 0 - произвольный период, 1 - на дату, 2 - неделя, 3 - декада, 4 - месяц, 5 - квартал, 6 - полугодие, 7 - год
Перем мМинимальнаяШиринаКолонкиПоказатель Экспорт;
Перем мВыбиратьИмяРегистра Экспорт;
Перем мВыбиратьИспользованиеСвойств Экспорт;
Перем мРежимФормыНастройкиБезГруппировокКолонок Экспорт;
Перем мНазваниеОтчета Экспорт;

Перем мСтруктураИзмеренийСтроки;
Перем мСтруктураИзмеренийКолонки;
Перем мПоляДетали;

Перем мТекущаяНастройка Экспорт;

Перем мТекущаяСтраницаНастройка Экспорт;
Перем мРасширеннаяНастройка Экспорт;

Перем мВосстанавливатьНастройкиПриОткрытии Экспорт;
Перем мРассчитыватьШиринуКолонкиПоНазванию Экспорт;
Перем мРассчитыватьШиринуКолонок Экспорт;

Перем мВидПредставленияПолей Экспорт;

Перем мМассивПоляГруппировка;
Перем мМассивПоляРесурс;
Перем мМассивБыстрыеОтборы;

Перем мПутьКПостроителюОтчета Экспорт;
Перем мДействиеПолеВводаВидСравненияПриИзменении Экспорт;
Перем мДействиеПолеВводаЗначениеПриИзменении Экспорт;
Перем мДействиеКоманднаяПанельФормыПечать Экспорт;
Перем мДействиеКоманднаяПанельФормыСправкаФормы Экспорт;
Перем мДействиеВстроеннаяСправкаonclick Экспорт;
Перем мДействиеВстроеннаяСправкаonmouseout Экспорт;
Перем мДействиеВстроеннаяСправкаonmouseover Экспорт;

Перем мИмяЭлементаУправленияПолеТабличногоДокументаРезультат Экспорт;
Перем мИмяЭлементаУправленияБыстрыеОтборы Экспорт;
Перем мИмяЭлементаУправленияДополнительныеЭлементы Экспорт;
Перем мИмяЭлементаУправленияСправкаФормы Экспорт;

Перем мОбработкиПодключаемыеКОтчету;
Перем мДействиеФормыВыполнитьВнешнююОбработку Экспорт;

Процедура СформироватьОтчет(ТабличныйДокумент, Очищать = Истина, ФиксироватьЗаголовок = Истина, Отчет = Неопределено) Экспорт
	
	Если ТабличныйДокумент = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ ВыполнитьОбработкиПодключаемыеКОтчету(Перечисления.СпособыЗапускаВнешнихОбработокВОтчетахНаБазеУниверсального.АвтоматическиПередФормированиемОтчета, Отчет, ТабличныйДокумент) Тогда
		
		Возврат;
		
	КонецЕсли;
	
	ДобавитьАвтоупорядочивание();
	ВыбранныеПоляУсловноеОформлениеПоказатели = ДобавитьПоказателиВВыбранныеПоляИУсловноеОформление();
	
	Если ВыводВозможен() Тогда
		
		Если Очищать Тогда
		
			ТабличныйДокумент.Очистить();
		
		КонецЕсли;
		
		ОтборПериодичность = ДобавитьОтборПериодичность();
		
		ПостроительОтчета.Макет = ПолучитьМакетПостротеляОтчета();
		ПостроительОтчета.МакетОформления = _ПолучитьМакетОформления();
		ПостроительОтчета.ОформитьМакет();
		
		ПостроительОтчета.Параметры.Вставить("ДатаНач", ?(ДатаНач = Дата('00010101000000'), ДатаНач, Новый Граница(НачалоДня(ДатаНач), ВидГраницы.Включая)));
		ПостроительОтчета.Параметры.Вставить("ДатаКон", ?(ДатаКон = Дата('00010101000000'), ДатаКон, Новый Граница(КонецДня(ДатаКон), ВидГраницы.Включая)));
		ПостроительОтчета.Параметры.Вставить("ДатаНачала", ?(ДатаНач = Дата('00010101000000'), ДатаНач, НачалоДня(ДатаНач)));
		ПостроительОтчета.Параметры.Вставить("ДатаКонца", ?(ДатаКон = Дата('00010101000000'), Дата("39991231"), КонецДня(ДатаКон)));
		
		СвязанныеПоля = ДобавитьСвязанныеПоля();
		ПоляПорядка = ДобавитьПоляПорядка();
		
		ПостроительОтчета.Выполнить();
		ПостроительОтчета.Вывести(ТабличныйДокумент);
		
		УдалитьПоляПорядка(ПоляПорядка);
		УдалитьСвязанныеПоля(СвязанныеПоля);
		УдалитьОтборПериодичность(ОтборПериодичность);
		
		ОформитьЗаголовок(ТабличныйДокумент);
		
		РасчетШириныКолонок(ТабличныйДокумент);
		
		Если ФиксироватьЗаголовок Тогда
			
			УстановитьФиксацию(ТабличныйДокумент);
			
		КонецЕсли;
		
		УстановитьКолонтитул(ТабличныйДокумент);
		
	КонецЕсли;
		
	УдалитьПоказателиИзВыбранныхПолейИУсловногоОформления(ВыбранныеПоляУсловноеОформлениеПоказатели);
	УправлениеОтображениемЗаголовка(ЭтотОбъект, ТабличныйДокумент);
	
	ВыполнитьОбработкиПодключаемыеКОтчету(Перечисления.СпособыЗапускаВнешнихОбработокВОтчетахНаБазеУниверсального.АвтоматическиПослеФормированияОтчета, Отчет, ТабличныйДокумент);
	
КонецПроцедуры // СформироватьОтчет()

Функция ПолучитьРезультат() Экспорт
	
	ДобавитьАвтоупорядочивание();
	ВыбранныеПоляУсловноеОформлениеПоказатели = ДобавитьПоказателиВВыбранныеПоляИУсловноеОформление();
	
	ОтборПериодичность = ДобавитьОтборПериодичность();
	
	ПостроительОтчета.Параметры.Вставить("ДатаНач", ?(ДатаНач = Дата('00010101000000'), ДатаНач, Новый Граница(НачалоДня(ДатаНач), ВидГраницы.Включая)));
	ПостроительОтчета.Параметры.Вставить("ДатаКон", ?(ДатаКон = Дата('00010101000000'), ДатаКон, Новый Граница(КонецДня(ДатаКон), ВидГраницы.Включая)));
	ПостроительОтчета.Параметры.Вставить("ДатаНачала", ?(ДатаНач = Дата('00010101000000'), ДатаНач, НачалоДня(ДатаНач)));
	ПостроительОтчета.Параметры.Вставить("ДатаКонца", ?(ДатаКон = Дата('00010101000000'), Дата("39991231"), КонецДня(ДатаКон)));
	
	СвязанныеПоля = ДобавитьСвязанныеПоля();
	ПоляПорядка = ДобавитьПоляПорядка();
	
	ПостроительОтчета.Выполнить();
	Результат = ПостроительОтчета.Результат;
	
	УдалитьПоляПорядка(ПоляПорядка);
	УдалитьСвязанныеПоля(СвязанныеПоля);
	УдалитьОтборПериодичность(ОтборПериодичность);
	
	УдалитьПоказателиИзВыбранныхПолейИУсловногоОформления(ВыбранныеПоляУсловноеОформлениеПоказатели);
	
	Возврат Результат;
	
КонецФункции // ПолучитьРезультат()

Процедура УстановитьНачальныеНастройки(УниверсальныйОтчет = Ложь, ДополнительныеПараметры = Неопределено) Экспорт
	
	Если УниверсальныйОтчет Тогда
		
		МетаданныеРегистра = Метаданные.РегистрыНакопления.Найти(ИмяРегистра);
		мНазваниеОтчета = ?(МетаданныеРегистра <> Неопределено И мВыбиратьИмяРегистра, Метаданные.РегистрыНакопления[ИмяРегистра].Синоним, мНазваниеОтчета);
		
		УстановитьНачальныеНастройкиПоМетаданнымРегистра(ИмяРегистра);
		
	КонецЕсли;
	
	Для каждого ТекущаяСтрока из Показатели.Строки Цикл
			
		УстановитьПометкиРодителей(ТекущаяСтрока);
			
	КонецЦикла;
	
	ОтборПериодичность = ПостроительОтчета.Отбор.Найти("Периодичность");
	
	Если ОтборПериодичность <> Неопределено Тогда
		
		ПостроительОтчета.Отбор.Удалить(ПостроительОтчета.Отбор.Индекс(ОтборПериодичность));
		
	КонецЕсли;
	
	ПостроительОтчета.ВыбранныеПоля.Очистить();
	
КонецПроцедуры // УстановитьНачальныеНастройки()

Процедура ПереустановитьНачальныеНастройки(Объект, Отчет, Форма, ДополнительныеПараметры = Неопределено) Экспорт
	
	Перем ТекущиеНастройкиОтчета;
	
	Универсальный = (Метаданные.НайтиПоТипу(ТипЗнч(ЭтотОбъект)) = Отчет.Метаданные());

	Показатели.Строки.Очистить();
	мМассивБыстрыеОтборы = Неопределено;
	
	Если Универсальный Тогда
				
		ТекущиеНастройки = Отчет.ПостроительОтчета.ПолучитьНастройки();
		Отчет.УстановитьНачальныеНастройки(Универсальный);
		Отчет.ПостроительОтчета.УстановитьНастройки(ТекущиеНастройки);
				
	Иначе
		
		Отчет.УстановитьНачальныеНастройки(ДополнительныеПараметры);
				
	КонецЕсли;
	
	Объект.ДобавитьПанельБыстрыеОтборы(Объект, Форма);
	Объект.УправлениеПанельюБыстрыеОтборы(Объект, Форма);
	
	Объект.УстановитьЗаголовокФормы(Объект, Отчет, Форма);
	
КонецПроцедуры // ПереустановитьНачальныеНастройки()
	
Функция _ПолучитьМакетОформления()
	
	МакетОформления = ПолучитьМакетОформления(?(ВариантОформления = Неопределено, СтандартноеОформление.Интерфейс, ВариантОформления));
	
	ОбластьШапкаТаблицы = МакетОформления.Области.Найти("ШапкаТаблицы");
		
	Если ТипЗнч(ОбластьШапкаТаблицы) = Тип("ОбластьЯчеекТабличногоДокумента") Тогда
		
		ОбластьШапкаТаблицы.Шрифт = Новый Шрифт(ОбластьШапкаТаблицы.Шрифт,, 8);
		
	КонецЕсли;
	
	ОбластьЗаголовок = МакетОформления.Области.Найти("Заголовок");
	
	Если ТипЗнч(ОбластьЗаголовок) = Тип("ОбластьЯчеекТабличногоДокумента") Тогда
		
		ОбластьЗаголовок.Шрифт = Новый Шрифт(ОбластьШапкаТаблицы.Шрифт,, 8, Ложь, Ложь, Ложь);
		
	КонецЕсли;
	
	Возврат МакетОформления;
	
КонецФункции // _ПолучитьМакетОформления()

Функция ВыводВозможен()
	
	Результат = Истина;
	
	Если ПостроительОтчета.ИзмеренияСтроки.Количество() = 0 И ПостроительОтчета.ИзмеренияКолонки.Количество() = 0 И ПостроительОтчета.ВыбранныеПоля.Количество() = 0 Тогда
		
		Результат = Ложь;
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции // ВыводВозможен()

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ФОРМИРОВАНИЯ МАКЕТА ОТЧЕТА

Функция ПолучитьМакетПостротеляОтчета()
	
	ПостроительОтчета.ДобавлениеПредставлений = ТипДобавленияПредставлений.Добавлять;
	ПостроительОтчета.ЗаполнениеРасшифровки = ВидЗаполненияРасшифровкиПостроителяОтчета.Расшифровка;
	ПостроительОтчета.ОбрабатыватьПрерываниеПользователя = Истина;
	ПостроительОтчета.ОтображатьСостояние = Истина;
	ПостроительОтчета.РазмещениеИзмеренийВКолонках = ТипРазмещенияИзмерений.Вместе;
	ПостроительОтчета.РазмещениеИзмеренийВСтроках = ТипРазмещенияИзмерений.ОтдельноИТолькоВИтогах;
	ПостроительОтчета.РазмещениеРеквизитовИзмеренийВКолонках = ТипРазмещенияРеквизитовИзмерений.Отдельно;
	ПостроительОтчета.РазмещениеРеквизитовИзмеренийВСтроках = ТипРазмещенияРеквизитовИзмерений.Отдельно;
	ПостроительОтчета.ВыводитьОбщиеИтоги = ВыводитьОбщиеИтоги;
	ПостроительОтчета.АвтоДетальныеЗаписи = Истина;
	ПостроительОтчета.ВыводитьДетальныеЗаписи = ВыводитьДетальныеЗаписи;
	ПостроительОтчета.АвтоДетальныеЗаписи = Ложь;
	
	// Сброс предыдущего и получение автоматического макета
	ПостроительОтчета.Макет = Неопределено;
	ПостроительОтчета.МакетОформления = Неопределено;
	
	АвтоматическийМакет = ПостроительОтчета.Макет;
	
	// Результирующий макет
	Макет = Новый ТабличныйДокумент;
	
	Макет.Область(, 1,, 1).ШиринаКолонки = 2;
	
	// Получение структуры измерений строк и полей
	ПолучитьСтруктуруИзмеренийСтроки(АвтоматическийМакет);
	
	// Получение структуры измерений колонок и полей
	ПолучитьСтруктуруИзмеренийКолонки(АвтоматическийМакет);
	
	// Получение структуры деталей
	ПолучитьСтруктуруДетали(АвтоматическийМакет);
	
	Для каждого Область из АвтоматическийМакет.Области Цикл
		
		Если Область.Имя = "Заголовок" Тогда
			
			ДобавитьОбластьЗаголовок(Макет);
			
		ИначеЕсли Область.Имя = "ШапкаСтрок" Тогда
			
			ДобавитьОбластьШапкаСтрок(Макет);
			
		ИначеЕсли Область.Имя = "ШапкаТаблицы" Тогда
			
			ДобавитьОбластьШапкаТаблицы(Макет);
			
		ИначеЕсли Область.Имя = "ЗаголовокИтогаПоСтроке" Тогда
			
			ДобавитьОбластьЗаголовокИтогаПоСтроке(Макет);
			
		ИначеЕсли Область.Имя = "ЗаголовокИтогаПоКолонке" Тогда
			
			ДобавитьОбластьЗаголовокИтогаПоКолонке(Макет);
			
		ИначеЕсли Область.Имя = "ОбщиеИтоги" Тогда
			
			ДобавитьОбластьОбщиеИтоги(Макет, АвтоматическийМакет);
			
		ИначеЕсли Область.Имя = "Ресурсы" Тогда
			
			ДобавитьОбластьРесурсы(Макет);
			
		ИначеЕсли Область.Имя = "РесурсыИтогПоСтроке" Тогда
			
			ДобавитьОбластьРесурсыИтогПоСтроке(Макет);
			
		ИначеЕсли Область.Имя = "РесурсыИтогПоКолонке" Тогда
			
			ДобавитьОбластьРесурсыИтогПоКолонке(Макет);
			
		ИначеЕсли Область.Имя = "Детали" Тогда
			
			ДобавитьОбластьДетали(Макет, АвтоматическийМакет);
			
		Иначе
			
			ДобавитьОбластьИзмерение(Макет, АвтоматическийМакет, Область.Имя);
			
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Макет;
	
КонецФункции // ПолучитьМакетПостротеляОтчета()

Процедура ДобавитьОбластьЗаголовок(Макет)
	
	НомерСтрокиМакета = Макет.ВысотаТаблицы + 1;
	ВысотаЗаголовка = 0;
	
	// Название отчета
	ОбластьЗаголовок = Макет.Область(НомерСтрокиМакета + ВысотаЗаголовка, 2, НомерСтрокиМакета + ВысотаЗаголовка, 2);
	ОбластьЗаголовок.Заполнение = ТипЗаполненияОбластиТабличногоДокумента.Текст;
	ОбластьЗаголовок.Текст = мНазваниеОтчета;
	
	ВысотаЗаголовка = ВысотаЗаголовка + 1;
	
	// Период
	Если мРежимВводаПериода = -1 Тогда // указание периода не требуется
		
		ОписаниеПериода = "";
		
	ИначеЕсли мРежимВводаПериода = 0 Тогда // произвольный период
		
		Если ДатаНач = '00010101' И ДатаКон = '00010101' Тогда
			
			ОписаниеПериода = НСтр("ru='Период не установлен'");

		ИначеЕсли ДатаНач = '00010101' ИЛИ ДатаКон = '00010101' Тогда
			
			ОписаниеПериода = Формат(ДатаНач, "ДФ = дд.ММ.гггг; ДП = ...") + " - " + Формат(ДатаКон, "ДФ = дд.ММ.гггг; ДП = ...");

		ИначеЕсли ДатаНач <= ДатаКон Тогда
			
			ОписаниеПериода = ПредставлениеПериода(НачалоДня(ДатаНач), КонецДня(ДатаКон), "ФП = Истина");
			
		Иначе
			
			ОписаниеПериода = НСтр("ru='Неправильно задан период!'");

		КонецЕсли;
		
	ИначеЕсли мРежимВводаПериода = 1 Тогда // на дату
		
		Если ДатаКон = '00010101' Тогда

			ОписаниеПериода = НСтр("ru='на '") + Формат(ТекущаяДата(), "ДП = ...");

		Иначе

			ОписаниеПериода = НСтр("ru='на конец дня '") + Формат(ДатаКон, "ДФ = дд.ММ.гггг; ДП = ...");

		КонецЕсли;
		
	ИначеЕсли ДатаНач <> '00010101' Тогда // фиксированный период
		
		_ДатаНач = ДатаНач;
		_ДатаКон = ДатаКон;
		
		УстановитьПериод(_ДатаНач, _ДатаКон, мРежимВводаПериода, 0);
		ОписаниеПериода = ПредставлениеПериода(_ДатаНач, _ДатаКон, "ФП = Истина");
		
	Иначе
		
		ОписаниеПериода = НСтр("ru='Период не установлен'");
		
	КонецЕсли;
	
	Если НЕ ПустаяСтрока(ОписаниеПериода) Тогда
		
		ОбластьЗаголовок = Макет.Область(НомерСтрокиМакета + ВысотаЗаголовка, 2, НомерСтрокиМакета + ВысотаЗаголовка, 2);
		ОбластьЗаголовок.Заполнение = ТипЗаполненияОбластиТабличногоДокумента.Текст;
		ОбластьЗаголовок.Текст = НСтр("ru='Период: '") + ОписаниеПериода;
		
		ВысотаЗаголовка = ВысотаЗаголовка + 1;
	
	КонецЕсли;
	
	// Показатели
	ПредставлениеПоказатели = "";
	
	Для каждого ТекущийПоказатель из Показатели.Строки Цикл
						
		// Группа показателей
		Если ТекущийПоказатель.Строки.Количество() > 0 И ТекущийПоказатель.Использование > 0 Тогда
							
			КоличествоПоказателейВГруппе = 0;
			ПредставлениеПодчиненныеПоказатели = "";
						
			Для каждого Показатель из ТекущийПоказатель.Строки Цикл
								
				Если Показатель.Использование > 0 Тогда
								
					ПредставлениеПодчиненныеПоказатели = ПредставлениеПодчиненныеПоказатели + ?(ПустаяСтрока(ПредставлениеПодчиненныеПоказатели), "", ", ") + Показатель.Представление;
					КоличествоПоказателейВГруппе = КоличествоПоказателейВГруппе + 1;
									
				КонецЕсли;
								
			КонецЦикла;
							
			Если КоличествоПоказателейВГруппе > 0 Тогда
				
				ПредставлениеПоказатели = ПредставлениеПоказатели + " " + ТекущийПоказатель.Представление + "(" + ПредставлениеПодчиненныеПоказатели + ");";
				
			КонецЕсли;
							
		ИначеЕсли ТекущийПоказатель.Использование > 0 Тогда // Показатель
			
			ПредставлениеПоказатели = ПредставлениеПоказатели + " " + ТекущийПоказатель.Представление + ";";
							
		КонецЕсли;
						
	КонецЦикла;
	
	Если НЕ ПустаяСтрока(ПредставлениеПоказатели) Тогда
		
		ОбластьЗаголовок = Макет.Область(НомерСтрокиМакета + ВысотаЗаголовка, 2, НомерСтрокиМакета + ВысотаЗаголовка, 2);
		ОбластьЗаголовок.Заполнение = ТипЗаполненияОбластиТабличногоДокумента.Текст;
		ОбластьЗаголовок.Текст = НСтр("ru='Показатели:'") + ПредставлениеПоказатели;
		
		ВысотаЗаголовка = ВысотаЗаголовка + 1;
		
	КонецЕсли;
	
	// Группировки строк
	ПредставлениеГруппировкиСтрок = "";
	
	Для каждого ИзмерениеСтроки из ПостроительОтчета.ИзмеренияСтроки Цикл

		ПредставлениеГруппировкиСтрок = ПредставлениеГруппировкиСтрок + " " + ИзмерениеСтроки.Представление + " (" + ИзмерениеСтроки.ТипИзмерения + ");";

	КонецЦикла;
	
	Если НЕ ПустаяСтрока(ПредставлениеГруппировкиСтрок) Тогда
		
		ОбластьЗаголовок = Макет.Область(НомерСтрокиМакета + ВысотаЗаголовка, 2, НомерСтрокиМакета + ВысотаЗаголовка, 2);
		ОбластьЗаголовок.Заполнение = ТипЗаполненияОбластиТабличногоДокумента.Текст;
		ОбластьЗаголовок.Текст = НСтр("ru='Группировки строк:'") + ПредставлениеГруппировкиСтрок;
		
		ВысотаЗаголовка = ВысотаЗаголовка + 1;
		
	КонецЕсли;
	
	// Группировки колонок
	ПредставлениеГруппировкиКолонок = "";
	
	Для каждого ИзмерениеКолонки из ПостроительОтчета.ИзмеренияКолонки Цикл

		ПредставлениеГруппировкиКолонок = ПредставлениеГруппировкиКолонок + " " + ИзмерениеКолонки.Представление + " (" + ИзмерениеКолонки.ТипИзмерения + ");";

	КонецЦикла;
	
	Если НЕ ПустаяСтрока(ПредставлениеГруппировкиКолонок) Тогда
		
		ОбластьЗаголовок = Макет.Область(НомерСтрокиМакета + ВысотаЗаголовка, 2, НомерСтрокиМакета + ВысотаЗаголовка, 2);
		ОбластьЗаголовок.Заполнение = ТипЗаполненияОбластиТабличногоДокумента.Текст;
		ОбластьЗаголовок.Текст = НСтр("ru='Группировки колонок:'") + ПредставлениеГруппировкиКолонок;
		
		ВысотаЗаголовка = ВысотаЗаголовка + 1;
		
	КонецЕсли;
	
	// Отборы
	ПредставлениеОтборы = "";
	
	Для каждого Отбор из ПостроительОтчета.Отбор Цикл
		
		Если Отбор.Использование И Отбор.Имя <> "Периодичность" Тогда
			
			ПредставлениеОтборы = ПредставлениеОтборы + "
			|" + Отбор.Представление + " ";
			
			Если Отбор.ВидСравнения = ВидСравнения.Интервал Тогда
				
                ПредставлениеОтборы = ПредставлениеОтборы + НСтр("ru=' > '");
				ПредставлениеОтборы = ПредставлениеОтборы + Отбор.ЗначениеС;
                ПредставлениеОтборы = ПредставлениеОтборы + НСтр("ru=' И < '");
				ПредставлениеОтборы = ПредставлениеОтборы + Отбор.ЗначениеПо;
				
			ИначеЕсли Отбор.ВидСравнения = ВидСравнения.ИнтервалВключаяГраницы Тогда
				
                ПредставлениеОтборы = ПредставлениеОтборы + НСтр("ru=' >= '");
				ПредставлениеОтборы = ПредставлениеОтборы + Отбор.ЗначениеС;
                ПредставлениеОтборы = ПредставлениеОтборы + НСтр("ru=' И <= '");
				ПредставлениеОтборы = ПредставлениеОтборы + Отбор.ЗначениеПо;
				
			ИначеЕсли Отбор.ВидСравнения = ВидСравнения.ИнтервалВключаяНачало Тогда
				
                ПредставлениеОтборы = ПредставлениеОтборы + НСтр("ru=' >= '");
				ПредставлениеОтборы = ПредставлениеОтборы + Отбор.ЗначениеС;
                ПредставлениеОтборы = ПредставлениеОтборы + НСтр("ru=' И < '");
				ПредставлениеОтборы = ПредставлениеОтборы + Отбор.ЗначениеПо;
				
			ИначеЕсли Отбор.ВидСравнения = ВидСравнения.ИнтервалВключаяОкончание Тогда
				
                ПредставлениеОтборы = ПредставлениеОтборы + НСтр("ru=' > '");
				ПредставлениеОтборы = ПредставлениеОтборы + Отбор.ЗначениеС;
                ПредставлениеОтборы = ПредставлениеОтборы + НСтр("ru=' И <= '");
				ПредставлениеОтборы = ПредставлениеОтборы + Отбор.ЗначениеПо;
				
			ИначеЕсли Отбор.ВидСравнения = ВидСравнения.ВСписке ИЛИ Отбор.ВидСравнения = ВидСравнения.ВСпискеПоИерархии ИЛИ Отбор.ВидСравнения = ВидСравнения.НеВСписке ИЛИ Отбор.ВидСравнения = ВидСравнения.НеВСпискеПоИерархии Тогда
				
				ПредставлениеОтборы = ПредставлениеОтборы + Отбор.ВидСравнения + " (" + Отбор.Значение + ")";
				
			Иначе
				
				ПредставлениеОтборы = ПредставлениеОтборы + Отбор.ВидСравнения + " " + Отбор.Значение;
				
			КонецЕсли;
			
			ПредставлениеОтборы = ПредставлениеОтборы + ";";
			
		КонецЕсли;
		
	КонецЦикла;
	
	Если НЕ ПустаяСтрока(ПредставлениеОтборы) Тогда
		
		ОбластьЗаголовок = Макет.Область(НомерСтрокиМакета + ВысотаЗаголовка, 2, НомерСтрокиМакета + ВысотаЗаголовка, 2);
		ОбластьЗаголовок.Заполнение = ТипЗаполненияОбластиТабличногоДокумента.Текст;
		ОбластьЗаголовок.Текст = НСтр("ru='Отборы:'") + ПредставлениеОтборы;
		
		ВысотаЗаголовка = ВысотаЗаголовка + 1;
		
	КонецЕсли;
	
	// Дополнительные поля
	ПредставлениеДополнительныеПоля = "";
	
	Для каждого ВыбранноеПоле из ПостроительОтчета.ВыбранныеПоля Цикл

		Если НЕ ЭтоПолеПоказатель(ВыбранноеПоле.Имя) Тогда
			
			ПредставлениеДополнительныеПоля = ПредставлениеДополнительныеПоля + "
			|" + ВыбранноеПоле.Представление + " ("+ ПолучитьРазмещениеВыбранногоПоля(ВыбранноеПоле.Имя) + ", " + ПолучитьПоложениеВыбранногоПоля(ВыбранноеПоле.Имя) + ");";
			
		КонецЕсли;

	КонецЦикла;
	
	Если НЕ ПустаяСтрока(ПредставлениеДополнительныеПоля) Тогда
		
		ОбластьЗаголовок = Макет.Область(НомерСтрокиМакета + ВысотаЗаголовка, 2, НомерСтрокиМакета + ВысотаЗаголовка, 2);
		ОбластьЗаголовок.Заполнение = ТипЗаполненияОбластиТабличногоДокумента.Текст;
		ОбластьЗаголовок.Текст = НСтр("ru='Дополнительные поля:'") + ПредставлениеДополнительныеПоля;
		
		ВысотаЗаголовка = ВысотаЗаголовка + 1;
		
	КонецЕсли;
	
	// Сортировка
	ПредставлениеСортировка = "";
	
	Для каждого ПолеПорядка из ПостроительОтчета.Порядок Цикл

		ПредставлениеСортировка = ПредставлениеСортировка + " " + ПолеПорядка.Представление + " (" + ПолеПорядка.Направление + ");";

	КонецЦикла;
	
	Если НЕ ПустаяСтрока(ПредставлениеСортировка) Тогда
		
		ОбластьЗаголовок = Макет.Область(НомерСтрокиМакета + ВысотаЗаголовка, 2, НомерСтрокиМакета + ВысотаЗаголовка, 2);
		ОбластьЗаголовок.Заполнение = ТипЗаполненияОбластиТабличногоДокумента.Текст;
		ОбластьЗаголовок.Текст = НСтр("ru='Сортировка:'") + ПредставлениеСортировка;
		
		ВысотаЗаголовка = ВысотаЗаголовка + 1;
		
	КонецЕсли;
	
	ОбластьЗаголовок = Макет.Область(НомерСтрокиМакета,, НомерСтрокиМакета + ВысотаЗаголовка);
	ОбластьЗаголовок.Имя = "Заголовок";
	
КонецПроцедуры // ДобавитьОбластьЗаголовок()

Процедура ДобавитьОбластьШапкаСтрок(Макет)
	
	НомерСтрокиМакета = Макет.ВысотаТаблицы + 1;
	
	ОбластьШапкаСтрок = Макет.Область(НомерСтрокиМакета, 1, НомерСтрокиМакета + ПолучитьВысотуШапки() - 1, ПолучитьШиринуШапкиСтрок() + 1);
	ОбластьШапкаСтрок.Имя = "ШапкаСтрок";
	
	ИндексИзмерения = 0;
	
	Для каждого ИзмерениеСтроки из ПостроительОтчета.ИзмеренияСтроки Цикл
		
		СоставЗаголовкаИзмеренияСтроки = ПолучитьСоставЗаголовкаИзмеренияСтроки(ИзмерениеСтроки.Имя);
		
		Для Индекс = 0 по СоставЗаголовкаИзмеренияСтроки.Количество() - 1 Цикл
		
			ОбластьИзмерениеСтроки = Макет.Область(НомерСтрокиМакета + ИндексИзмерения, 2 + Индекс, НомерСтрокиМакета + ИндексИзмерения, 2 + Индекс);
			ОбластьИзмерениеСтроки.Заполнение = ТипЗаполненияОбластиТабличногоДокумента.Текст;
			ОбластьИзмерениеСтроки.Текст = СоставЗаголовкаИзмеренияСтроки[Индекс];
			ОбластьИзмерениеСтроки.ГоризонтальноеПоложение = ГоризонтальноеПоложение.Лево;
			ОбластьИзмерениеСтроки.ВертикальноеПоложение = ВертикальноеПоложение.Верх;
			ОбластьИзмерениеСтроки.РазмещениеТекста = ТипРазмещенияТекстаТабличногоДокумента.Переносить;
			
		КонецЦикла;
		
		ИндексИзмерения = ИндексИзмерения + 1;
		
	КонецЦикла;
	
	Если ИндексИзмерения < ПолучитьВысотуШапки() Тогда
		
		ОбластьИзмерениеСтроки = Макет.Область(НомерСтрокиМакета + ИндексИзмерения, 2, НомерСтрокиМакета + ПолучитьВысотуШапки() - 1, ПолучитьШиринуШапкиСтрок() + 1);
		ОбластьИзмерениеСтроки.Объединить();
		ОбластьИзмерениеСтроки.ГраницаСнизу = Новый Линия(ТипЛинииЯчейкиТабличногоДокумента.Сплошная);
		
	КонецЕсли;
	
КонецПроцедуры // ДобавитьОбластьШапкаСтрок()

Процедура ДобавитьОбластьШапкаТаблицы(Макет)
	
	НомерСтрокиМакета = Макет.ВысотаТаблицы + 1;
	
	ОбластьШапкаТаблицы = Макет.Область(НомерСтрокиМакета,, НомерСтрокиМакета + ПолучитьВысотуШапки() - 1);
	ОбластьШапкаТаблицы.Имя = "ШапкаТаблицы";
	
	ВременнаяОбласть = Макет.Область(НомерСтрокиМакета, 2, НомерСтрокиМакета + ПолучитьВысотуШапки() - 1, 2 + ПолучитьШиринуШапкиСтрок() + ПолучитьШиринуОбластиИзмерениеКолонки() - 1);
	ВременнаяОбласть.ГраницаСверху = Новый Линия(ТипЛинииЯчейкиТабличногоДокумента.Сплошная, 1);
	ВременнаяОбласть.ГраницаСнизу = Новый Линия(ТипЛинииЯчейкиТабличногоДокумента.Сплошная, 1);
	ВременнаяОбласть.ГраницаСлева = Новый Линия(ТипЛинииЯчейкиТабличногоДокумента.Сплошная, 1);
	ВременнаяОбласть.ГраницаСправа = Новый Линия(ТипЛинииЯчейкиТабличногоДокумента.Сплошная, 1);
	
	КоличествоУровнейПоказателей = ПолучитьКоличествоУровнейПоказателей();
	
	ИндексИзмерения = 0;
	
	Для каждого ИзмерениеСтроки из ПостроительОтчета.ИзмеренияСтроки Цикл
		
		СоставЗаголовкаИзмеренияСтроки = ПолучитьСоставЗаголовкаИзмеренияСтроки(ИзмерениеСтроки.Имя);
		
		Для Индекс = 0 по СоставЗаголовкаИзмеренияСтроки.Количество() - 1 Цикл
		
			ОбластьИзмерениеСтроки = Макет.Область(НомерСтрокиМакета + ИндексИзмерения, 2 + Индекс, НомерСтрокиМакета + ИндексИзмерения, 2 + Индекс);
			ОбластьИзмерениеСтроки.Заполнение = ТипЗаполненияОбластиТабличногоДокумента.Текст;
			ОбластьИзмерениеСтроки.Текст = СоставЗаголовкаИзмеренияСтроки[Индекс];
			ОбластьИзмерениеСтроки.ГоризонтальноеПоложение = ГоризонтальноеПоложение.Лево;
			ОбластьИзмерениеСтроки.ВертикальноеПоложение = ВертикальноеПоложение.Верх;
			ОбластьИзмерениеСтроки.РазмещениеТекста = ТипРазмещенияТекстаТабличногоДокумента.Переносить;
			
		КонецЦикла;
		
		ИндексИзмерения = ИндексИзмерения + 1;
		
	КонецЦикла;
		
	Если ТипЗнч(мПоляДетали) = Тип("Массив") И мПоляДетали.Количество() > 0 Тогда
		
		ДобавленоПолейДетальнойЗаписи = 0;
		
		Для каждого ПолеДетальнойЗаписи из мПоляДетали Цикл
			
			Для каждого ВыбранноеПоле из ПостроительОтчета.ВыбранныеПоля Цикл
					
				Если ВыбранноеПоле.Имя = ПолеДетальнойЗаписи ИЛИ ВыбранноеПоле.Имя + "Представление" = ПолеДетальнойЗаписи Тогда
						
					ОбластьПолеДетальнойЗаписи = Макет.Область(ОбластьШапкаТаблицы.Низ, 2 + ДобавленоПолейДетальнойЗаписи, ОбластьШапкаТаблицы.Низ, 2 + ДобавленоПолейДетальнойЗаписи);
					ОбластьПолеДетальнойЗаписи.Заполнение = ТипЗаполненияОбластиТабличногоДокумента.Текст;
					ОбластьПолеДетальнойЗаписи.Текст = ПолучитьПредставлениеПоля(ВыбранноеПоле);
					ОбластьПолеДетальнойЗаписи.ГоризонтальноеПоложение = ГоризонтальноеПоложение.Лево;
					ОбластьПолеДетальнойЗаписи.ВертикальноеПоложение = ВертикальноеПоложение.Центр;
					ОбластьПолеДетальнойЗаписи.РазмещениеТекста = ТипРазмещенияТекстаТабличногоДокумента.Переносить;
									
					ДобавленоПолейДетальнойЗаписи = ДобавленоПолейДетальнойЗаписи + 1;
					
					Прервать;
						
				КонецЕсли;
					
			КонецЦикла;
			
		КонецЦикла;
		
	КонецЕсли;
	
	ОбластьШапкаТаблицы = Макет.Область(НомерСтрокиМакета, 2, НомерСтрокиМакета + ПолучитьВысотуШапки() - 1, ПолучитьШиринуШапкиСтрок() + 1);
	
	ДобавленоПоказателей = 0;
				
	Для каждого ТекущийПоказатель из Показатели.Строки Цикл
						
		// Группа показателей
		Если ТекущийПоказатель.Строки.Количество() > 0 И ТекущийПоказатель.Использование > 0 Тогда
							
			КоличествоПоказателейВГруппе = 0;
						
			Для каждого Показатель из ТекущийПоказатель.Строки Цикл
								
				Если Показатель.Использование > 0 Тогда
								
					ОбластьПоказатель = Макет.Область(ОбластьШапкаТаблицы.Верх + 1, ОбластьШапкаТаблицы.Лево + ПолучитьШиринуШапкиСтрок() + ДобавленоПоказателей, ОбластьШапкаТаблицы.Низ, ОбластьШапкаТаблицы.Лево + ПолучитьШиринуШапкиСтрок() + ДобавленоПоказателей);
					ОбластьПоказатель.Объединить();
					ОбластьПоказатель.Заполнение = ТипЗаполненияОбластиТабличногоДокумента.Текст;
					ОбластьПоказатель.Текст = Показатель.ПредставлениеВОтчете;
					ОбластьПоказатель.ГоризонтальноеПоложение = ГоризонтальноеПоложение.Центр;
					ОбластьПоказатель.ВертикальноеПоложение = ВертикальноеПоложение.Центр;
					ОбластьПоказатель.РазмещениеТекста = ТипРазмещенияТекстаТабличногоДокумента.Переносить;
					ОбластьПоказатель.ШиринаКолонки = ?(Показатель.Ширина = 0, мМинимальнаяШиринаКолонкиПоказатель, Показатель.Ширина);
									
					ДобавленоПоказателей = ДобавленоПоказателей + 1;
					КоличествоПоказателейВГруппе = КоличествоПоказателейВГруппе + 1;
									
				КонецЕсли;
								
			КонецЦикла;
							
			Если КоличествоПоказателейВГруппе > 0 Тогда
								
				ОбластьГруппаПоказателей = Макет.Область(ОбластьШапкаТаблицы.Верх, ОбластьШапкаТаблицы.Лево + ПолучитьШиринуШапкиСтрок() + ДобавленоПоказателей - КоличествоПоказателейВГруппе, ОбластьШапкаТаблицы.Верх, ОбластьШапкаТаблицы.Лево + ПолучитьШиринуШапкиСтрок() + ДобавленоПоказателей - 1);
				ОбластьГруппаПоказателей.Объединить();
				ОбластьГруппаПоказателей.Заполнение = ТипЗаполненияОбластиТабличногоДокумента.Текст;
				ОбластьГруппаПоказателей.Текст = ТекущийПоказатель.ПредставлениеВОтчете;
				ОбластьГруппаПоказателей.ГоризонтальноеПоложение = ГоризонтальноеПоложение.Центр;
				ОбластьГруппаПоказателей.ВертикальноеПоложение = ВертикальноеПоложение.Центр;
				ОбластьГруппаПоказателей.РазмещениеТекста = ТипРазмещенияТекстаТабличногоДокумента.Переносить;
								
			КонецЕсли;
							
		ИначеЕсли ТекущийПоказатель.Использование > 0 Тогда // Показатель
							
			ОбластьПоказатель = Макет.Область(ОбластьШапкаТаблицы.Верх, ОбластьШапкаТаблицы.Лево + ПолучитьШиринуШапкиСтрок() + ДобавленоПоказателей, ОбластьШапкаТаблицы.Низ, ОбластьШапкаТаблицы.Лево + ПолучитьШиринуШапкиСтрок() + ДобавленоПоказателей);
			ОбластьПоказатель.Объединить();
			ОбластьПоказатель.Заполнение = ТипЗаполненияОбластиТабличногоДокумента.Текст;
			ОбластьПоказатель.Текст = ТекущийПоказатель.ПредставлениеВОтчете;
			ОбластьПоказатель.ГоризонтальноеПоложение = ГоризонтальноеПоложение.Центр;
			ОбластьПоказатель.ВертикальноеПоложение = ВертикальноеПоложение.Центр;
			ОбластьПоказатель.РазмещениеТекста = ТипРазмещенияТекстаТабличногоДокумента.Переносить;
			ОбластьПоказатель.ШиринаКолонки = ?(ТекущийПоказатель.Ширина = 0, мМинимальнаяШиринаКолонкиПоказатель, ТекущийПоказатель.Ширина);
							
			ДобавленоПоказателей = ДобавленоПоказателей + 1;
							
		КонецЕсли;
						
	КонецЦикла;
	
КонецПроцедуры // ДобавитьОбластьШапкаТаблицы()

Процедура ДобавитьОбластьЗаголовокИтогаПоСтроке(Макет)
	
	НомерСтрокиМакета = Макет.ВысотаТаблицы + 1;
	
	ОбластьЗаголовокИтогаПоСтроке = Макет.Область(НомерСтрокиМакета, 2, НомерСтрокиМакета + ПолучитьВысотуШапки() - 1, ПолучитьШиринуОбластиИзмерениеКолонки() + 1);
	ОбластьЗаголовокИтогаПоСтроке.Имя = "ЗаголовокИтогаПоСтроке";
	
	КоличествоУровнейПоказателей = ПолучитьКоличествоУровнейПоказателей();
				
	ОбластьИтог = Макет.Область(ОбластьЗаголовокИтогаПоСтроке.Верх, ОбластьЗаголовокИтогаПоСтроке.Лево, ОбластьЗаголовокИтогаПоСтроке.Низ - КоличествоУровнейПоказателей, ОбластьЗаголовокИтогаПоСтроке.Право);
	ОбластьИтог.Объединить();
	ОбластьИтог.Заполнение = ТипЗаполненияОбластиТабличногоДокумента.Текст;
	ОбластьИтог.Текст = "Итог";
	ОбластьИтог.ГоризонтальноеПоложение = ГоризонтальноеПоложение.Центр;
	ОбластьИтог.ВертикальноеПоложение = ВертикальноеПоложение.Центр;
	ОбластьИтог.РазмещениеТекста = ТипРазмещенияТекстаТабличногоДокумента.Переносить;
				
	ДобавленоПоказателей = 0;
				
	Для каждого ТекущийПоказатель из Показатели.Строки Цикл
						
		// Группа показателей
		Если ТекущийПоказатель.Строки.Количество() > 0 И ТекущийПоказатель.Использование > 0 Тогда
							
			КоличествоПоказателейВГруппе = 0;
						
			Для каждого Показатель из ТекущийПоказатель.Строки Цикл
								
				Если Показатель.Использование > 0 Тогда
								
					ОбластьПоказатель = Макет.Область(ОбластьЗаголовокИтогаПоСтроке.Низ, ОбластьЗаголовокИтогаПоСтроке.Лево + ДобавленоПоказателей, ОбластьЗаголовокИтогаПоСтроке.Низ, ОбластьЗаголовокИтогаПоСтроке.Лево + ДобавленоПоказателей);
					ОбластьПоказатель.Заполнение = ТипЗаполненияОбластиТабличногоДокумента.Текст;
					ОбластьПоказатель.Текст = Показатель.ПредставлениеВОтчете;
					ОбластьПоказатель.ГоризонтальноеПоложение = ГоризонтальноеПоложение.Центр;
					ОбластьПоказатель.ВертикальноеПоложение = ВертикальноеПоложение.Центр;
					ОбластьПоказатель.РазмещениеТекста = ТипРазмещенияТекстаТабличногоДокумента.Переносить;
					ОбластьПоказатель.ШиринаКолонки = ?(Показатель.Ширина = 0, мМинимальнаяШиринаКолонкиПоказатель, Показатель.Ширина);
									
					ДобавленоПоказателей = ДобавленоПоказателей + 1;
					КоличествоПоказателейВГруппе = КоличествоПоказателейВГруппе + 1;
									
				КонецЕсли;
								
			КонецЦикла;
							
			Если КоличествоПоказателейВГруппе > 0 Тогда
								
				ОбластьГруппаПоказателей = Макет.Область(ОбластьЗаголовокИтогаПоСтроке.Низ - 1, ОбластьЗаголовокИтогаПоСтроке.Лево + ДобавленоПоказателей - КоличествоПоказателейВГруппе, ОбластьЗаголовокИтогаПоСтроке.Низ - 1, ОбластьЗаголовокИтогаПоСтроке.Лево + ДобавленоПоказателей - 1);
				ОбластьГруппаПоказателей.Объединить();
				ОбластьГруппаПоказателей.Заполнение = ТипЗаполненияОбластиТабличногоДокумента.Текст;
				ОбластьГруппаПоказателей.Текст = ТекущийПоказатель.ПредставлениеВОтчете;
				ОбластьГруппаПоказателей.ГоризонтальноеПоложение = ГоризонтальноеПоложение.Центр;
				ОбластьГруппаПоказателей.ВертикальноеПоложение = ВертикальноеПоложение.Центр;
				ОбластьГруппаПоказателей.РазмещениеТекста = ТипРазмещенияТекстаТабличногоДокумента.Переносить;
								
			КонецЕсли;
							
		ИначеЕсли ТекущийПоказатель.Использование > 0 Тогда // Показатель
							
			ОбластьПоказатель = Макет.Область(ОбластьЗаголовокИтогаПоСтроке.Низ - КоличествоУровнейПоказателей + 1, ОбластьЗаголовокИтогаПоСтроке.Лево + ДобавленоПоказателей, ОбластьЗаголовокИтогаПоСтроке.Низ, ОбластьЗаголовокИтогаПоСтроке.Лево + ДобавленоПоказателей);
			ОбластьПоказатель.Объединить();
			ОбластьПоказатель.Заполнение = ТипЗаполненияОбластиТабличногоДокумента.Текст;
			ОбластьПоказатель.Текст = ТекущийПоказатель.ПредставлениеВОтчете;
			ОбластьПоказатель.ГоризонтальноеПоложение = ГоризонтальноеПоложение.Центр;
			ОбластьПоказатель.ВертикальноеПоложение = ВертикальноеПоложение.Центр;
			ОбластьПоказатель.РазмещениеТекста = ТипРазмещенияТекстаТабличногоДокумента.Переносить;
			ОбластьПоказатель.ШиринаКолонки = ?(ТекущийПоказатель.Ширина = 0, мМинимальнаяШиринаКолонкиПоказатель, ТекущийПоказатель.Ширина);
							
			ДобавленоПоказателей = ДобавленоПоказателей + 1;
							
		КонецЕсли;
						
	КонецЦикла;
	
КонецПроцедуры // ДобавитьОбластьЗаголовокИтогаПоСтроке()

Процедура ДобавитьОбластьЗаголовокИтогаПоКолонке(Макет)
	
	НомерСтрокиМакета = Макет.ВысотаТаблицы + 1;
	
	ОбластьШапкаСтрок = Макет.Область(НомерСтрокиМакета, 1, НомерСтрокиМакета, ПолучитьШиринуШапкиСтрок() + 1);
	ОбластьШапкаСтрок.Имя = "ЗаголовокИтогаПоКолонке";
	
	ОбластьШапкаСтрок = Макет.Область(НомерСтрокиМакета, 2, НомерСтрокиМакета, ПолучитьШиринуШапкиСтрок() + 1);
	ОбластьШапкаСтрок.Объединить();
	ОбластьШапкаСтрок.Заполнение = ТипЗаполненияОбластиТабличногоДокумента.Текст;
	ОбластьШапкаСтрок.Текст = "Итог";
	ОбластьШапкаСтрок.ГоризонтальноеПоложение = ГоризонтальноеПоложение.Лево;
	ОбластьШапкаСтрок.ВертикальноеПоложение = ВертикальноеПоложение.Верх;
	ОбластьШапкаСтрок.РазмещениеТекста = ТипРазмещенияТекстаТабличногоДокумента.Переносить;
	
КонецПроцедуры // ДобавитьОбластьЗаголовокИтогаПоКолонке()

Процедура ДобавитьОбластьОбщиеИтоги(Макет, АвтоматическийМакет)
	
	НомерСтрокиМакета = Макет.ВысотаТаблицы + 1;
	
	Если ТипЗнч(АвтоматическийМакет.Области.Найти("Ресурсы")) = Тип("ОбластьЯчеекТабличногоДокумента") Тогда
		
		ОбластьРесурсы = Макет.Область(НомерСтрокиМакета, 2, НомерСтрокиМакета, ПолучитьШиринуОбластиИзмерениеКолонки() + 1);
		ОбластьРесурсы.Имя = "ОбщиеИтоги";
	
		ДобавленоРесурсов = 0;
		
		Для каждого ТекущийПоказатель из Показатели.Строки Цикл
			
			Если ТекущийПоказатель.Строки.Количество() > 0 И ТекущийПоказатель.Использование > 0 Тогда
				
				Для каждого Показатель из ТекущийПоказатель.Строки Цикл
					
					Если Показатель.Использование > 0 Тогда
						
						ОбластьРесурс = Макет.Область(НомерСтрокиМакета, 2 + ДобавленоРесурсов, НомерСтрокиМакета, 2 + ДобавленоРесурсов);
						ОбластьРесурс.Заполнение = ТипЗаполненияОбластиТабличногоДокумента.Параметр;
						ОбластьРесурс.Параметр = Показатель.Имя;
						ОбластьРесурс.Формат = Показатель.ФорматнаяСтрока;
						ОбластьРесурс.ГоризонтальноеПоложение = ГоризонтальноеПоложение.Право;
						ОбластьРесурс.ВертикальноеПоложение = ВертикальноеПоложение.Низ;
						ОбластьРесурс.РазмещениеТекста = ТипРазмещенияТекстаТабличногоДокумента.Переносить;
						
						ДобавленоРесурсов = ДобавленоРесурсов + 1;
						
					КонецЕсли;
					
				КонецЦикла;
				
			ИначеЕсли ТекущийПоказатель.Использование > 0 Тогда
				
				ОбластьРесурс = Макет.Область(НомерСтрокиМакета, 2 + ДобавленоРесурсов, НомерСтрокиМакета, 2 + ДобавленоРесурсов);
				ОбластьРесурс.Заполнение = ТипЗаполненияОбластиТабличногоДокумента.Параметр;
				ОбластьРесурс.Параметр = ТекущийПоказатель.Имя;
				ОбластьРесурс.Формат = ТекущийПоказатель.ФорматнаяСтрока;
				ОбластьРесурс.ГоризонтальноеПоложение = ГоризонтальноеПоложение.Право;
				ОбластьРесурс.ВертикальноеПоложение = ВертикальноеПоложение.Низ;
				ОбластьРесурс.РазмещениеТекста = ТипРазмещенияТекстаТабличногоДокумента.Переносить;
				
				ДобавленоРесурсов = ДобавленоРесурсов + 1;
				
			КонецЕсли;
			
		КонецЦикла;
		
	Иначе
		
		ОбластьРесурсы = Макет.Область(НомерСтрокиМакета,, НомерСтрокиМакета);
		ОбластьРесурсы.Имя = "ОбщиеИтоги";
		
		ОбластьРесурсы = Макет.Область(НомерСтрокиМакета, 2, НомерСтрокиМакета, ПолучитьШиринуШапкиСтрок() + 1);
		ОбластьРесурсы.Объединить();
		ОбластьРесурсы.Заполнение = ТипЗаполненияОбластиТабличногоДокумента.Текст;
		ОбластьРесурсы.Текст = "Итог";
		ОбластьРесурсы.ГоризонтальноеПоложение = ГоризонтальноеПоложение.Лево;
		ОбластьРесурсы.ВертикальноеПоложение = ВертикальноеПоложение.Верх;
		ОбластьРесурсы.РазмещениеТекста = ТипРазмещенияТекстаТабличногоДокумента.Переносить;
		
		ДобавленоРесурсов = 0;
		
		Для каждого ТекущийПоказатель из Показатели.Строки Цикл
			
			Если ТекущийПоказатель.Строки.Количество() > 0 И ТекущийПоказатель.Использование > 0 Тогда
				
				Для каждого Показатель из ТекущийПоказатель.Строки Цикл
					
					Если Показатель.Использование > 0 Тогда
						
						ОбластьРесурс = Макет.Область(НомерСтрокиМакета, ПолучитьШиринуШапкиСтрок() + ДобавленоРесурсов + 2, НомерСтрокиМакета, ПолучитьШиринуШапкиСтрок() + ДобавленоРесурсов + 2);
						ОбластьРесурс.Заполнение = ТипЗаполненияОбластиТабличногоДокумента.Параметр;
						ОбластьРесурс.Параметр = Показатель.Имя;
						ОбластьРесурс.Формат = Показатель.ФорматнаяСтрока;
						ОбластьРесурс.ГоризонтальноеПоложение = ГоризонтальноеПоложение.Право;
						ОбластьРесурс.ВертикальноеПоложение = ВертикальноеПоложение.Низ;
						ОбластьРесурс.РазмещениеТекста = ТипРазмещенияТекстаТабличногоДокумента.Переносить;
						
						ДобавленоРесурсов = ДобавленоРесурсов + 1;
						
					КонецЕсли;
					
				КонецЦикла;
				
			ИначеЕсли ТекущийПоказатель.Использование > 0 Тогда
				
				ОбластьРесурс = Макет.Область(НомерСтрокиМакета, ПолучитьШиринуШапкиСтрок() + ДобавленоРесурсов + 2, НомерСтрокиМакета, ПолучитьШиринуШапкиСтрок() + ДобавленоРесурсов + 2);
				ОбластьРесурс.Заполнение = ТипЗаполненияОбластиТабличногоДокумента.Параметр;
				ОбластьРесурс.Параметр = ТекущийПоказатель.Имя;
				ОбластьРесурс.Формат = ТекущийПоказатель.ФорматнаяСтрока;
				ОбластьРесурс.ГоризонтальноеПоложение = ГоризонтальноеПоложение.Право;
				ОбластьРесурс.ВертикальноеПоложение = ВертикальноеПоложение.Низ;
				ОбластьРесурс.РазмещениеТекста = ТипРазмещенияТекстаТабличногоДокумента.Переносить;
				
				ДобавленоРесурсов = ДобавленоРесурсов + 1;
				
			КонецЕсли;
			
		КонецЦикла;
					
	КонецЕсли;
	
КонецПроцедуры // ДобавитьОбластьОбщиеИтоги()

Процедура ДобавитьОбластьРесурсы(Макет)
	
	НомерСтрокиМакета = Макет.ВысотаТаблицы + 1;
	
	ОбластьРесурсы = Макет.Область(НомерСтрокиМакета, 2, НомерСтрокиМакета, ПолучитьШиринуОбластиИзмерениеКолонки() + 1);
	ОбластьРесурсы.Имя = "Ресурсы";
	
	ДобавленоРесурсов = 0;
	
	Для каждого ТекущийПоказатель из Показатели.Строки Цикл
		
		Если ТекущийПоказатель.Строки.Количество() > 0 И ТекущийПоказатель.Использование > 0 Тогда
			
			Для каждого Показатель из ТекущийПоказатель.Строки Цикл
				
				Если Показатель.Использование > 0 Тогда
					
					ОбластьРесурс = Макет.Область(НомерСтрокиМакета, 2 + ДобавленоРесурсов, НомерСтрокиМакета, 2 + ДобавленоРесурсов);
					ОбластьРесурс.Заполнение = ТипЗаполненияОбластиТабличногоДокумента.Параметр;
					ОбластьРесурс.Параметр = Показатель.Имя;
					ОбластьРесурс.ПараметрРасшифровки = "Расшифровка";
					ОбластьРесурс.Формат = Показатель.ФорматнаяСтрока;
					ОбластьРесурс.ГоризонтальноеПоложение = ГоризонтальноеПоложение.Право;
					ОбластьРесурс.ВертикальноеПоложение = ВертикальноеПоложение.Низ;
					ОбластьРесурс.РазмещениеТекста = ТипРазмещенияТекстаТабличногоДокумента.Переносить;
					
					ДобавленоРесурсов = ДобавленоРесурсов + 1;
					
				КонецЕсли;
				
			КонецЦикла;
			
		ИначеЕсли ТекущийПоказатель.Использование > 0 Тогда
			
			ОбластьРесурс = Макет.Область(НомерСтрокиМакета, 2 + ДобавленоРесурсов, НомерСтрокиМакета, 2 + ДобавленоРесурсов);
			ОбластьРесурс.Заполнение = ТипЗаполненияОбластиТабличногоДокумента.Параметр;
			ОбластьРесурс.Параметр = ТекущийПоказатель.Имя;
			ОбластьРесурс.ПараметрРасшифровки = "Расшифровка";
			ОбластьРесурс.Формат = ТекущийПоказатель.ФорматнаяСтрока;
			ОбластьРесурс.ГоризонтальноеПоложение = ГоризонтальноеПоложение.Право;
			ОбластьРесурс.ВертикальноеПоложение = ВертикальноеПоложение.Низ;
			ОбластьРесурс.РазмещениеТекста = ТипРазмещенияТекстаТабличногоДокумента.Переносить;
			
			ДобавленоРесурсов = ДобавленоРесурсов + 1;
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры // ДобавитьОбластьРесурсы()

Процедура ДобавитьОбластьРесурсыИтогПоСтроке(Макет)
	
	НомерСтрокиМакета = Макет.ВысотаТаблицы + 1;
	
	ОбластьРесурсы = Макет.Область(НомерСтрокиМакета, 2, НомерСтрокиМакета, ПолучитьШиринуОбластиИзмерениеКолонки() + 1);
	ОбластьРесурсы.Имя = "РесурсыИтогПоСтроке";
	
	ДобавленоРесурсов = 0;
	
	Для каждого ТекущийПоказатель из Показатели.Строки Цикл
		
		Если ТекущийПоказатель.Строки.Количество() > 0 И ТекущийПоказатель.Использование > 0 Тогда
			
			Для каждого Показатель из ТекущийПоказатель.Строки Цикл
				
				Если Показатель.Использование > 0 Тогда
					
					ОбластьРесурс = Макет.Область(НомерСтрокиМакета, 2 + ДобавленоРесурсов, НомерСтрокиМакета, 2 + ДобавленоРесурсов);
					ОбластьРесурс.Заполнение = ТипЗаполненияОбластиТабличногоДокумента.Параметр;
					ОбластьРесурс.Параметр = Показатель.Имя;
					ОбластьРесурс.Формат = Показатель.ФорматнаяСтрока;
					ОбластьРесурс.ГоризонтальноеПоложение = ГоризонтальноеПоложение.Право;
					ОбластьРесурс.ВертикальноеПоложение = ВертикальноеПоложение.Низ;
					ОбластьРесурс.РазмещениеТекста = ТипРазмещенияТекстаТабличногоДокумента.Переносить;
					
					ДобавленоРесурсов = ДобавленоРесурсов + 1;
					
				КонецЕсли;
				
			КонецЦикла;
			
		ИначеЕсли ТекущийПоказатель.Использование > 0 Тогда
			
			ОбластьРесурс = Макет.Область(НомерСтрокиМакета, 2 + ДобавленоРесурсов, НомерСтрокиМакета, 2 + ДобавленоРесурсов);
			ОбластьРесурс.Заполнение = ТипЗаполненияОбластиТабличногоДокумента.Параметр;
			ОбластьРесурс.Параметр = ТекущийПоказатель.Имя;
			ОбластьРесурс.Формат = ТекущийПоказатель.ФорматнаяСтрока;
			ОбластьРесурс.ГоризонтальноеПоложение = ГоризонтальноеПоложение.Право;
			ОбластьРесурс.ВертикальноеПоложение = ВертикальноеПоложение.Низ;
			ОбластьРесурс.РазмещениеТекста = ТипРазмещенияТекстаТабличногоДокумента.Переносить;
			
			ДобавленоРесурсов = ДобавленоРесурсов + 1;
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры // ДобавитьОбластьРесурсыИтогПоСтроке()

Процедура ДобавитьОбластьРесурсыИтогПоКолонке(Макет)
	
	НомерСтрокиМакета = Макет.ВысотаТаблицы + 1;
	
	ОбластьРесурсы = Макет.Область(НомерСтрокиМакета, 2, НомерСтрокиМакета, ПолучитьШиринуОбластиИзмерениеКолонки() + 1);
	ОбластьРесурсы.Имя = "РесурсыИтогПоКолонке";
	
	ДобавленоРесурсов = 0;
	
	Для каждого ТекущийПоказатель из Показатели.Строки Цикл
		
		Если ТекущийПоказатель.Строки.Количество() > 0 И ТекущийПоказатель.Использование > 0 Тогда
			
			Для каждого Показатель из ТекущийПоказатель.Строки Цикл
				
				Если Показатель.Использование > 0 Тогда
					
					ОбластьРесурс = Макет.Область(НомерСтрокиМакета, 2 + ДобавленоРесурсов, НомерСтрокиМакета, 2 + ДобавленоРесурсов);
					ОбластьРесурс.Заполнение = ТипЗаполненияОбластиТабличногоДокумента.Параметр;
					ОбластьРесурс.Параметр = Показатель.Имя;
					ОбластьРесурс.Формат = Показатель.ФорматнаяСтрока;
					ОбластьРесурс.ГоризонтальноеПоложение = ГоризонтальноеПоложение.Право;
					ОбластьРесурс.ВертикальноеПоложение = ВертикальноеПоложение.Низ;
					ОбластьРесурс.РазмещениеТекста = ТипРазмещенияТекстаТабличногоДокумента.Переносить;
					
					ДобавленоРесурсов = ДобавленоРесурсов + 1;
					
				КонецЕсли;
				
			КонецЦикла;
			
		ИначеЕсли ТекущийПоказатель.Использование > 0 Тогда
			
			ОбластьРесурс = Макет.Область(НомерСтрокиМакета, 2 + ДобавленоРесурсов, НомерСтрокиМакета, 2 + ДобавленоРесурсов);
			ОбластьРесурс.Заполнение = ТипЗаполненияОбластиТабличногоДокумента.Параметр;
			ОбластьРесурс.Параметр = ТекущийПоказатель.Имя;
			ОбластьРесурс.Формат = ТекущийПоказатель.ФорматнаяСтрока;
			ОбластьРесурс.ГоризонтальноеПоложение = ГоризонтальноеПоложение.Право;
			ОбластьРесурс.ВертикальноеПоложение = ВертикальноеПоложение.Низ;
			ОбластьРесурс.РазмещениеТекста = ТипРазмещенияТекстаТабличногоДокумента.Переносить;
			
			ДобавленоРесурсов = ДобавленоРесурсов + 1;
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры // ДобавитьОбластьРесурсыИтогПоКолонке()

Процедура ДобавитьОбластьИзмерение(Макет, АвтоматическийМакет, ИмяОбласти)
	
	Измерение = Неопределено;
	ЭтоИзмерениеСтроки = Ложь;
	
	Для каждого ИзмерениеСтроки из ПостроительОтчета.ИзмеренияСтроки Цикл
		
		Если ИзмерениеСтроки.Имя = ИмяОбласти Тогда
				
			ЭтоИзмерениеСтроки = Истина;
			Измерение = ИзмерениеСтроки;
			Иерархия = Ложь;
			Прервать;
				
		ИначеЕсли ИзмерениеСтроки.Имя + "Иерархия" = ИмяОбласти Тогда
				
			ЭтоИзмерениеСтроки = Истина;
			Измерение = ИзмерениеСтроки;
			Иерархия = Истина;
			Прервать;
				
		КонецЕсли;
		
	КонецЦикла;
	
	Если Измерение = Неопределено Тогда
		
		Для каждого ИзмерениеКолонки из ПостроительОтчета.ИзмеренияКолонки Цикл
			
			Если ИзмерениеКолонки.Имя = ИмяОбласти Тогда
				
				Измерение = ИзмерениеКолонки;
				Иерархия = Ложь;
				Прервать;
				
			ИначеЕсли ИзмерениеКолонки.Имя + "Иерархия" = ИмяОбласти Тогда
				
				Измерение = ИзмерениеКолонки;
				Иерархия = Истина;
				Прервать;
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
	Если Измерение = Неопределено Тогда
		
		Возврат;
		
	КонецЕсли;
	
	НомерСтрокиМакета = Макет.ВысотаТаблицы + 1;
	
	Если ЭтоИзмерениеСтроки Тогда // Измерение строки
		
		Область = АвтоматическийМакет.ПолучитьОбласть(ИмяОбласти);
		
		Для ИндексСтолбца = 1 По Область.ШиринаТаблицы Цикл
				
			ТекущаяОбласть = Область.Область(1, ИндексСтолбца, 1, ИндексСтолбца);
			
			Если ЭтоОбластьДляВыводаПоля(ТекущаяОбласть, Измерение.Имя, Измерение.ПутьКДанным) Тогда
				
				// Назначение имени области результирующего макета
				Если ТипЗнч(АвтоматическийМакет.Области.Найти("Ресурсы")) = Тип("ОбластьЯчеекТабличногоДокумента") Тогда
					
					ОбластьРезультирующегоМакета = Макет.Область(НомерСтрокиМакета, 1, НомерСтрокиМакета, ПолучитьШиринуШапкиСтрок() + 1);
					
				Иначе
					
					ОбластьРезультирующегоМакета = Макет.Область(НомерСтрокиМакета,, НомерСтрокиМакета);
					
				КонецЕсли;
				
				ОбластьРезультирующегоМакета.Имя = ИмяОбласти;
					
				ИндексПоля = 2;
				
				Для каждого СоставПолей из ПолучитьСоставПолейИзмеренияСтроки(Измерение.Имя, ТекущаяОбласть.Параметр, Иерархия) Цикл
						
					ОбластьРезультирующегоМакета = Макет.Область(НомерСтрокиМакета, ИндексПоля, НомерСтрокиМакета, ИндексПоля);
					ОбластьРезультирующегоМакета.ПараметрРасшифровки = "Расшифровка";
					ОбластьРезультирующегоМакета.АвтоОтступ = ?(ИндексПоля = 2, 1, 0);
					ОбластьРезультирующегоМакета.РазмещениеТекста = ТипРазмещенияТекстаТабличногоДокумента.Переносить;
					
					Если СоставПолей.Свойство("Шаблон") Тогда
						
						ОбластьРезультирующегоМакета.Заполнение = ?(ПустаяСтрока(СоставПолей.Шаблон), ТипЗаполненияОбластиТабличногоДокумента.Текст, ТипЗаполненияОбластиТабличногоДокумента.Шаблон);
						ОбластьРезультирующегоМакета.Текст = СоставПолей.Шаблон;
						
					ИначеЕсли СоставПолей.Свойство("Параметр") Тогда
						
						ОбластьРезультирующегоМакета.Заполнение = ТипЗаполненияОбластиТабличногоДокумента.Параметр;
						ОбластьРезультирующегоМакета.Параметр = СоставПолей.Параметр;
						
						Если СоставПолей.Свойство("Формат") Тогда
							
							ОбластьРезультирующегоМакета.Формат = СоставПолей.Формат;
							
						КонецЕсли;
						
					КонецЕсли;
					
					ИндексПоля = ИндексПоля  + 1;
						
				КонецЦикла;
				
				ДобавленоРесурсов = 0;
				
				Для каждого ТекущийПоказатель из Показатели.Строки Цикл
					
					Если ТекущийПоказатель.Строки.Количество() > 0 И ТекущийПоказатель.Использование > 0 Тогда
						
						Для каждого Показатель из ТекущийПоказатель.Строки Цикл
							
							Если Показатель.Использование > 0 Тогда
								
								ОбластьРесурс = Макет.Область(НомерСтрокиМакета, ИндексПоля + ДобавленоРесурсов, НомерСтрокиМакета, ИндексПоля + ДобавленоРесурсов);
								ОбластьРесурс.Заполнение = ТипЗаполненияОбластиТабличногоДокумента.Параметр;
								ОбластьРесурс.Параметр = Показатель.Имя;
								ОбластьРесурс.ПараметрРасшифровки = "Расшифровка";
								ОбластьРесурс.Формат = Показатель.ФорматнаяСтрока;
								ОбластьРесурс.ГоризонтальноеПоложение = ГоризонтальноеПоложение.Право;
								ОбластьРесурс.ВертикальноеПоложение = ВертикальноеПоложение.Низ;
								ОбластьРесурс.РазмещениеТекста = ТипРазмещенияТекстаТабличногоДокумента.Переносить;
								
								ДобавленоРесурсов = ДобавленоРесурсов + 1;
								
							КонецЕсли;
							
						КонецЦикла;
						
					ИначеЕсли ТекущийПоказатель.Использование > 0 Тогда
						
						ОбластьРесурс = Макет.Область(НомерСтрокиМакета, ИндексПоля + ДобавленоРесурсов, НомерСтрокиМакета, ИндексПоля + ДобавленоРесурсов);
						ОбластьРесурс.Заполнение = ТипЗаполненияОбластиТабличногоДокумента.Параметр;
						ОбластьРесурс.Параметр = ТекущийПоказатель.Имя;
						ОбластьРесурс.ПараметрРасшифровки = "Расшифровка";
						ОбластьРесурс.Формат = ТекущийПоказатель.ФорматнаяСтрока;
						ОбластьРесурс.ГоризонтальноеПоложение = ГоризонтальноеПоложение.Право;
						ОбластьРесурс.ВертикальноеПоложение = ВертикальноеПоложение.Низ;
						ОбластьРесурс.РазмещениеТекста = ТипРазмещенияТекстаТабличногоДокумента.Переносить;
						
						ДобавленоРесурсов = ДобавленоРесурсов + 1;
						
					КонецЕсли;
					
				КонецЦикла;
					
				Прервать;
					
			КонецЕсли;
					
		КонецЦикла;
		
	Иначе // Измерение колонки
		
		ИменованнаяОбластьРезультирующегоМакета = Макет.Область(НомерСтрокиМакета, 2, НомерСтрокиМакета + ПолучитьВысотуШапки() - 1, ПолучитьШиринуОбластиИзмерениеКолонки() + 1);
		ИменованнаяОбластьРезультирующегоМакета.Имя = ИмяОбласти;
		
		ДобавленоПолей = 0;
		
		Для каждого ИзмерениеКолонки из ПостроительОтчета.ИзмеренияКолонки Цикл
			
			Если ТипЗнч(АвтоматическийМакет.Области.Найти(ИзмерениеКолонки.Имя)) = Тип("ОбластьЯчеекТабличногоДокумента") Тогда
				
				Область = АвтоматическийМакет.ПолучитьОбласть(ИзмерениеКолонки.Имя);
				
			Иначе
				
				Продолжить;
				
			КонецЕсли;
			
			Для ИндексСтроки = 1 По Область.ВысотаТаблицы Цикл
				
				// Получение области измерения колонки автоматического макета
				ТекущаяОбласть = Область.Область(ИндексСтроки, 1, ИндексСтроки, 1);
				
				Если ЭтоОбластьДляВыводаПоля(ТекущаяОбласть, ИзмерениеКолонки.Имя, ИзмерениеКолонки.ПутьКДанным) Тогда
					
					Для каждого СоставПолей из ПолучитьСоставПолейИзмеренияКолонки(ИзмерениеКолонки.Имя, ТекущаяОбласть.Параметр, Иерархия) Цикл
							
						ОбластьРезультирующегоМакета = Макет.Область(НомерСтрокиМакета + ДобавленоПолей, 2, НомерСтрокиМакета + ДобавленоПолей, ПолучитьШиринуОбластиИзмерениеКолонки() + 1);
						ОбластьРезультирующегоМакета.ПараметрРасшифровки = "Расшифровка";
						ОбластьРезультирующегоМакета.РазмещениеТекста = ТипРазмещенияТекстаТабличногоДокумента.Переносить;
						
						Если СоставПолей.Свойство("Шаблон") Тогда
							
							ОбластьРезультирующегоМакета.Заполнение = ?(ПустаяСтрока(СоставПолей.Шаблон), ТипЗаполненияОбластиТабличногоДокумента.Текст, ТипЗаполненияОбластиТабличногоДокумента.Шаблон);
							ОбластьРезультирующегоМакета.Текст = СоставПолей.Шаблон;
							
						ИначеЕсли СоставПолей.Свойство("Параметр") Тогда
							
							ОбластьРезультирующегоМакета.Заполнение = ТипЗаполненияОбластиТабличногоДокумента.Параметр;
							ОбластьРезультирующегоМакета.Параметр = СоставПолей.Параметр;
							
							Если СоставПолей.Свойство("Формат") Тогда
								
								ОбластьРезультирующегоМакета.Формат = СоставПолей.Формат;
								
							КонецЕсли;
							
						КонецЕсли;
						
						ОбластьРезультирующегоМакета.Объединить();
							
						ДобавленоПолей = ДобавленоПолей  + 1;
							
					КонецЦикла;
							
				КонецЕсли;
				
			КонецЦикла;
			
			Если ИзмерениеКолонки = Измерение Тогда
				
				КоличествоУровнейПоказателей = ПолучитьКоличествоУровнейПоказателей();
				
				Если КоличествоУровнейПоказателей > 0 И ДобавленоПолей < ИменованнаяОбластьРезультирующегоМакета.Низ - ИменованнаяОбластьРезультирующегоМакета.Верх - КоличествоУровнейПоказателей + 1 Тогда
					
					ОбластьИтог = Макет.Область(ИменованнаяОбластьРезультирующегоМакета.Верх + ДобавленоПолей, ИменованнаяОбластьРезультирующегоМакета.Лево, ИменованнаяОбластьРезультирующегоМакета.Низ - КоличествоУровнейПоказателей, ИменованнаяОбластьРезультирующегоМакета.Право);
					ОбластьИтог.Объединить();
					ОбластьИтог.Заполнение = ТипЗаполненияОбластиТабличногоДокумента.Текст;
					ОбластьИтог.Текст = "Итог";
					ОбластьИтог.ГоризонтальноеПоложение = ГоризонтальноеПоложение.Центр;
					ОбластьИтог.ВертикальноеПоложение = ВертикальноеПоложение.Центр;
					ОбластьИтог.РазмещениеТекста = ТипРазмещенияТекстаТабличногоДокумента.Переносить;
						
				КонецЕсли;
				
				ДобавленоПоказателей = 0;
				
				Для каждого ТекущийПоказатель из Показатели.Строки Цикл
						
					// Группа показателей
					Если ТекущийПоказатель.Строки.Количество() > 0 И ТекущийПоказатель.Использование > 0 Тогда
							
						КоличествоПоказателейВГруппе = 0;
						
						Для каждого Показатель из ТекущийПоказатель.Строки Цикл
								
							Если Показатель.Использование > 0 Тогда
								
								ОбластьПоказатель = Макет.Область(ИменованнаяОбластьРезультирующегоМакета.Низ, ИменованнаяОбластьРезультирующегоМакета.Лево + ДобавленоПоказателей, ИменованнаяОбластьРезультирующегоМакета.Низ, ИменованнаяОбластьРезультирующегоМакета.Лево + ДобавленоПоказателей);
								ОбластьПоказатель.Заполнение = ТипЗаполненияОбластиТабличногоДокумента.Текст;
								ОбластьПоказатель.Текст = Показатель.ПредставлениеВОтчете;
								ОбластьПоказатель.ГоризонтальноеПоложение = ГоризонтальноеПоложение.Центр;
								ОбластьПоказатель.ВертикальноеПоложение = ВертикальноеПоложение.Центр;
								ОбластьПоказатель.РазмещениеТекста = ТипРазмещенияТекстаТабличногоДокумента.Переносить;
								ОбластьПоказатель.ШиринаКолонки = ?(Показатель.Ширина = 0, мМинимальнаяШиринаКолонкиПоказатель, Показатель.Ширина);
									
								ДобавленоПоказателей = ДобавленоПоказателей + 1;
								КоличествоПоказателейВГруппе = КоличествоПоказателейВГруппе + 1;
									
							КонецЕсли;
								
						КонецЦикла;
							
						Если КоличествоПоказателейВГруппе > 0 Тогда
								
							ОбластьГруппаПоказателей = Макет.Область(ИменованнаяОбластьРезультирующегоМакета.Низ - 1, ИменованнаяОбластьРезультирующегоМакета.Лево + ДобавленоПоказателей - КоличествоПоказателейВГруппе, ИменованнаяОбластьРезультирующегоМакета.Низ - 1, ИменованнаяОбластьРезультирующегоМакета.Лево + ДобавленоПоказателей - 1);
							ОбластьГруппаПоказателей.Объединить();
							ОбластьГруппаПоказателей.Заполнение = ТипЗаполненияОбластиТабличногоДокумента.Текст;
							ОбластьГруппаПоказателей.Текст = ТекущийПоказатель.ПредставлениеВОтчете;
							ОбластьГруппаПоказателей.ГоризонтальноеПоложение = ГоризонтальноеПоложение.Центр;
							ОбластьГруппаПоказателей.ВертикальноеПоложение = ВертикальноеПоложение.Центр;
							ОбластьГруппаПоказателей.РазмещениеТекста = ТипРазмещенияТекстаТабличногоДокумента.Переносить;
							
						КонецЕсли;
							
					ИначеЕсли ТекущийПоказатель.Использование > 0 Тогда // Показатель
							
						ОбластьПоказатель = Макет.Область(ИменованнаяОбластьРезультирующегоМакета.Низ - КоличествоУровнейПоказателей + 1, ИменованнаяОбластьРезультирующегоМакета.Лево + ДобавленоПоказателей, ИменованнаяОбластьРезультирующегоМакета.Низ, ИменованнаяОбластьРезультирующегоМакета.Лево + ДобавленоПоказателей);
						ОбластьПоказатель.Объединить();
						ОбластьПоказатель.Заполнение = ТипЗаполненияОбластиТабличногоДокумента.Текст;
						ОбластьПоказатель.Текст = ТекущийПоказатель.ПредставлениеВОтчете;
						ОбластьПоказатель.ГоризонтальноеПоложение = ГоризонтальноеПоложение.Центр;
						ОбластьПоказатель.ВертикальноеПоложение = ВертикальноеПоложение.Центр;
						ОбластьПоказатель.РазмещениеТекста = ТипРазмещенияТекстаТабличногоДокумента.Переносить;
						ОбластьПоказатель.ШиринаКолонки = ?(ТекущийПоказатель.Ширина = 0, мМинимальнаяШиринаКолонкиПоказатель, ТекущийПоказатель.Ширина);
							
						ДобавленоПоказателей = ДобавленоПоказателей + 1;
							
					КонецЕсли;
						
				КонецЦикла;
				
				Прервать;
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры // ДобавитьОбластьИзмерение()

Процедура ДобавитьОбластьДетали(Макет, АвтоматическийМакет)
	
	НомерСтрокиМакета = Макет.ВысотаТаблицы + 1;
	
	Область = АвтоматическийМакет.ПолучитьОбласть("Детали");
	
	// Назначение имени области результирующего макета
	ОбластьРезультирующегоМакета = Макет.Область(НомерСтрокиМакета,, НомерСтрокиМакета);
	ОбластьРезультирующегоМакета.Имя = "Детали";
					
	ИндексПоля = 2;
				
	Для каждого Поле из мПоляДетали Цикл
		
		Если Поле = "" Тогда
				
			ОбластьРезультирующегоМакета = Макет.Область(НомерСтрокиМакета, ИндексПоля, НомерСтрокиМакета, ИндексПоля);
			ИндексПоля = ИндексПоля  + 1;
			
		Иначе
		
			Для ИндексСтолбца = 1 По Область.ШиринаТаблицы Цикл
						
				ТекущаяОбласть = Область.Область(1, ИндексСтолбца, 1, ИндексСтолбца);
					
				Если ЭтоОбластьДляВыводаПоля(ТекущаяОбласть, Поле, Поле) Тогда
							
					ОбластьРезультирующегоМакета = Макет.Область(НомерСтрокиМакета, ИндексПоля, НомерСтрокиМакета, ИндексПоля);
					ОбластьРезультирующегоМакета.ПараметрРасшифровки = ТекущаяОбласть.ПараметрРасшифровки;
					ОбластьРезультирующегоМакета.АвтоОтступ = ?(ИндексПоля = 2, 1, 0);
					ОбластьРезультирующегоМакета.РазмещениеТекста = ТипРазмещенияТекстаТабличногоДокумента.Переносить;
								
					ОбластьРезультирующегоМакета.Заполнение = ТипЗаполненияОбластиТабличногоДокумента.Параметр;
					ОбластьРезультирующегоМакета.Параметр = Поле;
									
					Если мСтруктураФорматаПолей.Свойство(Поле) Тогда
						
						ОбластьРезультирующегоМакета.Формат = мСтруктураФорматаПолей[Поле];
										
					КонецЕсли;
								
					ИндексПоля = ИндексПоля  + 1;
					
				КонецЕсли;
				
			КонецЦикла;
			
		КонецЕсли;
						
	КонецЦикла;
		
	ДобавленоРесурсов = 0;
				
	Для каждого ТекущийПоказатель из Показатели.Строки Цикл
					
		Если ТекущийПоказатель.Строки.Количество() > 0 И ТекущийПоказатель.Использование > 0 Тогда
						
			Для каждого Показатель из ТекущийПоказатель.Строки Цикл
							
				Если Показатель.Использование > 0 Тогда
								
					ОбластьРесурс = Макет.Область(НомерСтрокиМакета, ИндексПоля + ДобавленоРесурсов, НомерСтрокиМакета, ИндексПоля + ДобавленоРесурсов);
					ОбластьРесурс.Заполнение = ТипЗаполненияОбластиТабличногоДокумента.Параметр;
					ОбластьРесурс.Параметр = Показатель.Имя;
					ОбластьРесурс.ПараметрРасшифровки = "Расшифровка";
					ОбластьРесурс.Формат = Показатель.ФорматнаяСтрока;
					ОбластьРесурс.ГоризонтальноеПоложение = ГоризонтальноеПоложение.Право;
					ОбластьРесурс.ВертикальноеПоложение = ВертикальноеПоложение.Низ;
					ОбластьРесурс.РазмещениеТекста = ТипРазмещенияТекстаТабличногоДокумента.Переносить;
								
					ДобавленоРесурсов = ДобавленоРесурсов + 1;
								
				КонецЕсли;
							
			КонецЦикла;
						
		ИначеЕсли ТекущийПоказатель.Использование > 0 Тогда
						
			ОбластьРесурс = Макет.Область(НомерСтрокиМакета, ИндексПоля + ДобавленоРесурсов, НомерСтрокиМакета, ИндексПоля + ДобавленоРесурсов);
			ОбластьРесурс.Заполнение = ТипЗаполненияОбластиТабличногоДокумента.Параметр;
			ОбластьРесурс.Параметр = ТекущийПоказатель.Имя;
			ОбластьРесурс.ПараметрРасшифровки = "Расшифровка";
			ОбластьРесурс.Формат = ТекущийПоказатель.ФорматнаяСтрока;
			ОбластьРесурс.ГоризонтальноеПоложение = ГоризонтальноеПоложение.Право;
			ОбластьРесурс.ВертикальноеПоложение = ВертикальноеПоложение.Низ;
			ОбластьРесурс.РазмещениеТекста = ТипРазмещенияТекстаТабличногоДокумента.Переносить;
						
			ДобавленоРесурсов = ДобавленоРесурсов + 1;
						
		КонецЕсли;
					
	КонецЦикла;
	
КонецПроцедуры // ДобавитьОбластьДетали()

Процедура УстановитьФиксацию(ТабличныйДокумент) Экспорт
	
	ОбластьШапкаТаблицы = ТабличныйДокумент.Области.Найти("ШапкаТаблицы");
	
	Если ТипЗнч(ОбластьШапкаТаблицы) = Тип("ОбластьЯчеекТабличногоДокумента") Тогда
			
		ТабличныйДокумент.ФиксацияСверху = ОбластьШапкаТаблицы.Низ;
			
	Иначе
			
		ОбластьШапкаСтрок = ТабличныйДокумент.Области.Найти("ШапкаСтрок");
		
		Если ТипЗнч(ОбластьШапкаСтрок) = Тип("ОбластьЯчеекТабличногоДокумента") Тогда
				
			ТабличныйДокумент.ФиксацияСверху = ОбластьШапкаСтрок.Низ;
				
		КонецЕсли;
				
	КонецЕсли;
	
КонецПроцедуры // УстановитьФиксацию()

Процедура УстановитьКолонтитул(ТабличныйДокумент)
	
	УправлениеОтчетами.УстановитьКолонтитулыПоУмолчанию(ТабличныйДокумент, мНазваниеОтчета, Строка(ПараметрыСеанса.ТекущийПользователь));	
	
КонецПроцедуры // УстановитьКолонтитул()

Процедура ОформитьЗаголовок(ТабличныйДокумент)
	
	ОбластьЗаголовок = ТабличныйДокумент.Области.Найти("Заголовок");
	
	Если ТипЗнч(ОбластьЗаголовок) = Тип("ОбластьЯчеекТабличногоДокумента") Тогда
		
		ОбластьНазваниеОтчета = ТабличныйДокумент.Область(ОбластьЗаголовок.Верх, 2, ОбластьЗаголовок.Верх, 2);
		ОбластьНазваниеОтчета.Шрифт = Новый Шрифт(ОбластьНазваниеОтчета.Шрифт,, 12, Истина);
		
	КонецЕсли;
	
КонецПроцедуры // ОформитьЗаголовок()

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ РАСЧЕТА РАЗМЕРОВ ОБЛАСТЕЙ

Функция ПолучитьШиринуШапкиСтрок()
	
	ПередГруппировкойВОтдельнойКолонке = Ложь;
	ПередГруппировкойВОтдельныхКолонках = 0;
	ПослеГруппировкиВОтдельнойКолонке = Ложь;
	ПослеГруппировкиВОтдельныхКолонках = 0;
	
	Для каждого ИзмерениеСтроки из мСтруктураИзмеренийСтроки Цикл
		
		ПередГруппировкойВОтдельнойКолонке = ПередГруппировкойВОтдельнойКолонке ИЛИ ИзмерениеСтроки.Значение.ПередГруппировкой.ВОтдельнойКолонке.Количество() > 0;
		ПередГруппировкойВОтдельныхКолонках = Макс(ПередГруппировкойВОтдельныхКолонках, ИзмерениеСтроки.Значение.ПередГруппировкой.ВОтдельныхКолонках.Количество());
		ПослеГруппировкиВОтдельнойКолонке = ПослеГруппировкиВОтдельнойКолонке ИЛИ ИзмерениеСтроки.Значение.ПослеГруппировки.ВОтдельнойКолонке.Количество() > 0;
		ПослеГруппировкиВОтдельныхКолонках = Макс(ПослеГруппировкиВОтдельныхКолонках, ИзмерениеСтроки.Значение.ПослеГруппировки.ВОтдельныхКолонках.Количество());
		
	КонецЦикла;
	
	Возврат Макс(?(ПередГруппировкойВОтдельнойКолонке, 1, 0) + ПередГруппировкойВОтдельныхКолонках + ?(ПослеГруппировкиВОтдельнойКолонке, 1, 0) + ПослеГруппировкиВОтдельныхКолонках + 1, мПоляДетали.Количество());
	
КонецФункции // ПолучитьШиринуШапкиСтрок()

Функция ПолучитьВысотуШапки()
	
	КоличествоСтрокВИзмеренииКолонки = 0;
	КоличествоУровнейПоказателей = ПолучитьКоличествоУровнейПоказателей();
	
	Для каждого ИзмерениеКолонки из мСтруктураИзмеренийКолонки Цикл
		
		КоличествоСтрокВИзмеренииКолонки = КоличествоСтрокВИзмеренииКолонки	+
			?(ИзмерениеКолонки.Значение.ПередГруппировкой.ВОтдельнойКолонке.Количество() > 0, 1, 0) +
			ИзмерениеКолонки.Значение.ПередГруппировкой.ВОтдельныхКолонках.Количество() +
			?(ИзмерениеКолонки.Значение.ПослеГруппировки.ВОтдельнойКолонке.Количество() > 0, 1, 0) +
			ИзмерениеКолонки.Значение.ПослеГруппировки.ВОтдельныхКолонках.Количество() + 1;
		
	КонецЦикла;
	
	Возврат Макс(мСтруктураИзмеренийСтроки.Количество() + ?(ТипЗнч(мПоляДетали) = Тип("Массив"), ?(мПоляДетали.Количество() > 0, 1, 0), 0), КоличествоСтрокВИзмеренииКолонки + КоличествоУровнейПоказателей);
	
КонецФункции // ПолучитьВысотуШапки()

Функция ПолучитьШиринуОбластиИзмерениеКолонки()
	
	// Ширина области измерения колонки складывается из количества
	// группируемых и негруппируемых показателей
	
	ШиринаОбласти = 0;
	
	Для каждого ТекущийПоказатель из Показатели.Строки Цикл
		
		Если ТекущийПоказатель.Строки.Количество() > 0 И ТекущийПоказатель.Использование > 0 Тогда
			
			Для каждого Показатель из ТекущийПоказатель.Строки Цикл
				
				Если Показатель.Использование > 0 Тогда
					
					ШиринаОбласти = ШиринаОбласти + 1;
					
				КонецЕсли;
				
			КонецЦикла;
			
		ИначеЕсли ТекущийПоказатель.Использование > 0 Тогда
			
			ШиринаОбласти = ШиринаОбласти + 1;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат ШиринаОбласти;
	
КонецФункции // ПолучитьШиринуОбластиИзмерениеКолонки()

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ПОЛУЧЕНИЯ СТРУКТУРЫ И СОСТАВА ОБЛАСТЕЙ

Процедура ПолучитьСтруктуруИзмеренийСтроки(Макет)
	
	мСтруктураИзмеренийСтроки = Новый Структура;
	
	Для каждого ИзмерениеСтроки из ПостроительОтчета.ИзмеренияСтроки Цикл
			
		СтруктураИзмерения = Новый Структура("ПредставлениеИзмерения, ПередГруппировкой, ВместоГруппировки, ПослеГруппировки",
		ПолучитьПредставлениеПоля(ИзмерениеСтроки),
		Новый Структура("СГруппировками, ВОтдельныхКолонках, ВОтдельнойКолонке", Новый Массив, Новый Массив, Новый Массив),
		Новый Массив,
		Новый Структура("СГруппировками, ВОтдельныхКолонках, ВОтдельнойКолонке", Новый Массив, Новый Массив, Новый Массив));
			
		Если ТипЗнч(Макет.Области.Найти(ИзмерениеСтроки.Имя)) = Тип("ОбластьЯчеекТабличногоДокумента") Тогда
			
			ИменованнаяОбластьАвтоматическогоМакетаИзмерение = Макет.ПолучитьОбласть(ИзмерениеСтроки.Имя);
			
		Иначе
			
			// В качестве измерения строки выбрано поле, которое не может быть использовано в качестве измерения.
			мСтруктураИзмеренийСтроки.Вставить(ИзмерениеСтроки.Имя, СтруктураИзмерения);
			Продолжить;
			
		КонецЕсли;
		
		Для каждого ВыбранноеПоле из ПостроительОтчета.ВыбранныеПоля Цикл
			
			Для ИндексСтолбца = 3 По ИменованнаяОбластьАвтоматическогоМакетаИзмерение.ШиринаТаблицы Цикл
					
				ОбластьИзмеренияСтрокиАвтоматическогоМакета = ИменованнаяОбластьАвтоматическогоМакетаИзмерение.Область(1, ИндексСтолбца, 1, ИндексСтолбца);
							
				ДляИерархии = Ложь;
				
				Если ЭтоПолеСвязаноСИзмерением(ВыбранноеПоле.ПутьКДанным, ИзмерениеСтроки.ПутьКДанным, ДляИерархии) Тогда
						
					Если ЭтоОбластьДляВыводаПоля(ОбластьИзмеренияСтрокиАвтоматическогоМакета, ВыбранноеПоле.Имя, ВыбранноеПоле.ПутьКДанным) Тогда
									
						Положение = ПолучитьПоложениеВыбранногоПоля(ВыбранноеПоле.Имя);
						Размещение = ПолучитьРазмещениеВыбранногоПоля(ВыбранноеПоле.Имя);
								
						Положение = СтрЗаменить(Положение, " ", "");
								
						Если Положение = "Вместогруппировки" Тогда
									
							СтруктураИзмерения[Положение].Добавить(Новый Структура("ИмяПоля, ПредставлениеПоля, ДляИерархии", ОбластьИзмеренияСтрокиАвтоматическогоМакета.Параметр, ПолучитьПредставлениеПоля(ВыбранноеПоле, 1), ДляИерархии));
									
						ИначеЕсли Размещение = ТипРазмещенияРеквизитовИзмерений.ВместеСИзмерениями Тогда
									
							СтруктураИзмерения[Положение].СГруппировками.Добавить(Новый Структура("ИмяПоля, ПредставлениеПоля, ДляИерархии", ОбластьИзмеренияСтрокиАвтоматическогоМакета.Параметр, ПолучитьПредставлениеПоля(ВыбранноеПоле), ДляИерархии));
									
						ИначеЕсли Размещение = ТипРазмещенияРеквизитовИзмерений.Отдельно Тогда
									
							СтруктураИзмерения[Положение].ВОтдельныхКолонках.Добавить(Новый Структура("ИмяПоля, ПредставлениеПоля, ДляИерархии", ОбластьИзмеренияСтрокиАвтоматическогоМакета.Параметр, ПолучитьПредставлениеПоля(ВыбранноеПоле), ДляИерархии));
									
						ИначеЕсли Размещение = ТипРазмещенияРеквизитовИзмерений.Вместе Тогда
									
							СтруктураИзмерения[Положение].ВОтдельнойКолонке.Добавить(Новый Структура("ИмяПоля, ПредставлениеПоля, ДляИерархии", ОбластьИзмеренияСтрокиАвтоматическогоМакета.Параметр, ПолучитьПредставлениеПоля(ВыбранноеПоле), ДляИерархии));
									
						КонецЕсли;
								
					КонецЕсли;
						
				ИначеЕсли ЭтоПолеПоказатель(ВыбранноеПоле.ПутьКДанным) Тогда
						
					Если ЭтоОбластьДляВыводаПоля(ОбластьИзмеренияСтрокиАвтоматическогоМакета, ВыбранноеПоле.Имя, ВыбранноеПоле.ПутьКДанным) Тогда
									
						Положение = ПолучитьПоложениеВыбранногоПоля(ВыбранноеПоле.Имя);
						Размещение = ПолучитьРазмещениеВыбранногоПоля(ВыбранноеПоле.Имя);
								
						Положение = СтрЗаменить(Положение, " ", "");
								
						Если Положение = "Вместогруппировки" Тогда
									
							СтруктураИзмерения[Положение].Добавить(Новый Структура("ИмяПоля, ПредставлениеПоля, ДляИерархии", ОбластьИзмеренияСтрокиАвтоматическогоМакета.Параметр, ПолучитьПредставлениеПоля(ВыбранноеПоле, 1), Истина));
									
						ИначеЕсли Размещение = ТипРазмещенияРеквизитовИзмерений.ВместеСИзмерениями Тогда
									
							СтруктураИзмерения[Положение].СГруппировками.Добавить(Новый Структура("ИмяПоля, ПредставлениеПоля, ДляИерархии", ОбластьИзмеренияСтрокиАвтоматическогоМакета.Параметр, ПолучитьПредставлениеПоля(ВыбранноеПоле), Истина));
									
						ИначеЕсли Размещение = ТипРазмещенияРеквизитовИзмерений.Отдельно Тогда
									
							СтруктураИзмерения[Положение].ВОтдельныхКолонках.Добавить(Новый Структура("ИмяПоля, ПредставлениеПоля, ДляИерархии", ОбластьИзмеренияСтрокиАвтоматическогоМакета.Параметр, ПолучитьПредставлениеПоля(ВыбранноеПоле), Истина));
									
						ИначеЕсли Размещение = ТипРазмещенияРеквизитовИзмерений.Вместе Тогда
									
							СтруктураИзмерения[Положение].ВОтдельнойКолонке.Добавить(Новый Структура("ИмяПоля, ПредставлениеПоля, ДляИерархии", ОбластьИзмеренияСтрокиАвтоматическогоМакета.Параметр, ПолучитьПредставлениеПоля(ВыбранноеПоле), Истина));
									
						КонецЕсли;
								
					КонецЕсли;
						
				КонецЕсли;
						
			КонецЦикла;
		
			Если ТипЗнч(Макет.Области.Найти("Ресурсы")) = Тип("ОбластьЯчеекТабличногоДокумента") Тогда
				
				ИменованнаяОбластьАвтоматическогоМакетаРесурсы = Макет.ПолучитьОбласть("Ресурсы");
				
			Иначе
				
				мСтруктураИзмеренийСтроки.Вставить(ИзмерениеСтроки.Имя, СтруктураИзмерения);
				Продолжить;
				
			КонецЕсли;
			
			Для ИндексСтолбца = 1 По ИменованнаяОбластьАвтоматическогоМакетаРесурсы.ШиринаТаблицы Цикл
					
				ОбластьИзмеренияСтрокиАвтоматическогоМакета = ИменованнаяОбластьАвтоматическогоМакетаРесурсы.Область(1, ИндексСтолбца, 1, ИндексСтолбца);
				
				ДляИерархии = Ложь;
				
				Если ЭтоПолеСвязаноСИзмерением(ВыбранноеПоле.ПутьКДанным, ИзмерениеСтроки.ПутьКДанным, ДляИерархии) Тогда
						
					Если ЭтоОбластьДляВыводаПоля(ОбластьИзмеренияСтрокиАвтоматическогоМакета, ВыбранноеПоле.Имя, ВыбранноеПоле.ПутьКДанным) Тогда
									
						Положение = ПолучитьПоложениеВыбранногоПоля(ВыбранноеПоле.Имя);
						Размещение = ПолучитьРазмещениеВыбранногоПоля(ВыбранноеПоле.Имя);
								
						Положение = СтрЗаменить(Положение, " ", "");
								
						Если Положение = "Вместогруппировки" Тогда
									
							СтруктураИзмерения[Положение].Добавить(Новый Структура("ИмяПоля, ПредставлениеПоля, ДляИерархии", ОбластьИзмеренияСтрокиАвтоматическогоМакета.Параметр, ПолучитьПредставлениеПоля(ВыбранноеПоле), ДляИерархии));
									
						ИначеЕсли Размещение = ТипРазмещенияРеквизитовИзмерений.ВместеСИзмерениями Тогда
									
							СтруктураИзмерения[Положение].СГруппировками.Добавить(Новый Структура("ИмяПоля, ПредставлениеПоля, ДляИерархии", ОбластьИзмеренияСтрокиАвтоматическогоМакета.Параметр, ПолучитьПредставлениеПоля(ВыбранноеПоле), ДляИерархии));
									
						ИначеЕсли Размещение = ТипРазмещенияРеквизитовИзмерений.Отдельно Тогда
									
							СтруктураИзмерения[Положение].ВОтдельныхКолонках.Добавить(Новый Структура("ИмяПоля, ПредставлениеПоля, ДляИерархии", ОбластьИзмеренияСтрокиАвтоматическогоМакета.Параметр, ПолучитьПредставлениеПоля(ВыбранноеПоле), ДляИерархии));
									
						ИначеЕсли Размещение = ТипРазмещенияРеквизитовИзмерений.Вместе Тогда
									
							СтруктураИзмерения[Положение].ВОтдельнойКолонке.Добавить(Новый Структура("ИмяПоля, ПредставлениеПоля, ДляИерархии", ОбластьИзмеренияСтрокиАвтоматическогоМакета.Параметр, ПолучитьПредставлениеПоля(ВыбранноеПоле), ДляИерархии));
									
						КонецЕсли;
								
					КонецЕсли;
						
				КонецЕсли;
						
			КонецЦикла;
			
		КонецЦикла;
			
		мСтруктураИзмеренийСтроки.Вставить(ИзмерениеСтроки.Имя, СтруктураИзмерения);
	
	КонецЦикла;

КонецПроцедуры // ПолучитьСтруктуруИзмеренийСтроки()

Функция ПолучитьСоставПолейИзмеренияСтроки(ИмяИзмерения, ЗначениеИзмерения, Иерархия)
	
	Результат = Новый Массив;
	
	ПередГруппировкойВОтдельнойКолонке = Ложь;
	ПередГруппировкойВОтдельныхКолонках = 0;
	ПослеГруппировкиВОтдельнойКолонке = Ложь;
	ПослеГруппировкиВОтдельныхКолонках = 0;
	
	Для каждого ИзмерениеСтроки из мСтруктураИзмеренийСтроки Цикл
		
		ПередГруппировкойВОтдельнойКолонке = ПередГруппировкойВОтдельнойКолонке ИЛИ ИзмерениеСтроки.Значение.ПередГруппировкой.ВОтдельнойКолонке.Количество() > 0;
		ПередГруппировкойВОтдельныхКолонках = Макс(ПередГруппировкойВОтдельныхКолонках, ИзмерениеСтроки.Значение.ПередГруппировкой.ВОтдельныхКолонках.Количество());
		ПослеГруппировкиВОтдельнойКолонке = ПослеГруппировкиВОтдельнойКолонке ИЛИ ИзмерениеСтроки.Значение.ПослеГруппировки.ВОтдельнойКолонке.Количество() > 0;
		ПослеГруппировкиВОтдельныхКолонках = Макс(ПослеГруппировкиВОтдельныхКолонках, ИзмерениеСтроки.Значение.ПослеГруппировки.ВОтдельныхКолонках.Количество());
		
	КонецЦикла;
	
	// Перед группировкой в отдельной колонке
	Если ПередГруппировкойВОтдельнойКолонке Тогда
		
		Шаблон = "";
		
		Если мСтруктураИзмеренийСтроки[ИмяИзмерения].ПередГруппировкой.ВОтдельнойКолонке.Количество() = 1 Тогда
			
			Поле = мСтруктураИзмеренийСтроки[ИмяИзмерения].ПередГруппировкой.ВОтдельнойКолонке[0];
			
			Если Иерархия И НЕ Поле.ДляИерархии Тогда
				
				Результат.Добавить(Новый Структура("Шаблон", ""));
					
			ИначеЕсли мСтруктураФорматаПолей.Свойство(Поле.ИмяПоля) Тогда
				
				Результат.Добавить(Новый Структура("Параметр, Формат", Поле.ИмяПоля, мСтруктураФорматаПолей[Поле.ИмяПоля]));
				
			Иначе
				
				Результат.Добавить(Новый Структура("Параметр", Поле.ИмяПоля));
					
			КонецЕсли;
			
		Иначе
			
			Для каждого Поле из мСтруктураИзмеренийСтроки[ИмяИзмерения].ПередГруппировкой.ВОтдельнойКолонке Цикл
				
				Если Иерархия И НЕ Поле.ДляИерархии Тогда
					
					Продолжить;
					
				КонецЕсли;
				
				Шаблон = Шаблон + ?(ПустаяСтрока(Шаблон), "", ", ") + "[" + Поле.ИмяПоля + "]";
				
			КонецЦикла;
			
			Результат.Добавить(Новый Структура("Шаблон", Шаблон));
			
		КонецЕсли;
		
	КонецЕсли;
	
	// Перед группировкой в отдельных колонках
	Для каждого Поле из мСтруктураИзмеренийСтроки[ИмяИзмерения].ПередГруппировкой.ВОтдельныхКолонках Цикл
		
		Если Иерархия И НЕ Поле.ДляИерархии Тогда
			
			Результат.Добавить(Новый Структура("Шаблон", ""));
				
		ИначеЕсли мСтруктураФорматаПолей.Свойство(Поле.ИмяПоля) Тогда
			
			Результат.Добавить(Новый Структура("Параметр, Формат", Поле.ИмяПоля, мСтруктураФорматаПолей[Поле.ИмяПоля]));
			
		Иначе
			
			Результат.Добавить(Новый Структура("Параметр", Поле.ИмяПоля));
				
		КонецЕсли;
		
	КонецЦикла;
	
	// Перед группировкой в отдельных колонках (пустые)
	Для Индекс = мСтруктураИзмеренийСтроки[ИмяИзмерения].ПередГруппировкой.ВОтдельныхКолонках.Количество() По ПередГруппировкойВОтдельныхКолонках - 1 Цикл
		
		Результат.Добавить(Новый Структура("Шаблон", ""));
		
	КонецЦикла;
	
	// Значение измерения
	Если мСтруктураФорматаПолей.Свойство(ЗначениеИзмерения) Тогда
		
		Результат.Добавить(Новый Структура("Параметр, Формат", ЗначениеИзмерения, мСтруктураФорматаПолей[ЗначениеИзмерения]));
		
	Иначе
	
		Если мСтруктураИзмеренийСтроки[ИмяИзмерения].ПередГруппировкой.СГруппировками.Количество() = 0 И мСтруктураИзмеренийСтроки[ИмяИзмерения].ВместоГруппировки.Количество() = 1 И мСтруктураИзмеренийСтроки[ИмяИзмерения].ПослеГруппировки.СГруппировками.Количество() = 0 Тогда
			
			Поле = мСтруктураИзмеренийСтроки[ИмяИзмерения].ВместоГруппировки[0];
			
			Если Иерархия И НЕ Поле.ДляИерархии Тогда
							
				Если мСтруктураФорматаПолей.Свойство(ЗначениеИзмерения) Тогда
						
					Результат.Добавить(Новый Структура("Параметр, Формат", ЗначениеИзмерения, мСтруктураФорматаПолей[ЗначениеИзмерения]));
						
				Иначе
						
					Результат.Добавить(Новый Структура("Параметр", ЗначениеИзмерения));
							
				КонецЕсли;
				
			Иначе
							
				Если мСтруктураФорматаПолей.Свойство(ЗначениеИзмерения) Тогда
						
					Результат.Добавить(Новый Структура("Параметр, Формат", Поле.ИмяПоля, мСтруктураФорматаПолей[Поле.ИмяПоля]));
						
				Иначе
						
					Результат.Добавить(Новый Структура("Параметр", Поле.ИмяПоля));
							
				КонецЕсли;
							
			КонецЕсли;
			
		Иначе
			
			Шаблон = "";
			
			Для каждого Поле из мСтруктураИзмеренийСтроки[ИмяИзмерения].ПередГруппировкой.СГруппировками Цикл
				
				Если Иерархия И НЕ Поле.ДляИерархии Тогда
					
					Продолжить;
					
				КонецЕсли;
						
				Шаблон = Шаблон + ?(ПустаяСтрока(Шаблон), "", ", ") + "[" + Поле.ИмяПоля + "]";
						
			КонецЦикла;
				
			Если мСтруктураИзмеренийСтроки[ИмяИзмерения].ВместоГруппировки.Количество() > 0 Тогда
							
				Для каждого Поле из мСтруктураИзмеренийСтроки[ИмяИзмерения].ВместоГруппировки Цикл
								
					Если Иерархия И НЕ Поле.ДляИерархии Тогда
							
						Шаблон = Шаблон + ?(ПустаяСтрока(Шаблон), "", ", ") + "[" + ЗначениеИзмерения + "]";
							
					Иначе
							
						Шаблон = Шаблон + ?(ПустаяСтрока(Шаблон), "", ", ") + "[" + Поле.ИмяПоля + "]";
							
					КонецЕсли;
								
				КонецЦикла;
							
			Иначе
							
				Шаблон = Шаблон + ?(ПустаяСтрока(Шаблон), "", ", ") + "[" + ЗначениеИзмерения + "]";
							
			КонецЕсли;
					
			Для каждого Поле из мСтруктураИзмеренийСтроки[ИмяИзмерения].ПослеГруппировки.СГруппировками Цикл
				
				Если Иерархия И НЕ Поле.ДляИерархии Тогда
					
					Продолжить;
					
				КонецЕсли;
						
				Шаблон = Шаблон + ?(ПустаяСтрока(Шаблон), "", ", ") + "[" + Поле.ИмяПоля + "]";
						
			КонецЦикла;
			
			Результат.Добавить(Новый Структура("Шаблон", Шаблон));
			
		КонецЕсли;
		
	КонецЕсли;
	
	// После группировки в отдельной колонке
	Если ПослеГруппировкиВОтдельнойКолонке Тогда
		
		Шаблон = "";
		
		Если мСтруктураИзмеренийСтроки[ИмяИзмерения].ПослеГруппировки.ВОтдельнойКолонке.Количество() = 1 Тогда
			
			Поле = мСтруктураИзмеренийСтроки[ИмяИзмерения].ПослеГруппировки.ВОтдельнойКолонке[0];
			
			Если Иерархия И НЕ Поле.ДляИерархии Тогда
				
				Результат.Добавить(Новый Структура("Шаблон", ""));
					
			ИначеЕсли мСтруктураФорматаПолей.Свойство(Поле.ИмяПоля) Тогда
				
				Результат.Добавить(Новый Структура("Параметр, Формат", Поле.ИмяПоля, мСтруктураФорматаПолей[Поле.ИмяПоля]));
				
			Иначе
				
				Результат.Добавить(Новый Структура("Параметр", Поле.ИмяПоля));
					
			КонецЕсли;
			
		Иначе
			
			Для каждого Поле из мСтруктураИзмеренийСтроки[ИмяИзмерения].ПослеГруппировки.ВОтдельнойКолонке Цикл
				
				Если Иерархия И НЕ Поле.ДляИерархии Тогда
					
					Продолжить;
					
				КонецЕсли;
				
				Шаблон = Шаблон + ?(ПустаяСтрока(Шаблон), "", ", ") + "[" + Поле.ИмяПоля + "]";
				
			КонецЦикла;
			
			Результат.Добавить(Новый Структура("Шаблон", Шаблон));
			
		КонецЕсли;
		
	КонецЕсли;
	
	// После группировки в отдельных колонках
	Для каждого Поле из мСтруктураИзмеренийСтроки[ИмяИзмерения].ПослеГруппировки.ВОтдельныхКолонках Цикл
			
		Если Иерархия И НЕ Поле.ДляИерархии Тогда
			
			Результат.Добавить(Новый Структура("Шаблон", ""));
				
		ИначеЕсли мСтруктураФорматаПолей.Свойство(Поле.ИмяПоля) Тогда
			
			Результат.Добавить(Новый Структура("Параметр, Формат", Поле.ИмяПоля, мСтруктураФорматаПолей[Поле.ИмяПоля]));
			
		Иначе
			
			Результат.Добавить(Новый Структура("Параметр", Поле.ИмяПоля));
				
		КонецЕсли;
		
	КонецЦикла;
	
	// После группировки в отдельных колонках (пустые)
	Для Индекс = мСтруктураИзмеренийСтроки[ИмяИзмерения].ПослеГруппировки.ВОтдельныхКолонках.Количество() По ПослеГруппировкиВОтдельныхКолонках - 1 Цикл
		
		Результат.Добавить(Новый Структура("Шаблон", ""));
		
	КонецЦикла;
	
	Для Индекс = Результат.Количество() По мПоляДетали.Количество() - 1 Цикл
		
		Результат.Добавить(Новый Структура("Шаблон", ""));
		
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции // ПолучитьСоставПолейИзмеренияСтроки()

Функция ПолучитьСоставЗаголовкаИзмеренияСтроки(ИмяИзмерения)
	
	Результат = Новый Массив;
	
	ПередГруппировкойВОтдельнойКолонке = Ложь;
	ПередГруппировкойВОтдельныхКолонках = 0;
	ПослеГруппировкиВОтдельнойКолонке = Ложь;
	ПослеГруппировкиВОтдельныхКолонках = 0;
	
	Для каждого ИзмерениеСтроки из мСтруктураИзмеренийСтроки Цикл
		
		ПередГруппировкойВОтдельнойКолонке = ПередГруппировкойВОтдельнойКолонке ИЛИ ИзмерениеСтроки.Значение.ПередГруппировкой.ВОтдельнойКолонке.Количество() > 0;
		ПередГруппировкойВОтдельныхКолонках = Макс(ПередГруппировкойВОтдельныхКолонках, ИзмерениеСтроки.Значение.ПередГруппировкой.ВОтдельныхКолонках.Количество());
		ПослеГруппировкиВОтдельнойКолонке = ПослеГруппировкиВОтдельнойКолонке ИЛИ ИзмерениеСтроки.Значение.ПослеГруппировки.ВОтдельнойКолонке.Количество() > 0;
		ПослеГруппировкиВОтдельныхКолонках = Макс(ПослеГруппировкиВОтдельныхКолонках, ИзмерениеСтроки.Значение.ПослеГруппировки.ВОтдельныхКолонках.Количество());
		
	КонецЦикла;
	
	// Перед группировкой в отдельной колонке
	Если ПередГруппировкойВОтдельнойКолонке Тогда
		
		ПредставлениеЗаголовка = "";
		
		Для каждого Поле из мСтруктураИзмеренийСтроки[ИмяИзмерения].ПередГруппировкой.ВОтдельнойКолонке Цикл
			
			ПредставлениеЗаголовка = ПредставлениеЗаголовка + ?(ПустаяСтрока(ПредставлениеЗаголовка), "", ", ") + Поле.ПредставлениеПоля;
			
		КонецЦикла;
		
		Результат.Добавить(ПредставлениеЗаголовка);
		
	КонецЕсли;
	
	// Перед группировкой в отдельных колонках
	Для каждого Поле из мСтруктураИзмеренийСтроки[ИмяИзмерения].ПередГруппировкой.ВОтдельныхКолонках Цикл
		
		Результат.Добавить(Поле.ПредставлениеПоля);
		
	КонецЦикла;
	
	// Перед группировкой в отдельных колонках (пустые)
	Для Индекс = мСтруктураИзмеренийСтроки[ИмяИзмерения].ПередГруппировкой.ВОтдельныхКолонках.Количество() По ПередГруппировкойВОтдельныхКолонках - 1 Цикл
		
		Результат.Добавить("");
		
	КонецЦикла;
	
	// Значение измерения
	ПредставлениеЗаголовка = "";
	
	Для каждого Поле из мСтруктураИзмеренийСтроки[ИмяИзмерения].ПередГруппировкой.СГруппировками Цикл
		
		ПредставлениеЗаголовка = ПредставлениеЗаголовка + ?(ПустаяСтрока(ПредставлениеЗаголовка), "", ", ") + Поле.ПредставлениеПоля;
				
	КонецЦикла;
		
	Если мСтруктураИзмеренийСтроки[ИмяИзмерения].ВместоГруппировки.Количество() > 0 Тогда
					
		Для каждого Поле из мСтруктураИзмеренийСтроки[ИмяИзмерения].ВместоГруппировки Цикл
						
			ПредставлениеЗаголовка = ПредставлениеЗаголовка + ?(ПустаяСтрока(ПредставлениеЗаголовка), "", ", ") + Поле.ПредставлениеПоля;
						
		КонецЦикла;
					
	Иначе
					
		ПредставлениеЗаголовка = ПредставлениеЗаголовка + ?(ПустаяСтрока(ПредставлениеЗаголовка), "", ", ") + мСтруктураИзмеренийСтроки[ИмяИзмерения].ПредставлениеИзмерения;
					
	КонецЕсли;
			
	Для каждого Поле из мСтруктураИзмеренийСтроки[ИмяИзмерения].ПослеГруппировки.СГруппировками Цикл
		
		ПредставлениеЗаголовка = ПредставлениеЗаголовка + ?(ПустаяСтрока(ПредставлениеЗаголовка), "", ", ") + Поле.ПредставлениеПоля;
				
	КонецЦикла;
	
	Результат.Добавить(ПредставлениеЗаголовка);
			
	// После группировки в отдельной колонке
	Если ПослеГруппировкиВОтдельнойКолонке Тогда
		
		ПредставлениеЗаголовка = "";
		
		Для каждого Поле из мСтруктураИзмеренийСтроки[ИмяИзмерения].ПослеГруппировки.ВОтдельнойКолонке Цикл
			
			ПредставлениеЗаголовка = ПредставлениеЗаголовка + ?(ПустаяСтрока(ПредставлениеЗаголовка), "", ", ") + Поле.ПредставлениеПоля;
			
		КонецЦикла;
		
		Результат.Добавить(ПредставлениеЗаголовка);
		
	КонецЕсли;
	
	// После группировки в отдельных колонках
	Для каждого Поле из мСтруктураИзмеренийСтроки[ИмяИзмерения].ПослеГруппировки.ВОтдельныхКолонках Цикл
			
		Результат.Добавить(Поле.ПредставлениеПоля);
		
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции // ПолучитьСоставЗаголовкаИзмеренияСтроки()

Процедура ПолучитьСтруктуруИзмеренийКолонки(Макет)
	
	мСтруктураИзмеренийКолонки = Новый Структура;
	
	Для каждого ИзмерениеКолонки из ПостроительОтчета.ИзмеренияКолонки Цикл
			
		СтруктураИзмерения = Новый Структура("ПредставлениеИзмерения, ПередГруппировкой, ВместоГруппировки, ПослеГруппировки",
		ПолучитьПредставлениеПоля(ИзмерениеКолонки),
		Новый Структура("СГруппировками, ВОтдельныхКолонках, ВОтдельнойКолонке", Новый Массив, Новый Массив, Новый Массив),
		Новый Массив,
		Новый Структура("СГруппировками, ВОтдельныхКолонках, ВОтдельнойКолонке", Новый Массив, Новый Массив, Новый Массив));
		
		Если ТипЗнч(Макет.Области.Найти(ИзмерениеКолонки.Имя)) = Тип("ОбластьЯчеекТабличногоДокумента") Тогда
			
			ИменованнаяОбластьАвтоматическогоМакетаИзмерение = Макет.ПолучитьОбласть(ИзмерениеКолонки.Имя);
			
		Иначе
			
			// В качестве измерения колонки выбрано поле, которое не может быть использовано в качестве измерения.
			мСтруктураИзмеренийКолонки.Вставить(ИзмерениеКолонки.Имя, СтруктураИзмерения);
			Продолжить;
			
		КонецЕсли;
		
		Для каждого ВыбранноеПоле из ПостроительОтчета.ВыбранныеПоля Цикл
			
			Для ИндексСтроки = 2 По ИменованнаяОбластьАвтоматическогоМакетаИзмерение.ВысотаТаблицы Цикл
					
				ОбластьИзмеренияКолонкиАвтоматическогоМакета = ИменованнаяОбластьАвтоматическогоМакетаИзмерение.Область(ИндексСтроки, 1, ИндексСтроки, 1);
							
				Если ЭтоОбластьДляВыводаПоля(ОбластьИзмеренияКолонкиАвтоматическогоМакета, ВыбранноеПоле.Имя, ВыбранноеПоле.ПутьКДанным) Тогда
								
					Положение = ПолучитьПоложениеВыбранногоПоля(ВыбранноеПоле.Имя);
					Размещение = ПолучитьРазмещениеВыбранногоПоля(ВыбранноеПоле.Имя);
							
					Положение = СтрЗаменить(Положение, " ", "");
							
					Если Положение = "Вместогруппировки" Тогда
								
						СтруктураИзмерения[Положение].Добавить(Новый Структура("ИмяПоля, ПредставлениеПоля, ДляИерархии", ОбластьИзмеренияКолонкиАвтоматическогоМакета.Параметр, ПолучитьПредставлениеПоля(ВыбранноеПоле), Истина));
								
					ИначеЕсли Размещение = ТипРазмещенияРеквизитовИзмерений.ВместеСИзмерениями Тогда
								
						СтруктураИзмерения[Положение].СГруппировками.Добавить(Новый Структура("ИмяПоля, ПредставлениеПоля, ДляИерархии", ОбластьИзмеренияКолонкиАвтоматическогоМакета.Параметр, ПолучитьПредставлениеПоля(ВыбранноеПоле), Истина));
								
					ИначеЕсли Размещение = ТипРазмещенияРеквизитовИзмерений.Отдельно Тогда
								
						СтруктураИзмерения[Положение].ВОтдельныхКолонках.Добавить(Новый Структура("ИмяПоля, ПредставлениеПоля, ДляИерархии", ОбластьИзмеренияКолонкиАвтоматическогоМакета.Параметр, ПолучитьПредставлениеПоля(ВыбранноеПоле), Истина));
								
					ИначеЕсли Размещение = ТипРазмещенияРеквизитовИзмерений.Вместе Тогда
								
						СтруктураИзмерения[Положение].ВОтдельнойКолонке.Добавить(Новый Структура("ИмяПоля, ПредставлениеПоля, ДляИерархии", ОбластьИзмеренияКолонкиАвтоматическогоМакета.Параметр, ПолучитьПредставлениеПоля(ВыбранноеПоле), Истина));
								
					КонецЕсли;
							
				КонецЕсли;
						
			КонецЦикла;
		
			Если ТипЗнч(Макет.Области.Найти("Ресурсы")) = Тип("ОбластьЯчеекТабличногоДокумента") Тогда
				
				ИменованнаяОбластьАвтоматическогоМакетаРесурсы = Макет.ПолучитьОбласть("Ресурсы");
				
			Иначе
				
				мСтруктураИзмеренийКолонки.Вставить(ИзмерениеКолонки.Имя, СтруктураИзмерения);
				Продолжить;
				
			КонецЕсли;
			
			Для ИндексСтолбца = 1 По ИменованнаяОбластьАвтоматическогоМакетаРесурсы.ШиринаТаблицы Цикл
					
				ОбластьИзмеренияСтрокиАвтоматическогоМакета = ИменованнаяОбластьАвтоматическогоМакетаРесурсы.Область(1, ИндексСтолбца, 1, ИндексСтолбца);
					
				ДляИерархии = Ложь;
				
				Если ЭтоПолеСвязаноСИзмерением(ВыбранноеПоле.ПутьКДанным, ИзмерениеКолонки.ПутьКДанным, ДляИерархии) Тогда

					Если ЭтоОбластьДляВыводаПоля(ОбластьИзмеренияСтрокиАвтоматическогоМакета, ВыбранноеПоле.Имя, ВыбранноеПоле.ПутьКДанным) Тогда
									
						Положение = ПолучитьПоложениеВыбранногоПоля(ВыбранноеПоле.Имя);
						Размещение = ПолучитьРазмещениеВыбранногоПоля(ВыбранноеПоле.Имя);
								
						Положение = СтрЗаменить(Положение, " ", "");
								
						Если Положение = "Вместогруппировки" Тогда
									
							СтруктураИзмерения[Положение].Добавить(Новый Структура("ИмяПоля, ПредставлениеПоля, ДляИерархии", ОбластьИзмеренияСтрокиАвтоматическогоМакета.Параметр, ПолучитьПредставлениеПоля(ВыбранноеПоле), ДляИерархии));
									
						ИначеЕсли Размещение = ТипРазмещенияРеквизитовИзмерений.ВместеСИзмерениями Тогда
									
							СтруктураИзмерения[Положение].СГруппировками.Добавить(Новый Структура("ИмяПоля, ПредставлениеПоля, ДляИерархии", ОбластьИзмеренияСтрокиАвтоматическогоМакета.Параметр, ПолучитьПредставлениеПоля(ВыбранноеПоле), ДляИерархии));
									
						ИначеЕсли Размещение = ТипРазмещенияРеквизитовИзмерений.Отдельно Тогда
									
							СтруктураИзмерения[Положение].ВОтдельныхКолонках.Добавить(Новый Структура("ИмяПоля, ПредставлениеПоля, ДляИерархии", ОбластьИзмеренияСтрокиАвтоматическогоМакета.Параметр, ПолучитьПредставлениеПоля(ВыбранноеПоле), ДляИерархии));
									
						ИначеЕсли Размещение = ТипРазмещенияРеквизитовИзмерений.Вместе Тогда
									
							СтруктураИзмерения[Положение].ВОтдельнойКолонке.Добавить(Новый Структура("ИмяПоля, ПредставлениеПоля, ДляИерархии", ОбластьИзмеренияСтрокиАвтоматическогоМакета.Параметр, ПолучитьПредставлениеПоля(ВыбранноеПоле), ДляИерархии));
									
						КонецЕсли;
								
					КонецЕсли;
						
				КонецЕсли;
						
			КонецЦикла;
			
		КонецЦикла;
			
		мСтруктураИзмеренийКолонки.Вставить(ИзмерениеКолонки.Имя, СтруктураИзмерения);
	
	КонецЦикла;

КонецПроцедуры // ПолучитьСтруктуруИзмеренийКолонки()

Функция ПолучитьСоставПолейИзмеренияКолонки(ИмяИзмерения, ЗначениеИзмерения, Иерархия)
	
	Результат = Новый Массив;
	
	// Перед группировкой в отдельной колонке
	Если мСтруктураИзмеренийКолонки[ИмяИзмерения].ПередГруппировкой.ВОтдельнойКолонке.Количество() > 0 Тогда
		
		Если мСтруктураИзмеренийКолонки[ИмяИзмерения].ПередГруппировкой.ВОтдельнойКолонке.Количество() = 1 Тогда
			
			Поле = мСтруктураИзмеренийКолонки[ИмяИзмерения].ПередГруппировкой.ВОтдельнойКолонке[0];
			
			Если Иерархия И НЕ Поле.ДляИерархии Тогда
				
				Результат.Добавить(Новый Структура("Шаблон", ""));
					
			ИначеЕсли мСтруктураФорматаПолей.Свойство(Поле.ИмяПоля) Тогда
				
				Результат.Добавить(Новый Структура("Параметр, Формат", Поле.ИмяПоля, мСтруктураФорматаПолей[Поле.ИмяПоля]));
				
			Иначе
				
				Результат.Добавить(Новый Структура("Параметр", Поле.ИмяПоля));
					
			КонецЕсли;
			
		Иначе
			
			Шаблон = "";
			
			Для каждого Поле из мСтруктураИзмеренийКолонки[ИмяИзмерения].ПередГруппировкой.ВОтдельнойКолонке Цикл
				
				Если Иерархия И НЕ Поле.ДляИерархии Тогда
					
					Продолжить;
					
				КонецЕсли;
				
				Шаблон = Шаблон + ?(ПустаяСтрока(Шаблон), "", ", ") + "[" + Поле.ИмяПоля + "]";
				
			КонецЦикла;
			
			Результат.Добавить(Новый Структура("Шаблон", Шаблон));
			
		КонецЕсли;
		
	КонецЕсли;
	
	// Перед группировкой в отдельных колонках
	Для каждого Поле из мСтруктураИзмеренийКолонки[ИмяИзмерения].ПередГруппировкой.ВОтдельныхКолонках Цикл
		
		Если Иерархия И НЕ Поле.ДляИерархии Тогда
			
			Результат.Добавить(Новый Структура("Шаблон", ""));
				
		ИначеЕсли мСтруктураФорматаПолей.Свойство(Поле.ИмяПоля) Тогда
			
			Результат.Добавить(Новый Структура("Параметр, Формат", Поле.ИмяПоля, мСтруктураФорматаПолей[Поле.ИмяПоля]));
			
		Иначе
			
			Результат.Добавить(Новый Структура("Параметр", Поле.ИмяПоля));
				
		КонецЕсли;
		
	КонецЦикла;
	
	// Значение измерения
	Если мСтруктураФорматаПолей.Свойство(ЗначениеИзмерения) Тогда
		
		Результат.Добавить(Новый Структура("Параметр, Формат", ЗначениеИзмерения, мСтруктураФорматаПолей[ЗначениеИзмерения]));
		
	Иначе
		
		Если мСтруктураИзмеренийКолонки[ИмяИзмерения].ПередГруппировкой.СГруппировками.Количество() = 0 И мСтруктураИзмеренийКолонки[ИмяИзмерения].ВместоГруппировки.Количество() = 1 И мСтруктураИзмеренийКолонки[ИмяИзмерения].ПослеГруппировки.СГруппировками.Количество() = 0 Тогда
			
			Поле = мСтруктураИзмеренийКолонки[ИмяИзмерения].ВместоГруппировки[0];
			
			Если Иерархия И НЕ Поле.ДляИерархии Тогда
							
				Если мСтруктураФорматаПолей.Свойство(ЗначениеИзмерения) Тогда
						
					Результат.Добавить(Новый Структура("Параметр, Формат", ЗначениеИзмерения, мСтруктураФорматаПолей[ЗначениеИзмерения]));
						
				Иначе
						
					Результат.Добавить(Новый Структура("Параметр", ЗначениеИзмерения));
							
				КонецЕсли;
				
			Иначе
							
				Если мСтруктураФорматаПолей.Свойство(ЗначениеИзмерения) Тогда
						
					Результат.Добавить(Новый Структура("Параметр, Формат", Поле.ИмяПоля, мСтруктураФорматаПолей[Поле.ИмяПоля]));
						
				Иначе
						
					Результат.Добавить(Новый Структура("Параметр", Поле.ИмяПоля));
							
				КонецЕсли;
							
			КонецЕсли;
			
		Иначе
		
			Шаблон = "";
			
			Для каждого Поле из мСтруктураИзмеренийКолонки[ИмяИзмерения].ПередГруппировкой.СГруппировками Цикл
				
				Если Иерархия И НЕ Поле.ДляИерархии Тогда
					
					Продолжить;
					
				КонецЕсли;
						
				Шаблон = Шаблон + ?(ПустаяСтрока(Шаблон), "", ", ") + "[" + Поле.ИмяПоля + "]";
						
			КонецЦикла;
					
			Если мСтруктураИзмеренийКолонки[ИмяИзмерения].ВместоГруппировки.Количество() > 0 Тогда
						
				Для каждого Поле из мСтруктураИзмеренийКолонки[ИмяИзмерения].ВместоГруппировки Цикл
							
					Если Иерархия И НЕ Поле.ДляИерархии Тогда
						
						Шаблон = Шаблон + ?(ПустаяСтрока(Шаблон), "", ", ") + "[" + ЗначениеИзмерения + "]";
						
					Иначе
						
						Шаблон = Шаблон + ?(ПустаяСтрока(Шаблон), "", ", ") + "[" + Поле.ИмяПоля + "]";
						
					КонецЕсли;
					
							
				КонецЦикла;
						
			Иначе
						
				Шаблон = Шаблон + ?(ПустаяСтрока(Шаблон), "", ", ") + "[" + ЗначениеИзмерения + "]";
						
			КонецЕсли;
					
			Для каждого Поле из мСтруктураИзмеренийКолонки[ИмяИзмерения].ПослеГруппировки.СГруппировками Цикл
				
				Если Иерархия И НЕ Поле.ДляИерархии Тогда
					
					Продолжить;
					
				КонецЕсли;
						
				Шаблон = Шаблон + ?(ПустаяСтрока(Шаблон), "", ", ") + "[" + Поле.ИмяПоля + "]";
						
			КонецЦикла;
			
			Результат.Добавить(Новый Структура("Шаблон", Шаблон));
			
		КонецЕсли;
		
	КонецЕсли;
	
	// После группировки в отдельной колонке
	Если мСтруктураИзмеренийКолонки[ИмяИзмерения].ПослеГруппировки.ВОтдельнойКолонке.Количество() > 0 Тогда
		
		Если мСтруктураИзмеренийКолонки[ИмяИзмерения].ПередГруппировкой.ВОтдельнойКолонке.Количество() = 1 Тогда
			
			Поле = мСтруктураИзмеренийКолонки[ИмяИзмерения].ПередГруппировкой.ВОтдельнойКолонке[0];
			
			Если Иерархия И НЕ Поле.ДляИерархии Тогда
				
				Результат.Добавить(Новый Структура("Шаблон", ""));
					
			ИначеЕсли мСтруктураФорматаПолей.Свойство(Поле.ИмяПоля) Тогда
				
				Результат.Добавить(Новый Структура("Параметр, Формат", Поле.ИмяПоля, мСтруктураФорматаПолей[Поле.ИмяПоля]));
				
			Иначе
				
				Результат.Добавить(Новый Структура("Параметр", Поле.ИмяПоля));
					
			КонецЕсли;
			
		Иначе
		
			Шаблон = "";
			
			Для каждого Поле из мСтруктураИзмеренийКолонки[ИмяИзмерения].ПослеГруппировки.ВОтдельнойКолонке Цикл
				
				Если Иерархия И НЕ Поле.ДляИерархии Тогда
					
					Продолжить;
					
				КонецЕсли;
				
				Шаблон = Шаблон + ?(ПустаяСтрока(Шаблон), "", ", ") + "[" + Поле.ИмяПоля + "]";
				
			КонецЦикла;
			
			Результат.Добавить(Новый Структура("Шаблон", Шаблон));
			
		КонецЕсли;
		
	КонецЕсли;
	
	// После группировки в отдельных колонках
	Для каждого Поле из мСтруктураИзмеренийКолонки[ИмяИзмерения].ПослеГруппировки.ВОтдельныхКолонках Цикл
			
		Если Иерархия И НЕ Поле.ДляИерархии Тогда
			
			Результат.Добавить(Новый Структура("Шаблон", ""));
				
		ИначеЕсли мСтруктураФорматаПолей.Свойство(Поле.ИмяПоля) Тогда
			
			Результат.Добавить(Новый Структура("Параметр, Формат", Поле.ИмяПоля, мСтруктураФорматаПолей[Поле.ИмяПоля]));
			
		Иначе
			
			Результат.Добавить(Новый Структура("Параметр", Поле.ИмяПоля));
				
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции // ПолучитьСоставПолейИзмеренияКолонки()

Процедура ПолучитьСтруктуруДетали(Макет)
	
	мПоляДетали = Новый Массив;
	
	Если НЕ ПостроительОтчета.ВыводитьДетальныеЗаписи Тогда
		
		Возврат;
		
	КонецЕсли;
			
	Если ТипЗнч(Макет.Области.Найти("Детали")) = Тип("ОбластьЯчеекТабличногоДокумента") Тогда
			
		ИменованнаяОбластьАвтоматическогоМакетаДетали = Макет.ПолучитьОбласть("Детали");
			
	Иначе
			
		Возврат;
			
	КонецЕсли;
	
	Для каждого ВыбранноеПоле из ПостроительОтчета.ВыбранныеПоля Цикл
			
		Для ИндексСтолбца = 1 По ИменованнаяОбластьАвтоматическогоМакетаДетали.ШиринаТаблицы Цикл
					
			ОбластьИзмеренияСтрокиАвтоматическогоМакета = ИменованнаяОбластьАвтоматическогоМакетаДетали.Область(1, ИндексСтолбца, 1, ИндексСтолбца);
							
			Если ЭтоОбластьДляВыводаПоля(ОбластьИзмеренияСтрокиАвтоматическогоМакета, ВыбранноеПоле.Имя, ВыбранноеПоле.ПутьКДанным) И НЕ ЭтоПолеВыводитсяСИзмерением(ВыбранноеПоле.ПутьКДанным) Тогда
								
				мПоляДетали.Добавить(ОбластьИзмеренияСтрокиАвтоматическогоМакета.Параметр);
				Прервать;
							
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЦикла;
	
	ПередГруппировкойВОтдельнойКолонке = Ложь;
	ПередГруппировкойВОтдельныхКолонках = 0;
	ПослеГруппировкиВОтдельнойКолонке = Ложь;
	ПослеГруппировкиВОтдельныхКолонках = 0;
	
	Для каждого ИзмерениеСтроки из мСтруктураИзмеренийСтроки Цикл
		
		ПередГруппировкойВОтдельнойКолонке = ПередГруппировкойВОтдельнойКолонке ИЛИ ИзмерениеСтроки.Значение.ПередГруппировкой.ВОтдельнойКолонке.Количество() > 0;
		ПередГруппировкойВОтдельныхКолонках = Макс(ПередГруппировкойВОтдельныхКолонках, ИзмерениеСтроки.Значение.ПередГруппировкой.ВОтдельныхКолонках.Количество());
		ПослеГруппировкиВОтдельнойКолонке = ПослеГруппировкиВОтдельнойКолонке ИЛИ ИзмерениеСтроки.Значение.ПослеГруппировки.ВОтдельнойКолонке.Количество() > 0;
		ПослеГруппировкиВОтдельныхКолонках = Макс(ПослеГруппировкиВОтдельныхКолонках, ИзмерениеСтроки.Значение.ПослеГруппировки.ВОтдельныхКолонках.Количество());
		
	КонецЦикла;
	
	Для Индекс = мПоляДетали.Количество() По ?(ПередГруппировкойВОтдельнойКолонке, 1, 0) + ПередГруппировкойВОтдельныхКолонках + ?(ПослеГруппировкиВОтдельнойКолонке, 1, 0) + ПослеГруппировкиВОтдельныхКолонках Цикл
			
		мПоляДетали.Добавить("");
			
	КонецЦикла;
		
КонецПроцедуры // ПолучитьСтруктуруДетали()

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ВСПОМОГАТЕЛЬНЫЕ

Функция ПолучитьКоличествоУровнейПоказателей()
	
	КоличествоУровнейПоказателей = 0;
	
	Для каждого ТекущийПоказатель из Показатели.Строки Цикл
		
		Если ТекущийПоказатель.Строки.Количество() > 0 И ТекущийПоказатель.Использование > 0 Тогда
			
			КоличествоУровнейПоказателей = 2;
			Прервать;
			
		ИначеЕсли ТекущийПоказатель.Использование > 0 Тогда
			
			КоличествоУровнейПоказателей = 1;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат КоличествоУровнейПоказателей;
	
КонецФункции // ПолучитьКоличествоУровнейПоказателей()

Функция ПолучитьПредставлениеПоля(Поле, Вид = Неопределено)
	
	// Вид
	// 0 - Авто (полное наименование группировок и сокращенное наименование выбранных полей)
	// 1 - Полное наименование (полные наименования группировок и выбранных полей)
	// 2 - Сокращенное наименование (сокращенные наименования группировок и выбранных полей)
	
	ВидПредставления = Вид;
	
	Если ВидПредставления = Неопределено Тогда
		
		ВидПредставления = мВидПредставленияПолей;
		
	КонецЕсли;
	
	Представление = Поле.Представление;
	
	Если (ТипЗнч(Поле) = Тип("ИзмерениеПостроителяОтчета") И ВидПредставления = 2) ИЛИ (ТипЗнч(Поле) = Тип("ПолеПостроителяОтчета") И (ВидПредставления = 0 ИЛИ ВидПредставления = 2)) Тогда
		
		ПутьКДаннымПоСтрокам = СтрЗаменить(Поле.ПутьКДанным, ".", Символы.ПС);
		
		Поля = ПостроительОтчета.ДоступныеПоля;
		
		Для Индекс = 1 По СтрЧислоСтрок(ПутьКДаннымПоСтрокам) Цикл
			
			НайденноеПоле = Поля.Найти(СтрПолучитьСтроку(ПутьКДаннымПоСтрокам, Индекс));
			
			Если НайденноеПоле <> Неопределено Тогда
				
				Представление = НайденноеПоле.Представление;
				Поля = НайденноеПоле.Поля;
				
			Иначе
				
				Прервать;
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
	Возврат Представление;
	
КонецФункции // ПолучитьПредставлениеПоля()

Функция ЭтоОбластьДляВыводаПоля(Область, Имя, ПутьКДанным)
	
	Если Область.Заполнение = ТипЗаполненияОбластиТабличногоДокумента.Параметр И Показатели.Строки.Найти(Имя, "Имя", Истина) = Неопределено И Показатели.Строки.Найти(ПутьКДанным, "Имя", Истина) = Неопределено Тогда
		
		Возврат Область.Параметр = СтрЗаменить(ПутьКДанным, ".", "") ИЛИ Область.Параметр = СтрЗаменить(ПутьКДанным, ".", "") + "Представление" ИЛИ Область.Параметр = Имя ИЛИ Область.Параметр = Имя + "Представление";
		
	Иначе
		
		Возврат Ложь;
		
	КонецЕсли;
	
КонецФункции // ЭтоОбластьДляВыводаПоля()

Функция ЭтоПолеСвязаноСИзмерением(ПолеИмя, ИзмерениеИмя, ДляИерархии)
	
	Если мСоответствиеСвойствКатегорийИИзмерений[ПолеИмя] = Неопределено И
		мСоответствиеКонтактнойИнформацииИИзмерений[ПолеИмя] = Неопределено И
		мСоответствиеДанныхОФизлицеИИзмерений[ПолеИмя] = Неопределено И
		мСоответствиеПолейИИзмерений[ПолеИмя] = Неопределено Тогда
		
		ДляИерархии = Истина;
		Возврат Истина;
		
	ИначеЕсли мСоответствиеСвойствКатегорийИИзмерений[ПолеИмя] <> Неопределено И
		мСоответствиеСвойствКатегорийИИзмерений[ПолеИмя] = СтрЗаменить(ИзмерениеИмя, ".", "") Тогда
		
		ДляИерархии = Ложь;
		Возврат Истина;
		
	ИначеЕсли мСоответствиеКонтактнойИнформацииИИзмерений[ПолеИмя] <> Неопределено И
		мСоответствиеКонтактнойИнформацииИИзмерений[ПолеИмя] = СтрЗаменить(ИзмерениеИмя, ".", "") Тогда
		
		ДляИерархии = Ложь;
		Возврат Истина;
		
	ИначеЕсли мСоответствиеДанныхОФизлицеИИзмерений[ПолеИмя] <> Неопределено И
		мСоответствиеДанныхОФизлицеИИзмерений[ПолеИмя] = СтрЗаменить(ИзмерениеИмя, ".", "") Тогда
		
		ДляИерархии = Ложь;
		Возврат Истина;
		
	ИначеЕсли мСоответствиеПолейИИзмерений[ПолеИмя] <> Неопределено И
		мСоответствиеПолейИИзмерений[ПолеИмя].Свойство(СтрЗаменить(ИзмерениеИмя, ".", "")) Тогда
		
		ДляИерархии = Ложь;
		Возврат Истина;
		
	Иначе
		
		ДляИерархии = Ложь;
		Возврат Ложь;
		
	КонецЕсли;
	
КонецФункции // ЭтоПолеСвязаноСИзмерением()

Функция ЭтоПолеВыводитсяСИзмерением(ПолеИмя)
	
	ОбластиПоиска = Новый Массив;
	
	Для каждого СтруктураИзмерения из мСтруктураИзмеренийСтроки Цикл
		
		ОбластиПоиска.Добавить(СтруктураИзмерения.Значение.ВместоГруппировки);
		ОбластиПоиска.Добавить(СтруктураИзмерения.Значение.ПередГруппировкой.ВОтдельнойКолонке);
		ОбластиПоиска.Добавить(СтруктураИзмерения.Значение.ПередГруппировкой.ВОтдельныхКолонках);
		ОбластиПоиска.Добавить(СтруктураИзмерения.Значение.ПередГруппировкой.СГруппировками);
		ОбластиПоиска.Добавить(СтруктураИзмерения.Значение.ПослеГруппировки.ВОтдельнойКолонке);
		ОбластиПоиска.Добавить(СтруктураИзмерения.Значение.ПослеГруппировки.ВОтдельныхКолонках);
		ОбластиПоиска.Добавить(СтруктураИзмерения.Значение.ПослеГруппировки.СГруппировками);
		
	КонецЦикла;
	
	Для каждого СтруктураИзмерения из мСтруктураИзмеренийКолонки Цикл
		
		ОбластиПоиска.Добавить(СтруктураИзмерения.Значение.ВместоГруппировки);
		ОбластиПоиска.Добавить(СтруктураИзмерения.Значение.ПередГруппировкой.ВОтдельнойКолонке);
		ОбластиПоиска.Добавить(СтруктураИзмерения.Значение.ПередГруппировкой.ВОтдельныхКолонках);
		ОбластиПоиска.Добавить(СтруктураИзмерения.Значение.ПередГруппировкой.СГруппировками);
		ОбластиПоиска.Добавить(СтруктураИзмерения.Значение.ПослеГруппировки.ВОтдельнойКолонке);
		ОбластиПоиска.Добавить(СтруктураИзмерения.Значение.ПослеГруппировки.ВОтдельныхКолонках);
		ОбластиПоиска.Добавить(СтруктураИзмерения.Значение.ПослеГруппировки.СГруппировками);
		
	КонецЦикла;
	
	Для каждого ОбластьПоиска из ОбластиПоиска Цикл
		
		Для каждого Поле из ОбластьПоиска Цикл
			
			Если Поле.ИмяПоля = ПолеИмя Тогда
				
				Возврат Истина;
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЦикла;
	
	Возврат Ложь;
	
КонецФункции // ЭтоПолеВыводитсяСИзмерением()

Функция ЭтоПолеПоказатель(ПолеИмя) Экспорт
	
	Возврат (Показатели.Строки.Найти(ПолеИмя, "Имя", Истина) <> Неопределено);
	
КонецФункции // ЭтоПолеПоказатель()

Функция ДобавитьПоказателиВВыбранныеПоляИУсловноеОформление()
	
	МассивВыбранныеПоляПоказатели = Новый Массив;
	МассивУсловноеОформлениеПоказатели = Новый Массив;
	
	Для каждого ТекущийПоказатель из Показатели.Строки Цикл
			
		// Группа показателей
		Если ТекущийПоказатель.Строки.Количество() > 0 И ТекущийПоказатель.Использование > 0 Тогда
				
			КоличествоПоказателейВГруппе = 0;
			
			Для каждого Показатель из ТекущийПоказатель.Строки Цикл
					
				Если Показатель.Использование > 0 Тогда
					
					МассивВыбранныеПоляПоказатели.Добавить(ПостроительОтчета.ВыбранныеПоля.Добавить(Показатель.Имя));
						
				КонецЕсли;
					
			КонецЦикла;
				
		ИначеЕсли ТекущийПоказатель.Использование > 0 Тогда // Показатель
			
			МассивВыбранныеПоляПоказатели.Добавить(ПостроительОтчета.ВыбранныеПоля.Добавить(ТекущийПоказатель.Имя));
				
		КонецЕсли;
			
	КонецЦикла;
	
	Если ОтрицательноеКрасным И МассивВыбранныеПоляПоказатели.Количество() > 0 Тогда
		
		ЭлементОформления = ПостроительОтчета.УсловноеОформление.Добавить("ОтрицательноеКрасным", "Отрицательное красным");
		
		Для каждого ВыбранноеПоле из МассивВыбранныеПоляПоказатели Цикл
				
			ЭлементОформления.Область.Добавить(ВыбранноеПоле.ПутьКДанным, ВыбранноеПоле.Представление, ТипОбластиОформления.Поле);
				
		КонецЦикла;
		
		ЭлементОформления.Оформление.ВыделятьОтрицательные.Значение = Истина;
		ЭлементОформления.Оформление.ВыделятьОтрицательные.Использование = Истина;
		
		ЭлементОформления.Использование = Истина;
		
		МассивУсловноеОформлениеПоказатели.Добавить(ЭлементОформления);
		
	КонецЕсли;

	Возврат Новый Структура("ВыбранныеПоля, УсловноеОформление", МассивВыбранныеПоляПоказатели, МассивУсловноеОформлениеПоказатели);
	
КонецФункции // ДобавитьПоказателиВВыбранныеПоляИУсловноеОформление()

Процедура УдалитьПоказателиИзВыбранныхПолейИУсловногоОформления(ВыбранныеПоляУсловноеОформлениеПоказатели)
	
	Для каждого ВыбранноеПоле из ВыбранныеПоляУсловноеОформлениеПоказатели.ВыбранныеПоля Цикл
		
		ПостроительОтчета.ВыбранныеПоля.Удалить(ВыбранноеПоле);
		
	КонецЦикла;
	
	Для каждого ЭлементОформления из ВыбранныеПоляУсловноеОформлениеПоказатели.УсловноеОформление Цикл
		
		ПостроительОтчета.УсловноеОформление.Удалить(ЭлементОформления);
		
	КонецЦикла;
	
КонецПроцедуры // УдалитьПоказателиИзВыбранныхПолейИУсловногоОформления()

Функция ДобавитьОтборПериодичность()
	
	МассивОтборПериодичность = Новый Массив;
	
	ДоступныеПоля = ПостроительОтчета.Отбор.ПолучитьДоступныеПоля();
	
	Если ДоступныеПоля.Найти("Периодичность") = Неопределено Тогда
		
		Возврат МассивОтборПериодичность;
		
	КонецЕсли;
	
	Периодичность = 0;
	
	Для каждого Период из мСтруктураПериодов Цикл
		
		Периодичность = ?(НайтиПоле(Период.Ключ,,,,, Ложь, Истина), ?(Периодичность = 0, Период.Значение, Мин(Период.Значение, Периодичность)), Периодичность);
		
	КонецЦикла;
	
	ПолеОтборПериодичность = ПостроительОтчета.Отбор.Добавить("Периодичность");
	ПолеОтборПериодичность.Значение = Периодичность;
	ПолеОтборПериодичность.Использование = Истина;
	
	МассивОтборПериодичность.Добавить(ПолеОтборПериодичность);
	
	Возврат МассивОтборПериодичность;
	
КонецФункции // ДобавитьОтборПериодичность()

Процедура УдалитьОтборПериодичность(ОтборыПериодичность)
	
	Для каждого ЭлементОтборПериодичность из ОтборыПериодичность Цикл
		
		ПостроительОтчета.Отбор.Удалить(ПостроительОтчета.Отбор.Индекс(ЭлементОтборПериодичность));
		
	КонецЦикла;
	
КонецПроцедуры // УдалитьОтборПериодичность()

Функция ДобавитьСвязанныеПоля()
	
	МассивСвязанныеПоля = Новый Массив;
	
	Для каждого СвязьПолей из мСоответствиеСвязьПолей Цикл
		
		Если НайтиПоле(СвязьПолей.Ключ, Истина, Истина, Ложь, Истина, Ложь) И НЕ НайтиПоле(СвязьПолей.Значение, Истина, Истина, Ложь, Истина, Ложь) Тогда
			
			МассивСвязанныеПоля.Добавить(ПостроительОтчета.ВыбранныеПоля.Добавить(СвязьПолей.Значение));
			
		КонецЕсли;
		
	КонецЦикла;
	
	Для каждого ВыбранноеПоле из ПостроительОтчета.ВыбранныеПоля Цикл
		
		РодительскоеПолеПутьКДанным = Лев(ВыбранноеПоле.ПутьКДанным, Найти(ВыбранноеПоле.ПутьКДанным, ".") - 1);
		
		Если НЕ ПустаяСтрока(РодительскоеПолеПутьКДанным) И НЕ НайтиПоле(РодительскоеПолеПутьКДанным, Истина, Истина, Ложь, Истина, Ложь, Истина) Тогда
			
			МассивСвязанныеПоля.Добавить(ПостроительОтчета.ВыбранныеПоля.Добавить(РодительскоеПолеПутьКДанным));
			
		КонецЕсли;
		
	КонецЦикла;

	Возврат МассивСвязанныеПоля;
	
КонецФункции // ДобавитьСвязанныеПоля()

Процедура УдалитьСвязанныеПоля(СвязанныеПоля)
	
	Для каждого СвязанноеПоле из СвязанныеПоля Цикл
		
		ПостроительОтчета.ВыбранныеПоля.Удалить(СвязанноеПоле);
		
	КонецЦикла;
	
КонецПроцедуры // УдалитьСвязанныеПоля()

Функция ДобавитьПоляПорядка()
	
	МассивПоляПорядка = Новый Массив;
	
	Для каждого ИзмерениеСтроки из ПостроительОтчета.ИзмеренияСтроки Цикл
		
		Если НЕ НайтиПоле(ИзмерениеСтроки.ПутьКДанным, Ложь, Ложь, Ложь, Ложь, Истина) Тогда
			
			Попытка
				
				ПостроительОтчета.Порядок.Добавить(ИзмерениеСтроки.ПутьКДанным);
				
			Исключение
				
				Продолжить;
				
			КонецПопытки;
			
			МассивПоляПорядка.Вставить(0, ПостроительОтчета.Порядок.Количество() - 1);
			
		КонецЕсли;
			
	КонецЦикла;
	
	Для каждого ИзмерениеКолонки из ПостроительОтчета.ИзмеренияКолонки Цикл
		
		Если НЕ НайтиПоле(ИзмерениеКолонки.ПутьКДанным, Ложь, Ложь, Ложь, Ложь, Истина) Тогда
			
			Попытка
				
				ПостроительОтчета.Порядок.Добавить(ИзмерениеКолонки.ПутьКДанным);
				
			Исключение
				
				Продолжить;
				
			КонецПопытки;
			
			МассивПоляПорядка.Вставить(0, ПостроительОтчета.Порядок.Количество() - 1);
			
		КонецЕсли;
			
	КонецЦикла;
	
	Для каждого ВыбранноеПоле из ПостроительОтчета.ВыбранныеПоля Цикл
		
		Если НЕ НайтиПоле(ВыбранноеПоле.ПутьКДанным, Ложь, Ложь, Ложь, Ложь, Истина) Тогда
			
			Если мСтруктураПериодов.Свойство(СтрЗаменить(ВыбранноеПоле.ПутьКДанным, ".", "")) Тогда
				
				Попытка
					
					ПостроительОтчета.Порядок.Добавить(ВыбранноеПоле.ПутьКДанным);
					
				Исключение
					
					Продолжить;
					
				КонецПопытки;
				
				МассивПоляПорядка.Вставить(0, ПостроительОтчета.Порядок.Количество() - 1);
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат МассивПоляПорядка;
	
КонецФункции // ДобавитьПоляПорядка()

Процедура УдалитьПоляПорядка(ПоляПорядка)
	
	Для каждого ПолеПорядка из ПоляПорядка Цикл
		
		Если ПолеПорядка <= ПостроительОтчета.Порядок.Количество() - 1 Тогда
			
			ПостроительОтчета.Порядок.Удалить(ПолеПорядка);
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры // УдалитьПоляПорядка()

Процедура ДобавитьАвтоупорядочивание()
	
	Если ПустаяСтрока(ПостроительОтчета.Текст) Тогда
		
		Возврат;
		
	КонецЕсли;
		
	ДобавитьАвтоупорядочивание = Ложь;
	ТекстЗапроса = ПостроительОтчета.Текст;
	Автоупорядочивание = Найти(ТекстЗапроса, "АВТОУПОРЯДОЧИВАНИЕ") > 0;
	ДобавитьАвтоупорядочивание = ПостроительОтчета.ИзмеренияСтроки.Количество() > 0 ИЛИ ПостроительОтчета.ИзмеренияКолонки.Количество() > 0 ИЛИ ПостроительОтчета.Порядок.Количество() > 0;
	
	Если Автоупорядочивание И НЕ ДобавитьАвтоупорядочивание Тогда
		
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "АВТОУПОРЯДОЧИВАНИЕ", "");
		
		ТекущиеНастройки = ПостроительОтчета.ПолучитьНастройки();
		ПостроительОтчета.Текст = ТекстЗапроса;
		ПостроительОтчета.УстановитьНастройки(ТекущиеНастройки);
		
		УстановитьПредставленияПолей(мСтруктураПредставлениеПолей, ПостроительОтчета);
		УстановитьТипыЗначенийСвойствДляОтбора();
		
	ИначеЕсли НЕ Автоупорядочивание И ДобавитьАвтоупорядочивание Тогда
		
		ТекстЗапроса = ТекстЗапроса + "
		|АВТОУПОРЯДОЧИВАНИЕ";
		
		ТекущиеНастройки = ПостроительОтчета.ПолучитьНастройки();
		ПостроительОтчета.Текст = ТекстЗапроса;
		ПостроительОтчета.УстановитьНастройки(ТекущиеНастройки);
		
		УстановитьПредставленияПолей(мСтруктураПредставлениеПолей, ПостроительОтчета);
		УстановитьТипыЗначенийСвойствДляОтбора();
		
	КонецЕсли;
	
КонецПроцедуры // ДобавитьАвтоупорядочивание()

Функция НайтиПоле(ПутьКДанным, ИзмеренияСтроки = Истина, ИзмеренияКолонки = Истина, Отбор = Истина, ВыбранныеПоля = Истина, Порядок = Истина, ТочноеСоответствие = Ложь)
	
	Если ИзмеренияСтроки Тогда
		
		Для каждого ИзмерениеСтроки из ПостроительОтчета.ИзмеренияСтроки Цикл
			
			Если ИзмерениеСтроки.ПутьКДанным = ПутьКДанным ИЛИ НЕ ТочноеСоответствие И Лев(ИзмерениеСтроки.ПутьКДанным, Найти(ИзмерениеСтроки.ПутьКДанным, ".")) = ПутьКДанным + "." Тогда
				
				Возврат Истина;
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
	Если ИзмеренияКолонки Тогда
		
		Для каждого ИзмерениеКолонки из ПостроительОтчета.ИзмеренияКолонки Цикл
		
			Если ИзмерениеКолонки.ПутьКДанным = ПутьКДанным ИЛИ НЕ ТочноеСоответствие И Лев(ИзмерениеКолонки.ПутьКДанным, Найти(ИзмерениеКолонки.ПутьКДанным, ".")) = ПутьКДанным + "." Тогда
				
				Возврат Истина;
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
	Если Отбор Тогда
		
		Для каждого ЭлементОтбора из ПостроительОтчета.Отбор Цикл
			
			Если ЭлементОтбора.Использование И (ЭлементОтбора.ПутьКДанным = ПутьКДанным ИЛИ НЕ ТочноеСоответствие И Лев(ЭлементОтбора.ПутьКДанным, Найти(ЭлементОтбора.ПутьКДанным, ".")) = ПутьКДанным + ".") Тогда
				
				Возврат Истина;
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
	Если ВыбранныеПоля Тогда
		
		Для каждого ВыбранноеПоле из ПостроительОтчета.ВыбранныеПоля Цикл
			
			Если ВыбранноеПоле.ПутьКДанным = ПутьКДанным ИЛИ НЕ ТочноеСоответствие И Лев(ВыбранноеПоле.ПутьКДанным, Найти(ВыбранноеПоле.ПутьКДанным, ".")) = ПутьКДанным + "." Тогда
				
				Возврат Истина;
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
	Если Порядок Тогда
		
		Для каждого ЭлементПорядка из ПостроительОтчета.Порядок Цикл
			
			Если ЭлементПорядка.ПутьКДанным = ПутьКДанным ИЛИ НЕ ТочноеСоответствие И Лев(ЭлементПорядка.ПутьКДанным, Найти(ЭлементПорядка.ПутьКДанным, ".")) = ПутьКДанным + "." Тогда
				
				Возврат Истина;
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции // НайтиПоле()

Процедура УстановитьНачальныеНастройкиПоМетаданнымРегистра(ИмяРегистра)
	
	МетаданныеРегистра = Метаданные.РегистрыНакопления.Найти(ИмяРегистра);
	
	Если МетаданныеРегистра = Неопределено Тогда
	
		Возврат;
	
	КонецЕсли;
	
	Обороты = МетаданныеРегистра.ВидРегистра = Метаданные.СвойстваОбъектов.ВидРегистраНакопления.Обороты;
	
	ОписаниеПолейВыборки = "";
	ОписаниеПолейВыборкиПостроитель = "";
	ОписаниеИсточниковЗапроса = "";
	ОписаниеОтборовРезультатаЗапроса = "";
	ОписаниеУпорядочиванияРезультатаЗапроса = "";
	ОписаниеИтогиЗапроса = "";
	ОписаниеИтогиЗапросаПостроитель = "";
	
	Если ИспользоватьСвойстваИКатегории Тогда
		
		Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	НазначенияСвойствКатегорийОбъектов.Ссылка,
		|	НазначенияСвойствКатегорийОбъектов.Предопределенный,
		|	НазначенияСвойствКатегорийОбъектов.ТипЗначения
		|ИЗ
		|	ПланВидовХарактеристик.НазначенияСвойствКатегорийОбъектов КАК НазначенияСвойствКатегорийОбъектов
		|ГДЕ
		|	НазначенияСвойствКатегорийОбъектов.ПометкаУдаления = ЛОЖЬ
		|	И НазначенияСвойствКатегорийОбъектов.ЭтоГруппа = ЛОЖЬ");
		
		РезультатЗапросаНазначенияСвойствКатегорийОбъектов = Запрос.Выполнить();
		ВыборкаНазначенияСвойствКатегорийОбъектов = РезультатЗапросаНазначенияСвойствКатегорийОбъектов.Выбрать();
			
	КонецЕсли;
	
	Для каждого Измерение из МетаданныеРегистра.Измерения Цикл
		
		ОписаниеПолейВыборки = ОписаниеПолейВыборки + ?(ОписаниеПолейВыборки = "", "", ",") + "
		|	ИсточникДанных." + Измерение.Имя + " КАК " + Измерение.Имя + ",
		|	ПРЕДСТАВЛЕНИЕ(ИсточникДанных." + Измерение.Имя + ") КАК " + Измерение.Имя + "Представление";
		
		ОписаниеПолейВыборкиПостроитель = ОписаниеПолейВыборкиПостроитель + ?(ОписаниеПолейВыборкиПостроитель = "", "", ",") + "
		|	" + Измерение.Имя + ".*";
		
		ОписаниеИсточниковЗапроса = ОписаниеИсточниковЗапроса + ?(ОписаниеИсточниковЗапроса = "", "", ",") + "
		|	" + Измерение.Имя + ".* КАК " + Измерение.Имя;
		
		ОписаниеУпорядочиванияРезультатаЗапроса = ОписаниеУпорядочиванияРезультатаЗапроса + ?(ОписаниеУпорядочиванияРезультатаЗапроса = "", "", ",") + "
		|	" + Измерение.Имя + ".*";
		
		ОписаниеИтогиЗапросаПостроитель = ОписаниеИтогиЗапросаПостроитель + ?(ОписаниеИтогиЗапросаПостроитель = "", "", ",") + "
		|	" + Измерение.Имя + ".*";
		
		
		мСтруктураПредставлениеПолей.Вставить(Измерение.Имя, Измерение.Представление());
		
		Если ИспользоватьСвойстваИКатегории Тогда
			
			МассивНазначений = Новый Массив;
			
			Для каждого ТипИзмерения из Измерение.Тип.Типы() Цикл
				
				ВыборкаНазначенияСвойствКатегорийОбъектов.Сбросить();
				
				Пока ВыборкаНазначенияСвойствКатегорийОбъектов.Следующий() Цикл
					
					Если ВыборкаНазначенияСвойствКатегорийОбъектов.Ссылка = ПланыВидовХарактеристик.НазначенияСвойствКатегорийОбъектов.Справочник_ХарактеристикиНоменклатуры И
						ТипИзмерения = Тип("СправочникСсылка.ХарактеристикиНоменклатуры") ИЛИ
						ВыборкаНазначенияСвойствКатегорийОбъектов.Ссылка <> ПланыВидовХарактеристик.НазначенияСвойствКатегорийОбъектов.Справочник_ХарактеристикиНоменклатуры И
						ВыборкаНазначенияСвойствКатегорийОбъектов.ТипЗначения.СодержитТип(ТипИзмерения) И
						МассивНазначений.Найти(ВыборкаНазначенияСвойствКатегорийОбъектов.Ссылка) = Неопределено Тогда
						
						МассивНазначений.Добавить(ВыборкаНазначенияСвойствКатегорийОбъектов.Ссылка);
						
					КонецЕсли;
					
				КонецЦикла;
				
			КонецЦикла;
			
			Для каждого Назначение из МассивНазначений Цикл
				
				ДобавитьСвойстваИКатегорииДляПоля("ИсточникДанных." + Измерение.Имя, Измерение.Имя, Измерение.Представление(), Назначение);
				
			КонецЦикла;
			
		КонецЕсли;
		
	КонецЦикла;
	
	// Свойства и категории поля "Регистратор"
	Если ИспользоватьСвойстваИКатегории Тогда
		
		МассивНазначений = Новый Массив;
		
		Запрос = Новый Запрос(
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 0
		|	ИсточникДанных.Регистратор КАК Регистратор
		|ИЗ
		|	РегистрНакопления." + ИмяРегистра + " КАК ИсточникДанных");
		
		РезультатЗапроса = Запрос.Выполнить();
		
		Для каждого ТипРегистратора из РезультатЗапроса.Колонки.Регистратор.ТипЗначения.Типы() Цикл
			
			ВыборкаНазначенияСвойствКатегорийОбъектов.Сбросить();
			
			Пока ВыборкаНазначенияСвойствКатегорийОбъектов.Следующий() Цикл
				
				Если ВыборкаНазначенияСвойствКатегорийОбъектов.ТипЗначения.СодержитТип(ТипРегистратора) И
					МассивНазначений.Найти(ВыборкаНазначенияСвойствКатегорийОбъектов.Ссылка) = Неопределено Тогда
					
					МассивНазначений.Добавить(ВыборкаНазначенияСвойствКатегорийОбъектов.Ссылка);
					
				КонецЕсли;
				
			КонецЦикла;
			
		КонецЦикла;
		
		Для каждого Назначение из МассивНазначений Цикл
			
			ДобавитьСвойстваИКатегорииДляПоля("ИсточникДанных.Регистратор", "Регистратор", "Документ движения (регистратор)", Назначение);
			
		КонецЦикла;
			
	КонецЕсли;
	
	Если мМассивПоляГруппировка <> Неопределено Тогда
		
		Для каждого ПолеГруппировка из мМассивПоляГруппировка Цикл
			
			ПолеВыборки = СтрЗаменить(?(ПолеГруппировка.Выражение = Неопределено, "%ПолеВыборки%", ПолеГруппировка.Выражение), "%ПолеВыборки%", "ИсточникДанных." + ПолеГруппировка.Родитель + "." + ПолеГруппировка.ПутьКДанным);
			ИсточникЗапроса = СтрЗаменить(?(ПолеГруппировка.Выражение = Неопределено, "%ПолеВыборки%", ПолеГруппировка.Выражение), "%ПолеВыборки%", ПолеГруппировка.Родитель + "." + ПолеГруппировка.ПутьКДанным);
			
			ОписаниеПолейВыборки = ОписаниеПолейВыборки + ?(ОписаниеПолейВыборки = "", "", ",") + "
			|	" + ПолеВыборки + " КАК " + ПолеГруппировка.Имя + ",
			|	ПРЕДСТАВЛЕНИЕ(" + ПолеВыборки + ") КАК " + ПолеГруппировка.Имя + "Представление";
			
			ОписаниеПолейВыборкиПостроитель = ОписаниеПолейВыборкиПостроитель + ?(ОписаниеПолейВыборкиПостроитель = "", "", ",") + "
			|	" + ПолеГруппировка.Имя + ".*";
			
			ОписаниеИсточниковЗапроса = ОписаниеИсточниковЗапроса + ?(ОписаниеИсточниковЗапроса = "", "", ",") + "
			|	(" + ИсточникЗапроса + ").* КАК " + ПолеГруппировка.Имя;
				
			ОписаниеУпорядочиванияРезультатаЗапроса = ОписаниеУпорядочиванияРезультатаЗапроса + ?(ОписаниеУпорядочиванияРезультатаЗапроса = "", "", ",") + "
			|	" + ПолеГруппировка.Имя + ".*";
			
			ОписаниеИтогиЗапросаПостроитель = ОписаниеИтогиЗапросаПостроитель + ?(ОписаниеИтогиЗапросаПостроитель = "", "", ",") + "
			|	" + ПолеГруппировка.Имя + ".*";
			
			мСтруктураПредставлениеПолей.Вставить(ПолеГруппировка.Имя, ПолеГруппировка.Представление);
			
			ДокументыДобавлен = Ложь;
			
			Если ИспользоватьСвойстваИКатегории И ПолеГруппировка.Тип <> Неопределено И ТипЗнч(ПолеГруппировка.Тип) = Тип("ОписаниеТипов") Тогда
				
				МассивНазначений = Новый Массив;

				Для каждого ТипГруппировки из ПолеГруппировка.Тип.Типы() Цикл
					
					ВыборкаНазначенияСвойствКатегорийОбъектов.Сбросить();
					
					Пока ВыборкаНазначенияСвойствКатегорийОбъектов.Следующий() Цикл
						
						Если ВыборкаНазначенияСвойствКатегорийОбъектов.Ссылка = ПланыВидовХарактеристик.НазначенияСвойствКатегорийОбъектов.Справочник_ХарактеристикиНоменклатуры И
							ТипГруппировки = Тип("СправочникСсылка.ХарактеристикиНоменклатуры") ИЛИ
							ВыборкаНазначенияСвойствКатегорийОбъектов.Ссылка <> ПланыВидовХарактеристик.НазначенияСвойствКатегорийОбъектов.Справочник_ХарактеристикиНоменклатуры И
							ВыборкаНазначенияСвойствКатегорийОбъектов.ТипЗначения.СодержитТип(ТипГруппировки) И
							МассивНазначений.Найти(ВыборкаНазначенияСвойствКатегорийОбъектов.Ссылка) = Неопределено Тогда
							
							МассивНазначений.Добавить(ВыборкаНазначенияСвойствКатегорийОбъектов.Ссылка);
							
						КонецЕсли;
						
					КонецЦикла;
					
				КонецЦикла;
				
				Для каждого Назначение из МассивНазначений Цикл
					
					ДобавитьСвойстваИКатегорииДляПоля("ИсточникДанных." + ПолеГруппировка.Родитель + "." + ПолеГруппировка.ПутьКДанным, ПолеГруппировка.Имя, ПолеГруппировка.Представление, Назначение);
					
				КонецЦикла;
				
			КонецЕсли;
			
			УстановитьСвязьПолей(ПолеГруппировка.Имя, ПолеГруппировка.Родитель);
			
		КонецЦикла;
		
		мМассивПоляГруппировка = Неопределено;
		
	КонецЕсли;
	
	Для каждого Ресурс Из МетаданныеРегистра.Ресурсы Цикл
		
		Если Обороты Тогда
			
			ОписаниеПолейВыборки = ОписаниеПолейВыборки + ?(ОписаниеПолейВыборки = "", "", ",") + "
			|	ИсточникДанных." + Ресурс.Имя + "Оборот КАК " + Ресурс.Имя + "Оборот";
			
			ОписаниеПолейВыборкиПостроитель = ОписаниеПолейВыборкиПостроитель + ?(ОписаниеПолейВыборкиПостроитель = "", "", ",") + "
			|	" + Ресурс.Имя + "Оборот";
			
			ОписаниеОтборовРезультатаЗапроса = ОписаниеОтборовРезультатаЗапроса + ?(ОписаниеОтборовРезультатаЗапроса = "", "", ",") + "
			|	ИсточникДанных." + Ресурс.Имя + "Оборот КАК " + Ресурс.Имя + "Оборот";
			
			ОписаниеИтогиЗапроса = ОписаниеИтогиЗапроса + ?(ОписаниеИтогиЗапроса = "", "", ",") + "
			|	СУММА(" + Ресурс.Имя + "Оборот)";
			
			ОписаниеУпорядочиванияРезультатаЗапроса = ОписаниеУпорядочиванияРезультатаЗапроса + ?(ОписаниеУпорядочиванияРезультатаЗапроса = "", "", ",") + "
			|	" + Ресурс.Имя + "Оборот";
			
			ДобавитьПоказатель(Ресурс.Имя + "Оборот", Ресурс.Представление(), Истина, "ЧЦ = " + Формат(Ресурс.Тип.КвалификаторыЧисла.Разрядность, "ЧГ=0") + " ; ЧДЦ = " + Формат(Ресурс.Тип.КвалификаторыЧисла.РазрядностьДробнойЧасти, "ЧГ=0"));
			
			мСтруктураПредставлениеПолей.Вставить(Ресурс.Имя + "Оборот", Ресурс.Представление() + " (оборот)");
			
		Иначе
			
			ОписаниеПолейВыборки = ОписаниеПолейВыборки + ?(ОписаниеПолейВыборки = "", "", ",") + "
			|	ИсточникДанных." + Ресурс.Имя + "НачальныйОстаток КАК " + Ресурс.Имя + "НачальныйОстаток,
			|	ИсточникДанных." + Ресурс.Имя + "Приход КАК " + Ресурс.Имя + "Приход,
			|	ИсточникДанных." + Ресурс.Имя + "Расход КАК " + Ресурс.Имя + "Расход,
			|	ИсточникДанных." + Ресурс.Имя + "КонечныйОстаток КАК " + Ресурс.Имя + "КонечныйОстаток,
			|	ИсточникДанных." + Ресурс.Имя + "Оборот КАК " + Ресурс.Имя + "Оборот";
			
			ОписаниеПолейВыборкиПостроитель = ОписаниеПолейВыборкиПостроитель + ?(ОписаниеПолейВыборкиПостроитель = "", "", ",") + "
			|	" + Ресурс.Имя + "НачальныйОстаток,
			|	" + Ресурс.Имя + "Приход,
			|	" + Ресурс.Имя + "Расход,
			|	" + Ресурс.Имя + "КонечныйОстаток,
			|	" + Ресурс.Имя + "Оборот";
			
			ОписаниеОтборовРезультатаЗапроса = ОписаниеОтборовРезультатаЗапроса + ?(ОписаниеОтборовРезультатаЗапроса = "", "", ",") + "
			|	ИсточникДанных." + Ресурс.Имя + "НачальныйОстаток КАК " + Ресурс.Имя + "НачальныйОстаток,
			|	ИсточникДанных." + Ресурс.Имя + "Приход КАК " + Ресурс.Имя + "Приход,
			|	ИсточникДанных." + Ресурс.Имя + "Расход КАК " + Ресурс.Имя + "Расход,
			|	ИсточникДанных." + Ресурс.Имя + "КонечныйОстаток КАК " + Ресурс.Имя + "КонечныйОстаток,
			|	ИсточникДанных." + Ресурс.Имя + "Оборот КАК " + Ресурс.Имя + "Оборот";
			
			ОписаниеИтогиЗапроса = ОписаниеИтогиЗапроса + ?(ОписаниеИтогиЗапроса = "", "", ",") + "
			|	СУММА(" + Ресурс.Имя + "НачальныйОстаток),
			|	СУММА(" + Ресурс.Имя + "Приход),
			|	СУММА(" + Ресурс.Имя + "Расход),
			|	СУММА(" + Ресурс.Имя + "КонечныйОстаток),
			|	СУММА(" + Ресурс.Имя + "Оборот)";
			
			ОписаниеУпорядочиванияРезультатаЗапроса = ОписаниеУпорядочиванияРезультатаЗапроса + ?(ОписаниеУпорядочиванияРезультатаЗапроса = "", "", ",") + "
			|	" + Ресурс.Имя + "НачальныйОстаток,
			|	" + Ресурс.Имя + "Приход,
			|	" + Ресурс.Имя + "Расход,
			|	" + Ресурс.Имя + "КонечныйОстаток,
			|	" + Ресурс.Имя + "Оборот";
			
			ДобавитьПоказатель(Ресурс.Имя + "НачальныйОстаток", "Нач. остаток", Истина, "ЧЦ = " + Формат(Ресурс.Тип.КвалификаторыЧисла.Разрядность, "ЧГ=0") + " ; ЧДЦ = " + Формат(Ресурс.Тип.КвалификаторыЧисла.РазрядностьДробнойЧасти, "ЧГ=0"), Ресурс.Имя, Ресурс.Представление());
			ДобавитьПоказатель(Ресурс.Имя + "Приход", "Приход", Истина, "ЧЦ = " + Формат(Ресурс.Тип.КвалификаторыЧисла.Разрядность, "ЧГ=0") + " ; ЧДЦ = " + Формат(Ресурс.Тип.КвалификаторыЧисла.РазрядностьДробнойЧасти, "ЧГ=0"), Ресурс.Имя, Ресурс.Представление());
			ДобавитьПоказатель(Ресурс.Имя + "Расход", "Расход", Истина, "ЧЦ = " + Формат(Ресурс.Тип.КвалификаторыЧисла.Разрядность, "ЧГ=0") + " ; ЧДЦ = " + Формат(Ресурс.Тип.КвалификаторыЧисла.РазрядностьДробнойЧасти, "ЧГ=0"), Ресурс.Имя, Ресурс.Представление());
			ДобавитьПоказатель(Ресурс.Имя + "КонечныйОстаток", "Кон. остаток", Истина, "ЧЦ = " + Формат(Ресурс.Тип.КвалификаторыЧисла.Разрядность, "ЧГ=0") + " ; ЧДЦ = " + Формат(Ресурс.Тип.КвалификаторыЧисла.РазрядностьДробнойЧасти, "ЧГ=0"), Ресурс.Имя, Ресурс.Представление());
			ДобавитьПоказатель(Ресурс.Имя + "Оборот", "Оборот", Истина, "ЧЦ = " + Формат(Ресурс.Тип.КвалификаторыЧисла.Разрядность, "ЧГ=0") + " ; ЧДЦ = " + Формат(Ресурс.Тип.КвалификаторыЧисла.РазрядностьДробнойЧасти, "ЧГ=0"), Ресурс.Имя, Ресурс.Представление());
			
			мСтруктураПредставлениеПолей.Вставить(Ресурс.Имя + "НачальныйОстаток", Ресурс.Представление() + " (нач. ост.)");
			мСтруктураПредставлениеПолей.Вставить(Ресурс.Имя + "Приход", Ресурс.Представление() + " (приход)");
			мСтруктураПредставлениеПолей.Вставить(Ресурс.Имя + "Расход", Ресурс.Представление() + " (расход)");
			мСтруктураПредставлениеПолей.Вставить(Ресурс.Имя + "КонечныйОстаток", Ресурс.Представление() + " (кон. ост.)");
			мСтруктураПредставлениеПолей.Вставить(Ресурс.Имя + "Оборот", Ресурс.Представление() + " (оборот)");
			
		КонецЕсли;
		
	КонецЦикла;
	
	Если мМассивПоляРесурс <> Неопределено Тогда
		
		Для каждого ПолеРесурс из мМассивПоляРесурс Цикл
	
			ОписаниеПолейВыборки = ОписаниеПолейВыборки + ?(ОписаниеПолейВыборки = "", "", ",") + "
			|	" + ПолеРесурс.ПутьКДанным + " КАК " + ПолеРесурс.Имя;
					
			ОписаниеПолейВыборкиПостроитель = ОписаниеПолейВыборкиПостроитель + ?(ОписаниеПолейВыборкиПостроитель = "", "", ",") + "
			|	" + ПолеРесурс.Имя;
					
			ОписаниеОтборовРезультатаЗапроса = ОписаниеОтборовРезультатаЗапроса + ?(ОписаниеОтборовРезультатаЗапроса = "", "", ",") + "
			|	" + ПолеРесурс.ПутьКДанным + " КАК " + ПолеРесурс.Имя;
			
			ОписаниеУпорядочиванияРезультатаЗапроса = ОписаниеУпорядочиванияРезультатаЗапроса + ?(ОписаниеУпорядочиванияРезультатаЗапроса = "", "", ",") + "
			|	" + ПолеРесурс.Имя;
			
			Если ПолеРесурс.Итог = Неопределено Тогда
						
				ОписаниеИтогиЗапроса = ОписаниеИтогиЗапроса + ?(ОписаниеИтогиЗапроса = "", "", ",") + "
				|	СУММА(" + ПолеРесурс.Имя + ")";
				
			Иначе
				
				ОписаниеИтогиЗапроса = ОписаниеИтогиЗапроса + ?(ОписаниеИтогиЗапроса = "", "", ",") + "
				|	" + ПолеРесурс.Итог + " КАК " + ПолеРесурс.Имя;
						
			КонецЕсли;
					
			мСтруктураПредставлениеПолей.Вставить(ПолеРесурс.Имя, ПолеРесурс.Представление);
			
		КонецЦикла;
		
		мМассивПоляРесурс = Неопределено;
		
	КонецЕсли;
	
	ОписаниеПолейВыборки = "ВЫБРАТЬ РАЗРЕШЕННЫЕ" + ОписаниеПолейВыборки + ?(ОписаниеПолейВыборки = "", "", ",") + "
	|	ИсточникДанных.Регистратор КАК Регистратор,
	|	ПРЕДСТАВЛЕНИЕ(ИсточникДанных.Регистратор) КАК РегистраторПредставление,
	|	ИсточникДанных.Период КАК Период,
	|	НачалоПериода(ИсточникДанных.Период, День) КАК ПериодДень,
	|	НачалоПериода(ИсточникДанных.Период, Неделя) КАК ПериодНеделя,
	|	НачалоПериода(ИсточникДанных.Период, Декада) КАК ПериодДекада,
	|	НачалоПериода(ИсточникДанных.Период, Месяц) КАК ПериодМесяц,
	|	НачалоПериода(ИсточникДанных.Период, Квартал) КАК ПериодКвартал,
	|	НачалоПериода(ИсточникДанных.Период, Полугодие) КАК ПериодПолугодие,
	|	НачалоПериода(ИсточникДанных.Период, Год) КАК ПериодГод
	|	//ПОЛЯ_СВОЙСТВА
	|	//ПОЛЯ_КАТЕГОРИИ";

	ОписаниеПолейВыборкиПостроитель = "
	|{ВЫБРАТЬ" + ОписаниеПолейВыборкиПостроитель + ?(ОписаниеПолейВыборкиПостроитель = "", "", ",") + "
	|	Регистратор.* КАК Регистратор,
	|	Период КАК Период,
	|	ПериодДень,
	|	ПериодНеделя,
	|	ПериодДекада,
	|	ПериодМесяц,
	|	ПериодКвартал,
	|	ПериодПолугодие,
	|	ПериодГод
	|	//ПСЕВДОНИМЫ_СВОЙСТВА
	|	//ПСЕВДОНИМЫ_КАТЕГОРИИ
	|}";
	
	Если Обороты Тогда
		
		ОписаниеИсточниковЗапроса =	"
		|ИЗ РегистрНакопления." + ИмяРегистра + ".Обороты(&ДатаНач, &ДатаКон, Регистратор {&Периодичность}, {" +
		ОписаниеИсточниковЗапроса + "}) КАК ИсточникДанных
		|//СОЕДИНЕНИЯ";
		
	Иначе
		
		ОписаниеИсточниковЗапроса =	"
		|ИЗ РегистрНакопления." + ИмяРегистра + ".ОстаткиИОбороты(&ДатаНач, &ДатаКон, Регистратор {&Периодичность},, {" +
		ОписаниеИсточниковЗапроса + "}) КАК ИсточникДанных
		|//СОЕДИНЕНИЯ";
		
	КонецЕсли;
	
	ОписаниеОтборовРезультатаЗапроса = "
	|{ГДЕ" + ОписаниеОтборовРезультатаЗапроса + ?(ОписаниеОтборовРезультатаЗапроса = "", "", ",") + "
	|	ИсточникДанных.Регистратор.* КАК Регистратор,
	|	ИсточникДанных.Период КАК Период,
	|	НачалоПериода(ИсточникДанных.Период, День) КАК ПериодДень,
	|	НачалоПериода(ИсточникДанных.Период, Неделя) КАК ПериодНеделя,
	|	НачалоПериода(ИсточникДанных.Период, Декада) КАК ПериодДекада,
	|	НачалоПериода(ИсточникДанных.Период, Месяц) КАК ПериодМесяц,
	|	НачалоПериода(ИсточникДанных.Период, Квартал) КАК ПериодКвартал,
	|	НачалоПериода(ИсточникДанных.Период, Полугодие) КАК ПериодПолугодие,
	|	НачалоПериода(ИсточникДанных.Период, Год) КАК ПериодГод
	|	//УСЛОВИЯ_СВОЙСТВА
	|	//УСЛОВИЯ_КАТЕГОРИИ
	|}";
	
	ОписаниеУпорядочиванияРезультатаЗапроса = "
	|{УПОРЯДОЧИТЬ ПО" + ОписаниеУпорядочиванияРезультатаЗапроса + ?(ОписаниеУпорядочиванияРезультатаЗапроса = "", "", ",") + "
	|	Регистратор.*,
	|	Период,
	|	ПериодДень,
	|	ПериодНеделя,
	|	ПериодДекада,
	|	ПериодМесяц,
	|	ПериодКвартал,
	|	ПериодПолугодие,
	|	ПериодГод
	|	//ПСЕВДОНИМЫ_СВОЙСТВА
	|	//ПСЕВДОНИМЫ_КАТЕГОРИИ
	|}";
	
	ОписаниеИтогиЗапроса = "
	|ИТОГИ" + ОписаниеИтогиЗапроса + "
	|	//ИТОГИ_СВОЙСТВА
	|	//ИТОГИ_КАТЕГОРИИ
	|ПО
	|	ОБЩИЕ";
	
	ОписаниеИтогиЗапросаПостроитель = "
	|{ИТОГИ ПО" + ОписаниеИтогиЗапросаПостроитель + ?(ОписаниеИтогиЗапросаПостроитель = "", "", ",") + "
	|	Регистратор.*,
	|	Период,
	|	ПериодДень,
	|	ПериодНеделя,
	|	ПериодДекада,
	|	ПериодМесяц,
	|	ПериодКвартал,
	|	ПериодПолугодие,
	|	ПериодГод
	|	//ПСЕВДОНИМЫ_СВОЙСТВА
	|	//ПСЕВДОНИМЫ_КАТЕГОРИИ
	|}";
	
	ТекстЗапроса =
		ОписаниеПолейВыборки +
		ОписаниеПолейВыборкиПостроитель +
		ОписаниеИсточниковЗапроса +
		ОписаниеОтборовРезультатаЗапроса +
		ОписаниеУпорядочиванияРезультатаЗапроса +
		ОписаниеИтогиЗапроса +
		ОписаниеИтогиЗапросаПостроитель;
		
	ТекстПоля = "";
	
	Если ИспользоватьСвойстваИКатегории Тогда
		
		ДобавитьВТекстЗапросаСвойстваИКатегории(ТекстЗапроса);
		
	КонецЕсли;
	
	ПостроительОтчета.Текст = ТекстЗапроса;
	
	ОтборПериодичность = ПостроительОтчета.Отбор.Найти("Периодичность");
	
	Если ОтборПериодичность <> Неопределено Тогда
		
		ПостроительОтчета.Отбор.Удалить(ПостроительОтчета.Отбор.Индекс(ОтборПериодичность));
		
	КонецЕсли;
										  
	УстановитьПредставленияПолей(мСтруктураПредставлениеПолей, ПостроительОтчета);
	УстановитьТипыЗначенийСвойствДляОтбора();
		
КонецПроцедуры // УстановитьНачальныеНастройкиПоМетаданнымРегистра()

Процедура ДобавитьВТекстЗапросаСвойстваИКатегории(ТекстЗапроса, ТаблицаПолей = Неопределено, СтруктураПредставлениеПолей = Неопределено, СоответствиеНазначений = Неопределено, Параметры = Неопределено, СоответствиеСвойствКатегорийИИзмерений = Неопределено, СтруктураЗаменять = Неопределено) Экспорт

	// Добавляемые фрагменты запроса
	Если ТипЗнч(СтруктураЗаменять) = Тип("Структура") И СтруктураЗаменять.Количество() > 0 Тогда
		
		ЗаменятьПоляСвойства = ?(СтруктураЗаменять.Свойство("ПоляСвойства"), СтруктураЗаменять.ПоляСвойства, "//ПОЛЯ_СВОЙСТВА");
		ЗаменятьПоляКатегории = ?(СтруктураЗаменять.Свойство("ПоляКатегории"), СтруктураЗаменять.ПоляКатегории, "//ПОЛЯ_КАТЕГОРИИ");
		ЗаменятьПсевдонимыСвойства = ?(СтруктураЗаменять.Свойство("ПсевдонимыСвойства"), СтруктураЗаменять.ПсевдонимыСвойства, "//ПСЕВДОНИМЫ_СВОЙСТВА");
		ЗаменятьПсевдонимыКатегории = ?(СтруктураЗаменять.Свойство("ПсевдонимыКатегории"), СтруктураЗаменять.ПсевдонимыКатегории, "//ПСЕВДОНИМЫ_КАТЕГОРИИ");
		ЗаменятьСоединения = ?(СтруктураЗаменять.Свойство("Соединения"), СтруктураЗаменять.Соединения, "//СОЕДИНЕНИЯ");
		ЗаменятьУсловияСвойства = ?(СтруктураЗаменять.Свойство("УсловияСвойства"), СтруктураЗаменять.УсловияСвойства, "//УСЛОВИЯ_СВОЙСТВА");
		ЗаменятьУсловияКатегории = ?(СтруктураЗаменять.Свойство("УсловияКатегории"), СтруктураЗаменять.УсловияКатегории, "//УСЛОВИЯ_КАТЕГОРИИ");
		ЗаменятьИтогиСвойства = ?(СтруктураЗаменять.Свойство("ИтогиСвойства"), СтруктураЗаменять.ИтогиСвойства, "//ИТОГИ_СВОЙСТВА");
		ЗаменятьИтогиКатегории = ?(СтруктураЗаменять.Свойство("ИтогиКатегории"), СтруктураЗаменять.ИтогиКатегории, "//ИТОГИ_КАТЕГОРИИ");
		
	Иначе
		
		ЗаменятьПоляСвойства = "//ПОЛЯ_СВОЙСТВА";
		ЗаменятьПоляКатегории = "//ПОЛЯ_КАТЕГОРИИ";
		ЗаменятьПсевдонимыСвойства = "//ПСЕВДОНИМЫ_СВОЙСТВА";
		ЗаменятьПсевдонимыКатегории = "//ПСЕВДОНИМЫ_КАТЕГОРИИ";
		ЗаменятьСоединения = "//СОЕДИНЕНИЯ";
		ЗаменятьУсловияСвойства = "//УСЛОВИЯ_СВОЙСТВА";
		ЗаменятьУсловияКатегории = "//УСЛОВИЯ_КАТЕГОРИИ";
		ЗаменятьИтогиСвойства = "//ИТОГИ_СВОЙСТВА";
		ЗаменятьИтогиКатегории = "//ИТОГИ_КАТЕГОРИИ";
		
	КонецЕсли;
	
	Если ТаблицаПолей = Неопределено Тогда
		
		ТаблицаПолей = мТаблицаПолей;
		
	КонецЕсли;
	
	Если СтруктураПредставлениеПолей = Неопределено Тогда
		
		СтруктураПредставлениеПолей = мСтруктураПредставлениеПолей;
		
	КонецЕсли;
	
	Если СоответствиеНазначений = Неопределено Тогда
		
		СоответствиеНазначений = мСоответствиеНазначений;
		
	КонецЕсли;
	
	Если Параметры = Неопределено Тогда
		
		Параметры = ПостроительОтчета.Параметры;
		
	КонецЕсли;
	
	Если СоответствиеСвойствКатегорийИИзмерений = Неопределено Тогда
		
		СоответствиеСвойствКатегорийИИзмерений = мСоответствиеСвойствКатегорийИИзмерений;
		
	КонецЕсли;
	
	ТекстПоляСвойства = "";
	ТекстПоляКатегории = "";
	ТекстПсевдонимыСвойства = "";
	ТекстПсевдонимыКатегории = "";
	ТекстСоединения = "";
	ТекстУсловияСвойства = "";
	ТекстУсловияКатегории = "";
	ТекстИтогиСвойства = "";
	ТекстИтогиКатегории = "";
	
	Если ТипЗнч(СоответствиеСвойствКатегорийИИзмерений) <> Тип("Соответствие") Тогда
		
		СоответствиеСвойствКатегорийИИзмерений = Новый Соответствие;
		
	КонецЕсли;
	
	СоответствиеДобавленныеКатегории = Новый Соответствие;

	Для каждого Поле из ТаблицаПолей Цикл
		
		СвойстваОбъектов = ПланыВидовХарактеристик.СвойстваОбъектов.Выбрать(, Новый Структура("НазначениеСвойства", Поле.Назначение));
		
		Пока СвойстваОбъектов.Следующий() Цикл

			Если СвойстваОбъектов.ЭтоГруппа ИЛИ СвойстваОбъектов.ПометкаУдаления Тогда
				
				Продолжить;
				
			КонецЕсли;
			
			Если СвойстваОбъектов.ТипЗначения.Типы().Количество() > 1 Тогда
				
				ПараметрПустоеЗначениеСвойства = "Неопределено";
				
			Иначе
				
				ТипСвойства = СвойстваОбъектов.ТипЗначения.Типы()[0];
				ВозможныеТипыСвойств = Метаданные.ПланыВидовХарактеристик.СвойстваОбъектов.Тип.Типы();
				
				ИндексТекущегоВозможногоТипа = 1;
				
				Для каждого ВозможныйТипСвойства из ВозможныеТипыСвойств Цикл
					
					Если ВозможныйТипСвойства = ТипСвойства Тогда
						
						ПараметрПустоеЗначениеСвойства = "&ПараметрПустоеЗначениеСвойства" + Формат(ИндексТекущегоВозможногоТипа, "ЧГ=0");
						
					КонецЕсли;
					
					ИндексТекущегоВозможногоТипа = ИндексТекущегоВозможногоТипа + 1;
					
				КонецЦикла;
				
			КонецЕсли;
			
			ИмяСвойства = Поле.ИмяИзмерения + "Свойство" + СтрЗаменить(Строка(СвойстваОбъектов.Ссылка.УникальныйИдентификатор()), "-", "");

			// Поля
			ТекстПоляСвойства = ТекстПоляСвойства + ",
			|	" + ИмяСвойства + ".Значение КАК " + ИмяСвойства + "Значение,
			|	ПРЕДСТАВЛЕНИЕ(" + ИмяСвойства + ".Значение) КАК " + ИмяСвойства + "ЗначениеПредставление";
			
			// Псевдонимы
			ТекстПсевдонимыСвойства = ТекстПсевдонимыСвойства + ",
			|	" + ИмяСвойства + "Значение";
			
			// Соединения
			ТекстСоединения = ТекстСоединения + Символы.ПС + 
			"	{ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЗначенияСвойствОбъектов КАК " + ИмяСвойства + "
			|	ПО " + ИмяСвойства + ".Объект = " + Поле.ПутьКДанным + "
			|	И " + ИмяСвойства + ".Свойство = &Параметр" + ИмяСвойства + "}";
			
			// Условия
			ТекстУсловияСвойства = ТекстУсловияСвойства + ",
			|	ЕСТЬNULL (" + ИмяСвойства + ".Значение, " + ПараметрПустоеЗначениеСвойства + ") КАК " + ИмяСвойства + "Значение";
				
			// Итоги
			ТекстИтогиСвойства = ТекстИтогиСвойства + ",
			|	МАКСИМУМ(" + ИмяСвойства + "Значение)";

			Параметры.Вставить("Параметр" + ИмяСвойства, СвойстваОбъектов.Ссылка);
			СтруктураПредставлениеПолей.Вставить(ИмяСвойства + "Значение", СвойстваОбъектов.Наименование + " (св-во " + Поле.Представление + ")");
			СоответствиеНазначений.Вставить(СвойстваОбъектов.Наименование + " (св-во " + Поле.Представление + ")", СвойстваОбъектов.Ссылка);
			СоответствиеСвойствКатегорийИИзмерений.Вставить(ИмяСвойства + "Значение", Поле.ИмяИзмерения);
			УстановитьСвязьПолей(ИмяСвойства + "Значение", Поле.ИмяИзмерения);
				
		КонецЦикла;
		
		Если Поле.НетКатегорий = Истина Тогда
					
			Продолжить;
					
		КонецЕсли;
		
		КатегорииОбъектов = Справочники.КатегорииОбъектов.Выбрать(,, Новый Структура("НазначениеКатегории", Поле.Назначение));
		
		Пока КатегорииОбъектов.Следующий() Цикл

			Если КатегорииОбъектов.ЭтоГруппа ИЛИ КатегорииОбъектов.ПометкаУдаления Тогда
				
				Продолжить;
				
			КонецЕсли;
			
			ИмяКатегории = Поле.ИмяИзмерения + "Категория" + СтрЗаменить(Строка(КатегорииОбъектов.Ссылка.УникальныйИдентификатор()), "-", "");
				
			// Поля
			ТекстПоляКатегории = ТекстПоляКатегории + ",
			|	ВЫБОР КОГДА " + ИмяКатегории + ".Категория ЕСТЬ NULL ТОГДА Ложь ИНАЧЕ Истина КОНЕЦ КАК " + ИмяКатегории + "Категория";
				
			// Псевдонимы
			ТекстПсевдонимыКатегории = ТекстПсевдонимыКатегории + ",
			|	" + ИмяКатегории + "Категория";
			
			// Соединения
			ТекстСоединения = ТекстСоединения + Символы.ПС + 
			"	{ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КатегорииОбъектов КАК " + ИмяКатегории + "
			|	ПО " + ИмяКатегории + ".Объект = " + Поле.ПутьКДанным + "
			|	И " + ИмяКатегории + ".Категория = &Параметр" + ИмяКатегории + "}";
				
			// Условия
			ТекстУсловияКатегории = ТекстУсловияКатегории + ",
			|	ВЫБОР КОГДА " + ИмяКатегории + ".Категория ЕСТЬ NULL ТОГДА Ложь ИНАЧЕ Истина КОНЕЦ КАК " + ИмяКатегории + "Категория";
			
			// Итоги
			ТекстИтогиКатегории = ТекстИтогиКатегории + ",
			|	МАКСИМУМ(" + ИмяКатегории + "Категория)";

			Параметры.Вставить("Параметр" + ИмяКатегории, КатегорииОбъектов.Ссылка);
			СтруктураПредставлениеПолей.Вставить(ИмяКатегории + "Категория", КатегорииОбъектов.Наименование + " (категория " + Поле.Представление + ")");
			СоответствиеСвойствКатегорийИИзмерений.Вставить(ИмяКатегории + "Категория", Поле.ИмяИзмерения);
			УстановитьСвязьПолей(ИмяКатегории + "Категория", Поле.ИмяИзмерения);

			мСтруктураФорматаПолей.Вставить(ИмяКатегории + "Категория", "БЛ=; БИ=" + КатегорииОбъектов.Наименование + "");
				
		КонецЦикла;
		
		Если СоответствиеДобавленныеКатегории.Получить(Поле.ПутьКДанным) = Неопределено Тогда
			
			// Поля
			ТекстПоляКатегории = ТекстПоляКатегории + ",
			|	" + Поле.ИмяИзмерения + "Категории.Категория КАК " + Поле.ИмяИзмерения + "КатегорииКатегория";
			
			// Псевдонимы
			ТекстПсевдонимыКатегории = ТекстПсевдонимыКатегории + ",
			|	" + Поле.ИмяИзмерения + "КатегорииКатегория";
			
			// Соединения
			ТекстСоединения = ТекстСоединения + Символы.ПС + 
			"	{ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КатегорииОбъектов КАК " + Поле.ИмяИзмерения + "Категории
			|	ПО " + Поле.ИмяИзмерения + "Категории.Объект = " + Поле.ПутьКДанным + "}";
			
			// Условия
			ТекстУсловияКатегории = ТекстУсловияКатегории + ",
			|	" + Поле.ИмяИзмерения + "Категории.Категория КАК " + Поле.ИмяИзмерения + "КатегорииКатегория";
			
			СтруктураПредставлениеПолей.Вставить(Поле.ИмяИзмерения + "КатегорииКатегория", Поле.Представление + " (категории)");
			СоответствиеНазначений.Вставить(Поле.Представление + " (категории)", Поле.Назначение);
			УстановитьСвязьПолей(Поле.ИмяИзмерения + "КатегорииКатегория", Поле.ИмяИзмерения);
			
			СоответствиеДобавленныеКатегории.Вставить(Поле.ПутьКДанным, Поле.ПутьКДанным);
			
		КонецЕсли;

	КонецЦикла;
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, ЗаменятьПоляСвойства, ТекстПоляСвойства);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, ЗаменятьПоляКатегории, ТекстПоляКатегории);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, ЗаменятьПсевдонимыСвойства, ТекстПсевдонимыСвойства);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, ЗаменятьПсевдонимыКатегории, ТекстПсевдонимыКатегории);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, ЗаменятьСоединения, ТекстСоединения);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, ЗаменятьУсловияСвойства, ТекстУсловияСвойства);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, ЗаменятьУсловияКатегории, ТекстУсловияКатегории);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, ЗаменятьИтогиСвойства, ТекстИтогиСвойства);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, ЗаменятьИтогиКатегории, ТекстИтогиКатегории);
	
	мТаблицаПолей.Очистить();

КонецПроцедуры // ДобавитьВТекстЗапросаСвойстваИКатегории()

Процедура ДобавитьВТекстЗапросаКонтактнуюИнформацию(ТекстЗапроса, ТаблицаПолей = Неопределено, СтруктураПредставлениеПолей = Неопределено, Параметры = Неопределено, СоответствиеКонтактнойИнформацииИИзмерений = Неопределено, СтруктураЗаменять = Неопределено) Экспорт

	// Добавляемые фрагменты запроса
	Если ТипЗнч(СтруктураЗаменять) = Тип("Структура") И СтруктураЗаменять.Количество() > 0 Тогда
		
		ЗаменятьПоляКонтактнаяИнформация = ?(СтруктураЗаменять.Свойство("ПоляКонтактнаяИнформация"), СтруктураЗаменять.ПоляКонтактнаяИнформация, "//ПОЛЯ_КОНТАКТНАЯИНФОРМАЦИЯ");
		ЗаменятьПсевдонимыКонтактнаяИнформация = ?(СтруктураЗаменять.Свойство("ПсевдонимыКонтактнаяИнформация"), СтруктураЗаменять.ПсевдонимыКонтактнаяИнформация, "//ПСЕВДОНИМЫ_КОНТАКТНАЯИНФОРМАЦИЯ");
		ЗаменятьСоединения = ?(СтруктураЗаменять.Свойство("Соединения"), СтруктураЗаменять.Соединения, "//КОНТАКТНАЯИНФОРМАЦИЯ_СОЕДИНЕНИЯ");
		ЗаменятьУсловияКонтактнаяИнформация = ?(СтруктураЗаменять.Свойство("УсловияКонтактнаяИнформация"), СтруктураЗаменять.УсловияКонтактнаяИнформация, "//УСЛОВИЯ_КОНТАКТНАЯИНФОРМАЦИЯ");
		ЗаменятьИтогиКонтактнаяИнформация = ?(СтруктураЗаменять.Свойство("ИтогиКонтактнаяИнформация"), СтруктураЗаменять.ИтогиКонтактнаяИнформация, "//ИТОГИ_КОНТАКТНАЯИНФОРМАЦИЯ");
		
	Иначе
		
		ЗаменятьПоляКонтактнаяИнформация = "//ПОЛЯ_КОНТАКТНАЯИНФОРМАЦИЯ";
		ЗаменятьПсевдонимыКонтактнаяИнформация = "//ПСЕВДОНИМЫ_КОНТАКТНАЯИНФОРМАЦИЯ";
		ЗаменятьСоединения = "//КОНТАКТНАЯИНФОРМАЦИЯ_СОЕДИНЕНИЯ";
		ЗаменятьУсловияКонтактнаяИнформация = "//УСЛОВИЯ_КОНТАКТНАЯИНФОРМАЦИЯ";
		ЗаменятьИтогиКонтактнаяИнформация = "//ИТОГИ_КОНТАКТНАЯИНФОРМАЦИЯ";
		
	КонецЕсли;
	
	Если ТаблицаПолей = Неопределено Тогда
		
		ТаблицаПолей = мТаблицаПолейКонтактнаяИнформация;
		
	КонецЕсли;
	
	Если СтруктураПредставлениеПолей = Неопределено Тогда
		
		СтруктураПредставлениеПолей = мСтруктураПредставлениеПолей;
		
	КонецЕсли;
	
	Если Параметры = Неопределено Тогда
		
		Параметры = ПостроительОтчета.Параметры;
		
	КонецЕсли;
	
	Если СоответствиеКонтактнойИнформацииИИзмерений = Неопределено Тогда
		
		СоответствиеКонтактнойИнформацииИИзмерений = мСоответствиеКонтактнойИнформацииИИзмерений;
		
	КонецЕсли;
	
	ТекстПоляКонтактнаяИнформация = "";
	ТекстПсевдонимыКонтактнаяИнформация = "";
	ТекстСоединения = "";
	ТекстУсловияКонтактнаяИнформация = "";
	ТекстИтогиКонтактнаяИнформация = "";
	
	Если ТипЗнч(СоответствиеКонтактнойИнформацииИИзмерений) <> Тип("Соответствие") Тогда
		
		СоответствиеКонтактнойИнформацииИИзмерений = Новый Соответствие;
		
	КонецЕсли;

	Для каждого Поле из ТаблицаПолей Цикл
		
		Запрос = Новый Запрос(
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
		|	""#ПроизвольныйВид#"" КАК Объект,
		|	КонтактнаяИнформация.Тип КАК Тип,
		|	КонтактнаяИнформация.Вид КАК Наименование
		|ИЗ
		|	РегистрСведений.КонтактнаяИнформация КАК КонтактнаяИнформация
		|ГДЕ
		|	(НЕ КонтактнаяИнформация.Вид В
		|				(ВЫБРАТЬ РАЗЛИЧНЫЕ
		|					ВидыКонтактнойИнформации.Ссылка
		|				ИЗ
		|					Справочник.ВидыКонтактнойИнформации КАК ВидыКонтактнойИнформации))
		|	И КонтактнаяИнформация.Объект ССЫЛКА " + Поле.ТипОбъекта + "
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВидыКонтактнойИнформации.Ссылка,
		|	ВидыКонтактнойИнформации.Тип,
		|	ВидыКонтактнойИнформации.Наименование
		|ИЗ
		|	Справочник.ВидыКонтактнойИнформации КАК ВидыКонтактнойИнформации
		|ГДЕ
		|	ВидыКонтактнойИнформации.ПометкаУдаления = ЛОЖЬ
		|	И ВидыКонтактнойИнформации.ВидОбъектаКонтактнойИнформации = &ВидОбъектаКонтактнойИнформации");
		
		Запрос.УстановитьПараметр("ВидОбъектаКонтактнойИнформации", Поле.ВидОбъекта);
		
		ВидыКонтактнойИнформации = Запрос.Выполнить().Выбрать();	
		
		Индекс = 1;
		
		Пока ВидыКонтактнойИнформации.Следующий() Цикл
			
			Если ВидыКонтактнойИнформации.Объект = "#ПроизвольныйВид#" тогда
				
				ИмяВидаКонтактнойИнформации = Поле.ИмяИзмерения + "ВидКонтактнойИнформации" + Формат(Индекс, "ЧГ=0");
				Параметры.Вставить("Параметр" + ИмяВидаКонтактнойИнформации, ВидыКонтактнойИнформации.Наименование);
				
				Индекс = Индекс + 1;
				
			Иначе
				
				ИмяВидаКонтактнойИнформации = Поле.ИмяИзмерения + "ВидКонтактнойИнформации" + СтрЗаменить(Строка(ВидыКонтактнойИнформации.Объект.УникальныйИдентификатор()), "-", "");
				Параметры.Вставить("Параметр" + ИмяВидаКонтактнойИнформации, ВидыКонтактнойИнформации.Объект);
				
			КонецЕсли;
			
			// Поля
			ТекстПоляКонтактнаяИнформация = ТекстПоляКонтактнаяИнформация + ",
			|	" + ИмяВидаКонтактнойИнформации + ".Представление КАК " + ИмяВидаКонтактнойИнформации + "Значение";
				
			// Псевдонимы
			ТекстПсевдонимыКонтактнаяИнформация = ТекстПсевдонимыКонтактнаяИнформация + ",
			|	" + ИмяВидаКонтактнойИнформации + "Значение";
			
			// Соединения
			ТекстСоединения = ТекстСоединения + Символы.ПС + 
			"	{ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КонтактнаяИнформация КАК " + ИмяВидаКонтактнойИнформации + "
			|	ПО " + ИмяВидаКонтактнойИнформации + ".Объект = " + Поле.ПутьКДанным + "
			|	И " + ИмяВидаКонтактнойИнформации + ".Вид = &Параметр" + ИмяВидаКонтактнойИнформации + "}";
			
			// Условия
			ТекстУсловияКонтактнаяИнформация = ТекстУсловияКонтактнаяИнформация + ",
			|	ЕСТЬNULL (" + ИмяВидаКонтактнойИнформации + ".Представление, """") КАК " + ИмяВидаКонтактнойИнформации + "Значение";
				
			// Итоги
			ТекстИтогиКонтактнаяИнформация = ТекстИтогиКонтактнаяИнформация + ",
			|	МАКСИМУМ(" + ИмяВидаКонтактнойИнформации + "Значение)";
			
			СтруктураПредставлениеПолей.Вставить(ИмяВидаКонтактнойИнформации + "Значение", СокрЛП(ВидыКонтактнойИнформации.Наименование));
			СоответствиеКонтактнойИнформацииИИзмерений.Вставить(ИмяВидаКонтактнойИнформации + "Значение", Поле.ИмяИзмерения);
				
		КонецЦикла;
		
	КонецЦикла;
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, ЗаменятьПоляКонтактнаяИнформация, ТекстПоляКонтактнаяИнформация);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, ЗаменятьПсевдонимыКонтактнаяИнформация, ТекстПсевдонимыКонтактнаяИнформация);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, ЗаменятьСоединения, ТекстСоединения);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, ЗаменятьУсловияКонтактнаяИнформация, ТекстУсловияКонтактнаяИнформация);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, ЗаменятьИтогиКонтактнаяИнформация, ТекстИтогиКонтактнаяИнформация);
	
	мТаблицаПолейКонтактнаяИнформация.Очистить();

КонецПроцедуры // ДобавитьВТекстЗапросаКонтактнуюИнформацию()
	
Процедура УстановитьТипыЗначенийСвойствДляОтбора(НастраиваемыйПостроительОтчета = Неопределено, СоответствиеСвойствКатегорийИИзмерений = Неопределено, СоответствиеНазначений = Неопределено, СтруктураПредставлениеПолей = Неопределено) Экспорт
	
	Если НастраиваемыйПостроительОтчета = Неопределено Тогда
		
		НастраиваемыйПостроительОтчета = ПостроительОтчета;
		
	КонецЕсли;
	
	Если СоответствиеНазначений = Неопределено Тогда
		
		СоответствиеНазначений = мСоответствиеНазначений;
		
	КонецЕсли;
	
	Если СтруктураПредставлениеПолей = Неопределено Тогда
		
		СтруктураПредставлениеПолей = мСтруктураПредставлениеПолей;
		
	КонецЕсли;
	
	Если СоответствиеСвойствКатегорийИИзмерений = Неопределено Тогда
		
		СоответствиеСвойствКатегорийИИзмерений = мСоответствиеСвойствКатегорийИИзмерений;
		
	КонецЕсли;
	
	Если СоответствиеСвойствКатегорийИИзмерений.Количество() = 0 Тогда
		
		Возврат;
		
	КонецЕсли;
	
	Для каждого Поле из СтруктураПредставлениеПолей Цикл
		
		Если СоответствиеСвойствКатегорийИИзмерений[Поле.Ключ] <> Неопределено Тогда
			
			Назначение = СоответствиеНазначений[Поле.Значение];
			
			Если Назначение <> Неопределено Тогда
					
				ДоступноеПоле = НастраиваемыйПостроительОтчета.ДоступныеПоля.Найти(Поле.Ключ);
				
				Если ДоступноеПоле <> Неопределено И ДоступноеПоле.Отбор Тогда
					
					ДоступноеПоле.ТипЗначения = Назначение.ТипЗначения;
					
				КонецЕсли;

			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
	ВозможныеТипыСвойств = Метаданные.ПланыВидовХарактеристик.СвойстваОбъектов.Тип.Типы();
	
	ИндексТекущегоВозможногоТипа = 1;
			
	Для каждого ВозможныйТипСвойства из ВозможныеТипыСвойств Цикл
				
		НастраиваемыйПостроительОтчета.Параметры.Вставить("ПараметрПустоеЗначениеСвойства" + Формат(ИндексТекущегоВозможногоТипа, "ЧГ=0"), ОбщегоНазначения.ПустоеЗначениеТипа(ВозможныйТипСвойства));
		ИндексТекущегоВозможногоТипа = ИндексТекущегоВозможногоТипа + 1;
				
	КонецЦикла;
	
КонецПроцедуры // УстановитьТипыЗначенийСвойствДляОтбора()

Функция ПолучитьИнтервальноеПоле(ТаблицаИнтервалов, СтруктураПараметры, СоответствиеТиповИнтервальныхПолей, ПолеИнтервала, ИсходноеПоле, ТипИсходногоПоля, СтрокаФорматирования = "") Экспорт
	
	ТекстПоляВыбора = Символы.Таб + "ВЫБОР ";
	ТипГраницыИнтервалаСтрока = Ложь;
	
	Если ТипИсходногоПоля = Тип("Строка") Тогда
		
		ПредыдущаяГраницаИнтервала = " ";
		ТипГраницыИнтервалаСтрока = Истина;
		
	ИначеЕсли ТипИсходногоПоля = Тип("Дата") Тогда
		
		ПредыдущаяГраницаИнтервала = '00010101';
		
	Иначе
		
		ПредыдущаяГраницаИнтервала = 0;
		
	КонецЕсли;
	
	Для Каждого СтрокаИнтервал Из ТаблицаИнтервалов Цикл
		
		Если СтрокаИнтервал.ИнтервальноеПоле = ПолеИнтервала Тогда
			
			ТекущаяГраницаИнтервала = ?(ПустаяСтрока(СтрокаИнтервал.ВГраница), " ", СтрокаИнтервал.ВГраница);
			
			Если ТипГраницыИнтервалаСтрока Тогда
				
				_ИсходноеПолеПредыдущаяГраница = "ПОДСТРОКА(" + ИсходноеПоле + ", 1, " + ?(СтрДлина(ПредыдущаяГраницаИнтервала) = 0, 1, СтрДлина(ПредыдущаяГраницаИнтервала)) + ")";
				_ИсходноеПолеТекущаяГраница = "ПОДСТРОКА(" + ИсходноеПоле + ", 1, " + ?(СтрДлина(ТекущаяГраницаИнтервала) = 0, 1, СтрДлина(ТекущаяГраницаИнтервала)) + ")";
				
			Иначе
				
				_ИсходноеПолеПредыдущаяГраница = ИсходноеПоле;
				_ИсходноеПолеТекущаяГраница = ИсходноеПоле;
				
			КонецЕсли;
			
			ТекстПоляВыбора = ТекстПоляВыбора + Символы.ПС + Символы.Таб + "КОГДА " + _ИсходноеПолеПредыдущаяГраница + " >= " + ЗначениеВТекстЗапроса(ПредыдущаяГраницаИнтервала) + " И " + _ИсходноеПолеТекущаяГраница + " <= " + ЗначениеВТекстЗапроса(ТекущаяГраницаИнтервала) + " ТОГДА &Интервал" + ПолеИнтервала + ЗначениеВТекстЗапроса(ТаблицаИнтервалов.Индекс(СтрокаИнтервал) + 1);
			
			СтруктураПараметры.Вставить("Интервал" + ПолеИнтервала + ЗначениеВТекстЗапроса(ТаблицаИнтервалов.Индекс(СтрокаИнтервал) + 1), СтрокаИнтервал.Название);
			ПредыдущаяГраницаИнтервала = ТекущаяГраницаИнтервала;
			
		КонецЕсли;
		
	КонецЦикла;	
	
	Если Найти(ТекстПоляВыбора, "КОГДА") > 0 Тогда
		
		ТекстПоляВыбора = ТекстПоляВыбора + Символы.ПС + Символы.Таб + "ИНАЧЕ &Интервал" + ПолеИнтервала + "Прочие";
		ТекстПоляВыбора = ТекстПоляВыбора + Символы.ПС + Символы.Таб + "КОНЕЦ КАК " + ПолеИнтервала;
		
	Иначе
		
		ТекстПоляВыбора = Символы.Таб + "&Интервал" + ПолеИнтервала + "Прочие КАК " + ПолеИнтервала;
		
	КонецЕсли;
	
	СтруктураПараметры.Вставить("Интервал" + ПолеИнтервала + "Прочие", Нстр("ru = 'Остальные'"));
	
	СоответствиеТиповИнтервальныхПолей.Вставить(ПолеИнтервала, ТипИсходногоПоля);
	
	Возврат ТекстПоляВыбора;
	
КонецФункции // ПолучитьИнтервальноеПоле()

Функция ЗначениеВТекстЗапроса(Значение)
	
	Если ТипЗнч(Значение) = Тип("Число") Тогда
		
		Результат = Формат(Значение,"ЧРД=.; ЧН=; ЧГ=0");
		
	ИначеЕсли ТипЗнч(Значение) = Тип("Строка") Тогда
		
		Результат = """" + Значение + """";
		
	ИначеЕсли ТипЗнч(Значение) = Тип("Дата") Тогда
		
		Результат = "ДАТАВРЕМЯ(" + ЗначениеВТекстЗапроса(Год(Значение)) + ", " + ЗначениеВТекстЗапроса(Месяц(Значение)) + ", " + ЗначениеВТекстЗапроса(День(Значение)) + ", " + ЗначениеВТекстЗапроса(Час(Значение)) + ", " + ЗначениеВТекстЗапроса(Минута(Значение)) + ", " + ЗначениеВТекстЗапроса(Секунда(Значение)) + ")";
		
	Иначе
		
		Результат = "0";
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции // ЗначениеВТекстЗапроса()

Процедура ДобавитьВТекстЗапросаИнтервальныеГруппировкиФизлиц(ТекстЗапроса, ИмяПоляФизЛицо = "Работники.ФизЛицо", ТаблицаИнтервалов = Неопределено, СтруктураПредставлениеПолей = Неопределено, Параметры = Неопределено, СоответствиеТиповИнтервальныхПолей = Неопределено, СтруктураЗаменять = Неопределено) Экспорт
	
	// Добавляемые фрагменты запроса
	Если ТипЗнч(СтруктураЗаменять) = Тип("Структура") И СтруктураЗаменять.Количество() > 0 Тогда
		
		ЗаменятьПоляИнтервальныеГруппировкиФизЛиц = ?(СтруктураЗаменять.Свойство("ПоляИнтервальныеГруппировкиФизЛиц"), СтруктураЗаменять.ПоляИнтервальныеГруппировкиФизЛиц, "//ПОЛЯ_ИНТЕРВАЛЬНЫЕГРУППИРОВКИФИЗЛИЦ");
		ЗаменятьИтогиИнтервальныеГруппировкиФизЛиц = ?(СтруктураЗаменять.Свойство("ИтогиИнтервальныеГруппировкиФизЛиц"), СтруктураЗаменять.ИтогиИнтервальныеГруппировкиФизЛиц, "//ИНТЕРВАЛЬНЫЕГРУППИРОВКИФИЗЛИЦ");
		
	Иначе
		
		ЗаменятьПоляИнтервальныеГруппировкиФизЛиц = "//ПОЛЯ_ИНТЕРВАЛЬНЫЕГРУППИРОВКИФИЗЛИЦ";
		ЗаменятьИтогиИнтервальныеГруппировкиФизЛиц = "//ИТОГИ_ИНТЕРВАЛЬНЫЕГРУППИРОВКИФИЗЛИЦ";
		
	КонецЕсли;
	
	Если ТаблицаИнтервалов = Неопределено Тогда
		
		ТаблицаИнтервалов = Интервалы;
		
	КонецЕсли;
	
	Если СтруктураПредставлениеПолей = Неопределено Тогда
		
		СтруктураПредставлениеПолей = мСтруктураПредставлениеПолей;
		
	КонецЕсли;
	
	Если СоответствиеТиповИнтервальныхПолей = Неопределено Тогда
		
		СоответствиеТиповИнтервальныхПолей = мСоответствиеТиповИнтервальныхПолей;
		
	КонецЕсли;
	
	Если Параметры = Неопределено Тогда
		
		Параметры = ПостроительОтчета.Параметры;
		
	КонецЕсли;
	
	ПолеВозраст =
	"ВЫБОР
	|	КОГДА ГОД(" + ИмяПоляФизЛицо + ".ДатаРождения) = 1
	|		ТОГДА 0
	|	ИНАЧЕ &ДатаАктуальности_Год - ГОД(" + ИмяПоляФизЛицо + ".ДатаРождения) + ВЫБОР
	|			КОГДА МЕСЯЦ(" + ИмяПоляФизЛицо + ".ДатаРождения) * 100 + ДЕНЬ(" + ИмяПоляФизЛицо + ".ДатаРождения) > &ДатаАктуальности_Месяц * 100 + &ДатаАктуальности_День
	|				ТОГДА -1
	|			ИНАЧЕ 0
	|		КОНЕЦ
	|	КОНЕЦ";
	
	СтруктураПредставлениеПолей.Вставить("ВозрастИН", "Возраст (интервалы)");
	
	ПолеФИО = 
	"ВЫБОР
	|	КОГДА ФИОФизЛиц.Фамилия ЕСТЬ NULL
	|		ТОГДА " + ИмяПоляФизЛицо + ".Наименование
	|	ИНАЧЕ ФИОФизЛиц.Фамилия + "" "" + ФИОФизЛиц.Имя + "" "" + ФИОФизЛиц.Отчество
	|КОНЕЦ";
	
	СтруктураПредставлениеПолей.Вставить("ФиоПолноеИН", "Начало ФИО (интервалы)");
	
	ТекстПолеСИнтервалами =	",
	|" + ПолучитьИнтервальноеПоле(ТаблицаИнтервалов, Параметры, СоответствиеТиповИнтервальныхПолей, "ВозрастИН", ПолеВозраст, Тип("Число")) + ",
	|" + ПолучитьИнтервальноеПоле(ТаблицаИнтервалов, Параметры, СоответствиеТиповИнтервальныхПолей, "ФиоПолноеИН", ПолеФИО, Тип("Строка"));
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, ЗаменятьПоляИнтервальныеГруппировкиФизЛиц, ТекстПолеСИнтервалами);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, ЗаменятьИтогиИнтервальныеГруппировкиФизЛиц, ",
	|	ВозрастИН,
	|	ФиоПолноеИН");
	
КонецПроцедуры // ДобавитьВТекстЗапросаИнтервальныеГруппировкиФизлиц()

Процедура УстановитьПредставленияПолей(СтруктураПредставлениеПолей, ПостроительОтчета) Экспорт

	СтруктураПредставлениеПолей.Вставить("Периодичность", "Периодичность итогов");
	СтруктураПредставлениеПолей.Вставить("Регистратор", "Документ движения (регистратор)");
	СтруктураПредставлениеПолей.Вставить("Период", "Период");
	СтруктураПредставлениеПолей.Вставить("ПериодДень", "По дням");
	СтруктураПредставлениеПолей.Вставить("ПериодНеделя", "По неделям");
	СтруктураПредставлениеПолей.Вставить("ПериодДекада", "По декадам");
	СтруктураПредставлениеПолей.Вставить("ПериодМесяц", "По месяцам");
	СтруктураПредставлениеПолей.Вставить("ПериодКвартал", "По кварталам");
	СтруктураПредставлениеПолей.Вставить("ПериодПолугодие", "По полугодиям");
	СтруктураПредставлениеПолей.Вставить("ПериодГод", "По годам");

	СтруктураКоллекцийПостроителяОтчета = Новый Структура("ДоступныеПоля, ВыбранныеПоля, ИзмеренияКолонки, ИзмеренияСтроки, Отбор, Порядок");
	
	Для каждого ЭлементСтруктуры Из СтруктураКоллекцийПостроителяОтчета Цикл
		
		Для Индекс = 0 По ПостроительОтчета[ЭлементСтруктуры.Ключ].Количество() - 1 Цикл
			
			Представление = Неопределено;
			
			Если Найти(ПостроительОтчета[ЭлементСтруктуры.Ключ][Индекс].ПутьКДанным, ".") = 0 Тогда
				
				Попытка
					
					СтруктураПредставлениеПолей.Свойство(ПостроительОтчета[ЭлементСтруктуры.Ключ][Индекс].ПутьКДанным, Представление);
					
				Исключение
					
				КонецПопытки;
				
			КонецЕсли;
				
			Если Представление = Неопределено Тогда
					
				Продолжить;
				
			Иначе
				
				ПостроительОтчета[ЭлементСтруктуры.Ключ][Индекс].Представление = Представление;
					
			КонецЕсли;
				
		КонецЦикла;
		
	КонецЦикла; 
	
КонецПроцедуры // УстановитьПредставленияПолей()

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ДЛЯ РАБОТЫ СО СПРАВКОЙ ФОРМЫ

Процедура ПодготовитьСправкуФормы(Объект, Отчет, Форма) Экспорт
	
	Если Форма.ЭлементыФормы.Найти("ПанельСправкаФормы") = Неопределено Тогда
		
		Возврат;
		
	КонецЕсли;
	
	ЦветФонаСправки = РаботаСДиалогами.ВернутьШестнадцатиричноеПредставлениеЦвета(РаботаСДиалогами.ВстроеннаяСправка_ЦветФона());
	ЦветСсылкиСправки = РаботаСДиалогами.ВернутьШестнадцатиричноеПредставлениеЦвета(РаботаСДиалогами.ВстроеннаяСправка_ЦветСсылки());
	
	Форма.ЭлементыФормы.ПанельСправкаФормы.ЦветРамки = РаботаСДиалогами.ВстроеннаяСправка_ЦветРамки();
	Форма.ЭлементыФормы.НадписиЗакрытьСправкуФормы.ЦветФона	= РаботаСДиалогами.ВстроеннаяСправка_ЦветФона();
	Форма.ЭлементыФормы.НадписиЗакрытьСправкуФормы.ЦветТекста = РаботаСДиалогами.ВстроеннаяСправка_ЦветСсылки();
	
	ТекстМакета = "";
	
	Если Отчет.Метаданные().Макеты.Найти("МакетВстроеннойСправки") <> Неопределено Тогда
		
		ТекстМакета = Отчет.ПолучитьМакет("МакетВстроеннойСправки").ПолучитьТекст();
		
	Иначе
		
		ТекстМакета = Отчет.ПолучитьТекстСправкиФормы();
		
	КонецЕсли;
	
	ТекстМакетаВстроеннойСправки =
	"<HTML>
	|	<HEAD>
	|		<META http-equiv=Content-Type content=""text/html; charset=utf-8"">" + РаботаСДиалогами.ВстроеннаяСправка_СтилиДокумента() + "
	|	</HEAD>
	|
	|	<BODY aLink=" + ЦветСсылкиСправки + " vLink=" + ЦветСсылкиСправки + " link=" + ЦветСсылкиСправки + " bgColor=" + ЦветФонаСправки + " scroll=auto><FONT face=""Verdana"" size=1>
	|		<IMG src=" + РаботаСДиалогами.ПолучитьПутьККартинкеДляHTML(БиблиотекаКартинок.КартинкаВстроеннойСправкиФормы, Форма.ЭлементыФормы.ВстроеннаяСправка) + ">
	|" + ТекстМакета + "
	|	</FONT></BODY>
	|</HTML>";
	
	Форма.ЭлементыФормы.ВстроеннаяСправка.УстановитьТекст(ТекстМакетаВстроеннойСправки);
	
КонецПроцедуры // ПодготовитьСправкуФормы()

Функция ПолучитьТекстСправкиФормы() Экспорт
	
	Возврат "";
	
КонецФункции // ПолучитьТекстСправкиФормы()

Процедура onclick(Объект, Отчет, Форма, Элемент, pEvtObj) Экспорт
	
	РаботаСДиалогами.ПолеHTMLДокументаOnClick(Элемент, pEvtObj, Форма);
	
КонецПроцедуры // onclick()

Процедура onmouseout(Объект, Отчет, Форма, Элемент, pEvtObj) Экспорт
	
	РаботаСДиалогами.ПолеHTMLДокументаOnMouseOut(Элемент, pEvtObj);
	
КонецПроцедуры // onmouseout()

Процедура onmouseover(Объект, Отчет, Форма, Элемент, pEvtObj) Экспорт
	
	РаботаСДиалогами.ПолеHTMLДокументаOnMouseOver(Элемент, pEvtObj);
	
КонецПроцедуры // onmouseover()

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ДЛЯ РАБОТЫ С ПОДКЛЮЧАЕМЫМИ ОБРАБОТКАМИ

Функция ВыполнитьОбработкиПодключаемыеКОтчету(СпособЗапуска, Объект = Неопределено, ТабличныйДокумент)
	
	Если Объект = Неопределено Тогда
		Возврат Истина;
	КонецЕсли;
	
	Если мОбработкиПодключаемыеКОтчету = Неопределено Тогда
		ИмяМетаданныхОбъекта = "Отчет." + Объект.Метаданные().Имя;
		мОбработкиПодключаемыеКОтчету = УниверсальныеМеханизмы.ПолучитьСписокОбработокПодключаемыхКОтчету(ИмяМетаданныхОбъекта);
	КонецЕсли;
	
	МассивОбработкиПодключаемыеКОтчету = мОбработкиПодключаемыеКОтчету.НайтиСтроки(Новый Структура("СпособЗапуска", СпособЗапуска));
	
	Для каждого ЭлОбработка Из МассивОбработкиПодключаемыеКОтчету Цикл
		Результат = УниверсальныеМеханизмы.ВыполнитьОбработкуПодключаемуюКОтчету(ЭлОбработка, Объект, ТабличныйДокумент);
		Если Результат = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		Если Результат.Отказ Тогда
			Возврат Ложь;
		КонецЕсли;	
		
	КонецЦикла; 
	
	Возврат Истина;
	
КонецФункции // ВыполнитьОбработкиПодключаемыеКОтчету

// Процедура вызывает обработку, которая была выбрана из меню пользователем
// Результат выполнения обработки не обрабатывается
//
Процедура ОбработатьНажатиеНаКнопкуОбработкиПодключаемойКОтчету(ИдентификаторОбработки, Объект = Неопределено, ТабличныйДокумент) Экспорт
	
	УниверсальныеМеханизмы.ВыполнитьОбработкуПодключаемуюКОтчету(мОбработкиПодключаемыеКОтчету.Найти(ИдентификаторОбработки, "Идентификатор"), Объект, ТабличныйДокумент);
	
КонецПроцедуры // ОбработатьНажатиеНаКнопкуОбработкиПодключаемойКОтчету

// Процедура формирует список обработок, заполняет меню обработками выполяемых вручную
//
Процедура УстановитьОбработкиПодключаемыеКОтчету(Объект, Отчет, Форма)
	
	Если Отчет = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если мОбработкиПодключаемыеКОтчету = Неопределено Тогда
		ИмяМетаданныхОбъекта = "Отчет." + Отчет.Метаданные().Имя;
		мОбработкиПодключаемыеКОтчету = УниверсальныеМеханизмы.ПолучитьСписокОбработокПодключаемыхКОтчету(ИмяМетаданныхОбъекта);
	КонецЕсли; 
	
	// Сформируем меню, содержащее обработки вызываемые вручную
	Если мДействиеФормыВыполнитьВнешнююОбработку <> Неопределено Тогда
		ОбработкиЗапускаемыеВручную = мОбработкиПодключаемыеКОтчету.НайтиСтроки(Новый Структура("СпособЗапуска", Перечисления.СпособыЗапускаВнешнихОбработокВОтчетахНаБазеУниверсального.Вручную));
		Если ОбработкиЗапускаемыеВручную.Количество() <> 0 Тогда
			ИндексКнопки = -1;
			
			КнопкиПодмеюДействияФормы = Форма.ЭлементыФормы.КоманднаяПанельФормы.Кнопки.Подменю.Кнопки;
			КнопкаНастройка = КнопкиПодмеюДействияФормы.Найти("Настройка");
			Если КнопкаНастройка <> Неопределено Тогда
				ИндексКнопки = КнопкиПодмеюДействияФормы.Индекс(КнопкаНастройка);
				КнопкиПодмеюДействияФормы.Вставить(ИндексКнопки + 1, "РазделительОбработки", ТипКнопкиКоманднойПанели.Разделитель);
				ИндексКнопки = ИндексКнопки + 2;
			Иначе
				КнопкаДействиеЗакрыть = КнопкиПодмеюДействияФормы.Найти("ДействиеЗакрыть");
				Если КнопкаДействиеЗакрыть <> Неопределено Тогда
					ИндексКнопки = КнопкиПодмеюДействияФормы.Индекс(КнопкаДействиеЗакрыть);
					КнопкиПодмеюДействияФормы.Вставить(ИндексКнопки, "РазделительОбработки", ТипКнопкиКоманднойПанели.Разделитель);
					ИндексКнопки = ИндексКнопки - 1;
				КонецЕсли; 
			КонецЕсли; 
			
			Для каждого ЭлОбработка Из ОбработкиЗапускаемыеВручную Цикл
				
				Если ИндексКнопки = -1 Тогда
					Кнопка = КнопкиПодмеюДействияФормы.Добавить();
				Иначе
					Кнопка = КнопкиПодмеюДействияФормы.Вставить(ИндексКнопки);
					ИндексКнопки = ИндексКнопки + 1;
				КонецЕсли; 
				Кнопка.Имя = ЭлОбработка.Идентификатор;
				Кнопка.ТипКнопки = ТипКнопкиКоманднойПанели.Действие;
				Кнопка.Текст = ЭлОбработка.Наименование;
				Кнопка.Пояснение = "Выполнить обработку """ + ЭлОбработка.Наименование + """";
				Кнопка.Действие = мДействиеФормыВыполнитьВнешнююОбработку;
			КонецЦикла;
		КонецЕсли; 
	КонецЕсли; 
	
КонецПроцедуры // УстановитьОбработкиПодключаемыеКОтчету
 
////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ РАСЧЕТА ШИРИНЫ КОЛОНОК

Процедура РасчетШириныКолонок(ТабличныйДокумент) Экспорт
	
	Перем МаксимальнаяШиринаКолонки;
	Перем КонечнаяСтрока, НачальнаяСтрока, ТекущаяКолонка, ТекущаяСтрока, НачалоДанных;
	Перем ОбластьШапки, ОбластьПодвала;
	Перем ШиринаКолонки, ТекстЯчейки, НомерСтрокиТекста;
	Перем КоличествоУровнейГруппировокСтрок, Отступ;
	Перем ШириныКолонок;
	
	Если НЕ мРассчитыватьШиринуКолонок Тогда
		
		Возврат;
		
	КонецЕсли;
	
	Состояние(НСтр("ru='Выполняется расчет ширины колонок...'"));
	
	// Ограничение максимальной ширины колонки
	МаксимальнаяШиринаКолонки = 50;
	
	// Массив, в который будут помещаться ширины колонок
	ШириныКолонок = Новый Массив;
	
	// Получим количество уровней группировок в отчете для учета автоматического отступа
	КоличествоУровнейГруппировокСтрок = ТабличныйДокумент.КоличествоУровнейГруппировокСтрок();
	
	// Инициализируем начальные строки
	НачальнаяСтрока = 0;
	НачалоДанных = 0;
	
	// Найдем в результирующем документе область шапки таблицы
	ОбластьШапки = ТабличныйДокумент.Области.Найти("ШапкаТаблицы");
	
	Если ТипЗнч(ОбластьШапки) = Тип("ОбластьЯчеекТабличногоДокумента") Тогда
		
		// Из шапки таблицы получим начальную строку с которой будем рассчитывать ширины
		НачальнаяСтрока = ОбластьШапки.Верх;
		НачалоДанных = ОбластьШапки.Низ + 1;
		
	Иначе
		
		// Если область шапки таблицы не найдена, найдем область шапки строк
		ОбластьШапки = ТабличныйДокумент.Области.Найти("ШапкаСтрок");
		
		Если ТипЗнч(ОбластьШапки) = Тип("ОбластьЯчеекТабличногоДокумента") Тогда
			
			// Из шапки таблицы получим начальную строку с которой будем рассчитывать ширины
			НачальнаяСтрока = ОбластьШапки.Верх;
			НачалоДанных = ОбластьШапки.Низ + 1;
			
		КонецЕсли;
			
	КонецЕсли;
	
	// Получим область подвала отчета и вычислим конечную строку расчета
	ОбластьПодвала = ТабличныйДокумент.Области.Найти("Подвал");
	
	Если ТипЗнч(ОбластьПодвала) = Тип("ОбластьЯчеекТабличногоДокумента") Тогда
		
		// Область подвала найдена
		КонечнаяСтрока = ОбластьПодвала.Верх - 1;
		
	Иначе 
		
		// Область подвала не найдена
		КонечнаяСтрока = ТабличныйДокумент.ВысотаТаблицы;
		
	КонецЕсли;
	
	// Переберем все колонки отчета
	Для ТекущаяКолонка = 1 По ТабличныйДокумент.ШиринаТаблицы Цикл
		
		АвтоОтступ = 0;
		
		// Переберем строки, которые будут использованы для расчета ширин колонок
		Для ТекущаяСтрока = НачальнаяСтрока По КонечнаяСтрока Цикл
			
			ШиринаКолонки = 0;

			// Получим область текущей ячейки
			ОбластьЯчейки = ТабличныйДокумент.Область(ТекущаяСтрока, ТекущаяКолонка);
			
			Если ОбластьЯчейки.Лево <> ТекущаяКолонка Или ОбластьЯчейки.Верх <> ТекущаяСтрока Тогда
				
				// Данная ячейка принадлежит объединенным ячейкам и не является начальной ячейкой
				Продолжить;
				
			КонецЕсли;
			
			Если КоличествоУровнейГруппировокСтрок > 0 И ТекущаяСтрока = НачалоДанных Тогда
				
				// Для первой строки с данными получим значение автоотступа
				АвтоОтступ = ОбластьЯчейки.АвтоОтступ;
				
			КонецЕсли;
			
			// Получим текст ячейки
			ТекстЯчейки = ОбластьЯчейки.Текст;
			
			// Для каждой строки из текста ячейки рассчитаем количество символов в строке
			Для НомерСтрокиТекста = 1 По СтрЧислоСтрок(ТекстЯчейки) Цикл
				
				ШиринаТекстаЯчейки = СтрДлина(СтрПолучитьСтроку(ТекстЯчейки, НомерСтрокиТекста));
				
				Если НЕ мРассчитыватьШиринуКолонкиПоНазванию И ТекущаяСтрока < НачалоДанных И ШиринаТекстаЯчейки > 0 Тогда
					
					ШиринаТекстаЯчейки = мМинимальнаяШиринаКолонкиПоказатель;
					
				КонецЕсли;
				
				// Если используется автоотступ, то прибавим к ширине ячейки его величину
				Если АвтоОтступ <> Неопределено И АвтоОтступ > 0 Тогда
					
					ШиринаТекстаЯчейки = ШиринаТекстаЯчейки + КоличествоУровнейГруппировокСтрок * АвтоОтступ;
					
				КонецЕсли;
				
				ШиринаКолонки = Макс(ШиринаКолонки, ШиринаТекстаЯчейки);

			КонецЦикла;

			Если ШиринаКолонки > МаксимальнаяШиринаКолонки Тогда
				
				// Ограничим ширину колонки
				ШиринаКолонки = МаксимальнаяШиринаКолонки;
				
			КонецЕсли;
			
			Если ШиринаКолонки <> 0 Тогда
			// Ширина колонки рассчитана
				
				// Определим, сколько ячеек по ширине используется в области для текущей ячейки
				КоличествоКолонок = ОбластьЯчейки.Право - ОбластьЯчейки.Лево;
				
				// Переберем все ячейки, расположенные в области
				Для НомерКолонки = 0 По КоличествоКолонок Цикл
					
					Если ШириныКолонок.ВГраница() >= ТекущаяКолонка - 1 + НомерКолонки Тогда
						
						// В массиве ширин колонок уже был элемент для текущей колонки
						Если ШириныКолонок[ТекущаяКолонка - 1 + НомерКолонки] = Неопределено Тогда
							
							// Значение ширины колонки еще не было установлено
							ШириныКолонок[ТекущаяКолонка - 1 + НомерКолонки] = ШиринаКолонки / (КоличествоКолонок + 1);
							
						Иначе
							
							// Значение ширины колонки уже было установлено
							// Вычислим максимум ширины колонки
							ШириныКолонок[ТекущаяКолонка - 1 + НомерКолонки] = Макс(ШириныКолонок[ТекущаяКолонка - 1 + НомерКолонки], ШиринаКолонки / (КоличествоКолонок + 1));
							
						КонецЕсли;
						
					Иначе
						
						// В массиве ширин колонок еще не было элемента для данной колонки
						// Добавим элемент в массив ширин колонок
						ШириныКолонок.Вставить(ТекущаяКолонка - 1 + НомерКолонки, ШиринаКолонки / (КоличествоКолонок + 1));
						
					КонецЕсли;
					
				КонецЦикла;
				
			КонецЕсли;
			
		КонецЦикла;	// Конец цикла перебора строк
		
	КонецЦикла;	// Конец цикла перебора колонок
	
	// Переберем все элементы в массиве вычисленных ширин колонок
	Для ТекущаяКолонка = 0 По ШириныКолонок.ВГраница() Цикл
		
		Если ШириныКолонок[ТекущаяКолонка] <> Неопределено Тогда
			
			// Ширина колонок установлена
			// Установим ширину области ячеек
			Если ТабличныйДокумент.Область(, ТекущаяКолонка + 1, НачалоДанных, ТекущаяКолонка + 1).ШиринаКолонки < ШириныКолонок[ТекущаяКолонка] + 1 Тогда
				
				ТабличныйДокумент.Область(, ТекущаяКолонка + 1, НачалоДанных, ТекущаяКолонка + 1).ШиринаКолонки = ШириныКолонок[ТекущаяКолонка] + 1;
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;

КонецПроцедуры // РасчетШириныКолонок()

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ 

Процедура УстановитьРазмещениеВыбранногоПоля(Имя, Значение) Экспорт
	
	Если ПустаяСтрока(Имя) Тогда
		
		Возврат;
		
	ИначеЕсли НЕ мРазмещениеПоложениеВыбранныхПолей.Свойство(Имя) Тогда
		
		мРазмещениеПоложениеВыбранныхПолей.Вставить(Имя, Новый Структура("Размещение", Значение));
		
	ИначеЕсли НЕ мРазмещениеПоложениеВыбранныхПолей[Имя].Свойство("Размещение") Тогда
		
		мРазмещениеПоложениеВыбранныхПолей[Имя].Вставить("Размещение", Значение);
		
	Иначе
		
		мРазмещениеПоложениеВыбранныхПолей[Имя].Размещение = Значение;
		
	КонецЕсли;
	
КонецПроцедуры // УстановитьРазмещениеВыбранногоПоля()

Процедура УстановитьПоложениеВыбранногоПоля(Имя, Значение) Экспорт
	
	Если ПустаяСтрока(Имя) Тогда
		
		Возврат;
		
	ИначеЕсли НЕ мРазмещениеПоложениеВыбранныхПолей.Свойство(Имя) Тогда
		
		мРазмещениеПоложениеВыбранныхПолей.Вставить(Имя, Новый Структура("Положение", Значение));
		
	ИначеЕсли НЕ мРазмещениеПоложениеВыбранныхПолей[Имя].Свойство("Положение") Тогда
		
		мРазмещениеПоложениеВыбранныхПолей[Имя].Вставить("Положение", Значение);
		
	Иначе
		
		мРазмещениеПоложениеВыбранныхПолей[Имя].Положение = Значение;
		
	КонецЕсли;
	
КонецПроцедуры // УстановитьПоложениеВыбранногоПоля()

Функция ПолучитьРазмещениеВыбранногоПоля(Имя) Экспорт
	
	РазмещениеПоУмолчанию = ТипРазмещенияРеквизитовИзмерений.ВместеСИзмерениями;
	
	Если ПустаяСтрока(Имя) Тогда
		
		Возврат РазмещениеПоУмолчанию;
		
	Иначе
		
		СтруктураНастройки = Неопределено;
		
		Если НЕ мРазмещениеПоложениеВыбранныхПолей.Свойство(Имя, СтруктураНастройки) Тогда
			
			Возврат РазмещениеПоУмолчанию;
			
		ИначеЕсли ТипЗнч(СтруктураНастройки) = Тип("Структура") Тогда
			
			Размещение = Неопределено;
			
			Если НЕ СтруктураНастройки.Свойство("Размещение", Размещение) Тогда
				
				Возврат РазмещениеПоУмолчанию;
				
			Иначе
				
				Возврат Размещение;
				
			КонецЕсли;
			
		Иначе	
			
			Возврат РазмещениеПоУмолчанию;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецФункции // ПолучитьРазмещениеВыбранногоПоля()

Функция ПолучитьПоложениеВыбранногоПоля(Имя) Экспорт
	
	ПоложениеПоУмолчанию = НСтр("ru='После группировки'");
	
	Если ПустаяСтрока(Имя) Тогда
		
		Возврат ПоложениеПоУмолчанию;
		
	Иначе
		
		СтруктураНастройки = Неопределено;
		
		Если НЕ мРазмещениеПоложениеВыбранныхПолей.Свойство(Имя, СтруктураНастройки) Тогда
			
			Возврат ПоложениеПоУмолчанию;
			
		ИначеЕсли ТипЗнч(СтруктураНастройки) = Тип("Структура") Тогда
			
			Положение = Неопределено;
			
			Если НЕ СтруктураНастройки.Свойство("Положение", Положение) Тогда
				
				Возврат ПоложениеПоУмолчанию;
				
			Иначе
				
				Возврат Положение;
				
			КонецЕсли;
			
		Иначе	
			
			Возврат ПоложениеПоУмолчанию;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецФункции // ПолучитьПоложениеВыбранногоПоля()

Процедура СкопироватьРазмещениеПоложениеВыбранныхПолей(Приемник, Источник)
	
	Если ТипЗнч(Источник) = Тип("Структура") Тогда
		
		Если ТипЗнч(Приемник) = Тип("Структура") Тогда
			
			Приемник.Очистить();
			
		Иначе
			
			Приемник = Новый Структура;
			
		КонецЕсли;
		
		Для каждого ЭлементИсточника Из Источник Цикл
			
			Приемник.Вставить(ЭлементИсточника.Ключ, Новый Структура("Размещение, Положение", ЭлементИсточника.Значение.Размещение, ЭлементИсточника.Значение.Положение));
			
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры // СкопироватьРазмещениеПоложениеВыбранныхПолей()

Процедура ВосстановитьДеревоПоказателей(Приемник, Источник)
	
	Если ТипЗнч(Источник) = Тип("ДеревоЗначений") Тогда
		
		Если ТипЗнч(Приемник) <> Тип("ДеревоЗначений") ИЛИ Приемник.Строки.Количество() = 0 Тогда
			
			Приемник = Источник.Скопировать();
			
		Иначе
			
			Для каждого ТекущийПоказатель из Источник.Строки Цикл
								
				// Группа показателей
				Если ТекущийПоказатель.Строки.Количество() > 0 Тогда
					
					НайденнаяГруппаПоказателей = Приемник.Строки.Найти(ТекущийПоказатель.Имя, "Имя", Ложь);
					
					// В приемнике найдена группа показателей с требуемым именем
					Если НайденнаяГруппаПоказателей <> Неопределено Тогда
						
						НайденнаяГруппаПоказателей.Использование = ТекущийПоказатель.Использование;
						Приемник.Строки.Сдвинуть(НайденнаяГруппаПоказателей, Источник.Строки.Индекс(ТекущийПоказатель) - Приемник.Строки.Индекс(НайденнаяГруппаПоказателей));
						
						Для каждого Показатель из ТекущийПоказатель.Строки Цикл
							
							НайденныйПоказатеmель = НайденнаяГруппаПоказателей.Строки.Найти(Показатель.Имя, "Имя", Ложь);
							
							// В группе показателей приемнике найден показатель с требуемым именем
							Если НайденныйПоказатеmель <> Неопределено Тогда
								
								НайденныйПоказатеmель.Использование = Показатель.Использование;
								НайденнаяГруппаПоказателей.Строки.Сдвинуть(НайденныйПоказатеmель, ТекущийПоказатель.Строки.Индекс(Показатель) - НайденнаяГруппаПоказателей.Строки.Индекс(НайденныйПоказатеmель));
								
							КонецЕсли;
											
						КонецЦикла;
						
					КонецЕсли;
									
				Иначе // Показатель
					
					НайденныйПоказатеmель = Приемник.Строки.Найти(ТекущийПоказатель.Имя, "Имя", Ложь);
					
					// В приемнике найден показатель с требуемым именем
					Если НайденныйПоказатеmель <> Неопределено Тогда
						
						НайденныйПоказатеmель.Использование = ТекущийПоказатель.Использование;
						Приемник.Строки.Сдвинуть(НайденныйПоказатеmель, Источник.Строки.Индекс(ТекущийПоказатель) - Приемник.Строки.Индекс(НайденныйПоказатеmель));
						
					КонецЕсли;
									
				КонецЕсли;
								
			КонецЦикла;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры // ВосстановитьДеревоПоказателей()

Процедура УстановитьПометки(ТекущаяСтрока) Экспорт
	
	УстановитьПометкиПодчиненных(ТекущаяСтрока);
	УстановитьПометкиРодителей(ТекущаяСтрока);
	
КонецПроцедуры // УстановитьПометки()

Процедура УстановитьПометкиПодчиненных(СтрокаРодитель) Экспорт

	Использование = СтрокаРодитель.Использование;
	Подчиненные = СтрокаРодитель.Строки;

	Если Подчиненные.Количество() > 0 Тогда
		
		Для каждого ТекущаяСтрока из Подчиненные Цикл
			
			ТекущаяСтрока.Использование = Использование;
			УстановитьПометкиПодчиненных(ТекущаяСтрока);
			
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры // УстановитьПометкиПодчиненных()

Процедура УстановитьПометкиРодителей(Родитель) Экспорт

	Если Родитель = Неопределено Тогда
		
		Возврат;
		
	КонецЕсли;
	
	Использование = Родитель.Использование;
	
	Если Родитель.Строки.Количество() Тогда

		НайденыВключенные  = Ложь;
		НайденыВыключенные = Ложь;

		Для каждого Строка из Родитель.Строки Цикл
			
			Если Строка.Использование = 0 Тогда
				
				НайденыВыключенные = Истина;
				
			ИначеЕсли Строка.Использование = 1 Тогда
				
				НайденыВключенные  = Истина;
				
			КонецЕсли;
			
			Если НайденыВключенные И НайденыВыключенные Тогда
				
				Прервать;
				
			КонецЕсли;
			
		КонецЦикла;
		
		Если НайденыВключенные И НайденыВыключенные Тогда
			
			Использование = 2;
			
		ИначеЕсли НайденыВключенные И (Не НайденыВыключенные) Тогда
			
			Использование = 1;
			
		ИначеЕсли (Не НайденыВключенные) И НайденыВыключенные Тогда
			
			Использование = 0;
			
		ИначеЕсли (Не НайденыВключенные) И (Не НайденыВыключенные) Тогда
			
			Использование = 2;
			
		КонецЕсли;

	КонецЕсли; 

	Родитель.Использование = Использование;
	УстановитьПометкиРодителей(Родитель.Родитель);
	
КонецПроцедуры // УстановитьПометкиРодителей()

Процедура ДобавитьСвойстваИКатегорииДляПоля(ПутьКДанным, ИмяИзмерения, Представление, Назначение, НетКатегорий = Ложь, ТаблицаПолей = Неопределено) Экспорт
	
	Если ТаблицаПолей = Неопределено Тогда
		
		ТаблицаПолей = мТаблицаПолей;
		
	КонецЕсли;
	
	СтрокаТаблицыПолей = ТаблицаПолей.Добавить();
		
	СтрокаТаблицыПолей.ПутьКДанным = ПутьКДанным;
	СтрокаТаблицыПолей.ИмяИзмерения = ИмяИзмерения;
	СтрокаТаблицыПолей.Представление = Представление;
	СтрокаТаблицыПолей.Назначение = Назначение;
	СтрокаТаблицыПолей.НетКатегорий = НетКатегорий;
	
КонецПроцедуры // ДобавитьСвойстваИКатегорииДляПоля()

Процедура ДобавитьКонтактнуюИнформацияДляПоля(ПутьКДанным, ИмяИзмерения, Представление, ВидОбъекта, ТипОбъекта, ТаблицаПолей = Неопределено) Экспорт
	
	Если ТаблицаПолей = Неопределено Тогда
		
		ТаблицаПолей = мТаблицаПолейКонтактнаяИнформация;
		
	КонецЕсли;
	
	СтрокаТаблицыПолей = ТаблицаПолей.Добавить();
		
	СтрокаТаблицыПолей.ПутьКДанным = ПутьКДанным;
	СтрокаТаблицыПолей.ИмяИзмерения = ИмяИзмерения;
	СтрокаТаблицыПолей.Представление = Представление;
	СтрокаТаблицыПолей.ВидОбъекта = ВидОбъекта;
	СтрокаТаблицыПолей.ТипОбъекта = ТипОбъекта;
	
КонецПроцедуры // ДобавитьКонтактнуюИнформацияДляПоля()

Процедура ДобавитьДанныеОФизлицеДляПоля(ПутьКДанным, ИмяИзмерения, Представление, ТаблицаПолей = Неопределено) Экспорт
	
	Если ТаблицаПолей = Неопределено Тогда
		
		ТаблицаПолей = мТаблицаПолейДанныеОФизлице;
		
	КонецЕсли;
	
	СтрокаТаблицыПолей = ТаблицаПолей.Добавить();
		
	СтрокаТаблицыПолей.ПутьКДанным = ПутьКДанным;
	СтрокаТаблицыПолей.ИмяИзмерения = ИмяИзмерения;
	СтрокаТаблицыПолей.Представление = Представление;
	
КонецПроцедуры // ДобавитьКонтактнуюИнформацияДляПоля()

Процедура ОчиститьНастройкиПостроителя(ИзмеренияСтроки = Истина, ИзмеренияКолонки = Истина, ВыбранныеПоля = Истина, Порядок = Истина) Экспорт
	
	Если ИзмеренияСтроки Тогда
		
		ПостроительОтчета.ИзмеренияСтроки.Очистить();
		
	КонецЕсли;
	
	Если ИзмеренияКолонки Тогда
		
		ПостроительОтчета.ИзмеренияКолонки.Очистить();
		
	КонецЕсли;
	
	Если ВыбранныеПоля Тогда
		
		ПостроительОтчета.ВыбранныеПоля.Очистить();
		
	КонецЕсли;
	
	Если Порядок Тогда
		
		ПостроительОтчета.Порядок.Очистить();
		
	КонецЕсли;
	
КонецПроцедуры // ОчиститьНастройкиПостроителя()

Функция ДобавитьИзмерениеСтроки(ПутьКДанным, Знач ТипИзмерения = Неопределено) Экспорт
	
	ТипИзмерения = ?(ТипИзмерения = Неопределено, ТипИзмеренияПостроителяОтчета.Элементы, ТипИзмерения);
	
	Попытка
		
		ПостроительОтчета.ИзмеренияСтроки.Добавить(ПутьКДанным,, ТипИзмерения);
		
	Исключение
		
		Возврат Ложь;
		
	КонецПопытки;
	
	Возврат Истина;
	
КонецФункции // ДобавитьИзмерениеСтроки()

Функция ДобавитьИзмерениеКолонки(ПутьКДанным, Знач ТипИзмерения = Неопределено) Экспорт
	
	ТипИзмерения = ?(ТипИзмерения = Неопределено, ТипИзмеренияПостроителяОтчета.Элементы, ТипИзмерения);
	
	Попытка
	
		ПостроительОтчета.ИзмеренияКолонки.Добавить(ПутьКДанным,, ТипИзмерения);
		
	Исключение
		
		Возврат Ложь;
		
	КонецПопытки;
	
	Возврат Истина;
	
КонецФункции // ДобавитьИзмерениеКолонки()

Функция ДобавитьОтбор(ПутьКДанным, Использование = Неопределено, ВидСравнения = Неопределено, Значение = Неопределено, ЗначениеС = Неопределено, ЗначениеПо = Неопределено, ИспользоватьВБыстрыхОтборах = Истина) Экспорт
	
	ДобавляемыйЭлементОтбора = Неопределено;
	
	Для каждого ЭлементОтбора из ПостроительОтчета.Отбор Цикл
		
		Если ЭлементОтбора.ПутьКДанным = ПутьКДанным Тогда
			
			ДобавляемыйЭлементОтбора = ЭлементОтбора;
			Прервать;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Если ДобавляемыйЭлементОтбора = Неопределено Тогда
		
		Попытка
			
			ДобавляемыйЭлементОтбора = ПостроительОтчета.Отбор.Добавить(ПутьКДанным);
			
		Исключение
			
			Возврат Ложь;
			
		КонецПопытки;
		
	КонецЕсли;
	
	Если ИспользоватьВБыстрыхОтборах Тогда
				
		Если мМассивБыстрыеОтборы = Неопределено Тогда
					
			мМассивБыстрыеОтборы = Новый Массив;
					
		КонецЕсли;
				
		Если мМассивБыстрыеОтборы.Найти(ДобавляемыйЭлементОтбора) = Неопределено Тогда
			
			мМассивБыстрыеОтборы.Добавить(ДобавляемыйЭлементОтбора);
			
		КонецЕсли;
				
	КонецЕсли;
	
	Если Использование <> Неопределено Тогда
		
		ДобавляемыйЭлементОтбора.Использование = Использование;
		
	КонецЕсли;
	
	Если ВидСравнения <> Неопределено Тогда
		
		ДобавляемыйЭлементОтбора.ВидСравнения = ВидСравнения;
		
	КонецЕсли;
	
	Если Значение <> Неопределено Тогда
		
		ДобавляемыйЭлементОтбора.Значение = Значение;
		
	КонецЕсли;
	
	Если ЗначениеС <> Неопределено Тогда
		
		ДобавляемыйЭлементОтбора.ЗначениеС = ЗначениеС;
		
	КонецЕсли;
	
	Если ЗначениеПо <> Неопределено Тогда
		
		ДобавляемыйЭлементОтбора.ЗначениеПо = ЗначениеПо;
		
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции // ДобавитьОтбор()

Функция ДобавитьДополнительноеПоле(ПутьКДанным, Размещение = Неопределено, Положение = 3) Экспорт
	
	Попытка
	
		ВыбранноеПоле = ПостроительОтчета.ВыбранныеПоля.Добавить(ПутьКДанным);
		
	Исключение
		
		Возврат Ложь;
		
	КонецПопытки;
	
	Если Размещение = Неопределено Тогда
		
		УстановитьРазмещениеВыбранногоПоля(ВыбранноеПоле.Имя, ТипРазмещенияРеквизитовИзмерений.ВместеСИзмерениями);
		
	Иначе
		
		УстановитьРазмещениеВыбранногоПоля(ВыбранноеПоле.Имя, Размещение);
		
	КонецЕсли;
	
	Если Положение = 1 Тогда
		
		УстановитьПоложениеВыбранногоПоля(ВыбранноеПоле.Имя, НСтр("ru='Перед группировкой'"));
		
	ИначеЕсли Положение = 2 Тогда
		
		УстановитьПоложениеВыбранногоПоля(ВыбранноеПоле.Имя, НСтр("ru='Вместо группировки'"));
		
	ИначеЕсли Положение = 3 Тогда
		
		УстановитьПоложениеВыбранногоПоля(ВыбранноеПоле.Имя, НСтр("ru='После группировки'"));
		
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции // ДобавитьДополнительноеПоле()

Функция ДобавитьПорядок(ПутьКДанным, Знач Направление = Неопределено) Экспорт
	
	Направление = ?(Направление = Неопределено, НаправлениеСортировки.Возр, Направление);
	
	Попытка
	
		ПостроительОтчета.Порядок.Добавить(ПутьКДанным,,, Направление);
		
	Исключение
		
		Возврат Ложь;
		
	КонецПопытки;
	
	Возврат Истина;
	
КонецФункции // ДобавитьПорядок()

Процедура ДобавитьПоказатель(ИмяПоля, ПредставлениеПоля = Неопределено, ВключенПоУмолчанию = Неопределено, ФорматнаяСтрока = Неопределено, ИмяГруппы = Неопределено, ПредставлениеГруппы = Неопределено, Ширина = 0) Экспорт
	
	Если ИмяГруппы <> Неопределено Тогда
		
		Группа = Показатели.Строки.Найти(ИмяГруппы, "Имя", Ложь);
		
		Если Группа = Неопределено Тогда
			
			Группа = Показатели.Строки.Добавить();
			Группа.Имя = ИмяГруппы;
			Группа.Использование = Ложь;
			
		КонецЕсли;
			
		Если ПредставлениеГруппы <> Неопределено Тогда
			
			Группа.Представление = СтрЗаменить(ПредставлениеГруппы, Символы.ПС, " ");
			Группа.ПредставлениеВОтчете = ПредставлениеГруппы;
			
		КонецЕсли;
		
		Показатель = Группа.Строки.Найти(ИмяПоля, "Имя");
		
		Если Показатель = Неопределено Тогда
			
			Показатель = Группа.Строки.Добавить();
			Показатель.Имя = ИмяПоля;
			
		КонецЕсли;
			
		Если ПредставлениеПоля <> Неопределено Тогда
			
			Показатель.Представление = СтрЗаменить(ПредставлениеПоля, Символы.ПС, " ");
			Показатель.ПредставлениеВОтчете = ПредставлениеПоля;
			
		КонецЕсли;
		
		Показатель.Ширина = Ширина;
		
		Если ВключенПоУмолчанию <> Неопределено Тогда
			
			Показатель.Использование = ВключенПоУмолчанию;
			
		КонецЕсли;
		
		Если ФорматнаяСтрока <> Неопределено Тогда
			
			Показатель.ФорматнаяСтрока = ФорматнаяСтрока;
			
		КонецЕсли;
		
	Иначе
		
		Показатель = Показатели.Строки.Найти(ИмяПоля, "Имя");
		
		Если Показатель = Неопределено Тогда
			
			Показатель = Показатели.Строки.Добавить();
			Показатель.Имя = ИмяПоля;
			
		КонецЕсли;
			
		Если ПредставлениеПоля <> Неопределено Тогда
			
			Показатель.Представление = СтрЗаменить(ПредставлениеПоля, Символы.ПС, " ");
			Показатель.ПредставлениеВОтчете = ПредставлениеПоля;
			
		КонецЕсли;
		
		Показатель.Ширина = Ширина;
		
		Если ВключенПоУмолчанию <> Неопределено Тогда
			
			Показатель.Использование = ВключенПоУмолчанию;
			
		КонецЕсли;
		
		Если ФорматнаяСтрока <> Неопределено Тогда
			
			Показатель.ФорматнаяСтрока = ФорматнаяСтрока;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры // ДобавитьПоказатель()

Процедура ДобавитьПолеГруппировка(Имя, Родитель, ПутьКДанным, Представление, Тип = Неопределено, Выражение = Неопределено) Экспорт
	
	Если мМассивПоляГруппировка = Неопределено Тогда
		
		мМассивПоляГруппировка = Новый Массив;
		
	КонецЕсли;
	
	мМассивПоляГруппировка.Добавить(Новый Структура("Имя, Родитель, ПутьКДанным, Представление, Тип, Выражение", Имя, Родитель, ПутьКДанным, Представление, Тип, Выражение));
	
КонецПроцедуры // ДобавитьДоступноеПоле()

Процедура ДобавитьПолеРесурс(Имя, ПутьКДанным, Представление, Итог = Неопределено) Экспорт
	
	Если мМассивПоляРесурс = Неопределено Тогда
		
		мМассивПоляРесурс = Новый Массив;
		
	КонецЕсли;
	
	мМассивПоляРесурс.Добавить(Новый Структура("Имя, ПутьКДанным, Представление, Итог", Имя, ПутьКДанным, Представление, Итог));
	
КонецПроцедуры // ДобавитьПолеРесурс()

Процедура УстановитьСвязьПолей(ПолеПутьКДанным, ПолеРодительПутьКДанным) Экспорт
	
	мСоответствиеСвязьПолей.Вставить(ПолеПутьКДанным, ПолеРодительПутьКДанным);
	
КонецПроцедуры // УстановитьСвязьПолей()

Процедура УстановитьСвязьПоляИИзмерения(ИмяПоля, ИмяИзмерения) Экспорт
	
	Если мСоответствиеПолейИИзмерений[ИмяПоля] = Неопределено Тогда
		
		мСоответствиеПолейИИзмерений.Вставить(ИмяПоля, Новый Структура);
		
	КонецЕсли;
	
	мСоответствиеПолейИИзмерений[ИмяПоля].Вставить(ИмяИзмерения);
		
КонецПроцедуры // УстановитьСвязьПоляИИзмерения()

Функция ПолеНачалоВыбора(Расшифровка = Неопределено, НачальноеЗначениеВыбора = Неопределено, Измерение = Ложь, Отбор = Ложь, Поле = Ложь, Порядок = Ложь, ДочерниеПоля = Истина) Экспорт
	
	ФормаВыборПоля = ПолучитьФорму("ФормаВыборПоля");
	ФормаВыборПоля.Расшифровка = Расшифровка;
	ФормаВыборПоля.НачальноеЗначениеВыбора = НачальноеЗначениеВыбора;
	
	ФормаВыборПоля.Измерение = Измерение;
	ФормаВыборПоля.Отбор = Отбор;
	ФормаВыборПоля.Поле = Поле;
	ФормаВыборПоля.Порядок = Порядок;
	ФормаВыборПоля.ДочерниеПоля = ДочерниеПоля;
	
	Возврат ФормаВыборПоля.ОткрытьМодально();
	
КонецФункции // ПолеНачалоВыбора()

Процедура ПолеОбработкаВыбора(Элемент, Выбор, ЭлементФормыТабличноеПоле, СтандартнаяОбработка) Экспорт
	
	СтандартнаяОбработка = Ложь;
	
	Если Выбор = Неопределено ИЛИ Выбор.Количество() = 0 Тогда
		
		ЭлементФормыТабличноеПоле.ЗакончитьРедактированиеСтроки(Истина);
		Возврат;
		
	КонецЕсли;
	
	Если Выбор.Количество() = 1 И ТипЗнч(Выбор[0].ПутьКДанным) <> Тип("Массив") Тогда
		
		Элемент.Значение = Выбор[0].ПутьКДанным;
		
	Иначе
		
		Для каждого ВыбранноеЗначение из Выбор Цикл
			
			Если ТипЗнч(ВыбранноеЗначение.ПутьКДанным) = Тип("Массив") Тогда
				
				Для каждого ПутьКДанным из ВыбранноеЗначение.ПутьКДанным Цикл
					
					Если ЭлементФормыТабличноеПоле.Данные = "ПостроительОтчета.ИзмеренияСтроки" Тогда
						
						ДобавитьИзмерениеСтроки(ПутьКДанным);
						
					ИначеЕсли ЭлементФормыТабличноеПоле.Данные = "ПостроительОтчета.ИзмеренияКолонки" Тогда
						
						ДобавитьИзмерениеКолонки(ПутьКДанным);
						
					ИначеЕсли ЭлементФормыТабличноеПоле.Данные = "ПостроительОтчета.Отбор" Тогда
						
						ДобавитьОтбор(ПутьКДанным,,,,,, Ложь);
						
					ИначеЕсли ЭлементФормыТабличноеПоле.Данные = "ПостроительОтчета.ВыбранныеПоля" Тогда
						
						ДобавитьДополнительноеПоле(ПутьКДанным);
						
					ИначеЕсли ЭлементФормыТабличноеПоле.Данные = "ПостроительОтчета.Порядок" Тогда
						
						ДобавитьПорядок(ПутьКДанным);
					
					КонецЕсли;
					
				КонецЦикла;
				
			Иначе
			
				Если ЭлементФормыТабличноеПоле.Данные = "ПостроительОтчета.ИзмеренияСтроки" Тогда
					
					ДобавитьИзмерениеСтроки(ВыбранноеЗначение.ПутьКДанным);
					
				ИначеЕсли ЭлементФормыТабличноеПоле.Данные = "ПостроительОтчета.ИзмеренияКолонки" Тогда
					
					ДобавитьИзмерениеКолонки(ВыбранноеЗначение.ПутьКДанным);
					
				ИначеЕсли ЭлементФормыТабличноеПоле.Данные = "ПостроительОтчета.Отбор" Тогда
						
					ДобавитьОтбор(ВыбранноеЗначение.ПутьКДанным,,,,,, Ложь);
					
				ИначеЕсли ЭлементФормыТабличноеПоле.Данные = "ПостроительОтчета.ВыбранныеПоля" Тогда
					
					ДобавитьДополнительноеПоле(ВыбранноеЗначение.ПутьКДанным);
					
				ИначеЕсли ЭлементФормыТабличноеПоле.Данные = "ПостроительОтчета.Порядок" Тогда
					
					ДобавитьПорядок(ВыбранноеЗначение.ПутьКДанным);
					
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры // ПолеОбработкаВыбора()

Процедура ОбработкаРасшифровкиУниверсальногоОтчета(Расшифровка, Отчет, ДополнительныеПараметры = Неопределено) Экспорт
	
	Если ТипЗнч(Расшифровка) = Тип("Структура") Тогда
		
		Выбор = ПолеНачалоВыбора(Расшифровка,, Истина);
		
		Если Выбор = Неопределено ИЛИ Выбор.Количество() = 0 Тогда
			
			Возврат;
			
		КонецЕсли;
		
		ВыполнятьРасшифровку = Ложь;
		
		Для каждого ВыбранноеЗначение из Выбор Цикл
			
			Если ЗначениеЗаполнено(ВыбранноеЗначение.Значение) Тогда
				
				ОткрытьЗначение(ВыбранноеЗначение.Значение);
				
			ИначеЕсли ЗначениеЗаполнено(ВыбранноеЗначение.ПутьКДанным) Тогда
				
				ВыполнятьРасшифровку = Истина;
				
			КонецЕсли;
			
		КонецЦикла;
		
		Если НЕ ВыполнятьРасшифровку Тогда
			
			Возврат;
			
		КонецЕсли;
		
		Если Строка(Отчет) = "ВнешняяОбработкаОбъект." + Отчет.Метаданные().Имя Тогда
					
			ОтчетРасшифровка = ВнешниеОбработки.Создать(Отчет.ИспользуемоеИмяФайла);
					
		ИначеЕсли Строка(Отчет) = "ВнешнийОтчетОбъект." + Отчет.Метаданные().Имя Тогда
					
			ОтчетРасшифровка = ВнешниеОтчеты.Создать(Отчет.ИспользуемоеИмяФайла);
			
		Иначе
			
			ОтчетРасшифровка = Отчеты[Отчет.Метаданные().Имя].Создать();
			
		КонецЕсли;
				
		ОтчетРасшифровкаУниверсальный = (Метаданные.НайтиПоТипу(ТипЗнч(ЭтотОбъект)) = Отчет.Метаданные());
		
		УниверсальныйОтчетРасшифровка = ?(ОтчетРасшифровкаУниверсальный, ОтчетРасшифровка, ОтчетРасшифровка.УниверсальныйОтчет);
		УниверсальныйОтчетТекущий = ?(ОтчетРасшифровкаУниверсальный, Отчет, Отчет.УниверсальныйОтчет);
		
		УниверсальныйОтчетРасшифровка.Интервалы = УниверсальныйОтчетТекущий.Интервалы.Скопировать();
		УниверсальныйОтчетРасшифровка.ВариантОформления = УниверсальныйОтчетТекущий.ВариантОформления;
		УниверсальныйОтчетРасшифровка.ИмяРегистра = УниверсальныйОтчетТекущий.ИмяРегистра;
		УниверсальныйОтчетРасшифровка.ИспользоватьСвойстваИКатегории = УниверсальныйОтчетТекущий.ИспользоватьСвойстваИКатегории;
		УниверсальныйОтчетРасшифровка.ВыводитьОбщиеИтоги = УниверсальныйОтчетТекущий.ВыводитьОбщиеИтоги;
		УниверсальныйОтчетРасшифровка.ОтрицательноеКрасным = УниверсальныйОтчетТекущий.ОтрицательноеКрасным;
		УниверсальныйОтчетРасшифровка.ПоказыватьЗаголовок = УниверсальныйОтчетТекущий.ПоказыватьЗаголовок;
		УниверсальныйОтчетРасшифровка.НеПоказыватьСправкуФормы = УниверсальныйОтчетТекущий.НеПоказыватьСправкуФормы;
		УниверсальныйОтчетРасшифровка.БыстрыйОтборРазвернут = УниверсальныйОтчетТекущий.БыстрыйОтборРазвернут;
		УниверсальныйОтчетРасшифровка.мРежимФормыНастройкиБезГруппировокКолонок = УниверсальныйОтчетТекущий.мРежимФормыНастройкиБезГруппировокКолонок;
		УниверсальныйОтчетРасшифровка.мВосстанавливатьНастройкиПриОткрытии = Ложь;
		УниверсальныйОтчетРасшифровка.мНастройкаПериода = УниверсальныйОтчетТекущий.мНастройкаПериода;
		УниверсальныйОтчетРасшифровка.ДатаНач = УниверсальныйОтчетТекущий.ДатаНач;
		УниверсальныйОтчетРасшифровка.ДатаКон = УниверсальныйОтчетТекущий.ДатаКон;
		
		Если ОтчетРасшифровкаУниверсальный Тогда
			
			ОтчетРасшифровка.УстановитьНачальныеНастройки(Истина);
			
		Иначе
			
			ОтчетРасшифровка.УстановитьНачальныеНастройки(ДополнительныеПараметры);
			
		КонецЕсли;
		
		СкопироватьРазмещениеПоложениеВыбранныхПолей(УниверсальныйОтчетРасшифровка.мРазмещениеПоложениеВыбранныхПолей, УниверсальныйОтчетТекущий.мРазмещениеПоложениеВыбранныхПолей);
		
		УниверсальныйОтчетРасшифровка.Показатели = УниверсальныйОтчетТекущий.Показатели.Скопировать();
		
		УниверсальныйОтчетРасшифровка.ОчиститьНастройкиПостроителя();
		
		Для каждого ИзмерениеСтроки из УниверсальныйОтчетТекущий.ПостроительОтчета.ИзмеренияСтроки Цикл
			
			УниверсальныйОтчетРасшифровка.ДобавитьИзмерениеСтроки(ИзмерениеСтроки.ПутьКДанным);
			
		КонецЦикла;
		
		Для каждого ИзмерениеКолонки из УниверсальныйОтчетТекущий.ПостроительОтчета.ИзмеренияКолонки Цикл
			
			УниверсальныйОтчетРасшифровка.ДобавитьИзмерениеКолонки(ИзмерениеКолонки.ПутьКДанным);
			
		КонецЦикла;
		
		Для каждого ВыбранноеПоле из УниверсальныйОтчетТекущий.ПостроительОтчета.ВыбранныеПоля Цикл
			
			УниверсальныйОтчетРасшифровка.ДобавитьДополнительноеПоле(ВыбранноеПоле.ПутьКДанным, ПолучитьРазмещениеВыбранногоПоля(ВыбранноеПоле.Имя), ПолучитьПоложениеВыбранногоПоля(ВыбранноеПоле.Имя));
			
		КонецЦикла;
		
		Для каждого ЭлементПорядка из УниверсальныйОтчетТекущий.ПостроительОтчета.Порядок Цикл
			
			УниверсальныйОтчетРасшифровка.ДобавитьПорядок(ЭлементПорядка.ПутьКДанным, ЭлементПорядка.Направление);
			
		КонецЦикла;
		
		Для каждого ВыбранноеЗначение из Выбор Цикл
			
			Если ЗначениеЗаполнено(ВыбранноеЗначение.ПутьКДанным) Тогда
				
				Если ТипЗнч(ВыбранноеЗначение.ПутьКДанным) = Тип("Массив") Тогда
					
					Для каждого ПутьКДанным из ВыбранноеЗначение.ПутьКДанным Цикл
						
						Если ВыбранноеЗначение.ДетальнаяЗапись И ПостроительОтчета.ИзмеренияКолонки.Количество() = 0 Тогда
							
							УниверсальныйОтчетРасшифровка.ВыводитьДетальныеЗаписи = Истина;
							УниверсальныйОтчетРасшифровка.ДобавитьДополнительноеПоле(ПутьКДанным);
							
						Иначе
							
							УниверсальныйОтчетРасшифровка.ДобавитьИзмерениеСтроки(ПутьКДанным);
							
						КонецЕсли;
						
					КонецЦикла;
					
				Иначе
					
					Если ВыбранноеЗначение.ДетальнаяЗапись И ПостроительОтчета.ИзмеренияКолонки.Количество() = 0 Тогда
						
						УниверсальныйОтчетРасшифровка.ВыводитьДетальныеЗаписи = Истина;
						УниверсальныйОтчетРасшифровка.ДобавитьДополнительноеПоле(ВыбранноеЗначение.ПутьКДанным);
						
					Иначе
						
						УниверсальныйОтчетРасшифровка.ДобавитьИзмерениеСтроки(ВыбранноеЗначение.ПутьКДанным);
						
					КонецЕсли;
					
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЦикла;
			
		УниверсальныйОтчетТекущий.ПостроительОтчета.НастроитьРасшифровку(УниверсальныйОтчетРасшифровка.ПостроительОтчета, Расшифровка);
		
		Для каждого ЭлементОтбора из УниверсальныйОтчетТекущий.ПостроительОтчета.Отбор Цикл
			
			Если НЕ ЭлементОтбора.Использование Тогда
			
				УниверсальныйОтчетРасшифровка.ДобавитьОтбор(ЭлементОтбора.ПутьКДанным,,,,,, Ложь);
				
			КонецЕсли;
			
		КонецЦикла;
		
		ФормаОтчетРасшифровка = ОтчетРасшифровка.ПолучитьФорму();
		ФормаОтчетРасшифровка.Открыть();
		ФормаОтчетРасшифровка.ОбновитьОтчет();
			
	Иначе
		
		ОткрытьЗначение(Расшифровка);
		
	КонецЕсли;
	
КонецПроцедуры // ОбработкаРасшифровкиУниверсальногоОтчета()

Процедура УстановитьПериод(ДатаНач, ДатаКон, Периодичность, КоличествоПериодов) Экспорт
	
	Если Периодичность = 2 Тогда
		
		ТекущаяПериодичность = "НЕДЕЛЯ";
		
	ИначеЕсли Периодичность = 3 Тогда
		
		ТекущаяПериодичность = "ДЕКАДА";
		
	ИначеЕсли Периодичность = 4 Тогда
		
		ТекущаяПериодичность = "МЕСЯЦ";
		
	ИначеЕсли Периодичность = 5 Тогда
		
		ТекущаяПериодичность = "КВАРТАЛ";
		
	ИначеЕсли Периодичность = 6 Тогда
		
		ТекущаяПериодичность = "ПОЛУГОДИЕ";
		
	ИначеЕсли Периодичность = 7 Тогда
		
		ТекущаяПериодичность = "ГОД";
		
	Иначе
		
		Возврат;
		
	КонецЕсли;
	
	Если ДатаНач = Дата('00010101') Тогда
		
		ДатаНач = ТекущаяДата();
		
	КонецЕсли;
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	НАЧАЛОПЕРИОДА(ДОБАВИТЬКДАТЕ(&ДатаНач, " + Строка(ТекущаяПериодичность) + "," + КоличествоПериодов + "), " + Строка(ТекущаяПериодичность) + ") КАК ДатаНач,
	|	КОНЕЦПЕРИОДА(ДОБАВИТЬКДАТЕ(&ДатаНач, " + Строка(ТекущаяПериодичность) + "," + КоличествоПериодов + "), " + Строка(ТекущаяПериодичность) + ") КАК ДатаКон");
		
	Запрос.УстановитьПараметр("ДатаНач", ДатаНач);
		
	Выборка = Запрос.Выполнить().Выбрать();
	Выборка.Следующий();
		
	ДатаНач = Выборка.ДатаНач;
	ДатаКон = Выборка.ДатаКон;
	
КонецПроцедуры // УстановитьПериод()

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ СОХРАНЕНИЯ И ВОССТАНОВЛЕНИЯ НАСТРОЕК

Функция ВосстановитьНастройки(Объект, Отчет, Форма, ИмяОбъекта = Неопределено) Экспорт
	
	Перем СохраненнаяНастройка;
	
	СтруктураНастройки = Новый Структура;
	СтруктураНастройки.Вставить("Пользователь", глЗначениеПеременной("глТекущийПользователь"));
	СтруктураНастройки.Вставить("ИмяОбъекта", ?(ИмяОбъекта = Неопределено, Строка(Отчет), ИмяОбъекта));
	СтруктураНастройки.Вставить("НаименованиеНастройки", ?(Объект.мТекущаяНастройка = Неопределено, Неопределено, Объект.мТекущаяНастройка.НаименованиеНастройки));
	
	Результат = УниверсальныеМеханизмы.ВосстановлениеНастроек(СтруктураНастройки);
	
	Если Результат <> Неопределено Тогда
			
		Объект.мТекущаяНастройка = Результат;
		Отчет.ВосстановитьНастройкиИзСтруктуры(Результат.СохраненнаяНастройка);
		ВосстановитьПараметрыПечати(Объект, Отчет, Форма, Результат.СохраненнаяНастройка);
			
	Иначе
		
		Объект.мТекущаяНастройка = СтруктураНастройки;
		
	КонецЕсли;
	
	Объект.УстановитьЗаголовокФормы(Объект, Отчет, Форма);

КонецФункции // ВосстановитьНастройки()

Процедура СохранитьНастройки(Объект, Отчет, Форма, ИмяОбъекта = Неопределено) Экспорт
	
	Перем СохраненнаяНастройка;
	
	Отчет.СформироватьСтруктуруДляСохраненияНастроек(СохраненнаяНастройка);
	СохранитьПараметрыПечати(Объект, Отчет, Форма, СохраненнаяНастройка);
	
	СтруктураНастройки = Новый Структура;
	СтруктураНастройки.Вставить("Пользователь", глЗначениеПеременной("глТекущийПользователь"));
	СтруктураНастройки.Вставить("ИмяОбъекта", ?(ИмяОбъекта = Неопределено, Строка(Отчет), ИмяОбъекта));
	СтруктураНастройки.Вставить("НаименованиеНастройки", ?(Объект.мТекущаяНастройка = Неопределено, Неопределено, Объект.мТекущаяНастройка.НаименованиеНастройки));
	СтруктураНастройки.Вставить("СохраненнаяНастройка", СохраненнаяНастройка);
	СтруктураНастройки.Вставить("ИспользоватьПриОткрытии", Ложь);
	СтруктураНастройки.Вставить("СохранятьАвтоматически", Ложь);
	
	Результат = УниверсальныеМеханизмы.СохранениеНастроек(СтруктураНастройки);
	
	Если Результат <> Неопределено Тогда
			
		Объект.мТекущаяНастройка = Результат;
			
	Иначе
		
		Объект.мТекущаяНастройка = СтруктураНастройки;
		
	КонецЕсли;
	
	Объект.УстановитьЗаголовокФормы(Объект, Отчет, Форма);
	
КонецПроцедуры // СохранитьНастройки()

Процедура СформироватьСтруктуруДляСохраненияНастроек(СтруктураСНастройками) Экспорт
	
	Если ТипЗнч(СтруктураСНастройками) <> Тип("Структура") Тогда
		
		СтруктураСНастройками = Новый Структура;
		
	КонецЕсли;
	
	мНастройкаПериода.ДатаНачала = ДатаНач;
	мНастройкаПериода.ДатаОкончания = ДатаКон;
	
	СтруктураСНастройками.Вставить("НастройкиПостроителя", ПостроительОтчета.ПолучитьНастройки());
	СтруктураСНастройками.Вставить("ИмяРегистра", ИмяРегистра);
	СтруктураСНастройками.Вставить("НастройкаПериода", мНастройкаПериода);
	СтруктураСНастройками.Вставить("ОтрицательноеКрасным", ОтрицательноеКрасным);
	СтруктураСНастройками.Вставить("ВыводитьОбщиеИтоги", ВыводитьОбщиеИтоги);
	СтруктураСНастройками.Вставить("ВыводитьДетальныеЗаписи", ВыводитьДетальныеЗаписи);
	СтруктураСНастройками.Вставить("ИспользоватьСвойстваИКатегории", ИспользоватьСвойстваИКатегории);
	СтруктураСНастройками.Вставить("Показатели", Показатели.Скопировать());
	СтруктураСНастройками.Вставить("ВариантОформления", ВариантОформления);
	СтруктураСНастройками.Вставить("Интервалы", Интервалы.Скопировать());
	СтруктураСНастройками.Вставить("РазмещениеПоложениеВыбранныхПолей");
	СкопироватьРазмещениеПоложениеВыбранныхПолей(СтруктураСНастройками.РазмещениеПоложениеВыбранныхПолей, мРазмещениеПоложениеВыбранныхПолей);
	
КонецПроцедуры // СформироватьСтруктуруДляСохраненияНастроек()

Функция ВосстановитьНастройкиИзСтруктуры(СтруктураСНастройками, Отчет = Неопределено) Экспорт
	
	Перем ДеревоПоказатели, ТаблицаИнтервалы, СохраненнаяНастройкаПериода;
	
	Если ТипЗнч(СтруктураСНастройками) <> Тип("Структура") Тогда
		
		Возврат Ложь;
		
	КонецЕсли;
	
	СтруктураСНастройками.Свойство("ИмяРегистра", ИмяРегистра);
	СтруктураСНастройками.Свойство("НастройкаПериода", СохраненнаяНастройкаПериода);
	
	Если ТипЗнч(СохраненнаяНастройкаПериода) = Тип("НастройкаПериода") Тогда
		
		мНастройкаПериода = СохраненнаяНастройкаПериода;
		
	КонецЕсли;
	
	ДатаНач = мНастройкаПериода.ДатаНачала;
	ДатаКон = мНастройкаПериода.ДатаОкончания;
	
	СтруктураСНастройками.Свойство("ИспользоватьСвойстваИКатегории", ИспользоватьСвойстваИКатегории);
	СтруктураСНастройками.Свойство("Интервалы", ТаблицаИнтервалы);
	
	Если ТипЗнч(ТаблицаИнтервалы) = Тип("ТаблицаЗначений") Тогда
		
		Интервалы = ТаблицаИнтервалы.Скопировать();
		
	КонецЕсли;
	
	Если Отчет = Неопределено Тогда
		
		УстановитьНачальныеНастройки(Истина);
		
	Иначе
		
		Отчет.УстановитьНачальныеНастройки();
		
	КонецЕсли;
	
	Если СтруктураСНастройками.Свойство("РазмещениеПоложениеВыбранныхПолей") Тогда
	
		СкопироватьРазмещениеПоложениеВыбранныхПолей(мРазмещениеПоложениеВыбранныхПолей, СтруктураСНастройками.РазмещениеПоложениеВыбранныхПолей);
		
	КонецЕсли;
	
	СтруктураСНастройками.Свойство("ОтрицательноеКрасным", ОтрицательноеКрасным);
	СтруктураСНастройками.Свойство("ВыводитьОбщиеИтоги", ВыводитьОбщиеИтоги);
	СтруктураСНастройками.Свойство("ВыводитьДетальныеЗаписи", ВыводитьДетальныеЗаписи);
	СтруктураСНастройками.Свойство("ВариантОформления", ВариантОформления);
	
	СтруктураСНастройками.Свойство("Показатели", ДеревоПоказатели);
	ВосстановитьДеревоПоказателей(Показатели, ДеревоПоказатели);
	
	ПостроительОтчета.УстановитьНастройки(СтруктураСНастройками.НастройкиПостроителя);
	
	Возврат Истина;
	
КонецФункции // ВосстановитьНастройкиИзСтруктуры()

Процедура ВосстановитьДополнительныеНастройки(Объект, Отчет, ИмяОбъекта = Неопределено) Экспорт
	
	Объект.БыстрыйОтборРазвернут = ?(ВосстановитьЗначение(СтрЗаменить(?(ИмяОбъекта = Неопределено, Строка(Отчет), ИмяОбъекта), ".", "_") + "_БыстрыйОтборРазвернут") = Истина, Истина, Ложь);
	Объект.ПоказыватьЗаголовок = ?(ВосстановитьЗначение(СтрЗаменить(?(ИмяОбъекта = Неопределено, Строка(Отчет), ИмяОбъекта), ".", "_") + "_ПоказыватьЗаголовок") = Истина, Истина, Ложь);
	Объект.НеПоказыватьСправкуФормы = ?(ВосстановитьЗначение(СтрЗаменить(?(ИмяОбъекта = Неопределено, Строка(Отчет), ИмяОбъекта), ".", "_") + "_НеПоказыватьСправкуФормы") = Истина, Истина, Ложь);
	
КонецПроцедуры // ВосстановитьДополнительныеНастройки()

Процедура СохранитьДополнительныеНастройки(Объект, Отчет, ИмяОбъекта = Неопределено) Экспорт
	
	СохранитьЗначение(СтрЗаменить(?(ИмяОбъекта = Неопределено, Строка(Отчет), ИмяОбъекта), ".", "_") + "_БыстрыйОтборРазвернут", Объект.БыстрыйОтборРазвернут);
	СохранитьЗначение(СтрЗаменить(?(ИмяОбъекта = Неопределено, Строка(Отчет), ИмяОбъекта), ".", "_") + "_ПоказыватьЗаголовок", Объект.ПоказыватьЗаголовок);
	СохранитьЗначение(СтрЗаменить(?(ИмяОбъекта = Неопределено, Строка(Отчет), ИмяОбъекта), ".", "_") + "_НеПоказыватьСправкуФормы", Объект.НеПоказыватьСправкуФормы);
	
КонецПроцедуры // СохранитьДополнительныеНастройки()

Процедура ВосстановитьПараметрыПечати(Объект, Отчет, Форма, СтруктураСНастройками) Экспорт
	
	Перем СохраненныеПараметрыПечати;
	
	СтруктураСНастройками.Свойство("ПараметрыПечати", СохраненныеПараметрыПечати);
	
	Если ТипЗнч(СохраненныеПараметрыПечати) = Тип("Структура") Тогда
		
		ПолеТабличногоДокументаРезультат = Форма.ЭлементыФормы[мИмяЭлементаУправленияПолеТабличногоДокументаРезультат];
	
		// Ориентация
		СохраненныеПараметрыПечати.Свойство("ОриентацияСтраницы", ПолеТабличногоДокументаРезультат.ОриентацияСтраницы);
		
		// Масштаб
		СохраненныеПараметрыПечати.Свойство("МасштабПечати", ПолеТабличногоДокументаРезультат.МасштабПечати);
		СохраненныеПараметрыПечати.Свойство("АвтоМасштаб", ПолеТабличногоДокументаРезультат.АвтоМасштаб);
		
		// Поля
		СохраненныеПараметрыПечати.Свойство("ПолеСлева", ПолеТабличногоДокументаРезультат.ПолеСлева);
		СохраненныеПараметрыПечати.Свойство("ПолеСверху", ПолеТабличногоДокументаРезультат.ПолеСверху);
		СохраненныеПараметрыПечати.Свойство("ПолеСправа", ПолеТабличногоДокументаРезультат.ПолеСправа);
		СохраненныеПараметрыПечати.Свойство("ПолеСнизу", ПолеТабличногоДокументаРезультат.ПолеСнизу);
		
		// Колонтитулы
		СохраненныеПараметрыПечати.Свойство("РазмерКолонтитулаСверху", ПолеТабличногоДокументаРезультат.РазмерКолонтитулаСверху);
		СохраненныеПараметрыПечати.Свойство("РазмерКолонтитулаСнизу", ПолеТабличногоДокументаРезультат.РазмерКолонтитулаСнизу);
		
		// Экземпляров на страницу
		СохраненныеПараметрыПечати.Свойство("ЭкземпляровНаСтранице", ПолеТабличногоДокументаРезультат.ЭкземпляровНаСтранице);
		
		// Черно-белая печать
		СохраненныеПараметрыПечати.Свойство("ЧерноБелаяПечать", ПолеТабличногоДокументаРезультат.ЧерноБелаяПечать);
		
	КонецЕсли;
	
КонецПроцедуры // ВосстановитьПараметрыПечати()

Процедура СохранитьПараметрыПечати(Объект, Отчет, Форма, СтруктураСНастройками) Экспорт
	
	ПараметрыПечати = Новый Структура;
	ПолеТабличногоДокументаРезультат = Форма.ЭлементыФормы[мИмяЭлементаУправленияПолеТабличногоДокументаРезультат];
	
	// Ориентация
	ПараметрыПечати.Вставить("ОриентацияСтраницы", ПолеТабличногоДокументаРезультат.ОриентацияСтраницы);
	
	// Масштаб
	ПараметрыПечати.Вставить("МасштабПечати", ПолеТабличногоДокументаРезультат.МасштабПечати);
	ПараметрыПечати.Вставить("АвтоМасштаб", ПолеТабличногоДокументаРезультат.АвтоМасштаб);
	
	// Поля
	ПараметрыПечати.Вставить("ПолеСлева", ПолеТабличногоДокументаРезультат.ПолеСлева);
	ПараметрыПечати.Вставить("ПолеСверху", ПолеТабличногоДокументаРезультат.ПолеСверху);
	ПараметрыПечати.Вставить("ПолеСправа", ПолеТабличногоДокументаРезультат.ПолеСправа);
	ПараметрыПечати.Вставить("ПолеСнизу", ПолеТабличногоДокументаРезультат.ПолеСнизу);
	
	// Колонтитулы
	ПараметрыПечати.Вставить("РазмерКолонтитулаСверху", ПолеТабличногоДокументаРезультат.РазмерКолонтитулаСверху);
	ПараметрыПечати.Вставить("РазмерКолонтитулаСнизу", ПолеТабличногоДокументаРезультат.РазмерКолонтитулаСнизу);
	
	// Экземпляров на страницу
	ПараметрыПечати.Вставить("ЭкземпляровНаСтранице", ПолеТабличногоДокументаРезультат.ЭкземпляровНаСтранице);
	
	// Черно-белая печать
	ПараметрыПечати.Вставить("ЧерноБелаяПечать", ПолеТабличногоДокументаРезультат.ЧерноБелаяПечать);
	
	СтруктураСНастройками.Вставить("ПараметрыПечати", ПараметрыПечати);
	
КонецПроцедуры // СохранитьПараметрыПечати()

Процедура УправлениеОтображениемЭлементовФормы(Объект, Форма, КнопкаИмя = Неопределено) Экспорт
	
	Если КнопкаИмя = "БыстрыеОтборы" Тогда
		
		БыстрыйОтборРазвернут = НЕ БыстрыйОтборРазвернут;
		
	ИначеЕсли КнопкаИмя = "ЗаголовокОтчета" Тогда
		
		ПоказыватьЗаголовок = НЕ ПоказыватьЗаголовок;
		
	ИначеЕсли КнопкаИмя = "СправкаФормы" ИЛИ КнопкаИмя = "НадписиЗакрытьСправкуФормы" Тогда
		
		НеПоказыватьСправкуФормы = НЕ НеПоказыватьСправкуФормы;
		
	КонецЕсли;

	Форма.ЭлементыФормы.КоманднаяПанельФормы.Кнопки.ЗаголовокОтчета.Пометка = Объект.ПоказыватьЗаголовок;
	Форма.ЭлементыФормы.КоманднаяПанельФормы.Кнопки.Подменю.Кнопки.ЗаголовокОтчета.Пометка = Объект.ПоказыватьЗаголовок;
	
	Форма.ЭлементыФормы.КоманднаяПанельФормы.Кнопки.БыстрыеОтборы.Пометка = Объект.БыстрыйОтборРазвернут;
	Форма.ЭлементыФормы.КоманднаяПанельФормы.Кнопки.Подменю.Кнопки.БыстрыеОтборы.Пометка = Объект.БыстрыйОтборРазвернут;
	
	Если Форма.ЭлементыФормы.КоманднаяПанельФормы.Кнопки.Найти("СправкаФормы") <> Неопределено Тогда
		
		Форма.ЭлементыФормы.КоманднаяПанельФормы.Кнопки.СправкаФормы.Пометка = НЕ Объект.НеПоказыватьСправкуФормы;
		
	КонецЕсли;
	
КонецПроцедуры // УправлениеОтображениемЭлементовФормы()

Процедура ДобавитьПанельБыстрыеОтборы(Объект, Форма) Экспорт
	
	КоличествоБыстрыхОтборов = 0;
	МаксимальноеКоличествоБыстрыхОтборов = 5;
	
	ЭлементУправленияВысота = 19;
	ИнтервалМеждуЭлементамиУправленияВертикальный = 5;
	ИнтервалМеждуЭлементамиУправленияГоризонтальный = 5;
	
	ФлажокИспользованиеШирина = 190;
	ПолеВводаВидСравненияШирина = 180;
	
	ЭлементУправления = Форма.ЭлементыФормы[мИмяЭлементаУправленияБыстрыеОтборы];
	
	Если Форма.ЭлементыФормы.Найти("ПанельБыстрыеОтборы") <> Неопределено Тогда
		
		Если Форма.ЭлементыФормы.Найти("ПолеВводаВидСравнения1") <> Неопределено Тогда
			
			ПолеВводаВидСравненияШирина = Форма.ЭлементыФормы.ПолеВводаВидСравнения1.Ширина;
			
		КонецЕсли;
		
		ЭлементУправления.Верх = Форма.ЭлементыФормы.ПанельБыстрыеОтборы.Верх;
		ЭлементУправления.Высота = ЭлементУправления.Высота + Форма.ЭлементыФормы.ПанельБыстрыеОтборы.Высота + ИнтервалМеждуЭлементамиУправленияВертикальный;
		
		ЭлементУправления.УстановитьПривязку(ГраницаЭлементаУправления.Верх, Форма.Панель, ГраницаЭлементаУправления.Верх);
		
		Форма.ЭлементыФормы.Удалить(Форма.ЭлементыФормы.ПанельБыстрыеОтборы);
		
	КонецЕсли;
	
	КоличествоБыстрыхОтборов = Мин(МаксимальноеКоличествоБыстрыхОтборов, ?(мМассивБыстрыеОтборы = Неопределено, 0, мМассивБыстрыеОтборы.Количество()));
	
	Если КоличествоБыстрыхОтборов = 0 Тогда
		
		ВысотаПанелиБыстрыеОтборы = ЭлементУправленияВысота;
		
	Иначе
		
		ВысотаПанелиБыстрыеОтборы = КоличествоБыстрыхОтборов * ИнтервалМеждуЭлементамиУправленияВертикальный + КоличествоБыстрыхОтборов * ЭлементУправленияВысота;
		
	КонецЕсли;
	
	ПанельВерх = ЭлементУправления.Верх;
	ПанельЛево = ЭлементУправления.Лево;
	ПанельШирина = ЭлементУправления.Ширина;
	ПанельВысота = ВысотаПанелиБыстрыеОтборы;
		
	ПанельБыстрыеОтборы = Форма.ЭлементыФормы.Добавить(Тип("Панель"), "ПанельБыстрыеОтборы");
	ПанельБыстрыеОтборы.ОтображениеЗакладок = ОтображениеЗакладок.НеИспользовать;
	ПанельБыстрыеОтборы.Верх = ПанельВерх;
	ПанельБыстрыеОтборы.Лево = ПанельЛево;
	ПанельБыстрыеОтборы.Ширина = ПанельШирина;
	ПанельБыстрыеОтборы.Высота = ПанельВысота;
	ПанельБыстрыеОтборы.УстановитьПривязку(ГраницаЭлементаУправления.Лево, Форма.Панель, ГраницаЭлементаУправления.Лево);
	ПанельБыстрыеОтборы.УстановитьПривязку(ГраницаЭлементаУправления.Право, Форма.Панель, ГраницаЭлементаУправления.Право);
	
	ЭлементУправления.Высота = ЭлементУправления.Высота - ПанельБыстрыеОтборы.Высота - ИнтервалМеждуЭлементамиУправленияВертикальный;
	ЭлементУправления.Верх = ПанельБыстрыеОтборы.Верх + ПанельБыстрыеОтборы.Высота + ИнтервалМеждуЭлементамиУправленияВертикальный;
	ЭлементУправления.УстановитьПривязку(ГраницаЭлементаУправления.Верх, ПанельБыстрыеОтборы, ГраницаЭлементаУправления.Низ);
	
	Если КоличествоБыстрыхОтборов = 0 Тогда
		
		// Надпись об отсутствии быстрых отборов
		НадписьОтборыОтсутствуют = Форма.ЭлементыФормы.Добавить(Тип("Надпись"), "НадписьОтборыОтсутствуют",, ПанельБыстрыеОтборы);
		НадписьОтборыОтсутствуют.Верх = 0;
		НадписьОтборыОтсутствуют.Высота = ПанельБыстрыеОтборы.Высота;
		НадписьОтборыОтсутствуют.Лево = 0;
		НадписьОтборыОтсутствуют.Ширина = ПанельБыстрыеОтборы.Ширина - 1;
		НадписьОтборыОтсутствуют.Заголовок = НСтр("ru = 'Отчет не содержит быстрых отборов'");
		НадписьОтборыОтсутствуют.ГоризонтальноеПоложение = ГоризонтальноеПоложение.Центр;
		НадписьОтборыОтсутствуют.ВертикальноеПоложение = ВертикальноеПоложение.Центр;
		
		НадписьОтборыОтсутствуют.УстановитьПривязку(ГраницаЭлементаУправления.Лево, ПанельБыстрыеОтборы, ГраницаЭлементаУправления.Лево);
		НадписьОтборыОтсутствуют.УстановитьПривязку(ГраницаЭлементаУправления.Право, ПанельБыстрыеОтборы, ГраницаЭлементаУправления.Право);
	
	Иначе
		
		Индекс = 1;
		
		Для каждого ЭлементОтбора из мМассивБыстрыеОтборы Цикл
			
			Если ПостроительОтчета.Отбор.Найти(ЭлементОтбора.Имя) = Неопределено Тогда
				
				Если НЕ ДобавитьОтбор(ЭлементОтбора.Имя,,,,,, Ложь) Тогда
					
					Продолжить;
					
				КонецЕсли;
				
			КонецЕсли;
				
			// Использование
			ФлажокИспользование = Форма.ЭлементыФормы.Добавить(Тип("Флажок"), "ФлажокИспользование" + Формат(Индекс, "ЧГ=0"),, ПанельБыстрыеОтборы);
			ФлажокИспользование.Верх = (ЭлементУправленияВысота + ИнтервалМеждуЭлементамиУправленияВертикальный) * (Индекс - 1);
			ФлажокИспользование.Высота = ЭлементУправленияВысота;
			ФлажокИспользование.Ширина = ФлажокИспользованиеШирина;
			ФлажокИспользование.Заголовок = ЭлементОтбора.Представление;
			ФлажокИспользование.Данные = мПутьКПостроителюОтчета + ".ПостроительОтчета.Отбор." + ЭлементОтбора.Имя + ".Использование";
			
			// Вид сравнения
			ПолеВводаВидСравнения = Форма.ЭлементыФормы.Добавить(Тип("ПолеВвода"), "ПолеВводаВидСравнения" + Формат(Индекс, "ЧГ=0"),, ПанельБыстрыеОтборы);
			ПолеВводаВидСравнения.Лево = ФлажокИспользование.Лево + ФлажокИспользование.Ширина + ИнтервалМеждуЭлементамиУправленияГоризонтальный;
			ПолеВводаВидСравнения.Верх = (ЭлементУправленияВысота + ИнтервалМеждуЭлементамиУправленияВертикальный) * (Индекс - 1);
			ПолеВводаВидСравнения.Высота = ЭлементУправленияВысота;
			ПолеВводаВидСравнения.Ширина = ПолеВводаВидСравненияШирина;
			ПолеВводаВидСравнения.Данные = мПутьКПостроителюОтчета + ".ПостроительОтчета.Отбор." + ЭлементОтбора.Имя + ".ВидСравнения";
			
			ПолеВводаВидСравнения.УстановитьПривязку(ГраницаЭлементаУправления.Лево, ФлажокИспользование, ГраницаЭлементаУправления.Право);
			ПолеВводаВидСравнения.УстановитьПривязку(ГраницаЭлементаУправления.Право, ПанельБыстрыеОтборы, ГраницаЭлементаУправления.Лево, ПанельБыстрыеОтборы, ГраницаЭлементаУправления.Право);
			
			ПолеВводаВидСравнения.УстановитьДействие("ПриИзменении", ?(мДействиеПолеВводаВидСравненияПриИзменении = Неопределено, Новый Действие("ПолеВводаВидСравненияПриИзменении"), мДействиеПолеВводаВидСравненияПриИзменении));
				
			// Значение
			ПолеВводаЗначение = Форма.ЭлементыФормы.Добавить(Тип("ПолеВвода"), "ПолеВводаЗначение" + Формат(Индекс, "ЧГ=0"),, ПанельБыстрыеОтборы);
			ПолеВводаЗначение.Лево = ПолеВводаВидСравнения.Лево + ПолеВводаВидСравнения.Ширина + ИнтервалМеждуЭлементамиУправленияГоризонтальный;
			ПолеВводаЗначение.Верх = (ЭлементУправленияВысота + ИнтервалМеждуЭлементамиУправленияВертикальный) * (Индекс - 1);
			ПолеВводаЗначение.Высота = ЭлементУправленияВысота;
			ПолеВводаЗначение.Ширина = ПанельШирина - 1 - ПолеВводаЗначение.Лево;
			ПолеВводаЗначение.Данные = мПутьКПостроителюОтчета + ".ПостроительОтчета.Отбор." + ЭлементОтбора.Имя + ".Значение";
			
			ПолеВводаЗначение.УстановитьПривязку(ГраницаЭлементаУправления.Лево, ПолеВводаВидСравнения, ГраницаЭлементаУправления.Право);
			ПолеВводаЗначение.УстановитьПривязку(ГраницаЭлементаУправления.Право, ПанельБыстрыеОтборы, ГраницаЭлементаУправления.Право);
			
			ПолеВводаЗначение.УстановитьДействие("ПриИзменении", ?(мДействиеПолеВводаЗначениеПриИзменении = Неопределено, Новый Действие("ПолеВводаЗначениеПриИзменении"), мДействиеПолеВводаЗначениеПриИзменении));
			
			// Значение с
			ПолеВводаЗначениеС = Форма.ЭлементыФормы.Добавить(Тип("ПолеВвода"), "ПолеВводаЗначениеС" + Формат(Индекс, "ЧГ=0"),, ПанельБыстрыеОтборы);
			ПолеВводаЗначениеС.Лево = ПолеВводаВидСравнения.Лево + ПолеВводаВидСравнения.Ширина + ИнтервалМеждуЭлементамиУправленияГоризонтальный;
			ПолеВводаЗначениеС.Верх = (ЭлементУправленияВысота + ИнтервалМеждуЭлементамиУправленияВертикальный) * (Индекс - 1);
			ПолеВводаЗначениеС.Высота = ЭлементУправленияВысота;
			ПолеВводаЗначениеС.Ширина = Цел((ПанельШирина - 1 - ПолеВводаЗначениеС.Лево - ИнтервалМеждуЭлементамиУправленияГоризонтальный) / 2);
			ПолеВводаЗначениеС.Данные = мПутьКПостроителюОтчета + ".ПостроительОтчета.Отбор." + ЭлементОтбора.Имя + ".ЗначениеС";
			
			ПолеВводаЗначениеС.УстановитьПривязку(ГраницаЭлементаУправления.Лево, ПолеВводаВидСравнения, ГраницаЭлементаУправления.Право);
			ПолеВводаЗначениеС.УстановитьПривязку(ГраницаЭлементаУправления.Право, ПанельБыстрыеОтборы, ГраницаЭлементаУправления.Лево, ПанельБыстрыеОтборы, ГраницаЭлементаУправления.Право);
			
			ПолеВводаЗначениеС.УстановитьДействие("ПриИзменении", ?(мДействиеПолеВводаЗначениеПриИзменении = Неопределено, Новый Действие("ПолеВводаЗначениеПриИзменении"), мДействиеПолеВводаЗначениеПриИзменении));
			
			// Значение по
			ПолеВводаЗначениеПо = Форма.ЭлементыФормы.Добавить(Тип("ПолеВвода"), "ПолеВводаЗначениеПо" + Формат(Индекс, "ЧГ=0"),, ПанельБыстрыеОтборы);
			ПолеВводаЗначениеПо.Лево = ПолеВводаЗначениеС.Лево + ПолеВводаЗначениеС.Ширина + ИнтервалМеждуЭлементамиУправленияГоризонтальный;
			ПолеВводаЗначениеПо.Верх = (ЭлементУправленияВысота + ИнтервалМеждуЭлементамиУправленияВертикальный) * (Индекс - 1);
			ПолеВводаЗначениеПо.Высота = ЭлементУправленияВысота;
			ПолеВводаЗначениеПо.Ширина = ПанельШирина - 1 - ПолеВводаЗначениеПо.Лево;
			ПолеВводаЗначениеПо.Данные = мПутьКПостроителюОтчета + ".ПостроительОтчета.Отбор." + ЭлементОтбора.Имя + ".ЗначениеПо";
			
			ПолеВводаЗначениеПо.УстановитьПривязку(ГраницаЭлементаУправления.Лево, ПолеВводаЗначениеС, ГраницаЭлементаУправления.Право);
			ПолеВводаЗначениеПо.УстановитьПривязку(ГраницаЭлементаУправления.Право, ПанельБыстрыеОтборы, ГраницаЭлементаУправления.Право);
			
			ПолеВводаЗначениеПо.УстановитьДействие("ПриИзменении", ?(мДействиеПолеВводаЗначениеПриИзменении = Неопределено, Новый Действие("ПолеВводаЗначениеПриИзменении"), мДействиеПолеВводаЗначениеПриИзменении));
				
			ИнициализацияПолейВводаЗначение(ПолеВводаВидСравнения, Форма);
			
			Индекс = Индекс + 1;
			
			Если Индекс > МаксимальноеКоличествоБыстрыхОтборов Тогда
				
				Прервать;
				
			КонецЕсли;
				
		КонецЦикла;
	
	КонецЕсли;
	
КонецПроцедуры // ДобавитьПанельБыстрыеОтборы()

Процедура ДобавитьПанельДополнительныеЭлементыФормы(Форма, СоставПанелиДополнительныхЭлементовФормы) Экспорт
	
	ЭлементУправленияВысота = 19;
	ИнтервалМеждуЭлементамиУправленияВертикальный = 5;
	ИнтервалМеждуЭлементамиУправленияГоризонтальный = 5;
	
	Если СоставПанелиДополнительныхЭлементовФормы = Неопределено ИЛИ СоставПанелиДополнительныхЭлементовФормы.Количество() = 0 Тогда
		
		Возврат;
		
	КонецЕсли;
	
	ВысотаПанелиДополнительныеЭлементыФормы = СоставПанелиДополнительныхЭлементовФормы.Количество() * ИнтервалМеждуЭлементамиУправленияВертикальный + СоставПанелиДополнительныхЭлементовФормы.Количество() * ЭлементУправленияВысота;
	
	ЭлементУправления = Форма.ЭлементыФормы[мИмяЭлементаУправленияДополнительныеЭлементы];

	ПанельВерх = ЭлементУправления.Верх;
	ПанельЛево = ЭлементУправления.Лево;
	ПанельШирина = ЭлементУправления.Ширина;
	ПанельВысота = ВысотаПанелиДополнительныеЭлементыФормы;
		
	ПанельДополнительныеЭлементыФормы = Форма.ЭлементыФормы.Добавить(Тип("Панель"), "ПанельДополнительныеЭлементыФормы");
	ПанельДополнительныеЭлементыФормы.ОтображениеЗакладок = ОтображениеЗакладок.НеИспользовать;
	ПанельДополнительныеЭлементыФормы.Верх = ПанельВерх;
	ПанельДополнительныеЭлементыФормы.Лево = ПанельЛево;
	ПанельДополнительныеЭлементыФормы.Ширина = ПанельШирина;
	ПанельДополнительныеЭлементыФормы.Высота = ПанельВысота;
	ПанельДополнительныеЭлементыФормы.УстановитьПривязку(ГраницаЭлементаУправления.Лево, Форма.Панель, ГраницаЭлементаУправления.Лево);
	ПанельДополнительныеЭлементыФормы.УстановитьПривязку(ГраницаЭлементаУправления.Право, Форма.Панель, ГраницаЭлементаУправления.Право);
		
	ЭлементУправления.Высота = ЭлементУправления.Высота - ПанельДополнительныеЭлементыФормы.Высота - ИнтервалМеждуЭлементамиУправленияВертикальный;
	ЭлементУправления.Верх = ПанельДополнительныеЭлементыФормы.Верх + ПанельДополнительныеЭлементыФормы.Высота + ИнтервалМеждуЭлементамиУправленияВертикальный;
	ЭлементУправления.УстановитьПривязку(ГраницаЭлементаУправления.Верх, ПанельДополнительныеЭлементыФормы, ГраницаЭлементаУправления.Низ);
	
	ИндексГруппы = 1;
	
	Для каждого ГруппаЭлементов из СоставПанелиДополнительныхЭлементовФормы Цикл
		
		ЛеваяГраницаПредыдущегоЭлемента = 0;
		ШиринаПредыдущегоЭлемента = 0;
		
		// Группа
		Если ГруппаЭлементов.Свойство("Имя") И НЕ ПустаяСтрока(ГруппаЭлементов.Имя) Тогда
			
			НадписьГруппаЭлементов = Форма.ЭлементыФормы.Добавить(Тип("Надпись"), ГруппаЭлементов.Имя,, ПанельДополнительныеЭлементыФормы);
			НадписьГруппаЭлементов.Верх = (ЭлементУправленияВысота + ИнтервалМеждуЭлементамиУправленияВертикальный) * (ИндексГруппы - 1);
			НадписьГруппаЭлементов.Высота = ЭлементУправленияВысота;
			
			Если ГруппаЭлементов.Свойство("Ширина") Тогда
				
				НадписьГруппаЭлементов.Ширина = ГруппаЭлементов.Ширина;
				
			КонецЕсли;
			
			Если ГруппаЭлементов.Свойство("Заголовок") Тогда
				
				НадписьГруппаЭлементов.Заголовок = ГруппаЭлементов.Заголовок;
				
			КонецЕсли;
			
			ЛеваяГраницаПредыдущегоЭлемента = НадписьГруппаЭлементов.Лево;
			ШиринаПредыдущегоЭлемента = НадписьГруппаЭлементов.Ширина;
			
		КонецЕсли;
		
		Для каждого ЭлементГруппы из ГруппаЭлементов.СоставГруппы Цикл
			
			Если ЭлементГруппы.Свойство("ЭлементГруппы") И НЕ ЭлементГруппы.Видимость Тогда
				
				Продолжить;
				
			КонецЕсли;
			
			// Элемент группы
			Если ЭлементГруппы.Свойство("Имя") И НЕ ПустаяСтрока(ЭлементГруппы.Имя) Тогда
				
				ЭлементГруппыПереключатель = Форма.ЭлементыФормы.Добавить(Тип("Переключатель"), ЭлементГруппы.Имя,, ПанельДополнительныеЭлементыФормы);
				ЭлементГруппыПереключатель.Лево = ЛеваяГраницаПредыдущегоЭлемента + ШиринаПредыдущегоЭлемента + ИнтервалМеждуЭлементамиУправленияГоризонтальный;
				ЭлементГруппыПереключатель.Верх = (ЭлементУправленияВысота + ИнтервалМеждуЭлементамиУправленияВертикальный) * (ИндексГруппы - 1);
				ЭлементГруппыПереключатель.Высота = ЭлементУправленияВысота;
				
				Если ЭлементГруппы.Свойство("Ширина") Тогда
						
					ЭлементГруппыПереключатель.Ширина = ЭлементГруппы.Ширина;
						
				КонецЕсли;
				
				Если ЭлементГруппы.Свойство("Данные") Тогда
				
					ЭлементГруппыПереключатель.Данные = ЭлементГруппы.Данные;
					
				КонецЕсли;
				
				Если ЭлементГруппы.Свойство("ПервыйВГруппе") Тогда
				
					ЭлементГруппыПереключатель.ПервыйВГруппе = ЭлементГруппы.ПервыйВГруппе;
					
				КонецЕсли;
				
				Если ЭлементГруппы.Свойство("ВыбираемоеЗначение") Тогда
				
					ЭлементГруппыПереключатель.ВыбираемоеЗначение = ЭлементГруппы.ВыбираемоеЗначение;
					
				КонецЕсли;
				
				Если ЭлементГруппы.Свойство("Видимость") Тогда
				
					ЭлементГруппыПереключатель.Видимость = ЭлементГруппы.Видимость;
					
				КонецЕсли;
				
				Если ЭлементГруппы.Свойство("Доступность") Тогда
				
					ЭлементГруппыПереключатель.Доступность = ЭлементГруппы.Доступность;
					
				КонецЕсли;
				
				Если ЭлементГруппы.Свойство("Заголовок") Тогда
				
					ЭлементГруппыПереключатель.Заголовок = ЭлементГруппы.Заголовок;
					
				КонецЕсли;
				
				Если ЭлементГруппы.Свойство("ДействиеПриИзменении") Тогда
				
					ЭлементГруппыПереключатель.УстановитьДействие("ПриИзменении", ЭлементГруппы.ДействиеПриИзменении);
					
				КонецЕсли;
				
				ЛеваяГраницаПредыдущегоЭлемента = ЭлементГруппыПереключатель.Лево;
				ШиринаПредыдущегоЭлемента = ЭлементГруппыПереключатель.Ширина;
				
				Попытка
					
					Форма.ЭтотОбъект[ЭлементГруппы.Данные] = Форма.ЭтотОбъект[ЭлементГруппы.Данные];
					
				Исключение
					
					Попытка
						
						Форма[ЭлементГруппы.Данные] = Форма[ЭлементГруппы.Данные];
						
					Исключение
						
					КонецПопытки;
					
				КонецПопытки;
				
			КонецЕсли;
			
		КонецЦикла;
		
		ИндексГруппы = ИндексГруппы + 1;
		
	КонецЦикла;
	
КонецПроцедуры // ДобавитьПанельДополнительныеЭлементыФормы()

Процедура ДобавитьПанельСправкаФормы(Объект, Форма) Экспорт
	
	Если мДействиеКоманднаяПанельФормыСправкаФормы <> Неопределено Тогда
		
		Если Форма.ОтчетОбъект.Метаданные().Макеты.Найти("МакетВстроеннойСправки") <> Неопределено Тогда
			
			Если ПустаяСтрока(Форма.ОтчетОбъект.ПолучитьМакет("МакетВстроеннойСправки").ПолучитьТекст()) Тогда
			
				Возврат;
				
			КонецЕсли;
			
		Иначе
			
			Если ПустаяСтрока(Форма.ОтчетОбъект.ПолучитьТекстСправкиФормы()) Тогда
				
				Возврат;
				
			КонецЕсли;
			
		КонецЕсли;

		Если Форма.ЭлементыФормы.Найти("КоманднаяПанельФормы") <> Неопределено Тогда
			
			Если Форма.ЭлементыФормы.КоманднаяПанельФормы.Кнопки.Найти("Справка") <> Неопределено Тогда
				
				КнопкаСправкаФормы = Форма.ЭлементыФормы.КоманднаяПанельФормы.Кнопки.Справка;
				КнопкаСправкаФормы.Имя = "СправкаФормы";
				КнопкаСправкаФормы.Текст = НСтр("ru = 'Показать/скрыть справку'");
				КнопкаСправкаФормы.Подсказка = НСтр("ru = 'Показать/скрыть справку'");
				КнопкаСправкаФормы.Пояснение = НСтр("ru = 'Показать/скрыть справку'");
				КнопкаСправкаФормы.Картинка = БиблиотекаКартинок.ВстроеннаяСправкаФормы;
				КнопкаСправкаФормы.Действие = мДействиеКоманднаяПанельФормыСправкаФормы;
				
			КонецЕсли;
			
		КонецЕсли;
		
	Иначе
		
		Возврат;
		
	КонецЕсли;
	
	ШиринаПанелиСправкаФормы = 224;
	ШиринаРазделителяСправкаФормы = 6;
	ВысотаНадписиЗакрытьСправкуФормы = 35;
	
	ЭлементУправления = Форма.ЭлементыФормы[мИмяЭлементаУправленияДополнительныеЭлементы];

	// ПанельСправкаФормы
	ПанельСправкаФормыВерх = ЭлементУправления.Верх;
	ПанельСправкаФормыЛево = ЭлементУправления.Лево + ЭлементУправления.Ширина - ШиринаПанелиСправкаФормы;
	ПанельСправкаФормыШирина = ШиринаПанелиСправкаФормы;
	ПанельСправкаФормыВысота = ЭлементУправления.Высота;
		
	ПанельСправкаФормы = Форма.ЭлементыФормы.Добавить(Тип("Панель"), "ПанельСправкаФормы");
	ПанельСправкаФормы.ОтображениеЗакладок = ОтображениеЗакладок.НеИспользовать;
	ПанельСправкаФормы.Рамка = Новый Рамка(ТипРамкиЭлементаУправления.Одинарная);
	ПанельСправкаФормы.Верх = ПанельСправкаФормыВерх;
	ПанельСправкаФормы.Лево = ПанельСправкаФормыЛево;
	ПанельСправкаФормы.Ширина = ПанельСправкаФормыШирина;
	ПанельСправкаФормы.Высота = ПанельСправкаФормыВысота;
	
	// ВстроеннаяСправка
	ВстроеннаяСправка = Форма.ЭлементыФормы.Добавить(Тип("ПолеHTMLДокумента"), "ВстроеннаяСправка",, ПанельСправкаФормы);
	ВстроеннаяСправка.Рамка = Новый Рамка(ТипРамкиЭлементаУправления.БезРамки);
	ВстроеннаяСправка.Верх = 0;
	ВстроеннаяСправка.Лево = 0;
	ВстроеннаяСправка.Ширина = ПанельСправкаФормыШирина;
	ВстроеннаяСправка.Высота = ПанельСправкаФормыВысота - ВысотаНадписиЗакрытьСправкуФормы;
	ВстроеннаяСправка.УстановитьДействие("onclick", мДействиеВстроеннаяСправкаonclick);
	ВстроеннаяСправка.УстановитьДействие("onmouseout", мДействиеВстроеннаяСправкаonmouseout);
	ВстроеннаяСправка.УстановитьДействие("onmouseover", мДействиеВстроеннаяСправкаonmouseover);
	
	// НадписиЗакрытьСправкуФормы
	НадписиЗакрытьСправкуФормы = Форма.ЭлементыФормы.Добавить(Тип("Надпись"), "НадписиЗакрытьСправкуФормы",, ПанельСправкаФормы);
	НадписиЗакрытьСправкуФормы.Заголовок = НСтр("ru = 'Скрыть эту информацию'");
	НадписиЗакрытьСправкуФормы.ГиперСсылка = Истина;
	НадписиЗакрытьСправкуФормы.ГоризонтальноеПоложение = ГоризонтальноеПоложение.Право;
	НадписиЗакрытьСправкуФормы.ВертикальноеПоложение = ВертикальноеПоложение.Центр;
	НадписиЗакрытьСправкуФормы.Верх = ПанельСправкаФормыВысота - ВысотаНадписиЗакрытьСправкуФормы;
	НадписиЗакрытьСправкуФормы.Лево = 0;
	НадписиЗакрытьСправкуФормы.Ширина = ПанельСправкаФормыШирина;
	НадписиЗакрытьСправкуФормы.Высота = ВысотаНадписиЗакрытьСправкуФормы;
	НадписиЗакрытьСправкуФормы.УстановитьДействие("Нажатие", мДействиеКоманднаяПанельФормыСправкаФормы);
	
	// РазделительСправкаФормы
	РазделительСправкаФормыВерх = ЭлементУправления.Верх;
	РазделительСправкаФормыЛево = ЭлементУправления.Лево + ЭлементУправления.Ширина - ШиринаПанелиСправкаФормы - ШиринаРазделителяСправкаФормы;
	РазделительСправкаФормыШирина = ШиринаРазделителяСправкаФормы;
	РазделительСправкаФормыВысота = ЭлементУправления.Высота;
		
	РазделительСправкаФормы = Форма.ЭлементыФормы.Добавить(Тип("Разделитель"), "РазделительСправкаФормы");
	РазделительСправкаФормы.Верх = РазделительСправкаФормыВерх;
	РазделительСправкаФормы.Лево = РазделительСправкаФормыЛево;
	РазделительСправкаФормы.Ширина = РазделительСправкаФормыШирина;
	РазделительСправкаФормы.Высота = РазделительСправкаФормыВысота;
	РазделительСправкаФормы.Ориентация = Ориентация.Вертикально;
	
	//Установка привязок
	ПервыйЭлемент = Неопределено;
	ГраницаПервогоЭлемента = Неопределено;
	ВторойЭлемент = Неопределено;
	ГраницаВторогоЭлемента = Неопределено;
	
	ЭлементУправления.ПолучитьПривязку(ГраницаЭлементаУправления.Верх, ПервыйЭлемент, ГраницаПервогоЭлемента, ВторойЭлемент, ГраницаВторогоЭлемента);
	ПанельСправкаФормы.УстановитьПривязку(ГраницаЭлементаУправления.Верх, ПервыйЭлемент, ГраницаПервогоЭлемента, ВторойЭлемент, ГраницаВторогоЭлемента);
	РазделительСправкаФормы.УстановитьПривязку(ГраницаЭлементаУправления.Верх, ПервыйЭлемент, ГраницаПервогоЭлемента, ВторойЭлемент, ГраницаВторогоЭлемента);
	
	ЭлементУправления.ПолучитьПривязку(ГраницаЭлементаУправления.Низ, ПервыйЭлемент, ГраницаПервогоЭлемента, ВторойЭлемент, ГраницаВторогоЭлемента);
	ПанельСправкаФормы.УстановитьПривязку(ГраницаЭлементаУправления.Низ, ПервыйЭлемент, ГраницаПервогоЭлемента, ВторойЭлемент, ГраницаВторогоЭлемента);
	РазделительСправкаФормы.УстановитьПривязку(ГраницаЭлементаУправления.Низ, ПервыйЭлемент, ГраницаПервогоЭлемента, ВторойЭлемент, ГраницаВторогоЭлемента);
	
	ПанельСправкаФормы.УстановитьПривязку(ГраницаЭлементаУправления.Лево, РазделительСправкаФормы, ГраницаЭлементаУправления.Право);
	ПанельСправкаФормы.УстановитьПривязку(ГраницаЭлементаУправления.Право, Форма.Панель, ГраницаЭлементаУправления.Право);
	
	ВстроеннаяСправка.УстановитьПривязку(ГраницаЭлементаУправления.Лево, ПанельСправкаФормы, ГраницаЭлементаУправления.Лево);
	ВстроеннаяСправка.УстановитьПривязку(ГраницаЭлементаУправления.Право, ПанельСправкаФормы, ГраницаЭлементаУправления.Право);
	ВстроеннаяСправка.УстановитьПривязку(ГраницаЭлементаУправления.Верх, ПанельСправкаФормы, ГраницаЭлементаУправления.Верх);
	ВстроеннаяСправка.УстановитьПривязку(ГраницаЭлементаУправления.Низ, ПанельСправкаФормы, ГраницаЭлементаУправления.Низ);
	
	НадписиЗакрытьСправкуФормы.УстановитьПривязку(ГраницаЭлементаУправления.Лево, ПанельСправкаФормы, ГраницаЭлементаУправления.Лево);
	НадписиЗакрытьСправкуФормы.УстановитьПривязку(ГраницаЭлементаУправления.Право, ПанельСправкаФормы, ГраницаЭлементаУправления.Право);
	НадписиЗакрытьСправкуФормы.УстановитьПривязку(ГраницаЭлементаУправления.Верх, ПанельСправкаФормы, ГраницаЭлементаУправления.Низ);
	НадписиЗакрытьСправкуФормы.УстановитьПривязку(ГраницаЭлементаУправления.Низ, ПанельСправкаФормы, ГраницаЭлементаУправления.Низ);
	
	РазделительСправкаФормы.УстановитьПривязку(ГраницаЭлементаУправления.Лево, РазделительСправкаФормы, ГраницаЭлементаУправления.Право);
	РазделительСправкаФормы.УстановитьПривязку(ГраницаЭлементаУправления.Право, Форма.Панель, ГраницаЭлементаУправления.Право);
	
	ЭлементУправления.Ширина = ЭлементУправления.Ширина - ШиринаПанелиСправкаФормы - ШиринаРазделителяСправкаФормы;
	ЭлементУправления.УстановитьПривязку(ГраницаЭлементаУправления.Право, РазделительСправкаФормы, ГраницаЭлементаУправления.Право);
	
КонецПроцедуры // ДобавитьПанельСправкаФормы()

Процедура ДобавитьКнопкиПечать(Объект, Форма) Экспорт
	
	Если мДействиеКоманднаяПанельФормыПечать <> Неопределено Тогда

		Если Форма.ЭлементыФормы.Найти("КоманднаяПанельФормы") <> Неопределено Тогда
			
			Если Форма.ЭлементыФормы.КоманднаяПанельФормы.Кнопки.Найти("Подменю") <> Неопределено Тогда
				
				ПодменюДействия = Форма.ЭлементыФормы.КоманднаяПанельФормы.Кнопки.Подменю;
				
				Если ПодменюДействия.Кнопки.Найти("Справка") <> Неопределено Тогда
					
					Если мДействиеКоманднаяПанельФормыПечать <> Неопределено Тогда
						
						КнопкаПечать = ПодменюДействия.Кнопки.Вставить(ПодменюДействия.Кнопки.Индекс(ПодменюДействия.Кнопки.Справка), "Печать", ТипКнопкиКоманднойПанели.Действие, НСтр("ru = 'Печать...'"), мДействиеКоманднаяПанельФормыПечать);
						КнопкаПечать.Картинка = БиблиотекаКартинок.Печать;
						КнопкаПечать.СочетаниеКлавиш = Новый СочетаниеКлавиш(Клавиша.P,, Истина);
						
					КонецЕсли;
					
					ПодменюДействия.Кнопки.Вставить(ПодменюДействия.Кнопки.Индекс(ПодменюДействия.Кнопки.Справка), "РазделительПечать", ТипКнопкиКоманднойПанели.Разделитель);
					
				Иначе
					
					ПодменюДействия.Кнопки.Добавить("РазделительПечать", ТипКнопкиКоманднойПанели.Разделитель);
					
					Если мДействиеКоманднаяПанельФормыПечать <> Неопределено Тогда
						
						КнопкаПечать = ПодменюДействия.Кнопки.Добавить("Печать", ТипКнопкиКоманднойПанели.Действие, НСтр("ru = 'Печать...'"), мДействиеКоманднаяПанельФормыПечать);
						КнопкаПечать.Картинка = БиблиотекаКартинок.Печать;
						КнопкаПечать.СочетаниеКлавиш = Новый СочетаниеКлавиш(Клавиша.P,, Истина);
						
					КонецЕсли;
					
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры // ДобавитьКнопкиПечать()

Процедура УправлениеПанельюБыстрыеОтборы(Объект, Форма) Экспорт
	
	Если Форма.ЭлементыФормы.Найти("ПанельБыстрыеОтборы") = Неопределено Тогда
		
		Возврат;
		
	КонецЕсли;
	
	Если Объект.БыстрыйОтборРазвернут Тогда
		
		Форма.ЭлементыФормы.ПанельБыстрыеОтборы.Свертка = РежимСверткиЭлементаУправления.Нет;
		
	Иначе
		
		Форма.ЭлементыФормы.ПанельБыстрыеОтборы.Свертка = РежимСверткиЭлементаУправления.Верх;
		
	КонецЕсли;
	
КонецПроцедуры // УправлениеПанельюБыстрыеОтборы()

Процедура ИнициализацияПолейВводаЗначение(Элемент, Форма)
	
	ВидСравненияЗначение = Истина;
	ВидСравненияВСписке = Ложь;
	
	Индекс = СтрЗаменить(Элемент.Имя, "ПолеВводаВидСравнения", "");
	
	Если Элемент.Значение = ВидСравнения.Интервал ИЛИ Элемент.Значение = ВидСравнения.ИнтервалВключаяГраницы ИЛИ Элемент.Значение = ВидСравнения.ИнтервалВключаяНачало ИЛИ Элемент.Значение = ВидСравнения.ИнтервалВключаяОкончание Тогда
		
		ВидСравненияЗначение = Ложь;
		
	ИначеЕсли Элемент.Значение = ВидСравнения.ВСписке ИЛИ Элемент.Значение = ВидСравнения.ВСпискеПоИерархии ИЛИ Элемент.Значение = ВидСравнения.НеВСписке ИЛИ Элемент.Значение = ВидСравнения.НеВСпискеПоИерархии Тогда
		
		ВидСравненияВСписке = Истина;
		
	КонецЕсли;
		
	Если Форма.ЭлементыФормы.Найти("ПолеВводаЗначение" + Индекс) <> Неопределено Тогда
				
		Форма.ЭлементыФормы["ПолеВводаЗначение" + Индекс].Видимость = ВидСравненияЗначение;
				
	КонецЕсли;
			
	Если Форма.ЭлементыФормы.Найти("ПолеВводаЗначениеС" + Индекс) <> Неопределено Тогда
				
		Форма.ЭлементыФормы["ПолеВводаЗначениеС" + Индекс].Видимость = НЕ ВидСравненияЗначение;
				
	КонецЕсли;
			
	Если Форма.ЭлементыФормы.Найти("ПолеВводаЗначениеПо" + Индекс) <> Неопределено Тогда
				
		Форма.ЭлементыФормы["ПолеВводаЗначениеПо" + Индекс].Видимость = НЕ ВидСравненияЗначение;
				
	КонецЕсли;
	
	Если ВидСравненияВСписке Тогда
		
		Форма.ЭлементыФормы["ПолеВводаЗначение" + Индекс].ОграничениеТипа = Новый ОписаниеТипов("СписокЗначений");
		
	Иначе
		
		Форма.ЭлементыФормы["ПолеВводаЗначение" + Индекс].ОграничениеТипа = Новый ОписаниеТипов(Форма.ЭлементыФормы["ПолеВводаЗначение" + Индекс].ТипЗначения,, "СписокЗначений");
		
	КонецЕсли;
	
КонецПроцедуры // ИнициализацияПолейВводаЗначение()

Процедура ВидСравненияПриИзменении(Элемент, Форма) Экспорт
	
	ИнициализацияПолейВводаЗначение(Элемент, Форма);
	
	Индекс = СтрЗаменить(Элемент.Имя, "ПолеВводаВидСравнения", "");
	Форма.ЭлементыФормы["ФлажокИспользование" + Индекс].Значение = Истина;
		
КонецПроцедуры // ВидСравненияПриИзменении()

Процедура ЗначениеПриИзменении(Элемент, Форма) Экспорт
	
	Индекс = СтрЗаменить(СтрЗаменить(СтрЗаменить(Элемент.Имя, "ПолеВводаЗначение", ""), "С", ""), "По", "");
	
	Если Элемент.Имя = "ПолеВводаЗначение" + Индекс И НЕ Форма.ЭлементыФормы["ФлажокИспользование" + Индекс].Значение Тогда
		
		Попытка
			
			ЭтоГруппа = Форма.ЭлементыФормы["ПолеВводаЗначение" + Индекс].Значение.ЭтоГруппа;
			
		Исключение
			
			ЭтоГруппа = Ложь;
			
		КонецПопытки;
		
		Если ЭтоГруппа Тогда
			
			Форма.ЭлементыФормы["ПолеВводаВидСравнения" + Индекс].Значение = ВидСравнения.ВИерархии;
			
		КонецЕсли;
		
		Форма.ЭлементыФормы["ФлажокИспользование" + Индекс].Значение = Истина;
		
	КонецЕсли;
	
	Если Элемент.Имя = "ПолеВводаЗначение" + Индекс И ТипЗнч(Форма.ЭлементыФормы["ПолеВводаЗначение" + Индекс].Значение) = Тип("СписокЗначений") И Форма.ЭлементыФормы["ПолеВводаЗначение" + Индекс].Значение.ТипЗначения = Новый ОписаниеТипов() Тогда
		
		Форма.ЭлементыФормы["ПолеВводаЗначение" + Индекс].Значение.ТипЗначения = Новый ОписаниеТипов(Форма.ЭлементыФормы["ПолеВводаЗначение" + Индекс].ТипЗначения,, "СписокЗначений");
		
	КонецЕсли;
		
КонецПроцедуры // ВидСравненияПриИзменении()

Процедура УправлениеОтображениемЗаголовка(Объект, ПолеТабличногоДокументаРезультат) Экспорт
	
	ОбластьЗаголовок = ПолеТабличногоДокументаРезультат.Области.Найти("Заголовок");
	
	Если ТипЗнч(ОбластьЗаголовок) = Тип("ОбластьЯчеекТабличногоДокумента") Тогда
		
		ОбластьЗаголовок.Видимость = Объект.ПоказыватьЗаголовок;
		
	КонецЕсли;
	
КонецПроцедуры // УправлениеОтображениемЗаголовка()

Процедура УправлениеОтображениемСправкиФормы(Объект, Форма) Экспорт
	
	Если Форма.ЭлементыФормы.Найти("ПанельСправкаФормы") = Неопределено Тогда
		
		Возврат;
		
	КонецЕсли;
	
	Если (НЕ Объект.НеПоказыватьСправкуФормы И Форма.ЭлементыФормы.ПанельСправкаФормы.Свертка = РежимСверткиЭлементаУправления.Нет) ИЛИ
		(Объект.НеПоказыватьСправкуФормы И Форма.ЭлементыФормы.ПанельСправкаФормы.Свертка <> РежимСверткиЭлементаУправления.Нет) Тогда
		
		Возврат;
		
	КонецЕсли;
	
	Если Форма.ЭлементыФормы.ПанельСправкаФормы.Свертка = РежимСверткиЭлементаУправления.Нет Тогда
		
		Форма.ЭлементыФормы[мИмяЭлементаУправленияСправкаФормы].Ширина = Форма.ЭлементыФормы[мИмяЭлементаУправленияСправкаФормы].Ширина + Форма.ЭлементыФормы.РазделительСправкаФормы.Ширина;
		Форма.ЭлементыФормы.ПанельСправкаФормы.УстановитьПривязку(ГраницаЭлементаУправления.Лево);
		Форма.ЭлементыФормы.РазделительСправкаФормы.УстановитьПривязку(ГраницаЭлементаУправления.Право, Форма.ЭлементыФормы.ПанельСправкаФормы, ГраницаЭлементаУправления.Лево);
		Форма.ЭлементыФормы.ПанельСправкаФормы.Свертка = РежимСверткиЭлементаУправления.Право;
		Форма.ЭлементыФормы.РазделительСправкаФормы.Свертка = РежимСверткиЭлементаУправления.Право;
		
	Иначе
		
		Форма.ЭлементыФормы.РазделительСправкаФормы.Свертка = РежимСверткиЭлементаУправления.Нет;
		Форма.ЭлементыФормы.ПанельСправкаФормы.Свертка = РежимСверткиЭлементаУправления.Нет;
		Форма.ЭлементыФормы.РазделительСправкаФормы.УстановитьПривязку(ГраницаЭлементаУправления.Право, Форма.Панель, ГраницаЭлементаУправления.Право);
		Форма.ЭлементыФормы.ПанельСправкаФормы.УстановитьПривязку(ГраницаЭлементаУправления.Лево, Форма.ЭлементыФормы.РазделительСправкаФормы, ГраницаЭлементаУправления.Право);
		Форма.ЭлементыФормы[мИмяЭлементаУправленияСправкаФормы].Ширина = Форма.ЭлементыФормы[мИмяЭлементаУправленияСправкаФормы].Ширина - Форма.ЭлементыФормы.РазделительСправкаФормы.Ширина;
		
	КонецЕсли;
	
КонецПроцедуры // УправлениеОтображениемСправкиФормы()

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

Процедура ФормаПриОткрытии(Объект, Отчет, Форма, СоставПанелиДополнительныхЭлементовФормы = Неопределено, ИмяОбъекта = Неопределено) Экспорт
	
	Форма.КлючУникальности = Новый УникальныйИдентификатор;
	
	Если Объект.мРежимВводаПериода = -1 Тогда
		
		Форма.ЭлементыФормы.ПанельВыборПериода.Видимость = Ложь;
		
	ИначеЕсли Объект.мРежимВводаПериода = 0 Тогда
		
		Форма.ЭлементыФормы.ПанельВыборПериода.ТекущаяСтраница = Форма.ЭлементыФормы.ПанельВыборПериода.Страницы.Найти("СтраницаПроизвольныйПериод");
		
	ИначеЕсли Объект.мРежимВводаПериода = 1 Тогда
		
		Форма.ЭлементыФормы.ПанельВыборПериода.ТекущаяСтраница = Форма.ЭлементыФормы.ПанельВыборПериода.Страницы.Найти("СтраницаНаДату");
		
	Иначе
		
		Форма.ЭлементыФормы.ПанельВыборПериода.ТекущаяСтраница = Форма.ЭлементыФормы.ПанельВыборПериода.Страницы.Найти("СтраницаПериод");
		
		Если Объект.мРежимВводаПериода = 2 Тогда
			
			Форма.ЭлементыФормы.Период.Формат = Объект.мСтруктураФорматаПолей.ПериодНеделя;
			
		ИначеЕсли Объект.мРежимВводаПериода = 3 Тогда
			
			Форма.ЭлементыФормы.Период.Формат = Объект.мСтруктураФорматаПолей.ПериодДекада;
			
		ИначеЕсли Объект.мРежимВводаПериода = 4 Тогда
			
			Форма.ЭлементыФормы.Период.Формат = Объект.мСтруктураФорматаПолей.ПериодМесяц;
			
		ИначеЕсли Объект.мРежимВводаПериода = 5 Тогда
			
			Форма.ЭлементыФормы.Период.Формат = Объект.мСтруктураФорматаПолей.ПериодКвартал;
			
		ИначеЕсли Объект.мРежимВводаПериода = 6 Тогда
			
			Форма.ЭлементыФормы.Период.Формат = Объект.мСтруктураФорматаПолей.ПериодПолугодие;
			
		ИначеЕсли Объект.мРежимВводаПериода = 7 Тогда
			
			Форма.ЭлементыФормы.Период.Формат = Объект.мСтруктураФорматаПолей.ПериодГод;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Если Объект.мВосстанавливатьНастройкиПриОткрытии Тогда
		
		Если ЗначениеЗаполнено(глЗначениеПеременной("глТекущийПользователь")) И Не ЗначениеЗаполнено(Объект.ДатаНач) Тогда
			
			Объект.ДатаНач = УправлениеПользователями.ПолучитьЗначениеПоУмолчанию(глЗначениеПеременной("глТекущийПользователь"), "ОсновнаяДатаНачалаОтчетов");
			
		КонецЕсли;
		
		СтруктураНастройки = Новый Структура;
		СтруктураНастройки.Вставить("Пользователь", глЗначениеПеременной("глТекущийПользователь"));
		СтруктураНастройки.Вставить("ИмяОбъекта", ?(ИмяОбъекта = Неопределено, Строка(Отчет), ИмяОбъекта));
		
		Если УниверсальныеМеханизмы.ПолучитьНастройкуИспользоватьПриОткрытии(СтруктураНастройки) Тогда
			
			Объект.мТекущаяНастройка = СтруктураНастройки;
			Отчет.ВосстановитьНастройкиИзСтруктуры(СтруктураНастройки.СохраненнаяНастройка);
			ВосстановитьПараметрыПечати(Объект, Отчет, Форма, СтруктураНастройки.СохраненнаяНастройка);
			
		Иначе
			
			Отчет.УстановитьНачальныеНастройки();
			
		КонецЕсли;
		
		Объект.ВосстановитьДополнительныеНастройки(Объект, Отчет, ИмяОбъекта);
		
	КонецЕсли;
	
	Объект.ДобавитьПанельДополнительныеЭлементыФормы(Форма, СоставПанелиДополнительныхЭлементовФормы);
	Объект.ДобавитьПанельБыстрыеОтборы(Объект, Форма);
	Объект.ДобавитьПанельСправкаФормы(Объект, Форма);
	
	Объект.ДобавитьКнопкиПечать(Объект, Форма);
	
	Объект.УправлениеОтображениемЭлементовФормы(Объект, Форма);
	Объект.УправлениеПанельюБыстрыеОтборы(Объект, Форма);
	Объект.УправлениеОтображениемСправкиФормы(Объект, Форма);
	
	Объект.УстановитьЗаголовокФормы(Объект, Отчет, Форма);
	
	Объект.ПодготовитьСправкуФормы(Объект, Отчет, Форма);
	
	УстановитьОбработкиПодключаемыеКОтчету(Объект, Отчет, Форма);

КонецПроцедуры // ФормаПриОткрытии()

Процедура ФормаПриЗакрытии(Объект, Отчет, Форма, ИмяОбъекта = Неопределено) Экспорт
	
	Перем СохраненнаяНастройка;
	
	Если Объект.мТекущаяНастройка <> Неопределено И Объект.мТекущаяНастройка.НаименованиеНастройки <> Неопределено И Объект.мТекущаяНастройка.Свойство("СохранятьАвтоматически") И Объект.мТекущаяНастройка.СохранятьАвтоматически Тогда
	
		Отчет.СформироватьСтруктуруДляСохраненияНастроек(СохраненнаяНастройка);
		СохранитьПараметрыПечати(Объект, Отчет, Форма, СохраненнаяНастройка);
		
		СтруктураНастройки = Новый Структура;
		СтруктураНастройки.Вставить("Пользователь", глЗначениеПеременной("глТекущийПользователь"));
		СтруктураНастройки.Вставить("ИмяОбъекта", ?(ИмяОбъекта = Неопределено, Строка(Отчет), ИмяОбъекта));
		СтруктураНастройки.Вставить("НаименованиеНастройки", Объект.мТекущаяНастройка.НаименованиеНастройки);
		СтруктураНастройки.Вставить("СохраненнаяНастройка", СохраненнаяНастройка);
		СтруктураНастройки.Вставить("ИспользоватьПриОткрытии", Объект.мТекущаяНастройка.ИспользоватьПриОткрытии);
		СтруктураНастройки.Вставить("СохранятьАвтоматически", Объект.мТекущаяНастройка.СохранятьАвтоматически);
		
		УниверсальныеМеханизмы.СохранитьНастройку(СтруктураНастройки);

	КонецЕсли;
	
	Объект.СохранитьДополнительныеНастройки(Объект, Отчет, ИмяОбъекта);
	
КонецПроцедуры // ФормаПриЗакрытии()

Процедура ФормаНастройка(Объект, Отчет, Форма, ПолеТабличногоДокументаРезультат) Экспорт
	
	Перем ТекущиеНастройкиОтчета;
	
	ФормаНастройка = Объект.ПолучитьФорму("ФормаНастройка", Форма);
	
	Отчет.СформироватьСтруктуруДляСохраненияНастроек(ТекущиеНастройкиОтчета);
	
	КомандаЗакрытияФормы = ФормаНастройка.ОткрытьМодально();
	
	Если КомандаЗакрытияФормы = 2 Тогда
		
		Отчет.СформироватьОтчет(ПолеТабличногоДокументаРезультат);
		
	ИначеЕсли КомандаЗакрытияФормы <> 1 Тогда
		
		Отчет.ВосстановитьНастройкиИзСтруктуры(ТекущиеНастройкиОтчета);
		
	КонецЕсли;
	
КонецПроцедуры // ФормаНастройка()

Процедура ФормаНовыйОтчет(Объект, Отчет, ДополнительныеПараметры = Неопределено, УниверсальныйОтчет = Ложь) Экспорт
	
	Перем СтруктураСНастройками;
	
	Отчет.СформироватьСтруктуруДляСохраненияНастроек(СтруктураСНастройками);
	
	Если Строка(Отчет) = "ВнешняяОбработкаОбъект." + Отчет.Метаданные().Имя Тогда
				
		НовыйОтчет = ВнешниеОбработки.Создать(Отчет.ИспользуемоеИмяФайла);
				
	ИначеЕсли Строка(Отчет) = "ВнешнийОтчетОбъект." + Отчет.Метаданные().Имя Тогда
				
		НовыйОтчет = ВнешниеОтчеты.Создать(Отчет.ИспользуемоеИмяФайла);
		
	Иначе
		
		НовыйОтчет = Отчеты[Отчет.Метаданные().Имя].Создать();
		
	КонецЕсли;
	
	Если УниверсальныйОтчет Тогда
		
		НовыйОтчет.мВосстанавливатьНастройкиПриОткрытии = Ложь;
		НовыйОтчет.БыстрыйОтборРазвернут = Объект.БыстрыйОтборРазвернут;
		НовыйОтчет.ПоказыватьЗаголовок = Объект.ПоказыватьЗаголовок;
		НовыйОтчет.НеПоказыватьСправкуФормы = Объект.НеПоказыватьСправкуФормы;
		
	Иначе
		
		НовыйОтчет.УниверсальныйОтчет.мВосстанавливатьНастройкиПриОткрытии = Ложь;
		НовыйОтчет.УниверсальныйОтчет.БыстрыйОтборРазвернут = Объект.БыстрыйОтборРазвернут;
		НовыйОтчет.УниверсальныйОтчет.ПоказыватьЗаголовок = Объект.ПоказыватьЗаголовок;
		НовыйОтчет.УниверсальныйОтчет.НеПоказыватьСправкуФормы = Объект.НеПоказыватьСправкуФормы;
		
	КонецЕсли;
	
	НовыйОтчет.ВосстановитьНастройкиИзСтруктуры(СтруктураСНастройками);
	
	ФормаНовогоОтчета = НовыйОтчет.ПолучитьФорму();
	ФормаНовогоОтчета.Открыть();
	
КонецПроцедуры // ФормаНовыйОтчет()

Процедура УстановитьЗаголовокФормы(Объект, Отчет, Форма) Экспорт
	
	Если Объект.мТекущаяНастройка <> Неопределено И Объект.мТекущаяНастройка.Свойство("НаименованиеНастройки") И Объект.мТекущаяНастройка.НаименованиеНастройки <> Неопределено Тогда
		
		Форма.Заголовок = мНазваниеОтчета + ". Настройка: " + СокрЛП(Объект.мТекущаяНастройка.НаименованиеНастройки);
		
	Иначе
		
		Форма.Заголовок = мНазваниеОтчета;
		
	КонецЕсли;
	
КонецПроцедуры // УстановитьЗаголовокФормы()

Процедура ФормаНастройкаПериода(Объект) Экспорт
	
	мНастройкаПериода.УстановитьПериод(ДатаНач, ?(ДатаКон = Дата('00010101000000'), ДатаКон, КонецДня(ДатаКон)));
	
	Если Объект.мНастройкаПериода.Редактировать() Тогда
		
		Объект.ДатаНач = Объект.мНастройкаПериода.ПолучитьДатуНачала();
		Объект.ДатаКон = Объект.мНастройкаПериода.ПолучитьДатуОкончания();

	КонецЕсли;
	
КонецПроцедуры // ФормаНастройкаПериода()

Процедура ФормаМинусПериод(Объект) Экспорт
	
	УстановитьПериод(Объект.ДатаНач, Объект.ДатаКон, Объект.мРежимВводаПериода, -1);
	
КонецПроцедуры // ФормаМинусПериод()

Процедура ФормаПлюсПериод(Объект) Экспорт
	
	УстановитьПериод(Объект.ДатаНач, Объект.ДатаКон, Объект.мРежимВводаПериода, 1);
	
КонецПроцедуры // ФормаПлюсПериод()

Процедура ФормаПечать(Объект, Отчет, Форма) Экспорт
	
	Форма.ЭлементыФормы[мИмяЭлементаУправленияПолеТабличногоДокументаРезультат].Напечатать(Ложь);
	
КонецПроцедуры // ФормаПечать()

////////////////////////////////////////////////////////////////////////////////
// ОПЕРАТОРЫ ОСНОВНОЙ ПРОГРАММЫ

Если Показатели.Колонки.Количество() = 0 Тогда
	
	Показатели.Колонки.Добавить("Представление");
	Показатели.Колонки.Добавить("ПредставлениеВОтчете");
	Показатели.Колонки.Добавить("Ширина");
	Показатели.Колонки.Добавить("Использование");
	Показатели.Колонки.Добавить("Имя");
	Показатели.Колонки.Добавить("ФорматнаяСтрока");
	
КонецЕсли;

Если Интервалы.Колонки.Количество() = 0 Тогда
	
	Интервалы.Колонки.Добавить("ВГраница");
	Интервалы.Колонки.Добавить("ИнтервальноеПоле");
	Интервалы.Колонки.Добавить("Название");
	
КонецЕсли;

мРазмещениеПоложениеВыбранныхПолей = Новый Структура;

мСтруктураПредставлениеПолей = Новый Структура;
мСоответствиеСвойствКатегорийИИзмерений = Новый Соответствие;
мСоответствиеКонтактнойИнформацииИИзмерений = Новый Соответствие;
мСоответствиеДанныхОФизлицеИИзмерений = Новый Соответствие;
мСоответствиеПолейИИзмерений = Новый Соответствие;
мСоответствиеНазначений = Новый Соответствие;
мСоответствиеСвязьПолей = Новый Соответствие;
мСоответствиеТиповИнтервальныхПолей = Новый Соответствие;

мСтруктураФорматаПолей = Новый Структура;
мСтруктураФорматаПолей.Вставить("ПериодДень", "ДФ = дд.ММ.гггг");
мСтруктураФорматаПолей.Вставить("ПериодНеделя","ДФ = '""Неделя с"" дд.ММ.гггг '");
мСтруктураФорматаПолей.Вставить("ПериодДекада","ДФ = '""Декада с"" дд.ММ.гггг '");
мСтруктураФорматаПолей.Вставить("ПериодМесяц", "ДФ = 'ММММ гггг ""г.""'");
мСтруктураФорматаПолей.Вставить("ПериодКвартал", "ДФ = 'к ""квартал"" гггг ""г.""'");
мСтруктураФорматаПолей.Вставить("ПериодПолугодие","ДФ = '""Полугодие с"" дд.ММ.гггг'");
мСтруктураФорматаПолей.Вставить("ПериодГод", "ДФ = 'гггг ""г.""'");

мСтруктураПериодов = Новый Структура;
мСтруктураПериодов.Вставить("ПериодГод", 12);
мСтруктураПериодов.Вставить("ПериодПолугодие", 11);
мСтруктураПериодов.Вставить("ПериодКвартал", 10);
мСтруктураПериодов.Вставить("ПериодМесяц", 9);
мСтруктураПериодов.Вставить("ПериодДекада", 8);
мСтруктураПериодов.Вставить("ПериодНеделя", 7);
мСтруктураПериодов.Вставить("ПериодДень", 6);
мСтруктураПериодов.Вставить("Период", 3);
мСтруктураПериодов.Вставить("Регистратор", 2);

мТаблицаПолей = Новый ТаблицаЗначений;
мТаблицаПолей.Колонки.Добавить("ПутьКДанным");   // описание поля запроса, для которого добавляются свойства и категории
мТаблицаПолей.Колонки.Добавить("ИмяИзмерения");  // имя поля запроса, для которого добавляются свойства и категории
мТаблицаПолей.Колонки.Добавить("Представление"); // представление поля, для которого добавляются свойства и категории
мТаблицаПолей.Колонки.Добавить("Назначение");    // назначение свойств/категорий объектов для данного поля
мТаблицаПолей.Колонки.Добавить("НетКатегорий");  // признак НЕиспользования категорий для объекта

мТаблицаПолейКонтактнаяИнформация = Новый ТаблицаЗначений;
мТаблицаПолейКонтактнаяИнформация.Колонки.Добавить("ПутьКДанным");   // описание поля запроса, для которого добавляется контактная информация
мТаблицаПолейКонтактнаяИнформация.Колонки.Добавить("ИмяИзмерения");  // имя поля запроса, для которого добавляется контактная информация
мТаблицаПолейКонтактнаяИнформация.Колонки.Добавить("Представление"); // представление поля, для которого добавляется контактная информация
мТаблицаПолейКонтактнаяИнформация.Колонки.Добавить("ВидОбъекта");    // вид объекта-владельца контактной информации
мТаблицаПолейКонтактнаяИнформация.Колонки.Добавить("ТипОбъекта");    // тип объекта-владельца контактной информации

мТаблицаПолейДанныеОФизлице = Новый ТаблицаЗначений;
мТаблицаПолейДанныеОФизлице.Колонки.Добавить("ПутьКДанным");   // описание поля запроса, для которого добавляются данные о физ. лице
мТаблицаПолейДанныеОФизлице.Колонки.Добавить("ИмяИзмерения");  // имя поля запроса, для которого добавляются данные о физ. лице
мТаблицаПолейДанныеОФизлице.Колонки.Добавить("Представление"); // представление поля, для которого добавляются данные о физ. лице

мНастройкаПериода = Новый НастройкаПериода;
мНастройкаПериода.ВариантНастройки = ВариантНастройкиПериода.Период;

мРежимВводаПериода = 0;
мМинимальнаяШиринаКолонкиПоказатель = 14;
мВыбиратьИмяРегистра = Истина;
мВыбиратьИспользованиеСвойств = Истина;
мРежимФормыНастройкиБезГруппировокКолонок = Ложь;
мНазваниеОтчета = "";

ВыводитьОбщиеИтоги = Истина;

мВосстанавливатьНастройкиПриОткрытии = Истина;
мРассчитыватьШиринуКолонок = Истина;
мРассчитыватьШиринуКолонкиПоНазванию = Ложь;

// 0 - Авто (полное наименование группировок и сокращенное наименование выбранных полей)
// 1 - Полное наименование (полные наименования группировок и выбранных полей)
// 2 - Сокращенное наименование (сокращенные наименования группировок и выбранных полей)
мВидПредставленияПолей = 0;

мПутьКПостроителюОтчета = "ОтчетОбъект.УниверсальныйОтчет";
мИмяЭлементаУправленияПолеТабличногоДокументаРезультат = "ПолеТабличногоДокументаРезультат";
мИмяЭлементаУправленияБыстрыеОтборы = "ПолеТабличногоДокументаРезультат";
мИмяЭлементаУправленияДополнительныеЭлементы = "ПолеТабличногоДокументаРезультат";
мИмяЭлементаУправленияСправкаФормы = "ПолеТабличногоДокументаРезультат";
мДействиеПолеВводаВидСравненияПриИзменении = Неопределено;
мДействиеПолеВводаЗначениеПриИзменении = Неопределено;
мДействиеКоманднаяПанельФормыПечать = Неопределено;
мДействиеКоманднаяПанельФормыСправкаФормы = Неопределено;
мДействиеВстроеннаяСправкаonclick = Неопределено;
мДействиеВстроеннаяСправкаonmouseout = Неопределено;
мДействиеВстроеннаяСправкаonmouseover = Неопределено;
мДействиеФормыВыполнитьВнешнююОбработку = Неопределено;

#КонецЕсли