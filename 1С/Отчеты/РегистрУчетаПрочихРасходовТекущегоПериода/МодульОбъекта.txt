#Если Клиент Тогда
	
Перем НП Экспорт;

// Выполняет запрос и формирует табличный документ-результат отчета
// в соответствии с настройками, заданными значениями реквизитов отчета.
//
// Параметры:
//	ДокументРезультат - табличный документ, формируемый отчетом
//	ПоказыватьЗаголовок - признак видимости строк с заголовком отчета
//	ВысотаЗаголовка - параметр, через который возвращается высота заголовка в строках 
//
Процедура СформироватьОтчет(ДокументРезультат, ПоказыватьЗаголовок, ВысотаЗаголовка, ТолькоЗаголовок = Ложь) Экспорт

	ДокументРезультат.Очистить();

	Макет = ПолучитьМакет("Отчет");

	ОбластьЗаголовок  = Макет.ПолучитьОбласть("Заголовок");

	ОбластьЗаголовок.Параметры.Заголовок = ?(ВидОтчета = 1, "Регистр учета прочих расходов текущего периода",
	                                                       "Регистр учета косвенных расходов текущего периода");
	ОбластьЗаголовок.Параметры.НачалоПериода       = Формат(ДатаНач, "ДФ=dd.MM.yyyy");
	ОбластьЗаголовок.Параметры.КонецПериода        = Формат(ДатаКон, "ДФ=dd.MM.yyyy");
	ОбластьЗаголовок.Параметры.НазваниеОрганизации = Организация.НаименованиеПолное;
	ОбластьЗаголовок.Параметры.ИННОрганизации      = "" + Организация.ИНН + " / " + Организация.КПП;
	ДокументРезультат.Вывести(ОбластьЗаголовок);

	// Параметр для показа заголовка
	ВысотаЗаголовка = ДокументРезультат.ВысотаТаблицы;

	// Когда нужен только заголовок:
	Если ТолькоЗаголовок Тогда
		Возврат;
	КонецЕсли;

	Если ЗначениеЗаполнено(ВысотаЗаголовка) Тогда
		ДокументРезультат.Область("R1:R" + ВысотаЗаголовка).Видимость = ПоказыватьЗаголовок;
	КонецЕсли;
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ДатаНач",       НачалоДня(ДатаНач));
	Запрос.УстановитьПараметр("ДатаКон",       КонецДня(ДатаКон));
	Запрос.УстановитьПараметр("Организация",   Организация);
	Запрос.УстановитьПараметр("ВидСубконто",   ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.СтатьиЗатрат);
	Запрос.УстановитьПараметр("Счет91",        ПланыСчетов.Налоговый.ПрочиеДоходыИРасходы);
	Запрос.УстановитьПараметр("Счет91_2_9",    ПланыСчетов.Налоговый.ПрочиеКосвенныеРасходы);
	Запрос.УстановитьПараметр("ВидУчета",      Перечисления.ВидыУчетаПоПБУ18.НУ);

	СчетаЗатрат = Новый Массив;
	
	Если ВидОтчета = 1 Тогда
	СчетаЗатрат.Добавить(ПланыСчетов.Налоговый.ОсновноеПроизводство_);
	СчетаЗатрат.Добавить(ПланыСчетов.Налоговый.ВспомогательныеПроизводства);
	СчетаЗатрат.Добавить(ПланыСчетов.Налоговый.ОбщепроизводственныеРасходы);
	СчетаЗатрат.Добавить(ПланыСчетов.Налоговый.ОбщехозяйственныеРасходы);
	СчетаЗатрат.Добавить(ПланыСчетов.Налоговый.ОбслуживающиеПроизводства);
	СчетаЗатрат.Добавить(ПланыСчетов.Налоговый.БракВПроизводстве);
	СчетаЗатрат.Добавить(ПланыСчетов.Налоговый.РасходыНаПродажу);
	
	Запрос.УстановитьПараметр("ЕСН",                       Перечисления.ВидыРасходовНУ.ЕСН);
	Запрос.УстановитьПараметр("СтраховыеВзносы",           Перечисления.ВидыРасходовНУ.СтраховыеВзносы);
	Запрос.УстановитьПараметр("НалогиИСборы",              Перечисления.ВидыРасходовНУ.НалогиИСборы);
	Запрос.УстановитьПараметр("Командировочные",           Перечисления.ВидыРасходовНУ.КомандировочныеРасходы);
	Запрос.УстановитьПараметр("Представительские",         Перечисления.ВидыРасходовНУ.ПредставительскиеРасходы);
	Запрос.УстановитьПараметр("РасходыНаРекламу",          Перечисления.ВидыРасходовНУ.РасходыНаРекламуНормируемые);
	Запрос.УстановитьПараметр("РемонтОсновныхСредств",     Перечисления.ВидыРасходовНУ.РемонтОсновныхСредств);
	Запрос.УстановитьПараметр("ОсвоениеПриродныхРесурсов", Перечисления.ВидыРасходовНУ.ОсвоениеПриродныхРесурсов);
	Запрос.УстановитьПараметр("НаНИОКР",                   Перечисления.ВидыРасходовНУ.НИОКР);
	Запрос.УстановитьПараметр("НаНИОКР1_5",                Перечисления.ВидыРасходовНУ.НИОКРПоПеречнюПравительстваРФ);
	Запрос.УстановитьПараметр("ОбязательноеИДобровольноеСтрахованиеИмущества", Перечисления.ВидыРасходовНУ.ОбязательноеИДобровольноеСтрахованиеИмущества);
	Запрос.УстановитьПараметр("АрендаФедеральногоИМуниципальногоИмущества",    Перечисления.ВидыРасходовНУ.АрендаФедеральногоИМуниципальногоИмущества);
	Запрос.УстановитьПараметр("Прочие",                    Перечисления.ВидыРасходовНУ.ПрочиеРасходы);
	Запрос.УстановитьПараметр("Счет97_2",                  ПланыСчетов.Налоговый.ОтрицательныйРезультатОтРеализацииАмортизируемогоИмущества);
	
	ВидыЗатрат = Новый Массив;
	ВидыЗатрат.Добавить(Перечисления.ВидыРасходовНУ.ЕСН);
	ВидыЗатрат.Добавить(Перечисления.ВидыРасходовНУ.СтраховыеВзносы);
	ВидыЗатрат.Добавить(Перечисления.ВидыРасходовНУ.НалогиИСборы);
	ВидыЗатрат.Добавить(Перечисления.ВидыРасходовНУ.КомандировочныеРасходы);
	ВидыЗатрат.Добавить(Перечисления.ВидыРасходовНУ.ПредставительскиеРасходы);
	ВидыЗатрат.Добавить(Перечисления.ВидыРасходовНУ.РасходыНаРекламуНормируемые);
	ВидыЗатрат.Добавить(Перечисления.ВидыРасходовНУ.РемонтОсновныхСредств);
	ВидыЗатрат.Добавить(Перечисления.ВидыРасходовНУ.ОсвоениеПриродныхРесурсов);
	ВидыЗатрат.Добавить(Перечисления.ВидыРасходовНУ.НИОКР);
	ВидыЗатрат.Добавить(Перечисления.ВидыРасходовНУ.НИОКРПоПеречнюПравительстваРФ);
	ВидыЗатрат.Добавить(Перечисления.ВидыРасходовНУ.ОбязательноеИДобровольноеСтрахованиеИмущества);
	ВидыЗатрат.Добавить(Перечисления.ВидыРасходовНУ.АрендаФедеральногоИМуниципальногоИмущества);
	ВидыЗатрат.Добавить(Перечисления.ВидыРасходовНУ.ПрочиеРасходы);
	Запрос.УстановитьПараметр("ВидыЗатрат", ВидыЗатрат);
	
Иначе
	СчетаЗатрат.Добавить(ПланыСчетов.Налоговый.КосвенныеРасходыОсновногоПроизводства);
	СчетаЗатрат.Добавить(ПланыСчетов.Налоговый.КосвенныеРасходыВспомогательныхПроизводств);
	СчетаЗатрат.Добавить(ПланыСчетов.Налоговый.КосвенныеОбщепроизводственныеРасходы);
	СчетаЗатрат.Добавить(ПланыСчетов.Налоговый.КосвенныеОбщехозяйственныеРасходы);
	СчетаЗатрат.Добавить(ПланыСчетов.Налоговый.КосвенныеРасходыОбслуживающихПроизводств);
	СчетаЗатрат.Добавить(ПланыСчетов.Налоговый.КосвенныеРасходыПоВыявленномуБраку);
	СчетаЗатрат.Добавить(ПланыСчетов.Налоговый.РасходыНаПродажу);
	
	Запрос.УстановитьПараметр("Сч20",ПланыСчетов.Налоговый.КосвенныеРасходыОсновногоПроизводства);
	Запрос.УстановитьПараметр("Сч23",ПланыСчетов.Налоговый.КосвенныеРасходыВспомогательныхПроизводств);
	Запрос.УстановитьПараметр("Сч25",ПланыСчетов.Налоговый.КосвенныеОбщепроизводственныеРасходы);
	Запрос.УстановитьПараметр("Сч26",ПланыСчетов.Налоговый.КосвенныеОбщехозяйственныеРасходы);
	Запрос.УстановитьПараметр("Сч28",ПланыСчетов.Налоговый.КосвенныеРасходыПоВыявленномуБраку);
	Запрос.УстановитьПараметр("Сч29",ПланыСчетов.Налоговый.КосвенныеРасходыОбслуживающихПроизводств);
	Запрос.УстановитьПараметр("Сч44",ПланыСчетов.Налоговый.РасходыНаПродажу);
КонецЕсли;
	
	
	Запрос.УстановитьПараметр("СчетаЗатрат",   СчетаЗатрат);
	Запрос.УстановитьПараметр("ТранспортныеРасходы", Перечисления.ВидыРасходовНУ.ТранспортныеРасходы);
	
	Если ВидОтчета = 1 Тогда
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	НалоговыйОборотыДтКт.Период КАК ДатаОперации,
	|	НалоговыйОборотыДтКт.Регистратор КАК Расшифровка,
	|	ВЫБОР
	|		КОГДА НалоговыйОборотыДтКт.СубконтоДт1.ВидРасходовНУ = &ЕСН
	|			ТОГДА ""ЕСН""
	|		КОГДА НалоговыйОборотыДтКт.СубконтоДт1.ВидРасходовНУ = &СтраховыеВзносы
	|			ТОГДА ""Страховые взносы""
	|		КОГДА НалоговыйОборотыДтКт.СубконтоДт1.ВидРасходовНУ = &НалогиИСборы
	|			ТОГДА ""Налоги и сборы""
	|		КОГДА НалоговыйОборотыДтКт.СубконтоДт1.ВидРасходовНУ = &Командировочные
	|			ТОГДА ""Командировочные расходы""
	|		КОГДА НалоговыйОборотыДтКт.СубконтоДт1.ВидРасходовНУ = &Представительские
	|			ТОГДА ""Представительские расходы""
	|		КОГДА НалоговыйОборотыДтКт.СубконтоДт1.ВидРасходовНУ = &РасходыНаРекламу
	|			ТОГДА ""Расходы на рекламу""
	|		КОГДА НалоговыйОборотыДтКт.СубконтоДт1.ВидРасходовНУ = &РемонтОсновныхСредств
	|			ТОГДА ""Расходы на ремонт основных средств""
	|		КОГДА НалоговыйОборотыДтКт.СубконтоДт1.ВидРасходовНУ = &ОсвоениеПриродныхРесурсов
	|			ТОГДА ""Расходы на освоение природных ресурсов""
	|		КОГДА НалоговыйОборотыДтКт.СубконтоДт1.ВидРасходовНУ = &НаНИОКР
	|			ТОГДА ""Расходы на научные исследования и (или) опытно-конструкторские разработки""
	|		КОГДА НалоговыйОборотыДтКт.СубконтоДт1.ВидРасходовНУ = &НаНИОКР1_5
	|			ТОГДА ""Расходы на научные исследования и (или) опытно-конструкторские разработки, списанные с коэффициентом 1.5""
	|		КОГДА НалоговыйОборотыДтКт.СубконтоДт1.ВидРасходовНУ = &ОбязательноеИДобровольноеСтрахованиеИмущества
	|			ТОГДА ""Расходы на обязательное и добровольное страхование имущества""
	|		КОГДА НалоговыйОборотыДтКт.СубконтоДт1.ВидРасходовНУ = &АрендаФедеральногоИМуниципальногоИмущества
	|			ТОГДА ""Расходы  по  аренде федерального имущества""
	|		ИНАЧЕ ""Прочие""
	|	КОНЕЦ КАК ВидЗатрат,
	|	НалоговыйОборотыДтКт.СуммаОборот КАК Сумма,
	|	Налоговый.Содержание,
	|	Налоговый.НомерСтроки КАК НомерПроводки
	|ИЗ
	|	РегистрБухгалтерии.Налоговый.ОборотыДтКт(
	|			&ДатаНач,
	|			&ДатаКон,
	|			Запись,
	|			СчетДт В ИЕРАРХИИ (&СчетаЗатрат),
	|			&ВидСубконто,
	|			,
	|			,
	|			Организация = &Организация
	|				И СубконтоДт1.ВидРасходовНУ В (&ВидыЗатрат)
	|				И ВидУчетаДт = &ВидУчета) КАК НалоговыйОборотыДтКт
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрБухгалтерии.Налоговый КАК Налоговый
	|		ПО НалоговыйОборотыДтКт.Регистратор = Налоговый.Регистратор
	|			И НалоговыйОборотыДтКт.НомерСтроки = Налоговый.НомерСтроки
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	НалоговыйОборотыДтКт.Период,
	|	НалоговыйОборотыДтКт.Регистратор,
	|	""Убыток от реализации амортизируемого имущества"",
	|	НалоговыйОборотыДтКт.СуммаОборот,
	|	Налоговый.Содержание,
	|	Налоговый.НомерСтроки
	|ИЗ
	|	РегистрБухгалтерии.Налоговый.ОборотыДтКт(
	|			&ДатаНач,
	|			&ДатаКон,
	|			Запись,
	|			СчетДт В ИЕРАРХИИ (&Счет91),
	|			,
	|			СчетКт В ИЕРАРХИИ (&Счет97_2),
	|			,
	|			Организация = &Организация
	|				И ВидУчетаДт = &ВидУчета) КАК НалоговыйОборотыДтКт
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрБухгалтерии.Налоговый КАК Налоговый
	|		ПО НалоговыйОборотыДтКт.Регистратор = Налоговый.Регистратор
	|			И НалоговыйОборотыДтКт.НомерСтроки = Налоговый.НомерСтроки
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	НалоговыйОборотыДтКт.Период,
	|	НалоговыйОборотыДтКт.Регистратор,
	|	""Прочие косвенные расходы"",
	|	НалоговыйОборотыДтКт.СуммаОборот,
	|	Налоговый.Содержание,
	|	Налоговый.НомерСтроки
	|ИЗ
	|	РегистрБухгалтерии.Налоговый.ОборотыДтКт(
	|			&ДатаНач,
	|			&ДатаКон,
	|			Запись,
	|			СчетДт = &Счет91_2_9,
	|			,
	|			,
	|			,
	|			Организация = &Организация
	|				И ВидУчетаДт = &ВидУчета) КАК НалоговыйОборотыДтКт
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрБухгалтерии.Налоговый КАК Налоговый
	|		ПО НалоговыйОборотыДтКт.Регистратор = Налоговый.Регистратор
	|			И НалоговыйОборотыДтКт.НомерСтроки = Налоговый.НомерСтроки
	|
	|УПОРЯДОЧИТЬ ПО
	|	ДатаОперации,
	|	НомерПроводки";
	
Иначе
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	НалоговыйОборотыДтКт.Период КАК ДатаОперации,
	|	НалоговыйОборотыДтКт.Регистратор КАК Расшифровка,
	|	ВЫБОР
	|		КОГДА НалоговыйОборотыДтКт.СчетДт В ИЕРАРХИИ (&Сч20)
	|			ТОГДА ""Расходы основного производства""
	|		КОГДА НалоговыйОборотыДтКт.СчетДт В ИЕРАРХИИ (&Сч23)
	|			ТОГДА ""Расходы вспомогательного производства""
	|		КОГДА НалоговыйОборотыДтКт.СчетДт В ИЕРАРХИИ (&Сч25)
	|			ТОГДА ""Общепроизводственные расходы""
	|		КОГДА НалоговыйОборотыДтКт.СчетДт В ИЕРАРХИИ (&Сч26)
	|			ТОГДА ""Общехозяйственные расходы""
	|		ИНАЧЕ ""Расходы на продажу""
	|	КОНЕЦ КАК ВидЗатрат,
	|	НалоговыйОборотыДтКт.СуммаОборот КАК Сумма,
	|	Налоговый.Содержание,
	|	Налоговый.НомерСтроки КАК НомерПроводки,
	|	НалоговыйОборотыДтКт.СубконтоДт1.ВидРасходовНУ КАК ВидЗатратНУ
	|ИЗ
	|	РегистрБухгалтерии.Налоговый.ОборотыДтКт(
	|			&ДатаНач,
	|			&ДатаКон,
	|			Запись,
	|			СчетДт В ИЕРАРХИИ (&СчетаЗатрат),
	|			&ВидСубконто,
	|			,
	|			,
	|			Организация = &Организация
	|				И ВидУчетаДт = &ВидУчета
	|				И (НЕ СубконтоДт1.ВидРасходовНУ = &ТранспортныеРасходы)) КАК НалоговыйОборотыДтКт
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрБухгалтерии.Налоговый КАК Налоговый
	|		ПО НалоговыйОборотыДтКт.Регистратор = Налоговый.Регистратор
	|			И НалоговыйОборотыДтКт.НомерСтроки = Налоговый.НомерСтроки
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	НалоговыйОборотыДтКт.Период,
	|	НалоговыйОборотыДтКт.Регистратор,
	|	""Прочие косвенные расходы"",
	|	НалоговыйОборотыДтКт.СуммаОборот,
	|	Налоговый.Содержание,
	|	Налоговый.НомерСтроки,
	|	НалоговыйОборотыДтКт.СубконтоДт1.ВидПрочихДоходовИРасходов
	|ИЗ
	|	РегистрБухгалтерии.Налоговый.ОборотыДтКт(
	|			&ДатаНач,
	|			&ДатаКон,
	|			Запись,
	|			СчетДт = &Счет91_2_9,
	|			,
	|			,
	|			,
	|			Организация = &Организация
	|				И ВидУчетаДт = &ВидУчета) КАК НалоговыйОборотыДтКт
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрБухгалтерии.Налоговый КАК Налоговый
	|		ПО НалоговыйОборотыДтКт.Регистратор = Налоговый.Регистратор
	|			И НалоговыйОборотыДтКт.НомерСтроки = Налоговый.НомерСтроки
	|
	|УПОРЯДОЧИТЬ ПО
	|	ДатаОперации,
	|	НомерПроводки";
	
КонецЕсли;

	Результат = Запрос.Выполнить();

	ТаблицаОтладки = Новый ТаблицаЗначений;
	ТаблицаОтладки = Результат.Выгрузить();

	ОбластьПодвал        = Макет.ПолучитьОбласть("Подвал");
	ОбластьШапкаТаблицы  = Макет.ПолучитьОбласть("ШапкаТаблицы");
	ОбластьСтрока        = Макет.ПолучитьОбласть("Строка");

	ДокументРезультат.Вывести(ОбластьШапкаТаблицы);

	Выборка = Результат.Выбрать();

	СписокВидов = Новый СписокЗначений();
	Для каждого ЭлементСписка из ВидыРасходов Цикл

		Если ЭлементСписка.Пометка Тогда
			СписокВидов.Добавить(ЭлементСписка.Значение);
		КонецЕсли;

	КонецЦикла;

	ИтогоСумма = 0;

	Пока Выборка.Следующий() Цикл

		Если СписокВидов.НайтиПоЗначению(Выборка.ВидЗатрат) = Неопределено Тогда
			Продолжить;
		КонецЕсли;

		ОбластьСтрока.Параметры.ДатаОперации  = Выборка.ДатаОперации;
		ОбластьСтрока.Параметры.СтатьяРасхода = Выборка.ВидЗатрат + ?(ВидОтчета = 2, ": " + Выборка.ВидЗатратНУ, "");
		ОбластьСтрока.Параметры.ОбъектУчета   = ПолучитьЗапись(Выборка.Содержание,Выборка.Расшифровка);
		ОбластьСтрока.Параметры.Сумма         = Выборка.Сумма;
		ОбластьСтрока.Параметры.Расшифровка   = Выборка.Расшифровка;

		ИтогоСумма = ИтогоСумма + Выборка.Сумма;

		ДокументРезультат.Вывести(ОбластьСтрока);

	КонецЦикла;

	СтруктураЛиц = РегламентированнаяОтчетность.ОтветственныеЛицаОрганизаций(Организация, ДатаКон);

	ОбластьПодвал.Параметры.ОтветственныйЗаРегистры = СтруктураЛиц.ОтветственныйЗаРегистры;
	ОбластьПодвал.Параметры.ИтогоСумма              = ИтогоСумма;

	ДокументРезультат.Вывести(ОбластьПодвал);

КонецПроцедуры // СформироватьОтчет

Процедура ЗаполнитьСписокВидовРасходов() Экспорт

		// Заполним список видов расходов если он пуст.
	    ВидыРасходов.Очистить(); 
		
		Если ВидОтчета = 1 Тогда
			
		ВидыРасходов.Добавить("ЕСН");
		ВидыРасходов.Добавить("Страховые взносы");
		ВидыРасходов.Добавить("Налоги и сборы");
		ВидыРасходов.Добавить("Командировочные расходы");
		ВидыРасходов.Добавить("Представительские расходы");
		ВидыРасходов.Добавить("Расходы на рекламу");
		ВидыРасходов.Добавить("Расходы на ремонт основных средств");
		ВидыРасходов.Добавить("Расходы на освоение природных ресурсов");
		ВидыРасходов.Добавить("Расходы на научные исследования и (или) опытно-конструкторские разработки", "Расходы на НИОКР");
		ВидыРасходов.Добавить("Расходы на научные исследования и (или) опытно-конструкторские разработки, списанные с коэффициентом 1.5", "Расходы на НИОКР по Перечню правительства РФ");
		ВидыРасходов.Добавить("Расходы на обязательное и добровольное страхование имущества", "Расходы на страхование имущества");
		ВидыРасходов.Добавить("Расходы  по  аренде федерального имущества");
		ВидыРасходов.Добавить("Убыток от реализации амортизируемого имущества");
		ВидыРасходов.Добавить("Прочие");
		ВидыРасходов.Добавить("Прочие косвенные расходы");
		
	Иначе
		
		ВидыРасходов.Добавить("Расходы основного производства");
		ВидыРасходов.Добавить("Расходы вспомогательного производства");
		ВидыРасходов.Добавить("Общепроизводственные расходы");
		ВидыРасходов.Добавить("Общехозяйственные расходы");
		ВидыРасходов.Добавить("Расходы на продажу");
		ВидыРасходов.Добавить("Прочие косвенные расходы");
		
	КонецЕсли;
	

	ВидыРасходов.ЗаполнитьПометки(Истина);

КонецПроцедуры

Функция ПолучитьЗапись(Содержание, ДокументЗаписи)
	
	ЕстьСодержание = ЗначениеЗаполнено(Содержание);
	
	Текст = ?(ЕстьСодержание, Содержание + ", ", "") + "(" + ДокументЗаписи + ")";
	
	Возврат Текст;
	
КонецФункции           


////////////////////////////////////////////////////////////////////////////////
// ОПЕРАТОРЫ ОСНОВНОЙ ПРОГРАММЫ
// 

НП = Новый НастройкаПериода;

#КонецЕсли