////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ УПРАВЛЕНИЯ КОМАНДНОЙ ПАНЕЛИ ФОРМЫ

// Обработчик события Нажатие кнопки КнопкаВыполнить командной панели ОсновныеДействияФормы.
//
Процедура ОсновныеДействияФормыВыполнить(Кнопка)
	
	ЭтаФорма.Закрыть(Истина);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ФОРМЫ

// Обработчик события ПриИзменении элемента формы ВыбранаГруппировкаПоНоменклатуре.
//
Процедура ВыбранаГруппировкаПоНоменклатуреПриИзменении(Элемент)
	
	// Запоминаем текущую настройку
	Настройки = ПостроительОтчета.ПолучитьНастройки(Истина, Истина, Истина, Истина);

	// Перезаполнение объекта (с новым текстом запроса)
	ЗаполнитьНачальныеНастройки();
	
	// Восстанавливаем запомненную настройку
	ПостроительОтчета.УстановитьНастройки(Настройки, Истина, Истина, Истина, Истина);
	
	Если ВыбранаГруппировкаПоНоменклатуре Тогда
		ПостроительОтчета.ИзмеренияСтроки.Добавить("Номенклатура");	
		
		// уберем пометку с показателя КоличествоСобытий
		СтрокаКоличестваСобытий = ПоказателиОтчета.Найти("КоличествоСобытий", "ИмяПоказателя");
		Если СтрокаКоличестваСобытий <> Неопределено Тогда
			СтрокаКоличестваСобытий.ИспользованиеПоказателя = Ложь;
		КонецЕсли; 
		
	Иначе
		
		// поставим пометку на указателе КоличествоСобытий
		СтрокаКоличестваСобытий = ПоказателиОтчета.Найти("КоличествоСобытий", "ИмяПоказателя");
		Если СтрокаКоличестваСобытий <> Неопределено Тогда
			СтрокаКоличестваСобытий.ИспользованиеПоказателя = Истина;
		КонецЕсли; 
		
	КонецЕсли;
		
КонецПроцедуры


// Обработчик события ПриИзменении элемента формы ИспользоватьСвойстваИКатегории.
//
Процедура ИспользоватьСвойстваИКатегорииПриИзменении(Элемент)

	// Запоминаем текущую настройку
	Настройки = ПостроительОтчета.ПолучитьНастройки(Истина, Истина, Истина, Истина);

	// Перезаполнение объекта (с новым текстом запроса)
	ЗаполнитьНачальныеНастройки();

	// Восстанавливаем запомненную настройку
	ПостроительОтчета.УстановитьНастройки(Настройки, Истина, Истина, Истина, Истина);
	
	Оповестить("ИзмененТекстЗапроса", , ЭтаФорма);
	
КонецПроцедуры

// Обработчик события ПередУдалением элемента формы Отбор.
//
Процедура ОтборПередУдалением(Элемент, Отказ)
	
	Если Элемент.ТекущиеДанные <> Неопределено Тогда
		Если мСписокОтбора.НайтиПоЗначению(Элемент.ТекущиеДанные.Имя) <> Неопределено Тогда
			Отказ = Истина;
		КонецЕсли; 
	КонецЕсли; 
	
КонецПроцедуры

// Обработчик события ПередНачаломИзменения элемента формы Отбор.
//
Процедура ОтборПередНачаломИзменения(Элемент, Отказ)
	
	Если Элемент.ТекущиеДанные <> Неопределено Тогда
		Если мСписокОтбора.НайтиПоЗначению(Элемент.ТекущиеДанные.Имя) <> Неопределено И Элемент.ТекущаяКолонка.Имя = "Имя" Тогда
			Отказ = Истина;
		КонецЕсли; 
	КонецЕсли; 
	
КонецПроцедуры

// Обработчик события ПередНачаломДобавления элемента формы Показатели.
//
Процедура ПоказателиПередНачаломДобавления(Элемент, Отказ, Копирование)
	
	Отказ = Истина;
	
КонецПроцедуры

Процедура ПоказателиПередНачаломИзменения(Элемент, Отказ)
	
	// запретим изменять флаг использования для показателя КоличествоСобытий если выбрана группировка по номенклатуре
	Если ВыбранаГруппировкаПоНоменклатуре Тогда
		Если Элемент.ТекущиеДанные.ИмяПоказателя = "КоличествоСобытий" Тогда
			Отказ = Истина;	
		КонецЕсли;
	КонецЕсли
	
КонецПроцедуры


// Обработчик события ПередУдалением элемента формы Показатели.
//
Процедура ПоказателиПередУдалением(Элемент, Отказ)
	
	Отказ = Истина;
	
КонецПроцедуры

// Обработчик события Нажатие элемента формы КоманднаяПанель1.УстановитьФлажки.
//
Процедура КоманднаяПанель1УстановитьФлажки(Кнопка)

	Для каждого Показатель Из ПоказателиОтчета Цикл
		Показатель.ИспользованиеПоказателя = Истина;
	КонецЦикла; 
	
КонецПроцедуры

// Обработчик события Нажатие элемента формы КоманднаяПанель1.СнятьФлажки.
//
Процедура КоманднаяПанель1СнятьФлажки(Кнопка)

	Для каждого Показатель Из ПоказателиОтчета Цикл
		Показатель.ИспользованиеПоказателя = Ложь;
	КонецЦикла; 
	
КонецПроцедуры

// Обработчик события Нажатие элемента формы КнопкаНастройкаПериода.
//
Процедура КнопкаНастройкаПериодаНажатие(Элемент)

	Если НП.Редактировать() Тогда
		ДатаНачала    = НП.ДатаНачала;
		ДатаОкончания = НП.ДатаОкончания;
	КонецЕсли;

КонецПроцедуры