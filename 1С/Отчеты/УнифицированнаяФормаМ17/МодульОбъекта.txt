#ЕСЛИ КЛИЕНТ ТОГДА

// Процедура формирования печатной формы отчета
//
Процедура ОбновитьОтчет(ТабДокумент) Экспорт

    Если НЕ ЗначениеЗаполнено(Организация) Тогда
		Предупреждение("Не указана организация");
		Возврат;
	КонецЕсли;
    Если НЕ ЗначениеЗаполнено(Материал) Тогда
		Предупреждение("Не указан материал");
		Возврат;
	КонецЕсли;
	
	ТабДокумент.Очистить();
	ТабДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;
	
	ТекстЗапроса = "
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	РегТовары.Период КАК Период,
	|	РегТовары.Регистратор КАК Док,
	|	РегТовары.Регистратор.Номер КАК НомерДок,
	|	ПРЕДСТАВЛЕНИЕ(РегТовары.Регистратор) КАК ПечДок,
	|	РегТовары.КоличествоНачальныйОстаток КАК КолНачОст,
	|	РегТовары.КоличествоПриход КАК КолПриход,
	|	РегТовары.КоличествоРасход КАК КолРасход,
	|	РегТовары.КоличествоКонечныйОстаток КАК КолКонОст,
	|	РегТовары.Номенклатура КАК Номенклатура
	|ИЗ
	|	РегистрНакопления.ТоварыОрганизаций.ОстаткиИОбороты(
	|		&НачГраница,
	|		&КонГраница,
	|		Регистратор,
	|		,
	|		Номенклатура = &Материал
	|		    И Склад = &Склад
	|		    И Организация = &Орг) КАК РегТовары
	|
	|УПОРЯДОЧИТь ПО
	|	РегТовары.Период,
	|	РегТовары.Регистратор
	|";
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапроса;
	
	Запрос.УстановитьПараметр( "НачГраница", Новый Граница(НачДата, ВидГраницы.Включая));
	Запрос.УстановитьПараметр( "КонГраница", Новый Граница(КонецДня(КонДата), ВидГраницы.Включая));
	Запрос.УстановитьПараметр( "Орг",        Организация);
	Запрос.УстановитьПараметр( "Склад",      Склад);
	Запрос.УстановитьПараметр( "Материал",   Материал);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Макет = ПолучитьМакет("М17");
	Область = Макет.ПолучитьОбласть("Заголовок");
	
	ИнфоОрг = РегламентированнаяОтчетность.ПолучитьСведенияОбОрганизации(Организация, КонДата, "ОКПО");
	
	// Вывод заголовка
	Область.Параметры.Организация     = Организация;
	Область.Параметры.ПечОрганизация  = Строка(Организация);
	Область.Параметры.ПечКодОКПО      = ИнфоОрг.ОКПО;
	Область.Параметры.ПечКодМатериала = Материал.Код;
	Область.Параметры.ПечМатериал     = Строка(Материал);
	Область.Параметры.Материал = Материал;
	Область.Параметры.ПечДата  = СтрЗаменить( Формат( НачДата, "ДФ=дд_МММ_гг"), "_", " ") + " г.";
	Область.Параметры.Склад    = Склад;
	Область.Параметры.ПечСклад = Строка(Склад);
	
	ТабДокумент.Вывести(Область);
	
	// Вывод шапки таблицы
	Область = Макет.ПолучитьОбласть("ТабШапка");
	ТабДокумент.Вывести(Область);
	
	// Вывод первой строчки с начальным остатком
	ОбластьОст = Макет.ПолучитьОбласть("ТабНачОст");
	Обход = РезультатЗапроса.Выбрать();
	Обход.Следующий(); // Получили данные с нач. остатком
	
	ПечЕдИзм = Строка(Материал.ЕдиницаХраненияОстатков);
	
	ОбластьОст.Параметры.ПечДатаЗаписи = СтрЗаменить( Формат( НачДата, "ДФ=дд_МММ_гг"), "_", " ") + " г.";
	ОбластьОст.Параметры.ВидРасхода    = "Остаток на дату " + Формат( НачДата, "ДЛФ=DD");
	ОбластьОст.Параметры.ПечЕдИзм      = ПечЕдИзм;
	ОбластьОст.Параметры.ПечОстаток    = Обход.КолНачОст;
	
	ТабДокумент.Вывести(ОбластьОст);
	
	ОбластьДанные = Макет.ПолучитьОбласть("ТабСтрока");
	
	НизПервойСтраницы = Новый Массив;
	НизПервойСтраницы.Добавить( ОбластьДанные);
	НизПервойСтраницы.Добавить( Макет.ПолучитьОбласть("ТабНачОст"));
	НизПервойСтраницы.Добавить( Макет.ПолучитьОбласть("ТабНиз"));
	
	НизВторойСтраницы = Новый Массив;
	НизВторойСтраницы.Добавить( ОбластьДанные);
	НизВторойСтраницы.Добавить( Макет.ПолучитьОбласть("ТабНачОст"));
	НизВторойСтраницы.Добавить( Макет.ПолучитьОбласть("Подвал"));
	
	// Вывод строк таблицы
	ПерваяСтраница = Истина;
	НомПП = 1;
	Остаток = 0;
	Пока Истина Цикл
	
		Если ЗначениеЗаполнено(Обход.Док) Тогда
		
			ОбластьДанные.Параметры.ПечДатаЗаписи = СтрЗаменить( Формат( Обход.Период, "ДФ=дд_МММ_гг"), "_", " ") + " г.";
			ОбластьДанные.Параметры.ПечДок     = Обход.ПечДок;
			ОбластьДанные.Параметры.ПечНомДок  = Обход.НомерДок;
			ОбластьДанные.Параметры.Док        = Обход.Док;
			ОбластьДанные.Параметры.ПечЕдИзм   = ПечЕдИзм;
			ОбластьДанные.Параметры.ПечНомПП   = НомПП;
			НомПП = НомПП + 1;
			
			ОбластьДанные.Параметры.ПечПриход  = Обход.КолПриход;
			ОбластьДанные.Параметры.ПечРасход  = Обход.КолРасход;
			ОбластьДанные.Параметры.ПечОстаток = Обход.КолКонОст;
			
			Если    ПерваяСтраница И ФормированиеПечатныхФорм.ПроверитьВыводТабличногоДокумента(ТабДокумент, НизПервойСтраницы)
			 ИЛИ НЕ ПерваяСтраница И ФормированиеПечатныхФорм.ПроверитьВыводТабличногоДокумента(ТабДокумент, НизВторойСтраницы) Тогда
			Иначе
				ПерваяСтраница = Ложь;
				Область = Макет.ПолучитьОбласть("ТабНиз");
				ТабДокумент.Вывести(Область);
				ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц();
				Область = Макет.ПолучитьОбласть("ОборотЗаголовок");
				ТабДокумент.Вывести(Область);
				Область = Макет.ПолучитьОбласть("ТабШапка");
				ТабДокумент.Вывести(Область);
			КонецЕсли;
			
			ТабДокумент.Вывести(ОбластьДанные);
			
		КонецЕсли;
		
		Остаток = Обход.КолКонОст;
		Если НЕ Обход.Следующий() Тогда
			Прервать;
		КонецЕсли;
		
	КонецЦикла;
	
	ОбластьОст.Параметры.ПечДатаЗаписи = СтрЗаменить( Формат( КонДата, "ДФ=дд_МММ_гг"), "_", " ") + " г.";
	ОбластьОст.Параметры.ВидРасхода    = "Остаток на дату " + Формат( КонДата, "ДЛФ=DD");
	ОбластьОст.Параметры.ПечЕдИзм      = ПечЕдИзм;
	ОбластьОст.Параметры.ПечОстаток    = Остаток;
	
	ТабДокумент.Вывести(ОбластьОст);
	
	Область = Макет.ПолучитьОбласть("ТабПустаяСтрока");
	Если ПерваяСтраница Тогда
	
		Пока ФормированиеПечатныхФорм.ПроверитьВыводТабличногоДокумента(ТабДокумент, НизПервойСтраницы, Ложь) Цикл
			ТабДокумент.Вывести(Область);
		КонецЦикла;
		Область = Макет.ПолучитьОбласть("ТабНиз");
		ТабДокумент.Вывести(Область);
		
		// Печать оборотной стороны формы
		ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		Область = Макет.ПолучитьОбласть("ОборотЗаголовок");
		ТабДокумент.Вывести(Область);
		Область = Макет.ПолучитьОбласть("ТабШапка");
		ТабДокумент.Вывести(Область);
	
	КонецЕсли;
	
	Область = Макет.ПолучитьОбласть("ТабПустаяСтрока");
	
	МассивПоследняяСтраница = Новый Массив;
	МассивПоследняяСтраница.Добавить( Область);
	МассивПоследняяСтраница.Добавить( Макет.ПолучитьОбласть("ТабНиз"));
	МассивПоследняяСтраница.Добавить( Макет.ПолучитьОбласть("Подвал"));
	
	Пока ФормированиеПечатныхФорм.ПроверитьВыводТабличногоДокумента(ТабДокумент, МассивПоследняяСтраница, Ложь) Цикл
		ТабДокумент.Вывести(Область);
	КонецЦикла;
	
	Область = Макет.ПолучитьОбласть("ТабНиз");
	ТабДокумент.Вывести(Область);
	
	Область = Макет.ПолучитьОбласть("Подвал");
	ТабДокумент.Вывести(Область);
	
	//ТабДокумент
	
КонецПроцедуры // ОбновитьОтчет()

#КОНЕЦЕСЛИ