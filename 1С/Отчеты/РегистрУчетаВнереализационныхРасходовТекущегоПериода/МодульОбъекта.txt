#Если Клиент Тогда
Перем НП Экспорт;

// Выполняет запрос и формирует табличный документ-результат отчета
// в соответствии с настройками, заданными значениями реквизитов отчета.
//
// Параметры:
//	ДокументРезультат - табличный документ, формируемый отчетом
//	ПоказыватьЗаголовок - признак видимости строк с заголовком отчета
//	ВысотаЗаголовка - параметр, через который возвращается высота заголовка в строках 
//
Процедура СформироватьОтчет(ДокументРезультат, ПоказыватьЗаголовок, ВысотаЗаголовка, ТолькоЗаголовок = Ложь) Экспорт

	ДокументРезультат.Очистить();

	Макет = ПолучитьМакет("Отчет");

	ОбластьЗаголовок  = Макет.ПолучитьОбласть("Заголовок");

	ОбластьЗаголовок.Параметры.НачалоПериода       = Формат(ДатаНач, "ДФ=dd.MM.yyyy");
	ОбластьЗаголовок.Параметры.КонецПериода        = Формат(ДатаКон, "ДФ=dd.MM.yyyy");
	ОбластьЗаголовок.Параметры.НазваниеОрганизации = Организация.НаименованиеПолное;
	ОбластьЗаголовок.Параметры.ИННОрганизации      = "" + Организация.ИНН + " / " + Организация.КПП;
	ДокументРезультат.Вывести(ОбластьЗаголовок);

	// Параметр для показа заголовка
	ВысотаЗаголовка = ДокументРезультат.ВысотаТаблицы;

	// Когда нужен только заголовок:
	Если ТолькоЗаголовок Тогда
		Возврат;
	КонецЕсли;

	Если ЗначениеЗаполнено(ВысотаЗаголовка) Тогда
		ДокументРезультат.Область("R1:R" + ВысотаЗаголовка).Видимость = ПоказыватьЗаголовок;
	КонецЕсли;
    	
	ФормированиеРезервовПоСомнительнымДолгам = Новый Массив;
	ФормированиеРезервовПоСомнительнымДолгам.Добавить(Перечисления.ВидыПрочихДоходовИРасходов.ПрочиеВнереализационныеДоходыРасходы);
	ФормированиеРезервовПоСомнительнымДолгам.Добавить(Перечисления.ВидыПрочихДоходовИРасходов.ОтчисленияВОценочныеРезервы);
	ФормированиеРезервовПоСомнительнымДолгам.Добавить(Перечисления.ВидыПрочихДоходовИРасходов.СписаниеДебиторскойКредиторскойЗадолженности);
		
	Запрос = Новый Запрос;

	Запрос.УстановитьПараметр("ПроцентыПоДолговымОбязательствам", Перечисления.ВидыПрочихДоходовИРасходов.ПроцентыКПолучениюУплате);
	Запрос.УстановитьПараметр("ФормированиеРезервовПоСомнительнымДолгам", ФормированиеРезервовПоСомнительнымДолгам);
	Запрос.УстановитьПараметр("НДСПоСписаннойКредиторскойЗадолженности", Перечисления.ВидыПрочихДоходовИРасходов.НДСПоСписаннойКредиторскойЗадолженности);
	Запрос.УстановитьПараметр("ОперацииСФинансовымиИнструментамиСрочныхСделокОР", Перечисления.ВидыПрочихДоходовИРасходов.ДоходыРасходыПоОперациямСФинансовымиИнструментамиСрочныхСделокОР);
	Запрос.УстановитьПараметр("ОперацииСФинансовымиИнструментамиСрочныхСделок", Перечисления.ВидыПрочихДоходовИРасходов.ДоходыРасходыПоОперациямСФинансовымиИнструментамиСрочныхСделок);
	Запрос.УстановитьПараметр("СодержаниеИмуществаПереданногоВАренду", Перечисления.ВидыПрочихДоходовИРасходов.ДоходыРасходыСвязанныеСоСдачейИмуществаВАренду);
	Запрос.УстановитьПараметр("ЛиквидацияВыводимыхИзЭксплуатацииОсновныхСредств", Перечисления.ВидыПрочихДоходовИРасходов.ДоходыРасходыСвязанныеСЛиквидациейОсновныхСредств);
	Запрос.УстановитьПараметр("УбыткиПрошлыхЛет", Перечисления.ВидыПрочихДоходовИРасходов.ПрибыльУбытокПрошлыхЛет);
	Запрос.УстановитьПараметр("СуммыБезнадежныхДолгов", Перечисления.ВидыПрочихДоходовИРасходов.СписаниеДебиторскойКредиторскойЗадолженности);
	Запрос.УстановитьПараметр("ПрочиеУбытки", Перечисления.ВидыПрочихДоходовИРасходов.ПрочиеУбыткиПриравненныеКВнереализационнымРасходам);
	Запрос.УстановитьПараметр("УслугиБанков", Перечисления.ВидыПрочихДоходовИРасходов.РасходыНаУслугиБанков);
	Запрос.УстановитьПараметр("КурсовыеРазницы", Перечисления.ВидыПрочихДоходовИРасходов.КурсовыеРазницы);
	Запрос.УстановитьПараметр("Штрафы", Перечисления.ВидыПрочихДоходовИРасходов.ВозмещениеУбытковКПолучениюУплате);
	Запрос.УстановитьПараметр("ДатаНач",      НачалоДня(ДатаНач));
	Запрос.УстановитьПараметр("ДатаКон",      БухгалтерскийУчет.КонецПериодаОтчета(ДатаКон));
	Запрос.УстановитьПараметр("Организация",  Организация);
	Запрос.УстановитьПараметр("Счет91_02_6",  ПланыСчетов.Налоговый.ВнереализационныеРасходы);
	Запрос.УстановитьПараметр("Счет96",       ПланыСчетов.Налоговый.РезервыПредстоящихРасходов);
	Запрос.УстановитьПараметр("ВидУчета",     Перечисления.ВидыУчетаПоПБУ18.НУ);
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	НалоговыйОборотыДтКт.Период КАК ДатаОперации,
	|	НалоговыйОборотыДтКт.Регистратор КАК Расшифровка,
	|	ВЫБОР 
	|		КОГДА НалоговыйОборотыДтКт.СубконтоДт1.ВидПрочихДоходовИРасходов = &ПроцентыПоДолговымОбязательствам ТОГДА 
	|			""Проценты по долговым обязательствам"" 
	|		КОГДА НалоговыйОборотыДтКт.СубконтоДт1.ВидПрочихДоходовИРасходов В ИЕРАРХИИ (&ФормированиеРезервовПоСомнительнымДолгам) И НалоговыйОборотыДтКт.СчетКт = &Счет96 ТОГДА 
	|			""Формирование резервов по сомнительным долгам"" 
	|		КОГДА НалоговыйОборотыДтКт.СубконтоДт1.ВидПрочихДоходовИРасходов = &НДСПоСписаннойКредиторскойЗадолженности ТОГДА 
	|			""НДС по списанной кредиторской задолженности"" 
	|		КОГДА НалоговыйОборотыДтКт.СубконтоДт1.ВидПрочихДоходовИРасходов = &ОперацииСФинансовымиИнструментамиСрочныхСделокОР ТОГДА 
	|			""Доходы расходы по операциям с финансовыми инструментами срочных сделок, обращающимися на организованном рынке"" 
	|		КОГДА НалоговыйОборотыДтКт.СубконтоДт1.ВидПрочихДоходовИРасходов = &ОперацииСФинансовымиИнструментамиСрочныхСделок ТОГДА 
	|			""Доходы расходы по операциям с финансовыми инструментами срочных сделок, не обращающимися на организованном рынке"" 
	|		КОГДА НалоговыйОборотыДтКт.СубконтоДт1.ВидПрочихДоходовИРасходов = &СодержаниеИмуществаПереданногоВАренду ТОГДА 
	|			""Содержание имущества, переданного в аренду"" 
	|		КОГДА НалоговыйОборотыДтКт.СубконтоДт1.ВидПрочихДоходовИРасходов = &ЛиквидацияВыводимыхИзЭксплуатацииОсновныхСредств ТОГДА 
	|			""Ликвидация выводимых из эксплуатации основных средств"" 
	|		КОГДА НалоговыйОборотыДтКт.СубконтоДт1.ВидПрочихДоходовИРасходов = &УбыткиПрошлыхЛет ТОГДА 
	|			""Убытки прошлых лет"" 
	|		КОГДА НалоговыйОборотыДтКт.СубконтоДт1.ВидПрочихДоходовИРасходов = &СуммыБезнадежныхДолгов ТОГДА 
	|			""Суммы безнадежных долгов, не покрытые за счет средств резерва"" 
	|		КОГДА НалоговыйОборотыДтКт.СубконтоДт1.ВидПрочихДоходовИРасходов = &ПрочиеУбытки ТОГДА 
	|			""Прочие убытки, приравненные к внереализационным расходам"" 
	|		КОГДА НалоговыйОборотыДтКт.СубконтоДт1.ВидПрочихДоходовИРасходов = &УслугиБанков ТОГДА 
	|			""Услуги банков""
	|		КОГДА НалоговыйОборотыДтКт.СубконтоДт1.ВидПрочихДоходовИРасходов = &КурсовыеРазницы ТОГДА 
	|			""Курсовые разницы""
	|		КОГДА НалоговыйОборотыДтКт.СубконтоДт1.ВидПрочихДоходовИРасходов = &Штрафы ТОГДА 
	|			""Штрафы, пени, неустойки""
	|		ИНАЧЕ 
	|			""Прочие внереализационные расходы"" 
	|	КОНЕЦ КАК ВидЗатрат,
	|	НалоговыйОборотыДтКт.СуммаОборот КАК Сумма,
	|	Налоговый.Содержание,
	|	Налоговый.НомерСтроки КАК НомерПроводки
	|ИЗ
	|	РегистрБухгалтерии.Налоговый.ОборотыДтКт(&ДатаНач, &ДатаКон, Запись, СчетДт = &Счет91_02_6, , , , Организация = &Организация И ВидУчетаДт = &ВидУчета) КАК НалоговыйОборотыДтКт
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрБухгалтерии.Налоговый КАК Налоговый
	|		ПО НалоговыйОборотыДтКт.Регистратор = Налоговый.Регистратор И НалоговыйОборотыДтКт.НомерСтроки = Налоговый.НомерСтроки
	|
	|ГДЕ
	|	НалоговыйОборотыДтКт.СубконтоДт1.ПринятиеКналоговомуУчету
	|
	|УПОРЯДОЧИТЬ ПО
	|	ДатаОперации,
	|	НомерПроводки
	|";
	Результат = Запрос.Выполнить();

	ТаблицаОтладки = Новый ТаблицаЗначений;
	ТаблицаОтладки = Результат.Выгрузить();
	
	ОбластьПодвал        = Макет.ПолучитьОбласть("Подвал");
	ОбластьШапкаТаблицы  = Макет.ПолучитьОбласть("ШапкаТаблицы");
	ОбластьСтрока        = Макет.ПолучитьОбласть("Строка");

	ДокументРезультат.Вывести(ОбластьШапкаТаблицы);

	Выборка = Результат.Выбрать();

	СписокВидов = Новый СписокЗначений();
	Для каждого ЭлементСписка из ВидыРасходов Цикл
		Если ЭлементСписка.Пометка Тогда
			СписокВидов.Добавить(ЭлементСписка.Значение);
		КонецЕсли;
	КонецЦикла;
    ИтогоСумма = 0;
	Пока Выборка.Следующий() Цикл

		Если СписокВидов.НайтиПоЗначению(Выборка.ВидЗатрат) = Неопределено Тогда
			Продолжить;
		КонецЕсли;

		ОбластьСтрока.Параметры.ДатаОперации        = Выборка.ДатаОперации;
		ОбластьСтрока.Параметры.СтатьяРасхода       = Выборка.ВидЗатрат;
		ОбластьСтрока.Параметры.ОбъектУчета			= Выборка.Содержание;
		ОбластьСтрока.Параметры.Сумма               = Выборка.Сумма;
		ИтогоСумма = ИтогоСумма + Выборка.Сумма;
		ОбластьСтрока.Параметры.Расшифровка         = Выборка.Расшифровка;

		ДокументРезультат.Вывести(ОбластьСтрока);

	КонецЦикла;

	СтруктураЛиц = РегламентированнаяОтчетность.ОтветственныеЛицаОрганизаций(Организация, ДатаКон);
	ОбластьПодвал.Параметры.ОтветственныйЗаРегистры = СтруктураЛиц.ОтветственныйЗаРегистры;
	ОбластьПодвал.Параметры.ИтогоСумма = ИтогоСумма;
	ДокументРезультат.Вывести(ОбластьПодвал);

КонецПроцедуры // СформироватьОтчет

////////////////////////////////////////////////////////////////////////////////
// ОПЕРАТОРЫ ОСНОВНОЙ ПРОГРАММЫ
// 

НП           = Новый НастройкаПериода;
#КонецЕсли