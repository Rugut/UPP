#Если Клиент Тогда

// Настройка - ссылка на справочник настроек
Перем мНастройка Экспорт;

// Все возможные группировки
Перем мДеревоГруппировки Экспорт;
// Все возможные показатели
Перем мТаблицаПоказатели Экспорт; 
// Все возможные фильтры
Перем мДеревоФильтры Экспорт;

// Все возможные описания типов
Перем мТаблицаТипы Экспорт;

//Запрос к базе
Перем Запрос;

Перем мСтруктураМетаданныеРегистраРесурсы;
Перем мСтруктураМетаданныеРегистраИзмерения;

// Структура, содержащая допустимые типы фильтров
Перем мСтруктураТиповФильтров Экспорт;
// Значение типа фильтра по умолчанию
Перем мТипФильтраПоУмолчанию Экспорт;
// Структура, содержащая допустимые поля реквизитов, по которым
// выполняется сортировка показателей (начост, приход, расход и т.д.)
Перем мСтруктураНазванийПолейСортировки Экспорт;
// Значения группировок, выводимые в качестве заголовков столбцов.
Перем мЗначимыеГруппировкиГор;

// Имя отчета в метаданных
Перем мИмяОтчета Экспорт;

Перем мНазваниеОтчета Экспорт;

// Список имен быстрых отборов
Перем мСписокОбязательныхОтборов Экспорт;


////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ
// 
// Определяет пустую ссылку одного из типов значений
// Параметры
//  ОписаниеТиповЗначения - Описание типов
//
// Возвращаемое значение:
//   Пустая ссылка (справочник, перечисление)
//
Функция ОпределитьПустоеЗначениеТипа(ОписаниеТиповЗначения) Экспорт

	Если ТипЗнч(ОписаниеТиповЗначения) = Тип("ОписаниеТипов") Тогда
		
		МассивТипов = ОписаниеТиповЗначения.Типы();
		Для каждого Тип Из МассивТипов Цикл
		
			Если Тип <> Тип("СписокЗначений") Тогда
			
				Возврат Новый(Тип);
			
			КонецЕсли; 
		
		КонецЦикла; 

	Иначе

		Возврат Неопределено;
		
	КонецЕсли; 

КонецФункции //ОпределитьПустоеЗначениеТипа()

// Функция возвращает ВидОтбора по строке
// 
// Параметры
//  СтрокаОтбора, строка вида отбора
// 
// Возвращаемое значение 
//  ЗначениеВозврата, ВидОтбора
Функция ВозвратитьВидаОтбораПоСтроке(СтрокаОтбора) Экспорт

	ЗначениеВозврата = Неопределено;

	СтрокаОтбора = ВРег(СтрокаОтбора);

	Если СтрокаОтбора = "РАВНО" Тогда
		ЗначениеВозврата = ВидСравнения.Равно;
	ИначеЕсли СтрокаОтбора = "НЕ РАВНО" Тогда
		ЗначениеВозврата = ВидСравнения.НеРавно;
	ИначеЕсли СтрокаОтбора = "В СПИСКЕ" Тогда
		ЗначениеВозврата = ВидСравнения.ВСписке;
	ИначеЕсли СтрокаОтбора = "НЕ В СПИСКЕ" Тогда
		ЗначениеВозврата = ВидСравнения.НеВСписке;
	ИначеЕсли СтрокаОтбора = "В ГРУППЕ ИЗ СПИСКА" Тогда
		ЗначениеВозврата = ВидСравнения.ВСпискеПоИерархии;
	ИначеЕсли СтрокаОтбора = "НЕ В ГРУППЕ ИЗ СПИСКА" Тогда
		ЗначениеВозврата = ВидСравнения.НеВСпискеПоИерархии;
	ИначеЕсли СтрокаОтбора = "В ГРУППЕ" Тогда
		ЗначениеВозврата = ВидСравнения.ВИерархии;
	ИначеЕсли СтрокаОтбора = "НЕ В ГРУППЕ" Тогда
		ЗначениеВозврата = ВидСравнения.НеВИерархии;
	КонецЕсли;

	Возврат ЗначениеВозврата;

КонецФункции // ВозвратитьСтрокуВидаОтбора()

// Процедура устанавливает значения быстрых отборов формы в соответствии
//  с табличной частью ФильтрыОтчета
// 
// Параметры
//  ФормаБыстрыхОтборов - форма, на которой расположены контролы быстрых отборов
//
// Возвращаемые значения
//  НЕТ
Процедура ПроставитьЗначенияБыстрыхОтборов(ФормаБыстрыхОтборов) Экспорт

	СписокОбработанныхОтборов = Новый СписокЗначений;
	
	Для каждого Строки Из ФильтрыОтчета Цикл

		Если мСписокОбязательныхОтборов.НайтиПоЗначению(Строки.ИмяФильтра) = Неопределено Тогда
			Продолжить;
		КонецЕсли; 
	
		Если СписокОбработанныхОтборов.НайтиПоЗначению(Строки.ИмяФильтра) <> Неопределено Тогда
			Продолжить;
		КонецЕсли; 
		
		ФормаБыстрыхОтборов.ЭлементыФормы["Флажок" + Строки.ИмяФильтра].Значение = Строки.ИспользованиеФильтра;
		ФормаБыстрыхОтборов.ЭлементыФормы["ВидСравнения" + Строки.ИмяФильтра].Значение = ВозвратитьВидаОтбораПоСтроке(Строки.ТипФильтра);
		Если Строки.ТипФильтра = "РАВНО" ИЛИ Строки.ТипФильтра = "НЕ РАВНО" Тогда
			ФормаБыстрыхОтборов.ЭлементыФормы["Значение" + Строки.ИмяФильтра].Значение = Строки.ЗначениеФильтра;
		Иначе
			СписокЗначенийОтбора = Новый СписокЗначений;
			Для каждого СтрокаФильтров Из ФильтрыОтчета Цикл
				Если СтрокаФильтров.ИмяФильтра = Строки.ИмяФильтра Тогда
					СписокЗначенийОтбора.Добавить(СтрокаФильтров.ЗначениеФильтра);
				КонецЕсли;
			КонецЦикла;
			ФормаБыстрыхОтборов.ЭлементыФормы["Значение" + Строки.ИмяФильтра].Значение = СписокЗначенийОтбора;
		КонецЕсли;

		СписокОбработанныхОтборов.Добавить(Строки.ИмяФильтра);
	
	КонецЦикла; 

КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОБЩЕГО НАЗНАЧЕНИЯ
//

// Процедура подготавливает таблицы, содержащие все возможные группировки, показатели отчета
// и описания типов возможных фильтров. Заполнение таблиц производится по данным макета отчета,
// "ПараметрыОтчетаТоварныйКалендарь".
Процедура ЗаполнитьНачальныеНастройки(ВосстановлениеНастройки = Ложь) Экспорт

	мНазваниеОтчета = "Оперативный товарный календарь";

	мТаблицаПоказатели.Очистить();
	мДеревоГруппировки.Строки.Очистить();
	мДеревоФильтры.Строки.Очистить();

	мТаблицаТипы.Очистить();

	//Создается структура, содержащая данные об измерениях регистров.

	мСтруктураМетаданныеРегистраИзмерения= Новый Структура;
	СтруктураМетаданныеРегистрЗаказыПокупателей=Новый Структура;
	СтруктураМетаданныеРегистрЗаказыПоставщикам=Новый Структура;
	СтруктураМетаданныеРегистрОстаткиТоваровКомпании=Новый Структура;

	Для Каждого Измерение Из Метаданные.РегистрыНакопления["ЗаказыПокупателей"].Измерения Цикл
		СтруктураМетаданныеРегистрЗаказыПокупателей.Вставить(Измерение.Имя, Новый Структура);
		СтруктураМетаданныеРегистрЗаказыПокупателей[Измерение.Имя].Вставить("Представление",Измерение.Представление());
		СтруктураМетаданныеРегистрЗаказыПокупателей[Измерение.Имя].Вставить("Тип",Измерение.Тип);
	КонецЦикла;

	мСтруктураМетаданныеРегистраИзмерения.Вставить("ЗаказыПокупателей",СтруктураМетаданныеРегистрЗаказыПокупателей);

	Для Каждого Измерение Из Метаданные.РегистрыНакопления["ЗаказыПоставщикам"].Измерения Цикл
		СтруктураМетаданныеРегистрЗаказыПоставщикам.Вставить(Измерение.Имя, Новый Структура);
		СтруктураМетаданныеРегистрЗаказыПоставщикам[Измерение.Имя].Вставить("Представление",Измерение.Представление());
		СтруктураМетаданныеРегистрЗаказыПоставщикам[Измерение.Имя].Вставить("Тип",Измерение.Тип);
	КонецЦикла;

	мСтруктураМетаданныеРегистраИзмерения.Вставить("ЗаказыПоставщикам",СтруктураМетаданныеРегистрЗаказыПоставщикам);

	Для Каждого Измерение Из Метаданные.РегистрыНакопления["ТоварыНаСкладах"].Измерения Цикл
		СтруктураМетаданныеРегистрОстаткиТоваровКомпании.Вставить(Измерение.Имя, Новый Структура);
		СтруктураМетаданныеРегистрОстаткиТоваровКомпании[Измерение.Имя].Вставить("Представление",Измерение.Представление());
		СтруктураМетаданныеРегистрОстаткиТоваровКомпании[Измерение.Имя].Вставить("Тип",Измерение.Тип);
	КонецЦикла;

	мСтруктураМетаданныеРегистраИзмерения.Вставить("ТоварыНаСкладах",СтруктураМетаданныеРегистрОстаткиТоваровКомпании);

	МассивВсехТипов = Новый Массив;

	МакетПараметрыОтчетов = ПолучитьМакет("ПараметрыОтчетаТоварныйКалендарь");

	ОбластьПоказатели = МакетПараметрыОтчетов.ПолучитьОбласть("Показатели");

	ОбластьПоказателиВысота = ОбластьПоказатели.ВысотаТаблицы;

	Для НСтр = 1 По ОбластьПоказателиВысота Цикл
		СтрПоказатели = мТаблицаПоказатели.Добавить();
		СтрПоказатели.ИмяПоля           = СокрЛП(ОбластьПоказатели.Область(НСтр, 1).Текст);
		СтрПоказатели.ПредставлениеПоля = СокрЛП(ОбластьПоказатели.Область(НСтр, 2).Текст);
		СтрПоказатели.ОписаниеПоля      = СокрЛП(ОбластьПоказатели.Область(НСтр, 3).Текст);
		СтрПоказатели.ВклПоУмолчанию    = Булево(СокрЛП(ОбластьПоказатели.Область(НСтр, 4).Текст));
		СтрПоказатели.ФорматнаяСтрока   = СокрЛП(ОбластьПоказатели.Область(НСтр, 5).Текст);

		Если ПустаяСтрока(СтрПоказатели.ОписаниеПоля) Тогда
			СтрПоказатели.ОписаниеПоля = СтрПоказатели.ИмяПоля;
		КонецЕсли;

		// Сразу же добавим показатели
		НоваяСтрока = ПоказателиОтчета.Добавить();
		НоваяСтрока.ИмяПоказателя           = СтрПоказатели.ИмяПоля;
		НоваяСтрока.ПредставлениеПоказателя = СтрПоказатели.ПредставлениеПоля;
		НоваяСтрока.ОписаниеПоказателя      = СтрПоказатели.ОписаниеПоля;
		
	КонецЦикла;

	ОбластьТипов = МакетПараметрыОтчетов.ПолучитьОбласть("Типы");
	ОбластьТиповВысота=ОбластьТипов.ВысотаТаблицы;

	ТекГруппировка="";
	МассивТипов=Новый Массив;
	СтрТаблицаТипы="";

	Для Нстр = 1 По ОбластьТиповВысота цикл
		Группировка=СокрЛП(ОбластьТипов.Область(НСтр, 1).Текст);
		Если Группировка<>ТекГруппировка Тогда
			Если МассивТипов.Количество()>0 Тогда
				СтрТаблицаТипы.ОписаниеТипов=Новый ОписаниеТипов(МассивТипов);
			КонецЕсли;

			СтрТаблицаТипы=мТаблицаТипы.Добавить();
			ТекГруппировка=Группировка;
			МассивТипов=Новый Массив;

			СтрТаблицаТипы.ИмяПоля=Группировка;
			ТекстТипа=СокрЛП(ОбластьТипов.Область(НСтр, 2).Текст);
			Если (Найти(ТекстТипа,".")=0) И (ТекстТипа<>"Число") И (ТекстТипа<>"Строка") И (ТекстТипа<>"Дата") Тогда

				Для Каждого Элемент Из Метаданные[ТекстТипа] Цикл
					МассивТипов.Добавить(Тип(Лев(ТекстТипа,СтрДлина(ТекстТипа)-1)+"Ссылка."+Элемент.Имя));
				КонецЦикла;

			Иначе

				МассивТипов.Добавить(Тип(ТекстТипа));

			КонецЕсли;

		Иначе
			ТекстТипа=СокрЛП(ОбластьТипов.Область(НСтр, 2).Текст);
			Если (Найти(ТекстТипа,".")=0) И (ТекстТипа<>"Число") И (ТекстТипа<>"Строка") И (ТекстТипа<>"Дата") Тогда

				Для Каждого Элемент Из Метаданные[ТекстТипа] Цикл
					МассивТипов.Добавить(Тип(Лев(ТекстТипа,СтрДлина(ТекстТипа)-1)+"Ссылка."+Элемент.Имя));
				КонецЦикла;

			Иначе

				МассивТипов.Добавить(Тип(ТекстТипа));

			КонецЕсли;

		КонецЕсли;

	КонецЦикла;

	Если МассивТипов.Количество()>0 Тогда
		СтрТаблицаТипы.ОписаниеТипов=Новый ОписаниеТипов(МассивТипов);
	КонецЕсли;

	ОбластьГруппировки = МакетПараметрыОтчетов.ПолучитьОбласть("Группировки");
	ОбластьГруппировкиВысота = ОбластьГруппировки.ВысотаТаблицы;

	МассивКатегории = Новый Массив;
	МассивКатегории.Добавить(Тип("СправочникСсылка.КатегорииОбъектов"));
	ОписаниеТиповКатегории = Новый ОписаниеТипов(МассивКатегории);

	Запрос = Новый Запрос;

	Запрос.Текст = "";

	МассивНазначений = Новый Массив;

	Для НСтр = 1 По ОбластьГруппировкиВысота Цикл
		СтрГруппировки = мДеревоГруппировки.Строки.Добавить();
		СтрГруппировки.ИмяПоля           = СокрЛП(ОбластьГруппировки.Область(НСтр, 1).Текст);
		СтрГруппировки.ПредставлениеПоля = СокрЛП(ОбластьГруппировки.Область(НСтр, 2).Текст);
		СтрГруппировки.ОписаниеПоля      = СокрЛП(ОбластьГруппировки.Область(НСтр, 3).Текст);
		СтрГруппировки.ВклПоУмолчанию    = Булево(СокрЛП(ОбластьГруппировки.Область(НСтр, 4).Текст));
		СтрГруппировки.РассчитыватьИтоги = Булево(СокрЛП(ОбластьГруппировки.Область(НСтр, 5).Текст));
		СтрГруппировки.ТипИтога = ?(Булево(СокрЛП(ОбластьГруппировки.Область(НСтр, 6).Текст)),"Иерархия", "Элементы");

		ИмяПоля = СтрГруппировки.ИмяПоля;

		Если ПустаяСтрока(СтрГруппировки.ОписаниеПоля) Тогда
			СтрГруппировки.ОписаниеПоля = СтрГруппировки.ИмяПоля;
		КонецЕсли;

		СтрВыводитьПустые = СокрЛП(ОбластьГруппировки.Область(НСтр, 10).Текст);
		Если Нрег(СтрВыводитьПустые) = Нрег("Истина")
			Или Нрег(СтрВыводитьПустые) = Нрег("Ложь") Тогда
			СтрГруппировки.ВыводитьПустые = Булево(СтрВыводитьПустые);
		Иначе
			СтрГруппировки.ВыводитьПустые = СтрВыводитьПустые;
		КонецЕсли;

		Если СтрГруппировки.ВыводитьПустые = Ложь Тогда
			СтрГруппировки.ВыводитьПустыеСвязаннаяГруппировка = СокрЛП(ОбластьГруппировки.Область(НСтр, 11).Текст);
		КонецЕсли;

		СтрНазначение = СокрЛП(ОбластьГруппировки.Область(НСтр, 9).Текст);

		Индекс = мДеревоГруппировки.Строки.Индекс(СтрГруппировки);

		Назначение = Неопределено;

		Если Не ПустаяСтрока(СтрНазначение) Тогда

			Попытка
				Назначение = ПланыВидовХарактеристик.НазначенияСвойствКатегорийОбъектов[Лев(СтрНазначение, Найти(СтрНазначение+",", ",")-1)];
			Исключение

			КонецПопытки;

		КонецЕсли;

		МассивНазначений.Добавить(Назначение);

		Если Назначение <> Неопределено Тогда

			Запрос.УстановитьПараметр("ИмяПоля"+Индекс, СтрГруппировки.ИмяПоля);
			Запрос.УстановитьПараметр("ПредставлениеПоля"+Индекс, СтрГруппировки.ПредставлениеПоля);
			Запрос.УстановитьПараметр("ОписаниеПоля"+Индекс, СтрГруппировки.ОписаниеПоля);
			Запрос.УстановитьПараметр("Назначение"+Индекс, МассивНазначений[Индекс]);

			Запрос.Текст = Запрос.Текст + "
			|ОБЪЕДИНИТЬ ВСЕ 
			|ВЫБРАТЬ 
			|	&ИмяПоля"+Индекс+" 							КАК ИмяПоля,
			|	&ОписаниеПоля"+Индекс+" 					КАК ОписаниеПоля,
			|	&ПредставлениеПоля"+Индекс+"				КАК ПредставлениеПоля,
			|	ПланВидовХарактеристик.СвойстваОбъектов.Представление КАК ПредставлениеСвойства,
			|	ПланВидовХарактеристик.СвойстваОбъектов.Ссылка  КАК Ссылка
			|
			|ИЗ
			|	ПланВидовХарактеристик.СвойстваОбъектов
			|
			|ГДЕ
			|	ПланВидовХарактеристик.СвойстваОбъектов.НазначениеСвойства = &Назначение"+Индекс;

			// Добавляем специальную строку "Свойства"
			СтрГруппировкиСвойства = СтрГруппировки.Строки.Добавить();
			СтрГруппировкиСвойства.ИмяПоля = "Свойства"+СтрГруппировки.ИмяПоля;
			СтрГруппировкиСвойства.ПредставлениеПоля = "Свойства";

		КонецЕсли;

		СтрФильтры = мДеревоФильтры.Строки.Добавить();
		СтрФильтры.ИмяПоля           = СтрГруппировки.ИмяПоля;
		СтрФильтры.ПредставлениеПоля = СтрГруппировки.ПредставлениеПоля;
		СтрФильтры.ОписаниеПоля      = СтрГруппировки.ОписаниеПоля;
		СтрФильтры.ВклПоУмолчанию    = СтрГруппировки.ВклПоУмолчанию;

		Если ПустаяСтрока(СтрФильтры.ОписаниеПоля) Тогда
			СтрФильтры.ОписаниеПоля = СтрФильтры.ИмяПоля;
		КонецЕсли;

		НайденнаяСтрока = мТаблицаТипы.Найти(СтрГруппировки.ИмяПоля, "ИмяПоля");
		СтрФильтры.ОписаниеТипов = НайденнаяСтрока.ОписаниеТипов;
		СтрФильтры.ИмяПоляВладелец = СокрЛП(СокрЛП(ОбластьГруппировки.Область(НСтр, 7).Текст));

		Если Назначение <> Неопределено Тогда
			// Добавляем специальную строку "Свойства"
			СтрФильтрыСвойства = СтрФильтры.Строки.Добавить();
			СтрФильтрыСвойства.ИмяПоля = "Свойства"+СтрФильтры.ИмяПоля;
			СтрФильтрыСвойства.ПредставлениеПоля = "Свойства";

			СтрФильтрыКатегории = СтрФильтры.Строки.Добавить();
			СтрФильтрыКатегории.ИмяПоля           = "Категории" + СтрГруппировки.ИмяПоля;
			СтрФильтрыКатегории.ПредставлениеПоля = "Категории " + НРег(СтрГруппировки.ПредставлениеПоля);
			СтрФильтрыКатегории.ОписаниеПоля      = СтрГруппировки.ОписаниеПоля+".Категория";
			СтрФильтрыКатегории.ВклПоУмолчанию    = Ложь;
			СтрФильтрыКатегории.ОписаниеТипов	 = ОписаниеТиповКатегории;
			СтрФильтрыКатегории.Свойство 		 = Назначение;

		КонецЕсли;

	КонецЦикла;

	Запрос.Текст = Сред(Запрос.Текст, 16); // Удаляем первое ОБЪЕДИНИТЬ ВСЕ

	// Добавим в таблицу группировок
	Если Не ПустаяСтрока(Запрос.Текст) Тогда

		Состояние("Получение списка свойств");
		ТаблицаСвойств = Запрос.Выполнить().Выгрузить();

		Для Каждого Строка Из ТаблицаСвойств Цикл

			Индекс = ТаблицаСвойств.Индекс(Строка);

			// Найдем строку "Свойства", к которой добавим строки с конкретными свойствами
			НайденнаяСтрока = мДеревоГруппировки.Строки.Найти("Свойства"+СокрЛП(Строка.ИмяПоля), "ИмяПоля", Истина);

			СтрГруппировки = НайденнаяСтрока.Строки.Добавить();
			СтрГруппировки.ИмяПоля           = "Свойство"+Индекс;
			СтрГруппировки.ПредставлениеПоля = СокрЛП(Строка.ПредставлениеСвойства); //+" (свойство "+НРег(СокрЛП(Строка.ПредставлениеПоля)) +")";
			СтрГруппировки.ОписаниеПоля      = СокрЛП(Строка.ОписаниеПоля) + ".Свойство"+Индекс+".Значение";
			СтрГруппировки.ВклПоУмолчанию    = Ложь;
			СтрГруппировки.РассчитыватьИтоги = Истина;
			СтрГруппировки.ТипИтога = "Иерархия";
			СтрГруппировки.ВыводитьПустые	 = Ложь;
			СтрГруппировки.ВыводитьПустыеСвязаннаяГруппировка = СокрЛП(Строка.ИмяПоля);
			СтрГруппировки.Свойство 		 = Строка.Ссылка;

			// Найдем строку "Свойства", к которой добавим строки с конкретными свойствами
			НайденнаяСтрока = мДеревоФильтры.Строки.Найти("Свойства"+СокрЛП(Строка.ИмяПоля), "ИмяПоля", Истина);

			СтрФильтры = НайденнаяСтрока.Строки.Добавить();
			СтрФильтры.ИмяПоля           = СтрГруппировки.ИмяПоля;
			СтрФильтры.ПредставлениеПоля = СтрГруппировки.ПредставлениеПоля;
			СтрФильтры.ОписаниеПоля      = СтрГруппировки.ОписаниеПоля;
			СтрФильтры.ВклПоУмолчанию    = Ложь;

			СтрФильтры.Свойство 		 = Строка.Ссылка;

			Если ПустаяСтрока(СтрФильтры.ОписаниеПоля) Тогда
				СтрФильтры.ОписаниеПоля = СтрФильтры.ИмяПоля;
			КонецЕсли;

			СтрФильтры.ОписаниеТипов = Строка.Ссылка.ТипЗначения;

			СтрФильтры.ИмяПоляВладелец = "";

		КонецЦикла;

		Состояние("");
	КонецЕсли;

	Для Каждого Строка Из мДеревоФильтры.Строки Цикл

		Типы = Строка.ОписаниеТипов.Типы();

		Для Каждого ЭлементТипа Из Типы Цикл

			// Если тип примитивный, то не анализируем
			Если ЭлементТипа = Тип("Число")
				ИЛИ ЭлементТипа = Тип("Строка")
				ИЛИ ЭлементТипа = Тип("Дата") 
				ИЛИ ЭлементТипа = Тип("Булево") Тогда
				Прервать;
			КонецЕсли;

			ПустоеЗначениеТипа = Новый(ЭлементТипа);

			МетаданныеСправочника = Метаданные.Справочники.Найти(ПустоеЗначениеТипа.Метаданные().Имя);

			// Если это не справочник, то не анализируем
			Если МетаданныеСправочника = Неопределено Тогда
				Продолжить;
			КонецЕсли;

			ВладелецСправочника = ПустоеЗначениеТипа.Владелец;
			Если ВладелецСправочника = Неопределено Тогда
				Продолжить;
			КонецЕсли;

			Для Каждого ПроверяемаяСтрока Из мДеревоФильтры.Строки Цикл
				Если ПроверяемаяСтрока.ОписаниеТипов.СодержитТип(ТипЗнч(ВладелецСправочника)) Тогда
					Строка.ИмяПоляВладелец = ПроверяемаяСтрока.ИмяПоля;
					Прервать;
				КонецЕсли;
			КонецЦикла;

		КонецЦикла;

	КонецЦикла;

	СтрокаГруппировки = ГруппировкиОтчета.Добавить();
	СтрокаГруппировки.ИмяГруппировки = "Номенклатура";
	СтрокаГруппировки.ПредставлениеГруппировки = "Номенклатура";
	СтрокаГруппировки.ОписаниеГруппировки = "Номенклатура";
	СтрокаГруппировки.РассчитыватьИтоги = Истина;
	СтрокаГруппировки.ТипИтога = "Иерархия";

	СтрокаГруппировки = ГруппировкиОтчета.Добавить();
	СтрокаГруппировки.ИмяГруппировки = "ЕдиницаХраненияОстатков";
	СтрокаГруппировки.ПредставлениеГруппировки = "Единица хранения остатков";
	СтрокаГруппировки.ОписаниеГруппировки = "Номенклатура.ЕдиницаХраненияОстатков";
	
КонецПроцедуры

// Служебная процедура, устанавливает признаки возможности расчета итогов по иерархии и 
// принадлежности поля к ссылочным типам
Процедура ЗаполнитьПараметрыПолей(ТаблицаГруппировки)

	Для Каждого СтрокаГруппировки Из ТаблицаГруппировки Цикл

		Если ПустаяСтрока(СтрокаГруппировки.ОписаниеГруппировки) Тогда
			ОписаниеПоляЗапроса = СтрокаГруппировки.ОписаниеГруппировки;
		Иначе
			ОписаниеПоляЗапроса = СтрокаГруппировки.ИмяГруппировки;
		КонецЕсли;


		// Описание типов получаем либо из заполненной таблицы фильтров, либо заново
		НайденнаяСтрока = мДеревоФильтры.Строки.Найти(СтрокаГруппировки.ИмяГруппировки, "ИмяПоля", Истина);

		Если НайденнаяСтрока <> Неопределено Тогда
			ОписаниеТиповГруппировки = НайденнаяСтрока.ОписаниеТипов;
		Иначе
			Возврат;
		КонецЕсли;

		МассивТипов = ОписаниеТиповГруппировки.Типы();

		СтрокаГруппировки.ИтогПоИерархии		= Ложь;
		СтрокаГруппировки.ЕстьПолеПредставление = Ложь;

		Для Каждого ЭлементТипа Из МассивТипов Цикл

			// Определим принадлежность к ссылочным типам (т.е. возможность использования поля Представление)

			// нет у примитивных
			Если ЭлементТипа = Тип("Число")
				ИЛИ ЭлементТипа = Тип("Строка")
				ИЛИ ЭлементТипа = Тип("Дата") Тогда

				СтрокаГруппировки.ЕстьПолеПредставление = Ложь;
				Прервать;
			Иначе

				// нет у перечисления
				ПустоеЗначениеТипа = Новый(ЭлементТипа);

				Если Метаданные.Перечисления.Найти(ПустоеЗначениеТипа.Метаданные().Имя) <> Неопределено Тогда
					СтрокаГруппировки.ЕстьПолеПредставление = Ложь;
					Прервать;
				Иначе
					СтрокаГруппировки.ЕстьПолеПредставление = Истина;
				КонецЕсли;
			КонецЕсли;

		КонецЦикла;

	КонецЦикла;

КонецПроцедуры

// Вывод показателей отчета
//
// Параметры:
//	Выборка       - выборка из результата отчета, которая обходится в процедуре
//	СтруктураПараметров - структура параметров, необходимых для вывода строки
//
Процедура ВывестиПоказатели(Выборка, СтруктураПараметров)

	ТабДок = СтруктураПараметров.ТабДок;

	ОбластьЗначенияПоказателя = СтруктураПараметров.ЗначенияПоказателя;

	ТабДок.Присоединить(СтруктураПараметров.ЗаголовкиПоказателей);

	ФорматПоказателя = "ЧЦ = 14; ЧДЦ = 3; ЧО = 0; ЧН=""-""";

	ЗначениеПоказателя="";

	НачальныйОстаток=?(ПустаяСтрока(Выборка["НачальныйОстаток"]),0,Выборка["НачальныйОстаток"]);
	Поступление=0;
	Отгрузка=0;

	ОбластьЗначенияПоказателя.Параметры.Поступление=""+Формат(Поступление, ФорматПоказателя);
	ОбластьЗначенияПоказателя.Параметры.Отгрузка=""+Формат(Отгрузка, ФорматПоказателя);
	ОбластьЗначенияПоказателя.Параметры.НачальныйОстаток=""+Формат(НачальныйОстаток, ФорматПоказателя);

	ОбластьЗначенияПоказателя.Параметры.РасшифровкаПоступление=Неопределено;
	ОбластьЗначенияПоказателя.Параметры.РасшифровкаОтгрузка=Неопределено;

	ТабДок.Присоединить(ОбластьЗначенияПоказателя);

	А=ТабДок.ВысотаТаблицы;
	В=4;

	Если НачальныйОстаток<0 Тогда
		ТабДок.Область(А,В,А,В).ЦветТекста=Новый Цвет(255,0,0);
	КонецЕсли;

	Поступление			=?(ПустаяСтрока(Выборка["Поступление"]),0,Выборка["Поступление"]);
	Отгрузка			=?(ПустаяСтрока(Выборка["Отгрузка"]),0,Выборка["Отгрузка"]);

	ЗначениеПоказателя="";

	ОбластьЗначенияПоказателя.Параметры.Поступление=""+Формат(Поступление, ФорматПоказателя);
	ОбластьЗначенияПоказателя.Параметры.Отгрузка=""+Формат(Отгрузка, ФорматПоказателя);
	ОбластьЗначенияПоказателя.Параметры.НачальныйОстаток=""+Формат(0, ФорматПоказателя);

	ПараметрыПоступление=Новый Структура;
	ПараметрыПоступление.Вставить("ИмяРегистра","ЗаказыПоставщикам");
	ОбластьЗначенияПоказателя.Параметры.РасшифровкаПоступление=ПараметрыПоступление;

	ПараметрыОтгрузка=Новый Структура;
	ПараметрыОтгрузка.Вставить("ИмяРегистра","ЗаказыПокупателей");
	ОбластьЗначенияПоказателя.Параметры.РасшифровкаОтгрузка=ПараметрыОтгрузка;

	ТабДок.Присоединить(ОбластьЗначенияПоказателя);

	Если НачальныйОстаток<0 Тогда
		ТабДок.Область(	ТабДок.ВысотаТаблицы,ТабДок.ШиринаТаблицы,
		ТабДок.ВысотаТаблицы,ТабДок.ШиринаТаблицы).ЦветТекста=Новый Цвет(255,0,0);
	КонецЕсли;

	РазвернутьСтроку(Выборка.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам,"Период", "Все"),СтруктураПараметров,ТабДок,НачальныйОстаток);

КонецПроцедуры // ВывестиСтроку()

// Обход выборки из результата запроса по группировкам для вывода строк отчета
//
// Параметры:
//
//	Выборка       - выборка из результата отчета, которая обходится в процедуре,
//	СтруктураПараметров - структура параметров, передеваемых в процедуру вывода
//	                строки отчета,
//	Номер         - число, номер обходимой группировки
//  МассивРасшифровки - массив группировок текущей строки (для расшифровки)
//
Процедура ВывестиГруппировку(Выборка, СтруктураПараметров, Номер, МассивРасшифровки)

	ОбработкаПрерыванияПользователя();

	ВсегоГруппировок = СтруктураПараметров.ВсегоГруппировок;

	ОбластьОбщийОтступ = СтруктураПараметров.ОбщийОтступ;
	ОбластьЗначениеГруппировки   = СтруктураПараметров.ЗначениеГруппировки;
	ОбластьЗначенияПоказателя    = СтруктураПараметров.ЗначенияПоказателя;

	ОформлениеСтроки                = СтруктураПараметров.ОформлениеСтроки;
	ОформлениеСтрокиИерархии        = СтруктураПараметров.ОформлениеСтрокиИерархии;

	СтруктураВыводГруппировок = СтруктураПараметров.СтруктураВыводГруппировок;

	// Назначение, корректировка макетов измерений
	Сдвиг = Макс(ОформлениеСтроки.Количество()-СтруктураПараметров.МассивГруппировки.Количество(),0);

	Пока Выборка.Следующий() Цикл

		Если Выборка.Группировка()<>"Период" Тогда  // Не выводим группировку, разворачивающуюся по горизонтали.

			ИспользоватьОформление = РаскрашиватьИзмерения;
			Если ОформлениеСтроки.Количество()>0 Тогда
				ИндексОформления = Сдвиг+((Номер) - (ОформлениеСтроки.Количество()-Сдвиг)*Цел((Номер)/(ОформлениеСтроки.Количество()-Сдвиг)));
			Иначе
				ИспользоватьОформление = Ложь;
			КонецЕсли;

			УровеньЗаписи   = Выборка.Уровень();
			СдвигУровня     = 0;

			ИмяГруппировки  = Выборка.Группировка();

			ТабДок = СтруктураПараметров.ТабДок;

			ЗначениеГруппировки = "";

			ЗначениеРасшифровки = Неопределено;

			ТипЗаписиВыборки = Выборка.ТипЗаписи();

			МассивВыводГруппировок = Новый Массив;

			Если ТипЗаписиВыборки = ТипЗаписиЗапроса.ИтогПоИерархии Тогда

				ЗначениеТекущейГруппировки = "" + Выборка[ИмяГруппировки + "Представление"];
				Если ПустаяСтрока(ЗначениеТекущейГруппировки) Тогда

					ЗначениеТекущейГруппировки = "<...>";
				КонецЕсли;

				ЗначениеГруппировки = ЗначениеГруппировки + ЗначениеТекущейГруппировки;

				Если ЗначениеРасшифровки = Неопределено Тогда 
					ЗначениеРасшифровки = Выборка[ИмяГруппировки];
				КонецЕсли;

			ИначеЕсли ТипЗаписиВыборки = ТипЗаписиЗапроса.ИтогПоГруппировке Тогда

				Если СтруктураВыводГруппировок.Свойство(ИмяГруппировки, МассивВыводГруппировок) Тогда

					КоличествоДопПолей=МассивВыводГруппировок.Количество();
					ВыведеноГруппировок=0;

					Для Каждого ВыводимаяГруппировка Из МассивВыводГруппировок Цикл

						ВыведеноГруппировок=ВыведеноГруппировок+1;

						ЗначениеТекущейГруппировки = "" + Выборка[ВыводимаяГруппировка + "Представление"];
						Если ПустаяСтрока(ЗначениеТекущейГруппировки) Тогда
							ЗначениеТекущейГруппировки = "<...>";
						КонецЕсли;

						ЗначениеГруппировки = ЗначениеГруппировки + ЗначениеТекущейГруппировки + ", ";

						Если ЗначениеРасшифровки = Неопределено Тогда 
							ЗначениеРасшифровки = Выборка[ВыводимаяГруппировка];
						КонецЕсли;

						МассивРасшифровки.Добавить(ВыводимаяГруппировка);

					КонецЦикла;

					// Если группировки нет в списке, то не выводим, нам нужна только выборка, 
					// т.о. искусственно получаем группировку по набору полей, указанных в одной строке
					// таблицы группировок
				Иначе

					МассивРасшифровки.Добавить(ИмяГруппировки);

					Перейти ~М1;

				КонецЕсли;

				ЗначениеТекущейГруппировки = "" + Выборка[ИмяГруппировки + "Представление"];
				Если ПустаяСтрока(ЗначениеТекущейГруппировки) Тогда
					ЗначениеТекущейГруппировки = "<...>";
				КонецЕсли;

				ЗначениеГруппировки = ЗначениеГруппировки + ЗначениеТекущейГруппировки;

				Если ЗначениеРасшифровки = Неопределено Тогда 
					ЗначениеРасшифровки = Выборка[ИмяГруппировки];
				КонецЕсли;

			КонецЕсли;

			// Для итогов по группировке сдвиг нужно уменьшить на количество пропущенных группировок, 
			// заранее рассчитанное для каждой группировки
			Если ТипЗаписиВыборки = ТипЗаписиЗапроса.ИтогПоГруппировке Тогда

				МассивРасшифровки.Добавить(ИмяГруппировки);

				ЗначениеРасшифровкиСтрока = Новый Структура;
				Для Каждого Элемент Из МассивРасшифровки Цикл
					ЗначениеРасшифровкиСтрока.Вставить(Элемент, Выборка[Элемент])
				КонецЦикла;

				СдвигУровня = СтруктураПараметров.СтруктураСдвигУровняГруппировок[ИмяГруппировки];

				// Для итогов по иерархии сдвиг нужно уменьшить на количество пропущенных группировок для предыдущей группировки
				// заранее рассчитанное для каждой группировки
			ИначеЕсли ТипЗаписиВыборки = ТипЗаписиЗапроса.ИтогПоИерархии Тогда

				ЗначениеРасшифровкиСтрока = Новый Структура;
				ЗначениеРасшифровкиСтрока.Вставить(ИмяГруппировки, Выборка[ИмяГруппировки]);

				СдвигУровня = СтруктураПараметров.СтруктураСдвигУровняГруппировок[СтруктураПараметров.МассивГруппировки[Номер]];

			КонецЕсли;

			УровеньЗаписи = УровеньЗаписи - СдвигУровня+1;

			НачСтрока = ТабДок.ВысотаТаблицы+1;

			ТабДок.Вывести(ОбластьОбщийОтступ, УровеньЗаписи);
			ТабДок.Область(ТабДок.ВысотаТаблицы-2,1).Расшифровка = ЗначениеРасшифровкиСтрока;

			ОбластьЗначениеГруппировки.Параметры.ЗначениеГруппировки = СокрЛП(ЗначениеГруппировки);
			ОбластьЗначениеГруппировки.Параметры.Расшифровка		 = ЗначениеРасшифровки;
			ОбластьЗначениеГруппировки.Область().Отступ = УровеньЗаписи;

			ТабДок.Присоединить(ОбластьЗначениеГруппировки);

			ВывестиПоказатели(Выборка, СтруктураПараметров);

			Если ИспользоватьОформление Тогда

				Область = ТабДок.Область(НачСтрока, 2, ТабДок.ВысотаТаблицы, ТабДок.ШиринаТаблицы);

				Если Выборка.ТипЗаписи() = ТипЗаписиЗапроса.ИтогПоГруппировке Тогда

					//Область.Шрифт      = ОформлениеСтроки[ИндексОформления].Шрифт;
					Область.ЦветФона   = ОформлениеСтроки[ИндексОформления].ЦветФона;
					Область.ЦветТекста = ОформлениеСтроки[ИндексОформления].ЦветТекста;
					//Область.Узор       = ОформлениеСтроки[ИндексОформления].Узор;
					//Область.ЦветУзора  = ОформлениеСтроки[ИндексОформления].ЦветУзора;

				ИначеЕсли Выборка.ТипЗаписи() = ТипЗаписиЗапроса.ИтогПоИерархии Тогда

					//Область.Шрифт      = ОформлениеСтрокиИерархии[ИндексОформления].Шрифт;
					Область.ЦветФона   = ОформлениеСтрокиИерархии[ИндексОформления].ЦветФона;
					Область.ЦветТекста = ОформлениеСтрокиИерархии[ИндексОформления].ЦветТекста;
					//Область.Узор       = ОформлениеСтрокиИерархии[ИндексОформления].Узор;
					//Область.ЦветУзора  = ОформлениеСтрокиИерархии[ИндексОформления].ЦветУзора;
				КонецЕсли;
			КонецЕсли;

			Если ТипЗаписиВыборки = ТипЗаписиЗапроса.ИтогПоИерархии Тогда
				ТабДок.Область(НачСтрока, 2, ТабДок.ВысотаТаблицы,2).Шрифт = СтруктураПараметров.ШрифтГрупп;
			КонецЕсли;

		КонецЕсли; 

~М1:	Если Номер+1 < СтруктураПараметров.МассивГруппировки.Количество() Тогда

			// На каждом уровне используется своя копия структуры расшифровок
			КопияМассивРасшифровки = Новый Массив;
			Для Каждого Элемент Из МассивРасшифровки Цикл
				КопияМассивРасшифровки.Добавить(Элемент);
			КонецЦикла;

			ВывестиГруппировку(Выборка.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, СтруктураПараметров.МассивГруппировки[Номер+1]), СтруктураПараметров, Номер + 1, КопияМассивРасшифровки);

		КонецЕсли; 

	КонецЦикла;

КонецПроцедуры // ВывестиГруппировку()

// Функция формирует текст запроса по переданной в качестве параметра таблице.
// Параметры:
//			Источник (строка): Таблица, по которой строится запрос.
//			КопияГруппировкиОтчета (таблица значений): таблица выбранных группировок.
// Возвращаемое значение:
//			Текст запроса к таблице - источнику.
//
Функция СформироватьТекстЗапроса(Источник,КопияГруппировкиОтчета, Первый = Ложь)

	ТекстПоляЗапроса = "";
	СтруктураИзмеренийРегистра=Новый Структура;

	// Таблица значений, в которой будут храниться данные о группировке и условиям по свойствам
	ТаблицаИсточниковСвойства = Новый ТаблицаЗначений;
	ТаблицаИсточниковСвойства.Колонки.Добавить("ИмяПоляОбъект");
	ТаблицаИсточниковСвойства.Колонки.Добавить("Свойство");
	ТаблицаИсточниковСвойства.Колонки.Добавить("ИмяТаблицы");
	МассивБулево = Новый Массив;
	МассивБулево.Добавить(Тип("Булево"));
	ОписаниеТиповБулево = Новый ОписаниеТипов(МассивБулево);
	ТаблицаИсточниковСвойства.Колонки.Добавить("НаложенФильтр", ОписаниеТиповБулево);
	МассивСтрока = Новый Массив; 
	МассивСтрока.Добавить(Тип("Строка"));
	КвалификаторСтроки = Новый КвалификаторыСтроки(100);
	ОписаниеТиповСтрока = Новый ОписаниеТипов(МассивСтрока, , КвалификаторСтроки);
	КвалификаторСтрокиНеогр = Новый КвалификаторыСтроки(0); // строка неограниченной длинны
	ОписаниеТиповСтрокаНеогр = Новый ОписаниеТипов(МассивСтрока, , КвалификаторСтрокиНеогр);
	ТаблицаИсточниковСвойства.Колонки.Добавить("СтрокаПараметров", ОписаниеТиповСтрокаНеогр);
	ТаблицаИсточниковСвойства.Колонки.Добавить("ОператорСравнения", ОписаниеТиповСтрока);

	// Таблица значений, в которой будут храниться данные об условиях по категориям
	ТаблицаИсточниковКатегории = Новый ТаблицаЗначений;
	ТаблицаИсточниковКатегории.Колонки.Добавить("ИмяПоляОбъект");
	ТаблицаИсточниковКатегории.Колонки.Добавить("СтрокаПараметров", ОписаниеТиповСтрокаНеогр);
	ТаблицаИсточниковКатегории.Колонки.Добавить("ТипФильтра", ОписаниеТиповСтрока);

	Для Каждого СтрокаГруппировки Из КопияГруппировкиОтчета Цикл

		ОписаниеПоляЗапроса = "";
		НайдСтрока=мДеревоГруппировки.Строки.Найти(СтрокаГруппировки.ИмяГруппировки, "ИмяПоля",Истина);
		Если НайдСтрока <> Неопределено Тогда
			ОписаниеПоляЗапроса = НайдСтрока.ОписаниеПоля;
		Иначе 
			ОписаниеПоляЗапроса = СтрокаГруппировки.ИмяГруппировки;
		КонецЕсли;

		Если Найти(ОписаниеПоляЗапроса, ".Свойство") > 0 Тогда

			НайдСтрока = мДеревоФильтры.Строки.Найти(СтрокаГруппировки.ИмяГруппировки, "ИмяПоля", Истина);
			Если НайдСтрока <> Неопределено Тогда
				НоваяСтрока = ТаблицаИсточниковСвойства.Добавить();
				НоваяСтрока.ИмяПоляОбъект = СтрЗаменить(Источник,".","")+"."+Лев(ОписаниеПоляЗапроса, Найти(ОписаниеПоляЗапроса, ".Свойство") - 1);
				НоваяСтрока.Свойство = НайдСтрока.Свойство;
				НоваяСтрока.ИмяТаблицы = СтрЗаменить(Источник,".","")+Лев(СтрЗаменить(ОписаниеПоляЗапроса, ".", ""), Найти(СтрЗаменить(ОписаниеПоляЗапроса, ".", ""), "Значение") - 1);
				НоваяСтрока.НаложенФильтр = Ложь;

				// Изменим описание поля запроса
				ОписаниеПоляЗапроса = НоваяСтрока.ИмяТаблицы + ".Значение";

			Иначе
				Предупреждение("Не найдено свойство " + СтрокаГруппировки.ИмяПоля);
				ЕстьОшибки = Истина;
			КонецЕсли;

		КонецЕсли;

		Если Найти(ОписаниеПоляЗапроса,"#")>0 Тогда // Обрабатывается реквизит документа.
			Если Источник="ЗаказыПокупателей" Тогда
				ОписаниеПоляЗапроса=СтрЗаменить(ОписаниеПоляЗапроса,"#","ЗаказПокупателя.");
			ИначеЕсли Источник="ЗаказыПоставщикам" Тогда
				ОписаниеПоляЗапроса=СтрЗаменить(ОписаниеПоляЗапроса,"#","ЗаказПоставщику.");
			Иначе
				ОписаниеПоляЗапроса=СтрЗаменить(ОписаниеПоляЗапроса,"#","");
			КонецЕсли;
		КонецЕсли;

		ТекстПоляЗапроса = ТекстПоляЗапроса + ",
		|	" + ОписаниеПоляЗапроса + " КАК " + СтрокаГруппировки.ИмяГруппировки;

		Если СтрокаГруппировки.ЕстьПолеПредставление Тогда
			ТекстПоляЗапроса = ТекстПоляЗапроса + ",
			|	" + ОписаниеПоляЗапроса + ".Представление КАК " + СтрокаГруппировки.ИмяГруппировки + "Представление";
		Иначе
			ТекстПоляЗапроса = ТекстПоляЗапроса + ",
			|	" + ОписаниеПоляЗапроса + " КАК " + СтрокаГруппировки.ИмяГруппировки + "Представление";
		КонецЕсли;

	КонецЦикла;

	//Добавляем предопределенную группировку по периоду

	Если Источник="ЗаказыПокупателей" Тогда

		ТекстПоляЗапроса = ТекстПоляЗапроса + ",
		|	ЗаказПокупателя.ДатаОтгрузки КАК Период,
		|	ЗаказПокупателя.ДатаОтгрузки КАК ПериодПредставление";

	ИначеЕсли Источник="ЗаказыПоставщикам" Тогда

		ТекстПоляЗапроса = ТекстПоляЗапроса + ",
		|	ЗаказПоставщику.ДатаПоступления КАК Период,
		|	ЗаказПоставщику.ДатаПоступления КАК ПериодПредставление";

	Иначе

		ТекстПоляЗапроса = ТекстПоляЗапроса + ",
		|	&ДатаОтчета КАК Период,
		|	&ДатаОтчета  КАК ПериодПредставление";

	КонецЕсли;

	// Добавляем список показателей

	Если Источник="ЗаказыПокупателей" Тогда

		ТекстПоляЗапроса = ТекстПоляЗапроса + ",
		|	0 КАК НачальныйОстаток,
		|	КоличествоОстаток КАК Отгрузка,
		|	0 КАК Поступление";

	ИначеЕсли Источник="ЗаказыПоставщикам" Тогда

		ТекстПоляЗапроса = ТекстПоляЗапроса + ",
		|	0 КАК НачальныйОстаток,
		|	0 КАК Отгрузка,
		|	КоличествоОстаток КАК Поступление";

	Иначе

		ТекстПоляЗапроса = ТекстПоляЗапроса + ",
		|	КоличествоОстаток КАК НачальныйОстаток,
		|	0 КАК Отгрузка,
		|	0 КАК Поступление";

	КонецЕсли;

	// Фильтр на значения измерений регистра, передаваемый в качестве параметров
	ТекстФильтры = "";

	// Фильтр на значения других полей, накладываемый на весь запрос
	ТекстОбщиеФильтры = "";

	ТаблицаСписокВсехФильтров = ФильтрыОтчета.Выгрузить();
	ТаблицаСписокВсехФильтров.Свернуть("ИмяФильтра, ПредставлениеФильтра, ТипФильтра, ИспользованиеФильтра",);

	СтруктураПоиска = Новый Структура("ИмяФильтра");

	НомерФильтра =0;

	// Фильтр
	Для Каждого СтрокаФильтры Из ТаблицаСписокВсехФильтров Цикл

		Если НЕ СтрокаФильтры.ИспользованиеФильтра Тогда
			Продолжить;
		КонецЕсли; 

		НомерФильтра = НомерФильтра+1;

		СтруктураПоиска.ИмяФильтра = СтрокаФильтры.ИмяФильтра;

		НайденнаяСтрока = мДеревоФильтры.Строки.Найти(СтрокаФильтры.ИмяФильтра, "ИмяПоля", Истина);
		Если НайденнаяСтрока <> Неопределено Тогда
			ОписаниеФильтра = НайденнаяСтрока.ОписаниеПоля;
		КонецЕсли;
		
		Если ПустаяСтрока(ОписаниеФильтра) Тогда
			ОписаниеФильтра = СтрокаФильтры.ИмяФильтра;
		КонецЕсли;

		Если Найти(ОписаниеФильтра,"#")>0 Тогда // Из документа
			Если Источник="ЗаказыПокупателей" Тогда
				ОписаниеФильтра=СтрЗаменить(ОписаниеФильтра,"#","ЗаказПокупателя.");
			ИначеЕсли Источник="ЗаказыПоставщикам" Тогда
				ОписаниеФильтра=СтрЗаменить(ОписаниеФильтра,"#","ЗаказПоставщику.");
			Иначе
				ОписаниеФильтра=СтрЗаменить(ОписаниеФильтра,"#","");
			КонецЕсли;
		КонецЕсли;

		НайденныеСтроки = ФильтрыОтчета.НайтиСтроки(СтруктураПоиска);

		// Формируем текст вложенного фильтра
		ТекстВложенногоФильтры = "";

		СтрЗначенияФильтры = "";

		Массив = Новый Массив;
		
		Для Каждого НайденнаяСтрока Из НайденныеСтроки Цикл
		
			Массив.Добавить(НайденнаяСтрока.ЗначениеФильтра);

			// Строка для шапки отчета
			СтрЗначенияФильтры = СтрЗначенияФильтры + ", " + НайденнаяСтрока.ЗначениеФильтра;
		
		КонецЦикла; 
		Запрос.УстановитьПараметр("Параметр" + НомерФильтра, Массив);

		Если Найти(ОписаниеФильтра, ".Свойство") > 0 Тогда

			НайдСтрока = мДеревоФильтры.Строки.Найти(СтрокаФильтры.ИмяФильтра, "ИмяПоля", Истина);
			Если НайдСтрока <> Неопределено Тогда
				ИмяТаблицы = Лев(СтрЗаменить(ОписаниеФильтра, ".", ""), Найти(СтрЗаменить(ОписаниеФильтра, ".", ""), "Значение") - 1);
				СтрокаИсточник = ТаблицаИсточниковСвойства.Найти(ИмяТаблицы, "ИмяТаблицы");
				Если СтрокаИсточник = Неопределено Тогда
					СтрокаИсточник = ТаблицаИсточниковСвойства.Добавить();
				КонецЕсли;

				СтрокаИсточник.ИмяПоляОбъект = Лев(ОписаниеФильтра, Найти(ОписаниеФильтра, ".Свойство") - 1);
				СтрокаИсточник.Свойство = НайдСтрока.Свойство;
				СтрокаИсточник.ИмяТаблицы = ИмяТаблицы;
				СтрокаИсточник.НаложенФильтр = Истина;
				СтрокаИсточник.СтрокаПараметров = СтрокаИсточник.СтрокаПараметров + ",  &Параметр" + НомерФильтра;

				Если Врег(СтрокаФильтры.ТипФильтра) = "РАВНО"
					ИЛИ Врег(СтрокаФильтры.ТипФильтра) = "В СПИСКЕ"
					ИЛИ Врег(СтрокаФильтры.ТипФильтра) = "В ГРУППЕ ИЗ СПИСКА"
					ИЛИ Врег(СтрокаФильтры.ТипФильтра) = "В ГРУППЕ" Тогда

					СтрокаИсточник.ОператорСравнения = " В ";

				ИначеЕсли Врег(СтрокаФильтры.ТипФильтра) = "НЕ РАВНО"
					ИЛИ Врег(СтрокаФильтры.ТипФильтра) = "НЕ В СПИСКЕ"
					ИЛИ Врег(СтрокаФильтры.ТипФильтра) = "НЕ В ГРУППЕ ИЗ СПИСКА"
					ИЛИ Врег(СтрокаФильтры.ТипФильтра) = "НЕ В ГРУППЕ" Тогда

					СтрокаИсточник.ОператорСравнения = " НЕ В ";

				КонецЕсли;

			Иначе
				Предупреждение("Не найдено свойство " + СтрокаФильтры.ИмяПоля);
				ЕстьОшибки = Истина;
			КонецЕсли;

		ИначеЕсли Найти(ОписаниеФильтра, ".Категория") > 0 Тогда

			НайдСтрока = мДеревоФильтры.Строки.Найти(СтрокаФильтры.ИмяФильтра, "ИмяПоля", Истина);
			Если НайдСтрока <> Неопределено Тогда

				ИмяПоляОбъект = Лев(ОписаниеФильтра, Найти(ОписаниеФильтра, ".Категория") - 1);

				СтрокаИсточник = ТаблицаИсточниковКатегории.Найти(ИмяПоляОбъект, "ИмяПоляОбъект");
				Если СтрокаИсточник = Неопределено Тогда
					СтрокаИсточник = ТаблицаИсточниковКатегории.Добавить();
				КонецЕсли;

				СтрокаИсточник.ИмяПоляОбъект = ИмяПоляОбъект;
				СтрокаИсточник.СтрокаПараметров = СтрокаИсточник.СтрокаПараметров + ",  &Параметр" + НомерФильтра;

				СтрокаИсточник.ТипФильтра = СтрокаФильтры.ТипФильтра;
			КонецЕсли;

		Иначе

			ТекстВложенногоФильтры = ТекстВложенногоФильтры + ",  &Параметр" + НомерФильтра;
		КонецЕсли;

	Если Врег(СтрокаФильтры.ТипФильтра) = "РАВНО" Тогда
		ОператорСравнения = " В ";
	ИначеЕсли Врег(СтрокаФильтры.ТипФильтра) = "НЕ РАВНО" Тогда
		ОператорСравнения = " НЕ В ";
	ИначеЕсли Врег(СтрокаФильтры.ТипФильтра) = "В СПИСКЕ" Тогда
		ОператорСравнения = " В ";
	ИначеЕсли Врег(СтрокаФильтры.ТипФильтра) = "НЕ В СПИСКЕ" Тогда
		ОператорСравнения = " НЕ В ";
	ИначеЕсли Врег(СтрокаФильтры.ТипФильтра) = "В ГРУППЕ ИЗ СПИСКА" Тогда
		ОператорСравнения = " В ИЕРАРХИИ ";
	ИначеЕсли Врег(СтрокаФильтры.ТипФильтра) = "НЕ В ГРУППЕ ИЗ СПИСКА" Тогда
		ОператорСравнения = " НЕ В ИЕРАРХИИ ";
	ИначеЕсли Врег(СтрокаФильтры.ТипФильтра) = "В ГРУППЕ" Тогда
		ОператорСравнения = " В ИЕРАРХИИ ";
	ИначеЕсли Врег(СтрокаФильтры.ТипФильтра) = "НЕ В ГРУППЕ" Тогда
		ОператорСравнения = " НЕ В ИЕРАРХИИ ";
	КонецЕсли;

		ТекстВложенногоФильтры = Сред(СокрЛП(ТекстВложенногоФильтры),2);

		Если Не ПустаяСтрока(ТекстВложенногоФильтры) Тогда

			// Фильтры, передаваемые в качестве параметров таблицы регистра

			Если мСтруктураМетаданныеРегистраИзмерения.Свойство(СтрЗаменить(Источник,".",""),СтруктураИзмеренийРегистра) Тогда
				Если СтруктураИзмеренийРегистра.Свойство(СтруктураПоиска.ИмяФильтра) Тогда
					ТекстФильтры = ТекстФильтры + " И " + ОписаниеФильтра 
					+ ОператорСравнения + " (" + ТекстВложенногоФильтры + ")";
				Иначе
					ТекстОбщиеФильтры = ТекстОбщиеФильтры 	+ " И " + ОписаниеФильтра+" "
					+ ОператорСравнения + " (" + ТекстВложенногоФильтры + ")";
				КонецЕсли;
				// Общие фильтры запроса
			Иначе
				ТекстОбщиеФильтры = ТекстОбщиеФильтры 	+ " И " + ОписаниеФильтра+" "
				+ ОператорСравнения + " (" + ТекстВложенногоФильтры + ")";
			КонецЕсли;

		КонецЕсли;

	КонецЦикла;
	
	// Источники - регистры сведений для получения свойств и категорий
	ТекстИсточникиСведений = "";
	
	Для Каждого Строка Из ТаблицаИсточниковСвойства Цикл
		
		ТекстИсточникиСведений = ТекстИсточникиСведений + Символы.ПС + 
		?(Строка.НаложенФильтр, "ВНУТРЕННЕЕ СОЕДИНЕНИЕ ", "ЛЕВОЕ СОЕДИНЕНИЕ ") + 
		"РегистрСведений.ЗначенияСвойствОбъектов КАК " + Строка.ИмяТаблицы + "
		|ПО " + Строка.ИмяТаблицы + ".Объект = " + Строка.ИмяПоляОбъект + "
		|И  " + Строка.ИмяТаблицы + ".Свойство = &Параметр" + Строка.ИмяТаблицы;
		
		Если Строка.НаложенФильтр И НЕ ПустаяСтрока(Строка.СтрокаПараметров) Тогда
			ТекстИсточникиСведений = ТекстИсточникиСведений + "
			|И  " + Строка.ИмяТаблицы + ".Значение " + Строка.ОператорСравнения + " (" + Сред(Строка.СтрокаПараметров, 2) + ")";
		КонецЕсли;
		
		Запрос.УстановитьПараметр("Параметр" + Строка.ИмяТаблицы, Строка.Свойство);
		
	КонецЦикла;

	Для Каждого Строка Из ТаблицаИсточниковКатегории Цикл

		Индекс = ТаблицаИсточниковКатегории.Индекс(Строка);

		Если Врег(Строка.ТипФильтра) = "РАВНО"
			ИЛИ Врег(Строка.ТипФильтра) = "В СПИСКЕ" Тогда

			// Ограничиваем выборку по регистру остатков набором различных объектов, принадлежащих одной категорий из указанных в списке
			ТекстИсточникиСведений = ТекстИсточникиСведений + Символы.ПС + 
			"ВНУТРЕННЕЕ СОЕДИНЕНИЕ (ВЫБРАТЬ РАЗЛИЧНЫЕ Объект ИЗ РегистрСведений.КатегорииОбъектов 
			|ГДЕ  Категория В (" + Сред(Строка.СтрокаПараметров, 2) + ")
			|) КАК Категории" + СтрЗаменить(Строка.ИмяПоляОбъект,".","") + "
			|ПО Категории" + СтрЗаменить(Строка.ИмяПоляОбъект,".","") + ".Объект = " + Строка.ИмяПоляОбъект;
			
		ИначеЕсли Врег(Строка.ТипФильтра) = "НЕ РАВНО" 
			ИЛИ Врег(Строка.ТипФильтра) = "НЕ В СПИСКЕ" Тогда

			// Ограничим выборку по регистру остатков только записями, объекты которых не принадлежат ни одной категории из указанных в списке 
			ТекстИсточникиСведений = ТекстИсточникиСведений + Символы.ПС + 
			"ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ РАЗЛИЧНЫЕ Объект ИЗ РегистрСведений.КатегорииОбъектов 
			|ГДЕ  Категория В (" + Сред(Строка.СтрокаПараметров, 2) + ")
			|) КАК Категории" + СтрЗаменить(Строка.ИмяПоляОбъект,".","") + "
			|ПО Категории" + СтрЗаменить(Строка.ИмяПоляОбъект,".","") + ".Объект = " + Строка.ИмяПоляОбъект;

			ТекстОбщиеФильтры = ТекстОбщиеФильтры + " И Категории" + СтрЗаменить(Строка.ИмяПоляОбъект,".","") + ".Объект ЕСТЬ NULL";

		КонецЕсли;
		
	КонецЦикла;
	// Фильтры

	// Удаление лишних запятых
	ТекстПоляЗапроса     = Сред(ТекстПоляЗапроса,         2);
	ТекстФильтры         = Сред(СокрЛП(ТекстФильтры),     2);
	ТекстОбщиеФильтры    = Сред(СокрЛП(ТекстОбщиеФильтры),2);

	ТекстЗапроса=	
	"ВЫБРАТЬ " + ?(Первый, "РАЗРЕШЕННЫЕ ", "") + ТекстПоляЗапроса + "
	|ИЗ 
	|	РегистрНакопления."+Источник+".Остатки(&ДатаОтчета,";

	Если Не ПустаяСтрока(ТекстФильтры) Тогда 
		ТекстЗапроса = ТекстЗапроса + Символы.ПС + ТекстФильтры;
	КонецЕсли;
	ТекстЗапроса= ТекстЗапроса + ") КАК "+Источник;

	ТекстЗапроса= ТекстЗапроса + ТекстИсточникиСведений;

	Если Не ПустаяСтрока(ТекстОбщиеФильтры) Тогда
		ТекстЗапроса = ТекстЗапроса  + Символы.ПС +
		"ГДЕ" + ТекстОбщиеФильтры;
	КонецЕсли;

	Возврат ТекстЗапроса;

КонецФункции

// Выполняет запрос и формирует табличный документ-результат отчета
// в соответствии с настройками, заданными значениями реквизитов отчета.
//
// Параметры:
//	ДокументРезультат - табличный документ, формируемый отчетом
//
Процедура СформироватьОтчет(ДокументРезультат, ПоказыватьЗаголовок, ВысотаЗаголовка, ТолькоЗаголовок = Ложь,  ЕстьОшибки = Ложь) Экспорт

	Запрос = Новый Запрос;

	// Список полей запроса
	ТекстЗапроса = "";
	ТекстПоляИтогов = "";
	ТекстПоляИтоговЗапроса = "";
	ТекстПоляУпорядочивания = "";

	мЗначимыеГруппировкиГор.Очистить();

	// Строки списков для шапки отчета
	СтрПоказатели = "";
	СтрГруппировки = "";
	СтрФильтры = "";

	ОсновноеПоле=Истина;

	Для Каждого Строка Из ГруппировкиОтчета цикл

		Если ПустаяСтрока(Строка.ИмяГруппировки) Тогда
			Предупреждение("Не указано имя группировки!", 60);
			Возврат;
		КонецЕсли;
		// Если не задано представление, берем его из имени
		Если ПустаяСтрока(Строка.ПредставлениеГруппировки) Тогда
			Строка.ПредставлениеГруппировки = Строка.ИмяГруппировки;
		КонецЕсли;
		Если Строка.РассчитыватьИтоги Тогда
			Если НЕ ОсновноеПоле Тогда
				ОсновноеПоле=Истина;
				СтрГруппировки=СтрГруппировки+"), ";
			Иначе
				СтрГруппировки=СтрГруппировки+", ";
			КонецЕсли;
		Иначе
			Если ОсновноеПоле Тогда
				ОсновноеПоле=Ложь;
				СтрГруппировки=СтрГруппировки+" (";
			Иначе
				СтрГруппировки=СтрГруппировки+", ";
			КонецЕсли;
		КонецЕсли;
		СтрГруппировки=СтрГруппировки+Строка.ПредставлениеГруппировки;

	КонецЦикла;

	Если НЕ ОсновноеПоле Тогда
		СтрГруппировки=СтрГруппировки+")";
		ОсновноеПоле=Истина;
	КонецЕсли;

	ТаблицаСписокВсехФильтров = ФильтрыОтчета.Выгрузить();
	ТаблицаСписокВсехФильтров.Свернуть("ИмяФильтра",);

	СтруктураПоиска = Новый Структура("ИмяФильтра");

	Для Каждого Строка Из ТаблицаСписокВсехФильтров Цикл

		Если ПустаяСтрока(Строка.ИмяФильтра) Тогда
			Предупреждение("Не указано имя отбора!", 60);
			Возврат;
		КонецЕсли;

		СтруктураПоиска.ИмяФильтра = Строка.ИмяФильтра;

		НайденныеСтроки = ФильтрыОтчета.НайтиСтроки(СтруктураПоиска);

		СтрТипФильтра = Нрег(НайденныеСтроки[0].ТипФильтра);

		Если НайденныеСтроки.Количество() = 1 Тогда
			// Если не задано представление, берем его из имени
			Если ПустаяСтрока(НайденныеСтроки[0].ПредставлениеФильтра) Тогда
				НайденныеСтроки[0].ПредставлениеФильтра = НайденныеСтроки[0].ИмяФильтра;
			КонецЕсли;

			// Для единичного значения используется все кроме первого слова
			СтрТипФильтра = Сред(СтрТипФильтра, Найти(СтрТипФильтра, " ") + 1);
			СтрФильтры=СтрФильтры		+", "+НайденныеСтроки[0].ПредставлениеФильтра+ " " + СтрТипФильтра 
			+ " "+НайденныеСтроки[0].ЗначениеФильтра;
		Иначе

			СтрФильтры=СтрФильтры+", "+НайденныеСтроки[0].ПредставлениеФильтра+" "+СтрТипФильтра+" [";
			Для Каждого НайденнаяСтрока Из НайденныеСтроки Цикл
				// Если не задано представление, берем его из имени
				Если ПустаяСтрока(НайденнаяСтрока.ПредставлениеФильтра) Тогда
					НайденнаяСтрока.ПредставлениеФильтра = НайденнаяСтрока.ИмяФильтра;
				КонецЕсли;

				СтрФильтры=СтрФильтры+НайденнаяСтрока.ЗначениеФильтра+", ";
			КонецЦикла;

			СтрФильтры=Лев(СокрЛП(СтрФильтры), СтрДлина(СокрЛП(СтрФильтры))-1)+"]";
		КонецЕсли;

	КонецЦикла;

	Если НЕ ОсновноеПоле Тогда
		ОсновноеПоле=Истина;
	КонецЕсли;

	Если ПоказателиОтчета.Количество() = 0 Тогда
		Предупреждение("Не выбрано ни одного показателя!", 60);
		ЕстьОшибки = Истина;
		Возврат;
	КонецЕсли;

	Для Каждого Строка Из ПоказателиОтчета цикл
		Если ПустаяСтрока(Строка.ИмяПоказателя) Тогда
			Предупреждение("Не указано имя показателя!", 60);
			Возврат;
		КонецЕсли;
		// Если не задано представление, берем его из имени
		Если ПустаяСтрока(Строка.ПредставлениеПоказателя) Тогда
			Строка.ПредставлениеПоказателя = Строка.ИмяПоказателя;
		КонецЕсли;
	КонецЦикла;

	// Структура вывода группировок: ключи определяют "основные" группировки,
	// значения - дополнительные.
	СтруктураВыводГруппировок = Новый Структура;
	МассивГруппировки    = Новый Массив;

	// Структура "поправок" сдвига группировок вправо: уровень записи строки 
	// формируемого запроса будет отличаться от нужного из-за пропусков группировок,
	// по которым не нужно выводить итоги.
	СтруктураСдвигУровняГруппировок = Новый Структура;

	// Далее используются таблицы значений, полученные выгрузкой строк из т.ч. отчета
	КопияГруппировкиОтчета = ГруппировкиОтчета .Выгрузить();

	СтруктураПоиска = Новый Структура;
	СтруктураПоиска.Вставить("РассчитыватьИтоги", Истина);

	НайденныеСтрокиСИтогами = КопияГруппировкиОтчета.НайтиСтроки(СтруктураПоиска);

	МассивБулево = Новый Массив;
	МассивБулево.Добавить(Тип("Булево"));
	ОписаниеТиповБулево = Новый ОписаниеТипов(МассивБулево);

	// Добавим две служебные колонки, содержащие признак ссылочности поля и 
	// возможность расчета итога по иерархии для тех полей, которым поставлен 
	// соответствующий флажок...
	КопияГруппировкиОтчета.Колонки.Добавить("ЕстьПолеПредставление", ОписаниеТиповБулево);
	КопияГруппировкиОтчета.Колонки.Добавить("ИтогПоИерархии", 		 ОписаниеТиповБулево);

	// ...и заполним эти служебные колонки
	ЗаполнитьПараметрыПолей(КопияГруппировкиОтчета);

	ВсегоГруппировок = 1;
	КолГруппировокБезИтогов = 0;
	Массив = Новый Массив;

	//Формируем тексты запросов

	// по регистру "ОстаткиТоваровКомпании": остатки.
	ТекстЗапроса = ТекстЗапроса + СформироватьТекстЗапроса("ТоварыНаСкладах", КопияГруппировкиОтчета, ИСТИНА);
	ТекстЗапроса = ТекстЗапроса + "
	|ОБЪЕДИНИТЬ ВСЕ
	|";
	// по регистру "ЗаказыПокупателей": остатки.
	ТекстЗапроса=ТекстЗапроса+СформироватьТекстЗапроса("ЗаказыПокупателей", КопияГруппировкиОтчета);
	ТекстЗапроса=ТекстЗапроса+"
	|ОБЪЕДИНИТЬ ВСЕ
	|";
	// по регистру "ЗаказыПоставщикам": остатки.
	ТекстЗапроса=ТекстЗапроса+СформироватьТекстЗапроса("ЗаказыПоставщикам", КопияГруппировкиОтчета);

	// Заголовок первой колонки шапки таблицы
	ЗаголовокКолонки = "Общий итог"; // этот заголовок выводится в случае отсутствия группировок

	Для Каждого СтрокаГруппировки Из КопияГруппировкиОтчета Цикл

		ДобавитьГруппировку = Ложь;
		Если (КопияГруппировкиОтчета.Индекс(СтрокаГруппировки) = КопияГруппировкиОтчета.Количество() - 1) Тогда
			ДобавитьГруппировку = Истина;
		Иначе
			Если КопияГруппировкиОтчета[КопияГруппировкиОтчета.Индекс(СтрокаГруппировки) + 1].РассчитыватьИтоги Тогда
				ДобавитьГруппировку = Истина;
			КонецЕсли;
		КонецЕсли;

		Если ДобавитьГруппировку Тогда
			СтруктураВыводГруппировок.Вставить(СтрокаГруппировки.ИмяГруппировки, Массив);
			Массив = Новый Массив;
		Иначе
			Массив.Добавить(СтрокаГруппировки.ИмяГруппировки);
			КолГруппировокБезИтогов = КолГруппировокБезИтогов + 1;
		КонецЕсли;

		ВсегоГруппировок = ВсегоГруппировок + 1;
		СтруктураСдвигУровняГруппировок.Вставить(СтрокаГруппировки.ИмяГруппировки, КолГруппировокБезИтогов);
		МассивГруппировки.Добавить(СтрокаГруппировки.ИмяГруппировки);

		Если Врег(СтрокаГруппировки.ТипИтога) = "ИЕРАРХИЯ" Тогда

			ТекстПоляИтогов = ТекстПоляИтогов + ",
			|	" + СтрокаГруппировки.ИмяГруппировки + " ИЕРАРХИЯ";

		ИначеЕсли Врег(СтрокаГруппировки.ТипИтога) = "ТОЛЬКО ИЕРАРХИЯ" Тогда

			ТекстПоляИтогов = ТекстПоляИтогов + ",
			|	" + СтрокаГруппировки.ИмяГруппировки + " ТОЛЬКО ИЕРАРХИЯ";

		Иначе
			ТекстПоляИтогов = ТекстПоляИтогов + ",
			|	" + СтрокаГруппировки.ИмяГруппировки;

		КонецЕсли;

		ТекстПоляИтогов = ТекстПоляИтогов + " КАК " + СтрокаГруппировки.ИмяГруппировки;

		Если СтрокаГруппировки.РассчитыватьИтоги Тогда

			ЗаголовокКолонки = СтрокаГруппировки.ПредставлениеГруппировки;

		КонецЕсли; 

	КонецЦикла;

	ТекстПоляИтогов = ТекстПоляИтогов + ",
	|Период КАК Период";

	Для Каждого СтрокаПоказатели Из ПоказателиОтчета Цикл // Формирование списка итогов запроса.

		ТекстПоляИтоговЗапроса = ТекстПоляИтоговЗапроса + ",
		|	СУММА(" + СтрокаПоказатели.ИмяПоказателя +")";

		СтрПоказатели = СтрПоказатели + ", " + СтрокаПоказатели.ПредставлениеПоказателя;
	КонецЦикла;

	////Задаем параметры для фильтров и формируем строку шапки
	//ТаблицаСписокВсехФильтров = ФильтрыОтчета.Выгрузить();
	//ТаблицаСписокВсехФильтров.Свернуть("ИмяФильтра, ПредставлениеФильтра, ОписаниеФильтра, ТипФильтра",);

	//СтруктураПоиска = Новый Структура("ИмяФильтра");

	//Для Каждого СтрокаФильтры Из ТаблицаСписокВсехФильтров Цикл

	//	СтруктураПоиска.ИмяФильтра = СтрокаФильтры.ИмяФильтра;

	//	НайденныеСтроки = ФильтрыОтчета.НайтиСтроки(СтруктураПоиска);

	//	СтрЗначенияФильтры = "";

	//	Для Каждого НайденнаяСтрока Из НайденныеСтроки Цикл

	//		Запрос.УстановитьПараметр("Параметр" + НайденнаяСтрока.НомерСтроки, НайденнаяСтрока.ЗначениеФильтра);

	//	КонецЦикла;

	//КонецЦикла;

	// Удаление лишних запятых
	ТекстПоляИтогов      = Сред(ТекстПоляИтогов,2);
	ТекстПоляИтоговЗапроса      = Сред(ТекстПоляИтоговЗапроса,2);

	ТекстОбщиеФильтры    = Сред(СокрЛП(ТекстОбщиеФильтры),2);
	ТекстПоляУпорядочивания = Сред(ТекстПоляУпорядочивания, 2);

	СтрПоказатели  = Сред(СтрПоказатели,  2);
	СтрГруппировки = Сред(СтрГруппировки, 2);
	СтрФильтры     = Сред(СтрФильтры,     2);
	
	ТекстЗапроса = ТекстЗапроса + Символы.ПС +
	"УПОРЯДОЧИТЬ ПО
	|	Период
	|";	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"ЗаказПокупателя.Склад.Представление ","ВЫБОР КОГДА ЗаказПокупателя.СкладГруппа ССЫЛКА Справочник.Склады ТОГДА ЗаказПокупателя.СкладГруппа.Представление ИНАЧЕ """" КОНЕЦ ");
    ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"ЗаказПокупателя.Склад ","ВЫБОР КОГДА ЗаказПокупателя.СкладГруппа ССЫЛКА Справочник.Склады ТОГДА ЗаказПокупателя.СкладГруппа ИНАЧЕ ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка) КОНЕЦ ");
	Запрос.Текст=ТекстЗапроса;
	Запрос.УстановитьПараметр("ДатаОтчета",?(ДатаОтчета='00010101000000','00010101000000',КонецДня(ДатаОтчета)));

	Если Не ПустаяСтрока(ТекстПоляИтогов) Тогда 
		Запрос.Текст = Запрос.Текст + Символы.ПС +  
		"ИТОГИ " + ТекстПоляИтоговЗапроса + Символы.ПС + 
		" ПО " + ТекстПоляИтогов;
	КонецЕсли;
	
	Макет = ПолучитьМакет("Макет");

	ДокументРезультат.Очистить();
	СтруктураПараметров = Новый Структура;
	СтруктураПараметров.Вставить("ОбщийОтступ", Макет.ПолучитьОбласть("ОбщийОтступ|Строка"));
	СтруктураПараметров.Вставить("ЗначениеГруппировки",   Макет.ПолучитьОбласть("Значение|Строка"));
	СтруктураПараметров.Вставить("ЗначениеГруппировкиГор",   Макет.ПолучитьОбласть("План|ШапкаГруппы"));
	СтруктураПараметров.Вставить("ЗаголовкиПоказателей",Макет.ПолучитьОбласть("Показатели|Строка"));

	СтруктураПараметров.Вставить("ЗначенияПоказателя",    Макет.ПолучитьОбласть("План|Строка"));
	СтруктураПараметров.Вставить("ТабДок",    ДокументРезультат);

	СтруктураПараметров.Вставить("МассивПоказатели", ПоказателиОтчета.ВыгрузитьКолонку("ИмяПоказателя"));

	СтруктураПараметров.Вставить("ВсегоГруппировок", 			ВсегоГруппировок);
	СтруктураПараметров.Вставить("СтруктураВыводГруппировок", 	СтруктураВыводГруппировок);

	СтруктураПараметров.Вставить("МассивГруппировки", 	МассивГруппировки);

	СтруктураПараметров.Вставить("СтруктураСдвигУровняГруппировок", СтруктураСдвигУровняГруппировок);

	// наклонный шрифт для групп
	СтруктураПараметров.Вставить("ШрифтГрупп", Новый Шрифт(Макет.Область("Строка|План").Шрифт,,,,Истина));

	// форматная строка для вывода показателей
	СтруктураПараметров.Вставить("ФорматПоказателей", 			Новый Структура);
	Для Каждого Строка Из ПоказателиОтчета Цикл
		МетаданныеРесурса = 0;
		ФорматнаяСтрока = "";

		Нстр = мТаблицаПоказатели.Найти(Строка.ИмяПоказателя, "ИмяПоля");
		Если Не (Нстр = Неопределено) Тогда
			ФорматнаяСтрока = НСтр.ФорматнаяСтрока;
		КонецЕсли;

		// Если не задано при нач. заполнении, то из метаданных
		Если ПустаяСтрока(ФорматнаяСтрока) Тогда
			Если мСтруктураМетаданныеРегистраРесурсы.Свойство(Строка.ИмяПоказателя, МетаданныеРесурса) Тогда
				ФорматнаяСтрока = "ЧЦ = " 	+ Строка(МетаданныеРесурса.Тип.КвалификаторыЧисла.Разрядность) 
				+ " ; ЧДЦ = " 
				+ Строка(МетаданныеРесурса.Тип.КвалификаторыЧисла.РазрядностьДробнойЧасти);
			КонецЕсли;
		КонецЕсли;

		СтруктураПараметров.ФорматПоказателей.Вставить(Строка.ИмяПоказателя, ФорматнаяСтрока);
	КонецЦикла;

	// Оформление измерений
	ОформлениеСтроки = Новый Массив;

	ОформлениеСтрокиИерархии = Новый Массив;
	
	Если РаскрашиватьИзмерения Тогда

		ТабДокОформлениеИзмерений = Макет.ПолучитьОбласть("ОформлениеИзмерений");
		Для Сч = 1 По ТабДокОформлениеИзмерений.ВысотаТаблицы Цикл

			// Нечетные строки - иерархия, четные - измерения
			Если Сч - Цел(Сч/2)*2 = 1 Тогда

				ОформлениеСтроки.Добавить(ТабДокОформлениеИзмерений.Область(Сч,2));
			Иначе
				ОформлениеСтрокиИерархии.Добавить(ТабДокОформлениеИзмерений.Область(Сч,2));
			КонецЕсли;
							
		КонецЦикла;

	КонецЕсли; 

	// Оформление строк
	СтруктураПараметров.Вставить("ОформлениеСтроки", ОформлениеСтроки);
	СтруктураПараметров.Вставить("ОформлениеСтрокиИерархии", ОформлениеСтрокиИерархии);
	СтруктураПараметров.Вставить("СтруктураФорматаПолей", Новый Структура);

	// 1. Вывод шапки таблицы
	Для Сч = 1 По 3 Цикл
		Если Сч = 1 Тогда
			Префикс = "Верх";
		ИначеЕсли Сч = 2 Тогда
			// Если нет фильтров, не выводим
			Если ПустаяСтрока(СтрФильтры) Тогда
				Продолжить;
			КонецЕсли;
			Префикс = "Середина";
		ИначеЕсли Сч = 3 Тогда
			Префикс = "Низ";
		КонецЕсли;

		ДокументРезультат.Вывести(Макет.ПолучитьОбласть("ОбщийОтступ|Шапка" + Префикс));

		// Вывод шапки отчета
		ОбластьЗначение   = Макет.ПолучитьОбласть("Значение|Шапка" + Префикс);

		Если Префикс = "Верх" Тогда

			ОбластьЗначение.Параметры.ЗаголовокОтчета = "Оперативный товарный календарь";

			ОбластьЗначение.Параметры.Период = "По состоянию заказов на " +Формат(ДатаОтчета,"ДЛФ=ДД");

			Если  ДатаОтчета = '00010101000000' Тогда
				ОбластьЗначение.Параметры.Период = "По состоянию заказов на момент последнего движения ";
			Иначе
				ОбластьЗначение.Параметры.Период = "По состоянию заказов на: " + Формат(ДатаОтчета, "ДФ = ""дд.ММ.гггг""");
			КонецЕсли;

			ДокументРезультат.Присоединить(ОбластьЗначение);

		ИначеЕсли Префикс = "Середина" Тогда

			ОбластьЗначение.Параметры.Фильтры = "Отбор: " + СтрФильтры;
			ДокументРезультат.Присоединить(ОбластьЗначение);

			ДокументРезультат.Область(ДокументРезультат.ВысотаТаблицы, 2, 
			ДокументРезультат.ВысотаТаблицы,  4).Объединить();

		ИначеЕсли Префикс = "Низ" Тогда

			ОбластьЗначение.Параметры.Группировки="Группировки: " +СтрГруппировки;
			ОбластьЗначение.Параметры.Показатели = "Показатели: " + СтрПоказатели;
			ДокументРезультат.Присоединить(ОбластьЗначение);

			ДокументРезультат.Область(ДокументРезультат.ВысотаТаблицы-1, 2, 
			ДокументРезультат.ВысотаТаблицы-1, 4).Объединить();

			ДокументРезультат.Область(ДокументРезультат.ВысотаТаблицы, 2, 
			ДокументРезультат.ВысотаТаблицы,   4).Объединить();

		КонецЕсли;

	КонецЦикла;

	// Параметр для показа заголовка
	ВысотаЗаголовка = ДокументРезультат.ВысотаТаблицы;

	// Когда нужен только заголовок:
	Если ТолькоЗаголовок Тогда

		Возврат;
	
	КонецЕсли; 

	// Выполнение сформированного запроса
	Результат = Запрос.Выполнить();

	ДокументРезультат.Вывести(Макет.ПолучитьОбласть("ОбщийОтступ|ШапкаГруппы"));
	ОбластьЗначение   = Макет.ПолучитьОбласть("Значение|ШапкаГруппы");

	ОбластьЗначение.Область(2,1).Текст = ЗаголовокКолонки;

	ДокументРезультат.Присоединить(ОбластьЗначение);

	ДокументРезультат.Присоединить(Макет.ПолучитьОбласть("Показатели|ШапкаГруппы"));

	СтруктураПараметров.ЗначениеГруппировкиГор.Параметры.ПредставлениеГруппировки="Остаток на дату отчета";
	ДокументРезультат.Присоединить(СтруктураПараметров.ЗначениеГруппировкиГор);

	СтруктураПараметров.ЗначениеГруппировкиГор.Параметры.ПредставлениеГруппировки="Итого";
	ДокументРезультат.Присоединить(СтруктураПараметров.ЗначениеГруппировкиГор);

	ВывестиШапку(Результат.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам,"Период","Все"), СтруктураПараметров);

	// Зафиксируем заголовок отчета
	ДокументРезультат.ФиксацияСверху = ДокументРезультат.ВысотаТаблицы;
	ДокументРезультат.ФиксацияСлева = 3;

	// 2. Вывод строк отчета
	ДокументРезультат.НачатьАвтогруппировкуСтрок();

	Если МассивГруппировки.Количество()>0 Тогда

		ВывестиГруппировку(Результат.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, МассивГруппировки[0]), СтруктураПараметров, 0, Новый Массив);
		
	КонецЕсли;

	ДокументРезультат.ЗакончитьАвтогруппировкуСтрок();

	// 3. Вывод подвала отчета
	ДокументРезультат.Вывести(Макет.ПолучитьОбласть("ОбщийОтступ|Подвал"));

	ОбластьЗначение   = Макет.ПолучитьОбласть("Значение|Подвал");

	ДокументРезультат.Присоединить(ОбластьЗначение);

	ОбластьПоказатель = Макет.ПолучитьОбласть("План|Подвал");

	// Управление заголовком
	Если ЗначениеЗаполнено(ВысотаЗаголовка) Тогда
		ДокументРезультат.Область(1,,ВысотаЗаголовка).Видимость = ПоказыватьЗаголовок;
	КонецЕсли;

	// Первую колонку не печатаем
	ДокументРезультат.ОбластьПечати = ДокументРезультат.Область(1,2,ДокументРезультат.ВысотаТаблицы,ДокументРезультат.ШиринаТаблицы);

	// Заполним общую расшифровку:
	СтруктураНастроекОтчета = Новый Структура;
	СтруктураНастроекОтчета.Вставить("ИмяОтчета", мИмяОтчета);

	Для Каждого Реквизит Из Метаданные.Отчеты[мИмяОтчета].Реквизиты Цикл
		СтруктураНастроекОтчета.Вставить(Реквизит.Имя, ЭтотОбъект[Реквизит.Имя]);
	КонецЦикла;

	Для Каждого ТабличнаяЧасть Из Метаданные.Отчеты[мИмяОтчета].ТабличныеЧасти Цикл
		СтруктураНастроекОтчета.Вставить(ТабличнаяЧасть.Имя, ЭтотОбъект[ТабличнаяЧасть.Имя].Выгрузить());
	КонецЦикла;
	
	ДокументРезультат.Область(1,1).Расшифровка = СтруктураНастроекОтчета;

	ДокументРезультат.ОриентацияСтраницы=ОриентацияСтраницы.Ландшафт;
	ДокументРезультат.Автомасштаб=Истина;

КонецПроцедуры

// Процедура выводит шапку отчета (остатки на начало, сводные обороты и 
// все периоды, по которым есть данные в запросе.
// Параметры:
// Выборка       - выборка из результата отчета, которая обходится в процедуре
// СтруктураПараметров - структура параметров, необходимая для вывода шапки
//
Процедура ВывестиШапку(Выборка, СтруктураПараметров)

	ОбластьЗначениеГруппировкиГор   = СтруктураПараметров.ЗначениеГруппировкиГор;
	ТабДок = СтруктураПараметров.ТабДок;

	ОбработкаПрерыванияПользователя();

	Пока Выборка.Следующий() Цикл

		Если Выборка["Период"]=ДатаОтчета Тогда
			Продолжить;  // Остатки на дату отчета выводятся в начале отчета.
		КонецЕсли;

		ЗначениеВыборки=Выборка["Период"];

		Если ПустаяСтрока(ЗначениеВыборки) ИЛИ Выборка["Период"]<ДатаОтчета Тогда
			ЗначениеВыборки="("+Формат(Выборка["Период"],"ДЛФ=Д")+")";
		Иначе
			ЗначениеВыборки = Формат(Выборка["Период"],"ДЛФ=Д");
		КонецЕсли;

		ОбластьЗначениеГруппировкиГор.Параметры.ПредставлениеГруппировки=ЗначениеВыборки;

		ТабДок.Присоединить(ОбластьЗначениеГруппировкиГор);

		Если (НЕ ПустаяСтрока(Выборка["Период"])) Тогда
			Если Выборка["Период"]<ДатаОтчета Тогда
				ТабДок.Область(	ТабДок.ВысотаТаблицы,ТабДок.ШиринаТаблицы,
				ТабДок.ВысотаТаблицы,ТабДок.ШиринаТаблицы).ЦветТекста=Новый Цвет(255,0,0);
			КонецЕсли;
		КонецЕсли;

		мЗначимыеГруппировкиГор.Добавить(Выборка["Период"]);

	КонецЦикла;

КонецПроцедуры // ВывестиШапку()

// Процедура выводит строку данных в отчет
// Параметры:
// 	Выборка       - выборка из результата отчета, которая обходится в процедуре
// 	СтруктураПараметров - структура параметров, необходимая для вывода шапки
//  НачальныйОстаток - остаток на начало периода отчета, от которого рассчитываются
//  последующие
//
Процедура РазвернутьСтроку (Выборка, СтруктураПараметров,ТабДок,НачальныйОстаток)

	ОбластьЗначенияПоказателя    = СтруктураПараметров.ЗначенияПоказателя;
	//Структура для значений эталонных данных

	СтруктураЭталон=Новый Структура;
	ЗначениеЭталон=0;
	ВыведеноПериодов=1;

	Пока Выборка.Следующий() Цикл

		Если Выборка["Период"]=ДатаОтчета Тогда
			Продолжить;  // Остатки на дату отчета выводим в начале отчета.
		КонецЕсли;

		Если мЗначимыеГруппировкиГор.НайтиПоЗначению(Выборка["Период"])<>Неопределено Тогда

			Поступление			=?(ПустаяСтрока(Выборка["Поступление"]),0,Выборка["Поступление"]);
			Отгрузка			=?(ПустаяСтрока(Выборка["Отгрузка"]),0,Выборка["Отгрузка"]);
			НачальныйОстаток	=НачальныйОстаток+Поступление-Отгрузка;

			ЗначениеПоказателя="";
			ФорматПоказателя = "ЧЦ = 14; ЧДЦ = 3; ЧО=0; ЧН=""-""";

			ОбластьЗначенияПоказателя.Параметры.Поступление=""+Формат(Поступление, ФорматПоказателя);
			ОбластьЗначенияПоказателя.Параметры.Отгрузка=""+Формат(Отгрузка, ФорматПоказателя);
			ОбластьЗначенияПоказателя.Параметры.НачальныйОстаток=""+Формат(НачальныйОстаток, ФорматПоказателя);

            ПараметрыПоступление=Новый Структура;
			ПараметрыПоступление.Вставить("ИмяРегистра","ЗаказыПоставщикам");
            ПараметрыПоступление.Вставить("ДатаПоставки",Выборка["Период"]);
            ОбластьЗначенияПоказателя.Параметры.РасшифровкаПоступление=ПараметрыПоступление;

            ПараметрыОтгрузка=Новый Структура;
			ПараметрыОтгрузка.Вставить("ИмяРегистра","ЗаказыПокупателей");
            ПараметрыОтгрузка.Вставить("ДатаОтгрузки",Выборка["Период"]);
            ОбластьЗначенияПоказателя.Параметры.РасшифровкаОтгрузка=ПараметрыОтгрузка;
	
			ТабДок.Присоединить(ОбластьЗначенияПоказателя);

			Если НачальныйОстаток<0 Тогда
				А=ТабДок.ВысотаТаблицы;
				В=5+ВыведеноПериодов;

				Если НачальныйОстаток<0 Тогда
					ТабДок.Область(А,В,А,В).ЦветТекста=Новый Цвет(255,0,0);
				КонецЕсли;

			КонецЕсли;

			ВыведеноПериодов=ВыведеноПериодов+1;

		КонецЕсли;

	КонецЦикла;

КонецПроцедуры 

////////////////////////////////////////////////////////////////////////////////
// ОПЕРАТОРЫ ОСНОВНОЙ ПРОГРАММЫ
// 

мДеревоГруппировки 	= Новый ДеревоЗначений;
мТаблицаПоказатели  = Новый ТаблицаЗначений;
мДеревоФильтры     	= Новый ДеревоЗначений;
мТаблицаТипы	    = Новый ТаблицаЗначений;

МассивСтрока = Новый Массив; 
МассивСтрока.Добавить(Тип("Строка"));
КвалификаторСтроки = Новый КвалификаторыСтроки(100);
ОписаниеТиповСтрока = Новый ОписаниеТипов(МассивСтрока, , КвалификаторСтроки);

МассивБулево = Новый Массив;
МассивБулево.Добавить(Тип("Булево"));
ОписаниеТиповБулево = Новый ОписаниеТипов(МассивБулево);

МассивТаблицаЗначений = Новый Массив;
МассивТаблицаЗначений.Добавить(Тип("ТаблицаЗначений"));
ОписаниеТиповТаблицаЗначений = Новый ОписаниеТипов(МассивТаблицаЗначений);

ОписаниеТиповБулевоИСтрока = Новый ОписаниеТипов(ОписаниеТиповСтрока, МассивБулево);

МассивТаблицаЗначений = Новый Массив;
МассивТаблицаЗначений.Добавить(Тип("ТаблицаЗначений"));
ОписаниеТиповТаблицаЗначений = Новый ОписаниеТипов(МассивТаблицаЗначений);

МассивОписаниеТипов = Новый Массив;
МассивОписаниеТипов.Добавить(Тип("ОписаниеТипов"));
ОписаниеТиповОписаниеТипов = Новый ОписаниеТипов(МассивОписаниеТипов);

МассивСвойство = Новый Массив;
МассивСвойство.Добавить(Тип("ПланВидовХарактеристикСсылка.СвойстваОбъектов"));
МассивСвойство.Добавить(Тип("ПланВидовХарактеристикСсылка.НазначенияСвойствКатегорийОбъектов"));
ОписаниеТиповСвойство = Новый ОписаниеТипов(МассивСвойство);

// Инициализация таблиц всех возможных показателей, группировок,  фильтров

мТаблицаПоказатели.Колонки.Добавить("ИмяПоля", ОписаниеТиповСтрока);
мТаблицаПоказатели.Колонки.Добавить("ПредставлениеПоля", ОписаниеТиповСтрока);
мТаблицаПоказатели.Колонки.Добавить("ОписаниеПоля", ОписаниеТиповСтрока);
мТаблицаПоказатели.Колонки.Добавить("ВклПоУмолчанию", ОписаниеТиповБулево);
мТаблицаПоказатели.Колонки.Добавить("Пометка", ОписаниеТиповБулево);
мТаблицаПоказатели.Колонки.Добавить("ФорматнаяСтрока", ОписаниеТиповСтрока);

мДеревоГруппировки.Колонки.Добавить("ИмяПоля", ОписаниеТиповСтрока);
мДеревоГруппировки.Колонки.Добавить("ПредставлениеПоля", ОписаниеТиповСтрока);
мДеревоГруппировки.Колонки.Добавить("ОписаниеПоля", ОписаниеТиповСтрока);
мДеревоГруппировки.Колонки.Добавить("РассчитыватьИтоги", ОписаниеТиповБулево);
мДеревоГруппировки.Колонки.Добавить("ТипИтога", ОписаниеТиповБулево);
мДеревоГруппировки.Колонки.Добавить("ВыводитьПустые", ОписаниеТиповБулевоИСтрока);
мДеревоГруппировки.Колонки.Добавить("ВыводитьПустыеСвязаннаяГруппировка", ОписаниеТиповСтрока);
мДеревоГруппировки.Колонки.Добавить("НазначенияСвойств", ОписаниеТиповТаблицаЗначений);
мДеревоГруппировки.Колонки.Добавить("ВклПоУмолчанию", ОписаниеТиповБулево);
мДеревоГруппировки.Колонки.Добавить("Пометка", ОписаниеТиповБулево);
мДеревоГруппировки.Колонки.Добавить("ОписаниеТипов", ОписаниеТиповОписаниеТипов);
мДеревоГруппировки.Колонки.Добавить("Свойство", ОписаниеТиповСвойство);

мДеревоФильтры.Колонки.Добавить("ИмяПоля", ОписаниеТиповСтрока);
мДеревоФильтры.Колонки.Добавить("ПредставлениеПоля", ОписаниеТиповСтрока);
мДеревоФильтры.Колонки.Добавить("ОписаниеПоля", ОписаниеТиповСтрока);
мДеревоФильтры.Колонки.Добавить("ИмяПоляВладелец", ОписаниеТиповСтрока);
мДеревоФильтры.Колонки.Добавить("ВклПоУмолчанию", ОписаниеТиповБулево);
мДеревоФильтры.Колонки.Добавить("Пометка", ОписаниеТиповБулево);
мДеревоФильтры.Колонки.Добавить("ОписаниеТипов", ОписаниеТиповОписаниеТипов);
мДеревоФильтры.Колонки.Добавить("Свойство", ОписаниеТиповСвойство);

мСтруктураТиповФильтров = Новый Структура;
мСтруктураТиповФильтров.Вставить("ОдноИз", "Одно из:");
мСтруктураТиповФильтров.Вставить("ВсеКроме", "Все, кроме:");

мТипФильтраПоУмолчанию = "ОдноИз";

мТаблицаТипы.Колонки.Добавить("ИмяПоля", ОписаниеТиповСтрока);
мТаблицаТипы.Колонки.Добавить("ОписаниеТипов");

мЗначимыеГруппировкиГор=Новый ("СписокЗначений");

мСтруктураНазванийПолейСортировки = Новый Структура;
мСтруктураНазванийПолейСортировки.Вставить("Оборот", "Оборот");

мИмяОтчета = Метаданные().Имя;

мНазваниеОтчета = "";

мВыбиратьИмяРегистра = Истина;

мНаДату = Ложь;

ПоказыватьЗаголовок = Истина;

мСписокОбязательныхОтборов = Новый СписокЗначений;

мСтруктураМетаданныеРегистраРесурсы = Новый Структура;

#КонецЕсли