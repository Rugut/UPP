Перем ВысотаЗаголовка;
Перем ИдентификаторОкнаРасшифровки;
Перем ФормаНастройка;
Перем СтруктураРеквизитов;
Перем НеВосстанавливатьНастройку;

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОБЩЕГО НАЗНАЧЕНИЯ

// Управляет пометками кнопок ком. панели
//
// Параметры:
//	Нет.
//
Процедура УправлениеПометкамиКнопокКоманднойПанели()
	
	Если ПоказыватьЗаголовок Тогда
		ЭлементыФормы.КоманднаяПанельФормы.Кнопки.Заголовок.Пометка = Истина;
		ЭлементыФормы.КоманднаяПанельФормы.Кнопки.Подменю.Кнопки.Заголовок.Пометка = Истина;

	Иначе
		ЭлементыФормы.КоманднаяПанельФормы.Кнопки.Заголовок.Пометка = Ложь;
		ЭлементыФормы.КоманднаяПанельФормы.Кнопки.Подменю.Кнопки.Заголовок.Пометка = Ложь;

	КонецЕсли;
	
КонецПроцедуры // УправлениеПометкамиКнопокКоманднойПанели()

// Обновляет таблицу отчета
//
// Параметры:
//	Нет.
//
Процедура ОбновитьОтчет() Экспорт

	ЗаполнитьОбъектПоДиалогу();
	
	СформироватьОтчет(ЭлементыФормы.ДокументРезультат, ПоказыватьЗаголовок, ВысотаЗаголовка);

	// Заполним общую расшифровку:
	СтруктураНастроекОтчета = Новый Структура;

	ЭтаФорма.ТекущийЭлемент = ЭлементыФормы.ДокументРезультат;

	УправлениеПометкамиКнопокКоманднойПанели();
	
КонецПроцедуры // ОбновитьОтчет()

//  Заполняет реквизиты по диалогу
//
// Параметры:
//	Нет.
//
Процедура ЗаполнитьОбъектПоДиалогу()

	// Здесь должно быть расположено заполнение реквизитов
	// отчета по реквизитам формы, непосредственно не связанным
	// с реквизитами отчета (если таковые имеются)
	
КонецПроцедуры // ЗаполнитьОбъектПоДиалогу()

// Заполняет диалог по значениям реквизитов отчета
//
// Параметры:
//	Нет.
//
Процедура ЗаполнитьДиалогПоОбъекту()

	// Здесь должно быть расположено заполнение реквизитов формы
	// по реквизитам отчета, если они непосредственно не связаны
	// с реквизитами отчета (если таковые имеются)
	
КонецПроцедуры // ЗаполнитьДиалогПоОбъекту()

//  Управляет выводом заголовка
//
// Параметры:
//	Нет.
//
Процедура ВыводЗаголовка()

	// Перезаполнять заголовок можно только у "чистого" отчета
	Если ЭлементыФормы.ДокументРезультат.ВысотаТаблицы = 0 Тогда

		ЗаполнитьОбъектПоДиалогу();

		СформироватьОтчет(ЭлементыФормы.ДокументРезультат, ПоказыватьЗаголовок, ВысотаЗаголовка, Истина);
		ЭлементыФормы.ДокументРезультат.ТекущаяОбласть = ЭлементыФормы.ДокументРезультат.Область(1,1,1,1);

	КонецЕсли;


	Если ЗначениеЗаполнено(ВысотаЗаголовка) Тогда
		ЭлементыФормы.ДокументРезультат.Область(1,,ВысотаЗаголовка).Видимость = ПоказыватьЗаголовок;
	КонецЕсли;

	УправлениеПометкамиКнопокКоманднойПанели();
	
КонецПроцедуры // ВыводЗаголовка()

// Формирует текст заголовка
//
// Параметры:
//	Нет.
//
Процедура СформироватьЗаголовокФормы()

	// Вывод заголовка, описателя периода и фильтров и заголовка
	Если ДатаОтчета = '00010101000000' Тогда

		ОписаниеПериода     = "На текущий момент";

	Иначе

		ОписаниеПериода     = "" + Формат(ДатаОтчета, "ДФ = ""дд.ММ.гггг""; ДП = ""...""");

	КонецЕсли;


	Заголовок=мНазваниеОтчета+" (" + ОписаниеПериода + ") ";

КонецПроцедуры // СформироватьЗаголовокФормы()

// Функция формирует значение отбора используемое в запросе
// Параметры:
//    НайденныеСтроки - Строки табличной части "ФильтрыОтчета"
//    СоздаватьСписок - Булево, Флаг создания списка значений с значениями отбора
//
// Возвращаемое значение:
//   <Тип.Вид>   - СписокЗначений, Элемент справочника
//
Функция ПолучитьЗначениеФильтра(НайденныеСтроки, СоздаватьСписок)
	Перем ЗначениеФильтра;
	Если СоздаватьСписок Тогда
		ЗначениеФильтра = Новый СписокЗначений();
		Для Каждого Отбор Из НайденныеСтроки Цикл
			ЗначениеФильтра.Добавить(Отбор.ЗначениеФильтра);
		КонецЦикла;
	Иначе
		ЗначениеФильтра = ?(НайденныеСтроки.Количество() = 1, НайденныеСтроки[0].ЗначениеФильтра, Неопределено);
	КонецЕсли;

	Возврат ЗначениеФильтра;
КонецФункции // ПолучитьЗначениеФильтра()

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ НАЖАТИЯ КНОПОК КОМАНДНОЙ ПАНЕЛИ

// Процедура - обработчик нажатия кнопки "Настройка".
//
Процедура КоманднаяПанельФормыНастройка(Кнопка)
	
	ФормаНастройка = ПолучитьФорму("ФормаНастройка", ЭтаФорма);
	
	Если ФормаНастройка.ОткрытьМодально() = Истина Тогда
		ОбновитьОтчет();
	КонецЕсли;

КонецПроцедуры // ДополнительноНажатие()

// Процедура - обработчик нажатия кнопки "Обновить".
//
Процедура КоманднаяПанельФормыОбновить(Кнопка)

	ОбновитьОтчет();

КонецПроцедуры // ВыполнитьНажатие()

// Процедура - обработчик нажатия кнопки "Заголовок".
//
Процедура КоманднаяПанельЗаголовок(Кнопка)
	ПоказыватьЗаголовок = Не ПоказыватьЗаголовок;
	ВыводЗаголовка();
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

// Процедура - обработчик закрытия формы
//
Процедура ПриЗакрытии()
	
	ИмяОтчета = ЭтотОбъект.Метаданные().Имя;
	
	// Запомним параметры для данной формы
	СохранитьЗначение( ИмяОтчета+ "_ЗаголовокПомечен", ЭлементыФормы.КоманднаяПанельФормы.Кнопки.Заголовок.Пометка);

КонецПроцедуры // ПриЗакрытии()

// Процедура - обработчик обновления данных формы
//
Процедура ОбновлениеОтображения()

	СформироватьЗаголовокФормы();
	
КонецПроцедуры // ОбновлениеОтображения()

// Процедура - обработчик события перед открытием формы
//
Процедура ПередОткрытием(Отказ, СтандартнаяОбработка)

	Если НЕ ЗначениеЗаполнено(мНазваниеОтчета) Тогда

		ЗаполнитьНачальныеНастройки();

		// По умолчанию настраивается так:
		РаскрашиватьИзмерения = Истина;

		ВыводитьИтогиПоВсемУровням = Истина;


	КонецЕсли; 

	ЗаполнитьДиалогПоОбъекту();

	// Если форма открывается для несформированного отчета ...
	Если ЭлементыФормы.ДокументРезультат.ВысотаТаблицы = 0 Тогда

		ВысотаЗаголовка = 0;

		ВыводЗаголовка();

		СформироватьЗаголовокФормы();

	Иначе

		СтруктураРеквизитов = Новый Структура;

		Для каждого Реквизит Из Метаданные().Реквизиты Цикл
			СтруктураРеквизитов.Вставить(Реквизит.Имя, ЭтотОбъект[Реквизит.Имя]);
		КонецЦикла; 

		Для каждого ТЧ Из Метаданные().ТабличныеЧасти Цикл
			СтруктураРеквизитов.Вставить(ТЧ.Имя, ЭтотОбъект[ТЧ.Имя].Выгрузить());
		КонецЦикла; 

		НеВосстанавливатьНастройку = Истина;

	КонецЕсли; 

	УправлениеПометкамиКнопокКоманднойПанели();

КонецПроцедуры // ПередОткрытием()

// Процедура - обработчик события перед сохранением значений формы
//
Процедура ПередСохранениемЗначений(Отказ)

	СохраненныеНастройки = Новый Структура;
	СохраненныеНастройки.Вставить("ПоказателиОтчета", ПоказателиОтчета.Выгрузить());
	СохраненныеНастройки.Вставить("ГруппировкиОтчета", ГруппировкиОтчета.Выгрузить());
	СохраненныеНастройки.Вставить("ФильтрыОтчета", ФильтрыОтчета.Выгрузить());

	// Остальные реквизиты отчета сохраняются стандартно
	
КонецПроцедуры // ПередСохранениемЗначений()

// Процедура - обработчик события после восстановления значений формы
//
Процедура ПослеВосстановленияЗначений()

	Если Не Открыта() И (НеВосстанавливатьНастройку = Истина) Тогда

		Для каждого Реквизит Из Метаданные().Реквизиты Цикл
			ЭтотОбъект[Реквизит.Имя] = СтруктураРеквизитов[Реквизит.Имя];
		КонецЦикла; 

		Для каждого ТЧ Из Метаданные().ТабличныеЧасти Цикл
			ЭтотОбъект[ТЧ.Имя].Загрузить(СтруктураРеквизитов[ТЧ.Имя]);
		КонецЦикла; 

		Возврат;

	КонецЕсли; 

	Если ТипЗнч(СохраненныеНастройки) = Тип("Структура") Тогда

		ПоказателиОтчета.Загрузить(СохраненныеНастройки.ПоказателиОтчета);
		ГруппировкиОтчета.Загрузить(СохраненныеНастройки.ГруппировкиОтчета);
		ФильтрыОтчета.Загрузить(СохраненныеНастройки.ФильтрыОтчета);
		// Остальные реквизиты отчета восстанавливаются стандартно

	КонецЕсли; 

	ЗаполнитьДиалогПоОбъекту();

	ЭлементыФормы.ДокументРезультат.Очистить();
	
	ВыводЗаголовка();

	СформироватьЗаголовокФормы();
	
КонецПроцедуры // ПослеВосстановленияЗначений()

// Процедура - обработчик сообщений
//
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)

	Если ИмяСобытия = "ИзмененоИмяРегистра" И Источник = ФормаНастройка Тогда
		СформироватьЗаголовокФормы();
		ЗаполнитьДиалогПоОбъекту();
	КонецЕсли; 
	
КонецПроцедуры // ОбработкаОповещения()

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ФОРМЫ

// Возвращает отчет с заданым именем и параметрами, настроенными для расшифровки
//
Функция ПодготовитьОтчет(ИмяОтчета, Расшифровка, Элемент, СтрокаЯчейки = "")

	НовыйОтчет = Отчеты[ИмяОтчета].Создать();

	НовыйОтчет.УниверсальныйОтчет.ДатаНач=НачалоДня(ДатаОтчета);
	НовыйОтчет.УниверсальныйОтчет.ДатаКон=КонецДня(ДатаОтчета);

	НовыйОтчет.УниверсальныйОтчет.ПоказыватьЗаголовок = ПоказыватьЗаголовок;

	НовыйОтчет.УстановитьНачальныеНастройки();

	ТаблицаСписокВсехФильтров = ФильтрыОтчета.Выгрузить();
	ТаблицаСписокВсехФильтров.Свернуть("ИмяФильтра, ПредставлениеФильтра, ТипФильтра, ИспользованиеФильтра",);
	СтруктураПоиска = Новый Структура("ИмяФильтра");
	Для Каждого Строка Из ТаблицаСписокВсехФильтров Цикл
		Если НЕ Строка.ИспользованиеФильтра Тогда
			Продолжить;
		КонецЕсли;
		НайденнаяСтрока = мДеревоФильтры.Строки.Найти(Строка.ИмяФильтра, "ИмяПоля", Истина);
		Если НайденнаяСтрока<>Неопределено Тогда

			ОписаниеФильтра = НайденнаяСтрока.ОписаниеПоля;

			Если Найти(ОписаниеФильтра,"#")>0 Тогда // Из документа
				Если ИмяОтчета="ВедомостьЗаказыПокупателей" Тогда
					ОписаниеФильтра=СтрЗаменить(ОписаниеФильтра,"#","ЗаказПокупателя.");
				Иначе
					ОписаниеФильтра=СтрЗаменить(ОписаниеФильтра,"#","ЗаказПоставщику.");
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		Если ПустаяСтрока(ОписаниеФильтра) Тогда
			ОписаниеФильтра = Строка.ИмяФильтра;
		КонецЕсли;
		
		СтруктураПоиска.ИмяФильтра = ОписаниеФильтра;
		НайденныеСтроки = ФильтрыОтчета.НайтиСтроки(СтруктураПоиска);

		ВидСравненияОтбора = ВидСравнения.Равно;

		Если Врег(Строка.ТипФильтра) = "РАВНО" Тогда
			ЗначениеФильтра = ПолучитьЗначениеФильтра(НайденныеСтроки, Ложь);
			НовыйОтчет.УниверсальныйОтчет.ДобавитьОтбор(ОписаниеФильтра, Истина, ВидСравнения.Равно, ЗначениеФильтра,,, Ложь);
		ИначеЕсли Врег(Строка.ТипФильтра) = "НЕ РАВНО" Тогда
			ЗначениеФильтра = ПолучитьЗначениеФильтра(НайденныеСтроки, Ложь);
			НовыйОтчет.УниверсальныйОтчет.ДобавитьОтбор(ОписаниеФильтра, Истина, ВидСравнения.НеРавно, ЗначениеФильтра,,, Ложь);
		ИначеЕсли Врег(Строка.ТипФильтра) = "В СПИСКЕ" Тогда
			ЗначениеФильтра = ПолучитьЗначениеФильтра(НайденныеСтроки, Истина);
			НовыйОтчет.УниверсальныйОтчет.ДобавитьОтбор(ОписаниеФильтра, Истина, ВидСравнения.ВСписке, ЗначениеФильтра,,, Ложь);
		ИначеЕсли Врег(Строка.ТипФильтра) = "НЕ В СПИСКЕ" Тогда
			ЗначениеФильтра = ПолучитьЗначениеФильтра(НайденныеСтроки, Истина);
			НовыйОтчет.УниверсальныйОтчет.ДобавитьОтбор(ОписаниеФильтра, Истина, ВидСравнения.НеВСписке, ЗначениеФильтра,,, Ложь);
		ИначеЕсли Врег(Строка.ТипФильтра) = "В ГРУППЕ ИЗ СПИСКА" Тогда
			ЗначениеФильтра = ПолучитьЗначениеФильтра(НайденныеСтроки, Истина);
			НовыйОтчет.УниверсальныйОтчет.ДобавитьОтбор(ОписаниеФильтра, Истина, ВидСравнения.ВСпискеПоИерархии, ЗначениеФильтра,,, Ложь);
		ИначеЕсли Врег(Строка.ТипФильтра) = "НЕ В ГРУППЕ ИЗ СПИСКА" Тогда
			ЗначениеФильтра = ПолучитьЗначениеФильтра(НайденныеСтроки, Истина);
			НовыйОтчет.УниверсальныйОтчет.ДобавитьОтбор(ОписаниеФильтра, Истина, ВидСравнения.НеВСпискеПоИерархии, ЗначениеФильтра,,, Ложь);
		ИначеЕсли Врег(Строка.ТипФильтра) = "В ГРУППЕ" Тогда
			ЗначениеФильтра = ПолучитьЗначениеФильтра(НайденныеСтроки, Ложь);
			НовыйОтчет.УниверсальныйОтчет.ДобавитьОтбор(ОписаниеФильтра, Истина, ВидСравнения.ВИерархии, ЗначениеФильтра,,, Ложь);
		ИначеЕсли Врег(Строка.ТипФильтра) = "НЕ В ГРУППЕ" Тогда
			ЗначениеФильтра = ПолучитьЗначениеФильтра(НайденныеСтроки, Ложь);
			НовыйОтчет.УниверсальныйОтчет.ДобавитьОтбор(ОписаниеФильтра, Истина, ВидСравнения.НеВИерархии, ЗначениеФильтра,,, Ложь);
		КонецЕсли;

	КонецЦикла;

	// Сбросим флаги
	Для каждого Строка Из НовыйОтчет.УниверсальныйОтчет.Показатели.Строки Цикл
		Строка.Использование = 0;
	КонецЦикла; 
	
	НовыйОтчет.УниверсальныйОтчет.Показатели.Строки.Найти("Количество", "Имя").Использование = 1;

	Если Расшифровка.Свойство("ДатаОтгрузки") Тогда

		СтрОтбор=НовыйОтчет.УниверсальныйОтчет.ПостроительОтчета.Отбор.Добавить("ЗаказПокупателя.ДатаОтгрузки");
		Если Расшифровка.ДатаОтгрузки <> NULL Тогда
			СтрОтбор.Значение      = Расшифровка.ДатаОтгрузки;
		Иначе
			СтрОтбор.Значение      = '00010101'
		КонецЕсли;
		СтрОтбор.ВидСравнения  = ВидСравнения.Равно;
		СтрОтбор.Использование  = Истина;

	КонецЕсли;

	Если Расшифровка.Свойство("ДатаПоставки") Тогда

		СтрОтбор=НовыйОтчет.УниверсальныйОтчет.ПостроительОтчета.Отбор.Добавить("ЗаказПоставщику.ДатаПоступления");
		Если Расшифровка.ДатаПоставки <> NULL Тогда
			СтрОтбор.Значение      = Расшифровка.ДатаПоставки;
		Иначе
			СтрОтбор.Значение      = '00010101'
		КонецЕсли;
		СтрОтбор.ВидСравнения  = ВидСравнения.Равно;
		СтрОтбор.Использование  = Истина;

	КонецЕсли;
	
	СтрокаЯчейки=Элемент.ТекущаяОбласть.Низ;
	
	Возврат НовыйОтчет;
	
КонецФункции // ПодготовитьОтчет()


// Процедура - обработчик события "Обработка расшифровки" поля табличного документа ДокументРезультат
//
Процедура ДокументРезультатОбработкаРасшифровки(Элемент, Расшифровка, СтандартнаяОбработка)

	Перем СтрокаЯчейки;

	Если ТипЗнч(Расшифровка) = Тип("Структура") Тогда

		СтандартнаяОбработка = Ложь;

		ОбщаяРасшифровка = Элемент.Область(1,1).Расшифровка;

		Если Не Расшифровка.Свойство("ИмяРегистра") Тогда // Расшифровка дополнительными группировками 

			СтандартнаяОбработка = Ложь;

			ОбщаяРасшифровка = Элемент.Область(1,1).Расшифровка;

			РасшифровкаСтрока = Расшифровка;

			ИмяОтчета = Метаданные().Имя;

			НовыйОтчет = Отчеты[ИмяОтчета].Создать();

			НовыйОтчет.ДатаОтчета = ДатаОтчета;
			НовыйОтчет.РаскрашиватьИзмерения = РаскрашиватьИзмерения;
			НовыйОтчет.ПоказыватьЗаголовок = ПоказыватьЗаголовок;

			НовыйОтчет.ЗаполнитьНачальныеНастройки();

			Для Каждого Реквизит Из Метаданные.Отчеты[ИмяОтчета].Реквизиты Цикл
				НовыйОтчет[Реквизит.Имя] = ЭтотОбъект[Реквизит.Имя];
			КонецЦикла;

			Для Каждого ТабличнаяЧасть Из Метаданные.Отчеты[ИмяОтчета].ТабличныеЧасти Цикл
				НовыйОтчет[ТабличнаяЧасть.Имя].Загрузить(ЭтотОбъект[ТабличнаяЧасть.Имя].Выгрузить());
			КонецЦикла;

			// В расшифровке текущей строки находятся значения группировок
			Для Каждого ЭлементСтруктурыРасшифровка Из РасшифровкаСтрока Цикл
				НайденнаяСтрокаГруппировки = НовыйОтчет.ГруппировкиОтчета.Найти(ЭлементСтруктурыРасшифровка.Ключ, "ИмяГруппировки");

				НайденнаяСтрока = мДеревоФильтры.Строки.Найти(ЭлементСтруктурыРасшифровка.Ключ, "ИмяПоля", Истина);
				Если НайденнаяСтрока <> Неопределено Тогда

					// Удаляем строку, по которой можно установить отбор
					Если НайденнаяСтрокаГруппировки <> Неопределено Тогда
						НовыйОтчет.ГруппировкиОтчета.Удалить(НайденнаяСтрокаГруппировки);
					КонецЕсли;

					НовыйФильтр = НовыйОтчет.ФильтрыОтчета.Добавить();
					НовыйФильтр.ИмяФильтра 			 = НайденнаяСтрока.ИмяПоля;
					НовыйФильтр.ОписаниеФильтра 	 = НайденнаяСтрока.ОписаниеПоля;
					НовыйФильтр.ПредставлениеФильтра = НайденнаяСтрока.ПредставлениеПоля;
					НовыйФильтр.ЗначениеФильтра 	 = ЭлементСтруктурыРасшифровка.Значение;
					НовыйФильтр.ИспользованиеФильтра = Истина;
					НовыйФильтр.ТипФильтра 			 = "Равно";

					МетаданныеОбъекта = Метаданные.НайтиПоТипу(ТипЗнч(ЭлементСтруктурыРасшифровка.Значение));

					Если МетаданныеОбъекта<>Неопределено Тогда
						Если Метаданные.Справочники.Найти(МетаданныеОбъекта)<>Неопределено 
							ИЛИ Метаданные.ПланыВидовХарактеристик.Найти(МетаданныеОбъекта)<>Неопределено  Тогда

							Если ЭлементСтруктурыРасшифровка.Значение.ЭтоГруппа Тогда
								НовыйФильтр.ТипФильтра 	     = "В списке по иерархии";
							КонецЕсли;
						КонецЕсли;
					КонецЕсли;
				КонецЕсли;
			КонецЦикла;

			// Выбор новой группировки
			Форма = НовыйОтчет.ПолучитьФорму("ФормаВыбора", ЭтаФорма, "дляФормаГруппировки");

			Форма.Заголовок = "Выберите детализирующую группировку";
			Если Форма.Открыта() Тогда
				Форма.Активизировать();
				Если Вопрос("Предыдущая операция выбора группировки не завершена.
					|Завершить?",РежимДиалогаВопрос.ДаНет,30,КодВозвратаДиалога.Да)=КодВозвратаДиалога.Да Тогда
					Форма.Закрыть();
				КонецЕсли;
			КонецЕсли;	

			СтруктураПараметров = Новый Структура;
			СтруктураПараметров.Вставить("МножественныйВыбор", Ложь);
			СтруктураПараметров.Вставить("ИсходнаяТаблица", "СписокГруппировок");

			СтруктураСуществующиеЗначения = Новый Структура;
			СтруктураНеиспользуемыеЗначения = Новый Структура;

			Для Каждого Строка Из ГруппировкиОтчета Цикл

				Если ПустаяСтрока(Строка.ИмяГруппировки) Тогда
					Продолжить;
				КонецЕсли;

				СтруктураНеиспользуемыеЗначения.Вставить(Строка.ИмяГруппировки);

			КонецЦикла;

			СтруктураПараметров.Вставить("СтруктураНеиспользуемыеЗначения", СтруктураНеиспользуемыеЗначения);
			СтруктураПараметров.Вставить("СтруктураСуществующиеЗначения", Новый Структура);

			// Передача параметров в форму
			Форма.НачальноеЗначениеВыбора = СтруктураПараметров;
			Форма.РежимВыбора = Истина;

			ВыбраннаяГруппировка = Форма.ОткрытьМодально();
			Если ВыбраннаяГруппировка = Неопределено Тогда
				Возврат;
			КонецЕсли;

			// Добавление выбранной группировки
			Для Каждого Строка Из ВыбраннаяГруппировка Цикл

				НоваяГруппировка = НовыйОтчет.ГруппировкиОтчета.Добавить();
				НоваяГруппировка.ИмяГруппировки = Строка.ИмяГруппировки;
				НоваяГруппировка.ПредставлениеГруппировки = Строка.ПредставлениеГруппировки;
				НоваяГруппировка.ОписаниеГруппировки = Строка.ОписаниеГруппировки;
				НоваяГруппировка.РассчитыватьИтоги = Истина;

			КонецЦикла;

		Иначе // Расшифровка универсальным отчетом

			Если Расшифровка.ИмяРегистра="ЗаказыПокупателей" Тогда

				НовыйОтчет = ПодготовитьОтчет("ВедомостьЗаказыПокупателей", Расшифровка, Элемент, СтрокаЯчейки);

				НовыйОтчет.УниверсальныйОтчет.ПостроительОтчета.ИзмеренияСтроки.Очистить();
				НовыйОтчет.УниверсальныйОтчет.ПостроительОтчета.ИзмеренияСтроки.Добавить("ДоговорКонтрагента.Владелец");

				Группировки=Элемент.Область(СтрокаЯчейки-1,1).Расшифровка;

				СтрГруппировки=НовыйОтчет.УниверсальныйОтчет.ПостроительОтчета.ИзмеренияСтроки.Добавить("ЗаказПокупателя");

			Иначе

				НовыйОтчет = ПодготовитьОтчет("ВедомостьЗаказыПоставщикам", Расшифровка, Элемент, СтрокаЯчейки);

				НовыйОтчет.УниверсальныйОтчет.ПостроительОтчета.ИзмеренияСтроки.Очистить();
				НовыйОтчет.УниверсальныйОтчет.ПостроительОтчета.ИзмеренияСтроки.Добавить("ДоговорКонтрагента.Владелец");

				Группировки=Элемент.Область(СтрокаЯчейки,1).Расшифровка;

				СтрГруппировки=НовыйОтчет.УниверсальныйОтчет.ПостроительОтчета.ИзмеренияСтроки.Добавить("ЗаказПоставщику");

			КонецЕсли;

			Если ТипЗнч(Группировки) = Тип("Структура") Тогда
				Для Каждого ЭлементСтруктурыРасшифровка Из Группировки Цикл
					НайденнаяСтрока = мДеревоФильтры.Строки.Найти(ЭлементСтруктурыРасшифровка.Ключ, "ИмяПоля", Истина);
					Если НайденнаяСтрока <> Неопределено Тогда
						Если Найти(НайденнаяСтрока.ОписаниеПоля,"#") > 0 Тогда // Из документа
							Если НовыйОтчет.УниверсальныйОтчет.ИмяРегистра = "ЗаказыПокупателей" Тогда
								ОписаниеФильтра = СтрЗаменить(НайденнаяСтрока.ОписаниеПоля,"#","ЗаказПокупателя.");
							Иначе
								ОписаниеФильтра = СтрЗаменить(НайденнаяСтрока.ОписаниеПоля,"#","ЗаказПоставщику.");
							КонецЕсли;
						Иначе
							ОписаниеФильтра = НайденнаяСтрока.ОписаниеПоля;
						КонецЕсли;

						СтрОтбор = НовыйОтчет.УниверсальныйОтчет.ПостроительОтчета.Отбор.Добавить(ОписаниеФильтра);
						СтрОтбор.Значение = ЭлементСтруктурыРасшифровка.Значение;

						СтрОтбор.ВидСравнения = ВидСравнения.Равно;
						СтрОтбор.Использование = Истина;
					КонецЕсли;
				КонецЦикла;
			КонецЕсли;
		КонецЕсли;

	//	НовыйОтчет.УниверсальныйОтчет.мВыбиратьИмяРегистра = Ложь;
		НовыйОтчет.УниверсальныйОтчет.мВосстанавливатьНастройкиПриОткрытии = Ложь;

		НовыйОтчетФорма = НовыйОтчет.УниверсальныйОтчет.ПолучитьФорму();

		НовыйОтчетФорма.ОбновитьОтчет();
		
		НовыйОтчетФорма.Открыть();
		НовыйОтчетФорма.Активизировать();

	КонецЕсли;

КонецПроцедуры // ДокументРезультатОбработкаРасшифровки()