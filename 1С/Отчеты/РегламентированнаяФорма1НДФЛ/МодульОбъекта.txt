Перем СохраненнаяНастройка Экспорт;        // Текущий вариант отчета

Перем ТаблицаВариантовОтчета Экспорт;      // Таблица вариантов доступных текущему пользователю

Перем ДанныеОФизЛицеОрганизации, ТаблицаРазличныхКодовСтандартныхВычетовФизЛиц, ВыборкаДоходыПоМесяцамПоВидам, ВыборкаСтандартныхВычетовФизЛица;
Перем ВыборкаИмущественныеВычеты, ВыборкаНалогиПомесяцамПоСтавкам, ВыборкаСальдоРасчетовНаНачалоГода, ДанныеОПредоставленныхСправках;
Перем ВыборкаВычетыПоМесяцамПоВидам, ДатаНачалаНП, ДатаКонцаНП, ДатаАктуальности, КодПоОКАТО, ДатаПодписи, Ответственный, ПервыйРаз, СводноПоКодамОКАТО;

////////////////////////////////////////////////////////////////////////////////
// ВСПОМОГАТЕЛЬНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

// Возвращает инициализированный массив чисел
Функция НовыйМассивЧисел(Размер)
	Массив = Новый Массив(Размер);
	Для Н =0 По Размер - 1 Цикл
		Массив[Н] = 0;
	КонецЦикла; 
	Возврат Массив;
КонецФункции 

// Преобразует массив чисел в массив чисел по нарастающей
Процедура ПреобразоватьМассивВНарастающий(Массив)
	НарастающееЧисло = 0;
	Для Н = 0 По Массив.Количество() -1 Цикл
		НарастающееЧисло = НарастающееЧисло + Массив[Н];
		Массив[Н] = НарастающееЧисло;
	КонецЦикла; 
КонецПроцедуры
 
// Преобразует значение в число
Функция ПараметрВЧисло(Параметр)
	Если Параметр = Неопределено Тогда
		Возврат 0;
	Иначе	
		Возврат Параметр;
	КонецЕсли; 
КонецФункции
 
////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

Процедура ВывестиГруппировку(ДокументРезультат, СтрокаДанных, Поле, Уровень)
	
	МакетГруппировки  = ПолучитьМакет("МакетГруппировки");
	ОбластьГрупировки = МакетГруппировки.ПолучитьОбласть("Группировка");
	
	ДоступноеПоле = ТиповыеОтчеты.ПолучитьДоступноеПоле(Новый ПолеКомпоновкиДанных(Поле), КомпоновщикНастроек.Настройки.ДоступныеПоляГруппировок);
	
	ОбластьГрупировки.Параметры.НазваниеПарметра = ДоступноеПоле.Заголовок;
	ОбластьГрупировки.Параметры.Значение = СтрокаДанных[СтрЗаменить(Поле, ".", "")];
	
	ДокументРезультат.Вывести(ОбластьГрупировки, Уровень+1);
	
КонецПроцедуры 

Процедура ВывестиКарточки(ДокументРезультат, СтрокиДеревоДанных, Уровень, Группировки, ЕстьОшибки)
	
	//выбрать карточки
	Для каждого СтрокаДерева из СтрокиДеревоДанных Цикл
		Если (Группировки.Количество() - 1) >= Уровень тогда 
			//Нарисовать группировку
			ВывестиГруппировку(ДокументРезультат, СтрокаДерева, Группировки.Получить(Уровень).Значение, Уровень);
			Если Группировки.Количество()-1 >= Уровень+1 тогда
				ДокументРезультат.ВывестиГоризонтальныйРазделительСтраниц();
			КонецЕсли;
			ВывестиКарточки(ДокументРезультат, СтрокаДерева.Строки, Уровень+1, Группировки, ЕстьОшибки);
			
		Иначе			//Нарисовать карточку физлица
			СформироватьКарточку1НДФЛ(ДокументРезультат, СтрокаДерева.ФизЛицо, СтрокаДерева.Год, СтрокаДерева.Организация, СтрокаДерева.ГоловнаяОрганизация, Уровень);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Процедура ПолучитьДанныеДляПостроенияОтчета(СписокФизЛицОрганизация)
	
	ДатаНачалаНП = ТиповыеОтчеты.ПолучитьПараметр(КомпоновщикНастроек, "НачалоПериода").Значение;
	ДатаКонцаНП  = ТиповыеОтчеты.ПолучитьПараметр(КомпоновщикНастроек, "КонецПериода").Значение;
	ДатаПодписи  = ТиповыеОтчеты.ПолучитьПараметр(КомпоновщикНастроек, "ДатаПодписи").Значение;
	Если ДатаПодписи = Неопределено или ДатаПодписи = '00010101' тогда
		ДатаПодписи = ОбщегоНазначения.ПолучитьРабочуюДату();
	КонецЕсли;

	СводноПоКодамОКАТО = ПустаяСтрока(КодПоОКАТО);
	
	// Массив кодов доходов, отражаемых в разделе 7: Доходы от продажи имущества
	МассивКодовДоходовРаздела7 = Новый Массив;
	МассивКодовДоходовРаздела7.Добавить(Справочники.ДоходыНДФЛ.Код1530);
	МассивКодовДоходовРаздела7.Добавить(Справочники.ДоходыНДФЛ.Код2210);
	
	ФизЛицоОтветственного = Ответственный.ФизЛицо;
	
	// Создание запроса и установка всех необходимых параметров
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Ответственный", Ответственный);
	Запрос.УстановитьПараметр("ФизлицоОтветственного", Ответственный.ФизЛицо);
	Запрос.УстановитьПараметр("НачалоНП", ДатаНачалаНП);
	Запрос.УстановитьПараметр("КонецНП", ДатаКонцаНП);
	Запрос.УстановитьПараметр("ПоследнийМесяцНП", НачалоМесяца(ДатаКонцаНП));
	Запрос.УстановитьПараметр("ДатаАктуальности", ДатаПодписи);
	Запрос.УстановитьПараметр("МассивКодовДоходовРаздела7", МассивКодовДоходовРаздела7);
	Запрос.УстановитьПараметр("ПустойКодПоОКАТО","");
	Запрос.УстановитьПараметр("ПустойКПП","");
	Запрос.УстановитьПараметр("СписокФизЛицОрганизация", СписокФизЛицОрганизация);
	Запрос.УстановитьПараметр("КодПоОКАТО",КодПоОКАТО);
	
	// Сформируем текст запроса с датами месяцев налогового периода
	// первый месяц
	КонецМесяца = КонецМесяца(ДатаНачалаНП);
	ПериодыТекст = "ВЫБРАТЬ ДАТАВРЕМЯ(" + Формат (КонецМесяца,"ДФ=гггг,М,д,Ч,м,с") + ") КАК Период";
	ПериодыТекст = ПериодыТекст + "
	|ПОМЕСТИТЬ ВТПериоды";
	// прибавим остальные месяцы
	Для Сч = 2 По 12 Цикл
		КонецМесяца = КонецМесяца(КонецМесяца+1);
    	ПериодыТекст = ПериодыТекст +" ОБЪЕДИНИТЬ ВСЕ ВЫБРАТЬ ДАТАВРЕМЯ(" + Формат(КонецМесяца,"ДФ=гггг,М,д,Ч,м,с") + ")";
	КонецЦикла;
	ПериодыТекст = ПериодыТекст + "
	|ИНДЕКСИРОВАТЬ ПО Период";
	
	Запрос.Текст = ПериодыТекст;
	Запрос.Выполнить();
	
	//-----------------------------------------------------------------------------
	// ВЫБОРКА СВЕДЕНИЙ ОБ ОРГАНИЗАЦИИ И ФИЗЛИЦЕ (Данные разделов 1 и 2)
	// 	
	
	//ДанныеОбОрганизацииИФизлицеТекст
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ФизЛицаОганизация.ГоловнаяОрганизация КАК ГоловнаяОрганизация,
	|	ФизЛицаОганизация.Организация,
	|	ФизЛицаОганизация.ФизЛицо КАК ФизЛицо,
	|	ФизЛицаОганизация.Год КАК Год
	|ПОМЕСТИТЬ ВТСписокФизЛицОрганизация
	|ИЗ
	|	&СписокФизЛицОрганизация КАК ФизЛицаОганизация
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ФизЛицо,
	|	ГоловнаяОрганизация,
	|	Год";
	Запрос.Выполнить();
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ГражданствоФизЛицаСрезПоследних.Год КАК Год,
	|	ГражданствоФизЛицаСрезПоследних.Физлицо КАК ФизЛицо,
	|	ГражданствоФизЛица.Страна
	|ПОМЕСТИТЬ ВТГражданствоФизЛиц
	|ИЗ
	|	(ВЫБРАТЬ
	|		Периоды.Год КАК Год,
	|		Периоды.Физлицо КАК Физлицо,
	|		МАКСИМУМ(ГражданствоФизЛица.Период) КАК ПериодСреза
	|	ИЗ
	|		(ВЫБРАТЬ
	|			МАКСИМУМ(Периоды.Период) КАК КонецНалоговогоПериода,
	|			СписокФизЛицОрганизация.Год КАК Год,
	|			СписокФизЛицОрганизация.ФизЛицо КАК Физлицо
	|		ИЗ
	|			ВТПериоды КАК Периоды
	|				ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТСписокФизЛицОрганизация КАК СписокФизЛицОрганизация
	|				ПО (ГОД(Периоды.Период) = СписокФизЛицОрганизация.Год)
	|		
	|		СГРУППИРОВАТЬ ПО
	|			СписокФизЛицОрганизация.Год,
	|			СписокФизЛицОрганизация.ФизЛицо) КАК Периоды
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ГражданствоФизЛиц КАК ГражданствоФизЛица
	|			ПО Периоды.КонецНалоговогоПериода >= ГражданствоФизЛица.Период
	|				И Периоды.Физлицо = ГражданствоФизЛица.ФизЛицо
	|	
	|	СГРУППИРОВАТЬ ПО
	|		Периоды.Физлицо,
	|		Периоды.Год) КАК ГражданствоФизЛицаСрезПоследних
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ГражданствоФизЛиц КАК ГражданствоФизЛица
	|		ПО ГражданствоФизЛицаСрезПоследних.ПериодСреза = ГражданствоФизЛица.Период
	|			И ГражданствоФизЛицаСрезПоследних.Физлицо = ГражданствоФизЛица.ФизЛицо
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ФизЛицо,
	|	Год";
	Запрос.Выполнить();
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СтатусФизЛицаСрезПоследних.Год КАК Год,
	|	СтатусФизЛицаСрезПоследних.Физлицо КАК ФизЛицо,
	|	ВЫБОР
	|		КОГДА СтатусФизЛица.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыНалогоплательщиковПоНДФЛ.НеРезидент)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК НеЯвляетсяНалоговымРезидентомРФ
	|ПОМЕСТИТЬ ВТСтатусФизЛиц
	|ИЗ
	|	(ВЫБРАТЬ
	|		Периоды.Год КАК Год,
	|		Периоды.Физлицо КАК Физлицо,
	|		МАКСИМУМ(СтатусФизЛица.Период) КАК ПериодСреза
	|	ИЗ
	|		(ВЫБРАТЬ
	|			МАКСИМУМ(Периоды.Период) КАК КонецНалоговогоПериода,
	|			СписокФизЛицОрганизация.Год КАК Год,
	|			СписокФизЛицОрганизация.ФизЛицо КАК Физлицо
	|		ИЗ
	|			ВТПериоды КАК Периоды
	|				ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТСписокФизЛицОрганизация КАК СписокФизЛицОрганизация
	|				ПО (ГОД(Периоды.Период) = СписокФизЛицОрганизация.Год)
	|		
	|		СГРУППИРОВАТЬ ПО
	|			СписокФизЛицОрганизация.Год,
	|			СписокФизЛицОрганизация.ФизЛицо) КАК Периоды
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтатусФизЛицКакНалогоплательщиковНДФЛ КАК СтатусФизЛица
	|			ПО Периоды.КонецНалоговогоПериода >= СтатусФизЛица.Период
	|				И Периоды.Физлицо = СтатусФизЛица.ФизЛицо
	|	
	|	СГРУППИРОВАТЬ ПО
	|		Периоды.Физлицо,
	|		Периоды.Год) КАК СтатусФизЛицаСрезПоследних
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтатусФизЛицКакНалогоплательщиковНДФЛ КАК СтатусФизЛица
	|		ПО СтатусФизЛицаСрезПоследних.ПериодСреза = СтатусФизЛица.Период
	|			И СтатусФизЛицаСрезПоследних.Физлицо = СтатусФизЛица.ФизЛицо
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ФизЛицо,
	|	Год";
	Запрос.Выполнить();
	
	ДанныеОбОрганизацииИФизлицеТекст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	СписокФизЛицОрганизация.ФизЛицо КАК ФизЛицо,
	|	СписокФизЛицОрганизация.ГоловнаяОрганизация КАК ГоловнаяОрганизация,
	|	СписокФизЛицОрганизация.Год КАК Год,
	|	СписокФизЛицОрганизация.Организация.ИНН КАК Организация_ИНН,
	|	СписокФизЛицОрганизация.Организация.КПП КАК Организация_КПП,
	|	СписокФизЛицОрганизация.Организация.КодИМНС КАК Организация_КодИМНС,
	|	СписокФизЛицОрганизация.Организация.КодПоОКАТО КАК Организация_КодПоОКАТО,
	|	ВЫБОР
	|		КОГДА СписокФизЛицОрганизация.Организация.ЮрФизЛицо = ЗНАЧЕНИЕ(Перечисление.ЮрФизЛицо.ЮрЛицо)
	|			ТОГДА СписокФизЛицОрганизация.Организация.НаименованиеПолное
	|		ИНАЧЕ ФИОПредпринимателя.Фамилия + "" "" + ФИОПредпринимателя.Имя + "" "" + ФИОПредпринимателя.Отчество
	|	КОНЕЦ КАК Организация_Наименование,
	|	ДанныеОФизЛице.ИНН КАК ФизЛицо_ИНН,
	|	ДанныеОФизЛице.СтраховойНомерПФР КАК ФизЛицо_СтраховойНомерПФР,
	|	ЕСТЬNULL(ФИОФизЛица.Фамилия + "" "" + ФИОФизЛица.Имя + "" "" + ФИОФизЛица.Отчество, ДанныеОФизЛице.Наименование) КАК ФизЛицо_ФИО,
	|	ЕСТЬNULL(ФИОФизлицаОтветственного.Фамилия + "" "" + ФИОФизлицаОтветственного.Имя + "" "" + ФИОФизлицаОтветственного.Отчество, ЕСТЬNULL(ДанныеОбОтветственном.ФизЛицо.Наименование, ДанныеОбОтветственном.Наименование)) КАК Ответственный_ФИО,
	|	ВЫБОР
	|		КОГДА ДолжностьОтветственного.ПериодЗавершения <= &ДатаАктуальности
	|				И ДолжностьОтветственного.ПериодЗавершения <> ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|			ТОГДА ДолжностьОтветственного.ДолжностьЗавершения.Наименование
	|		ИНАЧЕ ДолжностьОтветственного.Должность.Наименование
	|	КОНЕЦ КАК Ответственный_Должность,
	|	ПаспортныеДанныеФизЛица.ДокументВид.КодИМНС КАК ФизЛицо_КодДокументаУдЛичн,
	|	ПаспортныеДанныеФизЛица.ДокументСерия + "" "" + ПаспортныеДанныеФизЛица.ДокументНомер КАК ФизЛицо_СерияНомерДокументаУдЛичн,
	|	ДанныеОФизЛице.ДатаРождения КАК ФизЛицо_ДатаРождения,
	|	(НЕ ЕСТЬNULL(СтатусФизЛица.НеЯвляетсяНалоговымРезидентомРФ, ЛОЖЬ)) КАК ФизЛицо_Резидент,
	|	ЕСТЬNULL(ГражданствоФизЛица.Страна.Код, ""643"") КАК ФизЛицо_КодСтраны,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(СтатусФизЛица.НеЯвляетсяНалоговымРезидентомРФ, ЛОЖЬ)
	|			ТОГДА Страны.Код
	|		ИНАЧЕ """"
	|	КОНЕЦ КАК ФизЛицо_АдресКодСтраны,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(СтатусФизЛица.НеЯвляетсяНалоговымРезидентомРФ, ЛОЖЬ)
	|			ТОГДА """"
	|		ИНАЧЕ АдресРегистрацииФизЛица.Поле1
	|	КОНЕЦ КАК ФизЛицо_АдресИндекс,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(СтатусФизЛица.НеЯвляетсяНалоговымРезидентомРФ, ЛОЖЬ)
	|			ТОГДА """"
	|		ИНАЧЕ РегионРегистрацииФизЛица.Код
	|	КОНЕЦ КАК ФизЛицо_АдресРегион,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(СтатусФизЛица.НеЯвляетсяНалоговымРезидентомРФ, ЛОЖЬ)
	|			ТОГДА """"
	|		ИНАЧЕ АдресРегистрацииФизЛица.Поле3
	|	КОНЕЦ КАК ФизЛицо_АдресРайон,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(СтатусФизЛица.НеЯвляетсяНалоговымРезидентомРФ, ЛОЖЬ)
	|			ТОГДА АдресаИностранцев.Представление
	|		ИНАЧЕ АдресРегистрацииФизЛица.Поле4
	|	КОНЕЦ КАК ФизЛицо_АдресГород,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(СтатусФизЛица.НеЯвляетсяНалоговымРезидентомРФ, ЛОЖЬ)
	|			ТОГДА """"
	|		ИНАЧЕ АдресРегистрацииФизЛица.Поле5
	|	КОНЕЦ КАК ФизЛицо_АдресНаселенныйПункт,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(СтатусФизЛица.НеЯвляетсяНалоговымРезидентомРФ, ЛОЖЬ)
	|			ТОГДА """"
	|		ИНАЧЕ АдресРегистрацииФизЛица.Поле6
	|	КОНЕЦ КАК ФизЛицо_АдресУлица,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(СтатусФизЛица.НеЯвляетсяНалоговымРезидентомРФ, ЛОЖЬ)
	|			ТОГДА """"
	|		ИНАЧЕ АдресРегистрацииФизЛица.Поле7
	|	КОНЕЦ КАК ФизЛицо_АдресДом,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(СтатусФизЛица.НеЯвляетсяНалоговымРезидентомРФ, ЛОЖЬ)
	|			ТОГДА """"
	|		ИНАЧЕ АдресРегистрацииФизЛица.Поле8
	|	КОНЕЦ КАК ФизЛицо_АдресКорпус,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(СтатусФизЛица.НеЯвляетсяНалоговымРезидентомРФ, ЛОЖЬ)
	|			ТОГДА """"
	|		ИНАЧЕ АдресРегистрацииФизЛица.Поле9
	|	КОНЕЦ КАК ФизЛицо_АдресКвартира,
	|	НДФЛДоходыПредыдущегоМестаРаботы.Размер КАК ФизЛицо_ДоходыПредыдущегоМестаРаботы
	|ИЗ
	|	ВТСписокФизЛицОрганизация КАК СписокФизЛицОрганизация
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ФИОФизЛиц.СрезПоследних(
	|				&ДатаАктуальности,
	|				ФизЛицо В
	|					(ВЫБРАТЬ
	|						ФизЛица.Организация.ИндивидуальныйПредприниматель
	|					ИЗ
	|						ВТСписокФизЛицОрганизация КАК ФизЛица)) КАК ФИОПредпринимателя
	|		ПО (ФИОПредпринимателя.ФизЛицо = СписокФизЛицОрганизация.Организация.ИндивидуальныйПредприниматель)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ФизическиеЛица КАК ДанныеОФизЛице
	|		ПО (ДанныеОФизЛице.Ссылка = СписокФизЛицОрганизация.ФизЛицо)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Пользователи КАК ДанныеОбОтветственном
	|		ПО (ДанныеОбОтветственном.Ссылка = &Ответственный)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ФИОФизЛиц.СрезПоследних(
	|				&ДатаАктуальности,
	|				ФизЛицо В
	|					(ВЫБРАТЬ
	|						ФизЛица.ФизЛицо
	|					ИЗ
	|						ВТСписокФизЛицОрганизация КАК ФизЛица)) КАК ФИОФизЛица
	|		ПО (ФИОФизЛица.ФизЛицо = СписокФизЛицОрганизация.ФизЛицо)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ФИОФизЛиц.СрезПоследних(&ДатаАктуальности, ФизЛицо = &ФизЛицоОтветственного) КАК ФИОФизлицаОтветственного
	|		ПО (ИСТИНА)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПаспортныеДанныеФизЛиц.СрезПоследних(
	|				&ДатаАктуальности,
	|				ФизЛицо В
	|					(ВЫБРАТЬ
	|						ФизЛица.ФизЛицо
	|					ИЗ
	|						ВТСписокФизЛицОрганизация КАК ФизЛица)) КАК ПаспортныеДанныеФизЛица
	|		ПО (ПаспортныеДанныеФизЛица.ФизЛицо = СписокФизЛицОрганизация.ФизЛицо)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТГражданствоФизЛиц КАК ГражданствоФизЛица
	|		ПО СписокФизЛицОрганизация.ФизЛицо = ГражданствоФизЛица.ФизЛицо
	|			И СписокФизЛицОрганизация.Год = ГражданствоФизЛица.Год
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСтатусФизЛиц КАК СтатусФизЛица
	|		ПО СписокФизЛицОрганизация.ФизЛицо = СтатусФизЛица.ФизЛицо
	|			И СписокФизЛицОрганизация.Год = СтатусФизЛица.Год
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КонтактнаяИнформация КАК АдресРегистрацииФизЛица
	|		ПО (ДанныеОФизЛице.Ссылка = АдресРегистрацииФизЛица.Объект)
	|			И (АдресРегистрацииФизЛица.Вид = ЗНАЧЕНИЕ(Справочник.ВидыКонтактнойИнформации.ЮрАдресФизЛица))
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АдресныйКлассификатор КАК РегионРегистрацииФизЛица
	|		ПО (РегионРегистрацииФизЛица.ТипАдресногоЭлемента = 1)
	|			И (РегионРегистрацииФизЛица.Наименование + "" "" + РегионРегистрацииФизЛица.Сокращение = АдресРегистрацииФизЛица.Поле2)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КонтактнаяИнформация КАК АдресаИностранцев
	|		ПО (ДанныеОФизЛице.Ссылка = АдресаИностранцев.Объект)
	|			И (АдресаИностранцев.Вид = ЗНАЧЕНИЕ(Справочник.ВидыКонтактнойИнформации.ИнострАдресФизЛица))
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КлассификаторСтранМира КАК Страны
	|		ПО (Страны.Наименование = АдресаИностранцев.Поле1)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НДФЛДоходыПредыдущегоМестаРаботы КАК НДФЛДоходыПредыдущегоМестаРаботы
	|		ПО СписокФизЛицОрганизация.ФизЛицо = НДФЛДоходыПредыдущегоМестаРаботы.ФизЛицо
	|			И СписокФизЛицОрганизация.ГоловнаяОрганизация = НДФЛДоходыПредыдущегоМестаРаботы.Организация
	|			И (СписокФизЛицОрганизация.Год = ГОД(НДФЛДоходыПредыдущегоМестаРаботы.МесяцНалоговогоПериода))
	|			И (МЕСЯЦ(НДФЛДоходыПредыдущегоМестаРаботы.МесяцНалоговогоПериода) = 12)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РаботникиОрганизаций.СрезПоследних(
	|				&ДатаАктуальности,
	|				Сотрудник.ФизЛицо = &ФизЛицоОтветственного
	|					И Сотрудник.ВидЗанятости <> ЗНАЧЕНИЕ(Перечисление.ВидыЗанятостиВОрганизации.ВнутреннееСовместительство)) КАК ДолжностьОтветственного
	|		ПО (ВЫБОР
	|				КОГДА ДолжностьОтветственного.ПериодЗавершения <= &ДатаАктуальности
	|						И ДолжностьОтветственного.ПериодЗавершения <> ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|					ТОГДА ДолжностьОтветственного.ПричинаИзмененияСостоянияЗавершения
	|				ИНАЧЕ ДолжностьОтветственного.ПричинаИзмененияСостояния
	|			КОНЕЦ <> ЗНАЧЕНИЕ(Перечисление.ПричиныИзмененияСостояния.Увольнение))
	|			И (ДолжностьОтветственного.Организация = СписокФизЛицОрганизация.ГоловнаяОрганизация)
	|
	|УПОРЯДОЧИТЬ ПО
	|	ГоловнаяОрганизация,
	|	ФизЛицо,
	|	Год";
	
	// Поля:
	//	КодВычета  - код стандартного вычета (ссылка)
	//
	// Описание: выбирает все различные коды вычетов, которые действовали в течение года по физлицу в регистре "НДФЛСтандартныеВычетыФизлиц" 
	
	СписокРазличныхКодовСтандартныхВычетовФизЛицаЗаГодТекст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВычетыВОрганизации.ГоловнаяОрганизация,
	|	ПраваНаРазличныеВычеты.КодВычета,
	|	ВычетыВОрганизации.Физлицо,
	|	ВычетыВОрганизации.Год
	|ИЗ
	|	(ВЫБРАТЬ
	|		НДФЛПрименениеВычетов.Организация КАК ГоловнаяОрганизация,
	|		НДФЛПрименениеВычетов.Физлицо КАК Физлицо,
	|		ГОД(НДФЛПрименениеВычетов.Период) КАК Год
	|	ИЗ
	|		РегистрСведений.НДФЛПрименениеВычетов КАК НДФЛПрименениеВычетов
	|	ГДЕ
	|		НДФЛПрименениеВычетов.Период МЕЖДУ &НачалоНП И &КонецНП
	|		И НДФЛПрименениеВычетов.Физлицо В
	|				(ВЫБРАТЬ
	|					ФизЛица.ФизЛицо
	|				ИЗ
	|					ВТСписокФизЛицОрганизация КАК ФизЛица)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		НДФЛПрименениеВычетовСрезПоследних.Организация,
	|		НДФЛПрименениеВычетовСрезПоследних.Физлицо,
	|		ГОД(&НачалоНП)
	|	ИЗ
	|		РегистрСведений.НДФЛПрименениеВычетов.СрезПоследних(
	|				&НачалоНП,
	|				ФизЛицо В
	|					(ВЫБРАТЬ
	|						ФизЛица.ФизЛицо
	|					ИЗ
	|						ВТСписокФизЛицОрганизация КАК ФизЛица)) КАК НДФЛПрименениеВычетовСрезПоследних) КАК ВычетыВОрганизации
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|			НДФЛСтандартныеВычетыФизлицСрезПоследних.КодВычетаЛичный КАК КодВычета,
	|			НДФЛСтандартныеВычетыФизлицСрезПоследних.Физлицо КАК ФизЛицо,
	|			ГОД(&НачалоНП) КАК Год
	|		ИЗ
	|			РегистрСведений.НДФЛСтандартныеВычетыФизлиц.СрезПоследних(
	|					&НачалоНП,
	|					ФизЛицо В
	|						(ВЫБРАТЬ
	|							ФизЛица.ФизЛицо
	|						ИЗ
	|							ВТСписокФизЛицОрганизация КАК ФизЛица)) КАК НДФЛСтандартныеВычетыФизлицСрезПоследних
	|		ГДЕ
	|			НДФЛСтандартныеВычетыФизлицСрезПоследних.КодВычетаЛичный <> ЗНАЧЕНИЕ(Справочник.ВычетыНДФЛ.ПустаяСсылка)
	|		
	|		ОБЪЕДИНИТЬ ВСЕ
	|		
	|		ВЫБРАТЬ
	|			НДФЛСтандартныеВычетыФизлиц.КодВычетаЛичный,
	|			НДФЛСтандартныеВычетыФизлиц.Физлицо,
	|			ГОД(НДФЛСтандартныеВычетыФизлиц.Период)
	|		ИЗ
	|			РегистрСведений.НДФЛСтандартныеВычетыФизлиц КАК НДФЛСтандартныеВычетыФизлиц
	|		ГДЕ
	|			НДФЛСтандартныеВычетыФизлиц.Период МЕЖДУ &НачалоНП И &КонецНП
	|			И НДФЛСтандартныеВычетыФизлиц.КодВычетаЛичный <> ЗНАЧЕНИЕ(Справочник.ВычетыНДФЛ.ПустаяСсылка)
	|			И НДФЛСтандартныеВычетыФизлиц.Физлицо В
	|					(ВЫБРАТЬ
	|						ФизЛица.ФизЛицо
	|					ИЗ
	|						ВТСписокФизЛицОрганизация КАК ФизЛица)
	|		
	|		ОБЪЕДИНИТЬ ВСЕ
	|		
	|		ВЫБРАТЬ
	|			НДФЛСтандартныеВычетыФизлицСрезПоследних.КодВычета,
	|			НДФЛСтандартныеВычетыФизлицСрезПоследних.Физлицо,
	|			ГОД(&НачалоНП)
	|		ИЗ
	|			РегистрСведений.НДФЛСтандартныеВычетыНаДетей.СрезПоследних(
	|					&НачалоНП,
	|					ФизЛицо В
	|						(ВЫБРАТЬ
	|							ФизЛица.ФизЛицо
	|						ИЗ
	|							ВТСписокФизЛицОрганизация КАК ФизЛица)) КАК НДФЛСтандартныеВычетыФизлицСрезПоследних
	|		ГДЕ
	|			ВЫБОР
	|					КОГДА НДФЛСтандартныеВычетыФизлицСрезПоследних.ПериодЗавершения <= &НачалоНП
	|							И НДФЛСтандартныеВычетыФизлицСрезПоследних.ПериодЗавершения <> ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|						ТОГДА НДФЛСтандартныеВычетыФизлицСрезПоследних.КоличествоДетейЗавершения
	|					ИНАЧЕ НДФЛСтандартныеВычетыФизлицСрезПоследних.КоличествоДетей
	|				КОНЕЦ <> 0
	|		
	|		ОБЪЕДИНИТЬ ВСЕ
	|		
	|		ВЫБРАТЬ
	|			НДФЛСтандартныеВычетыФизлиц.КодВычета,
	|			НДФЛСтандартныеВычетыФизлиц.Физлицо,
	|			ГОД(НДФЛСтандартныеВычетыФизлиц.Период)
	|		ИЗ
	|			РегистрСведений.НДФЛСтандартныеВычетыНаДетей КАК НДФЛСтандартныеВычетыФизлиц
	|		ГДЕ
	|			НДФЛСтандартныеВычетыФизлиц.Период МЕЖДУ &НачалоНП И &КонецНП
	|			И НДФЛСтандартныеВычетыФизлиц.Физлицо В
	|					(ВЫБРАТЬ
	|						ФизЛица.ФизЛицо
	|					ИЗ
	|						ВТСписокФизЛицОрганизация КАК ФизЛица)
	|			И НДФЛСтандартныеВычетыФизлиц.КоличествоДетей <> 0) КАК ПраваНаРазличныеВычеты
	|		ПО ВычетыВОрганизации.Физлицо = ПраваНаРазличныеВычеты.ФизЛицо
	|			И ВычетыВОрганизации.Год = ПраваНаРазличныеВычеты.Год
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТСписокФизЛицОрганизация КАК СписокФизЛицОрганизация
	|		ПО ВычетыВОрганизации.Физлицо = СписокФизЛицОрганизация.ФизЛицо
	|			И ВычетыВОрганизации.Год = СписокФизЛицОрганизация.Год
	|			И ВычетыВОрганизации.ГоловнаяОрганизация = СписокФизЛицОрганизация.ГоловнаяОрганизация";									   

	//НомераИДатыПредоставленныхСправокОДоходахТекст
	//
	// Описание: выбирает номера и даты справок о доходах для 9-го раздела 
	
	НомераИДатыПредоставленныхСправокОДоходахТекст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ЕСТЬNULL(МАКСИМУМ(НесгруппированныеДанные.ДатаСправкиВИМНС), """") КАК ДатаСправкиВИМНС,
	|	ЕСТЬNULL(МАКСИМУМ(НесгруппированныеДанные.НомерСправкиВИМНС), """") КАК НомерСправкиВИМНС,
	|	ЕСТЬNULL(МАКСИМУМ(НесгруппированныеДанные.ВнешнийНомерСправкиВИМНС), """") КАК ВнешнийНомерСправкиВИМНС,
	|	ЕСТЬNULL(МАКСИМУМ(НесгруппированныеДанные.ДатаСправкиДляФизЛица), """") КАК ДатаСправкиДляФизЛица,
	|	ЕСТЬNULL(МАКСИМУМ(НесгруппированныеДанные.НомерСправкиДляФизЛица), """") КАК НомерСправкиДляФизЛица,
	|	ЕСТЬNULL(МАКСИМУМ(НесгруппированныеДанные.ВнешнийНомерСправкиДляФизЛица), """") КАК ВнешнийНомерСправкиДляФизЛица,
	|	ЕСТЬNULL(МАКСИМУМ(НесгруппированныеДанные.ДатаСправкиНаВзыскание), """") КАК ДатаСправкиНаВзыскание,
	|	ЕСТЬNULL(МАКСИМУМ(НесгруппированныеДанные.НомерСправкиНаВзыскание), """") КАК НомерСправкиНаВзыскание,
	|	ЕСТЬNULL(МАКСИМУМ(НесгруппированныеДанные.ВнешнийНомерСправкиНаВзыскание), """") КАК ВнешнийНомерСправкиНаВзыскание,
	|	НесгруппированныеДанные.ФизЛицо КАК ФизЛицо,
	|	НесгруппированныеДанные.ГоловнаяОрганизация КАК ГоловнаяОрганизация,
	|	НесгруппированныеДанные.ГОД КАК ГОД
	|ИЗ
	|	(ВЫБРАТЬ
	|		СведенияОДоходахФизлицРаботники.Ссылка.Организация КАК ГоловнаяОрганизация,
	|		СведенияОДоходахФизлицРаботники.ФизЛицо КАК ФизЛицо,
	|		СведенияОДоходахФизлицРаботники.Ссылка.НалоговыйПериод КАК ГОД,
	|		СведенияОДоходахФизлицРаботники.Ссылка.Дата КАК ДатаСправкиВИМНС,
	|		СведенияОДоходахФизлицРаботники.Ссылка.Номер КАК НомерСправкиВИМНС,
	|		СведенияОДоходахФизлицРаботники.НомерСправки КАК ВнешнийНомерСправкиВИМНС,
	|		NULL КАК ДатаСправкиДляФизЛица,
	|		NULL КАК НомерСправкиДляФизЛица,
	|		NULL КАК ВнешнийНомерСправкиДляФизЛица,
	|		NULL КАК ДатаСправкиНаВзыскание,
	|		NULL КАК НомерСправкиНаВзыскание,
	|		NULL КАК ВнешнийНомерСправкиНаВзыскание
	|	ИЗ
	|		Документ.СведенияОДоходахФизлиц.РаботникиОрганизации КАК СведенияОДоходахФизлицРаботники
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТСписокФизЛицОрганизация КАК СписокФизЛицОрганизация
	|			ПО СведенияОДоходахФизлицРаботники.ФизЛицо = СписокФизЛицОрганизация.ФизЛицо
	|				И СведенияОДоходахФизлицРаботники.Ссылка.НалоговыйПериод = СписокФизЛицОрганизация.Год
	|				И СведенияОДоходахФизлицРаботники.Ссылка.Организация = СписокФизЛицОрганизация.ГоловнаяОрганизация
	|	ГДЕ
	|		СведенияОДоходахФизлицРаботники.Ссылка.Проведен
	|		И СведенияОДоходахФизлицРаботники.Ссылка.НазначениеСправки = ЗНАЧЕНИЕ(Перечисление.ПорядокФормированияСправкиОДоходахФизлица.ДляПередачиВИФНС)
	|	
	|	ОБЪЕДИНИТЬ
	|	
	|	ВЫБРАТЬ
	|		СведенияОДоходахФизлицРаботники.Ссылка.Организация,
	|		СведенияОДоходахФизлицРаботники.ФизЛицо,
	|		СведенияОДоходахФизлицРаботники.Ссылка.НалоговыйПериод,
	|		NULL,
	|		NULL,
	|		NULL,
	|		СведенияОДоходахФизлицРаботники.Ссылка.Дата,
	|		СведенияОДоходахФизлицРаботники.Ссылка.Номер,
	|		СведенияОДоходахФизлицРаботники.НомерСправки,
	|		NULL,
	|		NULL,
	|		NULL
	|	ИЗ
	|		Документ.СведенияОДоходахФизлиц.РаботникиОрганизации КАК СведенияОДоходахФизлицРаботники
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТСписокФизЛицОрганизация КАК СписокФизЛицОрганизация
	|			ПО СведенияОДоходахФизлицРаботники.ФизЛицо = СписокФизЛицОрганизация.ФизЛицо
	|				И СведенияОДоходахФизлицРаботники.Ссылка.НалоговыйПериод = СписокФизЛицОрганизация.Год
	|				И СведенияОДоходахФизлицРаботники.Ссылка.Организация = СписокФизЛицОрганизация.ГоловнаяОрганизация
	|	ГДЕ
	|		СведенияОДоходахФизлицРаботники.Ссылка.Проведен
	|		И (СведенияОДоходахФизлицРаботники.Ссылка.НазначениеСправки = ЗНАЧЕНИЕ(Перечисление.ПорядокФормированияСправкиОДоходахФизлица.ФизлицуВРазрезеКодовОКАТО)
	|				ИЛИ СведенияОДоходахФизлицРаботники.Ссылка.НазначениеСправки = ЗНАЧЕНИЕ(Перечисление.ПорядокФормированияСправкиОДоходахФизлица.ФизлицуСводно))
	|	
	|	ОБЪЕДИНИТЬ
	|	
	|	ВЫБРАТЬ
	|		СведенияОДоходахФизлицРаботники.Ссылка.Организация,
	|		СведенияОДоходахФизлицРаботники.ФизЛицо,
	|		СведенияОДоходахФизлицРаботники.Ссылка.НалоговыйПериод,
	|		NULL,
	|		NULL,
	|		NULL,
	|		NULL,
	|		NULL,
	|		NULL,
	|		СведенияОДоходахФизлицРаботники.Ссылка.Дата,
	|		СведенияОДоходахФизлицРаботники.Ссылка.Номер,
	|		СведенияОДоходахФизлицРаботники.НомерСправки
	|	ИЗ
	|		Документ.СведенияОДоходахФизлиц.РаботникиОрганизации КАК СведенияОДоходахФизлицРаботники
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТСписокФизЛицОрганизация КАК СписокФизЛицОрганизация
	|			ПО СведенияОДоходахФизлицРаботники.ФизЛицо = СписокФизЛицОрганизация.ФизЛицо
	|				И СведенияОДоходахФизлицРаботники.Ссылка.НалоговыйПериод = СписокФизЛицОрганизация.Год
	|				И СведенияОДоходахФизлицРаботники.Ссылка.Организация = СписокФизЛицОрганизация.ГоловнаяОрганизация
	|	ГДЕ
	|		СведенияОДоходахФизлицРаботники.Ссылка.Проведен
	|		И СведенияОДоходахФизлицРаботники.Ссылка.НазначениеСправки = ЗНАЧЕНИЕ(Перечисление.ПорядокФормированияСправкиОДоходахФизлица.ДляПередачиВИФНС)
	|		И СведенияОДоходахФизлицРаботники.ПереданоНаВзыскание > 0) КАК НесгруппированныеДанные
	|
	|СГРУППИРОВАТЬ ПО
	|	НесгруппированныеДанные.ФизЛицо,
	|	НесгруппированныеДанные.ГоловнаяОрганизация,
	|	НесгруппированныеДанные.ГОД
	|ИТОГИ
	|	МАКСИМУМ(ДатаСправкиВИМНС),
	|	МАКСИМУМ(НомерСправкиВИМНС),
	|	МАКСИМУМ(ВнешнийНомерСправкиВИМНС),
	|	МАКСИМУМ(ДатаСправкиДляФизЛица),
	|	МАКСИМУМ(НомерСправкиДляФизЛица),
	|	МАКСИМУМ(ВнешнийНомерСправкиДляФизЛица),
	|	МАКСИМУМ(ДатаСправкиНаВзыскание),
	|	МАКСИМУМ(НомерСправкиНаВзыскание),
	|	МАКСИМУМ(ВнешнийНомерСправкиНаВзыскание)
	|ПО
	|	ГоловнаяОрганизация,
	|	ФизЛицо,
	|	ГОД";
	
	
	//-----------------------------------------------------------------------------
	// ВЫБОРКА ДОХОДОВ ПО ВИДАМ И ПО МЕСЯЦАМ НАЛОГОВОГО ПЕРИОДА ДЛЯ ВСЕХ РАЗДЕЛОВ КАРТОЧКИ
	// Выбираются все доходы по месяцам налогового периода в разрезе кодов дохода
	// !!! В свойстве "период" регистра "НДФЛСведенияОДоходах" хранится "МесяцНалоговогоПериода"
	
	//Описание поля Раздел налоговой карточки: 
	//	7 раздел - налог с доходов седьмого раздела
	//	3 раздел - налог с доходов по ставке 13% и со всех доходов нерезидента,
	//	4 раздел - налог с доходов по ставке 9%
	//	5 раздел - налог с доходов по ставке 30%
	
	
	Если СводноПоКодамОКАТО Тогда
		
		ДоходыПоМесяцамПоВидамТекст = 
		"ВЫБРАТЬ
		|	НДФЛСведенияОДоходах.Организация КАК ГоловнаяОрганизация,
		|	НДФЛСведенияОДоходах.ФизЛицо КАК ФизЛицо,
		|	ГОД(НДФЛСведенияОДоходах.Период) КАК Год,
		|	ВЫБОР
		|		КОГДА ГОД(НДФЛСведенияОДоходах.Период) > 2007
		|				И НДФЛСведенияОДоходах.КодДохода = ЗНАЧЕНИЕ(Справочник.ДоходыНДФЛ.Код1010)
		|			ТОГДА ""Раздел4""
		|		КОГДА НДФЛСведенияОДоходах.КодДохода.СтавкаНалогообложенияРезидента = ЗНАЧЕНИЕ(Перечисление.НДФЛСтавкиНалогообложенияРезидента.Ставка09)
		|			ТОГДА ""Раздел4""
		|		КОГДА (НЕ ЕСТЬNULL(ГражданствоФизЛицСрезПоследних.НеЯвляетсяНалоговымРезидентомРФ, ЛОЖЬ))
		|				И НДФЛСведенияОДоходах.КодДохода В (&МассивКодовДоходовРаздела7)
		|			ТОГДА ""Раздел7""
		|		КОГДА ЕСТЬNULL(ГражданствоФизЛицСрезПоследних.НеЯвляетсяНалоговымРезидентомРФ, ЛОЖЬ)
		|				ИЛИ НДФЛСведенияОДоходах.КодДохода.СтавкаНалогообложенияРезидента = ЗНАЧЕНИЕ(Перечисление.НДФЛСтавкиНалогообложенияРезидента.Ставка13)
		|			ТОГДА ""Раздел3""
		|		ИНАЧЕ ""Раздел5""
		|	КОНЕЦ КАК РАЗДЕЛ,
		|	НДФЛСведенияОДоходах.КодДохода.КодДляОтчетности КАК КодДохода,
		|	МЕСЯЦ(НДФЛСведенияОДоходах.Период) КАК Месяц,
		|	НДФЛСведенияОДоходах.СуммаДоходаОборот КАК СуммаДохода,
		|	НДФЛСведенияОДоходах.СуммаВычетаОборот КАК СуммаВычета
		|ИЗ
		|	РегистрНакопления.НДФЛСведенияОДоходах.Обороты(
		|			&НачалоНП,
		|			&КонецНП,
		|			Месяц,
		|			(ФизЛицо, Организация) В
		|				(ВЫБРАТЬ
		|					СписокФизЛицОрганизация.ФизЛицо,
		|					СписокФизЛицОрганизация.ГоловнаяОрганизация КАК Организация
		|				ИЗ
		|					ВТСписокФизЛицОрганизация КАК СписокФизЛицОрганизация)) КАК НДФЛСведенияОДоходах
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСтатусФизЛиц КАК ГражданствоФизЛицСрезПоследних
		|		ПО НДФЛСведенияОДоходах.ФизЛицо = ГражданствоФизЛицСрезПоследних.ФизЛицо
		|			И (ГОД(НДФЛСведенияОДоходах.Период) = ГражданствоФизЛицСрезПоследних.Год)
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТСписокФизЛицОрганизация КАК СписокФизЛицОрганизация
		|		ПО НДФЛСведенияОДоходах.ФизЛицо = СписокФизЛицОрганизация.ФизЛицо
		|			И (ГОД(НДФЛСведенияОДоходах.Период) = СписокФизЛицОрганизация.Год)
		|			И НДФЛСведенияОДоходах.Организация = СписокФизЛицОрганизация.ГоловнаяОрганизация
		|
		|УПОРЯДОЧИТЬ ПО
		|	ГоловнаяОрганизация,
		|	ФизЛицо,
		|	Год,
		|	РАЗДЕЛ,
		|	КодДохода
		|ИТОГИ
		|	СУММА(СуммаДохода),
		|	СУММА(СуммаВычета)
		|ПО
		|	ГоловнаяОрганизация,
		|	ФизЛицо,
		|	Год,
		|	РАЗДЕЛ,
		|	КодДохода";
		
		
		//-----------------------------------------------------------------------------
		// ВЫБОРКА ВЫЧЕТОВ ПО ВИДАМ И ПО МЕСЯЦАМ НАЛОГОВОГО ПЕРИОДА
		// Выбираются вычеты по 13% доходам (кроме доходов раздела 7). Для нерезидента - все вычеты. 
		
		ВычетыПоМесяцамПоВидамТекст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	НДФЛСведенияОДоходах.Организация КАК ГоловнаяОрганизация,
		|	НДФЛСведенияОДоходах.ФизЛицо КАК ФизЛицо,
		|	ГОД(НДФЛСведенияОДоходах.Период) КАК Год,
		|	НДФЛСведенияОДоходах.КодВычета.Код КАК КодВычета,
		|	МЕСЯЦ(НДФЛСведенияОДоходах.Период) КАК Месяц,
		|	СУММА(НДФЛСведенияОДоходах.СуммаВычета) КАК СуммаВычета
		|ИЗ
		|	РегистрНакопления.НДФЛСведенияОДоходах КАК НДФЛСведенияОДоходах
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТСписокФизЛицОрганизация КАК СписокФизЛицОрганизация
		|		ПО НДФЛСведенияОДоходах.Физлицо = СписокФизЛицОрганизация.ФизЛицо
		|			И ГОД(НДФЛСведенияОДоходах.Период)  = СписокФизЛицОрганизация.Год
		|			И НДФЛСведенияОДоходах.Организация = СписокФизЛицОрганизация.ГоловнаяОрганизация
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСтатусФизЛиц КАК ГражданствоФизЛицСрезПоследних
		|		ПО НДФЛСведенияОДоходах.ФизЛицо = ГражданствоФизЛицСрезПоследних.ФизЛицо
		|			И ГОД(НДФЛСведенияОДоходах.Период)  = ГражданствоФизЛицСрезПоследних.Год
		|ГДЕ
		|	НДФЛСведенияОДоходах.СуммаВычета <> 0
		|	И НДФЛСведенияОДоходах.Период МЕЖДУ &НачалоНП И &КонецНП
		|	И (НДФЛСведенияОДоходах.КодДохода.СтавкаНалогообложенияРезидента = ЗНАЧЕНИЕ(Перечисление.НДФЛСтавкиНалогообложенияРезидента.Ставка13)
		|				И (НЕ НДФЛСведенияОДоходах.КодДохода В (&МассивКодовДоходовРаздела7))
		|			ИЛИ (НЕ ЕСТЬNULL(ГражданствоФизЛицСрезПоследних.НеЯвляетсяНалоговымРезидентомРФ, ЛОЖЬ)))
		|
		|СГРУППИРОВАТЬ ПО
		|	НДФЛСведенияОДоходах.Организация,
		|	НДФЛСведенияОДоходах.ФизЛицо,
		|	ГОД(НДФЛСведенияОДоходах.Период),
		|	НДФЛСведенияОДоходах.КодВычета,
		|	МЕСЯЦ(НДФЛСведенияОДоходах.Период),
		|	НДФЛСведенияОДоходах.КодВычета.Код
		|ИТОГИ
		|	СУММА(СуммаВычета)
		|ПО
		|	ГоловнаяОрганизация,
		|	ФизЛицо,
		|	Год,
		|	КодВычета";
		
		//-----------------------------------------------------------------------------
		// НАЛОГИ (начисленные, удержанные, возвращенные ...) ПО МЕСЯЦАМ ПО РАЗДЕЛАМ ФОРМЫ
		
		//НалогиПоМесяцам
		//	Выбираются все начисленные налоги за месяцы текущего НП (Условие "МесяцНалоговогоПериода МЕЖДУ &НачалоНП И &КонецНП")
		// 		Поле "Месяц" - месяц налогового периода.
		//	Выбираются все удержанные налоги за месяцы текущего НП (Условие "МесяцНалоговогоПериода МЕЖДУ &НачалоНП И &КонецНП"),
		//  однако поле "месяц" вычисляется следующим образом:
		//		1) Если Период < НачалоНП то берется "Январь",
		//		2) Если Период > КонецНП то берется "Декабрь", 	
		//		3) Иначе Месяц = Месяц(Период) - период регистрации!!!. 	
		//	Выбираются все переданные на взыскание налоги по месяцам текущего НП(Условие "Период МЕЖДУ &НачалоНП И &КонецНП").
		// 	 Поле "Месяц" - период регистрации!!!
		//	Выбираются все возвращенные налоги за месяцы текущего НП (Условие "МесяцНалоговогоПериода МЕЖДУ &НачалоНП И &КонецНП"),
		//  однако поле "месяц" вычисляется следующим образом:
		//		1) Если Период < НачалоНП то берется "Январь",
		//		2) Если Период > КонецНП то берется "Декабрь", 	
		//		3) Иначе Месяц = Месяц(Период) - период регистрации!!!. 	
		// При возврате налог записывается с минусом!
		
		// НалогиПомесячноПоСтавкам
		//	Поля:
		//		Раздел - раздел налоговой карточки 
		//		Месяц - месяц налогового периода
		
		//		Начислено - сумма начисленного налога за текущий год за всю историю (без отбора по периоду регистарции, как и доходы)
		//		УдержаноВТекущемГоду - сумма удержанного налога за текущий год в текущем году
		//		УдержаноВПрошлыхГодах - сумма удержанного налога за текущий год в текущем году
		//		НачисленоВПрошлыхПериодах - сумма удержанного налога за текущий год
		//		ВозвращеноЗаТекущийГод - сумма налога, возвращенная в результате перерасчета за текущий год
		//		ВозвращеноЗаПрошлыеГоды - сумма налога, возвращенная в результате перерасчета за прошлые годы
		//		ПереданоНаВзыскание - сумма налога, переданная на взыскание
		//
		// Описание:
		//	Для каждого раздела карточки в разрезе месяцев налогового периода выбирает сведения о суммах налогов
		//
		
		//Описание поля Раздел налоговой карточки: 
		//	3 раздел - налог с доходов по ставке 13% и со всех доходов нерезидента,
		//	4 раздел - налог с доходов по ставке 9%
		//	5 раздел - налог с доходов по ставке 30%
		
		НалогиПомесячноПоСтавкамТекст = 
		"ВЫБРАТЬ
		|	НДФЛРасчетыСБюджетом.Организация КАК ГоловнаяОрганизация,
		|	НДФЛРасчетыСБюджетом.ФизЛицо КАК ФизЛицо,
		|	ГОД(НДФЛРасчетыСБюджетом.МесяцНалоговогоПериода) КАК ГОД,
		|	ВЫБОР
		|		КОГДА НДФЛРасчетыСБюджетом.ВидСтроки = ЗНАЧЕНИЕ(Перечисление.НДФЛРасчетыСБюджетомВидСтроки.Начисление)
		|			ТОГДА ""Начислено""
		|		КОГДА НДФЛРасчетыСБюджетом.ВидСтроки = ЗНАЧЕНИЕ(Перечисление.НДФЛРасчетыСБюджетомВидСтроки.Удержание)
		|			ТОГДА ""Удержано""
		|		КОГДА НДФЛРасчетыСБюджетом.ВидСтроки = ЗНАЧЕНИЕ(Перечисление.НДФЛРасчетыСБюджетомВидСтроки.ПереданоНаВзыскание)
		|			ТОГДА ""ПереданоНаВзыскание""
		|		КОГДА НДФЛРасчетыСБюджетом.ВидСтроки = ЗНАЧЕНИЕ(Перечисление.НДФЛРасчетыСБюджетомВидСтроки.ВозвратНалога)
		|			ТОГДА ""Возвращено""
		|		ИНАЧЕ """"
		|	КОНЕЦ КАК ВидСтроки,
		|	ВЫБОР
		|		КОГДА НДФЛРасчетыСБюджетом.ВидСтроки = ЗНАЧЕНИЕ(Перечисление.НДФЛРасчетыСБюджетомВидСтроки.Начисление)
		|			ТОГДА МЕСЯЦ(НДФЛРасчетыСБюджетом.МесяцНалоговогоПериода)
		|		КОГДА НДФЛРасчетыСБюджетом.ВидСтроки = ЗНАЧЕНИЕ(Перечисление.НДФЛРасчетыСБюджетомВидСтроки.Удержание)
		|			ТОГДА ВЫБОР
		|					КОГДА НДФЛРасчетыСБюджетом.Период < &НачалоНП
		|						ТОГДА 1
		|					КОГДА НДФЛРасчетыСБюджетом.Период > &КонецНП
		|						ТОГДА 12
		|					ИНАЧЕ МЕСЯЦ(НДФЛРасчетыСБюджетом.Период)
		|				КОНЕЦ
		|		КОГДА НДФЛРасчетыСБюджетом.ВидСтроки = ЗНАЧЕНИЕ(Перечисление.НДФЛРасчетыСБюджетомВидСтроки.ПереданоНаВзыскание)
		|			ТОГДА МЕСЯЦ(НДФЛРасчетыСБюджетом.Период)
		|		КОГДА НДФЛРасчетыСБюджетом.ВидСтроки = ЗНАЧЕНИЕ(Перечисление.НДФЛРасчетыСБюджетомВидСтроки.ВозвратНалога)
		|			ТОГДА ВЫБОР
		|					КОГДА НДФЛРасчетыСБюджетом.Период < &НачалоНП
		|						ТОГДА 1
		|					КОГДА НДФЛРасчетыСБюджетом.Период > &КонецНП
		|						ТОГДА 12
		|					ИНАЧЕ МЕСЯЦ(НДФЛРасчетыСБюджетом.Период)
		|				КОНЕЦ
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК Месяц,
		|	НДФЛРасчетыСБюджетом.СтавкаНалогообложенияРезидента КАК Ставка,
		|	ВЫБОР
		|		КОГДА НДФЛРасчетыСБюджетом.ВидСтроки = ЗНАЧЕНИЕ(Перечисление.НДФЛРасчетыСБюджетомВидСтроки.ВозвратНалога)
		|			ТОГДА -НДФЛРасчетыСБюджетом.Налог
		|		ИНАЧЕ НДФЛРасчетыСБюджетом.Налог
		|	КОНЕЦ КАК Налог,
		|	НДФЛРасчетыСБюджетом.КодДохода
		|ПОМЕСТИТЬ ВТДанныеОНалогах
		|ИЗ
		|	РегистрНакопления.НДФЛРасчетыСБюджетом КАК НДФЛРасчетыСБюджетом
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТСписокФизЛицОрганизация КАК СписокФизЛицОрганизация
		|		ПО НДФЛРасчетыСБюджетом.ФизЛицо = СписокФизЛицОрганизация.ФизЛицо
		|			И (ГОД(НДФЛРасчетыСБюджетом.МесяцНалоговогоПериода) = СписокФизЛицОрганизация.Год)
		|			И НДФЛРасчетыСБюджетом.Организация = СписокФизЛицОрганизация.ГоловнаяОрганизация
		|ГДЕ
		|	НДФЛРасчетыСБюджетом.МесяцНалоговогоПериода МЕЖДУ &НачалоНП И &КонецНП
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ФизЛицо,
		|	ГОД
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДанныеОНалогах.ГоловнаяОрганизация КАК ГоловнаяОрганизация,
		|	ДанныеОНалогах.ФизЛицо КАК ФизЛицо,
		|	ДанныеОНалогах.ГОД КАК Год,
		|	ДанныеОНалогах.Месяц,
		|	ВЫБОР
		|		КОГДА ДанныеОНалогах.ГОД > 2007
		|				И ДанныеОНалогах.КодДохода = ЗНАЧЕНИЕ(Справочник.ДоходыНДФЛ.Код1010)
		|			ТОГДА ""Раздел4""
		|		КОГДА ДанныеОНалогах.Ставка = ЗНАЧЕНИЕ(Перечисление.НДФЛСтавкиНалогообложенияРезидента.Ставка09)
		|			ТОГДА ""Раздел4""
		|		КОГДА ЕСТЬNULL(ГражданствоФизЛицСрезПоследних.НеЯвляетсяНалоговымРезидентомРФ, ЛОЖЬ)
		|				ИЛИ ДанныеОНалогах.Ставка = ЗНАЧЕНИЕ(Перечисление.НДФЛСтавкиНалогообложенияРезидента.Ставка13)
		|			ТОГДА ""Раздел3""
		|		ИНАЧЕ ""Раздел5""
		|	КОНЕЦ КАК РАЗДЕЛ,
		|	СУММА(ВЫБОР
		|			КОГДА ДанныеОНалогах.ВидСтроки = ""Начислено""
		|				ТОГДА ДанныеОНалогах.Налог
		|			ИНАЧЕ 0
		|		КОНЕЦ) КАК Начислено,
		|	СУММА(ВЫБОР
		|			КОГДА ДанныеОНалогах.ВидСтроки = ""Удержано""
		|				ТОГДА ДанныеОНалогах.Налог
		|			ИНАЧЕ 0
		|		КОНЕЦ) КАК Удержано,
		|	СУММА(ВЫБОР
		|			КОГДА ДанныеОНалогах.ВидСтроки = ""ПереданоНаВзыскание""
		|				ТОГДА ДанныеОНалогах.Налог
		|			ИНАЧЕ 0
		|		КОНЕЦ) КАК ПереданоНаВзыскание,
		|	СУММА(ВЫБОР
		|			КОГДА ДанныеОНалогах.ВидСтроки = ""Возвращено""
		|				ТОГДА ДанныеОНалогах.Налог
		|			ИНАЧЕ 0
		|		КОНЕЦ) КАК Возвращено
		|ИЗ
		|	ВТДанныеОНалогах КАК ДанныеОНалогах
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСтатусФизЛиц КАК ГражданствоФизЛицСрезПоследних
		|		ПО ДанныеОНалогах.ФизЛицо = ГражданствоФизЛицСрезПоследних.ФизЛицо
		|			И ДанныеОНалогах.ГОД = ГражданствоФизЛицСрезПоследних.Год
		|
		|СГРУППИРОВАТЬ ПО
		|	ДанныеОНалогах.Месяц,
		|	ДанныеОНалогах.ГоловнаяОрганизация,
		|	ДанныеОНалогах.ФизЛицо,
		|	ДанныеОНалогах.ГОД,
		|	ВЫБОР
		|		КОГДА ДанныеОНалогах.ГОД > 2007
		|				И ДанныеОНалогах.КодДохода = ЗНАЧЕНИЕ(Справочник.ДоходыНДФЛ.Код1010)
		|			ТОГДА ""Раздел4""
		|		КОГДА ДанныеОНалогах.Ставка = ЗНАЧЕНИЕ(Перечисление.НДФЛСтавкиНалогообложенияРезидента.Ставка09)
		|			ТОГДА ""Раздел4""
		|		КОГДА ЕСТЬNULL(ГражданствоФизЛицСрезПоследних.НеЯвляетсяНалоговымРезидентомРФ, ЛОЖЬ)
		|				ИЛИ ДанныеОНалогах.Ставка = ЗНАЧЕНИЕ(Перечисление.НДФЛСтавкиНалогообложенияРезидента.Ставка13)
		|			ТОГДА ""Раздел3""
		|		ИНАЧЕ ""Раздел5""
		|	КОНЕЦ
		|ИТОГИ ПО
		|	ГоловнаяОрганизация,
		|	ФизЛицо,
		|	Год,
		|	РАЗДЕЛ";
		
		
		//СальдоРасчетовНаНачалоГода 
		// Описание: выбирается сальдо по формуле
		//	Сальдо = остаток из рег-ра НДФЛРасчетыСБюджетом по измерению МесяцНалоговогоПериода < начало налогового периода 
		
		СальдоРасчетовНаНачалоГодаТекст = 
		"ВЫБРАТЬ
		|	НДФЛРасчетыСБюджетом.ФизЛицо КАК ФизЛицо,
		|	НДФЛРасчетыСБюджетом.Организация КАК ГоловнаяОрганизация,
		|	Года.Ссылка КАК Ставка,
		|	ЕСТЬNULL(СУММА(ВЫБОР
		|				КОГДА НДФЛРасчетыСБюджетом.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|					ТОГДА НДФЛРасчетыСБюджетом.Налог
		|				ИНАЧЕ -НДФЛРасчетыСБюджетом.Налог
		|			КОНЕЦ), 0) КАК Сальдо,
		|	ГОД(Года.Год) КАК Год
		|ИЗ
		|	(ВЫБРАТЬ РАЗЛИЧНЫЕ
		|		НАЧАЛОПЕРИОДА(РегламентированныйПроизводственныйКалендарь.Период, ГОД) КАК Год,
		|		НДФЛСтавкиНалогообложенияРезидента.Ссылка КАК Ссылка
		|	ИЗ
		|		ВТПериоды КАК РегламентированныйПроизводственныйКалендарь,
		|		Перечисление.НДФЛСтавкиНалогообложенияРезидента КАК НДФЛСтавкиНалогообложенияРезидента
		|	ГДЕ
		|		РегламентированныйПроизводственныйКалендарь.Период МЕЖДУ &НачалоНП И &КонецНП) КАК Года
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.НДФЛРасчетыСБюджетом КАК НДФЛРасчетыСБюджетом
		|		ПО Года.Ссылка = НДФЛРасчетыСБюджетом.СтавкаНалогообложенияРезидента
		|			И Года.Год > НДФЛРасчетыСБюджетом.МесяцНалоговогоПериода
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТСписокФизЛицОрганизация КАК СписокФизЛицОрганизация
		|		ПО НДФЛРасчетыСБюджетом.Физлицо = СписокФизЛицОрганизация.ФизЛицо
		|			И ГОД(Года.Год) = СписокФизЛицОрганизация.Год
		|			И НДФЛРасчетыСБюджетом.Организация = СписокФизЛицОрганизация.ГоловнаяОрганизация
		|
		|СГРУППИРОВАТЬ ПО
		|	НДФЛРасчетыСБюджетом.Организация,
		|	НДФЛРасчетыСБюджетом.ФизЛицо,
		|	Года.Год,
		|	Года.Ссылка";
		
		
		// МесяцыПоследнегоДоходаИлиНалогаТекст
		//	Поля:
		//		Месяц - месяц налогового периода - число от 1 до 12
		//		Физлицо
		//
		// Описание:
		//	Для каждого физлица рассчитывается последний месяц предоставления вычетов
		// 	как максимальное значение из двух следующих:
		//  1. Месяц самого позднего начисления
		//	2. Месяц самого позднего расчета налога   
		
		// ПрименениеВычетовВОрганизации
		//	Поля:
		//		Период - месяц налогового периода
		//		Физлицо
		//
		// Описание:
		//	Для каждого месяца налогового периода выбирает список физлиц, для которых применяются стандартные вычеты по 
		//	заданной организации. Учитываем тот факт, что применяемость всегда регистрируется на головную организацию,
		//  а также налоговое резидентство физлиц.
		//
			
		// Ограничим периоды предоставления вычетов
		// Ограничиваем месяцы предоставления вычетов месяцем, максимальным из двух: 
		// 1. Месяц последнего начисленного дохода ; 2. Месяц последнего расчета налога
		
		// ВТОблДоходыЗаГод 
		//	Поля:
		//		Период
		//		ОблагаемыйДоходЗаГод
		//
		//	Описание:
		//	Выбирает по всем периодам с начала года сумму дохода нарастающим итогом за год для расчета ограничения по стандартным вычетам
		//  Надо учитывать доходы, заработанные во всех структурных подразделениях организации
		//	и добавляем суммы совокупного дохода с предыдущего места работы в разрезе месяцев налогового периода,
		//  зарегистрированные на любое структурное подразделение организации
		
		// Временная таблица СтандартныеВычетыНаДетей
		//	Поля:
		//		Физлицо
		//		Период
		//		КодВычета
		//		КоличествоДетей
		//
		//	Описание:
		//	Выбирает по всем периодам года стандартные вычеты на детей на которые имеет право физлицо

		ПраваНаВычетыТекст =      
		"ВЫБРАТЬ
		|	ПоследниеМесяцы.ГоловнаяОрганизация КАК ГоловнаяОрганизация,
		|	ПоследниеМесяцы.ФизЛицо КАК ФизЛицо,
		|	ПоследниеМесяцы.Год КАК Год,
		|	МАКСИМУМ(ПоследниеМесяцы.Месяц) КАК Месяц
		|ПОМЕСТИТЬ ВТМесяцыПоследнегоДоходаИлиНалога
		|ИЗ
		|	(ВЫБРАТЬ РАЗЛИЧНЫЕ
		|		НДФЛСведенияОДоходах.Организация КАК ГоловнаяОрганизация,
		|		НДФЛСведенияОДоходах.ФизЛицо КАК ФизЛицо,
		|		МЕСЯЦ(НДФЛСведенияОДоходах.Период) КАК Месяц,
		|		ГОД(НДФЛСведенияОДоходах.Период) КАК Год
		|	ИЗ
		|		РегистрНакопления.НДФЛСведенияОДоходах КАК НДФЛСведенияОДоходах
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТСписокФизЛицОрганизация КАК СписокФизЛицОрганизация
		|			ПО НДФЛСведенияОДоходах.ФизЛицо = СписокФизЛицОрганизация.ФизЛицо
		|				И (ГОД(НДФЛСведенияОДоходах.Период) = СписокФизЛицОрганизация.Год)
		|				И НДФЛСведенияОДоходах.Организация = СписокФизЛицОрганизация.ГоловнаяОрганизация
		|	ГДЕ
		|		НДФЛСведенияОДоходах.Период МЕЖДУ &НачалоНП И &КонецНП
		|		И НДФЛСведенияОДоходах.КодДохода.СтавкаНалогообложенияРезидента = ЗНАЧЕНИЕ(Перечисление.НДФЛСтавкиНалогообложенияРезидента.Ставка13)
		|		И (НЕ НДФЛСведенияОДоходах.КодДохода В (&МассивКодовДоходовРаздела7))
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ РАЗЛИЧНЫЕ
		|		НДФЛРасчетыСбюджетом.Организация,
		|		НДФЛРасчетыСбюджетом.ФизЛицо,
		|		МЕСЯЦ(НДФЛРасчетыСбюджетом.МесяцНалоговогоПериода),
		|		ГОД(НДФЛРасчетыСбюджетом.Период)
		|	ИЗ
		|		РегистрНакопления.НДФЛРасчетыСБюджетом КАК НДФЛРасчетыСбюджетом
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТСписокФизЛицОрганизация КАК СписокФизЛицОрганизация
		|			ПО НДФЛРасчетыСбюджетом.ФизЛицо = СписокФизЛицОрганизация.ФизЛицо
		|				И (ГОД(НДФЛРасчетыСбюджетом.Период) = СписокФизЛицОрганизация.Год)
		|				И НДФЛРасчетыСбюджетом.Организация = СписокФизЛицОрганизация.ГоловнаяОрганизация
		|	ГДЕ
		|		НДФЛРасчетыСбюджетом.МесяцНалоговогоПериода МЕЖДУ &НачалоНП И &КонецНП
		|		И НДФЛРасчетыСбюджетом.СтавкаНалогообложенияРезидента = ЗНАЧЕНИЕ(Перечисление.НДФЛСтавкиНалогообложенияРезидента.Ставка13)
		|		И (НДФЛРасчетыСбюджетом.ВидСтроки = ЗНАЧЕНИЕ(Перечисление.НДФЛРасчетыСБюджетомВидСтроки.Начисление)
		|				ИЛИ НДФЛРасчетыСбюджетом.ВидСтроки = ЗНАЧЕНИЕ(Перечисление.НДФЛРасчетыСБюджетомВидСтроки.Удержание))) КАК ПоследниеМесяцы
		|
		|СГРУППИРОВАТЬ ПО
		|	ПоследниеМесяцы.Год,
		|	ПоследниеМесяцы.ГоловнаяОрганизация,
		|	ПоследниеМесяцы.ФизЛицо
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Год,
		|	ГоловнаяОрганизация,
		|	ФизЛицо
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	НДФЛПрименениеВычетов.Организация КАК ГоловнаяОрганизация,
		|	НДФЛПрименениеВычетовСрезПоследних.Физлицо КАК Физлицо,
		|	ГОД(НДФЛПрименениеВычетовСрезПоследних.Период) КАК Год,
		|	НДФЛПрименениеВычетовСрезПоследних.Период КАК Период
		|ПОМЕСТИТЬ ВТПрименениеВычетовПоОрганизациям
		|ИЗ
		|	(ВЫБРАТЬ
		|		МАКСИМУМ(НДФЛПрименениеВычетов.Период) КАК ПериодСреза,
		|		НДФЛПрименениеВычетов.Физлицо КАК Физлицо,
		|		Периоды.Период КАК Период
		|	ИЗ
		|		ВТПериоды КАК Периоды
		|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НДФЛПрименениеВычетов КАК НДФЛПрименениеВычетов
		|			ПО (НДФЛПрименениеВычетов.Период <= Периоды.Период)
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТСписокФизЛицОрганизация КАК СписокФизЛицОрганизация
		|			ПО (НДФЛПрименениеВычетов.Физлицо = СписокФизЛицОрганизация.ФизЛицо)
		|				И (ГОД(Периоды.Период) = СписокФизЛицОрганизация.Год)
		|	
		|	СГРУППИРОВАТЬ ПО
		|		НДФЛПрименениеВычетов.Физлицо,
		|		Периоды.Период) КАК НДФЛПрименениеВычетовСрезПоследних
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСтатусФизЛиц КАК ГражданствоФизЛицСрезПоследних
		|		ПО НДФЛПрименениеВычетовСрезПоследних.Физлицо = ГражданствоФизЛицСрезПоследних.ФизЛицо
		|			И (ГОД(НДФЛПрименениеВычетовСрезПоследних.Период) = ГражданствоФизЛицСрезПоследних.Год)
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НДФЛПрименениеВычетов КАК НДФЛПрименениеВычетов
		|		ПО НДФЛПрименениеВычетовСрезПоследних.ПериодСреза = НДФЛПрименениеВычетов.Период
		|			И НДФЛПрименениеВычетовСрезПоследних.Физлицо = НДФЛПрименениеВычетов.Физлицо
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТСписокФизЛицОрганизация КАК СписокФизЛицОрганизация
		|		ПО НДФЛПрименениеВычетовСрезПоследних.Физлицо = СписокФизЛицОрганизация.ФизЛицо
		|			И (НДФЛПрименениеВычетов.Организация = СписокФизЛицОрганизация.ГоловнаяОрганизация)
		|			И (ГОД(НДФЛПрименениеВычетовСрезПоследних.Период) = СписокФизЛицОрганизация.Год)
		|ГДЕ
		|	(НЕ ЕСТЬNULL(ГражданствоФизЛицСрезПоследних.НеЯвляетсяНалоговымРезидентомРФ, ЛОЖЬ))
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Год,
		|	ГоловнаяОрганизация,
		|	Физлицо
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ПрименениеВычетовВОрганизации.ГоловнаяОрганизация КАК ГоловнаяОрганизация,
		|	ПрименениеВычетовВОрганизации.Физлицо КАК Физлицо,
		|	ПрименениеВычетовВОрганизации.Период КАК Период
		|ПОМЕСТИТЬ ВТПрименениеВычетов
		|ИЗ
		|	ВТПрименениеВычетовПоОрганизациям КАК ПрименениеВычетовВОрганизации
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТМесяцыПоследнегоДоходаИлиНалога КАК МесяцыПоследнегоДоходаИлиНалога
		|		ПО (МЕСЯЦ(ПрименениеВычетовВОрганизации.Период) <= МесяцыПоследнегоДоходаИлиНалога.Месяц)
		|			И ПрименениеВычетовВОрганизации.Физлицо = МесяцыПоследнегоДоходаИлиНалога.ФизЛицо
		|			И ПрименениеВычетовВОрганизации.ГоловнаяОрганизация = МесяцыПоследнегоДоходаИлиНалога.ГоловнаяОрганизация
		|			И ПрименениеВычетовВОрганизации.Год = МесяцыПоследнегоДоходаИлиНалога.Год
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ГоловнаяОрганизация,
		|	Физлицо,
		|	Период
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	НДФЛОбороты.ФизЛицо КАК ФизЛицо,
		|	НДФЛОбороты.ГоловнаяОрганизация КАК ГоловнаяОрганизация,
		|	НДФЛОбороты.Период КАК Период,
		|	СУММА(НДФЛОбороты.ОблагаемыйДоход) КАК ОблагаемыйДоходЗаГод
		|ПОМЕСТИТЬ ВТОблДоходыЗаГод
		|ИЗ
		|	(ВЫБРАТЬ
		|		Периоды.Период КАК Период,
		|		НДФЛСведенияОДоходах.ФизЛицо КАК ФизЛицо,
		|		НДФЛСведенияОДоходах.Организация КАК ГоловнаяОрганизация,
		|		ГОД(Периоды.Период) КАК Год,
		|		НДФЛСведенияОДоходах.СуммаДохода - НДФЛСведенияОДоходах.СуммаВычета КАК ОблагаемыйДоход
		|	ИЗ
		|		ВТПериоды КАК Периоды
		|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.НДФЛСведенияОДоходах КАК НДФЛСведенияОДоходах
		|				ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТСписокФизЛицОрганизация КАК СписокФизЛицОрганизация
		|				ПО НДФЛСведенияОДоходах.ФизЛицо = СписокФизЛицОрганизация.ФизЛицо
		|					И НДФЛСведенияОДоходах.Организация = СписокФизЛицОрганизация.ГоловнаяОрганизация
		|			ПО Периоды.Период >= НДФЛСведенияОДоходах.Период
		|	ГДЕ
		|		НДФЛСведенияОДоходах.Период МЕЖДУ &НачалоНП И &КонецНП
		|		И НДФЛСведенияОДоходах.КодДохода.СтавкаНалогообложенияРезидента = ЗНАЧЕНИЕ(Перечисление.НДФЛСтавкиНалогообложенияРезидента.Ставка13)
		|		И (НЕ НДФЛСведенияОДоходах.КодДохода В (&МассивКодовДоходовРаздела7))
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		Периоды.Период,
		|		НДФЛДоходыПредыдущегоМестаРаботы.ФизЛицо,
		|		НДФЛДоходыПредыдущегоМестаРаботы.Организация,
		|		ГОД(Периоды.Период),
		|		НДФЛДоходыПредыдущегоМестаРаботы.Размер
		|	ИЗ
		|		ВТПериоды КАК Периоды
		|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НДФЛДоходыПредыдущегоМестаРаботы КАК НДФЛДоходыПредыдущегоМестаРаботы
		|				ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТСписокФизЛицОрганизация КАК СписокФизЛицОрганизация
		|				ПО НДФЛДоходыПредыдущегоМестаРаботы.ФизЛицо = СписокФизЛицОрганизация.ФизЛицо
		|					И НДФЛДоходыПредыдущегоМестаРаботы.Организация = СписокФизЛицОрганизация.ГоловнаяОрганизация
		|			ПО (НДФЛДоходыПредыдущегоМестаРаботы.МесяцНалоговогоПериода = НАЧАЛОПЕРИОДА(Периоды.Период, МЕСЯЦ))) КАК НДФЛОбороты
		|
		|СГРУППИРОВАТЬ ПО
		|	НДФЛОбороты.Период,
		|	НДФЛОбороты.ФизЛицо,
		|	НДФЛОбороты.ГоловнаяОрганизация
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ФизЛицо,
		|	ГоловнаяОрганизация,
		|	Период
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	НДФЛРазмерВычетовСрезПоследних.Период КАК Период,
		|	НДФЛРазмерВычетовСрезПоследних.КодВычета КАК КодВычета,
		|	НДФЛРазмерВычетов.Размер,
		|	НДФЛРазмерВычетов.ОграничениеПоДоходам
		|ПОМЕСТИТЬ ВТУстановленныеРазмерыВычетов
		|ИЗ
		|	(ВЫБРАТЬ
		|		Периоды.Период КАК Период,
		|		МАКСИМУМ(НДФЛРазмерВычетов.Период) КАК ПериодСреза,
		|		НДФЛРазмерВычетов.КодВычета КАК КодВычета
		|	ИЗ
		|		ВТПериоды КАК Периоды
		|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НДФЛРазмерВычетов КАК НДФЛРазмерВычетов
		|			ПО Периоды.Период >= НДФЛРазмерВычетов.Период
		|	
		|	СГРУППИРОВАТЬ ПО
		|		Периоды.Период,
		|		НДФЛРазмерВычетов.КодВычета) КАК НДФЛРазмерВычетовСрезПоследних
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НДФЛРазмерВычетов КАК НДФЛРазмерВычетов
		|		ПО НДФЛРазмерВычетовСрезПоследних.ПериодСреза = НДФЛРазмерВычетов.Период
		|			И НДФЛРазмерВычетовСрезПоследних.КодВычета = НДФЛРазмерВычетов.КодВычета
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Период,
		|	КодВычета
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ПрименениеВычетов.ГоловнаяОрганизация КАК ГоловнаяОрганизация,
		|	ГОД(ПрименениеВычетов.Период) КАК Год,
		|	ПрименениеВычетов.Период КАК Период,
		|	ПрименениеВычетов.Физлицо КАК ФизЛицо,
		|	СтандартныеВычеты.КодВычета КАК КодВычета,
		|	ВЫБОР
		|		КОГДА ЕСТЬNULL(РазмерыВычетов.ОграничениеПоДоходам, 0) = 0
		|				ИЛИ ЕСТЬNULL(ДоходыЗаГод.ОблагаемыйДоходЗаГод, 0) <= ЕСТЬNULL(РазмерыВычетов.ОграничениеПоДоходам, 0)
		|			ТОГДА ЕСТЬNULL(РазмерыВычетов.Размер, 0) * ЕСТЬNULL(СтандартныеВычеты.КоличествоДетей, 0)
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК РазмерВычета
		|ПОМЕСТИТЬ ВТСтандартныеВычеты
		|ИЗ
		|	ВТПрименениеВычетов КАК ПрименениеВычетов
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
		|			СрезПоследних.Период КАК Период,
		|			СрезПоследних.Физлицо КАК ФизЛицо,
		|			СрезПоследних.КодВычета КАК КодВычета,
		|			ВЫБОР
		|				КОГДА НДФЛСтандартныеВычетыНаДетей.ПериодЗавершения <= НАЧАЛОПЕРИОДА(СрезПоследних.Период, МЕСЯЦ)
		|						И НДФЛСтандартныеВычетыНаДетей.ПериодЗавершения <> ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
		|					ТОГДА НДФЛСтандартныеВычетыНаДетей.КоличествоДетейЗавершения
		|				ИНАЧЕ НДФЛСтандартныеВычетыНаДетей.КоличествоДетей
		|			КОНЕЦ КАК КоличествоДетей
		|		ИЗ
		|			(ВЫБРАТЬ
		|				НДФЛСтандартныеВычетыНаДетей.Физлицо КАК Физлицо,
		|				Периоды.Период КАК Период,
		|				НДФЛСтандартныеВычетыНаДетей.КодВычета КАК КодВычета,
		|				МАКСИМУМ(НДФЛСтандартныеВычетыНаДетей.Период) КАК ПериодРегистра
		|			ИЗ
		|				ВТПериоды КАК Периоды
		|					ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.НДФЛСтандартныеВычетыНаДетей КАК НДФЛСтандартныеВычетыНаДетей
		|						ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТСписокФизЛицОрганизация КАК СписокФизЛицОрганизация
		|						ПО НДФЛСтандартныеВычетыНаДетей.Физлицо = СписокФизЛицОрганизация.ФизЛицо
		|					ПО Периоды.Период >= НДФЛСтандартныеВычетыНаДетей.Период
		|			
		|			СГРУППИРОВАТЬ ПО
		|				НДФЛСтандартныеВычетыНаДетей.Физлицо,
		|				НДФЛСтандартныеВычетыНаДетей.КодВычета,
		|				Периоды.Период) КАК СрезПоследних
		|				ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.НДФЛСтандартныеВычетыНаДетей КАК НДФЛСтандартныеВычетыНаДетей
		|				ПО СрезПоследних.ПериодРегистра = НДФЛСтандартныеВычетыНаДетей.Период
		|					И СрезПоследних.КодВычета = НДФЛСтандартныеВычетыНаДетей.КодВычета
		|					И СрезПоследних.Физлицо = НДФЛСтандартныеВычетыНаДетей.Физлицо
		|		
		|		ОБЪЕДИНИТЬ ВСЕ
		|		
		|		ВЫБРАТЬ
		|			НДФЛСтандартныеВычетыФизлицСрезПоследних.Период,
		|			НДФЛСтандартныеВычетыФизлицСрезПоследних.Физлицо,
		|			ВычетыФизлиц.КодВычетаЛичный,
		|			1
		|		ИЗ
		|			(ВЫБРАТЬ
		|				МАКСИМУМ(ВычетыФизлиц.Период) КАК ПериодСреза,
		|				Периоды.Период КАК Период,
		|				ВычетыФизлиц.Физлицо КАК Физлицо
		|			ИЗ
		|				ВТПериоды КАК Периоды
		|					ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НДФЛСтандартныеВычетыФизлиц КАК ВычетыФизлиц
		|						ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТСписокФизЛицОрганизация КАК СписокФизЛицОрганизация
		|						ПО ВычетыФизлиц.Физлицо = СписокФизЛицОрганизация.ФизЛицо
		|					ПО Периоды.Период >= ВычетыФизлиц.Период
		|			
		|			СГРУППИРОВАТЬ ПО
		|				ВычетыФизлиц.Физлицо,
		|				Периоды.Период) КАК НДФЛСтандартныеВычетыФизлицСрезПоследних
		|				ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.НДФЛСтандартныеВычетыФизлиц КАК ВычетыФизлиц
		|				ПО НДФЛСтандартныеВычетыФизлицСрезПоследних.ПериодСреза = ВычетыФизлиц.Период
		|					И НДФЛСтандартныеВычетыФизлицСрезПоследних.Физлицо = ВычетыФизлиц.Физлицо) КАК СтандартныеВычеты
		|			ЛЕВОЕ СОЕДИНЕНИЕ ВТУстановленныеРазмерыВычетов КАК РазмерыВычетов
		|			ПО СтандартныеВычеты.КодВычета = РазмерыВычетов.КодВычета
		|				И СтандартныеВычеты.Период = РазмерыВычетов.Период
		|		ПО ПрименениеВычетов.Период = СтандартныеВычеты.Период
		|			И ПрименениеВычетов.Физлицо = СтандартныеВычеты.ФизЛицо
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТОблДоходыЗаГод КАК ДоходыЗаГод
		|		ПО ПрименениеВычетов.Период = ДоходыЗаГод.Период
		|			И ПрименениеВычетов.Физлицо = ДоходыЗаГод.ФизЛицо
		|			И ПрименениеВычетов.ГоловнаяОрганизация = ДоходыЗаГод.ГоловнаяОрганизация
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ГоловнаяОрганизация,
		|	ФизЛицо,
		|	Период,
		|	КодВычета
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	КОНЕЦПЕРИОДА(НДФЛРасчетыСБюджетом.МесяцНалоговогоПериода, МЕСЯЦ) КАК Период,
		|	ГОД(НДФЛРасчетыСБюджетом.МесяцНалоговогоПериода) КАК Год,
		|	НДФЛРасчетыСБюджетом.ФизЛицо КАК ФизЛицо,
		|	НДФЛРасчетыСБюджетом.Организация КАК ГоловнаяОрганизация,
		|	НДФЛРасчетыСБюджетом.КодВычета КАК КодВычета,
		|	СУММА(НДФЛРасчетыСБюджетом.ПримененныйВычет) КАК ПримененныйВычет
		|ПОМЕСТИТЬ ВТПредоставленныеВычеты
		|ИЗ
		|	РегистрНакопления.НДФЛПредоставленныеСтандартныеВычетыФизЛиц КАК НДФЛРасчетыСБюджетом
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТСписокФизЛицОрганизация КАК СписокФизЛицОрганизация
		|		ПО НДФЛРасчетыСБюджетом.ФизЛицо = СписокФизЛицОрганизация.ФизЛицо
		|			И (ГОД(НДФЛРасчетыСБюджетом.МесяцНалоговогоПериода) = СписокФизЛицОрганизация.Год)
		|			И НДФЛРасчетыСБюджетом.Организация = СписокФизЛицОрганизация.ГоловнаяОрганизация
		|ГДЕ
		|	НДФЛРасчетыСБюджетом.МесяцНалоговогоПериода МЕЖДУ &НачалоНП И &КонецНП
		|
		|СГРУППИРОВАТЬ ПО
		|	КОНЕЦПЕРИОДА(НДФЛРасчетыСБюджетом.МесяцНалоговогоПериода, МЕСЯЦ),
		|	ГОД(НДФЛРасчетыСБюджетом.МесяцНалоговогоПериода),
		|	НДФЛРасчетыСБюджетом.ФизЛицо,
		|	НДФЛРасчетыСБюджетом.Организация,
		|	НДФЛРасчетыСБюджетом.КодВычета
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ГоловнаяОрганизация,
		|	ФизЛицо,
		|	Период,
		|	КодВычета
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Периоды.ГоловнаяОрганизация КАК ГоловнаяОрганизация,
		|	Периоды.ФизЛицо КАК ФизЛицо,
		|	Периоды.Год КАК Год,
		|	МЕСЯЦ(Периоды.Период) КАК Месяц,
		|	Периоды.КодВычета,
		|	ВЫБОР
		|		КОГДА Периоды.Год < 2008
		|			ТОГДА Периоды.КодВычета.КодДляОтчетности2007
		|		КОГДА Периоды.Год = 2008
		|			ТОГДА Периоды.КодВычета.КодДляОтчетности2008
		|		ИНАЧЕ Периоды.КодВычета.КодДляОтчетности2009
		|	КОНЕЦ КАК КодВычетаКод,
		|	ВЫБОР
		|		КОГДА Периоды.Год > 2008
		|			ТОГДА ВЫБОР
		|					КОГДА Периоды.КодВычета = ЗНАЧЕНИЕ(Справочник.ВычетыНДФЛ.Код101)
		|						ТОГДА 1
		|					КОГДА Периоды.КодВычета = ЗНАЧЕНИЕ(Справочник.ВычетыНДФЛ.Код102)
		|						ТОГДА 2
		|					КОГДА Периоды.КодВычета = ЗНАЧЕНИЕ(Справочник.ВычетыНДФЛ.Код106)
		|						ТОГДА 6
		|					КОГДА Периоды.КодВычета = ЗНАЧЕНИЕ(Справочник.ВычетыНДФЛ.Код107)
		|						ТОГДА 7
		|					КОГДА Периоды.КодВычета = ЗНАЧЕНИЕ(Справочник.ВычетыНДФЛ.Код103)
		|						ТОГДА 3
		|					КОГДА Периоды.КодВычета = ЗНАЧЕНИЕ(Справочник.ВычетыНДФЛ.Код104)
		|						ТОГДА 4
		|					КОГДА Периоды.КодВычета = ЗНАЧЕНИЕ(Справочник.ВычетыНДФЛ.Код105)
		|						ТОГДА 5
		|					КОГДА Периоды.КодВычета = ЗНАЧЕНИЕ(Справочник.ВычетыНДФЛ.Код111)
		|						ТОГДА 8
		|					КОГДА Периоды.КодВычета = ЗНАЧЕНИЕ(Справочник.ВычетыНДФЛ.Код113)
		|						ТОГДА 9
		|					КОГДА Периоды.КодВычета = ЗНАЧЕНИЕ(Справочник.ВычетыНДФЛ.Код319)
		|						ТОГДА 90
		|					ИНАЧЕ -1
		|				КОНЕЦ
		|		КОГДА Периоды.КодВычета = ЗНАЧЕНИЕ(Справочник.ВычетыНДФЛ.Код101)
		|			ТОГДА 1
		|		КОГДА Периоды.КодВычета = ЗНАЧЕНИЕ(Справочник.ВычетыНДФЛ.Код102)
		|			ТОГДА 2
		|		КОГДА Периоды.КодВычета = ЗНАЧЕНИЕ(Справочник.ВычетыНДФЛ.Код106)
		|			ТОГДА 6
		|		КОГДА Периоды.КодВычета = ЗНАЧЕНИЕ(Справочник.ВычетыНДФЛ.Код107)
		|			ТОГДА 7
		|		КОГДА Периоды.КодВычета = ЗНАЧЕНИЕ(Справочник.ВычетыНДФЛ.Код103)
		|			ТОГДА 3
		|		КОГДА Периоды.КодВычета = ЗНАЧЕНИЕ(Справочник.ВычетыНДФЛ.Код104)
		|			ТОГДА 4
		|		КОГДА Периоды.КодВычета = ЗНАЧЕНИЕ(Справочник.ВычетыНДФЛ.Код105)
		|			ТОГДА 5
		|		ИНАЧЕ -1
		|	КОНЕЦ КАК НомерСтрокиВычета,
		|	ЕСТЬNULL(ПраваНаВычеты.РазмерВычета, 0) КАК РазмерВычета,
		|	ЕСТЬNULL(ПредоставленныеCтандартныеВычеты.ПримененныйВычет, 0) КАК ПримененныйВычет
		|ИЗ
		|	(ВЫБРАТЬ РАЗЛИЧНЫЕ
		|		ВТСтандартныеВычеты.Период КАК Период,
		|		ВТСтандартныеВычеты.ФизЛицо КАК ФизЛицо,
		|		ВТСтандартныеВычеты.Год КАК Год,
		|		ВТСтандартныеВычеты.КодВычета КАК КодВычета,
		|		ВТСтандартныеВычеты.ГоловнаяОрганизация КАК ГоловнаяОрганизация
		|	ИЗ
		|		ВТСтандартныеВычеты КАК ВТСтандартныеВычеты
		|	
		|	ОБЪЕДИНИТЬ
		|	
		|	ВЫБРАТЬ РАЗЛИЧНЫЕ
		|		ВТПредоставленныеВычеты.Период,
		|		ВТПредоставленныеВычеты.ФизЛицо,
		|		ВТПредоставленныеВычеты.Год,
		|		ВТПредоставленныеВычеты.КодВычета,
		|		ВТПредоставленныеВычеты.ГоловнаяОрганизация
		|	ИЗ
		|		ВТПредоставленныеВычеты КАК ВТПредоставленныеВычеты) КАК Периоды
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСтандартныеВычеты КАК ПраваНаВычеты
		|		ПО Периоды.Период = ПраваНаВычеты.Период
		|			И Периоды.КодВычета = ПраваНаВычеты.КодВычета
		|			И Периоды.ФизЛицо = ПраваНаВычеты.ФизЛицо
		|			И Периоды.ГоловнаяОрганизация = ПраваНаВычеты.ГоловнаяОрганизация
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТПредоставленныеВычеты КАК ПредоставленныеCтандартныеВычеты
		|		ПО Периоды.Период = ПредоставленныеCтандартныеВычеты.Период
		|			И Периоды.КодВычета = ПредоставленныеCтандартныеВычеты.КодВычета
		|			И Периоды.ФизЛицо = ПредоставленныеCтандартныеВычеты.ФизЛицо
		|			И Периоды.ГоловнаяОрганизация = ПредоставленныеCтандартныеВычеты.ГоловнаяОрганизация
		|
		|УПОРЯДОЧИТЬ ПО
		|	ГоловнаяОрганизация,
		|	ФизЛицо,
		|	Год,
		|	КодВычетаКод,
		|	Месяц";
		
		// ИмущественныеВычетыТекст
		//
		// Описание: выбирает суммы предоставленных имущественных вычетов по указанной организации
		
		ИмущественныеВычетыТекст = 
		"ВЫБРАТЬ
		|	МЕСЯЦ(МесяцыНалоговогоПериода.Период) КАК МесяцНП,
		|	НДФЛИмущественныеВычетыФизлицОбороты.ФизЛицо КАК ФизЛицо,
		|	НДФЛИмущественныеВычетыФизлицОбороты.Организация КАК ГоловнаяОрганизация,
		|	ГОД(МесяцыНалоговогоПериода.Период) КАК Год,
		|	НДФЛИмущественныеВычетыФизлицОбороты.КодВычетаИмущественный,
		|	СУММА(ЕСТЬNULL(НДФЛИмущественныеВычетыФизлицОбороты.РазмерРасход, 0)) КАК РазмерВычета
		|ИЗ
		|	ВТПериоды КАК МесяцыНалоговогоПериода
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.НДФЛИмущественныеВычетыФизлиц.Обороты(
		|				&НачалоНП,
		|				&КонецНП,
		|				Месяц,
		|				(ФизЛицо, Год, Организация) В
		|					(ВЫБРАТЬ
		|						СписокФизЛицОрганизация.ФизЛицо,
		|						СписокФизЛицОрганизация.Год,
		|						СписокФизЛицОрганизация.ГоловнаяОрганизация КАК Организация
		|					ИЗ
		|						ВТСписокФизЛицОрганизация КАК СписокФизЛицОрганизация)) КАК НДФЛИмущественныеВычетыФизлицОбороты
		|		ПО (НАЧАЛОПЕРИОДА(МесяцыНалоговогоПериода.Период, МЕСЯЦ) = НДФЛИмущественныеВычетыФизлицОбороты.Период)
		|
		|СГРУППИРОВАТЬ ПО
		|	НДФЛИмущественныеВычетыФизлицОбороты.ФизЛицо,
		|	НДФЛИмущественныеВычетыФизлицОбороты.Организация,
		|	НДФЛИмущественныеВычетыФизлицОбороты.КодВычетаИмущественный,
		|	МЕСЯЦ(МесяцыНалоговогоПериода.Период),
		|	ГОД(МесяцыНалоговогоПериода.Период)
		|
		|УПОРЯДОЧИТЬ ПО
		|	ГоловнаяОрганизация,
		|	ФизЛицо,
		|	Год,
		|	МесяцНП";
		
	Иначе
		
		ДоходыПоМесяцамПоВидамТекст = 
		"ВЫБРАТЬ
		|	НДФЛСведенияОДоходах.Организация КАК ГоловнаяОрганизация,
		|	НДФЛСведенияОДоходах.ФизЛицо КАК ФизЛицо,
		|	ГОД(НДФЛСведенияОДоходах.Период) КАК Год,
		|	ВЫБОР
		|		КОГДА ГОД(НДФЛСведенияОДоходах.Период) > 2007
		|				И НДФЛСведенияОДоходах.КодДохода = ЗНАЧЕНИЕ(Справочник.ДоходыНДФЛ.Код1010)
		|			ТОГДА ""Раздел4""
		|		КОГДА НДФЛСведенияОДоходах.КодДохода.СтавкаНалогообложенияРезидента = ЗНАЧЕНИЕ(Перечисление.НДФЛСтавкиНалогообложенияРезидента.Ставка09)
		|			ТОГДА ""Раздел4""
		|		КОГДА (НЕ ЕСТЬNULL(ГражданствоФизЛицСрезПоследних.НеЯвляетсяНалоговымРезидентомРФ, ЛОЖЬ))
		|				И НДФЛСведенияОДоходах.КодДохода В (&МассивКодовДоходовРаздела7)
		|			ТОГДА ""Раздел7""
		|		КОГДА ЕСТЬNULL(ГражданствоФизЛицСрезПоследних.НеЯвляетсяНалоговымРезидентомРФ, ЛОЖЬ)
		|				ИЛИ НДФЛСведенияОДоходах.КодДохода.СтавкаНалогообложенияРезидента = ЗНАЧЕНИЕ(Перечисление.НДФЛСтавкиНалогообложенияРезидента.Ставка13)
		|			ТОГДА ""Раздел3""
		|		ИНАЧЕ ""Раздел5""
		|	КОНЕЦ КАК РАЗДЕЛ,
		|	НДФЛСведенияОДоходах.КодДохода.КодДляОтчетности КАК КодДохода,
		|	МЕСЯЦ(НДФЛСведенияОДоходах.Период) КАК Месяц,
		|	СУММА(НДФЛСведенияОДоходах.СуммаДохода) КАК СуммаДохода,
		|	СУММА(НДФЛСведенияОДоходах.СуммаВычета) КАК СуммаВычета,
		|	НДФЛСведенияОДоходах.КодДохода.Код КАК КодДоходаКод,
		|	НДФЛСведенияОДоходах.КодДохода.Код КАК КодДоходаКод1
		|ИЗ
		|	РегистрНакопления.НДФЛСведенияОДоходах КАК НДФЛСведенияОДоходах
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТСписокФизЛицОрганизация КАК СписокФизЛицОрганизация
		|		ПО НДФЛСведенияОДоходах.ФизЛицо = СписокФизЛицОрганизация.ФизЛицо
		|			И НДФЛСведенияОДоходах.Организация = СписокФизЛицОрганизация.ГоловнаяОрганизация
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСтатусФизЛиц КАК ГражданствоФизЛицСрезПоследних
		|		ПО НДФЛСведенияОДоходах.ФизЛицо = ГражданствоФизЛицСрезПоследних.ФизЛицо
		|			И (ГОД(НДФЛСведенияОДоходах.Период) = ГражданствоФизЛицСрезПоследних.Год)
		|ГДЕ
		|	ВЫБОР
		|			КОГДА НДФЛСведенияОДоходах.КодПоОКАТО <> &ПустойКодПоОКАТО
		|				ТОГДА НДФЛСведенияОДоходах.КодПоОКАТО
		|			КОГДА ЕСТЬNULL(НДФЛСведенияОДоходах.ПодразделениеОрганизации.КодПоОКАТО, &ПустойКодПоОКАТО) <> &ПустойКодПоОКАТО
		|				ТОГДА НДФЛСведенияОДоходах.ПодразделениеОрганизации.КодПоОКАТО
		|			ИНАЧЕ ЕСТЬNULL(НДФЛСведенияОДоходах.ОбособленноеПодразделение.КодПоОКАТО, &ПустойКодПоОКАТО)
		|		КОНЕЦ = &КодПоОКАТО
		|	И НДФЛСведенияОДоходах.Период МЕЖДУ &НачалоНП И &КонецНП
		|
		|СГРУППИРОВАТЬ ПО
		|	НДФЛСведенияОДоходах.Организация,
		|	НДФЛСведенияОДоходах.ФизЛицо,
		|	ГОД(НДФЛСведенияОДоходах.Период),
		|	НДФЛСведенияОДоходах.КодДохода.КодДляОтчетности,
		|	ВЫБОР
		|		КОГДА ГОД(НДФЛСведенияОДоходах.Период) > 2007
		|				И НДФЛСведенияОДоходах.КодДохода = ЗНАЧЕНИЕ(Справочник.ДоходыНДФЛ.Код1010)
		|			ТОГДА ""Раздел4""
		|		КОГДА НДФЛСведенияОДоходах.КодДохода.СтавкаНалогообложенияРезидента = ЗНАЧЕНИЕ(Перечисление.НДФЛСтавкиНалогообложенияРезидента.Ставка09)
		|			ТОГДА ""Раздел4""
		|		КОГДА (НЕ ЕСТЬNULL(ГражданствоФизЛицСрезПоследних.НеЯвляетсяНалоговымРезидентомРФ, ЛОЖЬ))
		|				И НДФЛСведенияОДоходах.КодДохода В (&МассивКодовДоходовРаздела7)
		|			ТОГДА ""Раздел7""
		|		КОГДА ЕСТЬNULL(ГражданствоФизЛицСрезПоследних.НеЯвляетсяНалоговымРезидентомРФ, ЛОЖЬ)
		|				ИЛИ НДФЛСведенияОДоходах.КодДохода.СтавкаНалогообложенияРезидента = ЗНАЧЕНИЕ(Перечисление.НДФЛСтавкиНалогообложенияРезидента.Ставка13)
		|			ТОГДА ""Раздел3""
		|		ИНАЧЕ ""Раздел5""
		|	КОНЕЦ,
		|	МЕСЯЦ(НДФЛСведенияОДоходах.Период),
		|	НДФЛСведенияОДоходах.КодДохода.Код,
		|	НДФЛСведенияОДоходах.КодДохода.Код
		|
		|УПОРЯДОЧИТЬ ПО
		|	КодДоходаКод,
		|	НДФЛСведенияОДоходах.КодДохода.Код
		|ИТОГИ
		|	СУММА(СуммаДохода),
		|	СУММА(СуммаВычета)
		|ПО
		|	ГоловнаяОрганизация,
		|	ФизЛицо,
		|	Год,
		|	РАЗДЕЛ,
		|	КодДохода";
		
		//-----------------------------------------------------------------------------
		// ВЫБОРКА ВЫЧЕТОВ ПО ВИДАМ И ПО МЕСЯЦАМ НАЛОГОВОГО ПЕРИОДА
		// Выбираются вычеты по 13% доходам (кроме доходов раздела 7). Для нерезидента - все вычеты. 
		
		ВычетыПоМесяцамПоВидамТекст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	НДФЛСведенияОДоходах.Организация КАК ГоловнаяОрганизация,
		|	НДФЛСведенияОДоходах.ФизЛицо КАК ФизЛицо,
		|	ГОД(НДФЛСведенияОДоходах.Период) КАК Год,
		|	НДФЛСведенияОДоходах.КодВычета.Код КАК КодВычета,
		|	МЕСЯЦ(НДФЛСведенияОДоходах.Период) КАК Месяц,
		|	СУММА(НДФЛСведенияОДоходах.СуммаВычета) КАК СуммаВычета
		|ИЗ
		|	РегистрНакопления.НДФЛСведенияОДоходах КАК НДФЛСведенияОДоходах
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТСписокФизЛицОрганизация КАК СписокФизЛицОрганизация
		|		ПО НДФЛСведенияОДоходах.Физлицо = СписокФизЛицОрганизация.ФизЛицо
		|			И ГОД(НДФЛСведенияОДоходах.Период)  = СписокФизЛицОрганизация.Год
		|			И НДФЛСведенияОДоходах.Организация = СписокФизЛицОрганизация.ГоловнаяОрганизация
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСтатусФизЛиц КАК ГражданствоФизЛицСрезПоследних
		|		ПО НДФЛСведенияОДоходах.ФизЛицо = ГражданствоФизЛицСрезПоследних.ФизЛицо
		|			И ГОД(НДФЛСведенияОДоходах.Период)  = ГражданствоФизЛицСрезПоследних.Год
		|ГДЕ
		|	ВЫБОР
		|			КОГДА НДФЛСведенияОДоходах.КодПоОКАТО <> &ПустойКодПоОКАТО
		|				ТОГДА НДФЛСведенияОДоходах.КодПоОКАТО
		|			КОГДА ЕСТЬNULL(НДФЛСведенияОДоходах.ПодразделениеОрганизации.КодПоОКАТО, &ПустойКодПоОКАТО) <> &ПустойКодПоОКАТО
		|				ТОГДА НДФЛСведенияОДоходах.ПодразделениеОрганизации.КодПоОКАТО
		|			ИНАЧЕ ЕСТЬNULL(НДФЛСведенияОДоходах.ОбособленноеПодразделение.КодПоОКАТО, &ПустойКодПоОКАТО)
		|		КОНЕЦ = &КодПоОКАТО
		|	И НДФЛСведенияОДоходах.СуммаВычета <> 0
		|	И НДФЛСведенияОДоходах.Период МЕЖДУ &НачалоНП И &КонецНП
		|	И (НДФЛСведенияОДоходах.КодДохода.СтавкаНалогообложенияРезидента = ЗНАЧЕНИЕ(Перечисление.НДФЛСтавкиНалогообложенияРезидента.Ставка13)
		|				И (НЕ НДФЛСведенияОДоходах.КодДохода В (&МассивКодовДоходовРаздела7))
		|			ИЛИ (НЕ ЕСТЬNULL(ГражданствоФизЛицСрезПоследних.НеЯвляетсяНалоговымРезидентомРФ, ЛОЖЬ)))
		|
		|СГРУППИРОВАТЬ ПО
		|	НДФЛСведенияОДоходах.Организация,
		|	НДФЛСведенияОДоходах.ФизЛицо,
		|	ГОД(НДФЛСведенияОДоходах.Период),
		|	НДФЛСведенияОДоходах.КодВычета,
		|	МЕСЯЦ(НДФЛСведенияОДоходах.Период),
		|	НДФЛСведенияОДоходах.КодВычета.Код
		|ИТОГИ
		|	СУММА(СуммаВычета)
		|ПО
		|	ГоловнаяОрганизация,
		|	ФизЛицо,
		|	Год,
		|	КодВычета";
		
		//-----------------------------------------------------------------------------
		// НАЛОГИ (начисленные, удержанные, возвращенные ...) ПО МЕСЯЦАМ ПО РАЗДЕЛАМ ФОРМЫ
		
		//НалогиПоМесяцамТекст
		//	Выбираются все начисленные налоги за месяцы текущего НП (Условие "МесяцНалоговогоПериода МЕЖДУ &НачалоНП И &КонецНП")
		// 		Поле "Месяц" - месяц налогового периода.
		//	Выбираются все удержанные налоги за месяцы текущего НП (Условие "МесяцНалоговогоПериода МЕЖДУ &НачалоНП И &КонецНП"),
		//  однако поле "месяц" вычисляется следующим образом:
		//		1) Если Период < НачалоНП то берется "Январь",
		//		2) Если Период > КонецНП то берется "Декабрь", 	
		//		3) Иначе Месяц = Месяц(Период) - период регистрации!!!. 	
		//	Выбираются все переданные на взыскание налоги по месяцам текущего НП(Условие "Период МЕЖДУ &НачалоНП И &КонецНП").
		// 	 Поле "Месяц" - период регистрации!!!
		//	Выбираются все возвращенные налоги за месяцы текущего НП (Условие "МесяцНалоговогоПериода МЕЖДУ &НачалоНП И &КонецНП"),
		//  однако поле "месяц" вычисляется следующим образом:
		//		1) Если Период < НачалоНП то берется "Январь",
		//		2) Если Период > КонецНП то берется "Декабрь", 	
		//		3) Иначе Месяц = Месяц(Период) - период регистрации!!!. 	
		// При возврате налог записывается с минусом!
		
		// НалогиПомесячноПоСтавкамТекст
		//	Поля:
		//		Раздел - раздел налоговой карточки 
		//		Месяц - месяц налогового периода
		
		//		Начислено - сумма начисленного налога за текущий год за всю историю (без отбора по периоду регистарции, как и доходы)
		//		УдержаноВТекущемГоду - сумма удержанного налога за текущий год в текущем году
		//		УдержаноВПрошлыхГодах - сумма удержанного налога за текущий год в текущем году
		//		НачисленоВПрошлыхПериодах - сумма удержанного налога за текущий год
		//		ВозвращеноЗаТекущийГод - сумма налога, возвращенная в результате перерасчета за текущий год
		//		ВозвращеноЗаПрошлыеГоды - сумма налога, возвращенная в результате перерасчета за прошлые годы
		//		ПереданоНаВзыскание - сумма налога, переданная на взыскание
		//
		// Описание:
		//	Для каждого раздела карточки в разрезе месяцев налогового периода выбирает сведения о суммах налогов
		//
		
		//Описание поля Раздел налоговой карточки: 
		//	3 раздел - налог с доходов по ставке 13% и со всех доходов нерезидента,
		//	4 раздел - налог с доходов по ставке 9%
		//	5 раздел - налог с доходов по ставке 30%
		
		НалогиПомесячноПоСтавкамТекст = 
		"ВЫБРАТЬ
		|	НДФЛРасчетыСБюджетом.Организация КАК ГоловнаяОрганизация,
		|	НДФЛРасчетыСБюджетом.ФизЛицо КАК ФизЛицо,
		|	ГОД(НДФЛРасчетыСБюджетом.МесяцНалоговогоПериода) КАК ГОД,
		|	ВЫБОР
		|		КОГДА НДФЛРасчетыСБюджетом.ВидСтроки = ЗНАЧЕНИЕ(Перечисление.НДФЛРасчетыСБюджетомВидСтроки.Начисление)
		|			ТОГДА ""Начислено""
		|		КОГДА НДФЛРасчетыСБюджетом.ВидСтроки = ЗНАЧЕНИЕ(Перечисление.НДФЛРасчетыСБюджетомВидСтроки.Удержание)
		|			ТОГДА ""Удержано""
		|		КОГДА НДФЛРасчетыСБюджетом.ВидСтроки = ЗНАЧЕНИЕ(Перечисление.НДФЛРасчетыСБюджетомВидСтроки.ПереданоНаВзыскание)
		|			ТОГДА ""ПереданоНаВзыскание""
		|		КОГДА НДФЛРасчетыСБюджетом.ВидСтроки = ЗНАЧЕНИЕ(Перечисление.НДФЛРасчетыСБюджетомВидСтроки.ВозвратНалога)
		|			ТОГДА ""Возвращено""
		|		ИНАЧЕ """"
		|	КОНЕЦ КАК ВидСтроки,
		|	ВЫБОР
		|		КОГДА НДФЛРасчетыСБюджетом.ВидСтроки = ЗНАЧЕНИЕ(Перечисление.НДФЛРасчетыСБюджетомВидСтроки.Начисление)
		|			ТОГДА МЕСЯЦ(НДФЛРасчетыСБюджетом.МесяцНалоговогоПериода)
		|		КОГДА НДФЛРасчетыСБюджетом.ВидСтроки = ЗНАЧЕНИЕ(Перечисление.НДФЛРасчетыСБюджетомВидСтроки.Удержание)
		|			ТОГДА ВЫБОР
		|					КОГДА НДФЛРасчетыСБюджетом.Период < &НачалоНП
		|						ТОГДА 1
		|					КОГДА НДФЛРасчетыСБюджетом.Период > &КонецНП
		|						ТОГДА 12
		|					ИНАЧЕ МЕСЯЦ(НДФЛРасчетыСБюджетом.Период)
		|				КОНЕЦ
		|		КОГДА НДФЛРасчетыСБюджетом.ВидСтроки = ЗНАЧЕНИЕ(Перечисление.НДФЛРасчетыСБюджетомВидСтроки.ПереданоНаВзыскание)
		|			ТОГДА МЕСЯЦ(НДФЛРасчетыСБюджетом.Период)
		|		КОГДА НДФЛРасчетыСБюджетом.ВидСтроки = ЗНАЧЕНИЕ(Перечисление.НДФЛРасчетыСБюджетомВидСтроки.ВозвратНалога)
		|			ТОГДА ВЫБОР
		|					КОГДА НДФЛРасчетыСБюджетом.Период < &НачалоНП
		|						ТОГДА 1
		|					КОГДА НДФЛРасчетыСБюджетом.Период > &КонецНП
		|						ТОГДА 12
		|					ИНАЧЕ МЕСЯЦ(НДФЛРасчетыСБюджетом.Период)
		|				КОНЕЦ
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК Месяц,
		|	НДФЛРасчетыСБюджетом.СтавкаНалогообложенияРезидента КАК Ставка,
		|	ВЫБОР
		|		КОГДА НДФЛРасчетыСБюджетом.ВидСтроки = ЗНАЧЕНИЕ(Перечисление.НДФЛРасчетыСБюджетомВидСтроки.ВозвратНалога)
		|			ТОГДА -НДФЛРасчетыСБюджетом.Налог
		|		ИНАЧЕ НДФЛРасчетыСБюджетом.Налог
		|	КОНЕЦ КАК Налог,
		|	НДФЛРасчетыСБюджетом.КодДохода
		|ПОМЕСТИТЬ ВТДанныеОНалогах
		|ИЗ
		|	РегистрНакопления.НДФЛРасчетыСБюджетом КАК НДФЛРасчетыСБюджетом
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТСписокФизЛицОрганизация КАК СписокФизЛицОрганизация
		|		ПО НДФЛРасчетыСБюджетом.ФизЛицо = СписокФизЛицОрганизация.ФизЛицо
		|			И (ГОД(НДФЛРасчетыСБюджетом.МесяцНалоговогоПериода) = СписокФизЛицОрганизация.Год)
		|			И НДФЛРасчетыСБюджетом.Организация = СписокФизЛицОрганизация.ГоловнаяОрганизация
		|ГДЕ
		|	НДФЛРасчетыСБюджетом.МесяцНалоговогоПериода МЕЖДУ &НачалоНП И &КонецНП
		|	И ВЫБОР
		|			КОГДА НДФЛРасчетыСБюджетом.КодПоОКАТО <> &ПустойКодПоОКАТО
		|				ТОГДА НДФЛРасчетыСБюджетом.КодПоОКАТО
		|			КОГДА ЕСТЬNULL(НДФЛРасчетыСБюджетом.ПодразделениеОрганизации.КодПоОКАТО, &ПустойКодПоОКАТО) <> &ПустойКодПоОКАТО
		|				ТОГДА НДФЛРасчетыСБюджетом.ПодразделениеОрганизации.КодПоОКАТО
		|			ИНАЧЕ ЕСТЬNULL(НДФЛРасчетыСБюджетом.ОбособленноеПодразделение.КодПоОКАТО, &ПустойКодПоОКАТО)
		|		КОНЕЦ = &КодПоОКАТО
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ФизЛицо,
		|	ГОД
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДанныеОНалогах.ГоловнаяОрганизация КАК ГоловнаяОрганизация,
		|	ДанныеОНалогах.ФизЛицо КАК ФизЛицо,
		|	ДанныеОНалогах.ГОД КАК ГОД,
		|	ДанныеОНалогах.Месяц,
		|	ВЫБОР
		|		КОГДА ДанныеОНалогах.ГОД > 2007
		|				И ДанныеОНалогах.КодДохода = ЗНАЧЕНИЕ(Справочник.ДоходыНДФЛ.Код1010)
		|			ТОГДА ""Раздел4""
		|		КОГДА ДанныеОНалогах.Ставка = ЗНАЧЕНИЕ(Перечисление.НДФЛСтавкиНалогообложенияРезидента.Ставка09)
		|			ТОГДА ""Раздел4""
		|		КОГДА ЕСТЬNULL(ГражданствоФизЛицСрезПоследних.НеЯвляетсяНалоговымРезидентомРФ, ЛОЖЬ)
		|				ИЛИ ДанныеОНалогах.Ставка = ЗНАЧЕНИЕ(Перечисление.НДФЛСтавкиНалогообложенияРезидента.Ставка13)
		|			ТОГДА ""Раздел3""
		|		ИНАЧЕ ""Раздел5""
		|	КОНЕЦ КАК РАЗДЕЛ,
		|	СУММА(ВЫБОР
		|			КОГДА ДанныеОНалогах.ВидСтроки = ""Начислено""
		|				ТОГДА ДанныеОНалогах.Налог
		|			ИНАЧЕ 0
		|		КОНЕЦ) КАК Начислено,
		|	СУММА(ВЫБОР
		|			КОГДА ДанныеОНалогах.ВидСтроки = ""Удержано""
		|				ТОГДА ДанныеОНалогах.Налог
		|			ИНАЧЕ 0
		|		КОНЕЦ) КАК Удержано,
		|	СУММА(ВЫБОР
		|			КОГДА ДанныеОНалогах.ВидСтроки = ""ПереданоНаВзыскание""
		|				ТОГДА ДанныеОНалогах.Налог
		|			ИНАЧЕ 0
		|		КОНЕЦ) КАК ПереданоНаВзыскание,
		|	СУММА(ВЫБОР
		|			КОГДА ДанныеОНалогах.ВидСтроки = ""Возвращено""
		|				ТОГДА ДанныеОНалогах.Налог
		|			ИНАЧЕ 0
		|		КОНЕЦ) КАК Возвращено
		|ИЗ
		|	ВТДанныеОНалогах КАК ДанныеОНалогах
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСтатусФизЛиц КАК ГражданствоФизЛицСрезПоследних
		|		ПО ДанныеОНалогах.ФизЛицо = ГражданствоФизЛицСрезПоследних.ФизЛицо
		|			И ДанныеОНалогах.ГОД = ГражданствоФизЛицСрезПоследних.Год
		|
		|СГРУППИРОВАТЬ ПО
		|	ДанныеОНалогах.Месяц,
		|	ДанныеОНалогах.ГоловнаяОрганизация,
		|	ДанныеОНалогах.ФизЛицо,
		|	ДанныеОНалогах.ГОД,
		|	ВЫБОР
		|		КОГДА ДанныеОНалогах.ГОД > 2007
		|				И ДанныеОНалогах.КодДохода = ЗНАЧЕНИЕ(Справочник.ДоходыНДФЛ.Код1010)
		|			ТОГДА ""Раздел4""
		|		КОГДА ДанныеОНалогах.Ставка = ЗНАЧЕНИЕ(Перечисление.НДФЛСтавкиНалогообложенияРезидента.Ставка09)
		|			ТОГДА ""Раздел4""
		|		КОГДА ЕСТЬNULL(ГражданствоФизЛицСрезПоследних.НеЯвляетсяНалоговымРезидентомРФ, ЛОЖЬ)
		|				ИЛИ ДанныеОНалогах.Ставка = ЗНАЧЕНИЕ(Перечисление.НДФЛСтавкиНалогообложенияРезидента.Ставка13)
		|			ТОГДА ""Раздел3""
		|		ИНАЧЕ ""Раздел5""
		|	КОНЕЦ
		|ИТОГИ ПО
		|	ГоловнаяОрганизация,
		|	ФизЛицо,
		|	ГОД,
		|	РАЗДЕЛ";
		
		//СальдоРасчетовНаНачалоГода 
		// Описание: выбирается сальдо по формуле
		//	Сальдо = (приходы – расходы без учета строк ПереданоНаВзыскание по регистру НДФЛРасчетыСБюджетом по измерению МесяцНалоговогоПериода < начало налогового периода) – (оборот по виду строки ПереданоНаВзыскание по свойству Период < начало налогового периода)
		
		СальдоРасчетовНаНачалоГодаТекст = 
		"ВЫБРАТЬ
		|	НДФЛРасчетыСБюджетом.ФизЛицо КАК ФизЛицо,
		|	НДФЛРасчетыСБюджетом.Организация КАК ГоловнаяОрганизация,
		|	Года.Ссылка КАК Ставка,
		|	ЕСТЬNULL(СУММА(ВЫБОР
		|				КОГДА НДФЛРасчетыСБюджетом.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|					ТОГДА НДФЛРасчетыСБюджетом.Налог
		|				ИНАЧЕ -НДФЛРасчетыСБюджетом.Налог
		|			КОНЕЦ), 0) КАК Сальдо,
		|	ГОД(Года.Год) КАК Год
		|ИЗ
		|	(ВЫБРАТЬ РАЗЛИЧНЫЕ
		|		НАЧАЛОПЕРИОДА(РегламентированныйПроизводственныйКалендарь.Период, ГОД) КАК Год,
		|		НДФЛСтавкиНалогообложенияРезидента.Ссылка КАК Ссылка
		|	ИЗ
		|		ВТПериоды КАК РегламентированныйПроизводственныйКалендарь,
		|		Перечисление.НДФЛСтавкиНалогообложенияРезидента КАК НДФЛСтавкиНалогообложенияРезидента
		|	ГДЕ
		|		РегламентированныйПроизводственныйКалендарь.Период МЕЖДУ &НачалоНП И &КонецНП) КАК Года
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.НДФЛРасчетыСБюджетом КАК НДФЛРасчетыСБюджетом
		|		ПО Года.Ссылка = НДФЛРасчетыСБюджетом.СтавкаНалогообложенияРезидента
		|			И Года.Год > НДФЛРасчетыСБюджетом.МесяцНалоговогоПериода
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТСписокФизЛицОрганизация КАК СписокФизЛицОрганизация
		|		ПО (НДФЛРасчетыСБюджетом.ФизЛицо = СписокФизЛицОрганизация.ФизЛицо)
		|			И (ГОД(Года.Год) = СписокФизЛицОрганизация.Год)
		|			И (НДФЛРасчетыСБюджетом.Организация = СписокФизЛицОрганизация.ГоловнаяОрганизация)
		|ГДЕ
		|	ВЫБОР
		|			КОГДА НДФЛРасчетыСБюджетом.КодПоОКАТО <> &ПустойКодПоОКАТО
		|				ТОГДА НДФЛРасчетыСБюджетом.КодПоОКАТО
		|			КОГДА ЕСТЬNULL(НДФЛРасчетыСБюджетом.ПодразделениеОрганизации.КодПоОКАТО, &ПустойКодПоОКАТО) <> &ПустойКодПоОКАТО
		|				ТОГДА НДФЛРасчетыСБюджетом.ПодразделениеОрганизации.КодПоОКАТО
		|			ИНАЧЕ ЕСТЬNULL(НДФЛРасчетыСБюджетом.ОбособленноеПодразделение.КодПоОКАТО, &ПустойКодПоОКАТО)
		|		КОНЕЦ = &КодПоОКАТО
		|
		|СГРУППИРОВАТЬ ПО
		|	НДФЛРасчетыСБюджетом.Организация,
		|	НДФЛРасчетыСБюджетом.ФизЛицо,
		|	Года.Год,
		|	Года.Ссылка";
		
		
		//-----------------------------------------------------------------------------
		// Предоставленные стандартные вычеты
		
		//ПраваНаВычетыТекст
		//	Поля:
		//		Период
		//		КодВычета
		//      ПримененныйВычет
		//
		// Описание: за все месяцы налогового периода выбираем фактически предоставленные стандартные вычеты
		
		ПраваНаВычетыТекст = 
		"ВЫБРАТЬ
		|	НДФЛРасчетыСБюджетом.ФизЛицо КАК ФизЛицо,
		|	НДФЛРасчетыСБюджетом.Организация КАК ГоловнаяОрганизация,
		|	МЕСЯЦ(НДФЛРасчетыСБюджетом.МесяцНалоговогоПериода) КАК Месяц,
		|	ГОД(НДФЛРасчетыСБюджетом.МесяцНалоговогоПериода) КАК ГОД,
		|	НДФЛРасчетыСБюджетом.КодВычета,
		|	ВЫБОР
		|		КОГДА ГОД(НДФЛРасчетыСБюджетом.МесяцНалоговогоПериода) < 2008
		|			ТОГДА НДФЛРасчетыСБюджетом.КодВычета.КодДляОтчетности2007
		|		КОГДА ГОД(НДФЛРасчетыСБюджетом.МесяцНалоговогоПериода) = 2008
		|			ТОГДА НДФЛРасчетыСБюджетом.КодВычета.КодДляОтчетности2008
		|		ИНАЧЕ НДФЛРасчетыСБюджетом.КодВычета.КодДляОтчетности2009
		|	КОНЕЦ КАК КодВычетаКод,
		|	ВЫБОР
		|		КОГДА ГОД(НДФЛРасчетыСБюджетом.МесяцНалоговогоПериода) > 2008
		|			ТОГДА ВЫБОР
		|					КОГДА НДФЛРасчетыСБюджетом.КодВычета = ЗНАЧЕНИЕ(Справочник.ВычетыНДФЛ.Код101)
		|						ТОГДА 1
		|					КОГДА НДФЛРасчетыСБюджетом.КодВычета = ЗНАЧЕНИЕ(Справочник.ВычетыНДФЛ.Код102)
		|						ТОГДА 2
		|					КОГДА НДФЛРасчетыСБюджетом.КодВычета = ЗНАЧЕНИЕ(Справочник.ВычетыНДФЛ.Код106)
		|						ТОГДА 6
		|					КОГДА НДФЛРасчетыСБюджетом.КодВычета = ЗНАЧЕНИЕ(Справочник.ВычетыНДФЛ.Код107)
		|						ТОГДА 7
		|					КОГДА НДФЛРасчетыСБюджетом.КодВычета = ЗНАЧЕНИЕ(Справочник.ВычетыНДФЛ.Код103)
		|						ТОГДА 3
		|					КОГДА НДФЛРасчетыСБюджетом.КодВычета = ЗНАЧЕНИЕ(Справочник.ВычетыНДФЛ.Код104)
		|						ТОГДА 4
		|					КОГДА НДФЛРасчетыСБюджетом.КодВычета = ЗНАЧЕНИЕ(Справочник.ВычетыНДФЛ.Код105)
		|						ТОГДА 5
		|					КОГДА НДФЛРасчетыСБюджетом.КодВычета = ЗНАЧЕНИЕ(Справочник.ВычетыНДФЛ.Код111)
		|						ТОГДА 8
		|					КОГДА НДФЛРасчетыСБюджетом.КодВычета = ЗНАЧЕНИЕ(Справочник.ВычетыНДФЛ.Код113)
		|						ТОГДА 9
		|					КОГДА НДФЛРасчетыСБюджетом.КодВычета = ЗНАЧЕНИЕ(Справочник.ВычетыНДФЛ.Код319)
		|						ТОГДА 90
		|					ИНАЧЕ -1
		|				КОНЕЦ
		|		КОГДА НДФЛРасчетыСБюджетом.КодВычета = ЗНАЧЕНИЕ(Справочник.ВычетыНДФЛ.Код101)
		|			ТОГДА 1
		|		КОГДА НДФЛРасчетыСБюджетом.КодВычета = ЗНАЧЕНИЕ(Справочник.ВычетыНДФЛ.Код102)
		|			ТОГДА 2
		|		КОГДА НДФЛРасчетыСБюджетом.КодВычета = ЗНАЧЕНИЕ(Справочник.ВычетыНДФЛ.Код106)
		|			ТОГДА 6
		|		КОГДА НДФЛРасчетыСБюджетом.КодВычета = ЗНАЧЕНИЕ(Справочник.ВычетыНДФЛ.Код107)
		|			ТОГДА 7
		|		КОГДА НДФЛРасчетыСБюджетом.КодВычета = ЗНАЧЕНИЕ(Справочник.ВычетыНДФЛ.Код103)
		|			ТОГДА 3
		|		КОГДА НДФЛРасчетыСБюджетом.КодВычета = ЗНАЧЕНИЕ(Справочник.ВычетыНДФЛ.Код104)
		|			ТОГДА 4
		|		КОГДА НДФЛРасчетыСБюджетом.КодВычета = ЗНАЧЕНИЕ(Справочник.ВычетыНДФЛ.Код105)
		|			ТОГДА 5
		|		ИНАЧЕ -1
		|	КОНЕЦ КАК НомерСтрокиВычета,
		|	ЕСТЬNULL(СУММА(НДФЛРасчетыСБюджетом.ПримененныйВычет), 0) КАК ПримененныйВычет
		|ИЗ
		|	РегистрНакопления.НДФЛПредоставленныеСтандартныеВычетыФизЛиц КАК НДФЛРасчетыСБюджетом
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТСписокФизЛицОрганизация КАК СписокФизЛицОрганизация
		|		ПО НДФЛРасчетыСБюджетом.ФизЛицо = СписокФизЛицОрганизация.ФизЛицо
		|			И (ГОД(НДФЛРасчетыСБюджетом.МесяцНалоговогоПериода) = СписокФизЛицОрганизация.Год)
		|			И НДФЛРасчетыСБюджетом.Организация = СписокФизЛицОрганизация.ГоловнаяОрганизация
		|ГДЕ
		|	НДФЛРасчетыСБюджетом.МесяцНалоговогоПериода МЕЖДУ &НачалоНП И &КонецНП
		|	И ВЫБОР
		|			КОГДА НДФЛРасчетыСБюджетом.КодПоОКАТО <> &ПустойКодПоОКАТО
		|				ТОГДА НДФЛРасчетыСБюджетом.КодПоОКАТО
		|			КОГДА ЕСТЬNULL(НДФЛРасчетыСБюджетом.ПодразделениеОрганизации.КодПоОКАТО, &ПустойКодПоОКАТО) <> &ПустойКодПоОКАТО
		|				ТОГДА НДФЛРасчетыСБюджетом.ПодразделениеОрганизации.КодПоОКАТО
		|			ИНАЧЕ ЕСТЬNULL(НДФЛРасчетыСБюджетом.ОбособленноеПодразделение.КодПоОКАТО, &ПустойКодПоОКАТО)
		|		КОНЕЦ = &КодПоОКАТО
		|
		|СГРУППИРОВАТЬ ПО
		|	МЕСЯЦ(НДФЛРасчетыСБюджетом.МесяцНалоговогоПериода),
		|	ГОД(НДФЛРасчетыСБюджетом.МесяцНалоговогоПериода),
		|	НДФЛРасчетыСБюджетом.ФизЛицо,
		|	НДФЛРасчетыСБюджетом.Организация,
		|	НДФЛРасчетыСБюджетом.КодВычета,
		|	ВЫБОР
		|		КОГДА ГОД(НДФЛРасчетыСБюджетом.МесяцНалоговогоПериода) > 2008
		|			ТОГДА ВЫБОР
		|					КОГДА НДФЛРасчетыСБюджетом.КодВычета = ЗНАЧЕНИЕ(Справочник.ВычетыНДФЛ.Код101)
		|						ТОГДА 1
		|					КОГДА НДФЛРасчетыСБюджетом.КодВычета = ЗНАЧЕНИЕ(Справочник.ВычетыНДФЛ.Код102)
		|						ТОГДА 2
		|					КОГДА НДФЛРасчетыСБюджетом.КодВычета = ЗНАЧЕНИЕ(Справочник.ВычетыНДФЛ.Код106)
		|						ТОГДА 6
		|					КОГДА НДФЛРасчетыСБюджетом.КодВычета = ЗНАЧЕНИЕ(Справочник.ВычетыНДФЛ.Код107)
		|						ТОГДА 7
		|					КОГДА НДФЛРасчетыСБюджетом.КодВычета = ЗНАЧЕНИЕ(Справочник.ВычетыНДФЛ.Код103)
		|						ТОГДА 3
		|					КОГДА НДФЛРасчетыСБюджетом.КодВычета = ЗНАЧЕНИЕ(Справочник.ВычетыНДФЛ.Код104)
		|						ТОГДА 4
		|					КОГДА НДФЛРасчетыСБюджетом.КодВычета = ЗНАЧЕНИЕ(Справочник.ВычетыНДФЛ.Код105)
		|						ТОГДА 5
		|					КОГДА НДФЛРасчетыСБюджетом.КодВычета = ЗНАЧЕНИЕ(Справочник.ВычетыНДФЛ.Код111)
		|						ТОГДА 8
		|					КОГДА НДФЛРасчетыСБюджетом.КодВычета = ЗНАЧЕНИЕ(Справочник.ВычетыНДФЛ.Код113)
		|						ТОГДА 9
		|					КОГДА НДФЛРасчетыСБюджетом.КодВычета = ЗНАЧЕНИЕ(Справочник.ВычетыНДФЛ.Код319)
		|						ТОГДА 90
		|					ИНАЧЕ -1
		|				КОНЕЦ
		|		КОГДА НДФЛРасчетыСБюджетом.КодВычета = ЗНАЧЕНИЕ(Справочник.ВычетыНДФЛ.Код101)
		|			ТОГДА 1
		|		КОГДА НДФЛРасчетыСБюджетом.КодВычета = ЗНАЧЕНИЕ(Справочник.ВычетыНДФЛ.Код102)
		|			ТОГДА 2
		|		КОГДА НДФЛРасчетыСБюджетом.КодВычета = ЗНАЧЕНИЕ(Справочник.ВычетыНДФЛ.Код106)
		|			ТОГДА 6
		|		КОГДА НДФЛРасчетыСБюджетом.КодВычета = ЗНАЧЕНИЕ(Справочник.ВычетыНДФЛ.Код107)
		|			ТОГДА 7
		|		КОГДА НДФЛРасчетыСБюджетом.КодВычета = ЗНАЧЕНИЕ(Справочник.ВычетыНДФЛ.Код103)
		|			ТОГДА 3
		|		КОГДА НДФЛРасчетыСБюджетом.КодВычета = ЗНАЧЕНИЕ(Справочник.ВычетыНДФЛ.Код104)
		|			ТОГДА 4
		|		КОГДА НДФЛРасчетыСБюджетом.КодВычета = ЗНАЧЕНИЕ(Справочник.ВычетыНДФЛ.Код105)
		|			ТОГДА 5
		|		ИНАЧЕ -1
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА ГОД(НДФЛРасчетыСБюджетом.МесяцНалоговогоПериода) < 2008
		|			ТОГДА НДФЛРасчетыСБюджетом.КодВычета.КодДляОтчетности2007
		|		КОГДА ГОД(НДФЛРасчетыСБюджетом.МесяцНалоговогоПериода) = 2008
		|			ТОГДА НДФЛРасчетыСБюджетом.КодВычета.КодДляОтчетности2008
		|		ИНАЧЕ НДФЛРасчетыСБюджетом.КодВычета.КодДляОтчетности2009
		|	КОНЕЦ
		|
		|УПОРЯДОЧИТЬ ПО
		|	ГоловнаяОрганизация,
		|	ФизЛицо,
		|	ГОД,
		|	КодВычетаКод,
		|	Месяц";
		
		// ИмущественныеВычетыТекст
		//
		// Описание: выбирает суммы предоставленных имущественных вычетов по указанной организации
		
		ИмущественныеВычетыТекст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	МЕСЯЦ(МесяцыНалоговогоПериода.Период) КАК МесяцНП,
		|	НДФЛИмущественныеВычетыФизлиц.ГоловнаяОрганизация КАК ГоловнаяОрганизация,
		|	НДФЛИмущественныеВычетыФизлиц.ФизЛицо КАК ФизЛицо,
		|	НДФЛИмущественныеВычетыФизлиц.ГОД КАК ГОД,
		|	НДФЛИмущественныеВычетыФизлиц.КодВычетаИмущественный,
		|	СУММА(ЕСТЬNULL(НДФЛИмущественныеВычетыФизлиц.Размер, 0)) КАК РазмерВычета
		|ИЗ
		|	ВТПериоды КАК МесяцыНалоговогоПериода
		|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
		|			НДФЛИмущественныеВычетыФизлиц.ФизЛицо КАК ФизЛицо,
		|			НДФЛИмущественныеВычетыФизлиц.Год КАК ГОД,
		|			НДФЛИмущественныеВычетыФизлиц.Организация КАК ГоловнаяОрганизация,
		|			НДФЛИмущественныеВычетыФизлиц.КодВычетаИмущественный КАК КодВычетаИмущественный,
		|			КОНЕЦПЕРИОДА(НДФЛИмущественныеВычетыФизлиц.Период, МЕСЯЦ) КАК Период,
		|			СУММА(НДФЛИмущественныеВычетыФизлиц.Размер) КАК Размер
		|		ИЗ
		|			РегистрНакопления.НДФЛИмущественныеВычетыФизлиц КАК НДФЛИмущественныеВычетыФизлиц
		|				ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТСписокФизЛицОрганизация КАК СписокФизЛицОрганизация
		|				ПО НДФЛИмущественныеВычетыФизлиц.ФизЛицо = СписокФизЛицОрганизация.ФизЛицо
		|					И НДФЛИмущественныеВычетыФизлиц.Год = СписокФизЛицОрганизация.Год
		|					И НДФЛИмущественныеВычетыФизлиц.Организация = СписокФизЛицОрганизация.ГоловнаяОрганизация
		|		ГДЕ
		|			НДФЛИмущественныеВычетыФизлиц.Период МЕЖДУ &НачалоНП И &КонецНП
		|			И ВЫБОР
		|					КОГДА НДФЛИмущественныеВычетыФизлиц.КодПоОКАТО <> &ПустойКодПоОКАТО
		|						ТОГДА НДФЛИмущественныеВычетыФизлиц.КодПоОКАТО
		|					КОГДА ЕСТЬNULL(НДФЛИмущественныеВычетыФизлиц.ПодразделениеОрганизации.КодПоОКАТО, &ПустойКодПоОКАТО) <> &ПустойКодПоОКАТО
		|						ТОГДА НДФЛИмущественныеВычетыФизлиц.ПодразделениеОрганизации.КодПоОКАТО
		|					ИНАЧЕ ЕСТЬNULL(НДФЛИмущественныеВычетыФизлиц.ОбособленноеПодразделение.КодПоОКАТО, &ПустойКодПоОКАТО)
		|				КОНЕЦ = &КодПоОКАТО
		|		
		|		СГРУППИРОВАТЬ ПО
		|			НДФЛИмущественныеВычетыФизлиц.ФизЛицо,
		|			НДФЛИмущественныеВычетыФизлиц.Год,
		|			НДФЛИмущественныеВычетыФизлиц.Организация,
		|			НДФЛИмущественныеВычетыФизлиц.КодВычетаИмущественный,
		|			КОНЕЦПЕРИОДА(НДФЛИмущественныеВычетыФизлиц.Период, МЕСЯЦ)) КАК НДФЛИмущественныеВычетыФизлиц
		|		ПО МесяцыНалоговогоПериода.Период = НДФЛИмущественныеВычетыФизлиц.Период
		|
		|СГРУППИРОВАТЬ ПО
		|	НДФЛИмущественныеВычетыФизлиц.ГоловнаяОрганизация,
		|	НДФЛИмущественныеВычетыФизлиц.ФизЛицо,
		|	НДФЛИмущественныеВычетыФизлиц.ГОД,
		|	НДФЛИмущественныеВычетыФизлиц.КодВычетаИмущественный,
		|	МЕСЯЦ(МесяцыНалоговогоПериода.Период)
		|
		|УПОРЯДОЧИТЬ ПО
		|	ГоловнаяОрганизация,
		|	ФизЛицо,
		|	ГОД,
		|	МесяцНП";
		
	КонецЕсли;
	
	//-----------------------------------------------------------------------------
	// ВЫПОЛНЕНИЕ ЗАПРОСОВ
	
	// Сведения о физлице и организации
	Запрос.Текст = ДанныеОбОрганизацииИФизлицеТекст;
	ДанныеОФизЛицеОрганизации  = Запрос.Выполнить().Выбрать();
	
	// параметр  ФизЛицо_Резидент нужен для выполнения последующих запросов
	//Запрос.УстановитьПараметр("ФизЛицо_Резидент", ДанныеОФизЛицеОрганизации.ФизЛицо_Резидент);
	
	// Различные коды вычетов, действующие в течение года. Нужны для хвоста третьего раздела.
	Запрос.Текст = СписокРазличныхКодовСтандартныхВычетовФизЛицаЗаГодТекст;
	ТаблицаРазличныхКодовСтандартныхВычетовФизЛиц = Запрос.Выполнить().Выгрузить();	
	
	// Доходы по кодам и по месяцам налогового периода для всех разделов карточки
	Запрос.Текст = ДоходыПоМесяцамПоВидамТекст;     
	РезультатЗапросаДоходыПоМесяцамПоВидам = Запрос.Выполнить();
	ВыборкаДоходыПоМесяцамПоВидам = РезультатЗапросаДоходыПоМесяцамПоВидам.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	// Вычеты по кодам и по месяцам налогового периода для всех разделов карточки
	Запрос.Текст = ВычетыПоМесяцамПоВидамТекст;
	ВыборкаВычетыПоМесяцамПоВидам = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Запрос.Текст = ПраваНаВычетыТекст;      
	ВыборкаСтандартныхВычетовФизЛица = Запрос.Выполнить().Выбрать();
	
	// Имущественные вычеты
	Запрос.Текст = ИмущественныеВычетыТекст;      
	ВыборкаИмущественныеВычеты = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	//  Начисленные, удержанные, переданные на взыскание, возвращенные налоги для всех разделов по месяцам
	Запрос.Текст = НалогиПомесячноПоСтавкамТекст;    
	ВыборкаНалогиПомесяцамПоСтавкам = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	//  Сальдо по налогу на начало году по ставкам
	Запрос.Текст = СальдоРасчетовНаНачалоГодаТекст;   
	ВыборкаСальдоРасчетовНаНачалоГода = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	// Номера и даты справок о доходах для 9-го раздела
	Запрос.Текст = НомераИДатыПредоставленныхСправокОДоходахТекст;
	ДанныеОПредоставленныхСправках  = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам); 
	
КонецПроцедуры
 
Процедура ПолучитьСпискиФизЛицИСотрудников(СтрокиДеревоДанных, СписокФизЛиц)
	
	Для Каждого СтрокаДанных из СтрокиДеревоДанных Цикл
		Если СтрокаДанных.ФизЛицо <> Неопределено И СтрокаДанных.Организация <> Неопределено тогда
			
			СтрокаСписка = СписокФизЛиц.Добавить();
			СтрокаСписка.ФизЛицо     = СтрокаДанных.ФизЛицо;
			СтрокаСписка.Организация = СтрокаДанных.Организация;
			СтрокаСписка.ГоловнаяОрганизация = СтрокаДанных.ГоловнаяОрганизация;
			СтрокаСписка.Год = Год(СтрокаДанных.Год);
			
		ИначеЕсли СтрокаДанных.Строки.Количество() > 0 Тогда
			
			ПолучитьСпискиФизЛицИСотрудников(СтрокаДанных.Строки, СписокФизЛиц);
			
		КонецЕсли;
	КонецЦикла;
		
КонецПроцедуры

Функция СформироватьОтчет(ДокументРезультат, ЕстьОшибки = Неопределено, ВнешниеНаборыДанных = Неопределено) Экспорт
	
	ЗначениеПанелипользователя = ТиповыеОтчеты.ПолучитьЗначенияНастроекПанелиПользователяОбъекта(ЭтотОбъект);
	НастрокаПоУмолчанию        = КомпоновщикНастроек.ПолучитьНастройки();
	ТиповыеОтчеты.ПолучитьПримененуюНастройку(ЭтотОбъект);
	
	Группировка = ТиповыеОтчеты.ДобавитьГруппировку(КомпоновщикНастроек, "");
	Группировка.ПоляГруппировки.Элементы.Очистить();
	
	ТиповыеОтчеты.ДобавитьВыбранноеПоле(КомпоновщикНастроек, "ГоловнаяОрганизация");
	ТиповыеОтчеты.ДобавитьВыбранноеПоле(КомпоновщикНастроек, "Организация");
	ТиповыеОтчеты.ДобавитьВыбранноеПоле(КомпоновщикНастроек, "ГОД");
	ТиповыеОтчеты.ДобавитьВыбранноеПоле(КомпоновщикНастроек, "ФизЛицо");
	
	ЭлементСортировки = КомпоновщикНастроек.Настройки.Порядок.Элементы.Добавить(Тип("ЭлементПорядкаКомпоновкиДанных"));
	ЭлементСортировки.Использование = Истина;
	ЭлементСортировки.Поле = Новый ПолеКомпоновкиДанных("ФизЛицо");
	
	ДокументРезультат.Очистить();
	ДокументРезультат.НачатьАвтогруппировкуСтрок();
		
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
	
	ЗначениеПараметра = КомпоновщикНастроек.Настройки.ПараметрыДанных.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных("Период"));
	Если ЗначениеПараметра <> Неопределено тогда
		Если ЗначениеПараметра.Значение <> '00010101' тогда
			ДатаАктуальности = ЗначениеПараметра.Значение;
		Иначе
			ДатаАктуальности           = ОбщегоНазначения.ПолучитьРабочуюДату();
			ЗначениеПараметра.Значение = ОбщегоНазначения.ПолучитьРабочуюДату();
		КонецЕсли;
	Иначе
		ДатаАктуальности = ОбщегоНазначения.ПолучитьРабочуюДату();
	КонецЕсли;
	
	КодПоОКАТО    = ТиповыеОтчеты.ПолучитьПараметр(КомпоновщикНастроек, "КодПоОКАТО").Значение;
	Ответственный = ТиповыеОтчеты.ПолучитьПараметр(КомпоновщикНастроек, "Ответственный").Значение;
	ПервыйРаз     = истина;
	
	МакетКомпоновки = КомпоновщикМакета.Выполнить(СхемаКомпоновкиДанных, КомпоновщикНастроек.Настройки, , , Тип("ГенераторМакетаКомпоновкиДанныхДляКоллекцииЗначений"));
	
	ПроцессорКомпоновки = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновки.Инициализировать(МакетКомпоновки, , , Истина);
	
	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВКоллекциюЗначений;
	
	ДеревоДанных =  Новый ДеревоЗначений;
	
	ПроцессорВывода.УстановитьОбъект(ДеревоДанных);
	ПроцессорВывода.Вывести(ПроцессорКомпоновки);
	
	МассивТипов = Новый Массив(2);
	МассивТипов[0]= Тип("Null");
	СписокФизЛицОрганизация = Новый ТаблицаЗначений;
	МассивТипов[1]= Тип("СправочникСсылка.ФизическиеЛица");
	СписокФизЛицОрганизация.Колонки.Добавить("ФизЛицо", Новый ОписаниеТипов(МассивТипов));
	МассивТипов[1]= Тип("СправочникСсылка.Организации");
	СписокФизЛицОрганизация.Колонки.Добавить("ГоловнаяОрганизация", Новый ОписаниеТипов(МассивТипов));
	СписокФизЛицОрганизация.Колонки.Добавить("Организация", Новый ОписаниеТипов(МассивТипов));
	МассивТипов[1]= Тип("Число");
	СписокФизЛицОрганизация.Колонки.Добавить("Год", Новый ОписаниеТипов(МассивТипов));
	
	
	ПолучитьСпискиФизЛицИСотрудников(ДеревоДанных.Строки, СписокФизЛицОрганизация);
	Группировки = ТиповыеОтчеты.ПолучитьПоляГруппировок(КомпоновщикНастроек);
	Уровень = 0;
	
	ПолучитьДанныеДляПостроенияОтчета(СписокФизЛицОрганизация);
	
	ВывестиКарточки(ДокументРезультат, ДеревоДанных.Строки, Уровень, Группировки, ЕстьОшибки);
	
	КомпоновщикНастроек.ЗагрузитьНастройки(НастрокаПоУмолчанию);
	Возврат ДокументРезультат;
	
КонецФункции
	
// Заполняет табличный документ
// в соответствии с настройками, заданными значениями реквизитов отчета.
//
// Параметры:
//	ДокументРезультат - табличный документ, формируемый отчетом,
//
Процедура СформироватьКарточку1НДФЛ(ДокументРезультат, ФизЛицо, Год, Организация, ГоловнаяОрганизация, Уровень) Экспорт

	Ответственный = Справочники.Пользователи.ПустаяСсылка();

	Если НЕ ПервыйРаз тогда
		ДокументРезультат.ВывестиГоризонтальныйРазделительСтраниц();
	Иначе
		ПервыйРаз = ложь;
	КонецЕсли;
	                  
	НалоговыйПериод = Год(Год);
	Если НалоговыйПериод < 2009 Тогда
		Макет =	ПолучитьМакет("Форма1НДФЛ");
		Если НалоговыйПериод <= 2005 Тогда
		ИначеЕсли НалоговыйПериод >= 2008 Тогда
			Макет.Область("R118C2").Текст = "По ставке 9 (15)%";
		Иначе
	        Макет.Область("R118C2").Текст = "По ставке 9%";
		КонецЕсли;
	Иначе
		Макет =	ПолучитьМакет("Форма1НДФЛ_2009");
		Макет.Область("R120C2").Текст = "По ставке 9 (15)%";
	КонецЕсли;

	//-----------------------------------------------------------------------------
	// ЗАПОЛНЕНИЕ ФОРМЫ
	
	// Переменные оформления
	ЦветПодсветкиНеверноИсчисленногоНалога = Новый Цвет(0,0,255);
	ЦветЧерный = Новый Цвет(0,0,0);
	
	// Области-разделы
    Раздел1 = Макет.ПолучитьОбласть("Раздел1");									    
	Раздел2 = Макет.ПолучитьОбласть("Раздел2");									    
	Раздел3 = Макет.ПолучитьОбласть("Раздел3");									    
	Раздел4 = Макет.ПолучитьОбласть("Раздел4");									    
	Раздел6 = Макет.ПолучитьОбласть("Раздел6");									    
	Раздел7 = Макет.ПолучитьОбласть("Раздел7");									    
	Раздел8 = Макет.ПолучитьОбласть("Раздел8");									    
	Раздел9 = Макет.ПолучитьОбласть("Раздел9");									    
	// Каждый вид доходов по ставке 35% должен отражаться в отдельном пятом разделе
	МассивРазделов5 = Новый Массив;
	// Добавим сразу в массив один раздел, т.к. даже если не будет доходов по ставке 35%, то все равно надо будет показать один пустой пятый раздел
	МассивРазделов5.Добавить(Макет.ПолучитьОбласть("Раздел5"));
	
	//-----------------------------------------------------------------------------                                 
	// РАЗДЕЛ 1 и 2
	
	СтруктураОтбора = Новый Структура("ФизЛицо, Год, ГоловнаяОрганизация", ФизЛицо, НалоговыйПериод, ГоловнаяОрганизация);
	
	ДанныеОФизЛицеОрганизации.Сбросить();
	
	СписокРазличныхКодовСтандартныхВычетовФизЛицаЗаГод = ТаблицаРазличныхКодовСтандартныхВычетовФизЛиц.Скопировать(ТаблицаРазличныхКодовСтандартныхВычетовФизЛиц.НайтиСтроки(СтруктураОтбора)).ВыгрузитьКолонку("КодВычета"); 
	
	Если ДанныеОФизЛицеОрганизации.НайтиСледующий(СтруктураОтбора) тогда
		
		// Заполнение данных раздела 1
		Раздел1.Параметры.ГодНП = "" + Формат(Год, "ЧГ=0; ДФ=yyyy");
		Раздел1.Параметры.ИНН = "" + ДанныеОФизЛицеОрганизации.Организация_ИНН +"\" + ДанныеОФизЛицеОрганизации.Организация_КПП;
		Раздел1.Параметры.КодИМНС = ДанныеОФизЛицеОрганизации.Организация_КодИМНС;
		Раздел1.Параметры.КодОкато = ?(СводноПоКодамОКАТО,ДанныеОФизЛицеОрганизации.Организация_КодПоОКАТО,КодПоОКАТО);
		Раздел1.Параметры.НазваниеОрганизации = ДанныеОФизЛицеОрганизации.Организация_Наименование;
		
		// Заполнение данных раздела 2
		Раздел2.Параметры.ИНН = ДанныеОФизЛицеОрганизации.ФизЛицо_ИНН;
		Раздел2.Параметры.СтраховойНомерПФР = ДанныеОФизЛицеОрганизации.ФизЛицо_СтраховойНомерПФР;
		Раздел2.Параметры.ФИО = ДанныеОФизЛицеОрганизации.ФизЛицо_ФИО;
		Раздел2.Параметры.КодДокумента = ДанныеОФизЛицеОрганизации.ФизЛицо_КодДокументаУдЛичн;
		Раздел2.Параметры.СерияНомерДокумента = ДанныеОФизЛицеОрганизации.ФизЛицо_СерияНомерДокументаУдЛичн;
		Раздел2.Параметры.ДатаРождения = Формат(ДанныеОФизЛицеОрганизации.ФизЛицо_ДатаРождения, "ДФ=dd.MM.yyyy");
		Раздел2.Параметры.КодСтраны = ДанныеОФизЛицеОрганизации.ФизЛицо_КодСтраны;
		Раздел2.Параметры.СтранаПроживания = ДанныеОФизЛицеОрганизации.ФизЛицо_АдресКодСтраны;
		Раздел2.Параметры.Индекс = ДанныеОФизЛицеОрганизации.ФизЛицо_АдресИндекс;
		Раздел2.Параметры.Регион = ПроцедурыУправленияПерсоналом.ПолучитьКодРегионаРФИзКодаАдресногоЭлемента(ДанныеОФизЛицеОрганизации.ФизЛицо_АдресРегион);
		Раздел2.Параметры.Район = ДанныеОФизЛицеОрганизации.ФизЛицо_АдресРайон;
		Раздел2.Параметры.Город = ДанныеОФизЛицеОрганизации.ФизЛицо_АдресГород;
		Раздел2.Параметры.НаселенныйПункт = ДанныеОФизЛицеОрганизации.ФизЛицо_АдресНаселенныйПункт;
		Раздел2.Параметры.Улица = ДанныеОФизЛицеОрганизации.ФизЛицо_АдресУлица;
		Раздел2.Параметры.Дом = ДанныеОФизЛицеОрганизации.ФизЛицо_АдресДом;
		Раздел2.Параметры.Корпус = ДанныеОФизЛицеОрганизации.ФизЛицо_АдресКорпус;
		Раздел2.Параметры.Квартира = ДанныеОФизЛицеОрганизации.ФизЛицо_АдресКвартира;
		Раздел2.Параметры.Статус = ?(ДанныеОФизЛицеОрганизации.ФизЛицо_Резидент,1,2);
		Раздел2.Параметры.ЕстьВычеты = ?(СписокРазличныхКодовСтандартныхВычетовФизЛицаЗаГод.Количество()>0, "1", "2");
		ФизЛицо_Резидент = ДанныеОФизЛицеОрганизации.ФизЛицо_Резидент;
		
	КонецЕсли;
	
	//-----------------------------------------------------------------------------
	// Расчет последнего отображаемого месяца для нарастающих цифр по последнему месяцу дохода или налога 
	
	ПоследнийМесяцДоходовИНалогов = 1;
	
	// обходим налоги
	ВыборкаНалогиПомесяцамПоСтавкам.Сбросить();
	
	Если ВыборкаНалогиПомесяцамПоСтавкам.НайтиСледующий(ГоловнаяОрганизация, "ГоловнаяОрганизация") тогда
		ВыборкаФизЛиц  = ВыборкаНалогиПомесяцамПоСтавкам.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Если ВыборкаФизЛиц.НайтиСледующий(ФизЛицо, "ФизЛицо") тогда
			ВыборкаГод  = ВыборкаФизЛиц.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			Если ВыборкаГод.НайтиСледующий(НалоговыйПериод, "Год") тогда
				
				
				ВыборкаПоСтавкам = ВыборкаГод.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам); 
				Пока ВыборкаПоСтавкам.Следующий() Цикл
					ВыборкаМесяцев  = ВыборкаПоСтавкам.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
					Пока ВыборкаМесяцев.Следующий() Цикл
						ПоследнийМесяцДоходовИНалогов = Макс(ВыборкаМесяцев.Месяц, ПоследнийМесяцДоходовИНалогов);
					КонецЦикла;
				КонецЦикла;	
				
				
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Если ПоследнийМесяцДоходовИНалогов <> 12 Тогда
		// обходим доходы
		ВыборкаДоходыПоМесяцамПоВидам.Сбросить();
		
		Если ВыборкаДоходыПоМесяцамПоВидам.НайтиСледующий(ГоловнаяОрганизация, "ГоловнаяОрганизация") тогда
			ВыборкаФизЛиц  = ВыборкаДоходыПоМесяцамПоВидам.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			Если ВыборкаФизЛиц.НайтиСледующий(ФизЛицо, "ФизЛицо") тогда
				ВыборкаГод  = ВыборкаФизЛиц.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
				Если ВыборкаГод.НайтиСледующий(НалоговыйПериод, "Год") тогда
					ВыборкаДоходыПоМесяцамПоВидамВнутренняя = ВыборкаГод.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
					
					
					Пока ВыборкаДоходыПоМесяцамПоВидамВнутренняя.Следующий() Цикл
						ВыборкаДоходовРаздела  = ВыборкаДоходыПоМесяцамПоВидамВнутренняя.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
						Пока ВыборкаДоходовРаздела.Следующий() Цикл
							ВыборкаМесяцев  = ВыборкаДоходовРаздела.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
							Пока ВыборкаМесяцев.Следующий() Цикл
								ПоследнийМесяцДоходовИНалогов = Макс(ВыборкаМесяцев.Месяц, ПоследнийМесяцДоходовИНалогов);
							КонецЦикла;
						КонецЦикла;
					КонецЦикла;	
					
					
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	
	//-----------------------------------------------------------------------------
	// Заполнение данных раздела 3
	
	// признак резидента РФ
	ФизЛицо_Резидент = ДанныеОФизЛицеОрганизации.ФизЛицо_Резидент;

	// Переменные - итоги
	МесячныеДоходыБезВычетовРаздела3  = НовыйМассивЧисел(13);// сумма доходов по всем кодам доходов без вычетов помесячно (индекс первого месяца - 1)
	МесячныеСтандартныеВычеты  = НовыйМассивЧисел(13);// сумма стандартных вычетов по всем кодам вычетов помесячно
	
	// Сальдо по расчетам на начало года свернутое по всем ставкам
	СвернутоеСальдо  = 0;
	СоответствиеСальдоПоСтавкамНаНачалоГода = Новый Соответствие;
	ВыборкаСальдоРасчетовНаНачалоГода.Сбросить();
	Пока ВыборкаСальдоРасчетовНаНачалоГода.НайтиСледующий(СтруктураОтбора) Цикл
		СвернутоеСальдо = СвернутоеСальдо  + ВыборкаСальдоРасчетовНаНачалоГода.Сальдо;
		// заполним структуру сальд на начало года для расчета параметров 6 раздела
		Если ФизЛицо_Резидент Тогда
			СоответствиеСальдоПоСтавкамНаНачалоГода.Вставить(ВыборкаСальдоРасчетовНаНачалоГода.Ставка, ВыборкаСальдоРасчетовНаНачалоГода.Сальдо);
		Иначе// для нерезидента всё положим на ставку 13
			БылоСальдо = СоответствиеСальдоПоСтавкамНаНачалоГода.Получить(Перечисления.НДФЛСтавкиНалогообложенияРезидента.Ставка13); 
			СоответствиеСальдоПоСтавкамНаНачалоГода.Вставить(Перечисления.НДФЛСтавкиНалогообложенияРезидента.Ставка13, ?(БылоСальдо = Неопределено, 0, БылоСальдо) + ВыборкаСальдоРасчетовНаНачалоГода.Сальдо);
		КонецЕсли; 
		
	КонецЦикла; 
	Если СвернутоеСальдо > 0 Тогда
		Раздел3.Параметры["ДолгЗаНалогоплательщиком"] = СвернутоеСальдо;
	Иначе	
		Раздел3.Параметры["ДолгЗаНалоговымАгентом"] = - СвернутоеСальдо;
	КонецЕсли; 
	
	// Доходы с предыдущего месиа работы
	Раздел3.Параметры.ДоходыПредыдущегоМестаРаботы = ДанныеОФизЛицеОрганизации.ФизЛицо_ДоходыПредыдущегоМестаРаботы;

	
	// Доходы 13% (или все доходы нерезидента) по видам и по месяцам
	
	// спозиционируемся на доходах раздела 3
	ВыборкаДоходыПоМесяцамПоВидам.Сбросить();
	Если ВыборкаДоходыПоМесяцамПоВидам.НайтиСледующий(ГоловнаяОрганизация, "ГоловнаяОрганизация") тогда
		ВыборкаФизЛиц  = ВыборкаДоходыПоМесяцамПоВидам.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Если ВыборкаФизЛиц.НайтиСледующий(ФизЛицо, "ФизЛицо") тогда
			ВыборкаГод  = ВыборкаФизЛиц.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			Если ВыборкаГод.НайтиСледующий(НалоговыйПериод, "Год") тогда
				ВыборкаДоходыПоМесяцамПоВидамВнутренняя = ВыборкаГод.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
				Если ВыборкаДоходыПоМесяцамПоВидамВнутренняя.НайтиСледующий(Новый Структура("ГоловнаяОрганизация, Год, ФизЛицо, Раздел", ГоловнаяОрганизация, НалоговыйПериод, ФизЛицо,"Раздел3")) тогда
					// выберем доходы раздела
					НомерДохода = 0;
					ВыборкаДоходовРаздела = ВыборкаДоходыПоМесяцамПоВидамВнутренняя.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
					Пока ВыборкаДоходовРаздела.Следующий() Цикл
						НомерДохода = НомерДохода + 1;
						
						Если НомерДохода <= 10 Тогда
							Раздел3.Параметры["КодДохода" + НомерДохода] = ВыборкаДоходовРаздела.КодДохода;// код дохода
							Раздел3.Параметры["Доход" + НомерДохода + "_Итого"] = ВыборкаДоходовРаздела.СуммаДохода;// доход за год
							
							ВыборкаМесяцев  = ВыборкаДоходовРаздела.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
							Пока ВыборкаМесяцев.Следующий() Цикл
								Месяц = ВыборкаМесяцев.Месяц;
								Раздел3.Параметры["Доход" + НомерДохода + "_" + Месяц] = ВыборкаМесяцев.СуммаДохода;// доход за месяц по коду дохода
								// собираем суммы доходов без вычетов по всем видам доходов 
								МесячныеДоходыБезВычетовРаздела3[Месяц] = МесячныеДоходыБезВычетовРаздела3[Месяц] + ВыборкаМесяцев.СуммаДохода - ВыборкаМесяцев.СуммаВычета;
							КонецЦикла; 
						Иначе	
							Сообщить("Ошибка вывода данных (Раздел 3): форма не позволяет отразить более 10кодов дохода!");
						КонецЕсли; 
						
					КонецЦикла; 
				КонецЕсли;	
				
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	// Вычеты к 13% доходам (или все вычеты нерезидента) по видам и по месяцам
	НомерВычета = 0;
	ВыборкаВычетыПоМесяцамПоВидам.Сбросить();
	Если ВыборкаВычетыПоМесяцамПоВидам.НайтиСледующий(ГоловнаяОрганизация, "ГоловнаяОрганизация") тогда
		ВыборкаФизЛиц  = ВыборкаВычетыПоМесяцамПоВидам.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Если ВыборкаФизЛиц.НайтиСледующий(ФизЛицо, "ФизЛицо") тогда
			ВыборкаГод  = ВыборкаФизЛиц.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			Если ВыборкаГод.НайтиСледующий(НалоговыйПериод, "Год") тогда
				ВыборкаВычетыПоМесяцамПоВидамВнтреняя = ВыборкаГод.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
				
				Пока ВыборкаВычетыПоМесяцамПоВидамВнтреняя.Следующий() Цикл
					НомерВычета = НомерВычета + 1;
					Если НомерВычета <= 5 Тогда
						Раздел3.Параметры["КодВычета" + НомерВычета] = "Код "+ ВыборкаВычетыПоМесяцамПоВидамВнтреняя.КодВычета;// код вычета
						Раздел3.Параметры["Вычет" + НомерВычета + "_Итого"] = ВыборкаВычетыПоМесяцамПоВидамВнтреняя.СуммаВычета;// вычет за год
						
						ВыборкаМесяцев  = ВыборкаВычетыПоМесяцамПоВидамВнтреняя.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
						Пока ВыборкаМесяцев.Следующий() Цикл
							Месяц = ВыборкаМесяцев.Месяц;
							Раздел3.Параметры["Вычет" + НомерВычета + "_" + Месяц] = ВыборкаМесяцев.СуммаВычета;// вычет за месяц по коду вычета
						КонецЦикла; 
					Иначе	
						Сообщить("Ошибка вывода данных (раздел3): форма не позволяет отразить более 5 кодов вычетов!");
					КонецЕсли; 
				КонецЦикла; 
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	// Переносим в документ совокупные доходы без вычетов в разрезе месяцев и совокупный нарастающий доход без вычетов 
	НарастающийДоходБезВычетов = 0;
	Для Месяц = 1 По ПоследнийМесяцДоходовИНалогов Цикл
		Раздел3.Параметры["МДБезВ_" + Месяц] = МесячныеДоходыБезВычетовРаздела3[Месяц];	
		НарастающийДоходБезВычетов = НарастающийДоходБезВычетов  + МесячныеДоходыБезВычетовРаздела3[Месяц];
		Раздел3.Параметры["ГДБезВ_" + Месяц] = НарастающийДоходБезВычетов;	
	КонецЦикла; 
	Раздел3.Параметры["МДБезВ_Итого"] = НарастающийДоходБезВычетов;	
	Раздел3.Параметры["ГДБезВ_Итого"] = НарастающийДоходБезВычетов;	
	
	// Стандартные вычеты (коды 101-113)
	
	СоответствиеВычетов = Новый Соответствие;
	СоциальныйВычет = НовыйМассивЧисел(13);
	
	// выборка стандартных вычетов не имеет итогов
	ВыборкаСтандартныхВычетовФизЛица.Сбросить();
	
	Пока ВыборкаСтандартныхВычетовФизЛица.НайтиСледующий(СтруктураОтбора) Цикл
		
		Месяц = ВыборкаСтандартныхВычетовФизЛица.Месяц;
		
		Если ЗначениеЗаполнено(ВыборкаСтандартныхВычетовФизЛица.КодВычета) Тогда
			НомерВычета = ВыборкаСтандартныхВычетовФизЛица.НомерСтрокиВычета;
			Если НомерВычета = 90 Тогда
				Если НалоговыйПериод >= 2009 Тогда
					Раздел3.Параметры["СоцВычет_" + Месяц] = ВыборкаСтандартныхВычетовФизЛица.ПримененныйВычет;// вычет за месяц по коду вычета
					СоциальныйВычет[Месяц] = ВыборкаСтандартныхВычетовФизЛица.ПримененныйВычет;
				КонецЕсли;
			ИначеЕсли НомерВычета > 0 Тогда
				СоответствиеВычетов.Вставить(НомерВычета, ?(СоответствиеВычетов[НомерВычета] = Неопределено, 0, СоответствиеВычетов[НомерВычета]) + ВыборкаСтандартныхВычетовФизЛица.ПримененныйВычет);
				Раздел3.Параметры["СВычет" + НомерВычета + "_" + Месяц] = ВыборкаСтандартныхВычетовФизЛица.ПримененныйВычет;// вычет за месяц по коду вычета
				// собираем суммы вычетов 
				МесячныеСтандартныеВычеты[Месяц] = МесячныеСтандартныеВычеты[Месяц] + ВыборкаСтандартныхВычетовФизЛица.ПримененныйВычет; 
				// Если право на вычет отличается от фактически предоставленного - отметим это
				Если СводноПоКодамОКАТО И ВыборкаСтандартныхВычетовФизЛица.ПримененныйВычет <> ВыборкаСтандартныхВычетовФизЛица.РазмерВычета Тогда
					Раздел3.Область("СВычет" + НомерВычета + "_" + Месяц).Примечание.Текст = "Фактическая сумма вычета не совпадает с правом на вычет:
					|	право на вычет = " + ПараметрВЧисло(ВыборкаСтандартныхВычетовФизЛица.РазмерВычета);
				КонецЕсли; 						
			Иначе	
				Сообщить("Ошибка вывода данных (раздел №3): указан непредусмотренный код стандартного налогового вычета - " + ВыборкаСтандартныхВычетовФизЛица.КодВычетаКод +"!");
			КонецЕсли;
		КонецЕсли;
		
	КонецЦикла; 
		
	Для каждого Элемент Из СоответствиеВычетов Цикл
		Если Не ЗначениеЗаполнено(Элемент.Значение) Тогда
			Продолжить;
		КонецЕсли; 
		НомерВычета = Элемент.Ключ;
		Раздел3.Параметры["СВычет" + НомерВычета + "_Итого"] = Элемент.Значение;// вычет за год
	КонецЦикла;
	
	// Итоги по стандартным вычетам (коды 101-113) нарастающим итогом
	НарастающиеСтандартныеВычеты = 0;
	НарастающиеСоциальныеВычеты = 0;
	Для Месяц = 1 По ПоследнийМесяцДоходовИНалогов Цикл
		НарастающиеСтандартныеВычеты = НарастающиеСтандартныеВычеты  + МесячныеСтандартныеВычеты[Месяц];
		Раздел3.Параметры["ГСВ_" + Месяц] = НарастающиеСтандартныеВычеты;
		Если НалоговыйПериод >= 2009 Тогда
			НарастающиеСоциальныеВычеты = НарастающиеСоциальныеВычеты + СоциальныйВычет[Месяц];
			Раздел3.Параметры["ГСоцВ_" + Месяц] = НарастающиеСоциальныеВычеты;
			МесячныеСтандартныеВычеты[Месяц] = МесячныеСтандартныеВычеты[Месяц] + СоциальныйВычет[Месяц];
		КонецЕсли;
	КонецЦикла; 
	Раздел3.Параметры["ГСВ_Итого"] = НарастающиеСтандартныеВычеты;	
	Если НалоговыйПериод >= 2009 Тогда
		Раздел3.Параметры["СоцВычет_Итого"] = НарастающиеСоциальныеВычеты;	
	КонецЕсли;
	
	// Имущественные вычеты
	// выборка имущественных вычетов сгруппирована по Месяц
	ВыборкаИмущественныеВычеты.Сбросить();
	НарастающиеИмущественныеВычеты = 0;
	НарастающийВычетКод311 = 0;
	НарастающийВычетКод312 = 0;
	Пока ВыборкаИмущественныеВычеты.НайтиСледующий(СтруктураОтбора) Цикл
		Месяц = ВыборкаИмущественныеВычеты.МесяцНП;
		Если Месяц > ПоследнийМесяцДоходовИНалогов Тогда
			Прервать
		КонецЕсли;
		НарастающиеИмущественныеВычеты = НарастающиеИмущественныеВычеты + ВыборкаИмущественныеВычеты.РазмерВычета;
		МесячныеСтандартныеВычеты[Месяц] = МесячныеСтандартныеВычеты[Месяц] + ВыборкаИмущественныеВычеты.РазмерВычета;
		Раздел3.Параметры["ГИВ_" + Месяц] = НарастающиеИмущественныеВычеты;
		Если ВыборкаИмущественныеВычеты.КодВычетаИмущественный = Справочники.ВычетыНДФЛ.Код311 Тогда
			Раздел3.Параметры["ИВычет1" + "_" + Месяц] = ВыборкаИмущественныеВычеты.РазмерВычета;// вычет за месяц по коду вычета
			НарастающийВычетКод311 = НарастающийВычетКод311 + ВыборкаИмущественныеВычеты.РазмерВычета;
		Иначе
			Раздел3.Параметры["ИВычет2" + "_" + Месяц] = ВыборкаИмущественныеВычеты.РазмерВычета;// вычет за месяц по коду вычета
			НарастающийВычетКод312 = НарастающийВычетКод312 + ВыборкаИмущественныеВычеты.РазмерВычета;
		КонецЕсли;
	КонецЦикла; 
	Раздел3.Параметры["ГИВ_Итого"] = НарастающиеИмущественныеВычеты;	
	Раздел3.Параметры["ИВычет1_Итого"] = НарастающийВычетКод311;	
	Раздел3.Параметры["ИВычет2_Итого"] = НарастающийВычетКод312;	
	
	// Налоговая база с начала года
	НарастающаяНалоговаяБаза = 0;
	Для Месяц = 1 По ПоследнийМесяцДоходовИНалогов Цикл
		Если ФизЛицо_Резидент Тогда
			НарастающаяНалоговаяБаза = НарастающаяНалоговаяБаза + МесячныеДоходыБезВычетовРаздела3[Месяц] - МесячныеСтандартныеВычеты[Месяц];
			Раздел3.Параметры["НБаза_" + Месяц] = Макс(НарастающаяНалоговаяБаза,0);	
		Иначе	
			// Для нерезидента налоговая база показывается не нарастающим итогом
			Раздел3.Параметры["НБаза_" + Месяц] = Макс(МесячныеДоходыБезВычетовРаздела3[Месяц],0);	
		КонецЕсли; 
	КонецЦикла; 
	Раздел3.Параметры["НБаза_Итого"] = Макс(НарастающаяНалоговаяБаза,0);	
	
	ЕстьВычетОдинарный = Ложь;
	ЕстьВычетДвойной = Ложь;
	ЕстьВычетУчетверенный = Ложь;
	Для каждого Вычет Из СписокРазличныхКодовСтандартныхВычетовФизЛицаЗаГод Цикл
	   Если Не ЕстьВычетОдинарный Тогда
	   	   ЕстьВычетОдинарный = Вычет = Справочники.ВычетыНДФЛ.Код101
	   КонецЕсли;
	   Если Не ЕстьВычетДвойной Тогда
	   	   ЕстьВычетДвойной = Вычет = Справочники.ВычетыНДФЛ.Код102 
		   				Или Вычет = Справочники.ВычетыНДФЛ.Код106
		   				Или Вычет = Справочники.ВычетыНДФЛ.Код111
	   КонецЕсли;
		
	   Если Не ЕстьВычетУчетверенный Тогда
	   	   ЕстьВычетУчетверенный = Вычет = Справочники.ВычетыНДФЛ.Код107 
		   				Или Вычет = Справочники.ВычетыНДФЛ.Код113
	   КонецЕсли;
	
	КонецЦикла;
	// Права на вычеты
	Раздел3.Параметры.ПравоНаВычетОдинарный		= ?(НалоговыйПериод > 2008,  "1000 руб", "600 руб") + ?(ЕстьВычетОдинарный, " - V", "");
	Раздел3.Параметры.ПравоНаВычетДвойной		= ?(НалоговыйПериод > 2008,  "2000 руб", "1200 руб") + ?(ЕстьВычетДвойной, " - V", "");
	Раздел3.Параметры.ПравоНаВычетУчетверенный	= ?(НалоговыйПериод > 2008,  "4000 руб", "2400 руб") + ?(ЕстьВычетУчетверенный, " - V", "");
	Раздел3.Параметры.ПравоНаВычет103 = "400 руб"  + ?(СписокРазличныхКодовСтандартныхВычетовФизЛицаЗаГод.Найти(Справочники.ВычетыНДФЛ.Код103) <> Неопределено, " - V", "");
	Раздел3.Параметры.ПравоНаВычет104 = "500 руб"  + ?(СписокРазличныхКодовСтандартныхВычетовФизЛицаЗаГод.Найти(Справочники.ВычетыНДФЛ.Код104) <> Неопределено, " - V", "");
	Раздел3.Параметры.ПравоНаВычет105 = "3000 руб" + ?(СписокРазличныхКодовСтандартныхВычетовФизЛицаЗаГод.Найти(Справочники.ВычетыНДФЛ.Код105) <> Неопределено, " - V", "");
	
	//-----------------------------------------------------------------------------
	// Раздел 4 - Дивиденды
	
	// Переменные - итоги
	// в принципе код доходов на дивиденды один (1010), но для универсальности заведем итоговые массивы
	МесячныеДоходыРаздела4  = НовыйМассивЧисел(13);// сумма доходов по всем кодам доходов помесячно (индекс первого месяца - 1)
	МесячныеВычетыРаздела4  = НовыйМассивЧисел(13);// сумма доходов по всем кодам доходов без вычетов помесячно (индекс первого месяца - 1)
	
	// Соберем доходы и вычеты по всем видам доходов 
	
	// спозиционируемся на доходах раздела 4
	ВыборкаДоходыПоМесяцамПоВидам.Сбросить();
	Если ВыборкаДоходыПоМесяцамПоВидам.НайтиСледующий(ГоловнаяОрганизация, "ГоловнаяОрганизация") тогда
		ВыборкаФизЛиц  = ВыборкаДоходыПоМесяцамПоВидам.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Если ВыборкаФизЛиц.НайтиСледующий(ФизЛицо, "ФизЛицо") тогда
			ВыборкаГод  = ВыборкаФизЛиц.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			Если ВыборкаГод.НайтиСледующий(НалоговыйПериод, "Год") тогда
				ВыборкаДоходыПоМесяцамПоВидамВнутренняя = ВыборкаГод.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
				
				Если ВыборкаДоходыПоМесяцамПоВидамВнутренняя.НайтиСледующий(Новый Структура("ГоловнаяОрганизация, Год, ФизЛицо, Раздел", ГоловнаяОрганизация, НалоговыйПериод, ФизЛицо,"Раздел4")) тогда
					ВыборкаДоходовРаздела = ВыборкаДоходыПоМесяцамПоВидамВнутренняя.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
					Пока ВыборкаДоходовРаздела.Следующий() Цикл
						ВыборкаМесяцев  = ВыборкаДоходовРаздела.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
						Пока ВыборкаМесяцев.Следующий() Цикл
							Месяц = ВыборкаМесяцев.Месяц;
							МесячныеДоходыРаздела4[Месяц] = МесячныеДоходыРаздела4[Месяц] + ВыборкаМесяцев.СуммаДохода;
							МесячныеВычетыРаздела4[Месяц] = МесячныеВычетыРаздела4[Месяц] + ВыборкаМесяцев.СуммаВычета;
						КонецЦикла; 
					КонецЦикла; 
				КонецЕсли;	
			КонецЕсли;	
		КонецЕсли;	
	КонецЕсли;	
	
	// Выведем доходы и облагаемую базу
	ИтогоДоходовРаздела4 = 0;
	ИтогоНалоговаяБазаРаздела4 = 0;
	Для М =1 По ПоследнийМесяцДоходовИНалогов Цикл
		Раздел4.Параметры["Доход_" + М] = МесячныеДоходыРаздела4[М];	
		Раздел4.Параметры["НБаза_" + М]  = Макс(МесячныеДоходыРаздела4[М] - МесячныеВычетыРаздела4[М],0);	
		ИтогоДоходовРаздела4 = ИтогоДоходовРаздела4 + МесячныеДоходыРаздела4[М];
		ИтогоНалоговаяБазаРаздела4 = ИтогоНалоговаяБазаРаздела4 + МесячныеДоходыРаздела4[М] - МесячныеВычетыРаздела4[М];
	КонецЦикла; 
	Раздел4.Параметры["Доход_Итого"] = ИтогоДоходовРаздела4;	
	Раздел4.Параметры["НБаза_Итого"] = ИтогоНалоговаяБазаРаздела4;	
	
	
	//-----------------------------------------------------------------------------
	// Раздел 5 - Доходы по ставке 35%
	
	// Отразим каждый код дохода в отдельном пятом разделе
	
	// спозиционируемся на доходах раздела 5
	ВыборкаДоходыПоМесяцамПоВидам.Сбросить();
	Если ВыборкаДоходыПоМесяцамПоВидам.НайтиСледующий(ГоловнаяОрганизация, "ГоловнаяОрганизация") тогда
		ВыборкаФизЛиц  = ВыборкаДоходыПоМесяцамПоВидам.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Если ВыборкаФизЛиц.НайтиСледующий(ФизЛицо, "ФизЛицо") тогда
			ВыборкаГод  = ВыборкаФизЛиц.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			Если ВыборкаГод.НайтиСледующий(НалоговыйПериод, "Год") тогда
				ВыборкаДоходыПоМесяцамПоВидамВнутренняя = ВыборкаГод.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
				Если ВыборкаДоходыПоМесяцамПоВидамВнутренняя.НайтиСледующий(Новый Структура("ГоловнаяОрганизация, Год, ФизЛицо, Раздел", ГоловнаяОрганизация, НалоговыйПериод, ФизЛицо,"Раздел5")) тогда
					
					НомерДохода = 0;
					
					ВыборкаДоходовРаздела = ВыборкаДоходыПоМесяцамПоВидамВнутренняя.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
					Пока ВыборкаДоходовРаздела.Следующий() Цикл
						
						НомерДохода = НомерДохода + 1;
						Если НомерДохода > 1 Тогда // В массиве областей раздела 5 первый элемент всегда присутствует
							МассивРазделов5.Добавить(Макет.ПолучитьОбласть("Раздел5"));
						КонецЕсли; 
						
						ОбластьРаздела = МассивРазделов5[НомерДохода - 1]; 
						ОбластьРаздела.Параметры["КодДохода"] 		= ВыборкаДоходовРаздела.КодДохода;	
						ОбластьРаздела.Параметры["Доход_Итого"] 	= ВыборкаДоходовРаздела.СуммаДохода;	
						ОбластьРаздела.Параметры["Вычет_Итого"]  	= ВыборкаДоходовРаздела.СуммаВычета;	
						ОбластьРаздела.Параметры["НБаза_Итого"]  	= ВыборкаДоходовРаздела.СуммаДохода - ВыборкаДоходовРаздела.СуммаВычета;	
						
						ВыборкаМесяцев  = ВыборкаДоходовРаздела.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
						Пока ВыборкаМесяцев.Следующий() Цикл
							Месяц = ВыборкаМесяцев.Месяц;
							ОбластьРаздела.Параметры["Доход_" + Месяц] 	= ВыборкаМесяцев.СуммаДохода;	
							ОбластьРаздела.Параметры["Вычет_" + Месяц]  = ВыборкаМесяцев.СуммаВычета;	
							ОбластьРаздела.Параметры["НБаза_" + Месяц]  = Макс(ВыборкаМесяцев.СуммаДохода - ВыборкаМесяцев.СуммаВычета,0);	
						КонецЦикла; 
						
					КонецЦикла; 
				КонецЕсли;	
			КонецЕсли;	
		КонецЕсли;	
	КонецЕсли;	
	//-----------------------------------------------------------------------------
	// Раздел 7 - Доходы, налогообложение которых осуществляется налоговыми  органами
	
	// Отразим суммы доходов
	
	// спозиционируемся на доходах раздела 7
	ВыборкаДоходыПоМесяцамПоВидам.Сбросить();
	Если ВыборкаДоходыПоМесяцамПоВидам.НайтиСледующий(ГоловнаяОрганизация, "ГоловнаяОрганизация") тогда
		ВыборкаФизЛиц  = ВыборкаДоходыПоМесяцамПоВидам.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Если ВыборкаФизЛиц.НайтиСледующий(ФизЛицо, "ФизЛицо") тогда
			ВыборкаГод  = ВыборкаФизЛиц.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			Если ВыборкаГод.НайтиСледующий(НалоговыйПериод, "Год") тогда
				ВыборкаДоходыПоМесяцамПоВидамВнутренняя = ВыборкаГод.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
				Если ВыборкаДоходыПоМесяцамПоВидамВнутренняя.НайтиСледующий(Новый Структура("ГоловнаяОрганизация, Год, ФизЛицо, Раздел", ГоловнаяОрганизация, НалоговыйПериод, ФизЛицо,"Раздел7")) тогда
					НомерДохода = 0;
					ВыборкаДоходовРаздела = ВыборкаДоходыПоМесяцамПоВидамВнутренняя.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
					Пока ВыборкаДоходовРаздела.Следующий() Цикл
						НомерДохода = НомерДохода + 1;
						
						Если НомерДохода <=3 Тогда
							Раздел7.Параметры["КодДохода"+НомерДохода] = ВыборкаДоходовРаздела.КодДохода;	
							Раздел7.Параметры["Доход" + НомерДохода + "_Итого"]	= ВыборкаДоходовРаздела.СуммаДохода;	
							
							ВыборкаМесяцев  = ВыборкаДоходовРаздела.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
							Пока ВыборкаМесяцев.Следующий() Цикл
								Месяц = ВыборкаМесяцев.Месяц;
								Раздел7.Параметры["Доход" + НомерДохода + "_" + Месяц] 	= ВыборкаМесяцев.СуммаДохода;	
							КонецЦикла; 
						Иначе	
							Сообщить("Ошибка вывода данных (Раздел 7): форма не позволяет отразить более 3 кодов дохода!");
						КонецЕсли; 
					КонецЦикла; 
				КонецЕсли;	
			КонецЕсли;	
		КонецЕсли;	
	КонецЕсли;	
	
	//-----------------------------------------------------------------------------
	// Одновременный вывод налогов по разделам 3,4,5: Начисленный, удержанный, переданный на взыскание, возвращенный, сальдо
	
	ВыборкаНалогиПомесяцамПоСтавкам.Сбросить();
	Если ВыборкаНалогиПомесяцамПоСтавкам.НайтиСледующий(ГоловнаяОрганизация, "ГоловнаяОрганизация") тогда
		ВыборкаФизЛиц  = ВыборкаНалогиПомесяцамПоСтавкам.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Если ВыборкаФизЛиц.НайтиСледующий(ФизЛицо, "ФизЛицо") тогда
			ВыборкаГод  = ВыборкаФизЛиц.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			Если ВыборкаГод.НайтиСледующий(НалоговыйПериод, "Год") тогда
				ВыборкаНалогиПомесяцамПоСтавкамВнутренняя = ВыборкаГод.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
				
				Пока ВыборкаНалогиПомесяцамПоСтавкамВнутренняя.НайтиСледующий(СтруктураОтбора) Цикл
					
					// Начисленный и удержанный налог отражаем нарастающим итогом только в 3 разделе для резидентов
					Раздел = ВыборкаНалогиПомесяцамПоСтавкамВнутренняя.Раздел;
					Если Раздел = "Раздел3" Тогда
						НомерРаздела = 3;
						ОбластьРаздела = Раздел3;
						СтавкаНалога = ?(ФизЛицо_Резидент, ПроцедурыУправленияПерсоналом.ЗначениеСтавкиНДФЛотСтавкиНалогообложенияРезидента(Перечисления.НДФЛСтавкиНалогообложенияРезидента.Ставка13), ПроцедурыУправленияПерсоналом.ЗначениеСтавкиНДФЛСНерезидента());
					ИначеЕсли Раздел = "Раздел4" Тогда 	
						НомерРаздела = 4;
						ОбластьРаздела = Раздел4;
						СтавкаНалога = ?(ФизЛицо_Резидент, ПроцедурыУправленияПерсоналом.ЗначениеСтавкиНДФЛотСтавкиНалогообложенияРезидента(Перечисления.НДФЛСтавкиНалогообложенияРезидента.Ставка09),ПроцедурыУправленияПерсоналом.ЗначениеСтавкиНДФЛСНерезидента(Справочники.ДоходыНДФЛ.Код1010,ДатаКонцаНП));
					Иначе	
						НомерРаздела = 5;
						ОбластьРаздела = МассивРазделов5[0]; // Все налоги покажем для первого 35% дохода
						СтавкаНалога = ПроцедурыУправленияПерсоналом.ЗначениеСтавкиНДФЛотСтавкиНалогообложенияРезидента(Перечисления.НДФЛСтавкиНалогообложенияРезидента.Ставка35);
					КонецЕсли; 
					
					// Переложим данные из выборки в массивы
					НалогИсчисленный = НовыйМассивЧисел(13);
					НалогУдержанный = НовыйМассивЧисел(13);
					НалогИсчисленныйНарастающимИтогом = НовыйМассивЧисел(13);
					НалогУдержанныйНарастающимИтогом = НовыйМассивЧисел(13);
					НалогВозвращенный = НовыйМассивЧисел(13);
					НалогВзысканный = НовыйМассивЧисел(13);
					ВыборкаМесяцев  = ВыборкаНалогиПомесяцамПоСтавкамВнутренняя.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
					Пока ВыборкаМесяцев.Следующий() Цикл
						Месяц = ВыборкаМесяцев.Месяц;
						НалогИсчисленный[Месяц] = ВыборкаМесяцев.Начислено;
						НалогУдержанный[Месяц] = ВыборкаМесяцев.Удержано;
						НалогИсчисленныйНарастающимИтогом[Месяц] = ВыборкаМесяцев.Начислено;
						НалогУдержанныйНарастающимИтогом[Месяц] = ВыборкаМесяцев.Удержано;
						НалогВзысканный[Месяц] = ВыборкаМесяцев.ПереданоНаВзыскание;
						НалогВозвращенный[Месяц] = ВыборкаМесяцев.Возвращено;
					КонецЦикла;
					
					// для подсчета сальдо по расчетам необходимо всегда использовать
					// даные нарастающим итогом
					ПреобразоватьМассивВНарастающий(НалогИсчисленныйНарастающимИтогом);
					ПреобразоватьМассивВНарастающий(НалогУдержанныйНарастающимИтогом);
					
					// Если надо, преобразуем массивы в нарастающие
					Если Раздел = "Раздел3" И ФизЛицо_Резидент Тогда
						ПреобразоватьМассивВНарастающий(НалогИсчисленный);
						ПреобразоватьМассивВНарастающий(НалогУдержанный);
						ПреобразоватьМассивВНарастающий(НалогВзысканный);
						ПреобразоватьМассивВНарастающий(НалогВозвращенный);
					КонецЕсли; 
					
					// Вывод данных в карточку
					Для М = 1 По ПоследнийМесяцДоходовИНалогов Цикл
						
						ОбластьРаздела.Параметры["НИ_" + М] 	= НалогИсчисленный[М];
						
						// Рассчитаем сумму налога от базы по ставке
						НалогИсчисленныйПоБазе = Окр(ПараметрВЧисло(ОбластьРаздела.Параметры["НБаза_" + М]) * СтавкаНалога/100,0);
						
						// Если налог исчислен неверно, то выделим цветом
						Если СводноПоКодамОКАТО И НалогИсчисленный[М]  <> НалогИсчисленныйПоБазе Тогда
							ЦветТекстаЯчейки =  ЦветПодсветкиНеверноИсчисленногоНалога;
							ОбластьРаздела.Области["Р"+ НомерРаздела + "НИ_" + М].Примечание.Текст = "Неверно исчислена сумма налога:
							|	Налоговая база = " + ПараметрВЧисло(ОбластьРаздела.Параметры["НБаза_" + М]) + "
							|	Ставка налога = " + СтавкаНалога + "%
							|	Налог исчисленный = " + НалогИсчисленныйПоБазе;
						Иначе	
							ЦветТекстаЯчейки =  ЦветЧерный;
						КонецЕсли; 						
						ОбластьРаздела.Области["Р" + НомерРаздела + "НИ_" + М].ЦветТекста = ЦветТекстаЯчейки;
						
						
						ОбластьРаздела.Параметры["НУ_" + М] 	= НалогУдержанный[М];
						ОбластьРаздела.Параметры["Взыск_" + М] 	= НалогВзысканный[М];
						ОбластьРаздела.Параметры["Возврат_" + М]= НалогВозвращенный[М];
						
						Сальдо = НалогИсчисленныйНарастающимИтогом[М] -  НалогУдержанныйНарастающимИтогом[М];
						Если Сальдо > 0 Тогда
							ОбластьРаздела.Параметры["ДолгНП_" + М] = Сальдо; // долг за налогоплательщиком
						Иначе
							ОбластьРаздела.Параметры["ДолгНА_" + М] = - Сальдо; // долг за налоговым агентом
						КонецЕсли; 
					КонецЦикла; 
					
					// Итоги по строкам
					ОбластьРаздела.Параметры["НИ_Итого"] = ВыборкаНалогиПомесяцамПоСтавкамВнутренняя.Начислено;
					ОбластьРаздела.Параметры["НУ_Итого"] = ВыборкаНалогиПомесяцамПоСтавкамВнутренняя.Удержано;
					ОбластьРаздела.Параметры["Взыск_Итого"] = ВыборкаНалогиПомесяцамПоСтавкамВнутренняя.ПереданоНаВзыскание;
					ОбластьРаздела.Параметры["Возврат_Итого"] = ВыборкаНалогиПомесяцамПоСтавкамВнутренняя.Возвращено;
					Сальдо = ВыборкаНалогиПомесяцамПоСтавкамВнутренняя.Начислено -  ВыборкаНалогиПомесяцамПоСтавкамВнутренняя.Удержано;
					Если Сальдо > 0 Тогда
						ОбластьРаздела.Параметры["ДолгНП_Итого"] = Сальдо; // долг за налогоплательщиком
					Иначе
						ОбластьРаздела.Параметры["ДолгНА_Итого"] = - Сальдо; // долг за налоговым агентом
					КонецЕсли; 
				КонецЦикла; 
			КонецЕсли;	
		КонецЕсли;	
	КонецЕсли;	
	
	//-----------------------------------------------------------------------------
	// Раздел 6
	
	ИтогоНачислено = 0;
	ИтогоУдержано = 0;
	ИтогоПереданоНаВзыскание = 0;
	ИтогоСальдо = 0;
	
	Для Н = 1 по 3 Цикл
		
		Если Н = 1 Тогда
			СуффиксСтроки = "06";
			ОбластьРаздела = Раздел4;
			СальдоНаНачалоГода 	= СоответствиеСальдоПоСтавкамНаНачалоГода.Получить(Перечисления.НДФЛСтавкиНалогообложенияРезидента.Ставка09);
		ИначеЕсли Н = 2 Тогда
			СуффиксСтроки = ?(ФизЛицо_Резидент,"13","30");
			ОбластьРаздела = Раздел3;
			СальдоНаНачалоГода 	= СоответствиеСальдоПоСтавкамНаНачалоГода.Получить(Перечисления.НДФЛСтавкиНалогообложенияРезидента.Ставка13);
		Иначе	
			СуффиксСтроки = "35";
			ОбластьРаздела = МассивРазделов5[0];
			СальдоНаНачалоГода 	= СоответствиеСальдоПоСтавкамНаНачалоГода.Получить(Перечисления.НДФЛСтавкиНалогообложенияРезидента.Ставка35);
		КонецЕсли; 
		
		// Расчет параметров
		НачисленоЗаГод				= ПараметрВЧисло(ОбластьРаздела.Параметры["НИ_Итого"]);
		УдержаноЗаГод 				= ПараметрВЧисло(ОбластьРаздела.Параметры["НУ_Итого"]);
		ПереданоНаВзысканиеЗаГод 	= ПараметрВЧисло(ОбластьРаздела.Параметры["Взыск_Итого"]);
		ВозвращеноЗаГод 			= ПараметрВЧисло(ОбластьРаздела.Параметры["Возврат_Итого"]);
		
		СальдоНаКонецГода 			= ПараметрВЧисло(СальдоНаНачалоГода) + (НачисленоЗаГод - УдержаноЗаГод + ВозвращеноЗаГод) - ПереданоНаВзысканиеЗаГод;
		
		// Переписываем в таблицу
		Раздел6.Параметры["НИ_Ст" + СуффиксСтроки] = НачисленоЗаГод;
		// в колонке удержано показываем за минусом возврата 
		Раздел6.Параметры["НУ_Ст" + СуффиксСтроки] = УдержаноЗаГод - ВозвращеноЗаГод;
		Раздел6.Параметры["Взыск_Ст" + СуффиксСтроки] =  ПереданоНаВзысканиеЗаГод;
		Если СальдоНаКонецГода > 0 Тогда
			Раздел6.Параметры["ДолгНП_Ст" + СуффиксСтроки] =  СальдоНаКонецГода;
		Иначе	
			Раздел6.Параметры["ДолгНА_Ст" + СуффиксСтроки] =  - СальдоНаКонецГода;
		КонецЕсли; 
		
		// Расчет итогов по строкам
		ИтогоНачислено = ИтогоНачислено + НачисленоЗаГод;
		ИтогоУдержано = ИтогоУдержано + УдержаноЗаГод - ВозвращеноЗаГод;
		ИтогоПереданоНаВзыскание = ИтогоПереданоНаВзыскание + ПереданоНаВзысканиеЗаГод;
		ИтогоСальдо = ИтогоСальдо + СальдоНаКонецГода;
	КонецЦикла; 
	
	// вывод итогов по строкам раздела
	Раздел6.Параметры["НИ_Итого"] = ИтогоНачислено;
	Раздел6.Параметры["НУ_Итого"] = ИтогоУдержано;
	Раздел6.Параметры["Взыск_Итого"] =  ИтогоПереданоНаВзыскание;
	Если ИтогоСальдо > 0 Тогда
		Раздел6.Параметры["ДолгНП_Итого"] =  ИтогоСальдо;
	Иначе	
		Раздел6.Параметры["ДолгНА_Итого"] =  - ИтогоСальдо;
	КонецЕсли; 
	
	//-----------------------------------------------------------------------------
	// Раздел 9
	Раздел9.Параметры.ДатаПодписи = Формат(ДатаПодписи, "ДФ=dd.MM.yyyy");
	Раздел9.Параметры.ФИООтветственного = ДанныеОФизЛицеОрганизации.Ответственный_ФИО;
	Раздел9.Параметры.ДолжностьОтветственного = ДанныеОФизЛицеОрганизации.Ответственный_Должность;
	
	ДанныеОПредоставленныхСправках.Сбросить();
	Если ДанныеОПредоставленныхСправках.НайтиСледующий(ГоловнаяОрганизация, "ГоловнаяОрганизация") тогда
		ВыборкаФизЛиц  = ДанныеОПредоставленныхСправках.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Если ВыборкаФизЛиц.НайтиСледующий(ФизЛицо, "ФизЛицо") тогда
			ВыборкаГод  = ВыборкаФизЛиц.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			Если ВыборкаГод.НайтиСледующий(НалоговыйПериод, "Год") тогда
				
				Раздел9.Параметры.НомерСправкиВИМНС = Строка(ВыборкаГод.ВнешнийНомерСправкиВИМНС) + ?(ПустаяСтрока(ВыборкаГод.НомерСправкиВИМНС),"", " (" + ВыборкаГод.НомерСправкиВИМНС + ")") ;
				Раздел9.Параметры.ДатаСправкиВИМНС = Формат(ВыборкаГод.ДатаСправкиВИМНС, "ДФ=dd.MM.yyyy");
				Раздел9.Параметры.НомерСправкиДляФизЛица = Строка(ВыборкаГод.ВнешнийНомерСправкиДляФизЛица) + ?(ПустаяСтрока(ВыборкаГод.НомерСправкиДляФизЛица), "", " (" + ВыборкаГод.НомерСправкиДляФизЛица + ")");
				Раздел9.Параметры.ДатаСправкиДляФизЛица = Формат(ВыборкаГод.ДатаСправкиДляФизЛица, "ДФ=dd.MM.yyyy");
				Раздел9.Параметры.НомерСправкиНаВзыскание = Строка(ВыборкаГод.ВнешнийНомерСправкиНаВзыскание) + ?(ПустаяСтрока(ВыборкаГод.НомерСправкиНаВзыскание), "", " (" + ВыборкаГод.НомерСправкиНаВзыскание + ")");
				Раздел9.Параметры.ДатаСправкиНаВзыскание = Формат(ВыборкаГод.ДатаСправкиНаВзыскание, "ДФ=dd.MM.yyyy");
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	//Раздел3.Показать();
	
	//-----------------------------------------------------------------------------
	// Вывод страниц документа
	ДокументРезультат.Вывести(Раздел1);
	ДокументРезультат.Вывести(Раздел2);
	ДокументРезультат.ВывестиГоризонтальныйРазделительСтраниц();
	ДокументРезультат.Вывести(Раздел3);
	ДокументРезультат.ВывестиГоризонтальныйРазделительСтраниц();
	ДокументРезультат.Вывести(Раздел4);
	Для Каждого Раздел из МассивРазделов5  Цикл
		ДокументРезультат.Вывести(Раздел);
	КонецЦикла; 
	ДокументРезультат.ВывестиГоризонтальныйРазделительСтраниц();
	ДокументРезультат.Вывести(Раздел6);
	ДокументРезультат.Вывести(Раздел7);
	ДокументРезультат.Вывести(Раздел8);
	ДокументРезультат.Вывести(Раздел9);
	ДокументРезультат.Область(1,1).Примечание.Текст = " ";
	ДокументРезультат.Область(1,1).Примечание.Текст = "";

	//Параметры документа
	ДокументРезультат.Автомасштаб 			= 	Истина;
	ДокументРезультат.ОриентацияСтраницы 	= 	ОриентацияСтраницы.Ландшафт;
	ДокументРезультат.ТолькоПросмотр		= 	Истина;

КонецПроцедуры

// Настройка отчета при отработки расшифровки
Процедура Настроить(Отбор) Экспорт
	
	// Настройка отбора
	Для каждого ЭлементОтбора Из Отбор Цикл
		
		Если ТипЗнч(ЭлементОтбора) = Тип("ЭлементОтбораКомпоновкиДанных") Тогда
			ПолеОтбора = ЭлементОтбора.ЛевоеЗначение;
		Иначе
			ПолеОтбора = Новый ПолеКомпоновкиДанных(ЭлементОтбора.Поле);
		КонецЕсли;
		
		Если КомпоновщикНастроек.Настройки.ДоступныеПоляОтбора.НайтиПоле(ПолеОтбора) = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		НовыйЭлементОтбора = КомпоновщикНастроек.Настройки.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		Если ТипЗнч(ЭлементОтбора) = Тип("ЭлементОтбораКомпоновкиДанных") Тогда
			ЗаполнитьЗначенияСвойств(НовыйЭлементОтбора, ЭлементОтбора);
		Иначе
			НовыйЭлементОтбора.Использование  = Истина;
			НовыйЭлементОтбора.ЛевоеЗначение  = ПолеОтбора;
			Если ЭлементОтбора.Иерархия Тогда
				Если ТипЗнч(ЭлементОтбора.Значение) = Тип("СписокЗначений") Тогда
					НовыйЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.ВСпискеПоИерархии;
				Иначе
					НовыйЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.ВИерархии;
				КонецЕсли;
			Иначе
				Если ТипЗнч(ЭлементОтбора.Значение) = Тип("СписокЗначений") Тогда
					НовыйЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.ВСписке;
				Иначе
					НовыйЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
				КонецЕсли;
			КонецЕсли;
			
			НовыйЭлементОтбора.ПравоеЗначение = ЭлементОтбора.Значение;
			
		КонецЕсли;
				
	КонецЦикла;
	
	ТиповыеОтчеты.УдалитьДублиОтбора(КомпоновщикНастроек);
	
КонецПроцедуры

Процедура СохранитьНастройку() Экспорт

	СтруктураНастроек = ТиповыеОтчеты.ПолучитьСтруктуруПараметровТиповогоОтчета(ЭтотОбъект);
	СохранениеНастроек.СохранитьНастройкуОбъекта(СохраненнаяНастройка, СтруктураНастроек);
	
КонецПроцедуры

Процедура ПрименитьНастройку() Экспорт
	
	Схема = ТиповыеОтчеты.ПолучитьСхемуКомпоновкиОбъекта(ЭтотОбъект);

	// Считываение структуры настроек отчета
 	Если Не СохраненнаяНастройка.Пустая() Тогда
		
		СтруктураНастроек = СохраненнаяНастройка.ХранилищеНастроек.Получить();
		Если Не СтруктураНастроек = Неопределено Тогда
			КомпоновщикНастроек.ЗагрузитьНастройки(СтруктураНастроек.НастройкиКомпоновщика);
			ЗаполнитьЗначенияСвойств(ЭтотОбъект, СтруктураНастроек);
		Иначе
			КомпоновщикНастроек.ЗагрузитьНастройки(Схема.НастройкиПоУмолчанию);
		КонецЕсли;
		
	Иначе
		КомпоновщикНастроек.ЗагрузитьНастройки(Схема.НастройкиПоУмолчанию);
	КонецЕсли;

КонецПроцедуры

Процедура ПередВыводомЭлементРезультата(МакетКомпоновки, ПроцессорКомпоновки, ЭлементРезультата) Экспорт
	
КонецПроцедуры

Процедура ПередВыводомОтчета(МакетКомпоновки, ПроцессорКомпоновки) Экспорт
	
	
КонецПроцедуры

Процедура ПриВыводеЗаголовкаОтчета(ОбластьЗаголовок) Экспорт
КонецПроцедуры

Функция ПолучитьПараметрыИсполненияОтчета() Экспорт
	СписокПолейПодстановкиОтборовПоУмолчанию = Новый Соответствие;
	СписокПолейПодстановкиОтборовПоУмолчанию.Вставить("Организация", "ОсновнаяОрганизация");
	
	Возврат Новый Структура("ИспользоватьСобытияПриФормированииОтчета,
	|ПриВыводеЗаголовкаОтчета,
	|ПослеВыводаПанелиПользователя,
	|ПослеВыводаПериода,
	|ПослеВыводаПараметра,
	|ПослеВыводаГруппировки,
	|ПослеВыводаОтбора,
	|ДействияПанелиИзменениеФлажкаДопНастроек,
	|ПриПолучениеНастроекПользователя, 
	|ЗаполнитьОтборыПоУмолчанию, 
	|СписокПолейПодстановкиОтборовПоУмолчанию,
	|СписокДоступныхПредопределенныхНастроек,
	|МинимальныйПериодОтчета", 
	ложь, ложь, ложь, ложь, ложь, ложь, ложь, ложь, ложь, истина, СписокПолейПодстановкиОтборовПоУмолчанию,, "Год");

КонецФункции


Процедура ДоработатьКомпоновщикПередВыводом() Экспорт
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОПЕРАТОРЫ ОСНОВНОЙ ПРОГРАММЫ

Если СохраненнаяНастройка = Неопределено Тогда
	СохраненнаяНастройка =  Справочники.СохраненныеНастройки.ПустаяСсылка();
КонецЕсли;

Если КомпоновщикНастроек = Неопределено Тогда
	КомпоновщикНастроек =  Новый КомпоновщикНастроекКомпоновкиДанных;
КонецЕсли;

УправлениеОтчетами.ЗаменитьНазваниеПолейСхемыКомпоновкиДанных(СхемаКомпоновкиДанных);