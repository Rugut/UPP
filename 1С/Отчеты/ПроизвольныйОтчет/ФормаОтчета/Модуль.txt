////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ
//

Перем СоответствиеНастройкаРезультат Экспорт;         // Соответствие, в котором хранятся все результаты при текущем открытии отчета
Перем ПредставлениеНастройки         Экспорт;         // Представление настройки при редактировании отдельной настройки
Перем ПанельНастроекНарисована       Экспорт;         // Флаг, означающий что панель настроек после открытия нарисована
Перем ДействияЭлементовФормы         Экспорт;         // Структура, содеражащая действия элементов формы, формируемых программно

// ДЕЙСТВИЯ С РЕЗУЛЬТАТОМ

Процедура ДействияСРезультатомПечатьНажатие(Элемент)
	
	ТиповыеОтчеты.ПечатьТиповогоОтчета(ЭлементыФормы.Результат);
	
КонецПроцедуры

Процедура ДействияСРезультатомВывестиКопиюРезультатаВТабличныйДокумент(Кнопка)

	ТиповыеОтчеты.ПоказатьКопиюРезультата(ЭлементыФормы.Результат);
	
КонецПроцедуры

Процедура ДействияСРезультатомСохранитьРезультатВНастройке(Кнопка)
	
	ТиповыеОтчеты.СохранитьРезультатВНастройке(ЭтотОбъект, ЭтаФорма);
	
КонецПроцедуры

Процедура ДействияСРезультатомСравнитьТекущийРезультатССохраненным(Кнопка)
	
	ТиповыеОтчеты.СохранитьРезультатССохраненным(ЭтотОбъект, ЭтаФорма);
	
КонецПроцедуры

Процедура ДействияСРезультатомЗаголовок(Кнопка)
	
	Кнопка.Пометка = Не Кнопка.Пометка;
	ТиповыеОтчеты.УправлениеОтображениемЭлементовФормыТиповогоОтчета(ЭтотОбъект, ЭтаФорма);
	ТиповыеОтчеты.УправлениеОтображениемЗаголовкаТиповогоОтчета(ЭтотОбъект, ЭтаФорма.ЭлементыФормы.Результат);
	
КонецПроцедуры

// ОБЩИЕ ПРОЦЕДУРЫ

Процедура ПроверитьЗначенияПараметровПоказателяМонитораЭффективности(ЗначенияНастроек, ПараметрыПанели, Отказ)
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	МонитораЭффективностиОбъект = Отчеты.МониторЭффективности.Создать();
	МонитораЭффективностиОбъект.ПроверитьЗначенияПараметровПоказателя(ЗначенияНастроек, ПараметрыПанели, Отказ);
	МонитораЭффективностиОбъект = Неопределено;
	
КонецПроцедуры //

Процедура ОбновитьОтчет() Экспорт
	
	СостояниеМодифицированности = Модифицированность;
	Если НЕ ПанельНастроекНарисована тогда
		ОбновлениеОтображения();
	КонецЕсли;
	ТиповыеОтчеты.ЗагрузитьВРеквизитЗначенияНастроекПанелиПользователя(ЭтотОбъект, ЭтаФорма);
	ПараметрыПанели  = ТиповыеОтчеты.ПолучитьПараметрыПанелиПользователяОбъекта(ОтчетОбъект);
	ЗначенияНастроек = ТиповыеОтчеты.ПолучитьЗначенияНастроекПанелиПользователяОбъекта(ОтчетОбъект);
	Отказ = Ложь;
	ТиповыеОтчеты.ПроверитьЗначенияПараметров(ЗначенияНастроек, ПараметрыПанели, Отказ);
	
	Если ВидПроизвольногоОтчета = Перечисления.ВидыПроизвольныхОтчетов.ПоказательМонитораЭффективности
		ИЛИ ВидПроизвольногоОтчета = Перечисления.ВидыПроизвольныхОтчетов.ПоказательМонитораЭффективностиПланФакт
		ИЛИ ВидПроизвольногоОтчета = Перечисления.ВидыПроизвольныхОтчетов.ПоказательМонитораЭффективностиПрогноз Тогда
		
		ПроверитьЗначенияПараметровПоказателяМонитораЭффективности(ЗначенияНастроек, ПараметрыПанели, Отказ);		
	КонецЕсли;
	
	Если Отказ тогда
		Возврат;
	КонецЕсли;
	
	СформироватьОтчет(ЭлементыФормы.Результат, ДанныеРасшифровки);
	УправлениеОтчетами.УстановитьКолонтитулыПоУмолчанию(ЭлементыФормы.Результат, Заголовок, Строка(ПараметрыСеанса.ТекущийПользователь));
	Модифицированность = СостояниеМодифицированности;
	
КонецПроцедуры

// ОБРАБОТЧИКИ КНОПОК ФОРМЫ

Процедура ДействияФормыСформировать(Кнопка)
	
	ОбновитьОтчет();
	
КонецПроцедуры

Процедура ДействияФормыСохранитьНастройку(Кнопка)
	
	ТиповыеОтчеты.СохранитьТекущуюНастройку(ЭтотОбъект, ЭтаФорма);
	
КонецПроцедуры

Процедура ДействияФормыСохранитьНастройкуКак(Кнопка)
	
	СохраненнаяСохраненнаяНастройка = СохраненнаяНастройка;
	
	СохранениеНастроек.ВыбратьНастройкуФормы(СохраненнаяНастройка, ЭтаФорма, ПроизвольныйОтчет, Истина);
	
	Если Не РежимРедактированияНастройки Тогда
		ТаблицаВариантовОтчета = ТиповыеОтчеты.ПолучитьТаблицуДоступныхВариантов(ПроизвольныйОтчет, глЗначениеПеременной("глТекущийПользователь"));
		ТиповыеОтчеты.ОбновитьКнопкиВыбораНастроек(ЭтаФорма, ЭтотОбъект, ПредставлениеНастройки, РежимРедактированияНастройки);
	Иначе
		// Сохранили настройки отчета в СохраненнуюНастройку
		СохраненнаяНастройка = СохраненнаяСохраненнаяНастройка;
	КонецЕсли;
	
	
КонецПроцедуры

Процедура КнопкаСписокВариантовНажатие(Кнопка)
	
	СохранениеНастроек.ВыбратьНастройкуФормы(СохраненнаяНастройка, ЭтаФорма, ПроизвольныйОтчет, Ложь);
	ПанельНастроекНарисована = Ложь;
	
	Если Не РежимРедактированияНастройки тогда
		ТаблицаВариантовОтчета = ТиповыеОтчеты.ПолучитьТаблицуДоступныхВариантов(ПроизвольныйОтчет, глЗначениеПеременной("глТекущийПользователь"));
		ТиповыеОтчеты.ОбновитьКнопкиВыбораНастроек(ЭтаФорма, ЭтотОбъект, ПредставлениеНастройки, РежимРедактированияНастройки);
	КонецЕсли;
	
	Модифицированность = Ложь;
	
КонецПроцедуры

Процедура ДействияФормыРедактироватьНастройкуСтруктуры(Кнопка)
	
	РезультатРедактирования = ТиповыеОтчеты.РедактироватьСтруктуруОтчета(ЭтотОбъект, ЭтаФорма);
	Если РезультатРедактирования тогда
		ОбновитьОтчет();
	КонецЕсли;
	
КонецПроцедуры

Процедура ДействияФормыРедактироватьПанельПользователя(Кнопка)
	
	ТиповыеОтчеты.РедактироватьПанельПользователя(ЭтотОбъект, ЭтаФорма);
		
КонецПроцедуры

Процедура ДействияФормыРедактироватьОписаниеИПользователей(Кнопка)
	
	ТиповыеОтчеты.ОткрытьФормуСохраненнойНастройки(ЭтотОбъект, ЭтаФорма);
	
КонецПроцедуры

Процедура КоманднаяПанельПанельВариантов(Кнопка)
	
	Кнопка.Пометка = Не Кнопка.Пометка;
	ТиповыеОтчеты.УправлениеОтображениемПанелиВариантов(ЭтаФорма);
	
КонецПроцедуры

Процедура КоманднаяПанельПанельПользователя(Кнопка)
	
	Кнопка.Пометка = Не Кнопка.Пометка;
	ТиповыеОтчеты.УправлениеОтображениемПанелиПользователя(ЭтаФорма);
	
КонецПроцедуры

// ОБРАБОТЧИКИ ФОРМЫ

Процедура ПриОткрытии()
	
	ТиповыеОтчеты.ОбновитьТаблицуДоступныхНастроекПользователю(ЭтотОбъект);
	ТиповыеОтчеты.УстановитьВариантПоУмолчанию(ЭтотОбъект, ЭтаФорма);
	
	// Нарисуем кнопки выбора настроек на верхней панели
	ТиповыеОтчеты.ОбновитьКнопкиВыбораНастроек(ЭтаФорма, ЭтотОбъект, ПредставлениеНастройки, РежимРедактированияНастройки);
	ТиповыеОтчеты.ОбновитьФормуТиповогоОтчетаПоКомпоновщику(ЭтотОбъект, ЭтаФорма);
	ТиповыеОтчеты.УстановитьВидимостьПанелиВариантовПоУмолчанию(ЭтотОбъект, ЭтаФорма);
	ТиповыеОтчеты.УстановитьВидимостьПанелиПользователяПоУмолчанию(ЭтотОбъект, ЭтаФорма);
	ТиповыеОтчеты.ВосстановитьНастройкиФормыОтчета(ЭтотОбъект, ЭтаФорма);
	ТиповыеОтчеты.УправлениеОтображениемПанелиВариантов(ЭтаФорма);
	ТиповыеОтчеты.УправлениеОтображениемПанелиПользователя(ЭтаФорма)
	
КонецПроцедуры

Процедура ОбновлениеОтображения() Экспорт
	
	Если Не ПанельНастроекНарисована И Не Модифицированность тогда
		
		// Инициализация компоновщиков настроек и, если необходимо, загрузка настроек по умолчанию
		ЗагружатьНастройкиПоУмолчанию = Не РежимРедактированияНастройки И Не ЭтоОтработкаРасшифровки И СохраненнаяНастройка.Пустая();
		Схема = ТиповыеОтчеты.ИнициализироватьКомпоновщикНастроек(ЭтотОбъект, , ЗагружатьНастройкиПоУмолчанию);
		КомпоновщикНастроекПользователя.Инициализировать(Новый ИсточникДоступныхНастроекКомпоновкиДанных(Схема));
		
		Если Не ЭтоОтработкаРасшифровки И Не РежимРедактированияНастройки тогда
			ПрименитьНастройку();
			ТиповыеОтчеты.ПрименитьНастройкуПользователяНастройкиОтчета(ЭтотОбъект);
			ТиповыеОтчеты.ОбработкаФормыПослеПримененияНастройки(ЭтотОбъект, ЭтаФорма);
		Иначе
			// Отработаем изменение отображение панели настроек
			ТиповыеОтчеты.УправлениеОтображениемЭлементовФормыТиповогоОтчета(ЭтотОбъект, ЭтаФорма);
			ТиповыеОтчеты.ПерерисоватьПанельНастроек(ЭтотОбъект, ЭтаФорма, );
			
		КонецЕсли;
		
		ПанельНастроекНарисована = Истина;
		
	КонецЕсли;
	
	ТиповыеОтчеты.ОбновитьЗаголовокТиповогоОтчета(ЭтотОбъект, ЭтаФорма);
	
КонецПроцедуры

Процедура ОбработкаВыбора(ЗначениеВыбора, Источник)
	
	ТиповыеОтчеты.ОбработкаВыбораФормыОтчета(ЭтотОбъект, ЭтаФорма, ЗначениеВыбора, Источник);
	
КонецПроцедуры

Процедура РезультатОбработкаРасшифровки(Элемент, Расшифровка, СтандартнаяОбработка)
	
	ТиповыеОтчеты.ОбработкаРасшифровкиТиповогоОтчета(Расшифровка, СтандартнаяОбработка, ЭтотОбъект, ЭтаФорма,);
		
КонецПроцедуры

Процедура ПередЗакрытием(Отказ, СтандартнаяОбработка)
	
	Если РежимРедактированияНастройки тогда
		СтандартнаяОбработка = Ложь;
		Возврат;
	КонецЕсли;
	
	ТиповыеОтчеты.ОбработкаЗакрытияНастройкиОтчета(ЭтотОбъект, ЭтаФорма, Отказ, СтандартнаяОбработка);
	Если Не ЭтоОтработкаРасшифровки И Не РежимРедактированияНастройки тогда
		ТиповыеОтчеты.СохранитьПоследнююИспользуемуюНастройку(ЭтотОбъект);
	КонецЕсли;
	
КонецПроцедуры

// ОБРАБОТЧИКИ ЭЛЕМЕНТОВ ФОРМЫ НА ПАНЕЛИ

Процедура ОтборПриВыводеСтроки(Элемент, ОформлениеСтроки, ДанныеСтроки)
	
	ОформлениеСтроки.Ячейки.ЛевоеЗначениеДляКраткогоОтображенияЭлемента.ОтображатьКартинку = Ложь;
	
КонецПроцедуры

Процедура ОбработкаНажатияКнопкиСохраненнойНастройки(Элемент)
	
	ТиповыеОтчеты.ВыборВариантаОтчетаНаПанелиВариантов(ЭтотОбъект, ЭтаФорма, Элемент);
		
КонецПроцедуры

Процедура ДействияПанелиСохранитьСписок(Элемент)
	
	ТиповыеОтчеты.СохранитьСписокВТабличномПоле(ЭтотОбъект, ЭлементыФормы, Элемент);
		
КонецПроцедуры

Процедура ДействияПанелиЗагрузитьСписок(Элемент)
	
	ТиповыеОтчеты.ЗагрузитьСписокВТабличноеПоле(ЭтотОбъект, ЭлементыФормы, Элемент);
		
КонецПроцедуры

Процедура ДействияПанелиСтандартныйПериодПользователяПриИзменении(Элемент)
	
	СтандартныйПериод.Вариант = ?(Элемент.Значение = Неопределено, ВариантСтандартногоПериода.ПроизвольныйПериод, Элемент.Значение);
	
КонецПроцедуры

Процедура ДействияПанелиСтандартнаяДатаНачалаПользователяПриИзменении(Элемент)
	
	СтандартнаяДатаНачала.Вариант = ?(Элемент.Значение = Неопределено, ВариантСтандартнойДатыНачала.ПроизвольнаяДата, Элемент.Значение);
	
КонецПроцедуры

Процедура ДействияПанелиДатаСтандартногоПериодаПриИзменении(Элемент)
	
	ЭлементыФормы.ДинамическийОтборСтандартныйПериодПользователя.Значение = ВариантСтандартногоПериода.ПроизвольныйПериод;
	
КонецПроцедуры

Процедура ДействияПанелиДатаСтандартнойДатыНачалаПриИзменении(Элемент)
	
	ЭлементыФормы.ДинамическийОтборСтандартнаяДатаНачалаПользователя.Значение = ВариантСтандартнойДатыНачала.ПроизвольнаяДата;
	
КонецПроцедуры

Процедура ДействияПанелиИзменениеЗначенияДинамическогоОтбора(Элемент)
	
	ТиповыеОтчеты.ОбработкаИзмененияЗначенияДинамическогоОтбора(ЭлементыФормы, Элемент);
	
КонецПроцедуры

Процедура ДействияПанелиОкончаниеРедактированиеТабличногоПоля(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	ТиповыеОтчеты.ОбновитьИзмененияТабличногоПоля(ЭлементыФормы, Элемент);
	
КонецПроцедуры

Процедура ДействияПанелиУдалениеСтрокиТабличногоПоля(Элемент)
	
	ТиповыеОтчеты.ОбновитьИзмененияТабличногоПоля(ЭлементыФормы, Элемент);
	
КонецПроцедуры
                                                              
Процедура ДействияПанелиКнопкаПодборНажатие(Элемент)
	
	ТиповыеОтчеты.ОбработкаНажатияКнопкиПодбор(ЭтотОбъект, ЭтаФорма, Элемент);
	
КонецПроцедуры

Процедура ПриНажатииНаКнопкуВверх(Кнопка)
	
	Типовыеотчеты.ПриНажатииКнопкиВверхВниз(ЭтаФорма, ОтчетОбъект, Кнопка);
	
КонецПроцедуры

Процедура ПриНажатииНаКнопкуВниз(Кнопка)
	
	Типовыеотчеты.ПриНажатииКнопкиВверхВниз(ЭтаФорма, ОтчетОбъект, Кнопка);
	
КонецПроцедуры

Процедура ПриЗакрытии()
	
	ТиповыеОтчеты.СохранитьНастройкиФормыОтчета(ОтчетОбъект, ЭтаФорма);
	
КонецПроцедуры

Процедура ГруппировкиПриПолученииДанных(Элемент, ОформленияСтрок)
	
	ТиповыеОтчеты.ГруппировкаПриПолученииДанных(Элемент, ОформленияСтрок);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ ОТНОСЯЩИЕСЯ ТОЛЬКО К АНАЛИТИЧЕСКИМ ОТЧЕТАМ
//

Процедура ОбновитьКартинкуСпискаВыбораИндикатора(Элемент)

	Значение = Элемент.Значение;
	Картинка = ПроцедурыПроизвольныхОтчетов.ПолучитьКартинкуИндикатора(Значение);
	Если Картинка <> Неопределено Тогда
		Элемент.Картинка = Картинка;
	Иначе
		Элемент.Картинка = Новый Картинка;
	КонецЕсли;

КонецПроцедуры

Процедура УстановитьЗначениеНастройкиНаФорме(ИмяЭлементаФормы, ИмяЗначения, ЗначенияАналитическиеОтборы)

	ЭлементФормы = ЭлементыФормы.Найти(ИмяЭлементаФормы);
	Если ЭлементФормы = Неопределено Тогда
		Возврат;
	КонецЕсли; 
	
	Значение = ТиповыеОтчеты.ПолучитьЗначенияЭлементаСтруктуры(ЗначенияАналитическиеОтборы, ИмяЗначения);
	ЭлементФормы.Значение = Значение;
	
КонецПроцедуры //
 

Процедура НарисоватьАналитическиеОтборы(НастройкаСтраницы, ЗначенияНастроек) Экспорт
	
	ЦветФонаКнопки = Новый Цвет(246, 244, 236);
	
	// Вставка элементов управления аналитических отборов
	ОтображатьАналитическиеОтборы = Ложь;
	Если ЭлементыФормы.ПанельЗакладок.Страницы.АналитическиеОтборы.Видимость Тогда
		
		// Удалим старые элементы управления с закладки
		Количество = ЭлементыФормы.Количество();
		Для Индекс = 1 По Количество Цикл
			ЭлементФормы = ЭлементыФормы[Количество - Индекс];
			Если Найти(ЭлементФормы.Имя, "АналитическиеОтборы") > 0 Тогда
				ЭлементыФормы.Удалить(ЭлементФормы);
			КонецЕсли;
		КонецЦикла;
		
		ЭлементыФормы.ПанельЗакладок.ТекущаяСтраница = ЭлементыФормы.ПанельЗакладок.Страницы.АналитическиеОтборы;
		Верх = 6;
		// Нарисовать настройку на ограничение на количество записей
		НастройкаКоличествоЗаписей = НастройкаСтраницы.Строки.Найти("КоличествоЗаписей", "Имя");
		Если НастройкаКоличествоЗаписей.Использование Тогда
			ОтображатьАналитическиеОтборы = Истина;
			// Надпись Первые
			НовыйЭлемент = ЭлементыФормы.Добавить(Тип("Надпись"), "АналитическиеОтборыНадписьПервыеЗаписи",, ЭлементыФормы.ПанельЗакладок);
			НовыйЭлемент.Верх = Верх;
			НовыйЭлемент.Лево = 6;
			НовыйЭлемент.Ширина = 60;
			НовыйЭлемент.Заголовок = "Первые";
			
			// Надпись ПолеТоп
			НовыйЭлемент = ЭлементыФормы.Добавить(Тип("ПолеВвода"), "АналитическиеОтборыКоличествоТоп",, ЭлементыФормы.ПанельЗакладок);
			НовыйЭлемент.Верх = Верх;
			НовыйЭлемент.Лево = 68;
			НовыйЭлемент.Ширина = 32;
			НовыйЭлемент.ТипЗначения = Новый ОписаниеТипов("Число");
			НовыйЭлемент.ЦветФонаКнопки = ЦветФонаКнопки;
				
			// Флажок Процент записей
			НовыйЭлемент = ЭлементыФормы.Добавить(Тип("Флажок"), "АналитическиеОтборыПроцентТоп",, ЭлементыФормы.ПанельЗакладок);
			НовыйЭлемент.Верх = Верх + 2;
			НовыйЭлемент.Лево = 105;
			НовыйЭлемент.Ширина = 26;
			НовыйЭлемент.Заголовок = "%";
			
			// Надпись записей
			НовыйЭлемент = ЭлементыФормы.Добавить(Тип("Надпись"), "АналитическиеОтборыНадписьЗаписей",, ЭлементыФормы.ПанельЗакладок);
			НовыйЭлемент.Верх = Верх;
			НовыйЭлемент.Лево = 134;
			НовыйЭлемент.Ширина = 45;
			НовыйЭлемент.Заголовок = "записей";
			// Поле выбора ПолеТоп
			НовыйЭлемент = ЭлементыФормы.Добавить(Тип("ПолеВыбора"), "АналитическиеОтборыПолеТоп",, ЭлементыФормы.ПанельЗакладок);
			НовыйЭлемент.Верх = Верх;
			НовыйЭлемент.Лево = 188;
			НовыйЭлемент.Ширина = 110;
			НовыйЭлемент.ЦветФонаКнопки = ЦветФонаКнопки;
			ТиповыеОтчеты.УстановитьПравуюПривязку(НовыйЭлемент, ЭлементыФормы.ПанельЗакладок);
			
			// Инициализация ТОП
			ПоляГруппировок = ТиповыеОтчеты.ПолучитьПоляГруппировок(ЭтотОбъект.КомпоновщикНастроек, Истина);
			Если ТиповыеОтчеты.ПолучитьЭлементСтруктурыДетальныеЗаписи(ЭтотОбъект.КомпоновщикНастроек) <> Неопределено Тогда
				ПоляГруппировок.Добавить("ДетальныеЗаписи", "<Детальные записи>");
			КонецЕсли;
			ПоляГруппировок.Вставить(0, "", "");
			НовыйЭлемент.СписокВыбора = ПоляГруппировок;
			Значение = ТиповыеОтчеты.ПолучитьЗначенияЭлементаСтруктуры(ЗначенияНастроек.АналитическиеОтборы, "ПолеТоп");
			ТиповыеОтчеты.УстановитьДопустимоеЗначениеВПолеВыбора(НовыйЭлемент, Значение, Истина);
			
			Верх = Верх + 25;
			
		КонецЕсли;
		
		// Нарисовать настройку порога прочие
		НастройкаПорог = НастройкаСтраницы.Строки.Найти("Порог", "Имя");
		Если НастройкаПорог <> Неопределено И НастройкаПорог.Использование Тогда
			ОтображатьАналитическиеОтборы = Истина;
			// Надпись Порог
			НовыйЭлемент = ЭлементыФормы.Добавить(Тип("Надпись"), "АналитическиеОтборыНадписьПорог",, ЭлементыФормы.ПанельЗакладок);
			НовыйЭлемент.Верх = Верх;
			НовыйЭлемент.Лево = 6;
			НовыйЭлемент.Ширина = 60;
			НовыйЭлемент.Заголовок = "Порог сущ.";
			
			// Надпись ПроцентПорог
			НовыйЭлемент = ЭлементыФормы.Добавить(Тип("ПолеВвода"), "АналитическиеОтборыПроцентПорог",, ЭлементыФормы.ПанельЗакладок);
			НовыйЭлемент.Верх = Верх;
			НовыйЭлемент.Лево = 68;
			НовыйЭлемент.Ширина = 32;
			НовыйЭлемент.ТипЗначения = Новый ОписаниеТипов("Число");
			НовыйЭлемент.ЦветФонаКнопки = ЦветФонаКнопки;
				
			
			// Надпись процент
			НовыйЭлемент = ЭлементыФормы.Добавить(Тип("Надпись"), "АналитическиеОтборыНадписьПроцентПорог",, ЭлементыФормы.ПанельЗакладок);
			НовыйЭлемент.Верх = Верх;
			НовыйЭлемент.Лево = 102;
			НовыйЭлемент.Ширина = 5;
			НовыйЭлемент.Заголовок = "%";
			
			
			// Поле выбора Ресурс порога
			ПолеВыбораРесурсПорог = ЭлементыФормы.Добавить(Тип("ПолеВыбора"), "АналитическиеОтборыРесурсПорог",, ЭлементыФормы.ПанельЗакладок);
			ПолеВыбораРесурсПорог.Верх = Верх;
			ПолеВыбораРесурсПорог.Лево = 112;
			ПолеВыбораРесурсПорог.Ширина = 72;
			ПолеВыбораРесурсПорог.ЦветФонаКнопки = ЦветФонаКнопки;
			
			ПолеВыбораРесурсПорог.УстановитьПривязку(ГраницаЭлементаУправления.Право, Панель, ГраницаЭлементаУправления.Лево, Панель, ГраницаЭлементаУправления.Право);
			
			СписокРесурсов = ТиповыеОтчеты.ПолучитьСписокДоступныхРесурсов(ЭтотОбъект.КомпоновщикНастроек, Ложь, Ложь);
			Для каждого Элемент Из СписокРесурсов Цикл
				Элемент.Значение = Строка(Элемент.Значение);
			КонецЦикла;
			ПолеВыбораРесурсПорог.СписокВыбора = СписокРесурсов;
			Значение = ТиповыеОтчеты.ПолучитьЗначенияЭлементаСтруктуры(ЗначенияНастроек.АналитическиеОтборы, "РесурсПорог");
			ТиповыеОтчеты.УстановитьДопустимоеЗначениеВПолеВыбора(ПолеВыбораРесурсПорог, Значение, Истина);
			
			
			// Поле выбора Поле порога
			НовыйЭлемент = ЭлементыФормы.Добавить(Тип("ПолеВыбора"), "АналитическиеОтборыПолеПорог",, ЭлементыФормы.ПанельЗакладок);
			НовыйЭлемент.Верх = Верх;
			НовыйЭлемент.Лево = 188;
			НовыйЭлемент.Ширина = 110;
			НовыйЭлемент.ЦветФонаКнопки = ЦветФонаКнопки;
			НовыйЭлемент.УстановитьПривязку(ГраницаЭлементаУправления.Лево, ПолеВыбораРесурсПорог, ГраницаЭлементаУправления.Право);
			ТиповыеОтчеты.УстановитьПравуюПривязку(НовыйЭлемент, ЭлементыФормы.ПанельЗакладок);
			
			// Инициализация Поле порога
			ПоляГруппировок = ТиповыеОтчеты.ПолучитьПоляГруппировок(ЭтотОбъект.КомпоновщикНастроек, Истина);
			Если ТиповыеОтчеты.ПолучитьЭлементСтруктурыДетальныеЗаписи(ЭтотОбъект.КомпоновщикНастроек) <> Неопределено Тогда
				ПоляГруппировок.Добавить("ДетальныеЗаписи", "<Детальные записи>");
			КонецЕсли;
			ПоляГруппировок.Вставить(0, "", "");
			НовыйЭлемент.СписокВыбора = ПоляГруппировок;
			Значение = ТиповыеОтчеты.ПолучитьЗначенияЭлементаСтруктуры(ЗначенияНастроек.АналитическиеОтборы, "ПолеПорог");
			ТиповыеОтчеты.УстановитьДопустимоеЗначениеВПолеВыбора(НовыйЭлемент, Значение, Истина);
			
			Верх = Верх + 25;
			
		КонецЕсли;
		
		// Нарисовать настройку отбора индикаторов
		НастройкаИндикаторы = НастройкаСтраницы.Строки.Найти("Индикаторы", "Имя");
		Если НастройкаИндикаторы.Использование Тогда
			
			// Рисование отбора Тренд
			СписокПользовательскихПолей = Новый СписокЗначений;
			СписокПользовательскихПолей.Добавить("", "");
			Для каждого ПользовательскоеПоле Из ЭтотОбъект.КомпоновщикНастроек.Настройки.ПользовательскиеПоля.Элементы Цикл
				Если ТиповыеОтчеты.ПолучитьИмяФормыРедактированияПользовательскогоПоля(ПользовательскоеПоле) <> Неопределено 
					И ТипЗнч(ПользовательскоеПоле) <> Тип("ПользовательскоеПолеВыражениеКомпоновкиДанных")
					И ПользовательскоеПоле.Варианты.Элементы[0].Значение = "1Тренд" Тогда
					СписокПользовательскихПолей.Добавить(ПользовательскоеПоле.ПутьКДанным, ПользовательскоеПоле.Заголовок);
				КонецЕсли;
			КонецЦикла;
			Если СписокПользовательскихПолей.Количество() > 1 Тогда
				ОтображатьАналитическиеОтборы = Истина;
				// Надпись Тренд
				НовыйЭлемент = ЭлементыФормы.Добавить(Тип("Надпись"), "АналитическиеОтборыНадписьТренд",, ЭлементыФормы.ПанельЗакладок);
				НовыйЭлемент.Верх = Верх;
				НовыйЭлемент.Лево = 6;
				НовыйЭлемент.Ширина = 60;
				НовыйЭлемент.Заголовок = "Тренд";
				// Поле выбора ПолеТоп
				НовыйЭлемент = ЭлементыФормы.Добавить(Тип("ПолеВыбора"), "АналитическиеОтборыПолеОтборТренд",, ЭлементыФормы.ПанельЗакладок);
				НовыйЭлемент.Верх = Верх;
				НовыйЭлемент.Лево = 68;
				НовыйЭлемент.Ширина = 136;
				НовыйЭлемент.ЦветФонаКнопки = ЦветФонаКнопки;
				НовыйЭлемент.СписокВыбора = СписокПользовательскихПолей;
				
				Значение = ТиповыеОтчеты.ПолучитьЗначенияЭлементаСтруктуры(ЗначенияНастроек.АналитическиеОтборы, "ПолеОтборТренд");
				ТиповыеОтчеты.УстановитьДопустимоеЗначениеВПолеВыбора(НовыйЭлемент, Значение, Истина);
				
				// Надпись только
				НовыйЭлемент = ЭлементыФормы.Добавить(Тип("Надпись"), "АналитическиеОтборыНадписьТрендТолько",, ЭлементыФормы.ПанельЗакладок);
				НовыйЭлемент.Верх = Верх;
				НовыйЭлемент.Лево = 210;
				НовыйЭлемент.Ширина = 40;
				НовыйЭлемент.Заголовок = "только";
				
				// Поле выбора ЗначениеОтборТренд
				НовыйЭлемент = ЭлементыФормы.Добавить(Тип("ПолеВыбора"), "АналитическиеОтборыЗначениеОтборТренд",, ЭлементыФормы.ПанельЗакладок);
				НовыйЭлемент.Верх = Верх;
				НовыйЭлемент.Лево = 256;
				НовыйЭлемент.Ширина = 42;
				НовыйЭлемент.ШиринаСпискаВыбора = 3;
				НовыйЭлемент.ЦветФонаКнопки = ЦветФонаКнопки;
				НовыйЭлемент.СписокВыбора = ПолучитьСписокВыбораИндикатора("Тренд");
				Действие = Новый Действие("ОбновитьКартинкуСпискаВыбораИндикатора");
				НовыйЭлемент.УстановитьДействие("ПриИзменении", Действие);
				Значение = ТиповыеОтчеты.ПолучитьЗначенияЭлементаСтруктуры(ЗначенияНастроек.АналитическиеОтборы, "ЗначениеОтборТренд");
				ТиповыеОтчеты.УстановитьДопустимоеЗначениеВПолеВыбора(НовыйЭлемент, Значение);
				ОбновитьКартинкуСпискаВыбораИндикатора(НовыйЭлемент);
				ТиповыеОтчеты.УстановитьПравуюПривязку(НовыйЭлемент, ЭлементыФормы.ПанельЗакладок);
				Верх = Верх + 25;
			КонецЕсли;
			
			// Рисование отбора Состояние
			СписокПользовательскихПолей = Новый СписокЗначений;
			СписокПользовательскихПолей.Добавить("", "");
			Для каждого ПользовательскоеПоле Из ЭтотОбъект.КомпоновщикНастроек.Настройки.ПользовательскиеПоля.Элементы Цикл
				Если ТиповыеОтчеты.ПолучитьИмяФормыРедактированияПользовательскогоПоля(ПользовательскоеПоле) <> Неопределено 
					И ТипЗнч(ПользовательскоеПоле) <> Тип("ПользовательскоеПолеВыражениеКомпоновкиДанных")
					И ПользовательскоеПоле.Варианты.Элементы[0].Значение = "1Состояние" Тогда
					СписокПользовательскихПолей.Добавить(ПользовательскоеПоле.ПутьКДанным, ПользовательскоеПоле.Заголовок);
				КонецЕсли;
			КонецЦикла;
			Если СписокПользовательскихПолей.Количество() > 1 Тогда
				ОтображатьАналитическиеОтборы = Истина;
				// Надпись Состояние
				НовыйЭлемент = ЭлементыФормы.Добавить(Тип("Надпись"), "АналитическиеОтборыНадписьСостояние",, ЭлементыФормы.ПанельЗакладок);
				НовыйЭлемент.Верх = Верх;
				НовыйЭлемент.Лево = 6;
				НовыйЭлемент.Ширина = 60;
				НовыйЭлемент.Заголовок = "Состояние";
				// Поле выбора ПолеТоп
				НовыйЭлемент = ЭлементыФормы.Добавить(Тип("ПолеВыбора"), "АналитическиеОтборыПолеОтборСостояние",, ЭлементыФормы.ПанельЗакладок);
				НовыйЭлемент.Верх = Верх;
				НовыйЭлемент.Лево = 68;
				НовыйЭлемент.Ширина = 136;
				НовыйЭлемент.ЦветФонаКнопки = ЦветФонаКнопки;
				НовыйЭлемент.СписокВыбора= СписокПользовательскихПолей;
				Значение = ТиповыеОтчеты.ПолучитьЗначенияЭлементаСтруктуры(ЗначенияНастроек.АналитическиеОтборы, "ПолеОтборСостояние");
				ТиповыеОтчеты.УстановитьДопустимоеЗначениеВПолеВыбора(НовыйЭлемент, Значение, Истина);
				
				// Надпись только
				НовыйЭлемент = ЭлементыФормы.Добавить(Тип("Надпись"), "АналитическиеОтборыНадписьСостояниеТолько",, ЭлементыФормы.ПанельЗакладок);
				НовыйЭлемент.Верх = Верх;
				НовыйЭлемент.Лево = 210;
				НовыйЭлемент.Ширина = 40;
				НовыйЭлемент.Заголовок = "только";
				
				// Поле выбора ЗначениеОтборСостояние
				НовыйЭлемент = ЭлементыФормы.Добавить(Тип("ПолеВыбора"), "АналитическиеОтборыЗначениеОтборСостояние",, ЭлементыФормы.ПанельЗакладок);
				НовыйЭлемент.Верх = Верх;
				НовыйЭлемент.Лево = 256;
				НовыйЭлемент.Ширина = 42;
				НовыйЭлемент.ШиринаСпискаВыбора = 3;
				НовыйЭлемент.ЦветФонаКнопки = ЦветФонаКнопки;
				НовыйЭлемент.СписокВыбора = ПолучитьСписокВыбораИндикатора("Состояние");
				Действие = Новый Действие("ОбновитьКартинкуСпискаВыбораИндикатора");
				НовыйЭлемент.УстановитьДействие("ПриИзменении", Действие);
				Значение = ТиповыеОтчеты.ПолучитьЗначенияЭлементаСтруктуры(ЗначенияНастроек.АналитическиеОтборы, "ЗначениеОтборСостояние");
				ТиповыеОтчеты.УстановитьДопустимоеЗначениеВПолеВыбора(НовыйЭлемент, Значение);
				ОбновитьКартинкуСпискаВыбораИндикатора(НовыйЭлемент);
				ТиповыеОтчеты.УстановитьПравуюПривязку(НовыйЭлемент, ЭлементыФормы.ПанельЗакладок);
				Верх = Верх + 25;
			КонецЕсли;
			
		КонецЕсли;
		
		// Нарисовать настройку отбора ABC Классификации
		НастройкаABCКлассификация = НастройкаСтраницы.Строки.Найти("ABCКлассификация", "Имя");
		Если НастройкаABCКлассификация.Использование Тогда
			
			// Рисование отбора ABCКлассификация
			СписокПользовательскихПолей = Новый СписокЗначений;
			СписокПользовательскихПолей.Добавить("", "");
			Для каждого ПользовательскоеПоле Из ЭтотОбъект.КомпоновщикНастроек.Настройки.ПользовательскиеПоля.Элементы Цикл
				Если ТиповыеОтчеты.ПолучитьИмяФормыРедактированияПользовательскогоПоля(ПользовательскоеПоле) <> Неопределено 
				   И ТипЗнч(ПользовательскоеПоле) <> Тип("ПользовательскоеПолеВыражениеКомпоновкиДанных")
				   И ПользовательскоеПоле.Варианты.Элементы.Количество() > 0 
				   И ТиповыеОтчеты.ПолучитьПараметрИзСтроки(ПользовательскоеПоле.Варианты.Элементы[0].Значение) = "ДоработкаТаблицы" Тогда
					СписокПользовательскихПолей.Добавить(ПользовательскоеПоле.ПутьКДанным, ПользовательскоеПоле.Заголовок);
				КонецЕсли;
			КонецЦикла;         
			Если СписокПользовательскихПолей.Количество() > 1 Тогда
				ОтображатьАналитическиеОтборы = Истина;
				// Надпись ABCКлассификация
				НовыйЭлемент = ЭлементыФормы.Добавить(Тип("Надпись"), "АналитическиеОтборыНадписьABCКлассификация",, ЭлементыФормы.ПанельЗакладок);
				НовыйЭлемент.Верх = Верх;
				НовыйЭлемент.Лево = 6;
				НовыйЭлемент.Ширина = 60;
				НовыйЭлемент.Заголовок = "ABC Класс";
				// Поле выбора ПолеТоп
				НовыйЭлемент = ЭлементыФормы.Добавить(Тип("ПолеВыбора"), "АналитическиеОтборыПолеОтборABCКлассификация",, ЭлементыФормы.ПанельЗакладок);
				НовыйЭлемент.Верх = Верх;
				НовыйЭлемент.Лево = 68;
				НовыйЭлемент.Ширина = 136;
				НовыйЭлемент.ЦветФонаКнопки = ЦветФонаКнопки;
				НовыйЭлемент.СписокВыбора = СписокПользовательскихПолей;
				Значение = ТиповыеОтчеты.ПолучитьЗначенияЭлементаСтруктуры(ЗначенияНастроек.АналитическиеОтборы, "ПолеОтборABCКлассификация");
				ТиповыеОтчеты.УстановитьДопустимоеЗначениеВПолеВыбора(НовыйЭлемент, Значение, Истина);
				
				// Надпись только
				НовыйЭлемент = ЭлементыФормы.Добавить(Тип("Надпись"), "АналитическиеОтборыНадписьABCКлассификацияТолько",, ЭлементыФормы.ПанельЗакладок);
				НовыйЭлемент.Верх = Верх;
				НовыйЭлемент.Лево = 210;
				НовыйЭлемент.Ширина = 40;
				НовыйЭлемент.Заголовок = "только";
				
				// Поле выбора ЗначениеОтборABCКлассификация
				НовыйЭлемент = ЭлементыФормы.Добавить(Тип("ПолеВыбора"), "АналитическиеОтборыЗначениеОтборABCКлассификация",, ЭлементыФормы.ПанельЗакладок);
				НовыйЭлемент.Верх = Верх;
				НовыйЭлемент.Лево = 256;
				НовыйЭлемент.Ширина = 42;
				НовыйЭлемент.ЦветФонаКнопки = ЦветФонаКнопки;
				НовыйЭлемент.СписокВыбора = ПолучитьСписокВыбораИндикатора("ДоработкаТаблицы,ABCКлассификация,");
				Значение = ТиповыеОтчеты.ПолучитьЗначенияЭлементаСтруктуры(ЗначенияНастроек.АналитическиеОтборы, "ЗначениеОтборABCКлассификация");
				ТиповыеОтчеты.УстановитьДопустимоеЗначениеВПолеВыбора(НовыйЭлемент, Значение);
				ТиповыеОтчеты.УстановитьПравуюПривязку(НовыйЭлемент, ЭлементыФормы.ПанельЗакладок);
				Верх = Верх + 25;
			КонецЕсли;
		КонецЕсли;
		
		// Нарисовать настройку отбора скрыть нулевые
		НастройкаСкрытьНулевые = НастройкаСтраницы.Строки.Найти("СкрытьНулевые", "Имя");
		Если НастройкаСкрытьНулевые.Использование Тогда
			
			ОтображатьАналитическиеОтборы = Истина;
			// Флажок Процент записей
			НовыйЭлемент = ЭлементыФормы.Добавить(Тип("Флажок"), "АналитическиеОтборыСкрытьНулевые",, ЭлементыФормы.ПанельЗакладок);
			НовыйЭлемент.Верх = Верх + 2;
			НовыйЭлемент.Лево = 6;
			НовыйЭлемент.Ширина = 195;
			НовыйЭлемент.Заголовок = "Скрыть нулевые строки и колонки";
			
			Значение = ТиповыеОтчеты.ПолучитьЗначенияЭлементаСтруктуры(ЗначенияНастроек.АналитическиеОтборы, "СкрытьНулевые");
			НовыйЭлемент.Значение = Значение = Истина;
			ТиповыеОтчеты.УстановитьПравуюПривязку(НовыйЭлемент, ЭлементыФормы.ПанельЗакладок);
		КонецЕсли;
				
	КонецЕсли;
	
	УстановитьЗначениеНастройкиНаФорме("АналитическиеОтборыКоличествоТоп", "КоличествоТоп", ЗначенияНастроек.АналитическиеОтборы);
	УстановитьЗначениеНастройкиНаФорме("АналитическиеОтборыПроцентТоп", "ПроцентТоп", ЗначенияНастроек.АналитическиеОтборы);
	УстановитьЗначениеНастройкиНаФорме("АналитическиеОтборыПроцентПорог", "ПроцентПорог", ЗначенияНастроек.АналитическиеОтборы);
	
	// Если нет ни одного аналитического отбора, тогда и закладку не отображаем
	ЭлементыФормы.ПанельЗакладок.Страницы.АналитическиеОтборы.Видимость = ОтображатьАналитическиеОтборы;
	
	ОтображатьАналитическиеОтборы = ЭлементыФормы.ПанельЗакладок.Страницы.АналитическиеОтборы.Видимость;
	
КонецПроцедуры

Функция ПолучитьСписокВыбораИндикатора(ИмяИндикатора)
	
	Список = Новый СписокЗначений;
	Список.Добавить("", "");
	
	Если ИмяИндикатора = "ДоработкаТаблицы,ABCКлассификация," Тогда
		Список.Добавить("A - Класс");
		Список.Добавить("B - Класс");
		Список.Добавить("C - Класс");
	Иначе
		Для Индекс = 1 По 3 Цикл
			Значение = "" + Индекс + ИмяИндикатора;
			Картинка = ПроцедурыПроизвольныхОтчетов.ПолучитьКартинкуИндикатора(Значение);
			Список.Вставить(1, Значение, " ", , Картинка);
		КонецЦикла;
	КонецЕсли;

	Возврат Список;
	
КонецФункции

Процедура ПроизвольныйОтчетПриИзменении(Элемент)
	
	УстановитьПроизвольныйОтчет(ПроизвольныйОтчет,, ЭтаФорма);
	
КонецПроцедуры


ПанельНастроекНарисована = Ложь;

СоответствиеНастройкаРезультат = Новый Соответствие;

ДействияЭлементовФормы = Новый Структура;
ДействияЭлементовФормы.Вставить("ОбработкаНажатияКнопкиСохраненнойНастройки", Новый Действие("ОбработкаНажатияКнопкиСохраненнойНастройки"));
ДействияЭлементовФормы.Вставить("ДействияПанелиСохранитьСписок", Новый Действие("ДействияПанелиСохранитьСписок"));
ДействияЭлементовФормы.Вставить("ДействияПанелиЗагрузитьСписок", Новый Действие("ДействияПанелиЗагрузитьСписок"));
ДействияЭлементовФормы.Вставить("ДействияПанелиСтандартныйПериодПользователяПриИзменении", Новый Действие("ДействияПанелиСтандартныйПериодПользователяПриИзменении"));
ДействияЭлементовФормы.Вставить("ДействияПанелиСтандартнаяДатаНачалаПользователяПриИзменении", Новый Действие("ДействияПанелиСтандартнаяДатаНачалаПользователяПриИзменении"));
ДействияЭлементовФормы.Вставить("ДействияПанелиДатаСтандартногоПериодаПриИзменении", Новый Действие("ДействияПанелиДатаСтандартногоПериодаПриИзменении"));
ДействияЭлементовФормы.Вставить("ДействияПанелиДатаСтандартнойДатыНачалаПриИзменении", Новый Действие("ДействияПанелиДатаСтандартнойДатыНачалаПриИзменении"));
ДействияЭлементовФормы.Вставить("ДействияПанелиИзменениеЗначенияДинамическогоОтбора", Новый Действие("ДействияПанелиИзменениеЗначенияДинамическогоОтбора"));
ДействияЭлементовФормы.Вставить("ДействияПанелиОкончаниеРедактированиеТабличногоПоля", Новый Действие("ДействияПанелиОкончаниеРедактированиеТабличногоПоля"));
ДействияЭлементовФормы.Вставить("ДействияПанелиУдалениеСтрокиТабличногоПоля", Новый Действие("ДействияПанелиУдалениеСтрокиТабличногоПоля"));
ДействияЭлементовФормы.Вставить("ДействияПанелиКнопкаПодборНажатие", Новый Действие("ДействияПанелиКнопкаПодборНажатие"));
ДействияЭлементовФормы.Вставить("ПриНажатииНаКнопкуВверх", Новый Действие("ПриНажатииНаКнопкуВверх"));
ДействияЭлементовФормы.Вставить("ПриНажатииНаКнопкуВниз", Новый Действие("ПриНажатииНаКнопкуВниз"));
ДействияЭлементовФормы.Вставить("ГруппировкиПриПолученииДанных", Новый Действие("ГруппировкиПриПолученииДанных"));