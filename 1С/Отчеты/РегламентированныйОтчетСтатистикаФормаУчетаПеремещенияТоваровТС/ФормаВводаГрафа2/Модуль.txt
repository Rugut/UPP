Перем СтруктураРеквизитов;

Процедура КнопкаОКНажатие(Кнопка)
	
	Если ПрименитьИзменения() Тогда
		
		Модифицированность = Ложь;
		Закрыть(СтруктураРеквизитов);	
		
	КонецЕсли;
		
КонецПроцедуры

Процедура СтранаНазначенияПриИзменении()
	
	УстановкаДляРФ = Ложь;
	УстановкаДляРБ = Ложь;
	УстановкаДляРК = Ложь;
	
	Если ВладелецФормы.СтранаНазначения = "РФ" Тогда
	
		УстановкаДляРФ = Истина;
		ПолучательСтранаКод = "RU";
		ПолучательСтранаНаименование = "РОССИЯ";
		
	ИначеЕсли ВладелецФормы.СтранаНазначения = "РБ" Тогда
		
		УстановкаДляРБ = Истина;
		ПолучательСтранаКод = "BY";
		ПолучательСтранаНаименование = "БЕЛАРУСЬ";
		
	ИначеЕсли ВладелецФормы.СтранаНазначения = "РК" Тогда
		
		УстановкаДляРК = Истина;
		ПолучательСтранаКод = "KZ";
		ПолучательСтранаНаименование = "КАЗАХСТАН";
	
	КонецЕсли; 
	
	ЭлементыФормы.НадписьПолучательРФ_ОГРН.Видимость = УстановкаДляРФ;
	ЭлементыФормы.ПолучательРФ_ОГРН.Видимость = УстановкаДляРФ;
	ЭлементыФормы.НадписьПолучательРФ_ИНН.Видимость  = УстановкаДляРФ;
	ЭлементыФормы.ПолучательРФ_ИНН.Видимость  = УстановкаДляРФ;
	ЭлементыФормы.НадписьПолучательРФ_КПП.Видимость  = УстановкаДляРФ;
	ЭлементыФормы.ПолучательРФ_КПП.Видимость  = УстановкаДляРФ;
	
	ЭлементыФормы.НадписьПолучательРБ_УНП.Видимость  = УстановкаДляРБ;
	ЭлементыФормы.ПолучательРБ_УНП.Видимость  = УстановкаДляРБ;
	
	ЭлементыФормы.НадписьПолучательРК_БИН.Видимость  = УстановкаДляРК;
	ЭлементыФормы.ПолучательРК_БИН.Видимость  = УстановкаДляРК;
	ЭлементыФормы.НадписьПолучательРК_ИИН.Видимость  = УстановкаДляРК;
	ЭлементыФормы.ПолучательРК_ИИН.Видимость  = УстановкаДляРК;
	
КонецПроцедуры

Процедура ПередОткрытием(Отказ, СтандартнаяОбработка)
	
	СтранаНазначенияПриИзменении();
	
КонецПроцедуры

Процедура ПередЗакрытием(Отказ, СтандартнаяОбработка)
	
	Если Модифицированность Тогда
		
		ОтветНаВопрос = Вопрос("Данные были изменены. Применить изменения?", РежимДиалогаВопрос.ДаНетОтмена);
		Если ОтветНаВопрос = КодВозвратаДиалога.Да Тогда
			
			Если ПрименитьИзменения() Тогда
				
				ОповеститьОВыборе(СтруктураРеквизитов);
				
			Иначе	
				
				Отказ = Истина;	
				
			КонецЕсли;
			
		ИначеЕсли ОтветНаВопрос = КодВозвратаДиалога.Отмена Тогда
			
			Отказ = Истина;
			
		КонецЕсли;
		
	КонецЕсли;
	
	СтандартнаяОбработка = Ложь;
 
КонецПроцедуры

Функция ПолучитьПустуюСтруктуруРеквизитов() Экспорт
	
	ПустаяСтруктураРеквизитовФормы = Новый Структура;
	
	Для каждого ЭлементФормы Из ЭтаФорма.ЭлементыФормы Цикл
		
		Если ТипЗнч(ЭлементФормы) = Тип("Переключатель") ИЛИ ТипЗнч(ЭлементФормы) = Тип("ПолеВвода")  Тогда
		    Если НЕ ПустаяСтрока(ЭлементФормы.Данные) Тогда
				
				ПустаяСтруктураРеквизитовФормы.Вставить(ЭлементФормы.Данные);
								
			КонецЕсли; 
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат ПустаяСтруктураРеквизитовФормы;
	
КонецФункции	

Функция ПрименитьИзменения()
	    
	РеквСтрокаСообщения	= "";
	
	СтруктураРеквизитов = Новый Структура;
	
	Для каждого ЭлементФормы Из ЭтаФорма.ЭлементыФормы Цикл
		
		Если ТипЗнч(ЭлементФормы) = Тип("Переключатель") ИЛИ ТипЗнч(ЭлементФормы) = Тип("ПолеВвода")  Тогда
		    Если НЕ ПустаяСтрока(ЭлементФормы.Данные) Тогда
				
				ЗначениеРеквизита = ЭтаФорма[ЭлементФормы.Данные];
				СтруктураРеквизитов.Вставить(ЭлементФормы.Данные, ЗначениеРеквизита);
								
				//Проверка на заполненность полей, обязательных к заполнению
				Если ТипЗнч(ЭлементФормы) = Тип("ПолеВвода")  Тогда
					Если ЭлементФормы.Видимость И ЭлементФормы.АвтоОтметкаНезаполненного Тогда
						Если  НЕ ЗначениеЗаполнено(ЗначениеРеквизита) Тогда
							 ПредставлениеРекв = ЭлементФормы.Имя;
							 Попытка
							 	ПредставлениеРекв = ЭлементыФормы["Надпись" + ЭлементФормы.Имя].Заголовок;
							 Исключение	 КонецПопытки;
							
							 РеквСтрокаСообщения = РеквСтрокаСообщения + ?(ПустаяСтрока(РеквСтрокаСообщения), "", "," + Символы.ПС) + """" + ПредставлениеРекв + """";
							 
						КонецЕсли;	
					КонецЕсли; 
				КонецЕсли;
				
			КонецЕсли; 
		КонецЕсли;
		
	КонецЦикла; 
	
	Если НЕ ПустаяСтрока(РеквСтрокаСообщения) Тогда 
		
		ОтветНаВопрос = Вопрос("Не заполнены поля, обязательные к заполнению:" + Символы.ПС + Символы.ПС + 
			РеквСтрокаСообщения + "." + Символы.ПС + Символы.ПС + "Продолжить редактирование ?", РежимДиалогаВопрос.ДаНет);
		Если ОтветНаВопрос = КодВозвратаДиалога.Да Тогда
			Возврат Ложь;	
		КонецЕсли;
			
	КонецЕсли;		

	Возврат Истина;

КонецФункции

// Обработка выбора адресной информации из справочников

Процедура ПолучательНаименованиеНачалоВыбора(Элемент, СтандартнаяОбработка)
	
	ИмяСправочникаДляВыбора = "Контрагенты";
	Если ВладелецФормы.НапрПеремещения = "ИМ" Тогда
			ИмяСправочникаДляВыбора = "Организации";
	КонецЕсли; 
	Если НЕ ВладелецФормы.СуществуетСправочник(ИмяСправочникаДляВыбора) Тогда
		Возврат;
	КонецЕсли; 	
	
	СтандартнаяОбработка = Ложь;
	
	ВыбранноеЗначение = Неопределено;
	СтруктураСведений = ВладелецФормы.ПолучитьСведенияИзСправочника(Элемент, ИмяСправочникаДляВыбора, ВыбранноеЗначение);
	Если ВыбранноеЗначение = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	СтруктураАдреса = ВладелецФормы.АдресВФормате9ЗапятыхВСтруктуруПорталаТС(СтруктураСведений.Адрес);
	
	ПолучательНаименование = СтруктураСведений.Наименование;

	ПолучательИндекс =  СтруктураАдреса.Индекс;
	//ПолучательСтранаКод = СтруктураАдреса.СтранаКод;
	//ПолучательСтранаНаименование = СтруктураАдреса.СтранаНаименование;
	ПолучательОбласть = СтруктураАдреса.Область;
	ПолучательНаселенныйПункт = СтруктураАдреса.НаселенныйПункт;
	ПолучательУлицаНомерДома = СтруктураАдреса.УлицаНомерДома; 
	
	Если ВладелецФормы.СтранаНазначения = "РФ" Тогда
		ПолучательРФ_ИНН  = СтруктураСведений.ИНН;
		ПолучательРФ_КПП  = СтруктураСведений.КПП;
		ПолучательРФ_ОГРН = СтруктураСведений.ОГРН;
	ИначеЕсли ВладелецФормы.СтранаНазначения = "РБ" Тогда
		ПолучательРБ_УНП  = СтруктураСведений.ИНН;
	ИначеЕсли ВладелецФормы.СтранаНазначения = "РК" Тогда	
	    ПолучательРК_ИИН  = СтруктураСведений.ИНН;
	КонецЕсли; 
	
КонецПроцедуры