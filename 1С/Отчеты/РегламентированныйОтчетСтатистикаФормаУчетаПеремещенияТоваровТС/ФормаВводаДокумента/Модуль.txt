Перем СтруктураРеквизитов;

Процедура КнопкаОКНажатие(Кнопка)
	
	Если ПрименитьИзменения() Тогда
		
		Модифицированность = Ложь;
		Закрыть(СтруктураРеквизитов);	
		
	КонецЕсли;
		
КонецПроцедуры

Процедура ПередОткрытием(Отказ, СтандартнаяОбработка)
	
	ЭлементыФормы.ДокВыбор.СписокВыбора.Очистить();
	ЭлементыФормы.ДокВыбор.СписокВыбора.Добавить("", "<<Не выбрано>>");
	ЭлементыФормы.ДокВыбор.СписокВыбора.Добавить("Договор", "Договор");
	ЭлементыФормы.ДокВыбор.СписокВыбора.Добавить("Счёт-проформа (проформа-инвойс)", "Счёт-проформа (проформа-инвойс)");
	ЭлементыФормы.ДокВыбор.СписокВыбора.Добавить("Счёт-фактура (инвойс)", "Счёт-фактура (инвойс)");
	
КонецПроцедуры

Процедура ПередЗакрытием(Отказ, СтандартнаяОбработка)
	
	Если Модифицированность Тогда
		
		ОтветНаВопрос = Вопрос("Данные были изменены. Применить изменения?", РежимДиалогаВопрос.ДаНетОтмена);
		Если ОтветНаВопрос = КодВозвратаДиалога.Да Тогда
			
			Если ПрименитьИзменения() Тогда
				
				ОповеститьОВыборе(СтруктураРеквизитов);
				
			Иначе	
				
				Отказ = Истина;	
				
			КонецЕсли;
			
		ИначеЕсли ОтветНаВопрос = КодВозвратаДиалога.Отмена Тогда
			
			Отказ = Истина;
			
		КонецЕсли;
		
	КонецЕсли;
	
	СтандартнаяОбработка = Ложь;
 
КонецПроцедуры

Функция ПрименитьИзменения()
	
	РеквСтрокаСообщения	= "";
	
	СтруктураРеквизитов = Новый Структура;
	
	Для каждого ЭлементФормы Из ЭтаФорма.ЭлементыФормы Цикл
		
		Если ТипЗнч(ЭлементФормы) = Тип("ПолеВвода")  Тогда
			Если НЕ ПустаяСтрока(ЭлементФормы.Данные) Тогда
				
				ЗначениеРеквизита = ЭтаФорма[ЭлементФормы.Данные];
				СтруктураРеквизитов.Вставить(ЭлементФормы.Данные, ЗначениеРеквизита);
				
				//Проверка на заполненность полей, обязательных к заполнению
				Если ЭлементФормы.Видимость И ЭлементФормы.АвтоОтметкаНезаполненного Тогда
					Если  НЕ ЗначениеЗаполнено(ЗначениеРеквизита) Тогда
						ПредставлениеРекв = ЭлементФормы.Имя;
						Попытка
							ПредставлениеРекв = ЭлементыФормы["Надпись" + ЭлементФормы.Имя].Заголовок;
						Исключение	КонецПопытки;
						
						РеквСтрокаСообщения = РеквСтрокаСообщения + ?(ПустаяСтрока(РеквСтрокаСообщения), "", "," + Символы.ПС) + """" + ПредставлениеРекв + """";
						
					КонецЕсли;	
				КонецЕсли; 
				
			КонецЕсли; 
		КонецЕсли;
		
	КонецЦикла; 
	
	Если НЕ ПустаяСтрока(РеквСтрокаСообщения) Тогда 
		
		ОтветНаВопрос = Вопрос("Не заполнены поля, обязательные к заполнению:" + Символы.ПС + Символы.ПС + 
		РеквСтрокаСообщения + "." + Символы.ПС + Символы.ПС + "Продолжить редактирование ?", РежимДиалогаВопрос.ДаНет);
		Если ОтветНаВопрос = КодВозвратаДиалога.Да Тогда
			Возврат Ложь;	
		КонецЕсли;
		
	КонецЕсли;		
	
	Возврат Истина;
	
КонецФункции