#Если Клиент Тогда
	
////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ НАЧАЛЬНОЙ НАСТРОЙКИ ОТЧЕТА

// Функция заменяет комментарии в тексте запроса в зависимости от отражения в учете.
//
// Параметры
//  ИсходныхТекстЗапроса – Строка – Текст запроса.
//  СтруктураШапкиДокумента – Структура - Реквизиты документа "Расчет себестоимости".
//
// Возвращаемое значение:
//   Строка – Текст запроса, в котором заменены комментарии.
//
Функция ЗаменитьКомментарииВТекстеЗапроса(ИсходныйТекстЗапроса)
	
	// Управленческий учет
	Если ВидУчета = 0 Тогда
		ТекстЗапроса = СтрЗаменить(ИсходныйТекстЗапроса, "%СуффиксУчета%", "");
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "%СуффиксОрганизаций%", "");
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "//ДляУпрУчета", "");
		
	// Бухгалтерский учет	
	ИначеЕсли ВидУчета = 1 Тогда
		ТекстЗапроса = СтрЗаменить(ИсходныйТекстЗапроса, "%СуффиксУчета%", "БухгалтерскийУчет");
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "%СуффиксОрганизаций%", "Организаций");
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "//ДляРеглУчета", "");
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "//ДляБухУчета", "");
		
	// Налоговый учет	
	ИначеЕсли ВидУчета = 2 Тогда
		ТекстЗапроса = СтрЗаменить(ИсходныйТекстЗапроса, "%СуффиксУчета%", "НалоговыйУчет");
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "%СуффиксОрганизаций%", "Организаций");
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "//ДляРеглУчета", "");
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "//ДляНалУчета", "");
		
	КонецЕсли;

	Возврат ТекстЗапроса;

КонецФункции // ЗаменитьКомментарииВТекстеЗапроса()

// Процедура установки начальных настроек отчета с использованием текста запроса
//
Процедура УстановитьНачальныеНастройки(ДополнительныеПараметры = Неопределено) Экспорт
	
	// Настройка общих параметров универсального отчета
	УправлениеОтчетами.ВосстановитьРеквизитыОтчета(ЭтотОбъект, ДополнительныеПараметры);
	
	// Содержит название отчета, которое будет выводиться в шапке.
	УниверсальныйОтчет.мНазваниеОтчета = СокрЛП(ЭтотОбъект.Метаданные().Синоним) + " "
		+ ?(ВидУчета = 1, "(бухгалтерский учет)",
		  ?(ВидУчета = 2, "(налоговый учет)", "(управленческий учет)"));
	
	УниверсальныйОтчет.мВыбиратьИмяРегистра = Ложь;
	УниверсальныйОтчет.ИмяРегистра = "";
	УниверсальныйОтчет.ОтрицательноеКрасным = Истина;
	УниверсальныйОтчет.мВыбиратьИспользованиеСвойств = Ложь;
	УниверсальныйОтчет.мРежимФормыНастройкиБезГруппировокКолонок = Ложь;
	
	// Описание исходного текста запроса.
	ТекстЗапроса = "
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	//ДляРеглУчета УчетнаяПолитика.Организация КАК Организация,
	|	УчетнаяПолитика.СтатьяОбщепроизводственныеРасходы КАК СтатьяОбщепроизводственныеРасходы,
	|	УчетнаяПолитика.СтатьяОбщехозяйственныеРасходы КАК СтатьяОбщехозяйственныеРасходы,
	|	УчетнаяПолитика.СтатьяБракВПроизводстве КАК СтатьяБракВПроизводстве
	|
	|ПОМЕСТИТЬ УчетнаяПолитика
	|ИЗ
	|	РегистрСведений.УчетнаяПолитика%СуффиксОрганизаций%.СрезПоследних(&ДатаКон) КАК УчетнаяПолитика
	|ГДЕ
	|	УчетнаяПолитика.ДетализацияКосвенныхЗатратВСебестоимости = ЗНАЧЕНИЕ(Перечисление.ДетализацияКосвенныхЗатратВСебестоимости.ПоФиксированнойСтатьеЗатрат)
	|;
	|/////////////////////////////////////////////////////////////////////////////
	|
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	//ДляРеглУчета ЗатратыНаВыпускПродукции.Организация,
	|	//ДляРеглУчета ЗатратыНаВыпускПродукции.СчетУчета,
	|	ЗатратыНаВыпускПродукции.СтатьяЗатрат,
	|	ЗатратыНаВыпускПродукции.СтатьяЗатрат.ВидЗатрат КАК ВидЗатрат,
	|	ЗатратыНаВыпускПродукции.Затрата,
	|	ЗатратыНаВыпускПродукции.ХарактеристикаЗатраты,
	|	ЗатратыНаВыпускПродукции.СерияЗатраты,
	|	ЗатратыНаВыпускПродукции.НоменклатурнаяГруппаНЗП КАК НоменклатурнаяГруппа,
	|
	|	ВЫБОР КОГДА ЗатратыНаВыпускПродукции.ПодразделениеНЗП =
	|		//ДляУпрУчета ЗНАЧЕНИЕ(Справочник.Подразделения.ПустаяСсылка)
	|		//ДляРеглУчета ЗНАЧЕНИЕ(Справочник.ПодразделенияОрганизаций.ПустаяСсылка)
	|	ТОГДА
	|		ЗатратыНаВыпускПродукции.Подразделение
	|	ИНАЧЕ
	|		ЗатратыНаВыпускПродукции.ПодразделениеНЗП
	|	КОНЕЦ КАК Подразделение,
	|
	|	ЗатратыНаВыпускПродукции.Продукция,
	|	ЗатратыНаВыпускПродукции.ХарактеристикаПродукции,
	|	ЗатратыНаВыпускПродукции.СерияПродукции,
	|	ЗатратыНаВыпускПродукции.Спецификация,
	|	ЗатратыНаВыпускПродукции.ЗаказНЗП КАК Заказ,
	|	ЗатратыНаВыпускПродукции.Регистратор,
	|	ЗатратыНаВыпускПродукции.Количество,
	|	ЗатратыНаВыпускПродукции.Сумма КАК Стоимость,
	|	0 КАК КоличествоНЗП,
	|	0 КАК СтоимостьНЗП
	|
	|ПОМЕСТИТЬ ПроизводственныеЗатраты
	|ИЗ
	|	РегистрНакопления.ЗатратыНаВыпускПродукции%СуффиксУчета% КАК ЗатратыНаВыпускПродукции
	|ГДЕ
	|	ЗатратыНаВыпускПродукции.Период МЕЖДУ &ДатаНачала И &ДатаКонца
	|	И ЗатратыНаВыпускПродукции.Активность
	|	И (НЕ ЗатратыНаВыпускПродукции.КодОперации В (&МассивИсключаемыхКодовОперацийЗатратыНаВыпускПродукции))
	|{ГДЕ
	|	//ДляРеглУчета ЗатратыНаВыпускПродукции.Организация.* КАК Организация,
	|	//ДляРеглУчета ЗатратыНаВыпускПродукции.СчетУчета.* КАК СчетУчета,
	|	ЗатратыНаВыпускПродукции.СтатьяЗатрат.* КАК СтатьяЗатрат,
	|	ЗатратыНаВыпускПродукции.Затрата.* КАК Затрата,
	|	ЗатратыНаВыпускПродукции.ХарактеристикаЗатраты.* КАК ХарактеристикаЗатраты,
	|	ЗатратыНаВыпускПродукции.СерияЗатраты.* КАК СерияЗатраты,
	|	ЗатратыНаВыпускПродукции.НоменклатурнаяГруппаНЗП.* КАК НоменклатурнаяГруппа,
	|	Подразделение.* КАК Подразделение,
	|	ЗатратыНаВыпускПродукции.Продукция.* КАК Продукция,
	|	ЗатратыНаВыпускПродукции.ХарактеристикаПродукции.* КАК ХарактеристикаПродукции,
	|	ЗатратыНаВыпускПродукции.СерияПродукции.* КАК СерияПродукции,
	|	ЗатратыНаВыпускПродукции.Спецификация.* КАК Спецификация,
	|	ЗатратыНаВыпускПродукции.ЗаказНЗП.* КАК Заказ
	|}	
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	//ДляРеглУчета ЗатратыНаВыпускПродукцииНаработка.Организация,
	|	//ДляРеглУчета ЗатратыНаВыпускПродукцииНаработка.СчетУчета КАК СчетУчета,
	|	ЗатратыНаВыпускПродукцииНаработка.СтатьяЗатрат,
	|	ЗатратыНаВыпускПродукцииНаработка.СтатьяЗатрат.ВидЗатрат КАК ВидЗатрат,
	|	ВЫБОР КОГДА ЗатратыНаВыпускПродукцииНаработка.Затрата = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка) ТОГДА
	|		Неопределено
	|	ИНАЧЕ
	|		ЗатратыНаВыпускПродукцииНаработка.Затрата
	|	КОНЕЦ КАК Затрата,
	|	ЗатратыНаВыпускПродукцииНаработка.ХарактеристикаЗатраты,
	|	ЗатратыНаВыпускПродукцииНаработка.СерияЗатраты,
	|	ЗатратыНаВыпускПродукцииНаработка.НоменклатурнаяГруппаНЗП КАК НоменклатурнаяГруппа,
	|
	|	ВЫБОР КОГДА ЗатратыНаВыпускПродукцииНаработка.ПодразделениеНЗП =
	|		//ДляУпрУчета ЗНАЧЕНИЕ(Справочник.Подразделения.ПустаяСсылка)
	|		//ДляРеглУчета ЗНАЧЕНИЕ(Справочник.ПодразделенияОрганизаций.ПустаяСсылка)
	|	ТОГДА
	|		ЗатратыНаВыпускПродукцииНаработка.Подразделение
	|	ИНАЧЕ
	|		ЗатратыНаВыпускПродукцииНаработка.ПодразделениеНЗП
	|	КОНЕЦ КАК Подразделение,
	|
	|	ЗатратыНаВыпускПродукцииНаработка.Продукция,
	|	ЗатратыНаВыпускПродукцииНаработка.ХарактеристикаПродукции,
	|	NULL КАК СерияПродукции,
	|	NULL КАК Спецификация,
	|	ЗатратыНаВыпускПродукцииНаработка.ЗаказНЗП КАК Заказ,
	|	ЗатратыНаВыпускПродукцииНаработка.Регистратор,
	|	ВЫБОР КОГДА ЗатратыНаВыпускПродукцииНаработка.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) ТОГДА
	|		ЗатратыНаВыпускПродукцииНаработка.Количество
	|	ИНАЧЕ
	|		- ЗатратыНаВыпускПродукцииНаработка.Количество
	|	КОНЕЦ КАК Количество,
	|	ВЫБОР КОГДА ЗатратыНаВыпускПродукцииНаработка.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) ТОГДА
	|		ЗатратыНаВыпускПродукцииНаработка.Сумма
	|	ИНАЧЕ
	|		- ЗатратыНаВыпускПродукцииНаработка.Сумма
	|	КОНЕЦ КАК Стоимость,
	|	0 КАК КоличествоНЗП,
	|	0 КАК СтоимостьНЗП
	|ИЗ
	|	РегистрНакопления.ЗатратыНаВыпускПродукцииНаработка%СуффиксУчета% КАК ЗатратыНаВыпускПродукцииНаработка
	|ГДЕ
	|	ЗатратыНаВыпускПродукцииНаработка.Период МЕЖДУ &ДатаНачала И &ДатаКонца
	|	И ЗатратыНаВыпускПродукцииНаработка.Активность
	|	И (НЕ ЗатратыНаВыпускПродукцииНаработка.КодОперации В (&МассивИсключаемыхКодовОперацийЗатратыНаВыпускПродукции))
	|	И &УчитыватьНаработку
	|{ГДЕ
	|	//ДляРеглУчета ЗатратыНаВыпускПродукцииНаработка.Организация.* КАК Организация,
	|	//ДляРеглУчета ЗатратыНаВыпускПродукцииНаработка.СчетУчета.* КАК СчетУчета,
	|	ЗатратыНаВыпускПродукцииНаработка.СтатьяЗатрат.* КАК СтатьяЗатрат,
	|	ЗатратыНаВыпускПродукцииНаработка.Затрата.* КАК Затрата,
	|	ЗатратыНаВыпускПродукцииНаработка.ХарактеристикаЗатраты.* КАК ХарактеристикаЗатраты,
	|	ЗатратыНаВыпускПродукцииНаработка.СерияЗатраты.* КАК СерияЗатраты,
	|	ЗатратыНаВыпускПродукцииНаработка.НоменклатурнаяГруппа.* КАК НоменклатурнаяГруппа,
	|	Подразделение.* КАК Подразделение,
	|	ЗатратыНаВыпускПродукцииНаработка.Продукция.* КАК Продукция,
	|	ЗатратыНаВыпускПродукцииНаработка.ХарактеристикаПродукции.* КАК ХарактеристикаПродукции,
	|	ЗатратыНаВыпускПродукцииНаработка.Заказ.* КАК Заказ
	|}	
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	//ДляРеглУчета ЗатратыНаВыпускПродукцииНаработка.Организация,
	|	//ДляРеглУчета ЗатратыНаВыпускПродукцииНаработка.СчетУчета КАК СчетУчета,
	|	ЗатратыНаВыпускПродукцииНаработка.СтатьяЗатрат,
	|	ЗатратыНаВыпускПродукцииНаработка.СтатьяЗатрат.ВидЗатрат КАК ВидЗатрат,
	|	ВЫБОР КОГДА ЗатратыНаВыпускПродукцииНаработка.Затрата = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка) ТОГДА
	|		Неопределено
	|	ИНАЧЕ
	|		ЗатратыНаВыпускПродукцииНаработка.Затрата
	|	КОНЕЦ КАК Затрата,
	|	ЗатратыНаВыпускПродукцииНаработка.ХарактеристикаЗатраты,
	|	ЗатратыНаВыпускПродукцииНаработка.СерияЗатраты,
	|	ЗатратыНаВыпускПродукцииНаработка.НоменклатурнаяГруппаНЗП КАК НоменклатурнаяГруппа,
	|
	|	ВЫБОР КОГДА ЗатратыНаВыпускПродукцииНаработка.ПодразделениеНЗП =
	|		//ДляУпрУчета ЗНАЧЕНИЕ(Справочник.Подразделения.ПустаяСсылка)
	|		//ДляРеглУчета ЗНАЧЕНИЕ(Справочник.ПодразделенияОрганизаций.ПустаяСсылка)
	|	ТОГДА
	|		ЗатратыНаВыпускПродукцииНаработка.Подразделение
	|	ИНАЧЕ
	|		ЗатратыНаВыпускПродукцииНаработка.ПодразделениеНЗП
	|	КОНЕЦ КАК Подразделение,
	|
	|	ЗатратыНаВыпускПродукцииНаработка.Продукция,
	|	ЗатратыНаВыпускПродукцииНаработка.ХарактеристикаПродукции,
	|	NULL КАК СерияПродукции,
	|	NULL КАК Спецификация,
	|	ЗатратыНаВыпускПродукцииНаработка.ЗаказНЗП КАК Заказ,
	|	NULL КАК Регистратор,
	|	ЗатратыНаВыпускПродукцииНаработка.КоличествоНачальныйОстаток КАК Количество,
	|	ЗатратыНаВыпускПродукцииНаработка.СуммаНачальныйОстаток КАК Стоимость,
	|	0 КАК КоличествоНЗП,
	|	0 КАК СтоимостьНЗП
	|ИЗ
	|	РегистрНакопления.ЗатратыНаВыпускПродукцииНаработка%СуффиксУчета%.ОстаткиИОбороты(&ДатаНач, &ДатаКон, Период, ,
	|		&УчитыватьНаработку
	|		{
	|		//ДляРеглУчета Организация.* КАК Организация,
	|		//ДляРеглУчета СчетУчета.* КАК СчетУчета,
	|		СтатьяЗатрат.* КАК СтатьяЗатрат,
	|		Затрата.* КАК Затрата,
	|		ХарактеристикаЗатраты.* КАК ХарактеристикаЗатраты,
	|		СерияЗатраты.* КАК СерияЗатраты,
	|		НоменклатурнаяГруппа.* КАК НоменклатурнаяГруппа,
	|		Продукция.* КАК Продукция,
	|		ХарактеристикаПродукции.* КАК ХарактеристикаПродукции,
	|		Заказ.* КАК Заказ
	|		}	
	|	)КАК ЗатратыНаВыпускПродукцииНаработка
	|{ГДЕ
	|	Подразделение.* КАК Подразделение
	|}	
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	//ДляРеглУчета НезавершенноеПроизводствоОстатки.Организация,
	|	//ДляРеглУчета НезавершенноеПроизводствоОстатки.СчетУчета,
	|	НезавершенноеПроизводствоОстатки.СтатьяЗатрат,
	|	НезавершенноеПроизводствоОстатки.СтатьяЗатрат.ВидЗатрат КАК ВидЗатрат,
	|	НезавершенноеПроизводствоОстатки.Затрата,
	|	НезавершенноеПроизводствоОстатки.ХарактеристикаЗатраты,
	|	НезавершенноеПроизводствоОстатки.СерияЗатраты,
	|	НезавершенноеПроизводствоОстатки.НоменклатурнаяГруппа,
	|	НезавершенноеПроизводствоОстатки.Подразделение,
	|	NULL КАК Продукция,
	|	NULL КАК ХарактеристикаПродукции,
	|	NULL КАК СерияПродукции,
	|	NULL КАК Спецификация,
	|	НезавершенноеПроизводствоОстатки.Заказ КАК Заказ,
	|	NULL КАК Регистратор,
	|	0 КАК Количество,
	|	0 КАК Стоимость,
	|	НезавершенноеПроизводствоОстатки.КоличествоНачальныйОстаток КАК КоличествоНЗП,
	|	НезавершенноеПроизводствоОстатки.СтоимостьНачальныйОстаток КАК СтоимостьНЗП
	|ИЗ
	|	РегистрНакопления.НезавершенноеПроизводство%СуффиксУчета%.ОстаткиИОбороты(&ДатаНач, &ДатаКон, Период, ,
	|		{
	|		//ДляРеглУчета Организация.* КАК Организация, 
	|		//ДляРеглУчета СчетУчета.* КАК СчетУчета, 
	|		СтатьяЗатрат.* 			КАК СтатьяЗатрат, 
	|		Затрата.* 				КАК Затрата, 
	|		ХарактеристикаЗатраты.* КАК ХарактеристикаЗатраты, 
	|		СерияЗатраты.* 			КАК СерияЗатраты, 
	|		НоменклатурнаяГруппа.* 	КАК НоменклатурнаяГруппа, 
	|		Подразделение.* 		КАК Подразделение, 
	|		Заказ.* КАК Заказ
	|		}
	|	) КАК НезавершенноеПроизводствоОстатки
	|			
	|ОБЪЕДИНИТЬ ВСЕ
	|			
	|ВЫБРАТЬ
	|	//ДляРеглУчета НезавершенноеПроизводство.Организация,
	|	//ДляРеглУчета НезавершенноеПроизводство.СчетУчета,
	|	НезавершенноеПроизводство.СтатьяЗатрат,
	|	НезавершенноеПроизводство.СтатьяЗатрат.ВидЗатрат КАК ВидЗатрат,
	|	НезавершенноеПроизводство.Затрата,
	|	НезавершенноеПроизводство.ХарактеристикаЗатраты,
	|	НезавершенноеПроизводство.СерияЗатраты,
	|	НезавершенноеПроизводство.НоменклатурнаяГруппа,
	|	НезавершенноеПроизводство.Подразделение,
	|	NULL КАК Продукция,
	|	NULL КАК ХарактеристикаПродукции,
	|	NULL КАК СерияПродукции,
	|	NULL КАК Спецификация,
	|	НезавершенноеПроизводство.Заказ КАК Заказ,
	|	НезавершенноеПроизводство.Регистратор КАК Регистратор,
	|	0 КАК Количество,
	|	0 КАК Стоимость,
	|	ВЫБОР
	|		КОГДА НезавершенноеПроизводство.ВидДвижения = &Приход
	|			ТОГДА НезавершенноеПроизводство.Количество
	|		ИНАЧЕ 
	|			ВЫБОР КОГДА НезавершенноеПроизводство.КодОперации В (&МассивИсключаемыхКодовОперацийНЗП)
	|				И Не НезавершенноеПроизводство.Затрата ССЫЛКА Перечисление.ХарактерЗатрат
	|			ТОГДА
	|				-НезавершенноеПроизводство.Количество
	|			ИНАЧЕ
	|				0
	|			КОНЕЦ
	|	КОНЕЦ КАК КоличествоНЗП,
	|	ВЫБОР
	|		КОГДА НезавершенноеПроизводство.ВидДвижения = &Приход
	|			ТОГДА НезавершенноеПроизводство.Стоимость
	|		ИНАЧЕ
	|			ВЫБОР КОГДА НезавершенноеПроизводство.КодОперации В (&МассивИсключаемыхКодовОперацийНЗП)
	|				И Не НезавершенноеПроизводство.Затрата ССЫЛКА Перечисление.ХарактерЗатрат
	|			ТОГДА
	|				-НезавершенноеПроизводство.Стоимость
	|			ИНАЧЕ
	|				0
	|			КОНЕЦ
	|	КОНЕЦ КАК СтоимостьНЗП
	|ИЗ
	|	РегистрНакопления.НезавершенноеПроизводство%СуффиксУчета% КАК НезавершенноеПроизводство
	|ГДЕ
	|	НезавершенноеПроизводство.Период МЕЖДУ &ДатаНачала И &ДатаКонца
	|	И НезавершенноеПроизводство.Активность
	|{ГДЕ
	|	//ДляРеглУчета НезавершенноеПроизводство.Организация.* КАК Организация,
	|	//ДляРеглУчета НезавершенноеПроизводство.СчетУчета.* КАК СчетУчета,
	|	НезавершенноеПроизводство.СтатьяЗатрат.* 			КАК СтатьяЗатрат,
	|	НезавершенноеПроизводство.Затрата.* 				КАК Затрата,
	|	НезавершенноеПроизводство.ХарактеристикаЗатраты.* 	КАК ХарактеристикаЗатраты,
	|	НезавершенноеПроизводство.СерияЗатраты.* 			КАК СерияЗатраты,
	|	НезавершенноеПроизводство.НоменклатурнаяГруппа.* 	КАК НоменклатурнаяГруппа,
	|	НезавершенноеПроизводство.Подразделение.* 			КАК Подразделение,
	|	НезавершенноеПроизводство.Заказ.* КАК Заказ
	|}
	|;
	|/////////////////////////////////////////////////////////////////////////////
	|
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	//ДляРеглУчета ВложенныйЗапрос.Организация,
	|	//ДляРеглУчета ВложенныйЗапрос.СчетУчета,
	|
	|	ВЫБОР КОГДА ВложенныйЗапрос.Затрата = ЗНАЧЕНИЕ(Перечисление.ХарактерЗатрат.ОбщепроизводственныеРасходы) ТОГДА
	|		ЕСТЬNULL(УчетнаяПолитика.СтатьяОбщепроизводственныеРасходы, ВложенныйЗапрос.СтатьяЗатрат)
	|	КОГДА ВложенныйЗапрос.Затрата = ЗНАЧЕНИЕ(Перечисление.ХарактерЗатрат.ОбщехозяйственныеРасходы) ТОГДА
	|		ЕСТЬNULL(УчетнаяПолитика.СтатьяОбщехозяйственныеРасходы, ВложенныйЗапрос.СтатьяЗатрат)
	|	КОГДА ВложенныйЗапрос.Затрата = ЗНАЧЕНИЕ(Перечисление.ХарактерЗатрат.БракВПроизводстве) ТОГДА
	|		ЕСТЬNULL(УчетнаяПолитика.СтатьяБракВПроизводстве, ВложенныйЗапрос.СтатьяЗатрат)
	|	ИНАЧЕ
	|   	ВложенныйЗапрос.СтатьяЗатрат
	|	КОНЕЦ КАК СтатьяЗатрат,
	|	
	|	ВЫБОР КОГДА ВложенныйЗапрос.Затрата ССЫЛКА Перечисление.ХарактерЗатрат ТОГДА
	|		ЗНАЧЕНИЕ(Перечисление.ВидыЗатрат.Прочие)
	|	ИНАЧЕ
	|   	ВложенныйЗапрос.ВидЗатрат
	|	КОНЕЦ КАК ВидЗатрат,
	|
	|	ВложенныйЗапрос.Затрата,
	|	ВложенныйЗапрос.ХарактеристикаЗатраты,
	|	ВложенныйЗапрос.СерияЗатраты,
	|	ВложенныйЗапрос.НоменклатурнаяГруппа,
	|	ВложенныйЗапрос.Подразделение,
	|	ВложенныйЗапрос.Продукция,
	|	ВложенныйЗапрос.ХарактеристикаПродукции,
	|	ВложенныйЗапрос.СерияПродукции,
	|	ВложенныйЗапрос.Спецификация,
	|	ВложенныйЗапрос.Заказ,
	|	ВложенныйЗапрос.Регистратор,
	|	СУММА(ВложенныйЗапрос.Количество) КАК Количество,
	|	СУММА(ВложенныйЗапрос.Стоимость) КАК Стоимость,
	|	СУММА(ВложенныйЗапрос.КоличествоНЗП) КАК КоличествоНЗП,
	|	СУММА(ВложенныйЗапрос.СтоимостьНЗП) КАК СтоимостьНЗП,
	|	//ДляРеглУчета ПРЕДСТАВЛЕНИЕ(ВложенныйЗапрос.Организация) КАК ОрганизацияПредставление,
	|	//ДляРеглУчета ПРЕДСТАВЛЕНИЕ(ВложенныйЗапрос.СчетУчета) КАК СчетУчетаПредставление,
	|	ПРЕДСТАВЛЕНИЕ(
	|		ВЫБОР КОГДА ВложенныйЗапрос.Затрата = ЗНАЧЕНИЕ(Перечисление.ХарактерЗатрат.ОбщепроизводственныеРасходы) ТОГДА
	|			ЕСТЬNULL(УчетнаяПолитика.СтатьяОбщепроизводственныеРасходы, ВложенныйЗапрос.СтатьяЗатрат)
	|		КОГДА ВложенныйЗапрос.Затрата = ЗНАЧЕНИЕ(Перечисление.ХарактерЗатрат.ОбщехозяйственныеРасходы) ТОГДА
	|			ЕСТЬNULL(УчетнаяПолитика.СтатьяОбщехозяйственныеРасходы, ВложенныйЗапрос.СтатьяЗатрат)
	|		КОГДА ВложенныйЗапрос.Затрата = ЗНАЧЕНИЕ(Перечисление.ХарактерЗатрат.БракВПроизводстве) ТОГДА
	|			ЕСТЬNULL(УчетнаяПолитика.СтатьяБракВПроизводстве, ВложенныйЗапрос.СтатьяЗатрат)
	|		ИНАЧЕ
	|   		ВложенныйЗапрос.СтатьяЗатрат
	|		КОНЕЦ
	|	) КАК СтатьяЗатратПредставление,
	|	ПРЕДСТАВЛЕНИЕ(ВложенныйЗапрос.Затрата) 					КАК ЗатратаПредставление,
	|	ПРЕДСТАВЛЕНИЕ(ВложенныйЗапрос.ХарактеристикаЗатраты) 	КАК ХарактеристикаЗатратыПредставление,
	|	ПРЕДСТАВЛЕНИЕ(ВложенныйЗапрос.СерияЗатраты) 			КАК СерияЗатратыПредставление,
	|	ПРЕДСТАВЛЕНИЕ(ВложенныйЗапрос.НоменклатурнаяГруппа) 	КАК НоменклатурнаяГруппаПредставление,
	|	ПРЕДСТАВЛЕНИЕ(ВложенныйЗапрос.Подразделение) 			КАК ПодразделениеПредставление,
	|	ПРЕДСТАВЛЕНИЕ(ВложенныйЗапрос.Продукция) 				КАК ПродукцияПредставление,
	|	ПРЕДСТАВЛЕНИЕ(ВложенныйЗапрос.ХарактеристикаПродукции) 	КАК ХарактеристикаПродукцииПредставление,
	|	ПРЕДСТАВЛЕНИЕ(ВложенныйЗапрос.СерияПродукции) 			КАК СерияПродукцииПредставление,
	|	ПРЕДСТАВЛЕНИЕ(ВложенныйЗапрос.Спецификация) 			КАК СпецификацияПредставление,
	|	ПРЕДСТАВЛЕНИЕ(ВложенныйЗапрос.Заказ) 					КАК ЗаказПредставление,
	|	ПРЕДСТАВЛЕНИЕ(ВложенныйЗапрос.Регистратор) 				КАК РегистраторПредставление
	|	//ПОЛЯ_СВОЙСТВА
	|	//ПОЛЯ_КАТЕГОРИИ
	|{ВЫБРАТЬ
	|	//ДляРеглУчета Организация.*,
	|	//ДляРеглУчета СчетУчета.*,
	|	СтатьяЗатрат.*,
	|	Затрата.*,
	|	ХарактеристикаЗатраты.*,
	|	СерияЗатраты.*,
	|	НоменклатурнаяГруппа.*,
	|	Подразделение.*,
	|	Продукция.*,
	|	ХарактеристикаПродукции.*,
	|	СерияПродукции.*,
	|	Спецификация.*,
	|	Заказ.*,
	|	Регистратор.*,
	|	Количество,
	|	Стоимость,
	|	КоличествоНЗП,
	|	СтоимостьНЗП
	|	//ПСЕВДОНИМЫ_СВОЙСТВА
	|	//ПСЕВДОНИМЫ_КАТЕГОРИИ
	|}
	|ИЗ
	|	ПроизводственныеЗатраты КАК ВложенныйЗапрос
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		УчетнаяПолитика КАК УчетнаяПолитика
	|	ПО
	|		//ДляУпрУчета Истина ИЛИ УчетнаяПолитика.СтатьяОбщепроизводственныеРасходы = ЗНАЧЕНИЕ(Справочник.СтатьиЗатрат.ПустаяСсылка)
	|		//ДляРеглУчета ВложенныйЗапрос.Организация = УчетнаяПолитика.Организация
	|
	|СГРУППИРОВАТЬ ПО
	|	//ДляРеглУчета ВложенныйЗапрос.Организация,
	|	//ДляРеглУчета ВложенныйЗапрос.СчетУчета,
	|	ВложенныйЗапрос.СерияПродукции,
	|	ВложенныйЗапрос.НоменклатурнаяГруппа,
	|	ВложенныйЗапрос.СерияЗатраты,
	|	ВложенныйЗапрос.ХарактеристикаЗатраты,
	|	ВложенныйЗапрос.Затрата,
	|	ВЫБОР КОГДА ВложенныйЗапрос.Затрата = ЗНАЧЕНИЕ(Перечисление.ХарактерЗатрат.ОбщепроизводственныеРасходы) ТОГДА
	|		ЕСТЬNULL(УчетнаяПолитика.СтатьяОбщепроизводственныеРасходы, ВложенныйЗапрос.СтатьяЗатрат)
	|	КОГДА ВложенныйЗапрос.Затрата = ЗНАЧЕНИЕ(Перечисление.ХарактерЗатрат.ОбщехозяйственныеРасходы) ТОГДА
	|		ЕСТЬNULL(УчетнаяПолитика.СтатьяОбщехозяйственныеРасходы, ВложенныйЗапрос.СтатьяЗатрат)
	|	КОГДА ВложенныйЗапрос.Затрата = ЗНАЧЕНИЕ(Перечисление.ХарактерЗатрат.БракВПроизводстве) ТОГДА
	|		ЕСТЬNULL(УчетнаяПолитика.СтатьяБракВПроизводстве, ВложенныйЗапрос.СтатьяЗатрат)
	|	ИНАЧЕ
	|   	ВложенныйЗапрос.СтатьяЗатрат
	|	КОНЕЦ,
	|	ВЫБОР КОГДА ВложенныйЗапрос.Затрата ССЫЛКА Перечисление.ХарактерЗатрат ТОГДА
	|		ЗНАЧЕНИЕ(Перечисление.ВидыЗатрат.Прочие)
	|	ИНАЧЕ
	|   	ВложенныйЗапрос.ВидЗатрат
	|	КОНЕЦ,
	|	ВложенныйЗапрос.Подразделение,
	|	ВложенныйЗапрос.Продукция,
	|	ВложенныйЗапрос.ХарактеристикаПродукции,
	|	ВложенныйЗапрос.Регистратор,
	|	ВложенныйЗапрос.Спецификация,
	|	ВложенныйЗапрос.Заказ,
	|	ПРЕДСТАВЛЕНИЕ(
	|		ВЫБОР КОГДА ВложенныйЗапрос.Затрата = ЗНАЧЕНИЕ(Перечисление.ХарактерЗатрат.ОбщепроизводственныеРасходы) ТОГДА
	|			ЕСТЬNULL(УчетнаяПолитика.СтатьяОбщепроизводственныеРасходы, ВложенныйЗапрос.СтатьяЗатрат)
	|		КОГДА ВложенныйЗапрос.Затрата = ЗНАЧЕНИЕ(Перечисление.ХарактерЗатрат.ОбщехозяйственныеРасходы) ТОГДА
	|			ЕСТЬNULL(УчетнаяПолитика.СтатьяОбщехозяйственныеРасходы, ВложенныйЗапрос.СтатьяЗатрат)
	|		КОГДА ВложенныйЗапрос.Затрата = ЗНАЧЕНИЕ(Перечисление.ХарактерЗатрат.БракВПроизводстве) ТОГДА
	|			ЕСТЬNULL(УчетнаяПолитика.СтатьяБракВПроизводстве, ВложенныйЗапрос.СтатьяЗатрат)
	|		ИНАЧЕ
	|   		ВложенныйЗапрос.СтатьяЗатрат
	|		КОНЕЦ
	|	),
	|	ПРЕДСТАВЛЕНИЕ(ВложенныйЗапрос.Затрата),
	|	ПРЕДСТАВЛЕНИЕ(ВложенныйЗапрос.ХарактеристикаЗатраты),
	|	ПРЕДСТАВЛЕНИЕ(ВложенныйЗапрос.СерияЗатраты),
	|	ПРЕДСТАВЛЕНИЕ(ВложенныйЗапрос.НоменклатурнаяГруппа),
	|	ПРЕДСТАВЛЕНИЕ(ВложенныйЗапрос.Подразделение),
	|	ПРЕДСТАВЛЕНИЕ(ВложенныйЗапрос.Продукция),
	|	ПРЕДСТАВЛЕНИЕ(ВложенныйЗапрос.ХарактеристикаПродукции),
	|	ПРЕДСТАВЛЕНИЕ(ВложенныйЗапрос.СерияПродукции),
	|	ПРЕДСТАВЛЕНИЕ(ВложенныйЗапрос.Спецификация),
	|	ПРЕДСТАВЛЕНИЕ(ВложенныйЗапрос.Заказ),
	|	ПРЕДСТАВЛЕНИЕ(ВложенныйЗапрос.Регистратор)
	|
	|ИМЕЮЩИЕ
	|	(СУММА(ВложенныйЗапрос.Количество) <> 0
	|		ИЛИ СУММА(ВложенныйЗапрос.Стоимость) <> 0
	|		ИЛИ СУММА(ВложенныйЗапрос.КоличествоНЗП) <> 0
	|		ИЛИ СУММА(ВложенныйЗапрос.СтоимостьНЗП) <> 0) И
	|	((НЕ &ТолькоРасхождение)
	|		ИЛИ &ТолькоРасхождение
	|			И (СУММА(ВложенныйЗапрос.Количество) <> СУММА(ВложенныйЗапрос.КоличествоНЗП)
	|				ИЛИ СУММА(ВложенныйЗапрос.Количество) = 0
	|					И СУММА(ВложенныйЗапрос.КоличествоНЗП) = 0
	|					И СУММА(ВложенныйЗапрос.Стоимость) <> СУММА(ВложенныйЗапрос.СтоимостьНЗП)))
	|	//СОЕДИНЕНИЯ
	|{ГДЕ
	|	ВложенныйЗапрос.Регистратор,
	|	ВложенныйЗапрос.Количество,
	|	ВложенныйЗапрос.Стоимость,
	|	ВложенныйЗапрос.КоличествоНЗП,
	|	ВложенныйЗапрос.СтоимостьНЗП
	|	//УСЛОВИЯ_СВОЙСТВА
	|	//УСЛОВИЯ_КАТЕГОРИИ
	|}
	|{УПОРЯДОЧИТЬ ПО
	|	//ДляРеглУчета Организация.*,
	|	//ДляРеглУчета СчетУчета.*,
	|	СтатьяЗатрат.*,
	|	Затрата.*,
	|	ХарактеристикаЗатраты.*,
	|	СерияЗатраты.*,
	|	НоменклатурнаяГруппа.*,
	|	Подразделение.*,
	|	Продукция.*,
	|	ХарактеристикаПродукции.*,
	|	СерияПродукции.*,
	|	Спецификация.*,
	|	Заказ.*,
	|	Регистратор.*,
	|	Количество,
	|	Стоимость,
	|	КоличествоНЗП,
	|	СтоимостьНЗП
	|	//ПСЕВДОНИМЫ_СВОЙСТВА
	|	//ПСЕВДОНИМЫ_КАТЕГОРИИ
	|}
	|ИТОГИ
	|	СУММА(Количество),
	|	СУММА(Стоимость),
	|	СУММА(КоличествоНЗП),
	|	СУММА(СтоимостьНЗП)
	|	//ИТОГИ_СВОЙСТВА
	|	//ИТОГИ_КАТЕГОРИИ
	|ПО
	|	ОБЩИЕ
	|{ИТОГИ ПО
	|	//ДляРеглУчета Организация.*,
	|	//ДляРеглУчета СчетУчета.*,
	|	СтатьяЗатрат.*,
	|	Затрата.*,
	|	ХарактеристикаЗатраты.*,
	|	СерияЗатраты.*,
	|	НоменклатурнаяГруппа.*,
	|	Подразделение.*,
	|	Продукция.*,
	|	ХарактеристикаПродукции.*,
	|	СерияПродукции.*,
	|	Спецификация.*,
	|	Заказ.*,
	|	Регистратор.*
	|	//ПСЕВДОНИМЫ_СВОЙСТВА
	|	//ПСЕВДОНИМЫ_КАТЕГОРИИ
	|}";
	
	// В универсальном отчете включен флаг использования свойств и категорий.
	Если УниверсальныйОтчет.ИспользоватьСвойстваИКатегории Тогда
		
		// Добавление свойств и категорий поля запроса в таблицу полей.
		// Необходимо вызывать для каждого поля запроса, предоставляющего возможность использования свойств и категорий.
		
		// УниверсальныйОтчет.ДобавитьСвойстваИКатегорииДляПоля(<ПсевдонимТаблицы>.<Поле> , <ПсевдонимПоля>, <Представление>, <Назначение>);
        УниверсальныйОтчет.ДобавитьСвойстваИКатегорииДляПоля("Организация", 			"Организация", "Организация", ПланыВидовХарактеристик.НазначенияСвойствКатегорийОбъектов.Справочник_Организации);
		УниверсальныйОтчет.ДобавитьСвойстваИКатегорииДляПоля("НоменклатурнаяГруппа", 	"НоменклатурнаяГруппа", "Номенклатурная группа", ПланыВидовХарактеристик.НазначенияСвойствКатегорийОбъектов.Справочник_НоменклатурныеГруппы);
        УниверсальныйОтчет.ДобавитьСвойстваИКатегорииДляПоля("Продукция", 				"Продукция", "Продукция", ПланыВидовХарактеристик.НазначенияСвойствКатегорийОбъектов.Справочник_Номенклатура);
        УниверсальныйОтчет.ДобавитьСвойстваИКатегорииДляПоля("ХарактеристикаПродукции", "ХарактеристикаПродукции", "Характеристика продукции",   ПланыВидовХарактеристик.НазначенияСвойствКатегорийОбъектов.Справочник_ХарактеристикиНоменклатуры);
        УниверсальныйОтчет.ДобавитьСвойстваИКатегорииДляПоля("Затрата", 				"Затрата", "Затрата", ПланыВидовХарактеристик.НазначенияСвойствКатегорийОбъектов.Справочник_Номенклатура);
        УниверсальныйОтчет.ДобавитьСвойстваИКатегорииДляПоля("ХарактеристикаЗатраты", 	"ХарактеристикаЗатраты", "Характеристика затраты", ПланыВидовХарактеристик.НазначенияСвойствКатегорийОбъектов.Справочник_ХарактеристикиНоменклатуры);
		
		// Добавление свойств и категорий в исходный текст запроса.
		УниверсальныйОтчет.ДобавитьВТекстЗапросаСвойстваИКатегории(ТекстЗапроса);
		
	КонецЕсли;
	
	ТекстЗапроса = ЗаменитьКомментарииВТекстеЗапроса(ТекстЗапроса);
		
	// Инициализация текста запроса построителя отчета
	УниверсальныйОтчет.ПостроительОтчета.Текст = ТекстЗапроса;
	
	// Представления полей отчета.
	// Необходимо вызывать для каждого поля запроса.
	// УниверсальныйОтчет.мСтруктураПредставлениеПолей.Вставить(<ИмяПоля>, <ПредставлениеПоля>);
	
	// Представления предопределенных полей запроса.
	УниверсальныйОтчет.мСтруктураПредставлениеПолей.Вставить("Организация", 			"Организация");
	УниверсальныйОтчет.мСтруктураПредставлениеПолей.Вставить("СчетУчета", 				"Счет учета");
	УниверсальныйОтчет.мСтруктураПредставлениеПолей.Вставить("СтатьяЗатрат", 			"Статья затрат");
	УниверсальныйОтчет.мСтруктураПредставлениеПолей.Вставить("Затрата", 				"Затрата");
	УниверсальныйОтчет.мСтруктураПредставлениеПолей.Вставить("ХарактеристикаЗатраты", 	"Характеристика затраты");
	УниверсальныйОтчет.мСтруктураПредставлениеПолей.Вставить("СерияЗатраты", 			"Серия затраты");
	УниверсальныйОтчет.мСтруктураПредставлениеПолей.Вставить("НоменклатурнаяГруппа", 	"Номенклатурная группа");
	УниверсальныйОтчет.мСтруктураПредставлениеПолей.Вставить("Подразделение", 			"Подразделение");
	УниверсальныйОтчет.мСтруктураПредставлениеПолей.Вставить("Продукция", 				"Продукция");
	УниверсальныйОтчет.мСтруктураПредставлениеПолей.Вставить("ХарактеристикаПродукции", "Характеристика продукции");
	УниверсальныйОтчет.мСтруктураПредставлениеПолей.Вставить("СерияПродукции", 			"Серия продукции");
	УниверсальныйОтчет.мСтруктураПредставлениеПолей.Вставить("Спецификация", 			"Спецификация");
	УниверсальныйОтчет.мСтруктураПредставлениеПолей.Вставить("Заказ", 					"Заказ");
	
	УниверсальныйОтчет.мСтруктураПредставлениеПолей.Вставить("Количество", 				"Количество");
	УниверсальныйОтчет.мСтруктураПредставлениеПолей.Вставить("Стоимость", 				"Стоимость");
	УниверсальныйОтчет.мСтруктураПредставлениеПолей.Вставить("КоличествоНЗП", 			"Количество НЗП");
	УниверсальныйОтчет.мСтруктураПредставлениеПолей.Вставить("СтоимостьНЗП", 			"Стоимость НЗП");
	
	// Добавление показателей
	// Необходимо вызывать для каждого добавляемого показателя.
	// УниверсальныйОтчет.ДобавитьПоказатель(<ИмяПоказателя>, <ПредставлениеПоказателя>, <ВключенПоУмолчанию>, <Формат>, <ИмяГруппы>, <ПредставлениеГруппы>);
	УниверсальныйОтчет.ДобавитьПоказатель("Количество", 	"Количество", Истина, "ЧЦ=15; ЧДЦ=3", , );
	УниверсальныйОтчет.ДобавитьПоказатель("Стоимость", 		"Стоимость", Истина, "ЧЦ=15; ЧДЦ=2", , );
	УниверсальныйОтчет.ДобавитьПоказатель("КоличествоНЗП", 	"Количество НЗП", Истина, "ЧЦ=15; ЧДЦ=3", , );
	УниверсальныйОтчет.ДобавитьПоказатель("СтоимостьНЗП", 	"Стоимость НЗП", Истина, "ЧЦ=15; ЧДЦ=2", , );
	
	// Добавление предопредедленных группировок строк отчета.
	// Необходимо вызывать для каждой добавляемой группировки строки.
	// УниверсальныйОтчет.ДобавитьИзмерениеСтроки(<ПутьКДанным>);
	УниверсальныйОтчет.ДобавитьИзмерениеСтроки("Организация");
	УниверсальныйОтчет.ДобавитьИзмерениеСтроки("Подразделение");
	УниверсальныйОтчет.ДобавитьИзмерениеСтроки("СтатьяЗатрат");
	УниверсальныйОтчет.ДобавитьИзмерениеСтроки("Затрата");
	
	// Добавление предопредедленных группировок колонок отчета.
	// Необходимо вызывать для каждой добавляемой группировки колонки.
	// УниверсальныйОтчет.ДобавитьИзмерениеКолонки(<ПутьКДанным>);
	
	// Добавление предопредедленных отборов отчета.
	// Необходимо вызывать для каждого добавляемого отбора.
	// УниверсальныйОтчет.ДобавитьОтбор(<ПутьКДанным>);
	УниверсальныйОтчет.ДобавитьОтбор("Организация");
	УниверсальныйОтчет.ДобавитьОтбор("Подразделение");
	УниверсальныйОтчет.ДобавитьОтбор("СтатьяЗатрат");
	УниверсальныйОтчет.ДобавитьОтбор("Затрата");
	
	// Установка связи подчиненных и родительских полей
	// УниверсальныйОтчет.УстановитьСвязьПолей(<ПутьКДанным>, <ПутьКДаннымРодитель>);
	
	// Установка представлений полей
	УниверсальныйОтчет.УстановитьПредставленияПолей(УниверсальныйОтчет.мСтруктураПредставлениеПолей, УниверсальныйОтчет.ПостроительОтчета);
	
	// Установка типов значений свойств в отборах отчета
	УниверсальныйОтчет.УстановитьТипыЗначенийСвойствДляОтбора();
	
	// Заполнение начальных настроек универсального отчета
	УниверсальныйОтчет.УстановитьНачальныеНастройки(Ложь);
	
	// Добавление дополнительных полей
	// Необходимо вызывать для каждого добавляемого дополнительного поля.
	// УниверсальныйОтчет.ДобавитьДополнительноеПоле(<ПутьКДанным>);
	
КонецПроцедуры // УстановитьНачальныеНастройки()

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ФОРМИРОВАНИЯ ОТЧЕТА 
	
// Процедура формирования отчета
//
Процедура СформироватьОтчет(ТабличныйДокумент) Экспорт
	
	// Перед формирование отчета можно установить необходимые параметры универсального отчета.
	УниверсальныйОтчет.ПостроительОтчета.Параметры.Вставить("МатЗатраты", Перечисления.ВидыЗатрат.Материальные);
	УниверсальныйОтчет.ПостроительОтчета.Параметры.Вставить("ТолькоРасхождение", ПоказыватьТолькоРасхождение);
	УниверсальныйОтчет.ПостроительОтчета.Параметры.Вставить("УчитыватьНаработку", УчитыватьНаработку);
	
	МассивКодовОперацииНезавершенноеПроизводство = Новый Массив;
	МассивКодовОперацииНезавершенноеПроизводство.Добавить(Перечисления.КодыОперацийНезавершенноеПроизводство.ВозвратМатериаловИзПроизводстваФикс);
	МассивКодовОперацииНезавершенноеПроизводство.Добавить(Перечисления.КодыОперацийНезавершенноеПроизводство.ОприходованиеНЗПФикс);
	МассивКодовОперацииНезавершенноеПроизводство.Добавить(Перечисления.КодыОперацийНезавершенноеПроизводство.КорректировкаНЗПФикс);
	МассивКодовОперацииНезавершенноеПроизводство.Добавить(Перечисления.КодыОперацийНезавершенноеПроизводство.РаспределениеНЗПРегламент);
	
	МассивКодовОперацииЗатратыНаВыпускПродукции = Новый Массив;
	МассивКодовОперацииЗатратыНаВыпускПродукции.Добавить(Перечисления.КодыОперацийЗатратыНаВыпускПродукции.ОприходованиеНЗП);
	
	УниверсальныйОтчет.ПостроительОтчета.Параметры.Вставить("МассивИсключаемыхКодовОперацийНЗП", МассивКодовОперацииНезавершенноеПроизводство);
	УниверсальныйОтчет.ПостроительОтчета.Параметры.Вставить("МассивИсключаемыхКодовОперацийЗатратыНаВыпускПродукции", МассивКодовОперацииЗатратыНаВыпускПродукции);
	УниверсальныйОтчет.ПостроительОтчета.Параметры.Вставить("Приход", ВидДвиженияНакопления.Приход);
	
	УниверсальныйОтчет.СформироватьОтчет(ТабличныйДокумент,,, ЭтотОбъект);

КонецПроцедуры // СформироватьОтчет()

Функция ПолучитьТекстСправкиФормы() Экспорт
	
	ТекстСправкиФормы = "";
	
	Если глЗначениеПеременной("ИспользоватьРасширеннуюАналитикуУчетаНоменклатурыИЗатрат") Тогда
		НачалоИспользования = Формат(глЗначениеПеременной("ДатаНачалаИспользованияРасширеннойАналитикиУчетаНоменклатурыИЗатрат"), "ДФ=dd.MM.yyyy");
		
		ТекстСправкиФормы = "<P>С " + НачалоИспользования + " установлено использование расширенной аналитики учета затрат."
					+ "<BR><b>Использование отчета для расширенной аналитики не поддерживается<b>.<P>";
	КонецЕсли; 
	
	Возврат ТекстСправкиФормы;
	
КонецФункции


////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОБЩЕГО НАЗНАЧЕНИЯ

// Процедура обработки расшифровки
//
Процедура ОбработкаРасшифровки(Расшифровка, Объект) Экспорт
	
	// Дополнительные параметры в расшифровывающий отчет можно передать
	// посредством инициализации переменной "ДополнительныеПараметры".
	
	ДополнительныеПараметры = УправлениеОтчетами.СохранитьРеквизитыОтчета(ЭтотОбъект);
	УниверсальныйОтчет.ОбработкаРасшифровкиУниверсальногоОтчета(Расшифровка, Объект, ДополнительныеПараметры);
	
КонецПроцедуры // ОбработкаРасшифровки()

// Формирует структуру для сохранения настроек отчета
//
Процедура СформироватьСтруктуруДляСохраненияНастроек(СтруктураСНастройками) Экспорт
	
	УниверсальныйОтчет.СформироватьСтруктуруДляСохраненияНастроек(СтруктураСНастройками);
	УправлениеОтчетами.СохранитьРеквизитыОтчета(ЭтотОбъект, СтруктураСНастройками);
	
КонецПроцедуры // СформироватьСтруктуруДляСохраненияНастроек()

// Заполняет настройки отчета из структуры сохраненных настроек
//
Функция ВосстановитьНастройкиИзСтруктуры(СтруктураСНастройками) Экспорт
	
	УправлениеОтчетами.ВосстановитьРеквизитыОтчета(ЭтотОбъект, СтруктураСНастройками);
	Возврат УниверсальныйОтчет.ВосстановитьНастройкиИзСтруктуры(СтруктураСНастройками, ЭтотОбъект);
	
КонецФункции // ВосстановитьНастройкиИзСтруктуры()

// Содержит значение используемого режима ввода периода.
// Тип: Число.
// Возможные значения: 0 - произвольный период, 1 - на дату, 2 - неделя, 3 - декада, 4 - месяц, 5 - квартал, 6 - полугодие, 7 - год
// Значение по умолчанию: 0
// Пример:
// УниверсальныйОтчет.мРежимВводаПериода = 1;

#КонецЕсли