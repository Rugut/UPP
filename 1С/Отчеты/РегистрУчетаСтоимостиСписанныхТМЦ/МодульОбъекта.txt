#Если Клиент Тогда
	
//Обявление переменных
Перем НП Экспорт;

Перем Счет07;
Перем Счет08;
Перем Счет08_01;
Перем Счет08_02;
Перем Счет08_03;
Перем Счет08_04;
Перем Счет08_05;
Перем Счет08_08;
Перем Счет10;
Перем Счет20;
Перем Счет21;
Перем Счет23;
Перем Счет25;
Перем Счет26;
Перем Счет28;
Перем Счет29;
Перем Счет20_01_01;
Перем Счет23_01;
Перем Счет25_01;
Перем Счет28_01;
Перем Счет29_01;
Перем Счет20_01_02;
Перем Счет23_02;
Перем Счет25_02;
Перем Счет26_02;
Перем Счет28_02;
Перем Счет29_02;
Перем Счет41;
Перем Счет43;
Перем Счет44;
Перем Счет45;
Перем Счет45_21;
Перем Счет45_41;
Перем Счет45_43;
Перем Счет44_01;
Перем Счет58_01_1;
Перем Счет58_01_2;
Перем Счет58_02;
Перем Счет58_05;
Перем Счет90_02;
Перем Счет91_02;
Перем Счет91_02_1;
Перем Счет91_02_2;
Перем Счет91_02_3;
Перем Счет91_02_4;
Перем Счет91_02_5;
Перем Счет91_02_6;
Перем Счет94;
Перем Счет97;
Перем СчетНПВ;

Функция СчетПринадлежит(СчетВладелец, Счет)
	Если Счет.ПринадлежитЭлементу(СчетВладелец) ИЛИ Счет = СчетВладелец Тогда
		Возврат Истина;
	Иначе
		Возврат Ложь;
	КонецЕсли;
	
КонецФункции

Функция ВидРасходаПоОперации(Счет, Субконто1, Субконто2, Субконто3, КодОперации)

	ВидРасхода  = "";
	ОбъектУчета = "";
	
	Попытка
		ВидЗатратНалоговогоУчета = Субконто1.ВидРасходовНУ;
	Исключение
		Попытка
			ВидЗатратНалоговогоУчета = Субконто2.ВидРасходовНУ;
        Исключение
			Попытка
				ВидЗатратНалоговогоУчета = Субконто3.ВидРасходовНУ;
			Исключение
				ВидЗатратНалоговогоУчета = "";
			КонецПопытки;
		КонецПопытки;
	КонецПопытки;

	Если ВидЗатратНалоговогоУчета = Перечисления.ВидыРасходовНУ.ДобровольноеЛичноеСтрахование ИЛИ
		 ВидЗатратНалоговогоУчета = Перечисления.ВидыРасходовНУ.ДобровольноеЛичноеСтрахованиеНаСлучайСмертиИлиУтратыРаботоспособности ИЛИ
		 ВидЗатратНалоговогоУчета = Перечисления.ВидыРасходовНУ.ДобровольноеСтрахованиеПоДоговорамДолгосрочногоСтрахованияЖизниРаботников ИЛИ
		 НЕ ЗначениеЗаполнено(Счет) Тогда
		Возврат Новый Структура("ВидРасхода, ОбъектУчета");

	КонецЕсли;

	Если СчетПринадлежит(Счет07, Счет) Тогда
		ВидРасхода  = "На приобретение оборудования, требующего монтажа";
		ОбъектУчета = Субконто1;

	ИначеЕсли СчетПринадлежит(Счет08, Счет) Тогда
		ВидРасхода = "На приобретение ОС";
		ОбъектУчета = Субконто1;

		Если СчетПринадлежит(Счет08_01, Счет) ИЛИ
			 СчетПринадлежит(Счет08_02, Счет) ИЛИ
			 СчетПринадлежит(Счет08_03, Счет) ИЛИ
			 СчетПринадлежит(Счет08_04, Счет) Тогда
			ВидРасхода = "На приобретение ОС";

		ИначеЕсли СчетПринадлежит(Счет08_05, Счет) Тогда
			ВидРасхода = "На приобретение НМА";

		ИначеЕсли СчетПринадлежит(Счет08_08, Счет) Тогда
			ВидРасхода = "На НИОКР";

		КонецЕсли;

	ИначеЕсли СчетПринадлежит(Счет10, Счет) Тогда
		ВидРасхода  = ?(ВидТМЦ = 1, "Прочее внутреннее перемещение", "На приобретение материалов");
		ОбъектУчета = Субконто1;

	ИначеЕсли СчетПринадлежит(Счет21, Счет) Тогда
		ВидРасхода = "Прочее внутреннее перемещение";
		ОбъектУчета = Субконто1;
		
	ИначеЕсли СчетПринадлежит(Счет41, Счет) Тогда
		ВидРасхода = ?(ВидТМЦ = 0, "Прочее внутреннее перемещение", "На приобретение товаров");
		ОбъектУчета = Субконто1;
		
	ИначеЕсли СчетПринадлежит(Счет43, Счет) Тогда
		ВидРасхода = "Прочее внутреннее перемещение";
		ОбъектУчета = Субконто1;
		
	ИначеЕсли СчетПринадлежит(Счет45, Счет) Тогда
		ВидРасхода = "Отгрузка без перехода права собственности";
		ОбъектУчета = Субконто1;

	ИначеЕсли СчетПринадлежит(Счет58_01_1, Счет) Тогда
		ВидРасхода  = "На приобретение паев";
		ОбъектУчета = Субконто1;

	ИначеЕсли СчетПринадлежит(Счет58_01_2, Счет) ИЛИ
		      СчетПринадлежит(Счет58_02, Счет) Тогда
		ВидРасхода  = "На приобретение ценных бумаг";
		ОбъектУчета = Строка(Субконто1) + " (" + Строка(Субконто2) + ")";

	ИначеЕсли СчетПринадлежит(Счет58_05, Счет) Тогда
		ВидРасхода  = "На приобретение прав требования";
		ОбъектУчета = Строка(Субконто1) + " (" + Строка(Субконто2) + ")";

	ИначеЕсли СчетПринадлежит(Счет44_01, Счет) Тогда
		ВидРасхода  = "Транспортные расходы";
		Если ВидЗатратНалоговогоУчета = Перечисления.ВидыРасходовНУ.ТранспортныеРасходы Тогда
			ВидРасхода = "Транспортные расходы";
		КонецЕсли;

	ИначеЕсли (ВидЗатратНалоговогоУчета = Перечисления.ВидыРасходовНУ.ПредставительскиеРасходы)
			  И
			  (СчетПринадлежит(Счет44, Счет)    ИЛИ СчетПринадлежит(Счет20_01_02, Счет) ИЛИ
			   СчетПринадлежит(Счет23_02, Счет) ИЛИ СчетПринадлежит(Счет25_02, Счет)    ИЛИ
			   СчетПринадлежит(Счет26_02, Счет) ИЛИ СчетПринадлежит(Счет28_02, Счет)    ИЛИ
			   СчетПринадлежит(Счет29_02, Счет)) Тогда
			ВидРасхода = "Представительские расходы";

	ИначеЕсли (ВидЗатратНалоговогоУчета = Перечисления.ВидыРасходовНУ.РасходыНаРекламуНормируемые)
			  И
			  (СчетПринадлежит(Счет44, Счет)    ИЛИ СчетПринадлежит(Счет20_01_02, Счет) ИЛИ
			   СчетПринадлежит(Счет23_02, Счет) ИЛИ СчетПринадлежит(Счет25_02, Счет)    ИЛИ
			   СчетПринадлежит(Счет26_02, Счет) ИЛИ СчетПринадлежит(Счет28_02, Счет)    ИЛИ
			   СчетПринадлежит(Счет29_02, Счет)) Тогда
			ВидРасхода = "Расходы на рекламу (нормируемые)";

	ИначеЕсли (СчетПринадлежит(Счет44, Счет)    ИЛИ СчетПринадлежит(Счет20_01_02, Счет) ИЛИ
			   СчетПринадлежит(Счет23_02, Счет) ИЛИ СчетПринадлежит(Счет25_02, Счет)    ИЛИ
			   СчетПринадлежит(Счет26_02, Счет) ИЛИ СчетПринадлежит(Счет28_02, Счет)    ИЛИ
			   СчетПринадлежит(Счет29_02, Счет)
			   )
			   И
			   (ВидЗатратНалоговогоУчета <> Перечисления.ВидыРасходовНУ.ТранспортныеРасходы ИЛИ
			    ВидЗатратНалоговогоУчета <> Перечисления.ВидыРасходовНУ.ПредставительскиеРасходы ИЛИ
			    ВидЗатратНалоговогоУчета <> Перечисления.ВидыРасходовНУ.РасходыНаРекламуНормируемые ИЛИ
			    ВидЗатратНалоговогоУчета <> Перечисления.ВидыРасходовНУ.НеУчитываемыеВЦеляхНалогообложения) Тогда
			ВидРасхода  = "Косвенные расходы";
			ОбъектУчета = ВидЗатратНалоговогоУчета;

	ИначеЕсли (СчетПринадлежит(Счет20_01_01, Счет) ИЛИ 
			   СчетПринадлежит(Счет23_01, Счет)    ИЛИ СчетПринадлежит(Счет25_01, Счет) ИЛИ 
			   СчетПринадлежит(Счет29_01, Счет)    ИЛИ СчетПринадлежит(Счет28_01, Счет)) Тогда
			ВидРасхода  = "Прямые расходы";
			ОбъектУчета = ВидЗатратНалоговогоУчета;

	ИначеЕсли СчетПринадлежит(Счет90_02, Счет) Тогда
			ВидРасхода  = "Реализация";
			ОбъектУчета = Субконто1;
	
	ИначеЕсли СчетПринадлежит(Счет91_02_1, Счет) Тогда
		ВидРасхода = "Расходы на реализацию ОС";
		ОбъектУчета = Субконто2;

	ИначеЕсли СчетПринадлежит(Счет91_02_2, Счет) Тогда
		ВидРасхода = "Расходы на реализацию НМА";
		ОбъектУчета = Субконто2;

	ИначеЕсли СчетПринадлежит(Счет91_02_3, Счет) Тогда
		Если ТипЗнч(Субконто2) = Тип("СправочникСсылка.Номенклатура") Тогда
			Если НЕ Субконто2.Услуга Тогда
				ВидРасхода = "Расходы на реализацию прочего имущества";
			КонецЕсли;
		КонецЕсли;

	ИначеЕсли СчетПринадлежит(Счет91_02_4, Счет) Тогда
		ВидРасхода = "Расходы, связанные с реализацией имущественных прав";
		ОбъектУчета = Строка(Субконто2) + " (" + Строка(Субконто3) + ")";

	ИначеЕсли СчетПринадлежит(Счет91_02_5, Счет) Тогда
		ВидРасхода = "Расходы, связанные с реализацией ЦБ";
		ОбъектУчета = Строка(Субконто2);

	ИначеЕсли СчетПринадлежит(Счет91_02_6, Счет) Тогда
		Если ВидЗатратНалоговогоУчета <> Перечисления.ВидыРасходовНУ.НеУчитываемыеВЦеляхНалогообложения Тогда
			ВидРасхода  = "Внереализационные расходы";
			ОбъектУчета = ВидЗатратНалоговогоУчета;
		КонецЕсли;

	ИначеЕсли (СчетПринадлежит(Счет44, Счет) ИЛИ СчетПринадлежит(Счет20, Счет) ИЛИ 
			   СчетПринадлежит(Счет23, Счет) ИЛИ СчетПринадлежит(Счет25, Счет) ИЛИ 
			   СчетПринадлежит(Счет26, Счет) ИЛИ СчетПринадлежит(Счет28, Счет) ИЛИ 
			   СчетПринадлежит(Счет29, Счет) ИЛИ СчетПринадлежит(Счет91_02, Счет))
			   И 
			   (ВидЗатратНалоговогоУчета = Перечисления.ВидыРасходовНУ.НеУчитываемыеВЦеляхНалогообложения) Тогда
			ВидРасхода = "Расходы, не учитываемые в целях налогообложения";

	ИначеЕсли СчетПринадлежит(Счет94, Счет) Тогда
		ВидРасхода = "Недостачи и потери от порчи ценностей";
		
	ИначеЕсли СчетПринадлежит(Счет97, Счет) Тогда
		ВидРасхода = "Прочие РБП";
		//На долгосрочное страхование жизни
		//На освоение природных ресурсов
		//Страхование на оплату медицинских расходов
		//Страхование на случай смерти и утраты трудоспособности

	Иначе
		ВидРасхода = "Без расходов";

	КонецЕсли;

	Возврат Новый Структура("ВидРасхода, ОбъектУчета", ВидРасхода, ОбъектУчета);

КонецФункции // ВидРасходаПоОперации()

// Преобразует таблицу значений в дерево значений, группируя значения по первым КолГрупп колонкам
Функция ВДеревоСГруппировкой(ТаблицаРезультатов, КолГрупп = 1)
	
	ДеревоРезультатов = Новый ДеревоЗначений;
	Для Каждого Колонка ИЗ ТаблицаРезультатов.Колонки Цикл
		ДеревоРезультатов.Колонки.Добавить(Колонка.Имя);
	КонецЦикла;
	
	Для Каждого СтрокаТаблицы ИЗ ТаблицаРезультатов Цикл
		КоллекцияСтрокДерева = ДеревоРезультатов.Строки;
		Для Группа = 0 По КолГрупп - 1 Цикл
			Ключ = СтрокаТаблицы[Группа];
			СтрокаПоГруппе = КоллекцияСтрокДерева.Найти(Ключ, ТаблицаРезультатов.Колонки[Группа].Имя, Ложь); 
			Если СтрокаПоГруппе = Неопределено Тогда
				СтрокаПоГруппе = КоллекцияСтрокДерева.Добавить();
				СтрокаПоГруппе[Группа] = Ключ;
				СтрокаПоГруппе.Количество = 0;
				СтрокаПоГруппе.Стоимость = 0;
			КонецЕсли;
			СтрокаПоГруппе.Количество = СтрокаПоГруппе.Количество + СтрокаТаблицы.Количество;
			СтрокаПоГруппе.Стоимость = СтрокаПоГруппе.Стоимость + СтрокаТаблицы.Стоимость;
			КоллекцияСтрокДерева = СтрокаПоГруппе.Строки;
		КонецЦикла;
		ДетальнаяЗапись = КоллекцияСтрокДерева.Добавить();
		Для Колонка = КолГрупп - 1 По ТаблицаРезультатов.Колонки.Количество()-1 Цикл
			ДетальнаяЗапись[Колонка] = СтрокаТаблицы[Колонка];
		КонецЦикла;
	КонецЦикла;
		
	Возврат ДеревоРезультатов;
КонецФункции

//Рекурсивно выводит структуру дерева значений результата
Процедура ВывестиСтрокиДерева(ДеревоРезультатов, Уровень = 0, ДокументРезультат, Макет, УчетнаяПолитика)
	ОбластьГруппа   	 = Макет.ПолучитьОбласть("Группа");
	ОбластьСтрока        = Макет.ПолучитьОбласть("Строка");
	Для Каждого Строка из ДеревоРезультатов.Строки Цикл
		Отступ = "";
		Для  н = 1 по Уровень Цикл
			Отступ = Отступ + "    ";
		КонецЦикла;
		
		Если Строка.Строки.Количество() = 0 Тогда //детальные строки
			ОбластьСтрока.Параметры.ДатаОперации = Строка.ДатаОперации;
			ОбластьСтрока.Параметры.ОснованиеВыбытия = Строка.Регистратор;
			ОбластьСтрока.Параметры.Количество = Строка.Количество;
			ОбластьСтрока.Параметры.Цена = ?(Строка.Количество = 0, 0, Строка.Стоимость/Строка.Количество);
			ОбластьСтрока.Параметры.Стоимость = Строка.Стоимость;
			ОбластьСтрока.Параметры.Расшифровка = Строка.Регистратор;
			ДокументРезультат.Вывести(ОбластьСтрока);
		Иначе //группировка, содержащая подчиненные строки
			СтрокаГруппы = "";
			Для Н = 0 по Уровень Цикл
				СтрокаГруппы = СтрокаГруппы + Строка(Строка[Н]);
			КонецЦикла;
			ОбластьГруппа.Параметры.Группа = Отступ + СтрокаГруппы;
			ОбластьГруппа.Параметры.ГрКоличество = Строка.Количество;
			//ОбластьГруппа.Параметры.ГрЦена = ?(Строка.Количество = 0, 0, Строка.Стоимость/Строка.Количество);
			ОбластьГруппа.Параметры.ГрСтоимость = Строка.Стоимость;
			Если ЗначениеЗаполнено(СтрокаГруппы) Тогда
				ДокументРезультат.Вывести(ОбластьГруппа);
			КонецЕсли;
		КонецЕсли;
		ВывестиСтрокиДерева(Строка, Уровень+1, ДокументРезультат, Макет, УчетнаяПолитика);
	КонецЦикла;
КонецПроцедуры

// Выполняет запрос и формирует табличный документ-результат отчета
// в соответствии с настройками, заданными значениями реквизитов отчета.
//
// Параметры:
//	ДокументРезультат - табличный документ, формируемый отчетом
//	ПоказыватьЗаголовок - признак видимости строк с заголовком отчета
//	ВысотаЗаголовка - параметр, через который возвращается высота заголовка в строках 
//
Процедура СформироватьОтчет(ДокументРезультат, ПоказыватьЗаголовок, ВысотаЗаголовка, ТолькоЗаголовок = Ложь) Экспорт

	ДокументРезультат.Очистить();

	Макет = ПолучитьМакет("Отчет");

	ОбластьЗаголовок  = Макет.ПолучитьОбласть("Заголовок");
	
    СрезСведений = ОбщегоНазначения.ПолучитьПараметрыУчетнойПолитики(ДатаКон, Ложь, Организация);
	УчетнаяПолитика = ?(НЕ ЗначениеЗаполнено(СрезСведений) ИЛИ СрезСведений.Количество() = 0, Перечисления.СпособыОценки.ПустаяСсылка(), СрезСведений.СпособОценкиМПЗ);
	Заголовок = "Регистр учета стоимости";
	Если ВидТМЦ = 1 Тогда
		Заголовок = Заголовок + " материалов, списанных";
	ИначеЕсли ВидТМЦ = 2 Тогда
		Заголовок = Заголовок + " продукции, списанной";
	ИначеЕсли ВидТМЦ = 3 Тогда
		Заголовок = Заголовок + " полуфабрикатов, списанных";
	Иначе
		Заголовок = Заголовок + " товаров, списанных";
	КонецЕсли;
	Если УчетнаяПолитика = Перечисления.СпособыОценки.ФИФО Тогда
		Заголовок = Заголовок + " в отчетном периоде по методу ФИФО";
	ИначеЕсли УчетнаяПолитика = Перечисления.СпособыОценки.ЛИФО Тогда
		Заголовок = Заголовок + " в отчетном периоде по методу ЛИФО";
	Иначе
		Заголовок = Заголовок + " в отчетном периоде";
	КонецЕсли;

	ОбластьЗаголовок.Параметры.НачалоПериода       = Формат(ДатаНач, "ДФ=dd.MM.yyyy");
	ОбластьЗаголовок.Параметры.КонецПериода        = Формат(ДатаКон, "ДФ=dd.MM.yyyy");
	СведенияОбОрганизации = УправлениеКонтактнойИнформацией.СведенияОЮрФизЛице(Организация, ДатаКон);
	НазваниеОрганизации = ФормированиеПечатныхФорм.ОписаниеОрганизации(СведенияОбОрганизации, "НаименованиеДляПечатныхФорм");;
	ОбластьЗаголовок.Параметры.НазваниеОрганизации = НазваниеОрганизации;
	ОбластьЗаголовок.Параметры.ИННОрганизации      = "" + Организация.ИНН + " / " + Организация.КПП;
	ОбластьЗаголовок.Параметры.Заголовок		   = заголовок;
	ДокументРезультат.Вывести(ОбластьЗаголовок);

	// Параметр для показа заголовка
	ВысотаЗаголовка = ДокументРезультат.ВысотаТаблицы;

	// Когда нужен только заголовок:
	Если ТолькоЗаголовок Тогда
		Возврат;
	КонецЕсли;

	Если ЗначениеЗаполнено(ВысотаЗаголовка) Тогда
		ДокументРезультат.Область("R1:R" + ВысотаЗаголовка).Видимость = ПоказыватьЗаголовок;
	КонецЕсли;

	Запрос = Новый Запрос;

	Запрос.УстановитьПараметр("ДатаНачала",      Новый Граница(НачалоДня(ДатаНач), ВидГраницы.Включая));
	Запрос.УстановитьПараметр("ДатаКонца",      Новый Граница(КонецДня(ДатаКон), ВидГраницы.Включая));
	Запрос.УстановитьПараметр("Организация",  Организация);

	УчетПоСредней = ?(УчетнаяПолитика = Перечисления.СпособыОценки.ПоСредней, Истина, Ложь);
		
	Если ВидТМЦ = 1 Тогда
		Запрос.УстановитьПараметр("СчетУчета",    Счет10);
		Запрос.УстановитьПараметр("СчетУчетаОтг",    Счет45_41);
	ИначеЕсли ВидТМЦ = 2 Тогда
		Запрос.УстановитьПараметр("СчетУчета",    Счет43);
		Запрос.УстановитьПараметр("СчетУчетаОтг",    Счет45_43);
	ИначеЕсли ВидТМЦ = 3 Тогда
		Запрос.УстановитьПараметр("СчетУчета",    Счет21);
        Запрос.УстановитьПараметр("СчетУчетаОтг",    Счет45_21);
	Иначе
		Запрос.УстановитьПараметр("СчетУчета",    Счет41);
		Запрос.УстановитьПараметр("СчетУчетаОтг",    Счет45_41);
	КонецЕсли;
		Запрос.УстановитьПараметр("ВидУчета",     Перечисления.ВидыУчетаПоПБУ18.НУ);
		Запрос.УстановитьПараметр("Счет107",    ПланыСчетов.Налоговый.МатериалыПереданныеВПереработку);
		
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	НалоговыйДвиженияССубконто.Регистратор КАК Регистратор,
	|	НалоговыйДвиженияССубконто.СубконтоКт1 КАК Номенклатура,
	|	НалоговыйДвиженияССубконто.СубконтоКт2 КАК ДокументОприходования,
	|	НалоговыйДвиженияССубконто.СчетКт КАК КодОперации,
	|	НалоговыйДвиженияССубконто.СчетДт КАК КорСчет,
	|	НалоговыйДвиженияССубконто.СубконтоДт1 КАК КорСубконто1,
	|	НалоговыйДвиженияССубконто.СубконтоДт2 КАК КорСубконто2,
	|	НалоговыйДвиженияССубконто.СубконтоДт3 КАК КорСубконто3,
	|	СУММА(ЕСТЬNULL(НалоговыйДвиженияССубконто.КоличествоКт, 0)) КАК КоличествоРасход,
	|	СУММА(ЕСТЬNULL(НалоговыйДвиженияССубконто.Сумма, 0)) КАК СтоимостьРасход
	|ИЗ
	|	РегистрБухгалтерии.Налоговый.ДвиженияССубконто(
	|		&ДатаНачала,
	|		&ДатаКонца,
	|		Организация = &Организация
	|		    И СчетКт "+?(ВидТМЦ < 2,"В ИЕРАРХИИ (&СчетУчета) И Не СчетКт В ИЕРАРХИИ (&Счет107)","= (&СчетУчета)")+" И СчетДт <> &СчетУчетаОтг
	|		    И ВидУчетаКт = &ВидУчета
	|			И Активность) КАК НалоговыйДвиженияССубконто
	|
	|СГРУППИРОВАТЬ ПО
	|	НалоговыйДвиженияССубконто.Регистратор,
	|	НалоговыйДвиженияССубконто.СубконтоКт1,
	|	НалоговыйДвиженияССубконто.СубконтоКт2,
	|	НалоговыйДвиженияССубконто.СчетКт,
	|	НалоговыйДвиженияССубконто.СчетДт,
	|	НалоговыйДвиженияССубконто.СубконтоДт1,
	|	НалоговыйДвиженияССубконто.СубконтоДт2,
	|	НалоговыйДвиженияССубконто.СубконтоДт3";
	
	Если ВидТМЦ <> 1 Тогда
		Запрос.Текст = Запрос.Текст + " 
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	НалоговыйДвиженияССубконто.Регистратор КАК Регистратор,
		|	НалоговыйДвиженияССубконто.СубконтоКт2,
		|	НалоговыйДвиженияССубконто.СубконтоКт3,
		|	НалоговыйДвиженияССубконто.СчетКт,
		|	НалоговыйДвиженияССубконто.СчетДт,
		|	НалоговыйДвиженияССубконто.СубконтоДт1,
		|	НалоговыйДвиженияССубконто.СубконтоДт2,
		|	НалоговыйДвиженияССубконто.СубконтоДт3,
		|	СУММА(ВЫБОР
		|			КОГДА НалоговыйДвиженияССубконто.КоличествоКт = NULL
		|				ТОГДА 0
		|			ИНАЧЕ НалоговыйДвиженияССубконто.КоличествоКт
		|		КОНЕЦ),
		|	СУММА(ВЫБОР
		|			КОГДА НалоговыйДвиженияССубконто.Сумма = NULL
		|				ТОГДА 0
		|			ИНАЧЕ НалоговыйДвиженияССубконто.Сумма
		|		КОНЕЦ)
		|ИЗ
		|	РегистрБухгалтерии.Налоговый.ДвиженияССубконто(
		|		&ДатаНачала,
		|		&ДатаКонца,
		|		Организация = &Организация
		|		    И СчетКт = &СчетУчетаОтг И ВидУчетаКт = &ВидУчета
		|			И Активность) КАК НалоговыйДвиженияССубконто
		|
		|СГРУППИРОВАТЬ ПО
		|	НалоговыйДвиженияССубконто.Регистратор,
		|	НалоговыйДвиженияССубконто.СубконтоКт2,
		|	НалоговыйДвиженияССубконто.СубконтоКт3,
		|	НалоговыйДвиженияССубконто.СчетКт,
		|	НалоговыйДвиженияССубконто.СчетДт,
		|	НалоговыйДвиженияССубконто.СубконтоДт1,
		|	НалоговыйДвиженияССубконто.СубконтоДт2,
		|	НалоговыйДвиженияССубконто.СубконтоДт3";
		
	Иначе
		
		Запрос.Текст = Запрос.Текст + " 
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	НалоговыйДвиженияССубконто.Регистратор КАК Регистратор,
		|	НалоговыйДвиженияССубконто.СубконтоКт2 КАК Номенклатура,
		|	НалоговыйДвиженияССубконто.СубконтоКт3 КАК ДокументОприходования,
		|	НалоговыйДвиженияССубконто.СчетКт КАК КодОперации,
		|	НалоговыйДвиженияССубконто.СчетДт КАК КорСчет,
		|	НалоговыйДвиженияССубконто.СубконтоДт1 КАК КорСубконто1,
		|	НалоговыйДвиженияССубконто.СубконтоДт2 КАК КорСубконто2,
		|	НалоговыйДвиженияССубконто.СубконтоДт3 КАК КорСубконто3,
		|	СУММА(ЕСТЬNULL(НалоговыйДвиженияССубконто.КоличествоКт, 0)) КАК КоличествоРасход,
		|	СУММА(ЕСТЬNULL(НалоговыйДвиженияССубконто.Сумма, 0)) КАК СтоимостьРасход
		|ИЗ
		|	РегистрБухгалтерии.Налоговый.ДвиженияССубконто(
		|		&ДатаНачала,
		|		&ДатаКонца,
		|		Организация = &Организация
		|		    И СчетКт В ИЕРАРХИИ (&Счет107)
		|		    И ВидУчетаКт = &ВидУчета
		|			И Активность) КАК НалоговыйДвиженияССубконто
		|
		|СГРУППИРОВАТЬ ПО
		|	НалоговыйДвиженияССубконто.Регистратор,
		|	НалоговыйДвиженияССубконто.СубконтоКт2,
		|	НалоговыйДвиженияССубконто.СубконтоКт3,
		|	НалоговыйДвиженияССубконто.СчетКт,
		|	НалоговыйДвиженияССубконто.СчетДт,
		|	НалоговыйДвиженияССубконто.СубконтоДт1,
		|	НалоговыйДвиженияССубконто.СубконтоДт2,
		|	НалоговыйДвиженияССубконто.СубконтоДт3";
	
		
	КонецЕсли;
	
	Результат = Запрос.Выполнить();

	ОбластьПодвал        = Макет.ПолучитьОбласть("Подвал");
	ОбластьШапкаТаблицы  = Макет.ПолучитьОбласть("ШапкаТаблицы");
    ДокументРезультат.Вывести(ОбластьШапкаТаблицы);
    
	СписокВидов = Новый СписокЗначений();
	Для каждого ЭлементСписка из ВидыРасходов Цикл
		Если ЭлементСписка.Пометка Тогда
			СписокВидов.Добавить(ЭлементСписка.Значение);
		КонецЕсли;
	КонецЦикла;
	
	ТаблицаРезультатов = Новый ТаблицаЗначений;
	
	КолГрупп = 0;
	Для Каждого Группировка ИЗ Показатели Цикл
		Если Группировка.Использование Тогда
			ТаблицаРезультатов.Колонки.Добавить(Группировка.Имя);
			КолГрупп = КолГрупп + 1;
		КонецЕсли;
	КонецЦикла;
	ТаблицаРезультатов.Колонки.Добавить("ДокументОприходования");
	КолГрупп = КолГрупп + 1;
	ТаблицаРезультатов.Колонки.Добавить("ДатаОперации");
	ТаблицаРезультатов.Колонки.Добавить("Регистратор");
	ТаблицаРезультатов.Колонки.Добавить("Количество");
	ТаблицаРезультатов.Колонки.Добавить("Стоимость");
	Если ТаблицаРезультатов.Колонки.Найти("Товар") = Неопределено И УчетПоСредней Тогда
			ТаблицаРезультатов.Колонки.Добавить("Товар");
	КонецЕсли;
	
	ТаблицаЦен = Новый ТаблицаЗначений;
	ТаблицаЦен.Колонки.Добавить("Количество");
	ТаблицаЦен.Колонки.Добавить("Стоимость");
	ТаблицаЦен.Колонки.Добавить("Товар");
	ТаблицаЦен.Индексы.Добавить("Товар");

	Выборка = Результат.Выбрать();	
	Пока Выборка.Следующий() Цикл
		Если НЕ ЗначениеЗаполнено(Выборка.Регистратор) = 1 Тогда
			Продолжить;
		КонецЕсли;
		
		СтруктураВида = ВидРасходаПоОперации(Выборка.КорСчет, Выборка.КорСубконто1, Выборка.КорСубконто2, Выборка.КорСубконто3, Выборка.КодОперации);
		Если СписокВидов.НайтиПоЗначению(СтруктураВида.ВидРасхода) = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		СтрокаТаблицыРезультатов = ТаблицаРезультатов.Добавить();
		СтрокаТаблицыРезультатов.ДатаОперации = Выборка.Регистратор.Дата;
		СтрокаТаблицыРезультатов.Регистратор = Выборка.Регистратор;
		СтрокаТаблицыРезультатов.ДокументОприходования = Выборка.ДокументОприходования;
		СтрокаТаблицыРезультатов.Количество = Выборка.КоличествоРасход;
		СтрокаТаблицыРезультатов.Стоимость = Выборка.СтоимостьРасход;

		Если НЕ ТаблицаРезультатов.Колонки.Найти("ХарактерЗатрат") = Неопределено Тогда
			СтрокаТаблицыРезультатов.ХарактерЗатрат = СтруктураВида.ВидРасхода;
		КонецЕсли;
		Если НЕ ТаблицаРезультатов.Колонки.Найти("ОбъектУчета") = Неопределено Тогда
			СтрокаТаблицыРезультатов.ОбъектУчета = СтруктураВида.ОбъектУчета;
		КонецЕсли;
		Если НЕ ТаблицаРезультатов.Колонки.Найти("Товар") = Неопределено Тогда
			СтрокаТаблицыРезультатов.Товар = Выборка.Номенклатура;
		КонецЕсли;
		Если УчетПоСредней Тогда
			СтрокаТаблицаЦен = ТаблицаЦен.Добавить();
			СтрокаТаблицаЦен.Товар = Выборка.Номенклатура;
			СтрокаТаблицаЦен.Количество = Выборка.КоличествоРасход;
			СтрокаТаблицаЦен.Стоимость = Выборка.СтоимостьРасход;
		КонецЕсли;
	КонецЦикла;
	
	Если УчетПоСредней Тогда
		ТаблицаЦен.Свернуть("Товар", "Количество, Стоимость");
		Для Каждого СтрокаТаблицыРезультатов Из ТаблицаРезультатов Цикл
			СтрокаТаблицыЦен = ТаблицаЦен.Найти(СтрокаТаблицыРезультатов.Товар, "Товар");
			Если СтрокаТаблицыЦен = Неопределено Тогда
				Цена = 0;
			ИначеЕсли СтрокаТаблицыЦен.Количество = 0 Тогда
				Цена = 0;
			Иначе
				Цена = СтрокаТаблицыЦен.Стоимость / СтрокаТаблицыЦен.Количество;
			КонецЕсли;
			СтрокаТаблицыРезультатов.Стоимость = СтрокаТаблицыРезультатов.Количество * Цена;
		КонецЦикла;
		Если ТаблицаРезультатов.Колонки.Найти("Товар") = Неопределено Тогда
			ТаблицаРезультатов.Свернуть("ДатаОперации, Регистратор, ДокументОприходования", "Количество, Стоимость");
			
		КонецЕсли;
	КонецЕсли;

	ДеревоРезультатов = ВДеревоСГруппировкой(ТаблицаРезультатов, КолГрупп);
	СтрокаСортировки = "";
	Для Колонка = 0 по КолГрупп - 1 Цикл
		СтрокаСортировки = СтрокаСортировки + ДеревоРезультатов.Колонки[Колонка].Имя + " ,"; 
	КонецЦикла;
	СтрокаСортировки = СтрокаСортировки + "ДатаОперации";
	ДеревоРезультатов.Строки.Сортировать(СтрокаСортировки, Истина);
	
	//Вывести строки
	ВывестиСтрокиДерева(ДеревоРезультатов, 0, ДокументРезультат, Макет, УчетнаяПолитика);
	
	ОбластьПодвал        = Макет.ПолучитьОбласть("Подвал");
	ОбластьПодвал.Параметры.ИтогоСтоимость = ДеревоРезультатов.Строки.Итог("Стоимость");
	
	СтруктураЛиц = РегламентированнаяОтчетность.ОтветственныеЛицаОрганизаций(Организация, ДатаКон);
	ОбластьПодвал.Параметры.ОтветственныйЗаРегистры = СтруктураЛиц.ОтветственныйЗаРегистры;
	ДокументРезультат.Вывести(ОбластьПодвал);

КонецПроцедуры // СформироватьОтчет

////////////////////////////////////////////////////////////////////////////////
// ОПЕРАТОРЫ ОСНОВНОЙ ПРОГРАММЫ
// 

НП           = Новый НастройкаПериода;

Счет07       = ПланыСчетов.Налоговый.ОборудованиеКУстановке;
Счет08       = ПланыСчетов.Налоговый.ВложенияВоВнеоборотныеАктивы;
Счет08_01    = ПланыСчетов.Налоговый.ПриобретениеЗемельныхУчастков;
Счет08_02    = ПланыСчетов.Налоговый.ПриобретениеОбъектовПриродопользования;
Счет08_03    = ПланыСчетов.Налоговый.СтроительствоОбъектовОсновныхСредств;
Счет08_04    = ПланыСчетов.Налоговый.ПриобретениеОбъектовОсновныхСредств;
Счет08_05    = ПланыСчетов.Налоговый.ПриобретениеНематериальныхАктивов;
Счет08_08    = ПланыСчетов.Налоговый.ВыполнениеНИОКР;
Счет10       = ПланыСчетов.Налоговый.Материалы;

Счет20       = ПланыСчетов.Налоговый.ОсновноеПроизводство_;
Счет21       = ПланыСчетов.Налоговый.Полуфабрикаты;
Счет23       = ПланыСчетов.Налоговый.ВспомогательныеПроизводства;
Счет25       = ПланыСчетов.Налоговый.ОбщепроизводственныеРасходы;
Счет26       = ПланыСчетов.Налоговый.ОбщехозяйственныеРасходы;
Счет28       = ПланыСчетов.Налоговый.БракВПроизводстве;
Счет29       = ПланыСчетов.Налоговый.ОбслуживающиеПроизводства;

Счет20_01_01 = ПланыСчетов.Налоговый.ПрямыеРасходыОсновногоПроизводства;
Счет23_01    = ПланыСчетов.Налоговый.ПрямыеРасходыВспомогательныхПроизводств;
Счет25_01    = ПланыСчетов.Налоговый.ПрямыеОбщепроизводственныеРасходы;
Счет28_01    = ПланыСчетов.Налоговый.ПрямыеРасходыПоВыявленномуБраку;
Счет29_01    = ПланыСчетов.Налоговый.ПрямыеРасходыОбслуживающихПроизводств;

Счет20_01_02 = ПланыСчетов.Налоговый.КосвенныеРасходыОсновногоПроизводства;
Счет23_02    = ПланыСчетов.Налоговый.КосвенныеРасходыВспомогательныхПроизводств;
Счет25_02    = ПланыСчетов.Налоговый.КосвенныеОбщепроизводственныеРасходы;
Счет26_02    = ПланыСчетов.Налоговый.КосвенныеОбщехозяйственныеРасходы;
Счет28_02    = ПланыСчетов.Налоговый.КосвенныеРасходыПоВыявленномуБраку;
Счет29_02    = ПланыСчетов.Налоговый.КосвенныеРасходыОбслуживающихПроизводств;

Счет41       = ПланыСчетов.Налоговый.Товары;
Счет43       = ПланыСчетов.Налоговый.ГотоваяПродукция;
Счет44       = ПланыСчетов.Налоговый.РасходыНаПродажу;
Счет44_01    = ПланыСчетов.Налоговый.ИздержкиОбращения;

Счет45       = ПланыСчетов.Налоговый.ТоварыОтгруженные;
Счет45_21    = ПланыСчетов.Налоговый.ПрочиеТоварыОтгруженные;
Счет45_41    = ПланыСчетов.Налоговый.ПокупныеТоварыОтгруженные;
Счет45_43    = ПланыСчетов.Налоговый.ГотоваяПродукцияОтгруженная;

Счет58_01_1  = ПланыСчетов.Налоговый.Паи;
Счет58_01_2  = ПланыСчетов.Налоговый.Акции;
Счет58_02    = ПланыСчетов.Налоговый.ДолговыеЦенныеБумаги;
Счет58_05    = ПланыСчетов.Налоговый.ПриобретенныеПрава;

Счет90_02    = ПланыСчетов.Налоговый.СебестоимостьПродаж;
Счет91_02    = ПланыСчетов.Налоговый.ПрочиеРасходы;
Счет91_02_1  = ПланыСчетов.Налоговый.РасходыСвязанныеСРеализациейОсновныхСредств;
Счет91_02_2  = ПланыСчетов.Налоговый.РасходыСвязанныеСРеализациейНематериальныхАктивов;
Счет91_02_3  = ПланыСчетов.Налоговый.РасходыСвязанныеСРеализациейПрочегоИмущества;
Счет91_02_4  = ПланыСчетов.Налоговый.РасходыСвязанныеСРеализациейИмущественныхПрав;
Счет91_02_5  = ПланыСчетов.Налоговый.РасходыСвязанныеСРеализациейЦенныхБумаг;
Счет91_02_6  = ПланыСчетов.Налоговый.ВнереализационныеРасходы;

Счет94    	 = ПланыСчетов.Налоговый.НедостачиИПотериОтПорчиЦенностей;
Счет97       = ПланыСчетов.Налоговый.РасходыБудущихПериодов;
СчетНПВ      = ПланыСчетов.Налоговый.ПоступлениеИВыбытиеИмуществаРаботУслугПрав;

#КонецЕсли