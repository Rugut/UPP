// Процедура управляет показом в форме периода построения отчета.
//
Процедура ПоказатьПериод()

	СтрПериодОтчета = мОписаниеПериода();
	ЭлементыФормы.НадписьПериодСоставленияОтчета.Заголовок = СтрПериодОтчета;

	Заголовок = "Книга учета доходов и расходов (" + мОписаниеПериода() + ") " + ?(Организация.Пустая(), "", Организация.ПолноеНаименование());
	
КонецПроцедуры // ПоказатьПериод()

// Процедура устанавливает границы периода построения отчета.
//
// Параметры:
//  Шаг          - число, количество стандартных периодов, на которое необходимо
//                 сдвигать период построения отчета;
//
Процедура ИзменитьПериод(Шаг)

	Период = КонецКвартала(ДобавитьМесяц(Период, Шаг*3));
	ПоказатьПериод();
	УстановитьВидимостьДоступностьРеквизитов();

КонецПроцедуры // ИзменитьПериод()

Процедура Сформировать(Кнопка)
	
	СформироватьОтчет(ЭлементыФормы.ТитульныйЛист, ЭлементыФормы.Раздел1, ЭлементыФормы.Раздел2, ЭлементыФормы.Раздел3);
	
КонецПроцедуры

Функция ВыделятьНДС(УказаннаяОрганизация)
	
	Результат = Истина;
	
	Если ЗначениеЗаполнено(УказаннаяОрганизация) Тогда
		УчетнаяПолитика = ОбщегоНазначения.ПолучитьПараметрыУчетнойПолитикиРегл(Период, УказаннаяОрганизация);
		Если ЗначениеЗаполнено(УчетнаяПолитика) Тогда
			Если УчетнаяПолитика.УСН Тогда
				Если УчетнаяПолитика.ОбъектНалогообложенияУСН = Перечисления.ОбъектыНалогообложенияПоУСН.ДоходыМинусРасходы Тогда
					Результат = (УчетнаяПолитика.ПорядокПризнанияРасходовПоНДС = Перечисления.ПорядокПризнанияРасходовПоНДС.ПоОплатеПоставщику);
				КонецЕсли;
			КонецЕсли; 
		КонецЕсли;	
	КонецЕсли;
	
	Возврат Результат;

КонецФункции // 

Процедура УстановитьВидимостьДоступностьРеквизитов()
	
	Если ВыделятьНДС(Организация) Тогда
		ЭлементыФормы.НадписьРежимПечатиНДС.Доступность = Ложь;
		ЭлементыФормы.РежимПечатиНДС.Доступность = Ложь;
		РежимПечатиНДС = 2;
	Иначе
		ЭлементыФормы.НадписьРежимПечатиНДС.Доступность = Истина;
		ЭлементыФормы.РежимПечатиНДС.Доступность = Истина;
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

Процедура ПриОткрытии()
	
	Если Период = '00010101000000' Тогда
		Период = КонецКвартала(ТекущаяДата());
	КонецЕсли;
	
	ПоказатьПериод();
	
	Если РежимПечатиГраф4и6 = 0 Тогда
		РежимПечатиГраф4и6 = ВосстановитьЗначение("КУДиР_РежимПечатиГраф4и6");
	КонецЕсли;

	Если РежимПечатиГраф4и6 = 0 Тогда
		РежимПечатиГраф4и6 = 3;
	КонецЕсли;
	
	Если РежимПечатиНДС = 0 Тогда
		РежимПечатиНДС = ВосстановитьЗначение("КУДиР_РежимПечатиНДС");
	КонецЕсли;

	Если РежимПечатиНДС = 0 Тогда
		РежимПечатиНДС = 2;
	КонецЕсли;
	
	УстановитьВидимостьДоступностьРеквизитов();
	
КонецПроцедуры

Процедура ОбновлениеОтображения()

	ПоказатьПериод();

КонецПроцедуры

// Процедура вызывается по нажатию кнопки "<" формы.
//   Инициализирует изменение периода построения отчета.
//
Процедура КнопкаПредыдущийПериодНажатие(Элемент)

	ИзменитьПериод(-1);

КонецПроцедуры // КнопкаПредыдущийПериодНажатие()

// Процедура вызывается по нажатию кнопки ">" формы.
//   Инициализирует изменение периода построения отчета.
//
Процедура КнопкаСледующийПериодНажатие(Элемент)

	ИзменитьПериод(1);

КонецПроцедуры // КнопкаСледующийПериодНажатие()

Процедура ОрганизацияПриИзменении(Элемент)
	
	УстановитьВидимостьДоступностьРеквизитов();
	
КонецПроцедуры