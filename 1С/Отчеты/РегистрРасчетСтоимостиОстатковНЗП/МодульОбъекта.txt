#Если Клиент Тогда
	
Перем НП Экспорт;

// Выполняет запрос и формирует табличный документ-результат отчета
// в соответствии с настройками, заданными значениями реквизитов отчета.
//
// Параметры:
//	ДокументРезультат - табличный документ, формируемый отчетом
//	ПоказыватьЗаголовок - признак видимости строк с заголовком отчета
//	ВысотаЗаголовка - параметр, через который возвращается высота заголовка в строках 
//
Процедура СформироватьОтчет(ДокументРезультат, ПоказыватьЗаголовок, ВысотаЗаголовка, ТолькоЗаголовок = Ложь) Экспорт

	НачалоПериода = НачалоДня(ДатаНач);
	КонецПериода  = КонецДня(ДатаКон);

	ДокументРезультат.Очистить();

	Макет = ПолучитьМакет("Отчет");

	ОбластьЗаголовок  = Макет.ПолучитьОбласть("Заголовок");

	ОбластьЗаголовок.Параметры.НачалоПериода       = Формат(НачалоПериода, "ДФ=dd.MM.yyyy");
	ОбластьЗаголовок.Параметры.КонецПериода        = Формат(КонецПериода, "ДФ=dd.MM.yyyy");
	СведенияОбОрганизации = УправлениеКонтактнойИнформацией.СведенияОЮрФизЛице(Организация, ДатаКон);
	НазваниеОрганизации = ФормированиеПечатныхФорм.ОписаниеОрганизации(СведенияОбОрганизации, "НаименованиеДляПечатныхФорм");;
	ОбластьЗаголовок.Параметры.НазваниеОрганизации = НазваниеОрганизации;
	ОбластьЗаголовок.Параметры.ИННОрганизации      = "" + Организация.ИНН + " / " + Организация.КПП;
	Если НоменклатурнаяГруппа <> Справочники.НоменклатурныеГруппы.ПустаяСсылка() Тогда
		ОбластьЗаголовок.Параметры.НоменклатурнаяГруппа = НоменклатурнаяГруппа;
	Иначе
		ОбластьЗаголовок.Параметры.НоменклатурнаяГруппа = "По всем";
	КонецЕсли;
	
	Если Подразделение <> Справочники.ПодразделенияОрганизаций.ПустаяСсылка() Тогда
		ОбластьЗаголовок.Параметры.Подразделение = Подразделение;
	Иначе
		ОбластьЗаголовок.Параметры.Подразделение = "По всем";
		
	КонецЕсли;

	ДокументРезультат.Вывести(ОбластьЗаголовок);

	// Параметр для показа заголовка
	ВысотаЗаголовка = ДокументРезультат.ВысотаТаблицы;

	// Когда нужен только заголовок:
	Если ТолькоЗаголовок Тогда
		Возврат;
	КонецЕсли;

	Если ЗначениеЗаполнено(ВысотаЗаголовка) Тогда
		ДокументРезультат.Область("R1:R" + ВысотаЗаголовка).Видимость = ПоказыватьЗаголовок;
	КонецЕсли;
	
	СписокСчетов = Новый Массив;
		СписокСчетов.Добавить(ПланыСчетов.Налоговый.ПрямыеРасходыОсновногоПроизводства);
		СписокСчетов.Добавить(ПланыСчетов.Налоговый.ПрямыеРасходыВспомогательныхПроизводств);
		
	СписокСубконто = Новый Массив;
		СписокСубконто.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Подразделения);
		СписокСубконто.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.НоменклатурныеГруппы);

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ДатаНач",      Новый Граница(НачалоДня(ДатаНач), ВидГраницы.Включая));
	Запрос.УстановитьПараметр("ДатаКон",      Новый Граница(КонецДня(ДатаКон),  ВидГраницы.Включая));
	Запрос.УстановитьПараметр("Организация",  Организация);
	Запрос.УстановитьПараметр("Амортизация",  Перечисления.ВидыРасходовНУ.Амортизация);
	Запрос.УстановитьПараметр("ОплатаТруда",  Перечисления.ВидыРасходовНУ.ОплатаТруда);
	Запрос.УстановитьПараметр("Материальные", Перечисления.ВидыРасходовНУ.МатериальныеРасходы);
	Запрос.УстановитьПараметр("НалогиИСборы", Перечисления.ВидыРасходовНУ.НалогиИСборы);
	Запрос.УстановитьПараметр("ЕСН",          Перечисления.ВидыРасходовНУ.ЕСН);
	Запрос.УстановитьПараметр("СтраховыеВзносы", Перечисления.ВидыРасходовНУ.СтраховыеВзносы);
	Запрос.УстановитьПараметр("ВидУчета",     Перечисления.ВидыУчетаПоПБУ18.НУ);
    Запрос.УстановитьПараметр("Счет",         СписокСчетов);
	Запрос.УстановитьПараметр("ВыбПодразделение",Подразделение);
	Запрос.УстановитьПараметр("ВыбНоменклатурнаяГруппа",НоменклатурнаяГруппа);
	Запрос.УстановитьПараметр("ВидыСубконтоНЗП", СписокСубконто);
	СписокСубконто.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.СтатьиЗатрат);
	Запрос.УстановитьПараметр("ВидыСубконто", СписокСубконто);

	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	НезавершенноеПроизводствоНалоговыйУчетОстаткиИОбороты.Субконто2 КАК НоменклатурнаяГруппа,
	|	ЕСТЬNULL(НезавершенноеПроизводствоНалоговыйУчетОстаткиИОбороты.СуммаНачальныйОстаток, 0) КАК СтоимостьНачальныйОстаток,
	|	ЕСТЬNULL(НезавершенноеПроизводствоНалоговыйУчетОстаткиИОбороты.СуммаКонечныйОстаток, 0) КАК СтоимостьКонечныйОстаток,
	|	НезавершенноеПроизводствоНалоговыйУчетОстаткиИОбороты.Период КАК Период
	|ПОМЕСТИТЬ Остатки
	|ИЗ
	|	РегистрБухгалтерии.Налоговый.ОстаткиИОбороты(
	|			&ДатаНач,
	|			&ДатаКон,
	|			Месяц,
	|			Движения,
	|			Счет В (&Счет),
	|			&ВидыСубконтоНЗП,
	|			Организация = &Организация
	|				И ВидУчета = &ВидУчета) КАК НезавершенноеПроизводствоНалоговыйУчетОстаткиИОбороты
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЕСТЬNULL(НалоговыйОбороты.СуммаОборотДт, 0) КАК СуммаПрямыхРасходовЗаМесяц,
	|	НалоговыйОбороты.Период КАК Период,
	|	НалоговыйОбороты.Субконто2 КАК НоменклатурнаяГруппа,
	|	ВЫБОР
	|		КОГДА НалоговыйОбороты.Субконто3.ВидРасходовНУ = &Материальные
	|			ТОГДА ЕСТЬNULL(НалоговыйОбороты.СуммаОборотДт, 0)
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК МатериальныеРасходы,
	|	ВЫБОР
	|		КОГДА НалоговыйОбороты.Субконто3.ВидРасходовНУ = &Амортизация
	|			ТОГДА ЕСТЬNULL(НалоговыйОбороты.СуммаОборотДт, 0)
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК АмортизацияРасходы,
	|	ВЫБОР
	|		КОГДА НалоговыйОбороты.Субконто3.ВидРасходовНУ = &ОплатаТруда
	|			ТОГДА ЕСТЬNULL(НалоговыйОбороты.СуммаОборотДт, 0)
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ОплатаТрудаРасходы,
	|	ВЫБОР
	|		КОГДА НалоговыйОбороты.Субконто3.ВидРасходовНУ = &ЕСН
	|				ИЛИ НалоговыйОбороты.Субконто3.ВидРасходовНУ = &СтраховыеВзносы
	|				ИЛИ НалоговыйОбороты.Субконто3.ВидРасходовНУ = &НАлогиИСборы
	|			ТОГДА ЕСТЬNULL(НалоговыйОбороты.СуммаОборотДт, 0)
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ЕСНРасходы
	|ПОМЕСТИТЬ ВсеРасходы
	|ИЗ
	|	РегистрБухгалтерии.Налоговый.Обороты(
	|			&ДатаНач,
	|			&ДатаКон,
	|			Месяц,
	|			Счет В ИЕРАРХИИ (&Счет),
	|			&ВидыСубконто,
	|			ВидУчета = &ВидУчета
	|				И Организация = &Организация,
	|			(НЕ КорСчет В ИЕРАРХИИ (&Счет)),
	|			) КАК НалоговыйОбороты
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВсеРасходы.Период КАК Период,
	|	ВсеРасходы.НоменклатурнаяГруппа КАК НоменклатурнаяГруппа,
	|	ВсеРасходы.СуммаПрямыхРасходовЗаМесяц,
	|	ВсеРасходы.МатериальныеРасходы,
	|	ВсеРасходы.АмортизацияРасходы,
	|	ВсеРасходы.ОплатаТрудаРасходы,
	|	ВсеРасходы.ЕСНРасходы,
	|	ЕСТЬNULL(Остатки.СтоимостьНачальныйОстаток, 0) КАК СтоимостьНачальныйОстаток,
	|	ЕСТЬNULL(Остатки.СтоимостьКонечныйОстаток, 0) КАК СтоимостьКонечныйОстаток
	|ПОМЕСТИТЬ ВсеИтоги
	|ИЗ
	|	ВсеРасходы КАК ВсеРасходы
	|		ЛЕВОЕ СОЕДИНЕНИЕ Остатки КАК Остатки
	|		ПО ВсеРасходы.Период = Остатки.Период
	|			И ВсеРасходы.НоменклатурнаяГруппа = Остатки.НоменклатурнаяГруппа
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВсеИтоги.Период КАК Период,
	|	ВсеИтоги.НоменклатурнаяГруппа КАК НоменклатурнаяГруппа,
	|	ВсеИтоги.СуммаПрямыхРасходовЗаМесяц КАК СуммаПрямыхРасходовЗаМесяц,
	|	ВсеИтоги.МатериальныеРасходы КАК МатериальныеРасходы,
	|	ВсеИтоги.АмортизацияРасходы КАК Амортизация,
	|	ВсеИтоги.ОплатаТрудаРасходы КАК ОплатаТруда,
	|	ВсеИтоги.ЕСНРасходы КАК ЕСН,
	|	ВсеИтоги.СтоимостьНачальныйОстаток КАК СтоимостьНачальныйОстаток,
	|	ВсеИтоги.СтоимостьКонечныйОстаток КАК СтоимостьКонечныйОстаток
	|ИЗ
	|	ВсеИтоги КАК ВсеИтоги
	|ИТОГИ
	|	СУММА(СуммаПрямыхРасходовЗаМесяц),
	|	СУММА(МатериальныеРасходы),
	|	СУММА(Амортизация),
	|	СУММА(ОплатаТруда),
	|	СУММА(ЕСН),
	|	МИНИМУМ(СтоимостьНачальныйОстаток),
	|	МИНИМУМ(СтоимостьКонечныйОстаток)
	|ПО
	|	Период,
	|	НоменклатурнаяГруппа";
	
	
	Если Подразделение <> Справочники.ПодразделенияОрганизаций.ПустаяСсылка() И НоменклатурнаяГруппа <> Справочники.НоменклатурныеГруппы.ПустаяСсылка() Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст,"И Организация = &Организация", "И Организация = &Организация И
		|	Субконто1 В (&ВыбПодразделение) И
		|	Субконто2 В (&ВыбНоменклатурнаяГруппа)");
	ИначеЕсли НоменклатурнаяГруппа <> Справочники.НоменклатурныеГруппы.ПустаяСсылка() Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст,"И Организация = &Организация","И Организация = &Организация И
		|	Субконто2 В (&ВыбНоменклатурнаяГруппа)");
	ИначеЕсли Подразделение <> Справочники.ПодразделенияОрганизаций.ПустаяСсылка() Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст,"И Организация = &Организация","И Организация = &Организация И
		|	Субконто1 В (&ВыбПодразделение)");
	КонецЕсли;
	
	Результат = Запрос.Выполнить();

	ОбластьШапкаТаблицы  = Макет.ПолучитьОбласть("ШапкаТаблицы");
	ОбластьСтрока        = Макет.ПолучитьОбласть("Строка");
	ОбластьИтого         = Макет.ПолучитьОбласть("Итого");
	ОбластьПодвал        = Макет.ПолучитьОбласть("Подвал");

	ДокументРезультат.Вывести(ОбластьШапкаТаблицы);

	ВыборкаПериодов = Результат.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);

	Пока ВыборкаПериодов.Следующий() Цикл

		МесяцГод = Формат(ВыборкаПериодов.Период,"ДФ=MMMM.yyyy");
		МесяцГод = СтрЗаменить(МесяцГод,"."," ");

		СуммаПрямыхРасходовЗаМесяц = ?(ВыборкаПериодов.СуммаПрямыхРасходовЗаМесяц = NULL, 0, ВыборкаПериодов.СуммаПрямыхРасходовЗаМесяц);

		ОбластьИтого.Параметры.МесяцГод                          = МесяцГод;
		ОбластьИтого.Параметры.СуммаОстатокНЗПНаНачалоМесяца     = ВыборкаПериодов.СтоимостьНачальныйОстаток;
		ОбластьИтого.Параметры.СуммаМатериальныхРасходовЗаМесяц  = ВыборкаПериодов.МатериальныеРасходы;
		ОбластьИтого.Параметры.СуммаДругихРасходовЗаМесяц        = СуммаПрямыхРасходовЗаМесяц - ВыборкаПериодов.МатериальныеРасходы - ВыборкаПериодов.ОплатаТруда - ВыборкаПериодов.ЕСН - ВыборкаПериодов.Амортизация;
		ОбластьИтого.Параметры.СуммаРасходовНаОплатуТрудаЗаМесяц = ВыборкаПериодов.ОплатаТруда;
		ОбластьИтого.Параметры.СуммаЕСНЗаМесяц                   = ВыборкаПериодов.ЕСН;
		ОбластьИтого.Параметры.СуммаАмортизацииЗаМесяц           = ВыборкаПериодов.Амортизация;

		ОбластьИтого.Параметры.СуммаПрямыхРасхдовЗаМесяц         =  СуммаПрямыхРасходовЗаМесяц;
		ОбластьИтого.Параметры.СуммаПрямыхРасходовНаКонецМесяца  = ВыборкаПериодов.СтоимостьНачальныйОстаток + СуммаПрямыхРасходовЗаМесяц;
		ОбластьИтого.Параметры.СуммаОстатокНЗПНаКонецМесяца      = ВыборкаПериодов.СтоимостьКонечныйОстаток;
		ОбластьИтого.Параметры.СуммаОтносящаясяКВыпускуПродукции = ВыборкаПериодов.СтоимостьНачальныйОстаток + СуммаПрямыхРасходовЗаМесяц - ВыборкаПериодов.СтоимостьКонечныйОстаток;

		ДокументРезультат.Вывести(ОбластьИтого);

		ВыборкаСтрок = ВыборкаПериодов.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);

		Пока ВыборкаСтрок.Следующий() Цикл

			СуммаПрямыхРасходовЗаМесяц = ВыборкаСтрок.СуммаПрямыхРасходовЗаМесяц;

			ОбластьСтрока.Параметры.МесяцГод                          = МесяцГод;
			ОбластьСтрока.Параметры.ВидНоменклатуры                   = ВыборкаСтрок.НоменклатурнаяГруппа;
			ОбластьСтрока.Параметры.СуммаОстатокНЗПНаНачалоМесяца     = ВыборкаСтрок.СтоимостьНачальныйОстаток;
			ОбластьСтрока.Параметры.СуммаМатериальныхРасходовЗаМесяц  = ВыборкаСтрок.МатериальныеРасходы;
			ОбластьСтрока.Параметры.СуммаРасходовНаОплатуТрудаЗаМесяц = ВыборкаСтрок.ОплатаТруда;
			ОбластьСтрока.Параметры.СуммаЕСНЗаМесяц                   = ВыборкаСтрок.ЕСН;
			ОбластьСтрока.Параметры.СуммаАмортизацииЗаМесяц           = ВыборкаСтрок.Амортизация;
 			ОбластьСтрока.Параметры.СуммаДругихРасходовЗаМесяц        = СуммаПрямыхРасходовЗаМесяц - ВыборкаСтрок.МатериальныеРасходы - ВыборкаСтрок.ОплатаТруда - ВыборкаСтрок.ЕСН - ВыборкаСтрок.Амортизация;
			
			ОбластьСтрока.Параметры.СуммаПрямыхРасхдовЗаМесяц         = СуммаПрямыхРасходовЗаМесяц;
			ОбластьСтрока.Параметры.СуммаПрямыхРасходовНаКонецМесяца  = ВыборкаСтрок.СтоимостьНачальныйОстаток + СуммаПрямыхРасходовЗаМесяц;
			ОбластьСтрока.Параметры.СуммаОстатокНЗПНаКонецМесяца      = ВыборкаСтрок.СтоимостьКонечныйОстаток ;
			ОбластьСтрока.Параметры.СуммаОтносящаясяКВыпускуПродукции = ВыборкаСтрок.СтоимостьНачальныйОстаток + СуммаПрямыхРасходовЗаМесяц - ВыборкаСтрок.СтоимостьКонечныйОстаток;

			ДокументРезультат.Вывести(ОбластьСтрока);

		КонецЦикла;

	КонецЦикла;

	СтруктураЛиц = РегламентированнаяОтчетность.ОтветственныеЛицаОрганизаций(Организация, ДатаКон);

	ОбластьПодвал.Параметры.ОтветственныйЗаРегистры = СтруктураЛиц.ОтветственныйЗаРегистры;

	ДокументРезультат.Вывести(ОбластьПодвал);

КонецПроцедуры // СформироватьОтчет

////////////////////////////////////////////////////////////////////////////////
// ОПЕРАТОРЫ ОСНОВНОЙ ПРОГРАММЫ
// 

НП = Новый НастройкаПериода;

#КонецЕсли