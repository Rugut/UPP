Перем СписокОбъемныхТиповДиаграмм;

/////////////////////////////////////////////////////////////////////////
// ОБЩИЕ ПРОЦЕДУРЫ И ФУНКЦИИ
//

Процедура УправлениеДоступностьюЭлементов()

	Если МаксСерий = МаксимумСерий.НеЗадано Тогда
		ЭлементыФормы.МаксЗначСерий.Доступность = Ложь;
	Иначе
		ЭлементыФормы.МаксЗначСерий.Доступность = Истина;
		ЭлементыФормы.МаксЗначСерий.Данные = ?(МаксСерий = МаксимумСерий.Ограничено, "МаксКолвоСерий", "МаксПроцентСерий");
	КонецЕсли;
	
	Если СписокОбъемныхТиповДиаграмм.НайтиПоЗначению(ТипДиаг) <> Неопределено Тогда
		ЭлементыФормы.ПанельОбъемДиаграммы.ТекущаяСтраница = ЭлементыФормы.ПанельОбъемДиаграммы.Страницы.Объемная;
	Иначе
		ЭлементыФормы.ПанельОбъемДиаграммы.ТекущаяСтраница = ЭлементыФормы.ПанельОбъемДиаграммы.Страницы.Плоская;
	КонецЕсли;
	
	Если Не Счет.Пустая() Тогда
	
		ЭлементыФормы.Колво.Доступность         = Счет.Количественный;
		ЭлементыФормы.ВалютнаяСумма.Доступность = Счет.Валютный;
	
	КонецЕсли;
	
КонецПроцедуры


/////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЕ ЭЛЕМЕНТОВ ФОРМЫ
//

Процедура ОсновныеДействияФормыОК(Кнопка)
	
	Закрыть(Истина);
	
КонецПроцедуры

Процедура ВыбПериодНажатие(Элемент)

	Если НП.Редактировать() Тогда

		ДатаНач = НП.ПолучитьДатуНачала();
		ДатаКон = НП.ПолучитьДатуОкончания();

	КонецЕсли;

КонецПроцедуры

Процедура СчетПриИзменении(Элемент)
	
	Если Не Счет.Валютный и Ресурс = "ВалютнаяСумма" Тогда
		Ресурс = "Сумма";
	КонецЕсли;
	Если Не Счет.Количественный и Ресурс = "Количество" Тогда
		Ресурс = "Сумма";
	КонецЕсли;
	
	ПерезаполнитьНачальныеНастройки();
	
КонецПроцедуры

Процедура ПериодПриИзменении(Элемент)
	
	ПерезаполнитьНачальныеНастройки();
	
КонецПроцедуры

Процедура ОтборЗначениеНачалоВыбора(Элемент, СтандартнаяОбработка)
	
	СтрокаОтбора = ЭлементыФормы.Отбор.ТекущаяСтрока;
	
	// Принятая в конфигурации обработка работает только для равенства/неравенства
	Если СтрокаОтбора.ВидСравнения = ВидСравнения.Равно
		ИЛИ СтрокаОтбора.ВидСравнения = ВидСравнения.НеРавно Тогда
		
		ТипЗначенияПоля = СтрокаОтбора.ТипЗначения;
		НачалоВыбораЗначенияСубконто(Элемент, СтандартнаяОбработка, ТипЗначенияПоля);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриИзмененииРесурса(ПутьКДанным)
	
	ПерезаполнитьНачальныеНастройки();
	
КонецПроцедуры

/////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ
//

Процедура ПередОткрытием(Отказ, СтандартнаяОбработка)
	
	ПодключитьОбработчикИзмененияДанных("ВидРесурса",     "ПриИзмененииРесурса");
	ПодключитьОбработчикИзмененияДанных("СторонаРесурса", "ПриИзмененииРесурса");
	ПодключитьОбработчикИзмененияДанных("Ресурс",         "ПриИзмененииРесурса");
	
КонецПроцедуры

Процедура ОбновлениеОтображения()
	
	УправлениеДоступностьюЭлементов();
	
КонецПроцедуры

Процедура ПриОткрытии()
	
	Если РегламентированнаяОтчетность.ИДКонфигурации() <> "БП" Тогда
		// Проверка ведения однофирменности
		УчетПоВсемОрганизациям = Истина;
	КонецЕсли;

	БухгалтерскиеОтчеты.ПоддержкаОднофирменностиДляФормыНастроекБухОтчетов(ЭтаФорма, УчетПоВсемОрганизациям);
	
КонецПроцедуры




//////////////////////////////////////////////////////////
// МОДУЛЬ ФОРМЫ
//

СписокВидовПодписей = Новый СписокЗначений;
СписокВидовПодписей.Добавить(ВидПодписейКДиаграмме.ЗначениеПроцент, "Значение + процент");
СписокВидовПодписей.Добавить(ВидПодписейКДиаграмме.Значение, "Значение");
СписокВидовПодписей.Добавить(ВидПодписейКДиаграмме.Процент, "Процент");
СписокВидовПодписей.Добавить(ВидПодписейКДиаграмме.Серия, "Серия");
СписокВидовПодписей.Добавить(ВидПодписейКДиаграмме.СерияЗначениеПроцент, "Серия + значение + процент");
СписокВидовПодписей.Добавить(ВидПодписейКДиаграмме.СерияПроцент, "Серия + процент");
СписокВидовПодписей.Добавить(ВидПодписейКДиаграмме.СерияЗначение, "Серия + значение");
СписокВидовПодписей.Добавить(ВидПодписейКДиаграмме.Нет, "Нет");

ЭлементыФормы.ВидПодписей.СписокВыбора = СписокВидовПодписей;


СписокМакимумовСерий = Новый СписокЗначений;
СписокМакимумовСерий.Добавить(МаксимумСерий.НеЗадано, "Нет");
СписокМакимумовСерий.Добавить(МаксимумСерий.Ограничено, "Количество");
СписокМакимумовСерий.Добавить(МаксимумСерий.Процент, "Процент от максимального");

ЭлементыФормы.МаксимумСерий.СписокВыбора = СписокМакимумовСерий;


СписокРаздвижений = Новый СписокЗначений;
СписокРаздвижений.Добавить(АвтоРаздвижениеСерий.Нет, "Не раздвигать");
СписокРаздвижений.Добавить(АвтоРаздвижениеСерий.Максимум, "Максимальную");
СписокРаздвижений.Добавить(АвтоРаздвижениеСерий.Минимум, "Минимальную");
СписокРаздвижений.Добавить(АвтоРаздвижениеСерий.Все, "Все");

ЭлементыФормы.РаздвижениеСерий.СписокВыбора = СписокРаздвижений;


СписокОбъемныхТиповДиаграмм = Новый СписокЗначений;
СписокОбъемныхТиповДиаграмм.Добавить(ТипДиаграммы.ГистограммаОбъемная);
СписокОбъемныхТиповДиаграмм.Добавить(ТипДиаграммы.ГистограммаСНакоплениемОбъемная);
СписокОбъемныхТиповДиаграмм.Добавить(ТипДиаграммы.ГистограммаСНакоплениемГоризонтальнаяОбъемная);
СписокОбъемныхТиповДиаграмм.Добавить(ТипДиаграммы.ГистограммаГоризонтальнаяОбъемная);
СписокОбъемныхТиповДиаграмм.Добавить(ТипДиаграммы.ГистограммаНормированнаяГоризонтальнаяОбъемная);
СписокОбъемныхТиповДиаграмм.Добавить(ТипДиаграммы.КруговаяОбъемная);


ЭлементыФормы.ТипДиаг.СписокВыбора = ПолучитьСписокДоступныхТиповДиаграмм();