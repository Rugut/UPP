// Обработчик события Нажатие элемента ОсновныеДействияФормы.ОК.
//
Процедура СформироватьДиаграмму(Кнопка)
	
	ИмяПоказателя = "";
	ПредставлениеПоказателя = "";
	
	Для каждого СтрокаТаблицы Из ПоказателиДиаграммы Цикл
		Если СтрокаТаблицы.Использование Тогда
			ИмяПоказателя = СтрокаТаблицы.Имя;
			ПредставлениеПоказателя = СтрокаТаблицы.Представление;
			Прервать;
		КонецЕсли; 
	КонецЦикла; 
	
	Если ПустаяСтрока(ИмяПоказателя) Тогда
		Предупреждение("Необходимо выбрать показатель.");
		Возврат;
	КонецЕсли; 
	
	Если ИмяПоказателя = "ЦенаПродажи" Тогда   
		Предупреждение("Показатель 'Средняя цена продажи' используется только для гистограммы");
		Возврат;
	ИначеЕсли ИмяПоказателя = "РаспределениеПоПериодам" Тогда 
		Предупреждение("Показатель 'Период' используется только для гистограммы");
		Возврат;
	КонецЕсли;
	
	ТаблицаДанныхДиаграммы = Новый ТаблицаЗначений;
	
	XYZABCДиаграмма = Группы[0].Использование И Группы[1].Использование;
	
	Если XYZABCДиаграмма Тогда
		ТаблицаДанныхДиаграммы.Колонки.Добавить("Объект");
	ИначеЕсли ОбъектАнализа = "Контрагент" Тогда
		ТаблицаДанныхДиаграммы.Колонки.Добавить("Объект", Новый ОписаниеТипов("СправочникСсылка.Контрагенты"));
	ИначеЕсли ОбъектАнализа = "Номенклатура" Тогда
		ТаблицаДанныхДиаграммы.Колонки.Добавить("Объект", Новый ОписаниеТипов("СправочникСсылка.Номенклатура"));
	Иначе
		ТаблицаДанныхДиаграммы.Колонки.Добавить("Объект", Новый ОписаниеТипов("СправочникСсылка.Пользователи"));
	КонецЕсли;
	
	Для каждого СтрокаГруппы из Группы Цикл
		Если СтрокаГруппы.Использование Тогда
			ПерваяГруппировка = СтрокаГруппы.Группировка;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Если ПерваяГруппировка = "XYZ" Тогда
		
		КопияТаблицы = ТаблицаДиаграммы.Скопировать();
		КопияТаблицы.Свернуть("КлассXYZ");
		КопияТаблицы.Сортировать("КлассXYZ ВОЗР");
		
		Для каждого Класс Из КопияТаблицы Цикл
			Если Класс.КлассXYZ = Перечисления.XYZКлассификация.XКласс Тогда
				Колонка = ТаблицаДанныхДиаграммы.Колонки.Добавить("XКласс", Новый ОписаниеТипов("Число"));
				Колонка.Заголовок = "X-класс";
			ИначеЕсли Класс.КлассXYZ = Перечисления.XYZКлассификация.YКласс Тогда
				Колонка = ТаблицаДанныхДиаграммы.Колонки.Добавить("YКласс", Новый ОписаниеТипов("Число"));
				Колонка.Заголовок = "Y-класс";
			ИначеЕсли Класс.КлассXYZ = Перечисления.XYZКлассификация.ZКласс Тогда
				Колонка = ТаблицаДанныхДиаграммы.Колонки.Добавить("ZКласс", Новый ОписаниеТипов("Число"));
				Колонка.Заголовок = "Z-класс";
			Иначе
				Колонка = ТаблицаДанныхДиаграммы.Колонки.Добавить("КлассНеОпределен", Новый ОписаниеТипов("Число"));
				Колонка.Заголовок = "Класс не определен";
			КонецЕсли; 
		КонецЦикла;
		
		НазваниеОбъектаДиаграммы = ?(XYZABCДиаграмма,"КлассABC","Объект");
		
		ТаблицаДиаграммы.Сортировать(НазваниеОбъектаДиаграммы+" ВОЗР");
		
		Для каждого СтрокаТаблицы Из ТаблицаДиаграммы Цикл
			
			НайденнаяСтрокаТаблицы = ТаблицаДанныхДиаграммы.Найти(СтрокаТаблицы[НазваниеОбъектаДиаграммы], "Объект");
			Если НайденнаяСтрокаТаблицы = Неопределено Тогда
				НайденнаяСтрокаТаблицы = ТаблицаДанныхДиаграммы.Добавить();
				НайденнаяСтрокаТаблицы.Объект = СтрокаТаблицы[НазваниеОбъектаДиаграммы];
			КонецЕсли; 
			
			Если СтрокаТаблицы.КлассXYZ = Перечисления.XYZКлассификация.XКласс Тогда
				ИмяКласса = "XКласс";
			ИначеЕсли СтрокаТаблицы.КлассXYZ = Перечисления.XYZКлассификация.YКласс Тогда
				ИмяКласса = "YКласс";
			Иначе
				ИмяКласса = "ZКласс";
			КонецЕсли; 
			НайденнаяСтрокаТаблицы[ИмяКласса] = НайденнаяСтрокаТаблицы[ИмяКласса] + СтрокаТаблицы[ИмяПоказателя];
			
		КонецЦикла; 
		
	Иначе
		
		КопияТаблицы = ТаблицаДиаграммы.Скопировать();
		КопияТаблицы.Свернуть("КлассABC");
		КопияТаблицы.Сортировать("КлассABC ВОЗР");
		
		Для каждого Класс Из КопияТаблицы Цикл
			Если Класс.КлассABC = Перечисления.ABCКлассификация.AКласс Тогда
				Колонка = ТаблицаДанныхДиаграммы.Колонки.Добавить("AКласс", Новый ОписаниеТипов("Число"));
				Колонка.Заголовок = "А-класс";
			ИначеЕсли Класс.КлассABC = Перечисления.ABCКлассификация.BКласс Тогда
				Колонка = ТаблицаДанныхДиаграммы.Колонки.Добавить("BКласс", Новый ОписаниеТипов("Число"));
				Колонка.Заголовок = "В-класс";
			Иначе
				Колонка = ТаблицаДанныхДиаграммы.Колонки.Добавить("CКласс", Новый ОписаниеТипов("Число"));
				Колонка.Заголовок = "С-класс";
			КонецЕсли; 
		КонецЦикла;
		
		НазваниеОбъектаДиаграммы = ?(XYZABCДиаграмма,"КлассXYZ","Объект");
		
		ТаблицаДиаграммы.Сортировать(НазваниеОбъектаДиаграммы+" ВОЗР");
		
		Для каждого СтрокаТаблицы Из ТаблицаДиаграммы Цикл
			
			НайденнаяСтрокаТаблицы = ТаблицаДанныхДиаграммы.Найти(СтрокаТаблицы[НазваниеОбъектаДиаграммы], "Объект");
			Если НайденнаяСтрокаТаблицы = Неопределено Тогда
				НайденнаяСтрокаТаблицы = ТаблицаДанныхДиаграммы.Добавить();
				НайденнаяСтрокаТаблицы.Объект = СтрокаТаблицы[НазваниеОбъектаДиаграммы];
			КонецЕсли; 
			
			Если СтрокаТаблицы.КлассABC = Перечисления.ABCКлассификация.AКласс Тогда
				ИмяКласса = "AКласс";
			ИначеЕсли СтрокаТаблицы.КлассABC = Перечисления.ABCКлассификация.BКласс Тогда
				ИмяКласса = "BКласс";
			Иначе
				ИмяКласса = "CКласс";
			КонецЕсли; 
			НайденнаяСтрокаТаблицы[ИмяКласса] = НайденнаяСтрокаТаблицы[ИмяКласса] + СтрокаТаблицы[ИмяПоказателя];
			
		КонецЦикла; 
		
	КонецЕсли;
	
	ОтчетДиаграмма = Отчеты.Диаграмма.Создать();
	ОтчетДиаграмма.ТаблицаИсходныхДанных = ТаблицаДанныхДиаграммы;
	
	ОтчетДиаграмма.ИмяДиаграммы = СформироватьСтрокуНазванияОтчета()+", " + СформироватьСтрокуПериода() + ", " + ПредставлениеПоказателя;
	ОтчетДиаграмма.НастройкиДиаграммы = НастройкиДиаграммы;
	
	ФормаОтчета = ОтчетДиаграмма.ПолучитьФорму(, Этаформа.ВладелецФормы);
	
	ОтчетДиаграмма.ПостроитьДиаграмму(ФормаОтчета.ЭлементыФормы.Диаграмма);
	
	ЭтаФорма.Закрыть();
	
	ФормаОтчета.Открыть();
	
КонецПроцедуры

// Обработчик события Нажатие элемента ОсновныеДействияФормы.Гистрограмма.
//
Процедура СформироватьГистограмму(Кнопка)
	
	ИмяПоказателя = "";
	ПредставлениеПоказателя = "";
	
	Для каждого СтрокаТаблицы Из ПоказателиДиаграммы Цикл
		Если СтрокаТаблицы.Использование Тогда
			ИмяПоказателя = СтрокаТаблицы.Имя;
			ПредставлениеПоказателя = СтрокаТаблицы.Представление;
			Прервать;
		КонецЕсли; 
	КонецЦикла; 
		
	Если ПустаяСтрока(ИмяПоказателя) Тогда
		Предупреждение("Необходимо выбрать показатель.");
		Возврат;
	КонецЕсли; 
	
	ТаблицаДанныхДиаграммы = Новый ТаблицаЗначений;
	
	ТаблицаДанныхДиаграммы.Колонки.Добавить("Объект");
	
	ТаблицаДиаграммы.Сортировать(ИмяПоказателя);
	
	НазваниеКласса = "";
	Для каждого СтрокаГруппы из Группы Цикл
		Если НЕ СтрокаГруппы.Использование Тогда
			Продолжить;
		КонецЕсли;
		НазваниеКласса = "Класс"+СтрокаГруппы.Группировка;
		Прервать;
	КонецЦикла;
	
	ПоказыватьРаспределениеПоКлассам = ?(НазваниеКласса="",Ложь,ПоказыватьРаспределениеПоКлассам);
	
	Если ИмяПоказателя = "РаспределениеПоПериодам" Тогда
		
		Для каждого СтрокаТаблицы Из ТаблицаДиаграммы Цикл
			
			РаспределениеПоПериодам = СтрокаТаблицы[ИмяПоказателя];
			
			Для каждого СтрокаПериода из РаспределениеПоПериодам Цикл
				
				ЗначениеПоказателя = СтрокаПериода.Значение;
				
				КолонкаПредела = "До"+Формат(СтрокаПериода.Ключ,"ДФ=yyyy_MM_dd");
				
				Если ТаблицаДанныхДиаграммы.Колонки.Найти(КолонкаПредела) = Неопределено Тогда
					Колонка = ТаблицаДанныхДиаграммы.Колонки.Добавить(КолонкаПредела, Новый ОписаниеТипов("Число"));
					Колонка.Заголовок = Формат(СтрокаПериода.Ключ,"ДФ=dd.MM.yyyy");
				КонецЕсли;
				
				Если ПоказыватьРаспределениеПоКлассам Тогда
					НайденнаяСтрокаТаблицы = ТаблицаДанныхДиаграммы.Найти(СтрокаТаблицы[НазваниеКласса], "Объект");
					Если НайденнаяСтрокаТаблицы = Неопределено Тогда
						НайденнаяСтрокаТаблицы = ТаблицаДанныхДиаграммы.Добавить();
						НайденнаяСтрокаТаблицы.Объект = СтрокаТаблицы[НазваниеКласса];
					КонецЕсли; 
				Иначе
					НайденнаяСтрокаТаблицы = ТаблицаДанныхДиаграммы.Найти(СтрокаТаблицы.Объект, "Объект");
					Если НайденнаяСтрокаТаблицы = Неопределено Тогда
						НайденнаяСтрокаТаблицы = ТаблицаДанныхДиаграммы.Добавить();
						НайденнаяСтрокаТаблицы.Объект = СтрокаТаблицы.Объект;
					КонецЕсли; 
				КонецЕсли;
				
				Если ВысотаГистрограммы > 0 Тогда
					НайденнаяСтрокаТаблицы[КолонкаПредела] = Мин(ВысотаГистрограммы,НайденнаяСтрокаТаблицы[КолонкаПредела] + ЗначениеПоказателя)
				Иначе
					НайденнаяСтрокаТаблицы[КолонкаПредела] = НайденнаяСтрокаТаблицы[КолонкаПредела] + ЗначениеПоказателя;
				КонецЕсли;
				
			КонецЦикла;
			
		КонецЦикла; 
		
	Иначе
		
		ТекущийПредел = 0;                                              
		Пока ТекущийПредел <= МаксимальноеЗначение Цикл
			ТекущийПредел = ТекущийПредел + Интервал;
			Колонка = ТаблицаДанныхДиаграммы.Колонки.Добавить("До"+СтрЗаменить(ТекущийПредел,Символ(160),""), Новый ОписаниеТипов("Число"));
			Колонка.Заголовок = СокрЛП(ТекущийПредел);
		КонецЦикла;
		ТекущийПредел = Интервал;
		
		Для каждого СтрокаТаблицы Из ТаблицаДиаграммы Цикл
			
			ЗначениеПоказателя = СтрокаТаблицы[ИмяПоказателя];
			
			Если ЗначениеПоказателя > МаксимальноеЗначение Тогда
				Прервать;
			КонецЕсли;
			
			Пока ЗначениеПоказателя > ТекущийПредел Цикл
				ТекущийПредел = ТекущийПредел + Интервал;
			КонецЦикла;
			
			КолонкаПредела = "До"+СтрЗаменить(ТекущийПредел,Символ(160),"");
			
			Если ПоказыватьРаспределениеПоКлассам Тогда
				НайденнаяСтрокаТаблицы = ТаблицаДанныхДиаграммы.Найти(СтрокаТаблицы[НазваниеКласса], "Объект");
				Если НайденнаяСтрокаТаблицы = Неопределено Тогда
					НайденнаяСтрокаТаблицы = ТаблицаДанныхДиаграммы.Добавить();
					НайденнаяСтрокаТаблицы.Объект = СтрокаТаблицы[НазваниеКласса];
				КонецЕсли; 
			Иначе
				НайденнаяСтрокаТаблицы = ТаблицаДанныхДиаграммы.Найти("Распределение", "Объект");
				Если НайденнаяСтрокаТаблицы = Неопределено Тогда
					НайденнаяСтрокаТаблицы = ТаблицаДанныхДиаграммы.Добавить();
					НайденнаяСтрокаТаблицы.Объект = "Распределение";
				КонецЕсли; 
			КонецЕсли;
			
			Если ВысотаГистрограммы > 0 Тогда
				НайденнаяСтрокаТаблицы[КолонкаПредела] = Мин(ВысотаГистрограммы,НайденнаяСтрокаТаблицы[КолонкаПредела] + ?(СпособВывода=1,СтрокаТаблицы[ПараметрАнализа],1))
			Иначе
				НайденнаяСтрокаТаблицы[КолонкаПредела] = НайденнаяСтрокаТаблицы[КолонкаПредела] + ?(СпособВывода=1,СтрокаТаблицы[ПараметрАнализа],1);
			КонецЕсли;
			
		КонецЦикла; 
		
	КонецЕсли;
	
	ОтчетДиаграмма = Отчеты.Диаграмма.Создать();
	ОтчетДиаграмма.ТаблицаИсходныхДанных = ТаблицаДанныхДиаграммы;
	
	ОтчетДиаграмма.ИмяДиаграммы = СформироватьСтрокуНазванияОтчета()+", " + СформироватьСтрокуПериода() + ", " + ПредставлениеПоказателя;
	ОтчетДиаграмма.НастройкиДиаграммы = НастройкиДиаграммы;
	
	ФормаОтчета = ОтчетДиаграмма.ПолучитьФорму(, Этаформа.ВладелецФормы);
	
	ОтчетДиаграмма.ПостроитьДиаграмму(ФормаОтчета.ЭлементыФормы.Диаграмма);
	
	ФормаОтчета.Открыть();
КонецПроцедуры

// Обработчик события Нажатие элемента ОсновныеДействияФормы.ОК.
//
Процедура ОсновныеДействияФормыОК(Кнопка)
	
	Если ТипДиаграммы = 0 Тогда
		СформироватьДиаграмму(Кнопка);
	Иначе
		СформироватьГистограмму(Кнопка);
	КонецЕсли;
	
КонецПроцедуры

// Обработчик события ПриОткрытии Формы.
//
Процедура ПриОткрытии()
	
	СтрокаПоказателя = ПоказателиДиаграммы.Найти(ПараметрАнализа, "Имя");
	
	НоваяСтрока = ПоказателиДиаграммы.Добавить();
	НоваяСтрока.Имя = "КоэффициентВариации";
	НоваяСтрока.Представление = "Коэффициент вариации";
	
	Если ПоказателиДиаграммы.Найти("СуммаВыручки") <> Неопределено И ПоказателиДиаграммы.Найти("КоличествоПроданныхТоваров") <> Неопределено Тогда
		НоваяСтрока = ПоказателиДиаграммы.Добавить();
		НоваяСтрока.Имя = "ЦенаПродажи";
		НоваяСтрока.Представление = "Средняя цена продажи (для гистограммы)";
	КонецЕсли;
	
	НоваяСтрока = ПоказателиДиаграммы.Добавить();
	НоваяСтрока.Имя = "РаспределениеПоПериодам";
	НоваяСтрока.Представление = "Период (для гистограммы)";
	
	Если СтрокаПоказателя <> Неопределено Тогда
		СтрокаПоказателя.Использование = Истина;
		
		ТаблицаДиаграммы.Сортировать(ПараметрАнализа);
		
		ПоследняяСтрока = ТаблицаДиаграммы[ТаблицаДиаграммы.Количество()-1];
		МаксимальноеЗначение = ПоследняяСтрока[ПараметрАнализа];
		
		Интервал = Окр(МаксимальноеЗначение/Мин(ТаблицаДиаграммы.Количество(),20),0);
		Интервал = Окр(Интервал,-СтрДлина(СтрЗаменить(Интервал,Символ(160),""))+1);
	КонецЕсли;
	
	ЭлементыФормы.ПанельНастройкиГистограммы.Доступность = ТипДиаграммы=1;
	
КонецПроцедуры

// Обработчик события ПриИзмененииФлажка элемента формы ПоказателиДиаграммы.
//
Процедура ПоказателиДиаграммыПриИзмененииФлажка(Элемент, Колонка)
	
	Для каждого СтрокаТаблицы Из ПоказателиДиаграммы Цикл
		Если СтрокаТаблицы <> ЭлементыФормы.ПоказателиДиаграммы.ТекущаяСтрока Тогда
			СтрокаТаблицы.Использование = Ложь;
		КонецЕсли; 
	КонецЦикла; 
	
	ДоступностьНастроек = Истина;
	
	ИмяПараметра = ЭлементыФормы.ПоказателиДиаграммы.ТекущаяСтрока.Имя;
	
	Если ИмяПараметра = "РаспределениеПоПериодам" И Элемент.ТекущаяСтрока.Использование Тогда
		ДоступностьНастроек = Ложь;
		ТипДиаграммы        = 1;
		СпособВывода        = 1;
		ЭлементыФормы.ПанельНастройкиГистограммы.Доступность = Истина;
	ИначеЕсли ИмяПараметра = "ЦенаПродажи" И Элемент.ТекущаяСтрока.Использование Тогда
		ТипДиаграммы        = 1;
		ЭлементыФормы.ПанельНастройкиГистограммы.Доступность = Истина;
	КонецЕсли;
	
	ЭлементыФормы.СпособВывода.Доступность         = ДоступностьНастроек;
	ЭлементыФормы.СпособВывода2.Доступность        = ДоступностьНастроек;  
	ЭлементыФормы.МаксимальноеЗначение.Доступность = ДоступностьНастроек; 
	ЭлементыФормы.Интервал.Доступность             = ДоступностьНастроек; 
	
КонецПроцедуры

// Обработчик события ПередНачаломДобавления элемента формы ПоказателиДиаграммы.
//
Процедура ПоказателиДиаграммыПередНачаломДобавления(Элемент, Отказ, Копирование)
	
	Отказ = Истина;
	
КонецПроцедуры

// Обработчик события ПередУдалением элемента формы ПоказателиДиаграммы.
//
Процедура ПоказателиДиаграммыПередУдалением(Элемент, Отказ)
	
	Отказ = Истина;
	
КонецПроцедуры

Процедура ПоказателиДиаграммыПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	Если ТаблицаДиаграммы.Количество()>0 Тогда
		
		ИмяПоказателя = "";
		
		Для каждого СтрокаТаблицы Из ПоказателиДиаграммы Цикл
			Если СтрокаТаблицы.Использование Тогда
				ИмяПоказателя = СтрокаТаблицы.Имя;
				Прервать;
			КонецЕсли; 
		КонецЦикла; 
		
		Если НЕ ЗначениеЗаполнено(ИмяПоказателя) Тогда
			Возврат;
		КонецЕсли;
		
		ТаблицаДиаграммы.Сортировать(ИмяПоказателя);
		
		ПоследняяСтрока = ТаблицаДиаграммы[ТаблицаДиаграммы.Количество()-1];
		МаксимальноеЗначение = ПоследняяСтрока[ИмяПоказателя];
		
		Интервал = Окр(МаксимальноеЗначение/Мин(ТаблицаДиаграммы.Количество(),20),0);
		Интервал = Окр(Интервал,-СтрДлина(СтрЗаменить(Интервал,Символ(160),""))+1);
	КонецЕсли;
	
КонецПроцедуры

Процедура ДиаграммаПриИзменении(Элемент)
	ЭлементыФормы.ПанельНастройкиГистограммы.Доступность = ТипДиаграммы=1;
КонецПроцедуры

Процедура ГистограммаПриИзменении(Элемент)
	ЭлементыФормы.ПанельНастройкиГистограммы.Доступность = ТипДиаграммы=1;
КонецПроцедуры

ПоказателиДиаграммы.Колонки.Добавить("Имя" , Новый ОписаниеТипов("Строка"));

СпособВывода = 0;