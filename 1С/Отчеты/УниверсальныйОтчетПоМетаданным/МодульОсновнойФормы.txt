Перем ТипДанныхСтарый, ИмяОбъектаСтарый, ИмяТаблицыСтарый;

Процедура ПередОткрытием(Отказ, СтандартнаяОбработка)
	
	Если Не ЭтоОтработкаРасшифровки 
	   И Не СохранениеНастроек.ЗаполнитьНастройкиПриОткрытииОтчета(ОтчетОбъект) Тогда
		ИнициализироватьЭлементыФормы();
		ИнициализацияОтчета();
	Иначе
		ИнициализироватьЭлементыФормы();
	КонецЕсли;

	ТиповыеОтчеты.НазначитьФормеУникальныйКлючИдентификации(ЭтаФорма);
	ТиповыеОтчеты.ОбновитьФормуТиповогоОтчетаПоКомпоновщику(ОтчетОбъект, ЭтаФорма);
	ТиповыеОтчеты.УправлениеОтображениемЭлементовФормыТиповогоОтчета(ОтчетОбъект, ЭтаФорма);
	
КонецПроцедуры

Процедура ДействияФормыНастройки(Кнопка)
	
	Если ТиповыеОтчеты.РедактироватьНастройкиТиповогоОтчета(ОтчетОбъект, ЭтаФорма) Тогда
		ОбновитьОтчет();
	КонецЕсли;
	
КонецПроцедуры

Процедура ДействияФормыСформировать(Кнопка)
	
	ОбновитьОтчет();
	
КонецПроцедуры

Процедура ДействияФормыЗаголовок(Кнопка)
	
	Кнопка.Пометка = Не Кнопка.Пометка;
	ТиповыеОтчеты.УправлениеОтображениемЭлементовФормыТиповогоОтчета(ОтчетОбъект, ЭтаФорма);
	ТиповыеОтчеты.УправлениеОтображениемЗаголовкаТиповогоОтчета(ОтчетОбъект, ЭтаФорма.ЭлементыФормы.Результат);
	
КонецПроцедуры

Процедура ДействияФормыОткрытьВНовомОкне(Кнопка)
	
	ТиповыеОтчеты.ОткрытьВНовомОкнеТиповойОтчет(ОтчетОбъект, ЭтаФорма);
	
КонецПроцедуры

Процедура ДействияФормыВосстановитьЗначения(Кнопка)
	
	СохранениеНастроек.ВыбратьНастройкуФормы(СохраненнаяНастройка, ЭтаФорма, "ОтчетОбъект." + ОтчетОбъект.Метаданные().Имя, Ложь);
	
	ОбновитьСписокВыбораТипДанных();
	ОбновитьСписокВыбораОбъектов();
	ОбновитьСписокВыбораТаблиц();
	ЭлементыФормы.ТипДанных.Значение = ТипДанных;
	ЭлементыФормы.ИмяОбъекта.Значение = ИмяОбъекта;
	ИмяОбъектаСтарый = ИмяОбъекта;
	ТипДанныхСтарый = ТипДанных;
	ИмяТаблицыСтарый = ИмяТаблицы;
	
	ТиповыеОтчеты.ОбновитьФормуТиповогоОтчетаПоКомпоновщику(ОтчетОбъект, ЭтаФорма);
	ТиповыеОтчеты.ОбновитьЗаголовокТиповогоОтчета(ОтчетОбъект, ЭтаФорма);
	
КонецПроцедуры

Процедура ДействияФормыСохранитьЗначения(Кнопка)
	
	СохранениеНастроек.ВыбратьНастройкуФормы(СохраненнаяНастройка, ЭтаФорма, "ОтчетОбъект." + ОтчетОбъект.Метаданные().Имя, Истина);
	ТиповыеОтчеты.ОбновитьЗаголовокТиповогоОтчета(ОтчетОбъект, ЭтаФорма);
	
КонецПроцедуры

Процедура ДействияФормыОтбор(Кнопка)
	
	Кнопка.Пометка = Не Кнопка.Пометка;
	ПоказыватьБыстрыйОтбор = Кнопка.Пометка;
	ТиповыеОтчеты.УправлениеОтображениемЭлементовФормыТиповогоОтчета(ОтчетОбъект, ЭтаФорма);
	ТиповыеОтчеты.ОбработкаИзмененияТиповогоОтчетаНаФормеОтчета(ОтчетОбъект, ЭтаФорма);
	
КонецПроцедуры

Процедура ОбновлениеОтображения()
	
	ТиповыеОтчеты.ОбновитьЗаголовокТиповогоОтчета(ОтчетОбъект, ЭтаФорма);
	
КонецПроцедуры

Процедура КнопкаНастройкаПериодаНажатие(Элемент)
	
	ТиповыеОтчеты.НастроитьПериод(НастройкаПериода, НачалоПериода, КонецПериода);
	ТиповыеОтчеты.ОбновитьПараметрыПериодаПоФорме(КомпоновщикНастроек, ЭтаФорма);
	ТиповыеОтчеты.ОбработкаИзмененияТиповогоОтчетаНаФормеОтчета(ОтчетОбъект, ЭтаФорма);
	
КонецПроцедуры

Процедура ПолеВводаПериодПриИзменении(Элемент)
	
	ТиповыеОтчеты.ОбновитьПараметрыПериодаПоФорме(КомпоновщикНастроек, ЭтаФорма);
	ТиповыеОтчеты.ОбработкаИзмененияТиповогоОтчетаНаФормеОтчета(ОтчетОбъект, ЭтаФорма);
	
КонецПроцедуры

Процедура РезультатОбработкаРасшифровки(Элемент, Расшифровка, СтандартнаяОбработка)
	
	ТиповыеОтчеты.СтандартнаяОбработкаРасшифровкиТиповогоОтчета(ОтчетОбъект, ЭтаФорма, Расшифровка, СтандартнаяОбработка);
	
КонецПроцедуры

Процедура ДействияФормыПечать(Кнопка)
	
	ТиповыеОтчеты.ПечатьТиповогоОтчета(ЭлементыФормы.Результат);
	
КонецПроцедуры

Процедура ОбновитьОтчет() Экспорт
	
	СформироватьОтчет(ЭтаФорма.ЭлементыФормы.Результат, ЭтаФорма.ДанныеРасшифровки);
	
КонецПроцедуры

Процедура ИнициализироватьЭлементыФормы()
	
	ОбновитьСписокВыбораТипДанных();
	УстановитьПоУмолчаниюТипДанных();
	ОбновитьСписокВыбораОбъектов();
	УстановитьПоУмолчаниюОбъект();
	ОбновитьСписокВыбораТаблиц();
	УстановитьПоУмолчаниюТаблицу();
	ТиповыеОтчеты.ОбновитьФормуТиповогоОтчетаПоКомпоновщику(ОтчетОбъект, ЭтаФорма);
	ЭлементыФормы.ТипДанных.Значение = ТипДанных;
	ЭлементыФормы.ИмяОбъекта.Значение = ИмяОбъекта;
	ИмяОбъектаСтарый = ИмяОбъекта;
	ТипДанныхСтарый = ТипДанных;
	ИмяТаблицыСтарый = ИмяТаблицы;
	
КонецПроцедуры

Процедура ОбновитьСписокВыбораТипДанных()

	СписокТиповОбъектов = Новый СписокЗначений;
	СписокТиповОбъектов.Добавить("Документы", "Документ", , БиблиотекаКартинок.Документ);
	СписокТиповОбъектов.Добавить("Справочники", "Справочник", , БиблиотекаКартинок.Справочник);
	СписокТиповОбъектов.Добавить("РегистрыНакопления", "Регистр накопления", , БиблиотекаКартинок.РегистрНакопления);
	СписокТиповОбъектов.Добавить("РегистрыСведений", "Регистр сведений", , БиблиотекаКартинок.РегистрСведений);
	ЭлементыФормы.ТипДанных.СписокВыбора = СписокТиповОбъектов;
	
КонецПроцедуры

Функция ОбновитьСписокВыбораОбъектов()
 
	СписокОбъектов = Новый СписокЗначений;
	Для каждого Объект Из Метаданные[ТипДанных] Цикл
		СписокОбъектов.Добавить(Объект.Имя, Объект.Синоним);
	КонецЦикла;
	ЭлементыФормы.ИмяОбъекта.СписокВыбора = СписокОбъектов;
	
КонецФункции

Процедура ОбновитьСписокВыбораТаблиц()
	
	СписокВыбораТаблиц = Новый СписокЗначений;
	СписокВыбораТаблиц.Добавить("", "");
	Если ТипДанных = "Документы" ИЛИ ТипДанных = "Справочники" Тогда
		Для каждого ТабличнаяЧасть Из Метаданные[ТипДанных][ИмяОбъекта].ТабличныеЧасти Цикл
			СписокВыбораТаблиц.Добавить(ТабличнаяЧасть.Имя, ТабличнаяЧасть.Синоним);
		КонецЦикла;
	ИначеЕсли ТипДанных = "РегистрыНакопления" Тогда
		Если Метаданные[ТипДанных][ИмяОбъекта].ВидРегистра = Метаданные.СвойстваОбъектов.ВидРегистраНакопления.Остатки Тогда
			СписокВыбораТаблиц.Добавить("ОстаткиИОбороты", "Остатки и обороты");
		Иначе
			СписокВыбораТаблиц.Добавить("Обороты", "Обороты");
		КонецЕсли;
	ИначеЕсли ТипДанных = "РегистрыСведений" Тогда 
		Если Метаданные[ТипДанных][ИмяОбъекта].ПериодичностьРегистраСведений = Метаданные.СвойстваОбъектов.ПериодичностьРегистраСведений.Непериодический Тогда
		Иначе
			СписокВыбораТаблиц.Добавить("СрезПоследних", "Срез последних");
			СписокВыбораТаблиц.Добавить("СрезПервых", "Срез первых");                	
		КонецЕсли;
	КонецЕсли;
	ЭлементыФормы.ИмяТаблицы.СписокВыбора = СписокВыбораТаблиц;
	
КонецПроцедуры

Процедура УстановитьПоУмолчаниюТипДанных()
	
	Если ПустаяСтрока(ТипДанных) Тогда
		ТипДанных = "РегистрыНакопления";
	КонецЕсли;

КонецПроцедуры

Процедура УстановитьПоУмолчаниюОбъект()
	
	Если ПустаяСтрока(ИмяОбъекта) Тогда
		ИмяОбъекта = ЭлементыФормы.ИмяОбъекта.СписокВыбора[0].Значение;
	КонецЕсли;

КонецПроцедуры

Процедура УстановитьПоУмолчаниюТаблицу()
	
	СписокВыбораТаблиц = ЭлементыФормы.ИмяТаблицы.СписокВыбора;
	Если СписокВыбораТаблиц.Количество() > 1 Тогда
		ИмяТаблицы = СписокВыбораТаблиц[1].Значение;
		ЭлементыФормы.ИмяТаблицы.Видимость = Истина;
		ЭлементыФормы.НадписьИмяТаблицы.Видимость = Истина;
	Иначе
		ЭлементыФормы.ИмяТаблицы.Видимость = Ложь;
		ЭлементыФормы.НадписьИмяТаблицы.Видимость = Ложь;
	КонецЕсли;
	
КонецПроцедуры

Процедура ТипДанныхПриИзменении(Элемент)
	
	Если ТипДанныхСтарый = ТипДанных Тогда
		Возврат;
	Иначе
		ИмяОбъекта = "";
		ИмяТаблицы = "";
	КонецЕсли;
	
	ОбновитьСписокВыбораОбъектов();
	УстановитьПоУмолчаниюОбъект();
	ОбновитьСписокВыбораТаблиц();
	УстановитьПоУмолчаниюТаблицу();
	
	ИнициализацияОтчета();
	ТипДанныхСтарый = ТипДанных;
	ТиповыеОтчеты.ОбновитьФормуТиповогоОтчетаПоКомпоновщику(ОтчетОбъект, ЭтаФорма);
	ТиповыеОтчеты.ОбработкаИзмененияТиповогоОтчетаНаФормеОтчета(ОтчетОбъект, ЭтаФорма);
	
КонецПроцедуры

Процедура ИмяОбъектаПриИзменении(Элемент)
	
	Если ИмяОбъектаСтарый = ИмяОбъекта Тогда
		Возврат;
	Иначе
		ИмяТаблицы = "";
	КонецЕсли;
	
	ОбновитьСписокВыбораТаблиц();
	УстановитьПоУмолчаниюТаблицу();
	
	ИнициализацияОтчета();
	ИмяОбъектаСтарый = ИмяОбъекта;
	ТиповыеОтчеты.ОбновитьФормуТиповогоОтчетаПоКомпоновщику(ОтчетОбъект, ЭтаФорма);
	ТиповыеОтчеты.ОбработкаИзмененияТиповогоОтчетаНаФормеОтчета(ОтчетОбъект, ЭтаФорма);
	
КонецПроцедуры

Процедура ИмяТаблицыПриИзменении(Элемент)
	
	Если ИмяТаблицыСтарый = ИмяТаблицы Тогда
		Возврат;
	КонецЕсли;
	
	ИнициализацияОтчета();
	ИмяТаблицыСтарый = ИмяТаблицы;
	ТиповыеОтчеты.ОбновитьФормуТиповогоОтчетаПоКомпоновщику(ОтчетОбъект, ЭтаФорма);
	ТиповыеОтчеты.ОбработкаИзмененияТиповогоОтчетаНаФормеОтчета(ОтчетОбъект, ЭтаФорма);
	
КонецПроцедуры

Процедура ТабличноеПолеОтборПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	ТиповыеОтчеты.ОбработкаИзмененияТиповогоОтчетаНаФормеОтчета(ОтчетОбъект, ЭтаФорма);
	
КонецПроцедуры