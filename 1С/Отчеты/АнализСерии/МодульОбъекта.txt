//Печать наименования раздела
Процедура НапечататьРаздел(Макет, ДокументРезультат, Раздел)
	
	Секция = Макет.ПолучитьОбласть("Раздел");
	Если Раздел = 1 Тогда
		Секция.Параметры.Раздел = "Движение по сериям";
	ИначеЕсли Раздел = 2 Тогда
		Секция.Параметры.Раздел = "Внутренняя сертификация";
	ИначеЕсли Раздел = 3 Тогда
		Секция.Параметры.Раздел = "Внешняя сертификация";
	Иначе
		Секция.Параметры.Раздел = "Рекламации";
	КонецЕсли;
	
	ДокументРезультат.Вывести(Секция);
	
КонецПроцедуры//НапечататьРаздел()

//Относит документ, переданный в качестве параметра, к конкретному разделу
Функция ПолучитьРаздел(Ссылка)
	
	Если ТипЗнч(Ссылка) = Тип("ДокументСсылка.ВозвратТоваровОтПокупателя") Тогда
		Возврат 4;
	ИначеЕсли ТипЗнч(Ссылка) = Тип("ДокументСсылка.ЗаявкаНаСертификациюНоменклатуры") Тогда
		Если Ссылка.ВидОперации = Перечисления.ВидыОперацийЗаявкаНаСертификациюНоменклатуры.внутренняя Тогда
			Возврат 2;
		Иначе
			Возврат 3;
		КонецЕсли;
	 ИначеЕсли ТипЗнч(Ссылка) = Тип("ДокументСсылка.АктОтбораПробНоменклатуры") Тогда
		Если Ссылка.ВидОперации = Перечисления.ВидыОперацийАктОтбораПробНоменклатуры.ДляВнутреннейСертификации Тогда
			Возврат 2;
		Иначе
			Возврат 3;
		КонецЕсли;
	ИначеЕсли ТипЗнч(Ссылка) = Тип("ДокументСсылка.СертификацияНоменклатуры") Тогда
		Если Ссылка.ВидОперации = Перечисления.ВидыОперацийСертификацияНоменклатуры.внутренняя Тогда
			Возврат 2;
		Иначе
			Возврат 3;
		КонецЕсли;
	Иначе
		Возврат 1;
	КонецЕсли;

КонецФункции//ПолучитьРаздел()

// Процедура заполняет данными поле табличного документа отчета
//
// Параметры
//  ДокументРезультат - поле табличного документа
//
// Возвращаемое значение
//  НЕТ
//
Процедура СформироватьОтчет(ДокументРезультат) Экспорт
	
	Если НЕ ЗначениеЗаполнено(СерияНоменклатуры) Тогда
		ОбщегоНазначения.СообщитьОбОшибке("Не выбрана серия номенклатуры");
		Возврат;
	КонецЕсли;
	
	ДатаКон = ?(ЗначениеЗаполнено(ДатаОтчета), КонецДня(ДатаОтчета), КонецДня(ТекущаяДата()));
	
	Макет = ЭтотОбъект.ПолучитьМакет("АнализСерии");
	ДокументРезультат.Очистить();
	
	Секция = Макет.ПолучитьОбласть("Шапка");
	Секция.Параметры.ДатаОтч = Формат(ДатаКон,"ДЛФ = Д");
	Секция.Параметры.Серия = СерияНоменклатуры;
	Секция.Параметры.Наименование = СерияНоменклатуры.Владелец;
	
	ДокументРезультат.Вывести(Секция);
	
	Мас = Новый Массив(1);
	Мас[0] = СерияНоменклатуры;
	ТаблицаСсылок = НайтиПоСсылкам(Мас);
	ТаблицаДокументов = Новый ТаблицаЗначений;
	ТаблицаДокументов.Колонки.Добавить("Документ");
	ТаблицаДокументов.Колонки.Добавить("Дата");
	ТаблицаДокументов.Колонки.Добавить("Раздел");
	ТаблицаДокументов.Колонки.Добавить("Метаданные");
	
	Для каждого СтрокаТаблицыСсылок Из ТаблицаСсылок Цикл
		
		Если ОбщегоНазначения.ЭтоДокумент(СтрокаТаблицыСсылок.Метаданные) Тогда
			
			Если СтрокаТаблицыСсылок.Данные.Дата <= ДатаКон
					И СтрокаТаблицыСсылок.Данные.Проведен Тогда
					
				НоваяСтрока 			= ТаблицаДокументов.Добавить();
				НоваяСтрока.Документ 	= СтрокаТаблицыСсылок.Данные;
				НоваяСтрока.Дата 		= СтрокаТаблицыСсылок.Данные.Дата;
				НоваяСтрока.Метаданные	= СтрокаТаблицыСсылок.Метаданные;
				
				Если ПоРазделам Тогда
					НоваяСтрока.Раздел = ПолучитьРаздел(СтрокаТаблицыСсылок.Данные)
				Иначе
					НоваяСтрока.Раздел = "";
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
	ТаблицаДокументов.Сортировать("Раздел, Дата");
	ТекущийРаздел = "";
	
	Ном = 0;
	Для каждого СтрокаТаблицыДокументов Из ТаблицаДокументов Цикл
		
		Если СтрокаТаблицыДокументов.Раздел <> ТекущийРаздел Тогда
			ТекущийРаздел = СтрокаТаблицыДокументов.Раздел;
			НапечататьРаздел(Макет, ДокументРезультат, ТекущийРаздел);
			Ном = 0;
		КонецЕсли;
		
		Ном = Ном + 1;
		Секция = Макет.ПолучитьОбласть("Строка");
		Секция.Параметры.Ном 		= Ном;
		Секция.Параметры.Документ 	= СтрокаТаблицыДокументов.Метаданные;
		Секция.Параметры.НомерДата 	= Формат(СтрокаТаблицыДокументов.Документ.Дата,"ДЛФ = Д")
										+ Символы.ПС
										+ Строка(СтрокаТаблицыДокументов.Документ.Номер);
		Секция.Параметры.Документ1 	= СтрокаТаблицыДокументов.Документ;
		Секция.Параметры.Примечание = СтрокаТаблицыДокументов.Документ.Комментарий;
		
		ДокументРезультат.Вывести(Секция);
	
	КонецЦикла;
	
	Если ПоказатьОстатки Тогда
		
		Секция = Макет.ПолучитьОбласть("Шапка1");
		ДокументРезультат.Вывести(Секция);
		Секция = Макет.ПолучитьОбласть("Строка1");
		
		ЗапросСклады = Новый Запрос;
		ЗапросСклады.Текст = 
			"ВЫБРАТЬ РАЗРЕШЕННЫЕ
			|	Склад,
			|	КоличествоОстаток
			|ИЗ 
			|	РегистрНакопления.ТоварыНаСкладах.Остатки(&Дата)
			|ГДЕ 
			|	СерияНоменклатуры = &Серия";
			
		ЗапросСклады.УстановитьПараметр("Серия", СерияНоменклатуры);
		ЗапросСклады.УстановитьПараметр("Дата", Новый Граница(ДатаКон, ВидГраницы.Включая));
		
		Выборка = ЗапросСклады.Выполнить().Выбрать();
		Ном = 0;
		Пока Выборка.Следующий() Цикл
			
			Ном = Ном + 1;
			Секция.Параметры.Ном 		= Ном;
			Секция.Параметры.Склад 		= Выборка.Склад;
			Секция.Параметры.Остаток 	= Формат(Выборка.КоличествоОстаток, "ЧДЦ = 3");
			ДокументРезультат.Вывести(Секция);
			
		КонецЦикла;
		
		Секция = Макет.ПолучитьОбласть("Отгрузка");
		
		ЗапросВсегоРеализация = Новый Запрос;
		ЗапросВсегоРеализация.Текст = 
			"ВЫБРАТЬ РАЗРЕШЕННЫЕ
			|	СУММА(ТоварыНаСкладах.Количество) КАК КоличествоРеализовано
			|ИЗ
			|	РегистрНакопления.ТоварыНаСкладах КАК ТоварыНаСкладах
			|ГДЕ
			|	ТоварыНаСкладах.Период <= &ДатаКон
			|	И ТоварыНаСкладах.Регистратор ССЫЛКА Документ.РеализацияТоваровУслуг
			|	и ТоварыНаСкладах.СерияНоменклатуры = &Серия";
		
		ЗапросВсегоРеализация.УстановитьПараметр("Серия",	СерияНоменклатуры);
		ЗапросВсегоРеализация.УстановитьПараметр("ДатаКон", ДатаКон);
		
		ВыборкаВсегоРеализации = ЗапросВсегоРеализация.Выполнить().Выбрать();
		Пока ВыборкаВсегоРеализации.Следующий() Цикл
			Секция.Параметры.ОтгруженоВсего = Формат(ВыборкаВсегоРеализации.КоличествоРеализовано, "ЧДЦ = 3");
			ДокументРезультат.Вывести(Секция);
		КонецЦикла;
		
	КонецЕсли;
	
	ДокументРезультат.ТолькоПросмотр = Истина;
	
КонецПроцедуры //СформироватьОтчет()