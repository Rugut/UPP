#Если ТолстыйКлиентОбычноеПриложение Тогда


// Макет отчета
Перем мМакет;

// Структура, содержащая связь полей быстрого отбора с путями к данным отбора Построителя
Перем мСтруктураСвязиЭлементовСДанными Экспорт;

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОБЩЕГО НАЗНАЧЕНИЯ

// Процедура меняет видимость заголовка поля табличного документа
// 
// Параметры
//  Таб - табличный документ
//
// Возвращаемые значения
//  НЕТ
Процедура ИзменитьВидимостьЗаголовка(Таб) Экспорт

	ОбластьВидимости = Таб.Область(1,,0,);
	ОбластьВидимости.Видимость = ПоказыватьЗаголовок;

КонецПроцедуры

Процедура ЗаполнитьТаблицуВидовКИ() Экспорт
	
	ВидыКонтактнойИнформации.Добавить().ТипКонтактнойИнформации = Перечисления.ТипыКонтактнойИнформации.Телефон;	
	ВидыКонтактнойИнформации.Добавить().ТипКонтактнойИнформации = Перечисления.ТипыКонтактнойИнформации.Телефон;	
	ВидыКонтактнойИнформации.Добавить().ТипКонтактнойИнформации = Перечисления.ТипыКонтактнойИнформации.Телефон;	
	ВидыКонтактнойИнформации.Добавить().ТипКонтактнойИнформации = Перечисления.ТипыКонтактнойИнформации.Адрес;	
	ВидыКонтактнойИнформации.Добавить().ТипКонтактнойИнформации = Перечисления.ТипыКонтактнойИнформации.АдресЭлектроннойПочты;	
	ВидыКонтактнойИнформации.Добавить().ТипКонтактнойИнформации = Перечисления.ТипыКонтактнойИнформации.ВебСтраница;

КонецПроцедуры

// Процедура передает построителю отчета запрос
//
// Параметры
//  НЕТ
//
// Возвращаемое значение
//  НЕТ
//
Процедура ЗаполнитьНачальныеНастройки() Экспорт
	
	МассивОтбора = Новый Массив;

	ТекстЗапроса = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	РаботникиСрезПоследних.ФизЛицо КАК ФизЛицо,
	|	РаботникиСрезПоследних.Должность КАК Должность,
	|	РаботникиСрезПоследних.Подразделение КАК Подразделение,
	|	РаботникиСрезПоследних.Должность.Наименование КАК ДолжностьНаименование,
	|	РаботникиСрезПоследних.Подразделение.Наименование КАК ПодразделениеНаименование,
	|	РаботникиСрезПоследних.Физлицо.ОсновноеИзображение КАК ОсновноеИзображение,
	|	РаботникиСрезПоследних.ФизЛицо.Наименование КАК ФизЛицоНаименование,
	|	ФИОФизЛицСрезПоследних.Фамилия КАК Фамилия,
	|	ФИОФизЛицСрезПоследних.Имя КАК Имя,
	|	ФИОФизЛицСрезПоследних.Отчество КАК Отчество,
	|	ВЫРАЗИТЬ(КонтактнаяИнформация1.Представление КАК СТРОКА(30)) КАК Телефон1,
	|	ВЫРАЗИТЬ(КонтактнаяИнформация2.Представление КАК СТРОКА(30)) КАК Телефон2,
	|	ВЫРАЗИТЬ(КонтактнаяИнформация3.Представление КАК СТРОКА(30)) КАК Телефон3,
	|	ВЫРАЗИТЬ(КонтактнаяИнформация4.Представление КАК СТРОКА(100)) КАК Адрес,
	|	ВЫРАЗИТЬ(КонтактнаяИнформация5.Представление КАК СТРОКА(50)) КАК АдресЭлектроннойПочты,
	|	ВЫРАЗИТЬ(КонтактнаяИнформация6.Представление КАК СТРОКА(50)) КАК ВебСтраница
	|{ВЫБРАТЬ
	|	ФизЛицо КАК Фамилия,
	|	Имя,
	|	Отчество,
	|	ПодразделениеНаименование,
	|	ДолжностьНаименование,
	|	ОсновноеИзображение,
	|	Телефон1,
	|	Телефон2,
	|	Телефон3,
	|	Адрес,
	|	АдресЭлектроннойПочты,
	|	ВебСтраница}
	|ИЗ
	|	РегистрСведений.Работники.СрезПоследних(&ДатаНач, ) КАК РаботникиСрезПоследних
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КонтактнаяИнформация КАК КонтактнаяИнформация1
	|		ПО (&ИспользуетсяТелефон1)
	|			И (&ВидТелефон1 = КонтактнаяИнформация1.Вид)
	|			И (&Телефон = КонтактнаяИнформация1.Тип)
	|			И РаботникиСрезПоследних.Физлицо = КонтактнаяИнформация1.Объект
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КонтактнаяИнформация КАК КонтактнаяИнформация2
	|		ПО (&ИспользуетсяТелефон2)
	|			И (&ВидТелефон2 = КонтактнаяИнформация2.Вид)
	|			И (&Телефон = КонтактнаяИнформация2.Тип)
	|			И РаботникиСрезПоследних.Физлицо = КонтактнаяИнформация2.Объект
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КонтактнаяИнформация КАК КонтактнаяИнформация3
	|		ПО (&ИспользуетсяТелефон3)
	|			И (&ВидТелефон3 = КонтактнаяИнформация3.Вид)
	|			И (&Телефон = КонтактнаяИнформация3.Тип)
	|			И РаботникиСрезПоследних.Физлицо = КонтактнаяИнформация3.Объект
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КонтактнаяИнформация КАК КонтактнаяИнформация4
	|		ПО (&ИспользуетсяАдрес)
	|			И (&ВидАдрес = КонтактнаяИнформация4.Вид)
	|			И (&Адрес = КонтактнаяИнформация4.Тип)
	|			И РаботникиСрезПоследних.Физлицо = КонтактнаяИнформация4.Объект
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КонтактнаяИнформация КАК КонтактнаяИнформация5
	|		ПО (&ИспользуетсяАдресЭлектроннойПочты)
	|			И (&ВидАдресЭлектроннойПочты = КонтактнаяИнформация5.Вид)
	|			И (&АдресЭлектроннойПочты = КонтактнаяИнформация5.Тип)
	|			И РаботникиСрезПоследних.Физлицо = КонтактнаяИнформация5.Объект
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КонтактнаяИнформация КАК КонтактнаяИнформация6
	|		ПО (&ИспользуетсяВебСтраница)
	|			И (&ВидВебСтраница = КонтактнаяИнформация6.Вид)
	|			И (&ВебСтраница = КонтактнаяИнформация6.Тип)
	|			И РаботникиСрезПоследних.Физлицо = КонтактнаяИнформация6.Объект
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ФИОФизЛиц.СрезПоследних(&ДатаНач, ) КАК ФИОФизЛицСрезПоследних
	|		ПО РаботникиСрезПоследних.Физлицо = ФИОФизЛицСрезПоследних.ФизЛицо
	|ГДЕ
	|	РаботникиСрезПоследних.ПричинаИзмененияСостояния <> &Уволен
	|{ГДЕ
	|	РаботникиСрезПоследних.Подразделение,
	|	РаботникиСрезПоследних.Должность,
	|	РаботникиСрезПоследних.ФизЛицо}
	|{УПОРЯДОЧИТЬ ПО
	|	ПодразделениеНаименование,
	|	ДолжностьНаименование,
	|	ФизЛицоНаименование}
	|ИТОГИ ПО
	|	Подразделение,
	|	Должность
	|{ИТОГИ ПО
	|	Подразделение,
	|	Должность}";	 
	
	СтруктураПредставлениеПолей = Новый Структура("ПодразделениеНаименование,ДолжностьНаименование,Фамилия,Имя,Отчество,ОсновноеИзображение"
	+ ",Телефон1,Телефон2,Телефон3,Адрес,АдресЭлектроннойПочты,ВебСтраница",
		"Подразделение",
		"Должность",
		"Фамилия",
		"Имя",
		"Отчество",
		"ОсновноеИзображение",
		"Телефон1",
		"Телефон2",
		"Телефон3",
		"Адрес",
		"АдресЭлектроннойПочты",
		"ВебСтраница",
		"ОсновноеИзображение");

	ПостроительОтчета = Новый ПостроительОтчета;
	
		
	ПостроительОтчета.Текст = ТекстЗапроса;
	
	ПостроительОтчета.ТекстЗаголовка = "Визитные карточки работников";
	
	МассивОтбора.Добавить("Подразделение");
	МассивОтбора.Добавить("Должность");
	МассивОтбора.Добавить("ФизЛицо");
	
	УправлениеОтчетамиДополнительный.ОчиститьДополнительныеПоляПостроителя(ПостроительОтчета);
	УправлениеОтчетами.ЗаполнитьОтбор(МассивОтбора, ПостроительОтчета);
	
	УправлениеОтчетами.ЗаполнитьПредставленияПолей(СтруктураПредставлениеПолей, ПостроительОтчета);
	
	// Установим имена быстрых отборов
	Если ПостроительОтчета.Отбор.Найти("Подразделение") = Неопределено Тогда
		ПостроительОтчета.Отбор.Добавить("Подразделение");
	КонецЕсли;
	Если ПостроительОтчета.Отбор.Найти("Должность") = Неопределено Тогда
		ПостроительОтчета.Отбор.Добавить("Должность");
	КонецЕсли;
	Если ПостроительОтчета.Отбор.Найти("ФизЛицо") = Неопределено Тогда
		ПостроительОтчета.Отбор.Добавить("ФизЛицо");
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ФОРМИРОВАНИЕ ПЕЧАТНОЙ ФОРМЫ ОТЧЕТА

// Вывод строки отчета (с проверкой необходимости этого вывода)
//
// Параметры:
//	Выборка       - выборка из результата отчета, которая обходится в процедуре
//	СтруктураПараметров - структура параметров, необходимых для вывода строки
//
Процедура ВывестиСтроку(Выборка, СтруктураПараметров, ТипЗаписиВыборки, ИмяГруппировки, НомерВизитки, НовыйРаботник, СтарыйРаботник)
	
	ТабДок = СтруктураПараметров.ТабДок;
	Если ТипЗаписиВыборки = ТипЗаписиЗапроса.ДетальнаяЗапись Тогда
		
		ОбластьВизитки = СтруктураПараметров.ОбластьВизитки;
		
		Если НомерВизитки = 1 Тогда
			НомерВизитки = 2;
		Иначе	
			НомерВизитки = 1;
			Если НовыйРаботник Тогда
				ТабДок.Вывести(СтруктураПараметров.ОбластьВизиткиОбщийОтступ, Выборка.Уровень());
			КонецЕсли;
		КонецЕсли; 							
				
		ОбластьВизитки = СтруктураПараметров.ОбластьВизитки;					
		
		Если НовыйРаботник Тогда
			
			// Очистка всех полей визитки перед выводом
			Для К=0 по ОбластьВизитки.Рисунки.Количество()-1 Цикл
				Если ОбластьВизитки.Рисунки[К].ТипРисунка = ТипРисункаТабличногоДокумента.Текст тогда
					ОбластьВизитки.Рисунки[К].Текст = "";
				КонецЕсли;
			КонецЦикла; 
						
		КонецЕсли;						
		
		// Заполнение только выбранных полей визитки
		ОбластьВизитки.Рисунки["Фамилия"].Текст = Выборка.Фамилия;
		ОбластьВизитки.Рисунки["ИмяОтчество"].Текст = Выборка.Имя + " "+ Выборка.Отчество;
		ОбластьВизитки.Рисунки["Работники__Подразделение"].Текст = Выборка.ПодразделениеНаименование;
		ОбластьВизитки.Рисунки["Работники__Должность"].Текст = Выборка.ДолжностьНаименование;
		
		Если Выборка.ОсновноеИзображение <> Справочники.ХранилищеДополнительнойИнформации.ПустаяСсылка() Тогда
			ОбластьВизитки.Рисунки["Фотография"].Картинка = Выборка.ОсновноеИзображение.Хранилище.Получить();
		Иначе	
			ОбластьВизитки.Рисунки["Фотография"].Картинка = СтруктураПараметров.ПустаяКартинка;
		КонецЕсли; 
		
		ОбластьВизитки.Рисунки["Телефон1"].Текст = СокрЛП(Выборка.Телефон1);
		ОбластьВизитки.Рисунки["Телефон2"].Текст = СокрЛП(Выборка.Телефон2);	
		ОбластьВизитки.Рисунки["Телефон3"].Текст = СокрЛП(Выборка.Телефон3);
		ОбластьВизитки.Рисунки["АдресЭлектроннойПочты"].Текст = СокрЛП(Выборка.АдресЭлектроннойПочты);
		ОбластьВизитки.Рисунки["Адрес"].Текст = СокрЛП(Выборка.Адрес);
		ОбластьВизитки.Рисунки["ВебСтраница"].Текст = СокрЛП(Выборка.ВебСтраница);
		
		// Вывод разделителя между двумя визитками строки 
		Если НовыйРаботник Тогда
			ТабДок.Присоединить(ОбластьВизитки,Выборка.Уровень());
			
			Если НомерВизитки = 1 Тогда
				ТабДок.Присоединить(СтруктураПараметров.ОбластьРазделительВизиток, Выборка.Уровень());
			КонецЕсли;
		КонецЕсли;
		
	Иначе// Группировка
		
		УровеньЗаписи   = Выборка.Уровень();
		ОбластьСтрокаГруппировки = СтруктураПараметров.ОбластьСтрокаГруппировки;
		
		ЗначениеГруппировки = Выборка[ИмяГруппировки];
		Если НЕ ЗначениеЗаполнено(ЗначениеГруппировки) Тогда
			ЗначениеГруппировки = "<...>";
		КонецЕсли;
		
		ЗначениеРасшифровки = Выборка[ИмяГруппировки];
		
		ОбластьСтрокаГруппировки.Параметры.ЗначениеГруппировки = ?(ТипЗнч(ЗначениеГруппировки) = Тип("Строка"),СокрЛП(ЗначениеГруппировки),ЗначениеГруппировки);
		ОбластьСтрокаГруппировки.Параметры.Расшифровка = ЗначениеРасшифровки;
		
		ОбластьСтрокаГруппировки.Область().Отступ = УровеньЗаписи;
		ТабДок.Вывести(ОбластьСтрокаГруппировки);
		
		Если УровеньЗаписи = 0 Тогда
			ТабДок.Область(ТабДок.ВысотаТаблицы, 2, ТабДок.ВысотаТаблицы, ТабДок.ШиринаТаблицы).ЦветФона = ЦветаСтиля.ЦветФонаКнопки;
		Иначе
			ТабДок.Область(ТабДок.ВысотаТаблицы, 2, ТабДок.ВысотаТаблицы, ТабДок.ШиринаТаблицы).ЦветФона = ЦветаСтиля.ФонГруппировкиПромежуточногоУровня;
		КонецЕсли;
		
		НомерВизитки = 0;
		
	КонецЕсли;
	
КонецПроцедуры

// Обход выборки из результата запроса по группировкам для вывода строк отчета
//
// Параметры:
//
//	Выборка       - выборка из результата отчета, которая обходится в процедуре,
//	СтруктураПараметров - структура параметров, передеваемых в процедуру вывода
//	                строки отчета,
//	Номер         - число, номер обходимой группировки
//	НомерТочки    - число, номер точки диаграммы
//
Процедура ВывестиВыборку(Выборка, СтруктураПараметров, Номер, СтарыйРаботник = Null)

	ОбработкаПрерыванияПользователя();

	НомерВизитки = 0;
	СтарыйРаботник1 = СтарыйРаботник;
	
	// Берутся группировки все подряд 
	Пока Выборка.Следующий() Цикл		
		ВывестиСтроку(Выборка, СтруктураПараметров, Выборка.ТипЗаписи(), Выборка.Группировка(), НомерВизитки, НЕ(СтарыйРаботник1 = Выборка.ФизЛицо), СтарыйРаботник1);
		СтарыйРаботник1 = Выборка.ФизЛицо;
		ВывестиВыборку(Выборка.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам), СтруктураПараметров, Номер + 1, СтарыйРаботник1);
	КонецЦикла;

	// Если в строку вывели только одну визитку, то присоединим пустую визитку
    Если НомерВизитки = 1  Тогда
		ТабДок = СтруктураПараметров.ТабДок;
		ТабДок.Присоединить(СтруктураПараметров.ОбластьПустойВизитки, Выборка.Уровень());
	КонецЕсли; 
	
КонецПроцедуры // ВывестиВыборку()

// Формирует "шапку" отчета
//
// Параметры: 
//  Нет.
//
// Возвращаемое значение:
//  Нет.
//
Функция ЗаголовокОтчета(Отчет) 

	ДокументРезультат = Новый ТабличныйДокумент;
	Макет = мМакет;

	// Строки списков для шапки отчета
	СтрПоказатели = "";
	СтрГруппировки = "";
	СтрФильтры = "";

	Для Каждого Строка Из ПостроительОтчета.ИзмеренияСтроки цикл

		Если ПустаяСтрока(Строка.Имя) Тогда
			Предупреждение("Не указано имя группировки!", 60);
			Возврат ДокументРезультат;		
		КонецЕсли;		
		// Если не задано представление, берем его из имени
		Если ПустаяСтрока(Строка.Имя) Тогда
			Строка.ПредставлениеГруппировки = Строка.Имя;
		КонецЕсли;
		СтрГруппировки=СтрГруппировки + ", " + Строка.Имя;

	КонецЦикла;

	Для Каждого СтрокаПоказатели Из ПостроительОтчета.ДоступныеПоля Цикл
		// Строка для шапки отчета
		Если (Найти(СтрокаПоказатели.Имя,"Наименование") > 0) Или (СтрокаПоказатели.Имя = "ФизЛицо") Тогда
			Продолжить;
		КонецЕсли;
		СтрПоказатели = СтрПоказатели + ", " + СтрокаПоказатели.Имя;
	КонецЦикла;


	Для Каждого Строка Из ПостроительОтчета.Отбор Цикл

		Если ПустаяСтрока(Строка.Имя) Тогда
			Предупреждение("Не указано имя отбора!", 60);
			Возврат ДокументРезультат;
		КонецЕсли;
		
		Если Строка.Использование Тогда
			СтрФильтры=СтрФильтры+", "+ Строка.Имя + " " + Строка.ВидСравнения + " " + Строка.Значение;
		КонецЕсли;
		
	КонецЦикла;
	
	СтрПоказатели  = Сред(СтрПоказатели,  2);
	СтрГруппировки = Сред(СтрГруппировки, 2);
	СтрФильтры     = Сред(СтрФильтры,     2);

	Для Сч = 1 По 3 Цикл

		Если Сч = 1 Тогда
			Префикс = "Верх";
		ИначеЕсли Сч = 2 Тогда
			// Если нет фильтров, не выводим
			Если НЕ ЗначениеЗаполнено(СтрФильтры) Тогда
				Продолжить;
			КонецЕсли;
			Префикс = "Середина";
		ИначеЕсли Сч = 3 Тогда
			Префикс = "Низ";
		КонецЕсли;

		// Вывод шапки отчета
		ОбластьШапка = Макет.ПолучитьОбласть("Шапка" + Префикс);

		Если Префикс = "Верх" Тогда
			ОбластьШапка.Параметры.ЗаголовокОтчета = Отчет.Метаданные().Синоним;			
		ИначеЕсли Префикс = "Середина" Тогда
			ОбластьШапка.Параметры.Фильтры = "Отбор: " + СтрФильтры;
		ИначеЕсли Префикс = "Низ" Тогда
			ОбластьШапка.Параметры.Группировки = "Группировки: " + СтрГруппировки;
			ОбластьШапка.Параметры.Показатели = "Показатели: " + СтрПоказатели;
		КонецЕсли;

        ДокументРезультат.Вывести(ОбластьШапка);

	КонецЦикла;

	Возврат ДокументРезультат

КонецФункции

// Процедура заполняет ПолеТабличногоДокумента
//
// Параметры - Таб - ПолеТабличногоДокумента
Процедура СформироватьОтчет(Таб, ТолькоЗаголовок = Ложь) Экспорт
	
	ПостроительОтчета.Параметры.Вставить("ДатаНач", ДатаНач);
	ПостроительОтчета.Параметры.Вставить("Уволен", Перечисления.ПричиныИзмененияСостояния.Увольнение);
	ПостроительОтчета.Параметры.Вставить("Телефон", Перечисления.ТипыКонтактнойИнформации.Телефон);
	ПостроительОтчета.Параметры.Вставить("Адрес", Перечисления.ТипыКонтактнойИнформации.Адрес);
	ПостроительОтчета.Параметры.Вставить("АдресЭлектроннойПочты", Перечисления.ТипыКонтактнойИнформации.АдресЭлектроннойПочты);
	ПостроительОтчета.Параметры.Вставить("ВебСтраница", Перечисления.ТипыКонтактнойИнформации.ВебСтраница);
	
	ПостроительОтчета.Параметры.Вставить("ВидТелефон1", ВидыКонтактнойИнформации[0].ВидКонтактнойИнформации);
	ПостроительОтчета.Параметры.Вставить("ВидТелефон2", ВидыКонтактнойИнформации[1].ВидКонтактнойИнформации);
	ПостроительОтчета.Параметры.Вставить("ВидТелефон3", ВидыКонтактнойИнформации[2].ВидКонтактнойИнформации);
	ПостроительОтчета.Параметры.Вставить("ВидАдрес", ВидыКонтактнойИнформации[3].ВидКонтактнойИнформации);
	ПостроительОтчета.Параметры.Вставить("ВидАдресЭлектроннойПочты", ВидыКонтактнойИнформации[4].ВидКонтактнойИнформации);
	ПостроительОтчета.Параметры.Вставить("ВидВебСтраница", ВидыКонтактнойИнформации[5].ВидКонтактнойИнформации);
	
	ПостроительОтчета.Параметры.Вставить("ИспользуетсяТелефон1", ВидыКонтактнойИнформации[0].Использование);
	ПостроительОтчета.Параметры.Вставить("ИспользуетсяТелефон2", ВидыКонтактнойИнформации[1].Использование);
	ПостроительОтчета.Параметры.Вставить("ИспользуетсяТелефон3", ВидыКонтактнойИнформации[2].Использование);
	ПостроительОтчета.Параметры.Вставить("ИспользуетсяАдрес", ВидыКонтактнойИнформации[3].Использование);
	ПостроительОтчета.Параметры.Вставить("ИспользуетсяАдресЭлектроннойПочты", ВидыКонтактнойИнформации[4].Использование);
	ПостроительОтчета.Параметры.Вставить("ИспользуетсяВебСтраница", ВидыКонтактнойИнформации[5].Использование);
	
	ПостроительОтчета.Выполнить();
	
	Таб.Очистить();
			
	ОбластьЗаголовка = ЗаголовокОтчета(ЭтотОбъект);
	ВысотаЗаголовка = ОбластьЗаголовка.ВысотаТаблицы;
	Таб.Вывести(ОбластьЗаголовка);
	Таб.Область("R1:R" + ВысотаЗаголовка).Видимость = ПоказыватьЗаголовок;	
	
	// Вывод таблицы данных
    СтруктураПараметров = Новый Структура;
	СтруктураПараметров.Вставить("ТабДок", Таб);
	СтруктураПараметров.Вставить("ОбластьСтрокаГруппировки", мМакет.ПолучитьОбласть("СтрокаГруппировки"));
	СтруктураПараметров.Вставить("ОбластьВизиткиОбщийОтступ", мМакет.ПолучитьОбласть("СтрокаВизитки | ОбщийОтступ"));
	СтруктураПараметров.Вставить("ОбластьВизитки", мМакет.ПолучитьОбласть("СтрокаВизитки | КолонкаВизитка"));
	СтруктураПараметров.Вставить("ОбластьПустойВизитки", мМакет.ПолучитьОбласть("СтрокаВизитки | КолонкаПустаяВизитка"));
	СтруктураПараметров.Вставить("ОбластьРазделительВизиток", мМакет.ПолучитьОбласть("СтрокаВизитки | КолонкаРазделительВизиток"));
	СтруктураПараметров.Вставить("ПустаяКартинка", Новый Картинка);
	
	РезультатЗапроса = ПостроительОтчета.Результат;
	
	Таб.НачатьАвтогруппировкуСтрок();
	
	ВывестиВыборку(РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам), СтруктураПараметров, 0);	
	
	Таб.ЗакончитьАвтогруппировкуСтрок();

	//Параметры документа
	Таб.Автомасштаб 			= 	Истина;
	Таб.ОриентацияСтраницы	 	= 	ОриентацияСтраницы.Ландшафт;
	Таб.Защита              	= 	Ложь;
	Таб.ТолькоПросмотр      	= 	Истина;


КонецПроцедуры

// Определим значения переменных объекта
мМакет = ПолучитьМакет("Макет");
ПоказыватьЗаголовок = Истина;

#КонецЕсли