Процедура ПередОткрытием(Отказ, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(ТекущийСертификат) Тогда
		РезультатПоиска = Сертификаты.Найти(ТекущийСертификат.Отпечаток, "Отпечаток");
		Если РезультатПоиска <> Неопределено Тогда
			ЭлементыФормы.Сертификаты.ТекущаяСтрока = РезультатПоиска;
		КонецЕсли;
	КонецЕсли;
	
	Если Сертификаты.Количество() > 0 Тогда
		ЭлементыФормы.Сертификаты.ТекущаяСтрока = Сертификаты[0];
	КонецЕсли;
	
КонецПроцедуры

Процедура ДействияФормыОткрыть(Кнопка)
	
	ТекСертификат = ЭлементыФормы.Сертификаты.ТекущаяСтрока;
	Если ЗначениеЗаполнено(ТекСертификат) Тогда
		ПоказатьСертификатВСистемномОкне(ТекСертификат);
	КонецЕсли;
	
КонецПроцедуры

Функция СоздатьОбъектДляРаботыСКриптографией()
	
	Попытка
		Крипт = Новый("Addin.CryptS");
		Крипт.ИмяКриптопровайдера = "Crypto-Pro GOST R 34.10-2001 Cryptographic Service Provider";
		Крипт.ТипКриптопровайдера = 75;
		Возврат Крипт;
	Исключение
		Возврат Неопределено;
	КонецПопытки;
	
КонецФункции

Процедура ПоказатьСертификатВСистемномОкне(Сертификат)
	
	Крипт = СоздатьОбъектДляРаботыСКриптографией();
	Если Крипт <> Неопределено Тогда
		СерийныйНомер = Сертификат.СерийныйНомер;
		Поставщик = Сертификат.Поставщик;
		Крипт.ПоказатьСертификат(СерийныйНомер, Поставщик);
	КонецЕсли;
	
КонецПроцедуры

Процедура СертификатыПриАктивизацииСтроки(Элемент)
	
	ДоступностьКнопок = (ЭлементыФормы.Сертификаты.ТекущаяСтрока <> Неопределено);
	
	ЭлементыФормы.ДействияФормы.Кнопки.Выбрать.Доступность = ДоступностьКнопок;
	ЭлементыФормы.ДействияФормы.Кнопки.Открыть.Доступность = ДоступностьКнопок;
	
КонецПроцедуры

Процедура ДействияФормыВыбрать(Кнопка)
	
	ВыбратьСертификат();
	
КонецПроцедуры

Процедура ВыбратьСертификат()
	
	ТекСертификат = ЭлементыФормы.Сертификаты.ТекущаяСтрока;
	Если ТекСертификат = Неопределено Тогда
		Предупреждение("Не выбран сертификат.");
		Возврат;
	КонецЕсли;
	
	Результат = Новый Структура;
	Для Каждого Колонка Из Сертификаты.Колонки Цикл
		Результат.Вставить(Колонка.Имя, ТекСертификат[Колонка.Имя]);
	КонецЦикла;
	
	Закрыть(Результат);
	
КонецПроцедуры

Процедура СертификатыПриПолученииДанных(Элемент, ОформленияСтрок)
	
	Для Каждого ОформлениеСтроки Из ОформленияСтрок Цикл
		ОформлениеСтроки.Ячейки.Отпечаток.УстановитьТекст(РасставитьПробелыВОтпечатке(ОформлениеСтроки.ДанныеСтроки.Отпечаток));
	КонецЦикла;
	
КонецПроцедуры

Функция РасставитьПробелыВОтпечатке(Знач ЗначениеОтпечатка)
	
	Если НЕ ЗначениеЗаполнено(ЗначениеОтпечатка) Тогда
		Возврат "";
	КонецЕсли;
	
	Для Инд = 1 По 19 Цикл
		ВхождениеСимвола = 40 - Инд*2;
		ЗначениеОтпечатка = Лев(ЗначениеОтпечатка, ВхождениеСимвола) + " " + Сред(ЗначениеОтпечатка, ВхождениеСимвола + 1);
	КонецЦикла;
	
	Возврат ЗначениеОтпечатка;
	
КонецФункции

Процедура СертификатыВыбор(Элемент, ВыбраннаяСтрока, Колонка, СтандартнаяОбработка)
	
	ВыбратьСертификат();
	
КонецПроцедуры