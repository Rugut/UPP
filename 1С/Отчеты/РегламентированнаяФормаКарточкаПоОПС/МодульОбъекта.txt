////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

#Если ТолстыйКлиентОбычноеПриложение Тогда

// Заполняет табличный документ
// в соответствии с настройками, заданными значениями реквизитов отчета.
//
// Параметры:
//	ДокументРезультат - табличный документ, формируемый отчетом,
//
Процедура СформироватьОтчет(ДокументРезультат) Экспорт

	Отказ = Ложь;
	
	// Проверка параметров
	
	Если НЕ ЗначениеЗаполнено(Организация) Тогда
		ОбщегоНазначения.СообщитьОбОшибке("Не указана организация!", Отказ);
	КонецЕсли; 
	Если НЕ ЗначениеЗаполнено(НалоговыйПериод) Тогда
		ОбщегоНазначения.СообщитьОбОшибке("Не указан налоговый период!", Отказ);
	КонецЕсли; 
	Если НЕ ЗначениеЗаполнено(Физлицо) Тогда
		ОбщегоНазначения.СообщитьОбОшибке("Не указан сотрудник организации!", Отказ);
	КонецЕсли; 
	Если Отказ Тогда
		Возврат ;
	КонецЕсли; 	
	
	ДокументРезультат.Очистить();
	Макет =	ПолучитьМакет("ИндивидуальнаяКарточкаОПС");

	// Расчет вычисляемых параметров
	ДатаНачалаНП = НачалоГода(Дата(НалоговыйПериод,1,1));
	ДатаКонцаНП = КонецГода(Дата(НалоговыйПериод,1,1));
	ГоловнаяОрганизация = ОбщегоНазначения.ГоловнаяОрганизация(Организация);
	
	// определим формат вывода данных о налогах
	НалоговыйУчет.ОбновитьДанныеУчетнойПолитикиПоНалоговомуУчету(глЗначениеПеременной("УчетнаяПолитикаОтраженияЗарплатыВУчете"), ДатаНачалаНП, Организация);
	УчетнаяПолитика = глЗначениеПеременной("УчетнаяПолитикаОтраженияЗарплатыВУчете")[КонецМесяца(ДатаНачалаНП)][Организация];
	Если УчетнаяПолитика.ТочностьИсчисленияПФР = Перечисления.ПорядкиОкругленияЕСН.ВРубляхИКопейках Тогда
		ФорматВыводаДанных = "ЧДЦ=2; ЧРД=.";
	ИначеЕсли УчетнаяПолитика.ТочностьИсчисленияПФР = Перечисления.ПорядкиОкругленияЕСН.ВРублях Тогда
		ФорматВыводаДанных = "ЧДЦ=0; ЧРД=.";
	Иначе 	
		ФорматВыводаДанных = "ЧДЦ=5; ЧРД=.";
	КонецЕсли;
	
	// Создание запроса и установка всех необходимых параметров
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("ГоловнаяОрганизация", ГоловнаяОрганизация);
	Запрос.УстановитьПараметр("парамГоловнаяОрганизация", ГоловнаяОрганизация);
	Запрос.УстановитьПараметр("ФизЛицо", ФизЛицо);
	Запрос.УстановитьПараметр("НачалоНП", ДатаНачалаНП);
	Запрос.УстановитьПараметр("ВидСтавокЕСНиПФР", ГоловнаяОрганизация.ВидСтавокЕСНиПФР);
	Запрос.УстановитьПараметр("ГодНП", НалоговыйПериод);
	Запрос.УстановитьПараметр("КонецНП", ДатаКонцаНП);
	Запрос.УстановитьПараметр("ВидАдресаРегистрации" , Справочники.ВидыКонтактнойИнформации.ЮрАдресФизЛица);
	Запрос.УстановитьПараметр("ВидАдресаФактический" , Справочники.ВидыКонтактнойИнформации.ФактАдресФизЛица);

	СписокСтруктурныхПодразделений = ОбщегоНазначения.ПолучитьСписокОбособленныхПодразделенийОрганизации(ГоловнаяОрганизация);
	СписокСтруктурныхПодразделений.Добавить(ГоловнаяОрганизация);
	Запрос.УстановитьПараметр("СписокСтруктурныхПодразделений", СписокСтруктурныхПодразделений);
		
	// ---------------------------------------------------------------------------
	// Тексты запросов
	//

	// Сформируем текст запроса выборки месяцев налогового периода
	МесяцыНПТекст = "ВЫБРАТЬ 1 КАК МЕСЯЦ ПОМЕСТИТЬ ВТМесяцыНП";
	Для Сч = 2 По 12 Цикл
    	МесяцыНПТекст = МесяцыНПТекст +" ОБЪЕДИНИТЬ ВСЕ ВЫБРАТЬ " + Сч;
	КонецЦикла;
	
	Запрос.Текст = МесяцыНПТекст;
	Запрос.Выполнить();
	
	// первый месяц
	КонецМесяца = КонецМесяца(ДатаНачалаНП);
	ПериодыТекст = "ВЫБРАТЬ ДАТАВРЕМЯ(" + Формат(КонецМесяца,"ДФ=гггг,М,д,Ч,м,с") + ") КАК Период ПОМЕСТИТЬ ВТПериоды";
	// прибавим остальные месяцы
	Для Сч = 2 По 12 Цикл
		КонецМесяца = КонецМесяца(КонецМесяца+1);
		ПериодыТекст = ПериодыТекст +" ОБЪЕДИНИТЬ ВСЕ ВЫБРАТЬ ДАТАВРЕМЯ(" + Формат(КонецМесяца,"ДФ=гггг,М,д,Ч,м,с") + ")";
	КонецЦикла;
	
	Запрос.Текст = ПериодыТекст;
	Запрос.Выполнить();
	
	//-----------------------------------------------------------------------------
	// ВЫБОРКА СВЕДЕНИЙ О ФИЗЛИЦЕ 
	// 	
	
	ДанныеОФизлицеТекст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ЕСТЬNULL(ФИОФизЛица.Фамилия + "" "" + ФИОФизЛица.Имя + "" "" + ФИОФизЛица.Отчество, ДанныеОФизЛице.Наименование) КАК ФИО,
	|	ДанныеОФизЛице.ИНН КАК ИНН,
	|	ДанныеОФизЛице.СтраховойНомерПФР КАК СтраховойНомерПФР,
	|	ВЫБОР
	|		КОГДА ДанныеОФизЛице.Пол = ЗНАЧЕНИЕ(Перечисление.ПолФизическихЛиц.Мужской)
	|			ТОГДА ""М""
	|		ИНАЧЕ ""Ж""
	|	КОНЕЦ КАК Пол,
	|	ДанныеОФизЛице.ДатаРождения КАК ДатаРождения,
	|	ЕСТЬNULL(ГражданствоФизЛица.Страна.Наименование, ""Россия"") КАК Гражданство,
	|	ПаспортныеДанныеФизЛица.ДокументСерия КАК ДокументСерия,
	|	ПаспортныеДанныеФизЛица.ДокументНомер КАК ДокументНомер,
	|	ПаспортныеДанныеФизЛица.ДокументДатаВыдачи,
	|	ВЫРАЗИТЬ(ПаспортныеДанныеФизЛица.ДокументКемВыдан КАК СТРОКА(300)) КАК ДокументКемКогдаВыдан,
	|	"","" + АдресаРегистрации.Поле1 + "","" + АдресаРегистрации.Поле2 + "","" + АдресаРегистрации.Поле3 + "","" + АдресаРегистрации.Поле4 + "","" + АдресаРегистрации.Поле5 + "","" + АдресаРегистрации.Поле6 + "","" + АдресаРегистрации.Поле7 + "","" + АдресаРегистрации.Поле8 + "","" + АдресаРегистрации.Поле9 + "","" + ВЫБОР
	|		КОГДА АдресаРегистрации.ТипДома В (ЗНАЧЕНИЕ(Перечисление.ТипыДомов.Дом), ЗНАЧЕНИЕ(Перечисление.ТипыДомов.ПустаяСсылка))
	|			ТОГДА ""дом""
	|		ИНАЧЕ ""владение""
	|	КОНЕЦ + "","" + ВЫБОР
	|		КОГДА АдресаРегистрации.ТипКорпуса В (ЗНАЧЕНИЕ(Перечисление.ТипыКорпусов.Корпус), ЗНАЧЕНИЕ(Перечисление.ТипыКорпусов.ПустаяСсылка))
	|			ТОГДА ""корпус""
	|		ИНАЧЕ ""строение""
	|	КОНЕЦ + "","" + ВЫБОР
	|		КОГДА АдресаРегистрации.ТипКвартиры В (ЗНАЧЕНИЕ(Перечисление.ТипыКвартир.Квартира), ЗНАЧЕНИЕ(Перечисление.ТипыКвартир.ПустаяСсылка))
	|			ТОГДА ""кв.""
	|		ИНАЧЕ ""оф.""
	|	КОНЕЦ КАК АдресРегистрации,
	|	"","" + АдресаФактические.Поле1 + "","" + АдресаФактические.Поле2 + "","" + АдресаФактические.Поле3 + "","" + АдресаФактические.Поле4 + "","" + АдресаФактические.Поле5 + "","" + АдресаФактические.Поле6 + "","" + АдресаФактические.Поле7 + "","" + АдресаФактические.Поле8 + "","" + АдресаФактические.Поле9 + "","" + ВЫБОР
	|		КОГДА АдресаФактические.ТипДома В (ЗНАЧЕНИЕ(Перечисление.ТипыДомов.Дом), ЗНАЧЕНИЕ(Перечисление.ТипыДомов.ПустаяСсылка))
	|			ТОГДА ""дом""
	|		ИНАЧЕ ""владение""
	|	КОНЕЦ + "","" + ВЫБОР
	|		КОГДА АдресаФактические.ТипКорпуса В (ЗНАЧЕНИЕ(Перечисление.ТипыКорпусов.Корпус), ЗНАЧЕНИЕ(Перечисление.ТипыКорпусов.ПустаяСсылка))
	|			ТОГДА ""корпус""
	|		ИНАЧЕ ""строение""
	|	КОНЕЦ + "","" + ВЫБОР
	|		КОГДА АдресаФактические.ТипКвартиры В (ЗНАЧЕНИЕ(Перечисление.ТипыКвартир.Квартира), ЗНАЧЕНИЕ(Перечисление.ТипыКвартир.ПустаяСсылка))
	|			ТОГДА ""кв.""
	|		ИНАЧЕ ""оф.""
	|	КОНЕЦ КАК АдресФактический,
	|	ВЫБОР
	|		КОГДА ДолжностиРаботниковОрганизации.ПериодЗавершения <= &КонецНП
	|				И ДолжностиРаботниковОрганизации.ПериодЗавершения <> ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|			ТОГДА ДолжностиРаботниковОрганизации.ДолжностьЗавершения.Наименование
	|		ИНАЧЕ ДолжностиРаботниковОрганизации.Должность.Наименование
	|	КОНЕЦ КАК Должность,
	|	ВЫБОР
	|		КОГДА РаботникиОрганизации.Сотрудник ЕСТЬ NULL 
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК ТрудовойДоговор,
	|	ВЫБОР
	|		КОГДА РаботникиОрганизации.Сотрудник ЕСТЬ NULL 
	|				И ДоговорНаВыполнениеРаботСФизЛицом.ВидДоговора = ЗНАЧЕНИЕ(Перечисление.ВидыДоговоровСФизЛицами.Подряда)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ДоговорПодряда,
	|	ВЫБОР
	|		КОГДА РаботникиОрганизации.Сотрудник ЕСТЬ NULL 
	|				И ДоговорНаВыполнениеРаботСФизЛицом.ВидДоговора = ЗНАЧЕНИЕ(Перечисление.ВидыДоговоровСФизЛицами.Авторский)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК АвторскийДоговор,
	|	ВЫБОР
	|		КОГДА РаботникиОрганизации.Сотрудник ЕСТЬ НЕ NULL 
	|			ТОГДА ВЫБОР
	|					КОГДА РаботникиОрганизации.Сотрудник.НомерДоговора = """"
	|						ТОГДА ПриемНаРаботуВОрганизациюРаботникиОрганизации.Ссылка.Номер
	|					ИНАЧЕ РаботникиОрганизации.Сотрудник.НомерДоговора
	|				КОНЕЦ
	|		КОГДА ДоговорНаВыполнениеРаботСФизЛицом.ВидДоговора = ЗНАЧЕНИЕ(Перечисление.ВидыДоговоровСФизЛицами.Подряда)
	|			ТОГДА ДоговорНаВыполнениеРаботСФизЛицом.Номер
	|		КОГДА ДоговорНаВыполнениеРаботСФизЛицом.ВидДоговора = ЗНАЧЕНИЕ(Перечисление.ВидыДоговоровСФизЛицами.Авторский)
	|			ТОГДА ДоговорНаВыполнениеРаботСФизЛицом.Номер
	|		ИНАЧЕ """"
	|	КОНЕЦ КАК НомерДоговора,
	|	ВЫБОР
	|		КОГДА РаботникиОрганизации.Сотрудник ЕСТЬ НЕ NULL 
	|			ТОГДА ВЫБОР
	|					КОГДА ДолжностиРаботниковОрганизации.ПериодЗавершения <= &КонецНП
	|							И ДолжностиРаботниковОрганизации.ПериодЗавершения <> ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|							И ДолжностиРаботниковОрганизации.ПричинаИзмененияСостоянияЗавершения <> ЗНАЧЕНИЕ(Перечисление.ПричиныИзмененияСостояния.Увольнение)
	|						ТОГДА ДолжностиРаботниковОрганизации.ПериодЗавершения
	|					ИНАЧЕ ДолжностиРаботниковОрганизации.Период
	|				КОНЕЦ
	|		КОГДА ДоговорНаВыполнениеРаботСФизЛицом.ВидДоговора = ЗНАЧЕНИЕ(Перечисление.ВидыДоговоровСФизЛицами.Подряда)
	|			ТОГДА ДоговорНаВыполнениеРаботСФизЛицом.Дата
	|		КОГДА ДоговорНаВыполнениеРаботСФизЛицом.ВидДоговора = ЗНАЧЕНИЕ(Перечисление.ВидыДоговоровСФизЛицами.Авторский)
	|			ТОГДА ДоговорНаВыполнениеРаботСФизЛицом.Дата
	|		ИНАЧЕ """"
	|	КОНЕЦ КАК ДатаНазначенияНаДолжность
	|ИЗ
	|	Справочник.ФизическиеЛица КАК ДанныеОФизЛице
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ФИОФизЛиц.СрезПоследних(&КонецНП, ФизЛицо = &ФизЛицо) КАК ФИОФизЛица
	|		ПО (ИСТИНА)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РаботникиОрганизаций.СрезПоследних(
	|				&КонецНП,
	|				Сотрудник.ФизЛицо = &ФизЛицо
	|					И Организация = &ГоловнаяОрганизация
	|					И Сотрудник.ВидЗанятости <> ЗНАЧЕНИЕ(Перечисление.ВидыЗанятостиВОрганизации.ВнутреннееСовместительство)
	|					И ПричинаИзмененияСостояния = ЗНАЧЕНИЕ(Перечисление.ПричиныИзмененияСостояния.ПриемНаРаботу)) КАК РаботникиОрганизации
	|			ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПриемНаРаботуВОрганизацию.РаботникиОрганизации КАК ПриемНаРаботуВОрганизациюРаботникиОрганизации
	|			ПО (ПриемНаРаботуВОрганизациюРаботникиОрганизации.Ссылка = РаботникиОрганизации.Регистратор)
	|				И (ПриемНаРаботуВОрганизациюРаботникиОрганизации.Сотрудник = РаботникиОрганизации.Сотрудник)
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РаботникиОрганизаций.СрезПоследних(
	|					&КонецНП,
	|					Сотрудник.ФизЛицо = &ФизЛицо
	|						И Организация = &ГоловнаяОрганизация
	|						И Сотрудник.ВидЗанятости <> ЗНАЧЕНИЕ(Перечисление.ВидыЗанятостиВОрганизации.ВнутреннееСовместительство)
	|						И ПричинаИзмененияСостояния <> ЗНАЧЕНИЕ(Перечисление.ПричиныИзмененияСостояния.Увольнение)) КАК ДолжностиРаботниковОрганизации
	|			ПО РаботникиОрганизации.Сотрудник = ДолжностиРаботниковОрганизации.Сотрудник
	|		ПО (ИСТИНА)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПаспортныеДанныеФизЛиц.СрезПоследних(&КонецНП, ФизЛицо = &ФизЛицо) КАК ПаспортныеДанныеФизЛица
	|		ПО (ИСТИНА)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ГражданствоФизЛиц.СрезПоследних(&КонецНП, ФизЛицо = &ФизЛицо) КАК ГражданствоФизЛица
	|		ПО (ИСТИНА)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КонтактнаяИнформация КАК АдресаРегистрации
	|		ПО (АдресаРегистрации.Объект = &ФизЛицо)
	|			И (АдресаРегистрации.Вид = &ВидАдресаРегистрации)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КонтактнаяИнформация КАК АдресаФактические
	|		ПО (АдресаФактические.Объект = &ФизЛицо)
	|			И (АдресаФактические.Вид = &ВидАдресаФактический)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ДоговорНаВыполнениеРаботСФизЛицом КАК ДоговорНаВыполнениеРаботСФизЛицом
	|		ПО (ДоговорНаВыполнениеРаботСФизЛицом.Организация В (&СписокСтруктурныхПодразделений))
	|			И (ДоговорНаВыполнениеРаботСФизЛицом.ДатаОкончания <= &КонецНП
	|				ИЛИ ДоговорНаВыполнениеРаботСФизЛицом.ДатаНачала >= &КонецНП)
	|			И ДанныеОФизЛице.Ссылка = ДоговорНаВыполнениеРаботСФизЛицом.Сотрудник.Физлицо
	|			И (ДоговорНаВыполнениеРаботСФизЛицом.Проведен)
	|ГДЕ
	|	ДанныеОФизЛице.Ссылка = &ФизЛицо
	|
	|УПОРЯДОЧИТЬ ПО
	|	РаботникиОрганизации.Период УБЫВ,
	|	ДоговорНаВыполнениеРаботСФизЛицом.ДатаНачала УБЫВ";
	
	// УчетнаяПолитикаНалоговыйУчет
	// Таблица УчетнаяПолитикаНалоговыйУчетУСН - это список периодов, когда организация переходила на УСН
	// поля:
	//		УСН, 
	//		Месяц - месяц налогового периода
	
	Запрос.Текст = ЗаполнениеРегламентированнойОтчетностиПереопределяемый.ТекстЗапросаУСН();
	Запрос.Выполнить();
	
	// ДоходыПоМесяцамКодам
	// Таблица доходов ЕСН по Месяцам налогового периода и кодам дохода
	// Поля:
	//		Месяц
	//		КодДоходаЕСН
	//		Результат
	//		Скидка
	// Описание:
	// 	Выбираем зарегистрированные доходы из ЕСНСведенияОДоходах 	
	//  Запрос выполняется для списка обособленных подразделений.
	
	// ДанныеОПравеНаПенсию
	// Таблица Таблица Данные о праве на пенсию: - таблица это список иностранцев и периодов
	// Поля:
	//		Физлицо, 
	//		Месяц - месяц налогового периода
	// 
	// Описание:
	//	Выбираем Из Списка периодов
	//	Внутреннее соединение с ГражданствоФизЛиц.СрезПоследних
	//  по равенству периодов
	//  условие: что физлицо - не имеет права на пенсию
	//
	
	// ПоказателиДоходовПоМесяцам
	// Описание:
	//  Вычисляет показатели отчета, основанные на сведениях о доходах
					 
	// ПоказателиДоходовНарастающимИтогом
	// Описание:
	//  Вычисляет показатели отчета, основанные на сведениях о доходах - нарастающим итогом
	
	// ПоказателиНалогПоМесяцам
	// Описание:
	//	Вычисляет показатели отчета, основанные на сведениях о налогах.
	//  Из по ЕСН автоматически отнимается налог, приходящийся  на налоговую льготу инвалидов.
	
	//ДанныеРасчетаТекст
	// Описание: объединяет показатели доходов и налогов
	
	ДанныеРасчетаТекст = 
	"ВЫБРАТЬ
	|	ЕСНСведенияОДоходах.ФизЛицо КАК ФизЛицо
	|ПОМЕСТИТЬ ВТФизлицаБезНалоговойБазы
	|ИЗ
	|	РегистрНакопления.ЕСНСведенияОДоходах КАК ЕСНСведенияОДоходах
	|ГДЕ
	|	ЕСНСведенияОДоходах.ФизЛицо = &ФизЛицо
	|	И ЕСНСведенияОДоходах.Период МЕЖДУ &НачалоНП И &КонецНП
	|	И ЕСНСведенияОДоходах.Организация = &ГоловнаяОрганизация
	|	И (НЕ ЕСНСведенияОДоходах.ОблагаетсяЕНВД)
	|
	|СГРУППИРОВАТЬ ПО
	|	ЕСНСведенияОДоходах.ФизЛицо
	|
	|ИМЕЮЩИЕ
	|	СУММА(ВЫБОР
	|			КОГДА ЕСНСведенияОДоходах.КодДоходаЕСН.ВходитВБазуФедеральныйБюджет
	|				ТОГДА ЕСНСведенияОДоходах.Результат - ЕСНСведенияОДоходах.Скидка
	|			ИНАЧЕ 0
	|		КОНЕЦ) < 0
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ФизЛицо
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МЕСЯЦ(ЕСНСведенияОДоходах.Период) КАК Месяц,
	|	ЕСНСведенияОДоходах.КодДоходаЕСН,
	|	ЕСНСведенияОДоходах.ОблагаетсяЕНВД,
	|	СУММА(ЕСНСведенияОДоходах.Результат) КАК Результат,
	|	СУММА(ЕСНСведенияОДоходах.Скидка) КАК Скидка
	|ПОМЕСТИТЬ ВТДоходыПоМесяцамКодам
	|ИЗ
	|	РегистрНакопления.ЕСНСведенияОДоходах КАК ЕСНСведенияОДоходах
	|ГДЕ
	|	ЕСНСведенияОДоходах.ФизЛицо = &ФизЛицо
	|	И ЕСНСведенияОДоходах.Организация = &ГоловнаяОрганизация
	|	И ЕСНСведенияОДоходах.Период МЕЖДУ &НачалоНП И &КонецНП
	|
	|СГРУППИРОВАТЬ ПО
	|	ЕСНСведенияОДоходах.КодДоходаЕСН,
	|	МЕСЯЦ(ЕСНСведенияОДоходах.Период),
	|	ЕСНСведенияОДоходах.ОблагаетсяЕНВД
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Месяц
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МЕСЯЦ(Периоды.Период) КАК Месяц,
	|	ГражданствоФизЛиц.ФизЛицо
	|ПОМЕСТИТЬ ВТДанныеОПравеНаПенсию
	|ИЗ
	|	(ВЫБРАТЬ
	|		Периоды.Период КАК Период,
	|		ГражданствоФизЛиц.ФизЛицо КАК Физлицо,
	|		МАКСИМУМ(ГражданствоФизЛиц.Период) КАК ПериодРегистра
	|	ИЗ
	|		ВТПериоды КАК Периоды
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ГражданствоФизЛиц КАК ГражданствоФизЛиц
	|			ПО Периоды.Период >= ГражданствоФизЛиц.Период
	|				И (ГражданствоФизЛиц.ФизЛицо = &ФизЛицо)
	|	
	|	СГРУППИРОВАТЬ ПО
	|		ГражданствоФизЛиц.ФизЛицо,
	|		Периоды.Период) КАК Периоды
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ГражданствоФизЛиц КАК ГражданствоФизЛиц
	|		ПО Периоды.ПериодРегистра = ГражданствоФизЛиц.Период
	|			И Периоды.Физлицо = ГражданствоФизЛиц.ФизЛицо
	|			И (ГражданствоФизЛиц.НеИмеетПравоНаПенсию)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Месяц
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Доходы.Месяц КАК Месяц,
	|	СУММА(ВЫБОР
	|			КОГДА Иностр.ФизЛицо ЕСТЬ NULL 
	|					И УчетнаяПолитикаНалоговыйУчетУСН.УСН ЕСТЬ NULL 
	|					И (НЕ Доходы.ОблагаетсяЕНВД)
	|					И Доходы.КодДоходаЕСН.ВходитВБазуФедеральныйБюджет
	|					И ФизлицаБезНалоговойБазы.ФизЛицо ЕСТЬ NULL 
	|				ТОГДА Доходы.Результат - Доходы.Скидка
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК БазаПФРОбщ,
	|	СУММА(ВЫБОР
	|			КОГДА Иностр.ФизЛицо ЕСТЬ NULL 
	|					И УчетнаяПолитикаНалоговыйУчетУСН.УСН ЕСТЬ NULL 
	|					И (НЕ Доходы.ОблагаетсяЕНВД)
	|					И (НЕ Доходы.КодДоходаЕСН В (ЗНАЧЕНИЕ(Справочник.ДоходыЕСН.НеЯвляетсяОбъектом), ЗНАЧЕНИЕ(Справочник.ДоходыЕСН.ДенежноеСодержаниеПрокуроров), ЗНАЧЕНИЕ(Справочник.ДоходыЕСН.ДенежноеДовольствиеВоеннослужащих)))
	|				ТОГДА Доходы.Результат
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК НачисленоВсегоОбщ,
	|	СУММА(ВЫБОР
	|			КОГДА Иностр.ФизЛицо ЕСТЬ NULL 
	|					И УчетнаяПолитикаНалоговыйУчетУСН.УСН ЕСТЬ NULL 
	|					И (НЕ Доходы.ОблагаетсяЕНВД)
	|					И Доходы.КодДоходаЕСН = ЗНАЧЕНИЕ(Справочник.ДоходыЕСН.ВыплатыЗаСчетПрибыли)
	|				ТОГДА Доходы.Результат
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ВыплатыЗаСчетПрибылиОбщ,
	|	СУММА(ВЫБОР
	|			КОГДА Иностр.ФизЛицо ЕСТЬ NULL 
	|					И УчетнаяПолитикаНалоговыйУчетУСН.УСН ЕСТЬ NULL 
	|					И (НЕ Доходы.ОблагаетсяЕНВД)
	|				ТОГДА ВЫБОР
	|						КОГДА Доходы.КодДоходаЕСН В (ЗНАЧЕНИЕ(Справочник.ДоходыЕСН.ПособияЗаСчетФСС), ЗНАЧЕНИЕ(Справочник.ДоходыЕСН.НеОблагаетсяЦеликом))
	|							ТОГДА Доходы.Результат
	|						ИНАЧЕ Доходы.Скидка
	|					КОНЕЦ
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК НеОблагаетсяПоСт238Общ,
	|	СУММА(ВЫБОР
	|			КОГДА Иностр.ФизЛицо ЕСТЬ NULL 
	|					И (УчетнаяПолитикаНалоговыйУчетУСН.УСН ЕСТЬ НЕ NULL 
	|						ИЛИ Доходы.ОблагаетсяЕНВД)
	|					И ФизлицаБезНалоговойБазы.ФизЛицо ЕСТЬ NULL 
	|					И Доходы.КодДоходаЕСН.ВходитВБазуФедеральныйБюджет
	|				ТОГДА Доходы.Результат - Доходы.Скидка
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК БазаПФРСР,
	|	СУММА(ВЫБОР
	|			КОГДА Иностр.ФизЛицо ЕСТЬ NULL 
	|					И (УчетнаяПолитикаНалоговыйУчетУСН.УСН ЕСТЬ НЕ NULL 
	|						ИЛИ Доходы.ОблагаетсяЕНВД)
	|					И (НЕ Доходы.КодДоходаЕСН В (ЗНАЧЕНИЕ(Справочник.ДоходыЕСН.НеЯвляетсяОбъектом), ЗНАЧЕНИЕ(Справочник.ДоходыЕСН.ДенежноеСодержаниеПрокуроров), ЗНАЧЕНИЕ(Справочник.ДоходыЕСН.ДенежноеДовольствиеВоеннослужащих)))
	|				ТОГДА Доходы.Результат
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК НачисленоВсегоСР,
	|	СУММА(ВЫБОР
	|			КОГДА Иностр.ФизЛицо ЕСТЬ NULL 
	|					И (УчетнаяПолитикаНалоговыйУчетУСН.УСН ЕСТЬ НЕ NULL 
	|						ИЛИ Доходы.ОблагаетсяЕНВД)
	|					И Доходы.КодДоходаЕСН = ЗНАЧЕНИЕ(Справочник.ДоходыЕСН.ВыплатыЗаСчетПрибыли)
	|				ТОГДА Доходы.Результат
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ВыплатыЗаСчетПрибылиСР,
	|	СУММА(ВЫБОР
	|			КОГДА Иностр.ФизЛицо ЕСТЬ NULL 
	|					И (УчетнаяПолитикаНалоговыйУчетУСН.УСН ЕСТЬ НЕ NULL 
	|						ИЛИ Доходы.ОблагаетсяЕНВД)
	|				ТОГДА ВЫБОР
	|						КОГДА Доходы.КодДоходаЕСН В (ЗНАЧЕНИЕ(Справочник.ДоходыЕСН.ПособияЗаСчетФСС), ЗНАЧЕНИЕ(Справочник.ДоходыЕСН.НеОблагаетсяЦеликом))
	|							ТОГДА Доходы.Результат
	|						ИНАЧЕ Доходы.Скидка
	|					КОНЕЦ
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК НеОблагаетсяПоСт238СР,
	|	СУММА(ВЫБОР
	|			КОГДА Иностр.ФизЛицо ЕСТЬ NULL 
	|					И Доходы.КодДоходаЕСН.ВходитВБазуФедеральныйБюджет
	|					И ФизлицаБезНалоговойБазы.ФизЛицо ЕСТЬ NULL 
	|				ТОГДА Доходы.Результат - Доходы.Скидка
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК БазаПФРПоВсем,
	|	СУММА(ВЫБОР
	|			КОГДА Иностр.ФизЛицо ЕСТЬ NULL 
	|					И (НЕ Доходы.КодДоходаЕСН В (ЗНАЧЕНИЕ(Справочник.ДоходыЕСН.НеЯвляетсяОбъектом), ЗНАЧЕНИЕ(Справочник.ДоходыЕСН.ДенежноеСодержаниеПрокуроров), ЗНАЧЕНИЕ(Справочник.ДоходыЕСН.ДенежноеДовольствиеВоеннослужащих)))
	|				ТОГДА Доходы.Результат
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК НачисленоВсегоПоВсем,
	|	СУММА(ВЫБОР
	|			КОГДА Иностр.ФизЛицо ЕСТЬ NULL 
	|					И Доходы.КодДоходаЕСН = ЗНАЧЕНИЕ(Справочник.ДоходыЕСН.ВыплатыЗаСчетПрибыли)
	|				ТОГДА Доходы.Результат
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ВыплатыЗаСчетПрибылиПоВсем,
	|	СУММА(ВЫБОР
	|			КОГДА Иностр.ФизЛицо ЕСТЬ NULL 
	|					И Доходы.КодДоходаЕСН В (ЗНАЧЕНИЕ(Справочник.ДоходыЕСН.ПособияЗаСчетФСС), ЗНАЧЕНИЕ(Справочник.ДоходыЕСН.НеОблагаетсяЦеликом))
	|				ТОГДА Доходы.Результат
	|			ИНАЧЕ Доходы.Скидка
	|		КОНЕЦ) КАК НеОблагаетсяПоСт238ПоВсем
	|ПОМЕСТИТЬ ВТПоказателиДоходовПоМесяцам
	|ИЗ
	|	ВТДоходыПоМесяцамКодам КАК Доходы
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТФизлицаБезНалоговойБазы КАК ФизлицаБезНалоговойБазы
	|		ПО (ИСТИНА)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТДанныеОПравеНаПенсию КАК Иностр
	|		ПО Доходы.Месяц = Иностр.Месяц
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТУчетнаяПолитикаНалоговыйУчет КАК УчетнаяПолитикаНалоговыйУчетУСН
	|		ПО Доходы.Месяц = УчетнаяПолитикаНалоговыйУчетУСН.Месяц
	|
	|СГРУППИРОВАТЬ ПО
	|	Доходы.Месяц
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Месяц
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МесяцыНП.Месяц КАК Месяц,
	|	СУММА(ПоказателиДоходов.БазаПФРОбщ) КАК БазаПФРОбщ,
	|	СУММА(ПоказателиДоходов.НачисленоВсегоОбщ) КАК НачисленоВсегоОбщ,
	|	СУММА(ПоказателиДоходов.ВыплатыЗаСчетПрибылиОбщ) КАК ВыплатыЗаСчетПрибылиОбщ,
	|	СУММА(ПоказателиДоходов.НеОблагаетсяПоСт238Общ) КАК НеОблагаетсяПоСт238Общ,
	|	СУММА(ПоказателиДоходов.БазаПФРСР) КАК БазаПФРСР,
	|	СУММА(ПоказателиДоходов.НачисленоВсегоСР) КАК НачисленоВсегоСР,
	|	СУММА(ПоказателиДоходов.ВыплатыЗаСчетПрибылиСР) КАК ВыплатыЗаСчетПрибылиСР,
	|	СУММА(ПоказателиДоходов.НеОблагаетсяПоСт238СР) КАК НеОблагаетсяПоСт238СР,
	|	СУММА(ПоказателиДоходов.БазаПФРПоВсем) КАК БазаПФРПоВсем,
	|	СУММА(ПоказателиДоходов.НачисленоВсегоПоВсем) КАК НачисленоВсегоПоВсем,
	|	СУММА(ПоказателиДоходов.ВыплатыЗаСчетПрибылиПоВсем) КАК ВыплатыЗаСчетПрибылиПоВсем,
	|	СУММА(ПоказателиДоходов.НеОблагаетсяПоСт238ПоВсем) КАК НеОблагаетсяПоСт238ПоВсем
	|ПОМЕСТИТЬ ВТПоказателиДоходовНарастающимИтогом
	|ИЗ
	|	ВТМесяцыНП КАК МесяцыНП
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТПоказателиДоходовПоМесяцам КАК ПоказателиДоходов
	|		ПО МесяцыНП.Месяц >= ПоказателиДоходов.Месяц
	|
	|СГРУППИРОВАТЬ ПО
	|	МесяцыНП.Месяц
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Месяц
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МЕСЯЦ(ЕСНИсчисленный.Период) КАК Месяц,
	|	СУММА(ВЫБОР
	|			КОГДА УчетнаяПолитикаНалоговыйУчетУСН.УСН ЕСТЬ НЕ NULL 
	|				ТОГДА ЕСНИсчисленный.ПФРНакопительная
	|			ИНАЧЕ ЕСНИсчисленный.ПФРНакопительнаяЕНВД
	|		КОНЕЦ) КАК НачисленоПФРНакопительнаяСР,
	|	СУММА(ВЫБОР
	|			КОГДА УчетнаяПолитикаНалоговыйУчетУСН.УСН ЕСТЬ НЕ NULL 
	|				ТОГДА ЕСНИсчисленный.ПФРСтраховая
	|			ИНАЧЕ ЕСНИсчисленный.ПФРСтраховаяЕНВД
	|		КОНЕЦ) КАК НачисленоПФРСтраховаяСР,
	|	СУММА(ВЫБОР
	|			КОГДА УчетнаяПолитикаНалоговыйУчетУСН.УСН ЕСТЬ НЕ NULL 
	|				ТОГДА ЕСНИсчисленный.ПФРСтраховая + ЕСНИсчисленный.ПФРНакопительная
	|			ИНАЧЕ ЕСНИсчисленный.ПФРСтраховаяЕНВД + ЕСНИсчисленный.ПФРНакопительнаяЕНВД
	|		КОНЕЦ) КАК НачисленоПФРСР,
	|	СУММА(ВЫБОР
	|			КОГДА УчетнаяПолитикаНалоговыйУчетУСН.УСН ЕСТЬ NULL 
	|				ТОГДА ЕСНИсчисленный.ПФРНакопительная - ЕСНИсчисленный.ПФРНакопительнаяЕНВД
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК НачисленоПФРНакопительнаяОбщ,
	|	СУММА(ВЫБОР
	|			КОГДА УчетнаяПолитикаНалоговыйУчетУСН.УСН ЕСТЬ NULL 
	|				ТОГДА ЕСНИсчисленный.ПФРСтраховая - ЕСНИсчисленный.ПФРСтраховаяЕНВД
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК НачисленоПФРСтраховаяОбщ,
	|	СУММА(ВЫБОР
	|			КОГДА УчетнаяПолитикаНалоговыйУчетУСН.УСН ЕСТЬ NULL 
	|				ТОГДА ЕСНИсчисленный.ПФРСтраховая - ЕСНИсчисленный.ПФРСтраховаяЕНВД + ЕСНИсчисленный.ПФРНакопительная - ЕСНИсчисленный.ПФРНакопительнаяЕНВД
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК НачисленоПФРОбщ,
	|	СУММА(ЕСНИсчисленный.ПФРНакопительная) КАК НачисленоПФРНакопительнаяПоВсем,
	|	СУММА(ЕСНИсчисленный.ПФРСтраховая) КАК НачисленоПФРСтраховаяПоВсем,
	|	СУММА(ЕСНИсчисленный.ПФРСтраховая + ЕСНИсчисленный.ПФРНакопительная) КАК НачисленоПФРПоВсем
	|ПОМЕСТИТЬ ВТПоказателиНалоговПоМесяцам
	|ИЗ
	|	РегистрНакопления.ЕСНИсчисленный КАК ЕСНИсчисленный
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТУчетнаяПолитикаНалоговыйУчет КАК УчетнаяПолитикаНалоговыйУчетУСН
	|		ПО (МЕСЯЦ(ЕСНИсчисленный.Период) = УчетнаяПолитикаНалоговыйУчетУСН.Месяц)
	|ГДЕ
	|	ЕСНИсчисленный.ФизЛицо = &ФизЛицо
	|	И ЕСНИсчисленный.Организация = &ГоловнаяОрганизация
	|	И ЕСНИсчисленный.Период МЕЖДУ &НачалоНП И &КонецНП
	|
	|СГРУППИРОВАТЬ ПО
	|	МЕСЯЦ(ЕСНИсчисленный.Период)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Месяц
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МесяцыНП.Месяц КАК МЕСЯЦ,
	|	ВЫРАЗИТЬ(СУММА(ПоказателиНалогов.НачисленоПФРНакопительнаяСР) КАК ЧИСЛО(15, 1)) КАК НачисленоПФРНакопительнаяСР,
	|	ВЫРАЗИТЬ(СУММА(ПоказателиНалогов.НачисленоПФРСтраховаяСР) КАК ЧИСЛО(15, 1)) КАК НачисленоПФРСтраховаяСР,
	|	ВЫРАЗИТЬ(СУММА(ПоказателиНалогов.НачисленоПФРСР) КАК ЧИСЛО(15, 1)) КАК НачисленоПФРСР,
	|	ВЫРАЗИТЬ(СУММА(ПоказателиНалогов.НачисленоПФРНакопительнаяОбщ) КАК ЧИСЛО(15, 1)) КАК НачисленоПФРНакопительнаяОбщ,
	|	ВЫРАЗИТЬ(СУММА(ПоказателиНалогов.НачисленоПФРСтраховаяОбщ) КАК ЧИСЛО(15, 1)) КАК НачисленоПФРСтраховаяОбщ,
	|	ВЫРАЗИТЬ(СУММА(ПоказателиНалогов.НачисленоПФРОбщ) КАК ЧИСЛО(15, 1)) КАК НачисленоПФРОбщ,
	|	ВЫРАЗИТЬ(СУММА(ПоказателиНалогов.НачисленоПФРНакопительнаяПоВсем) КАК ЧИСЛО(15, 1)) КАК НачисленоПФРНакопительнаяПоВсем,
	|	ВЫРАЗИТЬ(СУММА(ПоказателиНалогов.НачисленоПФРСтраховаяПоВсем) КАК ЧИСЛО(15, 1)) КАК НачисленоПФРСтраховаяПоВсем,
	|	ВЫРАЗИТЬ(СУММА(ПоказателиНалогов.НачисленоПФРПоВсем) КАК ЧИСЛО(15, 1)) КАК НачисленоПФРПоВсем
	|ПОМЕСТИТЬ ВТПоказателиНалоговНарастающимИтогом
	|ИЗ
	|	ВТМесяцыНП КАК МесяцыНП
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТПоказателиНалоговПоМесяцам КАК ПоказателиНалогов
	|		ПО МесяцыНП.Месяц >= ПоказателиНалогов.Месяц
	|
	|СГРУППИРОВАТЬ ПО
	|	МесяцыНП.Месяц
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	МЕСЯЦ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МесяцыНП.Месяц КАК МЕСЯЦ,
	|	ЕСТЬNULL(ПоказателиДоходов.БазаПФРОбщ, 0) КАК БазаПФРОбщ,
	|	ЕСТЬNULL(ПоказателиДоходовНарастающимИтогом.БазаПФРОбщ, 0) КАК НарастБазаПФРОбщ,
	|	ЕСТЬNULL(ПоказателиДоходов.НачисленоВсегоОбщ, 0) КАК НачисленоВсегоОбщ,
	|	ЕСТЬNULL(ПоказателиДоходовНарастающимИтогом.НачисленоВсегоОбщ, 0) КАК НарастНачисленоВсегоОбщ,
	|	ЕСТЬNULL(ПоказателиДоходов.ВыплатыЗаСчетПрибылиОбщ, 0) КАК ВыплатыЗаСчетПрибылиОбщ,
	|	ЕСТЬNULL(ПоказателиДоходовНарастающимИтогом.ВыплатыЗаСчетПрибылиОбщ, 0) КАК НарастВыплатыЗаСчетПрибылиОбщ,
	|	ЕСТЬNULL(ПоказателиДоходов.НеОблагаетсяПоСт238Общ, 0) КАК НеОблагаетсяПоСт238Общ,
	|	ЕСТЬNULL(ПоказателиДоходовНарастающимИтогом.НеОблагаетсяПоСт238Общ, 0) КАК НарастНеОблагаетсяПоСт238Общ,
	|	ЕСТЬNULL(ПоказателиДоходов.БазаПФРСР, 0) КАК БазаПФРСР,
	|	ЕСТЬNULL(ПоказателиДоходовНарастающимИтогом.БазаПФРСР, 0) КАК НарастБазаПФРСР,
	|	ЕСТЬNULL(ПоказателиДоходов.НачисленоВсегоСР, 0) КАК НачисленоВсегоСР,
	|	ЕСТЬNULL(ПоказателиДоходовНарастающимИтогом.НачисленоВсегоСР, 0) КАК НарастНачисленоВсегоСР,
	|	ЕСТЬNULL(ПоказателиДоходов.ВыплатыЗаСчетПрибылиСР, 0) КАК ВыплатыЗаСчетПрибылиСР,
	|	ЕСТЬNULL(ПоказателиДоходовНарастающимИтогом.ВыплатыЗаСчетПрибылиСР, 0) КАК НарастВыплатыЗаСчетПрибылиСР,
	|	ЕСТЬNULL(ПоказателиДоходов.НеОблагаетсяПоСт238СР, 0) КАК НеОблагаетсяПоСт238СР,
	|	ЕСТЬNULL(ПоказателиДоходовНарастающимИтогом.НеОблагаетсяПоСт238СР, 0) КАК НарастНеОблагаетсяПоСт238СР,
	|	ЕСТЬNULL(ПоказателиДоходов.БазаПФРПоВсем, 0) КАК БазаПФРПоВсем,
	|	ЕСТЬNULL(ПоказателиДоходовНарастающимИтогом.БазаПФРПоВсем, 0) КАК НарастБазаПФРПоВсем,
	|	ЕСТЬNULL(ПоказателиДоходов.НачисленоВсегоПоВсем, 0) КАК НачисленоВсегоПоВсем,
	|	ЕСТЬNULL(ПоказателиДоходовНарастающимИтогом.НачисленоВсегоПоВсем, 0) КАК НарастНачисленоВсегоПоВсем,
	|	ЕСТЬNULL(ПоказателиДоходов.ВыплатыЗаСчетПрибылиПоВсем, 0) КАК ВыплатыЗаСчетПрибылиПоВсем,
	|	ЕСТЬNULL(ПоказателиДоходовНарастающимИтогом.ВыплатыЗаСчетПрибылиПоВсем, 0) КАК НарастВыплатыЗаСчетПрибылиПоВсем,
	|	ЕСТЬNULL(ПоказателиДоходов.НеОблагаетсяПоСт238ПоВсем, 0) КАК НеОблагаетсяПоСт238ПоВсем,
	|	ЕСТЬNULL(ПоказателиДоходовНарастающимИтогом.НеОблагаетсяПоСт238ПоВсем, 0) КАК НарастНеОблагаетсяПоСт238ПоВсем,
	|	ЕСТЬNULL(ПоказателиНалоговНарастающимИтогом.НачисленоПФРНакопительнаяСР, 0) - ЕСТЬNULL(ПоказателиНалоговПоПрошлыйМесяц.НачисленоПФРНакопительнаяСР, 0) КАК НачисленоПФРНакопительнаяСР,
	|	ЕСТЬNULL(ПоказателиНалоговНарастающимИтогом.НачисленоПФРНакопительнаяСР, 0) КАК НарастНачисленоПФРНакопительнаяСР,
	|	ЕСТЬNULL(ПоказателиНалоговНарастающимИтогом.НачисленоПФРСтраховаяСР, 0) - ЕСТЬNULL(ПоказателиНалоговПоПрошлыйМесяц.НачисленоПФРСтраховаяСР, 0) КАК НачисленоПФРСтраховаяСР,
	|	ЕСТЬNULL(ПоказателиНалоговНарастающимИтогом.НачисленоПФРСтраховаяСР, 0) КАК НарастНачисленоПФРСтраховаяСР,
	|	ЕСТЬNULL(ПоказателиНалоговНарастающимИтогом.НачисленоПФРСР, 0) - ЕСТЬNULL(ПоказателиНалоговПоПрошлыйМесяц.НачисленоПФРСР, 0) КАК НачисленоПФРСР,
	|	ЕСТЬNULL(ПоказателиНалоговНарастающимИтогом.НачисленоПФРСР, 0) КАК НарастНачисленоПФРСР,
	|	ЕСТЬNULL(ПоказателиНалоговНарастающимИтогом.НачисленоПФРНакопительнаяОбщ, 0) - ЕСТЬNULL(ПоказателиНалоговПоПрошлыйМесяц.НачисленоПФРНакопительнаяОбщ, 0) КАК НачисленоПФРНакопительнаяОбщ,
	|	ЕСТЬNULL(ПоказателиНалоговНарастающимИтогом.НачисленоПФРНакопительнаяОбщ, 0) КАК НарастНачисленоПФРНакопительнаяОбщ,
	|	ЕСТЬNULL(ПоказателиНалоговНарастающимИтогом.НачисленоПФРСтраховаяОбщ, 0) - ЕСТЬNULL(ПоказателиНалоговПоПрошлыйМесяц.НачисленоПФРСтраховаяОбщ, 0) КАК НачисленоПФРСтраховаяОбщ,
	|	ЕСТЬNULL(ПоказателиНалоговНарастающимИтогом.НачисленоПФРСтраховаяОбщ, 0) КАК НарастНачисленоПФРСтраховаяОбщ,
	|	ЕСТЬNULL(ПоказателиНалоговНарастающимИтогом.НачисленоПФРОбщ, 0) - ЕСТЬNULL(ПоказателиНалоговПоПрошлыйМесяц.НачисленоПФРОбщ, 0) КАК НачисленоПФРОбщ,
	|	ЕСТЬNULL(ПоказателиНалоговНарастающимИтогом.НачисленоПФРОбщ, 0) КАК НарастНачисленоПФРОбщ,
	|	ЕСТЬNULL(ПоказателиНалоговНарастающимИтогом.НачисленоПФРНакопительнаяПоВсем, 0) - ЕСТЬNULL(ПоказателиНалоговПоПрошлыйМесяц.НачисленоПФРНакопительнаяПоВсем, 0) КАК НачисленоПФРНакопительнаяПоВсем,
	|	ЕСТЬNULL(ПоказателиНалоговНарастающимИтогом.НачисленоПФРНакопительнаяПоВсем, 0) КАК НарастНачисленоПФРНакопительнаяПоВсем,
	|	ЕСТЬNULL(ПоказателиНалоговНарастающимИтогом.НачисленоПФРСтраховаяПоВсем, 0) - ЕСТЬNULL(ПоказателиНалоговПоПрошлыйМесяц.НачисленоПФРСтраховаяПоВсем, 0) КАК НачисленоПФРСтраховаяПоВсем,
	|	ЕСТЬNULL(ПоказателиНалоговНарастающимИтогом.НачисленоПФРСтраховаяПоВсем, 0) КАК НарастНачисленоПФРСтраховаяПоВсем,
	|	ЕСТЬNULL(ПоказателиНалоговНарастающимИтогом.НачисленоПФРПоВсем, 0) - ЕСТЬNULL(ПоказателиНалоговПоПрошлыйМесяц.НачисленоПФРПоВсем, 0) КАК НачисленоПФРПоВсем,
	|	ЕСТЬNULL(ПоказателиНалоговНарастающимИтогом.НачисленоПФРПоВсем, 0) КАК НарастНачисленоПФРПоВсем,
	|	ВЫБОР
	|		КОГДА ГОД(ФизическиеЛица.ДатаРождения) < 1967
	|			ТОГДА Ставки.ПФРНакопительная1вРублях
	|		ИНАЧЕ Ставки.ПФРНакопительная2вРублях
	|	КОНЕЦ КАК ПФРНакопительнаяВРублях,
	|	ВЫБОР
	|		КОГДА ГОД(ФизическиеЛица.ДатаРождения) < 1967
	|			ТОГДА Ставки.ПФРСтраховая1вРублях
	|		ИНАЧЕ Ставки.ПФРСтраховая2вРублях
	|	КОНЕЦ КАК ПФРСтраховаяВРублях,
	|	ВЫБОР
	|		КОГДА ГОД(ФизическиеЛица.ДатаРождения) < 1967
	|			ТОГДА Ставки.ПФРНакопительная1вПроцентах
	|		ИНАЧЕ Ставки.ПФРНакопительная2вПроцентах
	|	КОНЕЦ КАК ПФРНакопительнаяВПроцентах,
	|	ВЫБОР
	|		КОГДА ГОД(ФизическиеЛица.ДатаРождения) < 1967
	|			ТОГДА Ставки.ПФРСтраховая1вПроцентах
	|		ИНАЧЕ Ставки.ПФРСтраховая2вПроцентах
	|	КОНЕЦ КАК ПФРСтраховаяВПроцентах
	|ИЗ
	|	ВТМесяцыНП КАК МесяцыНП
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТПоказателиДоходовПоМесяцам КАК ПоказателиДоходов
	|		ПО МесяцыНП.Месяц = ПоказателиДоходов.Месяц
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТПоказателиНалоговПоМесяцам КАК ПоказателиНалогов
	|		ПО МесяцыНП.Месяц = ПоказателиНалогов.Месяц
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТПоказателиДоходовНарастающимИтогом КАК ПоказателиДоходовНарастающимИтогом
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СведенияОСтавкахЕСНиПФР КАК Ставки
	|			ПО ПоказателиДоходовНарастающимИтогом.БазаПФРОбщ > Ставки.СуммаДоходаС
	|				И ПоказателиДоходовНарастающимИтогом.БазаПФРОбщ <= Ставки.СуммаДоходаПо
	|				И (Ставки.ВидСтавокЕСНиПФР = &ВидСтавокЕСНиПФР)
	|				И (Ставки.Год = &ГодНП)
	|			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ФизическиеЛица КАК ФизическиеЛица
	|			ПО (ФизическиеЛица.Ссылка = &ФизЛицо)
	|		ПО МесяцыНП.Месяц = ПоказателиДоходовНарастающимИтогом.Месяц
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТПоказателиНалоговНарастающимИтогом КАК ПоказателиНалоговНарастающимИтогом
	|		ПО МесяцыНП.Месяц = ПоказателиНалоговНарастающимИтогом.МЕСЯЦ
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТПоказателиНалоговНарастающимИтогом КАК ПоказателиНалоговПоПрошлыйМесяц
	|		ПО (МесяцыНП.Месяц - 1 = ПоказателиНалоговПоПрошлыйМесяц.МЕСЯЦ)
	|
	|УПОРЯДОЧИТЬ ПО
	|	МЕСЯЦ";							   
	
	Если УчетнаяПолитика.ТочностьИсчисленияПФР = Перечисления.ПорядкиОкругленияЕСН.ВРубляхИКопейках Тогда
		ДанныеРасчетаТекст = СтрЗаменить(ДанныеРасчетаТекст,"ЧИСЛО(15, 1)","ЧИСЛО(15, 2)");
	ИначеЕсли УчетнаяПолитика.ТочностьИсчисленияПФР = Перечисления.ПорядкиОкругленияЕСН.ВРублях Тогда
		ДанныеРасчетаТекст = СтрЗаменить(ДанныеРасчетаТекст,"ЧИСЛО(15, 1)","ЧИСЛО(15, 0)");
	Иначе 	
		ДанныеРасчетаТекст = СтрЗаменить(ДанныеРасчетаТекст,"ЧИСЛО(15, 1)","ЧИСЛО(15, 5)");
	КонецЕсли;
	
	//-----------------------------------------------------------------------------
	// ВЫПОЛНЕНИЕ ЗАПРОСОВ
	
	// Сведения о физлице
	Запрос.Текст = ДанныеОФизлицеТекст;
	ДанныеОФизЛице  = Запрос.Выполнить().Выбрать();
	ДанныеОФизЛице.Следующий();
	
	// Данные расчета
	Запрос.Текст = ДанныеРасчетаТекст;
	ДанныеРасчета  = Запрос.Выполнить().Выбрать();
	
	//-----------------------------------------------------------------------------
	// ЗАПОЛНЕНИЕ ФОРМЫ
	
	// Области макета
    ОбластьШапка = Макет.ПолучитьОбласть("Шапка");
	ОбластьПустойМесяц = Макет.ПолучитьОбласть("ПустойМесяц");
	
	// Вывод шапки отчета
	ОбластьШапка.Параметры.Заполнить(ДанныеОФизЛице);
	ОбластьШапка.Параметры.ДокументКемКогдаВыдан = СОКРЛП(ДанныеОФизЛице.ДокументКемКогдаВыдан);	
	ОбластьШапка.Параметры.НалоговыйПериод = Формат(НалоговыйПериод,"ЧГ=");
	ОбластьШапка.Параметры.ДатаРождения = Формат(ДанныеОФизЛице.ДатаРождения,"ЧГ=; ДФ=dd.MM.yyyy");
	ОбластьШапка.Параметры.ДокументДатаВыдачи = Формат(ДанныеОФизЛице.ДокументДатаВыдачи,"ДФ=dd.MM.yyyy");
	Если СтрЗаменить(ДанныеОФизЛице.АдресФактический, ",","") = "" Тогда
		ОбластьШапка.Параметры.Адрес = УправлениеКонтактнойИнформацией.ПолучитьПредставлениеАдресаПоСтрока(ДанныеОФизЛице.АдресРегистрации);
	Иначе	
		ОбластьШапка.Параметры.Адрес = УправлениеКонтактнойИнформацией.ПолучитьПредставлениеАдресаПоСтрока(ДанныеОФизЛице.АдресФактический);	
	КонецЕсли; 
	Если ДанныеОФизЛице.ТрудовойДоговор Тогда
		ОбластьТекст = ОбластьШапка.Область("R8C6");
		ОбластьТекст.Шрифт = Новый Шрифт(ОбластьТекст.Шрифт,,,,,Истина);
	КонецЕсли;
	Если ДанныеОФизЛице.ДоговорПодряда Тогда
		ОбластьТекст = ОбластьШапка.Область("R8C7");
		ОбластьТекст.Шрифт = Новый Шрифт(ОбластьТекст.Шрифт,,,,,Истина);
	КонецЕсли;
	Если ДанныеОФизЛице.АвторскийДоговор Тогда
		ОбластьТекст = ОбластьШапка.Область("R8C9");
		ОбластьТекст.Шрифт = Новый Шрифт(ОбластьТекст.Шрифт,,,,,Истина);
	КонецЕсли;
	ДокументРезультат.Вывести(ОбластьШапка);
	
	// Вывод сведений о доходах и налогах по месяцам налогового периода
	
	ДокументРезультат.Вывести(Макет.ПолучитьОбласть("Заголовок1"));
	ОбластьМесяц = Макет.ПолучитьОбласть("МесяцОбщ");
	// настроим формат вывода данных о налогах
	ОбластиСтроки = ОбластьМесяц.Области;
	Для НомерКолонки = 8 По 10 Цикл
		ОбластиСтроки["П" + НомерКолонки + "Общ"].Формат = ФорматВыводаДанных;
		ОбластиСтроки["СНГ" + НомерКолонки + "Общ"].Формат = ФорматВыводаДанных;
	КонецЦикла;
	
	Месяц = 0;
	НачисленоВсегоОбщ = 0;
	Пока ДанныеРасчета.Следующий() Цикл
		
		Месяц = ДанныеРасчета.Месяц;
		
		ОбластьМесяц.Параметры.Заполнить(ДанныеРасчета);
		ОбластьМесяц.Параметры.Месяц = Формат(Дата(НалоговыйПериод,Месяц,1),"ДФ=ММММ");
		ОбластьМесяц.Параметры.СтавкаПФРНакопительная = ФормированиеПечатныхФорм.ПолучитьОписаниеРегрессивнойСтавки(ДанныеРасчета.ПФРНакопительнаяВРублях,ДанныеРасчета.ПФРНакопительнаяВПроцентах);
		ОбластьМесяц.Параметры.СтавкаПФРСтраховая = ФормированиеПечатныхФорм.ПолучитьОписаниеРегрессивнойСтавки(ДанныеРасчета.ПФРСтраховаяВРублях,ДанныеРасчета.ПФРСтраховаяВПроцентах);
	
		НачисленоВсегоОбщ = НачисленоВсегоОбщ + ДанныеРасчета.НачисленоВсегоОбщ;
		
		// Выведем месяц
		ДокументРезультат.Вывести(ОбластьМесяц);
		
	КонецЦикла; 
	
	Если Месяц < 12 Тогда  // дополним таблицу пустыми строками
		Для СчМесяцев = Месяц + 1 По 12 Цикл
			// Выведем пустой месяц
			ОбластьПустойМесяц.Параметры.Месяц = Формат(Дата(НалоговыйПериод,СчМесяцев,1),"ДФ=ММММ");
			ДокументРезультат.Вывести(ОбластьПустойМесяц);
		КонецЦикла;		
	КонецЕсли;
	
	ДокументРезультат.Вывести(Макет.ПолучитьОбласть("Заголовок2"));
	ОбластьМесяц = Макет.ПолучитьОбласть("МесяцСР");
	// настроим формат вывода данных о налогах
	ОбластиСтроки = ОбластьМесяц.Области;
	Для НомерКолонки = 8 По 10 Цикл
		ОбластиСтроки["П" + НомерКолонки + "СР"].Формат = ФорматВыводаДанных;
		ОбластиСтроки["СНГ" + НомерКолонки + "СР"].Формат = ФорматВыводаДанных;
	КонецЦикла;
	
	Месяц = 0;
	НачисленоВсегоСР = 0;
	ДанныеРасчета.Сбросить();
	Пока ДанныеРасчета.Следующий() Цикл
		
		Месяц = ДанныеРасчета.Месяц;
		
		ОбластьМесяц.Параметры.Заполнить(ДанныеРасчета);
		ОбластьМесяц.Параметры.Месяц = Формат(Дата(НалоговыйПериод,Месяц,1),"ДФ=ММММ");
		Если ДанныеРасчета.НарастНачисленоВсегоСР <> 0 Тогда
			ОбластьМесяц.Параметры.СтавкаПФРНакопительная = ФормированиеПечатныхФорм.ПолучитьОписаниеРегрессивнойСтавки(ДанныеРасчета.ПФРНакопительнаяВРублях,ДанныеРасчета.ПФРНакопительнаяВПроцентах);
			ОбластьМесяц.Параметры.СтавкаПФРСтраховая = ФормированиеПечатныхФорм.ПолучитьОписаниеРегрессивнойСтавки(ДанныеРасчета.ПФРСтраховаяВРублях,ДанныеРасчета.ПФРСтраховаяВПроцентах);
		КонецЕсли;
		
		НачисленоВсегоСР = НачисленоВсегоСР + ДанныеРасчета.НачисленоВсегоСР;
		
		// Выведем месяц
		ДокументРезультат.Вывести(ОбластьМесяц);
		
	КонецЦикла; 
	
	Если Месяц < 12 Тогда  // дополним таблицу пустыми строками
		Для СчМесяцев = Месяц + 1 По 12 Цикл
			// Выведем пустой месяц
			ОбластьПустойМесяц.Параметры.Месяц = Формат(Дата(НалоговыйПериод,СчМесяцев,1),"ДФ=ММММ");
			ДокументРезультат.Вывести(ОбластьПустойМесяц);
		КонецЦикла;		
	КонецЕсли;
	
	Месяц = 0;
	ДокументРезультат.Вывести(Макет.ПолучитьОбласть("Заголовок3"));
	Если НачисленоВсегоОбщ <> 0 И НачисленоВсегоСР <> 0 Тогда
		
		ОбластьМесяц = Макет.ПолучитьОбласть("МесяцПоВсем");
		// настроим формат вывода данных о налогах
		ОбластиСтроки = ОбластьМесяц.Области;
		Для НомерКолонки = 8 По 10 Цикл
			ОбластиСтроки["П" + НомерКолонки].Формат = ФорматВыводаДанных;
			ОбластиСтроки["СНГ" + НомерКолонки].Формат = ФорматВыводаДанных;
		КонецЦикла;
		
		ДанныеРасчета.Сбросить();
		Пока ДанныеРасчета.Следующий() Цикл
			
			Месяц = ДанныеРасчета.Месяц;
			
			ОбластьМесяц.Параметры.Заполнить(ДанныеРасчета);
			ОбластьМесяц.Параметры.Месяц = Формат(Дата(НалоговыйПериод,Месяц,1),"ДФ=ММММ");
			ОбластьМесяц.Параметры.СтавкаПФРНакопительная = ФормированиеПечатныхФорм.ПолучитьОписаниеРегрессивнойСтавки(ДанныеРасчета.ПФРНакопительнаяВРублях,ДанныеРасчета.ПФРНакопительнаяВПроцентах);
			ОбластьМесяц.Параметры.СтавкаПФРСтраховая = ФормированиеПечатныхФорм.ПолучитьОписаниеРегрессивнойСтавки(ДанныеРасчета.ПФРСтраховаяВРублях,ДанныеРасчета.ПФРСтраховаяВПроцентах);
			
			// Выведем месяц
			ДокументРезультат.Вывести(ОбластьМесяц);
			
		КонецЦикла; 
		
	КонецЕсли;
	
	Если Месяц < 12 Тогда  // дополним таблицу пустыми строками
		Для СчМесяцев = Месяц + 1 По 12 Цикл
			// Выведем пустой месяц
			ОбластьПустойМесяц.Параметры.Месяц = Формат(Дата(НалоговыйПериод,СчМесяцев,1),"ДФ=ММММ");
			ДокументРезультат.Вывести(ОбластьПустойМесяц);
		КонецЦикла;		
	КонецЕсли;
	
	//-----------------------------------------------------------------------------

	//Параметры документа
	ДокументРезультат.Автомасштаб 			= 	Истина;
	ДокументРезультат.ОриентацияСтраницы 	= 	ОриентацияСтраницы.Ландшафт;
	ДокументРезультат.ТолькоПросмотр		= 	Истина;
	
КонецПроцедуры

#КонецЕсли