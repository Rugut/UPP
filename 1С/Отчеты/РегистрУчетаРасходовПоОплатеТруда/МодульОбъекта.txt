#Если Клиент Тогда
	
Перем НП Экспорт;

Перем Счет07;
Перем Счет08;
Перем Счет08_01;
Перем Счет08_02;
Перем Счет08_03;
Перем Счет08_04;
Перем Счет08_05;
Перем Счет08_08;
Перем Счет10;
Перем Счет20;
Перем Счет23;
Перем Счет25;
Перем Счет26;
Перем Счет28;
Перем Счет29;
Перем Счет20_01_01;
Перем Счет23_01;
Перем Счет25_01;
Перем Счет28_01;
Перем Счет29_01;
Перем Счет20_01_02;
Перем Счет23_02;
Перем Счет25_02;
Перем Счет26_02;
Перем Счет28_02;
Перем Счет29_02;
Перем Счет41;
Перем Счет44;
Перем Счет44_01;
Перем Счет58_01_1;
Перем Счет58_01_2;
Перем Счет58_02;
Перем Счет58_05;
Перем Счет91_02;
Перем Счет91_02_1;
Перем Счет91_02_2;
Перем Счет91_02_3;
Перем Счет91_02_4;
Перем Счет91_02_5;
Перем Счет91_02_6;
Перем Счет97;
Перем СчетНПВ;

Перем мКэшСчетов;

Функция ПринадлежитСчету(Счет, Владелец)
	
	ДанныеИзКэша = мКэшСчетов[Счет];
	
	Если ДанныеИзКэша = Неопределено Тогда
		Результат = Счет.ПринадлежитЭлементу(Владелец);
		мКэшСчетов[Счет] = Новый Соответствие;
		мКэшСчетов[Счет][Владелец] = Результат;
		Возврат Результат;
		
	Иначе
		КэшПоСчету = ДанныеИзКэша[Владелец];
		ЕСли КэшПоСчету = Неопределено Тогда
			Результат = Счет.ПринадлежитЭлементу(Владелец);
			ДанныеИзКэша[Владелец] = Результат;
			Возврат Результат;
		Иначе
			Возврат КэшПоСчету;
		КонецЕсли;				
	КонецЕсли;	
	
КонецФункции

Функция ВидРасходаПоОперации(Счет, Субконто1, Субконто2, Субконто3)

	ВидРасхода  = "";
	ОбъектУчета = "";

	Попытка
		ВидЗатратНалоговогоУчета = Субконто1.ВидРасходовНУ;
	Исключение
		Попытка
			ВидЗатратНалоговогоУчета = Субконто2.ВидРасходовНУ;
        Исключение
			Попытка
				ВидЗатратНалоговогоУчета = Субконто3.ВидРасходовНУ;
			Исключение
				ВидЗатратНалоговогоУчета = "";
			КонецПопытки;
		КонецПопытки;
	КонецПопытки;
	
	Если ВидЗатратНалоговогоУчета = Перечисления.ВидыРасходовНУ.ДобровольноеЛичноеСтрахование ИЛИ
		 ВидЗатратНалоговогоУчета = Перечисления.ВидыРасходовНУ.ДобровольноеЛичноеСтрахованиеНаСлучайСмертиИлиУтратыРаботоспособности ИЛИ
		 ВидЗатратНалоговогоУчета = Перечисления.ВидыРасходовНУ.ДобровольноеСтрахованиеПоДоговорамДолгосрочногоСтрахованияЖизниРаботников Тогда
		Возврат Новый Структура("ВидРасхода, ОбъектУчета");

	КонецЕсли;

	Если НЕ ЗначениеЗаполнено(Счет) Тогда
		ВидРасхода = "Без расходов";
		Возврат Новый Структура("ВидРасхода, ОбъектУчета", ВидРасхода, ОбъектУчета);
	КонецЕсли;
	
	Если ПринадлежитСчету(Счет, Счет07) ИЛИ Счет = Счет07 Тогда
		ВидРасхода  = "На приобретение оборудования, требующего монтажа";
		ОбъектУчета = Субконто1;

	ИначеЕсли ПринадлежитСчету(Счет, Счет08) ИЛИ Счет = Счет08 Тогда
		ВидРасхода = "На приобретение ОС";
		ОбъектУчета = Субконто1;

		Если ПринадлежитСчету(Счет, Счет08_01) ИЛИ Счет = Счет08_01 ИЛИ
			 ПринадлежитСчету(Счет, Счет08_02) ИЛИ Счет = Счет08_02 ИЛИ
			 ПринадлежитСчету(Счет, Счет08_03) ИЛИ Счет = Счет08_03 ИЛИ
			 ПринадлежитСчету(Счет, Счет08_04) ИЛИ Счет = Счет08_04 Тогда
			ВидРасхода = "На приобретение ОС";

		ИначеЕсли ПринадлежитСчету(Счет, Счет08_05) ИЛИ Счет = Счет08_05 Тогда
			ВидРасхода = "На приобретение НМА";

		ИначеЕсли ПринадлежитСчету(Счет, Счет08_08) ИЛИ Счет = Счет08_08 Тогда
			ВидРасхода = "На НИОКР";

		КонецЕсли;

	ИначеЕсли ПринадлежитСчету(Счет, Счет10) ИЛИ Счет = Счет10 Тогда
		ВидРасхода  = "На приобретение материалов";
		ОбъектУчета = Субконто1;

	ИначеЕсли ПринадлежитСчету(Счет, Счет41) ИЛИ Счет = Счет41 Тогда
		ВидРасхода = "На приобретение товаров";
		ОбъектУчета = Субконто1;

	ИначеЕсли ПринадлежитСчету(Счет, Счет58_01_1) ИЛИ Счет = Счет58_01_1 Тогда
		ВидРасхода  = "На приобретение паев";
		ОбъектУчета = Субконто1;

	ИначеЕсли ПринадлежитСчету(Счет, Счет58_01_2) ИЛИ Счет = Счет58_01_2 ИЛИ
		      ПринадлежитСчету(Счет, Счет58_02)   ИЛИ Счет = Счет58_02 Тогда
		ВидРасхода  = "На приобретение ценных бумаг";
		ОбъектУчета = Строка(Субконто1) + " (" + Строка(Субконто2) + ")";

	ИначеЕсли ПринадлежитСчету(Счет, Счет58_05) ИЛИ Счет = Счет58_05 Тогда
		ВидРасхода  = "На приобретение прав требования";
		ОбъектУчета = Строка(Субконто1) + " (" + Строка(Субконто2) + ")";

	ИначеЕсли ПринадлежитСчету(Счет, Счет44_01) ИЛИ Счет = Счет44_01 Тогда
		ВидРасхода  = "Издержки обращения";;
		Если ВидЗатратНалоговогоУчета = Перечисления.ВидыРасходовНУ.ТранспортныеРасходы Тогда
			ВидРасхода = "Транспортные расходы";
		КонецЕсли;

	ИначеЕсли (ВидЗатратНалоговогоУчета = Перечисления.ВидыРасходовНУ.ПредставительскиеРасходы)
			  И
			  (ПринадлежитСчету(Счет, Счет44)       ИЛИ Счет = Счет44 ИЛИ
			   ПринадлежитСчету(Счет, Счет20_01_02) ИЛИ Счет = Счет20_01_02 ИЛИ
			   ПринадлежитСчету(Счет, Счет23_02)    ИЛИ Счет = Счет23_02 ИЛИ
			   ПринадлежитСчету(Счет, Счет25_02)    ИЛИ Счет = Счет25_02 ИЛИ
			   ПринадлежитСчету(Счет, Счет26_02)    ИЛИ Счет = Счет26_02 ИЛИ
			   ПринадлежитСчету(Счет, Счет28_02)    ИЛИ Счет = Счет28_02 ИЛИ
			   ПринадлежитСчету(Счет, Счет29_02)    ИЛИ Счет = Счет29_02 ) Тогда
			ВидРасхода = "Представительские расходы";

	ИначеЕсли (ВидЗатратНалоговогоУчета = Перечисления.ВидыРасходовНУ.РасходыНаРекламуНормируемые)
			  И
			  (ПринадлежитСчету(Счет, Счет44)       ИЛИ Счет = Счет44 ИЛИ
			   ПринадлежитСчету(Счет, Счет20_01_02) ИЛИ Счет = Счет20_01_02 ИЛИ
			   ПринадлежитСчету(Счет, Счет23_02)    ИЛИ Счет = Счет23_02 ИЛИ
			   ПринадлежитСчету(Счет, Счет25_02)    ИЛИ Счет = Счет25_02 ИЛИ
			   ПринадлежитСчету(Счет, Счет26_02)    ИЛИ Счет = Счет26_02 ИЛИ
			   ПринадлежитСчету(Счет, Счет28_02)    ИЛИ Счет = Счет28_02 ИЛИ
			   ПринадлежитСчету(Счет, Счет29_02)    ИЛИ Счет = Счет29_02) Тогда
			ВидРасхода = "Расходы на рекламу (нормируемые)";
			
	ИначеЕсли (ПринадлежитСчету(Счет, Счет44)    ИЛИ Счет = Счет44 ИЛИ
			   ПринадлежитСчету(Счет, Счет20)    ИЛИ Счет = Счет20 ИЛИ
			   ПринадлежитСчету(Счет, Счет23)    ИЛИ Счет = Счет23 ИЛИ
			   ПринадлежитСчету(Счет, Счет25)    ИЛИ Счет = Счет25 ИЛИ
			   ПринадлежитСчету(Счет, Счет26)    ИЛИ Счет = Счет26 ИЛИ
			   ПринадлежитСчету(Счет, Счет28)    ИЛИ Счет = Счет28 ИЛИ
			   ПринадлежитСчету(Счет, Счет29)    ИЛИ Счет = Счет29 ИЛИ
			   ПринадлежитСчету(Счет, Счет91_02) ИЛИ Счет = Счет91_02
			   )
			   И 
			   (ВидЗатратНалоговогоУчета = Перечисления.ВидыРасходовНУ.НеУчитываемыеВЦеляхНалогообложения) Тогда
			ВидРасхода = "Расходы, не учитываемые в целях налогообложения";

	ИначеЕсли (ПринадлежитСчету(Счет, Счет44)       ИЛИ Счет = Счет44 ИЛИ
			   ПринадлежитСчету(Счет, Счет20_01_02) ИЛИ Счет = Счет20_01_02 ИЛИ
			   ПринадлежитСчету(Счет, Счет23_02)    ИЛИ Счет = Счет23_02 ИЛИ
			   ПринадлежитСчету(Счет, Счет25_02)    ИЛИ Счет = Счет25_02 ИЛИ
			   ПринадлежитСчету(Счет, Счет26_02)    ИЛИ Счет = Счет26_02 ИЛИ
			   ПринадлежитСчету(Счет, Счет28_02)    ИЛИ Счет = Счет28_02 ИЛИ
			   ПринадлежитСчету(Счет, Счет29_02)    ИЛИ Счет = Счет29_02
			   )
			   И
			   (ВидЗатратНалоговогоУчета <> Перечисления.ВидыРасходовНУ.ТранспортныеРасходы ИЛИ
			    ВидЗатратНалоговогоУчета <> Перечисления.ВидыРасходовНУ.ПредставительскиеРасходы ИЛИ
			    ВидЗатратНалоговогоУчета <> Перечисления.ВидыРасходовНУ.РасходыНаРекламуНормируемые ИЛИ
			    ВидЗатратНалоговогоУчета <> Перечисления.ВидыРасходовНУ.НеУчитываемыеВЦеляхНалогообложения) Тогда
			ВидРасхода  = "Косвенные расходы";
			ОбъектУчета = ВидЗатратНалоговогоУчета;

	ИначеЕсли (ПринадлежитСчету(Счет, Счет20_01_01) ИЛИ Счет = Счет20_01_01 ИЛИ
			   ПринадлежитСчету(Счет, Счет23_01)    ИЛИ Счет = Счет23_01 ИЛИ
			   ПринадлежитСчету(Счет, Счет25_01)    ИЛИ Счет = Счет25_01 ИЛИ
			   ПринадлежитСчету(Счет, Счет29_01)    ИЛИ Счет = Счет29_01 ИЛИ
			   ПринадлежитСчету(Счет, Счет28_01)    ИЛИ Счет = Счет28_01 
			   ) Тогда
			ВидРасхода  = "Прямые расходы";
			ОбъектУчета = ВидЗатратНалоговогоУчета;

	ИначеЕсли ПринадлежитСчету(Счет, Счет91_02_1) ИЛИ Счет = Счет91_02_1 Тогда
		ВидРасхода = "Расходы на реализацию ОС";
		ОбъектУчета = Субконто2;

	ИначеЕсли ПринадлежитСчету(Счет, Счет91_02_2) ИЛИ Счет = Счет91_02_2 Тогда
		ВидРасхода = "Расходы на реализацию НМА";
		ОбъектУчета = Субконто2;

	ИначеЕсли ПринадлежитСчету(Счет, Счет91_02_3) ИЛИ Счет = Счет91_02_3 Тогда
		Если ТипЗнч(Субконто2) = Тип("СправочникСсылка.Номенклатура") Тогда
			Если НЕ Субконто2.Услуга Тогда
				ВидРасхода = "Расходы на реализацию прочего имущества";
			КонецЕсли;
		КонецЕсли;

	ИначеЕсли ПринадлежитСчету(Счет, Счет91_02_4) ИЛИ Счет = Счет91_02_4 Тогда
		ВидРасхода = "Расходы, связанные с реализацией имущественных прав";
		ОбъектУчета = Строка(Субконто2) + " (" + Строка(Субконто3) + ")";

	ИначеЕсли ПринадлежитСчету(Счет, Счет91_02_5) ИЛИ Счет = Счет91_02_5 Тогда
		ВидРасхода = "Расходы, связанные с реализацией ЦБ";
		ОбъектУчета = Строка(Субконто2);

	ИначеЕсли ПринадлежитСчету(Счет, Счет91_02_6) ИЛИ Счет = Счет91_02_6 Тогда
		Если ВидЗатратНалоговогоУчета <> Перечисления.ВидыРасходовНУ.НеУчитываемыеВЦеляхНалогообложения Тогда
			ВидРасхода  = "Внереализационные расходы";
			ОбъектУчета = ВидЗатратНалоговогоУчета;
		КонецЕсли;

	ИначеЕсли (ПринадлежитСчету(Счет, ПланыСчетов.Налоговый.ДоходыРасходыНеУчитываемые)) Тогда 
			ВидРасхода = "Расходы, не учитываемые в целях налогообложения";
	ИначеЕсли ПринадлежитСчету(Счет, Счет97) ИЛИ Счет = Счет97 Тогда
		ВидРасхода = "Прочие РБП";
		//На долгосрочное страхование жизни
		//На освоение природных ресурсов
		//Страхование на оплату медицинских расходов
		//Страхование на случай смерти и утраты трудоспособности

	Иначе
		ВидРасхода = "Без расходов";

	КонецЕсли;

	Возврат Новый Структура("ВидРасхода, ОбъектУчета", ВидРасхода, ОбъектУчета);

КонецФункции // ВидРасходаПоОперации()

// Выполняет запрос и формирует табличный документ-результат отчета
// в соответствии с настройками, заданными значениями реквизитов отчета.
//
// Параметры:
//	ДокументРезультат - табличный документ, формируемый отчетом
//	ПоказыватьЗаголовок - признак видимости строк с заголовком отчета
//	ВысотаЗаголовка - параметр, через который возвращается высота заголовка в строках 
//
Процедура СформироватьОтчет(ДокументРезультат, ПоказыватьЗаголовок, ВысотаЗаголовка, ТолькоЗаголовок = Ложь) Экспорт

	мКэшСчетов = Новый Соответствие;
	
	ДокументРезультат.Очистить();

	Макет = ПолучитьМакет("Отчет");

	ОбластьЗаголовок  = Макет.ПолучитьОбласть("Заголовок");

	ОбластьЗаголовок.Параметры.НачалоПериода       = Формат(ДатаНач, "ДФ=dd.MM.yyyy");
	ОбластьЗаголовок.Параметры.КонецПериода        = Формат(ДатаКон, "ДФ=dd.MM.yyyy");
	ОбластьЗаголовок.Параметры.НазваниеОрганизации = Организация.НаименованиеПолное;
	ОбластьЗаголовок.Параметры.ИННОрганизации      = "" + Организация.ИНН + " / " + Организация.КПП;
	ДокументРезультат.Вывести(ОбластьЗаголовок);

	// Параметр для показа заголовка
	ВысотаЗаголовка = ДокументРезультат.ВысотаТаблицы;

	// Когда нужен только заголовок:
	Если ТолькоЗаголовок Тогда
		Возврат;
	КонецЕсли;

	Если ЗначениеЗаполнено(ВысотаЗаголовка) Тогда
		ДокументРезультат.Область("R1:R" + ВысотаЗаголовка).Видимость = ПоказыватьЗаголовок;
	КонецЕсли;

	// Определим способ ведения учета расчетов с персоналом - сводно/подробно
	ВестиРеглУчетРасчетовСПерсоналомСводно = глЗначениеПеременной("ВестиРеглУчетРасчетовСПерсоналомСводно");

	Запрос = Новый Запрос;

	Запрос.УстановитьПараметр("ДатаНач",      НачалоДня(ДатаНач));
	Запрос.УстановитьПараметр("ДатаКон",      КонецДня(ДатаКон));
	Запрос.УстановитьПараметр("Организация",  Организация);
	Запрос.УстановитьПараметр("Счет70",       ПланыСчетов.Налоговый.РасчетыСПерсоналомПоОплатеТруда);
	Запрос.УстановитьПараметр("Счет97",       ПланыСчетов.Налоговый.РасходыНаОплатуТрудаБудущихПериодов);
	Запрос.УстановитьПараметр("Счет69",       ПланыСчетов.Налоговый.РасчетыПоСоциальномуСтрахованию);
	Запрос.УстановитьПараметр("ВидУчета",     Перечисления.ВидыУчетаПоПБУ18.НУ);
	Запрос.УстановитьПараметр("СубконтоОплатаТруда",       Справочники.РасходыБудущихПериодов.РБПНаОплатуТруда);
	
	Если ВестиРеглУчетРасчетовСПерсоналомСводно Тогда
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	КОНЕЦПЕРИОДА(НалоговыйОборотыДтКт.Ссылка.ПериодРегистрации, МЕСЯЦ) КАК ДатаОперации,
		|	НалоговыйОборотыДтКт.Ссылка КАК Расшифровка,
		|	НалоговыйОборотыДтКт.СчетДтНУ КАК СчетДт,
		|	НалоговыйОборотыДтКт.СубконтоДтНУ1 КАК СубконтоДт1,
		|	НалоговыйОборотыДтКт.СубконтоДтНУ2 КАК СубконтоДт2,
		|	НалоговыйОборотыДтКт.СубконтоДтНУ3 КАК СубконтоДт3,
		|	ВЫБОР
		|		КОГДА НалоговыйОборотыДтКт.СчетКтНУ В ИЕРАРХИИ (&Счет70)
		|			ТОГДА НалоговыйОборотыДтКт.СубконтоКтНУ1.Наименование
		|		ИНАЧЕ НалоговыйОборотыДтКт.СубконтоКтНУ2.Наименование
		|	КОНЕЦ КАК Работник,
		|	ВЫБОР
		|		КОГДА НалоговыйОборотыДтКт.СчетКтНУ В ИЕРАРХИИ (&Счет70)
		|			ТОГДА НалоговыйОборотыДтКт.СубконтоКтНУ2
		|		ИНАЧЕ НалоговыйОборотыДтКт.СубконтоКтНУ3
		|	КОНЕЦ КАК НаименованиеРасхода,
		|	НалоговыйОборотыДтКт.Сумма КАК Сумма
		|ИЗ
		|	(ВЫБРАТЬ
		|		СУММА(ОтражениеВНУ.Сумма) КАК Сумма,
		|		ОтражениеВНУ.Ссылка КАК Ссылка,
		|		ОтражениеВНУ.СчетДтНУ КАК СчетДтНУ,
		|		ОтражениеВНУ.СчетКтНУ КАК СчетКтНУ,
		|		ОтражениеВНУ.СубконтоДтНУ1 КАК СубконтоДтНУ1,
		|		ОтражениеВНУ.СубконтоДтНУ2 КАК СубконтоДтНУ2,
		|		ОтражениеВНУ.СубконтоДтНУ3 КАК СубконтоДтНУ3,
		|		ОтражениеВНУ.СубконтоКтНУ1 КАК СубконтоКтНУ1,
		|		ОтражениеВНУ.СубконтоКтНУ2 КАК СубконтоКтНУ2,
		|		ОтражениеВНУ.СубконтоКтНУ3 КАК СубконтоКтНУ3
		|	ИЗ
		|		Документ.ОтражениеЗарплатыВРеглУчете.ОтражениеВУчете КАК ОтражениеВНУ
		|	ГДЕ
		|		ОтражениеВНУ.Ссылка.Организация = &Организация
		|		И ОтражениеВНУ.Ссылка.Проведен
		|		И ОтражениеВНУ.Ссылка.ПериодРегистрации МЕЖДУ &ДатаНач И &ДатаКон
		|		И (НЕ ОтражениеВНУ.СчетДтНУ В ИЕРАРХИИ (&Счет69))
		|		И ((НЕ ОтражениеВНУ.СчетДтНУ В ИЕРАРХИИ (&Счет97))
		|					И ОтражениеВНУ.СчетКтНУ В ИЕРАРХИИ (&Счет70)
		|				ИЛИ ОтражениеВНУ.СчетКтНУ В ИЕРАРХИИ (&Счет97)
		|					И ОтражениеВНУ.СубконтоКтНУ1 = &СубконтоОплатаТруда)
		|	
		|	СГРУППИРОВАТЬ ПО
		|		ОтражениеВНУ.Ссылка,
		|		ОтражениеВНУ.СчетДтНУ,
		|		ОтражениеВНУ.СчетКтНУ,
		|		ОтражениеВНУ.СубконтоДтНУ1,
		|		ОтражениеВНУ.СубконтоДтНУ2,
		|		ОтражениеВНУ.СубконтоДтНУ3,
		|		ОтражениеВНУ.СубконтоКтНУ1,
		|		ОтражениеВНУ.СубконтоКтНУ2,
		|		ОтражениеВНУ.СубконтоКтНУ3) КАК НалоговыйОборотыДтКт
		|
		|УПОРЯДОЧИТЬ ПО
		|	ДатаОперации
		|ИТОГИ ПО
		|	СчетДт ТОЛЬКО ИЕРАРХИЯ";
	Иначе 
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	НалоговыйОборотыДтКт.Период КАК ДатаОперации,
		|	НалоговыйОборотыДтКт.Регистратор КАК Расшифровка,
		|	НалоговыйОборотыДтКт.СчетДт КАК СчетДт,
		|	НалоговыйОборотыДтКт.СубконтоДт1,
		|	НалоговыйОборотыДтКт.СубконтоДт2,
		|	НалоговыйОборотыДтКт.СубконтоДт3,
		|	НалоговыйОборотыДтКт.СубконтоКт1 КАК Работник,
		|	НалоговыйОборотыДтКт.СубконтоКт2 КАК НаименованиеРасхода,
		|	НалоговыйОборотыДтКт.СуммаОборот КАК Сумма
		|ИЗ
		|	РегистрБухгалтерии.Налоговый.ОборотыДтКт(
		|			&ДатаНач,
		|			&ДатаКон,
		|			Запись,
		|			(НЕ(СчетДт В ИЕРАРХИИ (&Счет97)
		|					И (НЕ СчетДт В ИЕРАРХИИ (&Счет69)))),
		|			,
		|			СчетКт В ИЕРАРХИИ (&Счет70),
		|			,
		|			Организация = &Организация
		|				И ВидУчетаКт = &ВидУчета) КАК НалоговыйОборотыДтКт
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	НалоговыйОборотыДтКт.Период,
		|	НалоговыйОборотыДтКт.Регистратор,
		|	НалоговыйОборотыДтКт.СчетДт,
		|	НалоговыйОборотыДтКт.СубконтоДт1,
		|	НалоговыйОборотыДтКт.СубконтоДт2,
		|	НалоговыйОборотыДтКт.СубконтоДт3,
		|	НалоговыйОборотыДтКт.СубконтоКт2,
		|	НалоговыйОборотыДтКт.СубконтоКт3,
		|	НалоговыйОборотыДтКт.СуммаОборот
		|ИЗ
		|	РегистрБухгалтерии.Налоговый.ОборотыДтКт(
		|			&ДатаНач,
		|			&ДатаКон,
		|			Запись,
		|			(НЕ СчетДт В ИЕРАРХИИ (&Счет69)),
		|			,
		|			СчетКт В ИЕРАРХИИ (&Счет97),
		|			,
		|			Организация = &Организация
		|				И ВидУчетаДт = &ВидУчета
		|				И СубконтоКт1 = &СубконтоОплатаТруда) КАК НалоговыйОборотыДтКт
		|
		|УПОРЯДОЧИТЬ ПО
		|	ДатаОперации
		|ИТОГИ ПО
		|	СчетДт ТОЛЬКО ИЕРАРХИЯ";
	КонецЕсли;
	
	Результат = Запрос.Выполнить().Выгрузить();
	Результат.Сортировать("ДатаОперации,Работник");

	ОбластьПодвал        = Макет.ПолучитьОбласть("Подвал");
	ОбластьШапкаТаблицы  = Макет.ПолучитьОбласть("ШапкаТаблицы");
	ОбластьСтрока        = Макет.ПолучитьОбласть("Строка");
	ОбластьИтог          = Макет.ПолучитьОбласть("Итог");
	
	ДокументРезультат.Вывести(ОбластьШапкаТаблицы);

	СписокВидов = Новый СписокЗначений();
	Для каждого ЭлементСписка из ВидыРасходов Цикл
		Если ЭлементСписка.Пометка Тогда
			СписокВидов.Добавить(ЭлементСписка.Значение);                       
		КонецЕсли;
	КонецЦикла;
    ИтогСумма = 0;
	Для Каждого Выборка Из Результат Цикл
		Если НЕ ЗначениеЗаполнено(Выборка.Расшифровка)  Тогда
			Продолжить;
		КонецЕсли;
		
		СтруктураВида = ВидРасходаПоОперации(Выборка.СчетДт, Выборка.СубконтоДт1, Выборка.СубконтоДт2, Выборка.СубконтоДт3);
	
		Если СписокВидов.НайтиПоЗначению(Выборка.НаименованиеРасхода) = Неопределено Тогда
			Продолжить;
		КонецЕсли;

		ОбластьСтрока.Параметры.ДатаОперации        = Выборка.ДатаОперации;
		ОбластьСтрока.Параметры.Работник            = Выборка.Работник;
		ОбластьСтрока.Параметры.НаименованиеРасхода = Выборка.НаименованиеРасхода;
		ОбластьСтрока.Параметры.Сумма               = Выборка.Сумма;
		ОбластьСтрока.Параметры.ОбъектУчета         = СтруктураВида.ВидРасхода + 
				?(НЕ ЗначениеЗаполнено(СтруктураВида.ОбъектУчета), "", " (" +СтруктураВида.ОбъектУчета+ ")");
		ОбластьСтрока.Параметры.Расшифровка         = Выборка.Расшифровка;

		ДокументРезультат.Вывести(ОбластьСтрока);
		ИтогСумма =  ИтогСумма + Выборка.Сумма;
		
	КонецЦикла;

	ОбластьИтог.Параметры.ИтогСумма = ИтогСумма;
	ДокументРезультат.Вывести(ОбластьИтог);
	СтруктураЛиц = РегламентированнаяОтчетность.ОтветственныеЛицаОрганизаций(Организация, ДатаКон);
	ОбластьПодвал.Параметры.ОтветственныйЗаРегистры = СтруктураЛиц.ОтветственныйЗаРегистры;
	ДокументРезультат.Вывести(ОбластьПодвал);
КонецПроцедуры // СформироватьОтчет

////////////////////////////////////////////////////////////////////////////////
// ОПЕРАТОРЫ ОСНОВНОЙ ПРОГРАММЫ
// 

НП           = Новый НастройкаПериода;

Счет07       = ПланыСчетов.Налоговый.ОборудованиеКУстановке;
Счет08       = ПланыСчетов.Налоговый.ВложенияВоВнеоборотныеАктивы;
Счет08_01    = ПланыСчетов.Налоговый.ПриобретениеЗемельныхУчастков;
Счет08_02    = ПланыСчетов.Налоговый.ПриобретениеОбъектовПриродопользования;
Счет08_03    = ПланыСчетов.Налоговый.СтроительствоОбъектовОсновныхСредств;
Счет08_04    = ПланыСчетов.Налоговый.ПриобретениеОбъектовОсновныхСредств;
Счет08_05    = ПланыСчетов.Налоговый.ПриобретениеНематериальныхАктивов;
Счет08_08    = ПланыСчетов.Налоговый.ВыполнениеНИОКР;
Счет10       = ПланыСчетов.Налоговый.Материалы;

Счет20       = ПланыСчетов.Налоговый.ОсновноеПроизводство_;
Счет23       = ПланыСчетов.Налоговый.ВспомогательныеПроизводства;
Счет25       = ПланыСчетов.Налоговый.ОбщепроизводственныеРасходы;
Счет26       = ПланыСчетов.Налоговый.ОбщехозяйственныеРасходы;
Счет28       = ПланыСчетов.Налоговый.БракВПроизводстве;
Счет29       = ПланыСчетов.Налоговый.ОбслуживающиеПроизводства;

Счет20_01_01 = ПланыСчетов.Налоговый.ПрямыеРасходыОсновногоПроизводства;
Счет23_01    = ПланыСчетов.Налоговый.ПрямыеРасходыВспомогательныхПроизводств;
Счет25_01    = ПланыСчетов.Налоговый.ПрямыеОбщепроизводственныеРасходы;
Счет28_01    = ПланыСчетов.Налоговый.ПрямыеРасходыПоВыявленномуБраку;
Счет29_01    = ПланыСчетов.Налоговый.ПрямыеРасходыОбслуживающихПроизводств;

Счет20_01_02 = ПланыСчетов.Налоговый.КосвенныеРасходыОсновногоПроизводства;
Счет23_02    = ПланыСчетов.Налоговый.КосвенныеРасходыВспомогательныхПроизводств;
Счет25_02    = ПланыСчетов.Налоговый.КосвенныеОбщепроизводственныеРасходы;
Счет26_02    = ПланыСчетов.Налоговый.КосвенныеОбщехозяйственныеРасходы;
Счет28_02    = ПланыСчетов.Налоговый.КосвенныеРасходыПоВыявленномуБраку;
Счет29_02    = ПланыСчетов.Налоговый.КосвенныеРасходыОбслуживающихПроизводств;

Счет41       = ПланыСчетов.Налоговый.Товары;
Счет44       = ПланыСчетов.Налоговый.РасходыНаПродажу;
Счет44_01    = ПланыСчетов.Налоговый.ИздержкиОбращения;

Счет58_01_1  = ПланыСчетов.Налоговый.Паи;
Счет58_01_2  = ПланыСчетов.Налоговый.Акции;
Счет58_02    = ПланыСчетов.Налоговый.ДолговыеЦенныеБумаги;
Счет58_05    = ПланыСчетов.Налоговый.ПриобретенныеПрава;

Счет91_02    = ПланыСчетов.Налоговый.ПрочиеРасходы;
Счет91_02_1  = ПланыСчетов.Налоговый.РасходыСвязанныеСРеализациейОсновныхСредств;
Счет91_02_2  = ПланыСчетов.Налоговый.РасходыСвязанныеСРеализациейНематериальныхАктивов;
Счет91_02_3  = ПланыСчетов.Налоговый.РасходыСвязанныеСРеализациейПрочегоИмущества;
Счет91_02_4  = ПланыСчетов.Налоговый.РасходыСвязанныеСРеализациейИмущественныхПрав;
Счет91_02_5  = ПланыСчетов.Налоговый.РасходыСвязанныеСРеализациейЦенныхБумаг;
Счет91_02_6  = ПланыСчетов.Налоговый.ВнереализационныеРасходы;

Счет97       = ПланыСчетов.Налоговый.РасходыБудущихПериодов;
СчетНПВ      = ПланыСчетов.Налоговый.ПоступлениеИВыбытиеИмуществаРаботУслугПрав;

#КонецЕсли