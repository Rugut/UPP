Перем мТекПоказатель;

// Функция определяет возможность редактирования значения
// текущего параметра
//
// Параметры:
//     Строка - строка таблицы значений
//
// Возврат:
//     Истина - если значение можно редактировать
//     Ложь - иначе
//
Функция ДоступностьЗначенияПоказателя(Строка)
	
	Если Строка = Неопределено Тогда
		Возврат Истина;
	КонецЕсли;
	
	Если Строка.Доступность.Количество() = 0 Тогда
		Флаг = Истина;
	Иначе
		Флаг = Ложь;
		Для Каждого Свойство Из Строка.Доступность Цикл
			СтрПарам = ТаблицаПараметров.Найти(Свойство.Ключ, "Имя");
			Если СтрПарам.Значение = Свойство.Значение Тогда
				Флаг = Истина;
				Прервать;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;

	Возврат Флаг;
	
КонецФункции // ДоступностьЗначенияПоказателя()

// Процедура заполнения таблицы параметров
//
Процедура ЗаполнитьТаблицуПараметров(Показатель)

	Если ПустаяСтрока(Показатель) Тогда
		// скопируем таблицу саму в себя, чтобы оторвать связь с СтруктураПараметровПоказателей
		// в противном случаем при очистке ТаблицаПараметров очистится СтруктураПараметровПоказателей
		ТаблицаПараметров = ТаблицаПараметров.Скопировать();
		ТаблицаПараметров.Очистить();
		Возврат;
	КонецЕсли;
	
	ТабПарам = Неопределено;
	СтруктураПараметровПоказателей.Свойство(Показатель, ТабПарам);
	
	Если ТабПарам = Неопределено Тогда
		ТаблицаПараметров.Очистить();
		Возврат;
	КонецЕсли;

	ТаблицаПараметров = ТабПарам.Скопировать();
	
КонецПроцедуры // ЗаполнитьТаблицуПараметров()
	
// Процедура - обработчик события Перед открытием формы
//
Процедура ПередОткрытием(Отказ, СтандартнаяОбработка)

	мТекПоказатель = НачальноеЗначениеВыбора;
	ЗаполнитьТаблицуПараметров(НачальноеЗначениеВыбора);
	
КонецПроцедуры // ПередОткрытием()

// Процедура - обработчик события АктивизацииОбъекта
//
// Вызывается из формы родителя при смене текущего показателя
// для соответствующего обновления данных текущей формы
//
Процедура ОбработкаАктивизацииОбъекта(АктивныйОбъект, Источник)

	Если ТипЗнч(АктивныйОбъект) = Тип("Строка") Тогда
		Если ТипЗнч(мТекПоказатель) = Тип("Строка") И Не ПустаяСтрока(мТекПоказатель) Тогда
			СтруктураПараметровПоказателей.Вставить(мТекПоказатель, ТаблицаПараметров);
		КонецЕсли;
		мТекПоказатель = АктивныйОбъект;
		ЗаполнитьТаблицуПараметров(АктивныйОбъект);
	КонецЕсли;
	
КонецПроцедуры // ОбработкаАктивизацииОбъекта()

// Процедура - обработчик события ПриНачалеРедактирования
//
Процедура ТабличноеПолеПараметровПриНачалеРедактирования(Элемент, НоваяСтрока)

	Если Не Элемент.ТекущаяСтрока = Неопределено Тогда
		
		ЭлементУправления = Элемент.Колонки["Значение"].ЭлементУправления; 
		ЭлементУправления.Доступность = ДоступностьЗначенияПоказателя(Элемент.ТекущаяСтрока);
		
		ТипЗнач = ТипЗнч( Элемент.ТекущаяСтрока.Значение);
		ЭлементУправления.РедактированиеТекста = (ТипЗнач = Тип("Дата") Или ТипЗнач = Тип("Число"));
		
		Флаг = Элемент.ТекущаяСтрока.Список.Количество() = 0;
		ЭлементУправления.КнопкаСпискаВыбора = НЕ Флаг;
		ЭлементУправления.КнопкаВыбора       = Флаг;
		ЭлементУправления.КнопкаОчистки      = Флаг;
		
	КонецЕсли;
		
КонецПроцедуры // ТабличноеПолеПараметровПриНачалеРедактирования()

// Процедура - обработчик события ПриВыводеСтроки табличного поля
//
Процедура ТабличноеПолеПараметровПриВыводеСтроки(Элемент, ОформлениеСтроки, ДанныеСтроки)
	
	Если ДоступностьЗначенияПоказателя(ДанныеСтроки) Тогда
		ОформлениеСтроки.ЦветТекста = ЦветаСтиля.ЦветТекстаФормы;
	Иначе
		ОформлениеСтроки.ЦветТекста = WebЦвета.НейтральноСерый;
	КонецЕсли;
	
КонецПроцедуры // ТабличноеПолеПараметровПриВыводеСтроки()

// Процедура - обработчик события Нажатие
//
Процедура ОсновныеДействияФормыОК(Кнопка)

	Если Не ПустаяСтрока(мТекПоказатель) Тогда
		СтруктураПараметровПоказателей.Вставить(мТекПоказатель, ТаблицаПараметров);
	КонецЕсли;
	Закрыть();
	
КонецПроцедуры // ОсновныеДействияФормыОК()

// Процедура - обработчик события НачалоВыбораИзСписка колонки Значение табличного поля ТабличноеПолеПараметров
//
Процедура ТабличноеПолеПараметровЗначениеНачалоВыбораИзСписка(Элемент, СтандартнаяОбработка)
	ТекСтрока = ЭлементыФормы.ТабличноеПолеПараметров.ТекущаяСтрока;
	Элемент.СписокВыбора = ТекСтрока.Список;
КонецПроцедуры // ТабличноеПолеПараметровЗначениеНачалоВыбораИзСписка()