Перем ВысотаЗаголовка;

// Обновляет таблицу отчета
//
// Параметры:
//	Нет.
//
Процедура ОбновитьОтчет() Экспорт

	НеЗаполнятьНастройкиПриОткрытии = Не Открыта();
	
	СформироватьОтчет(ЭлементыФормы.ДокументРезультат, ПоказыватьЗаголовок, ВысотаЗаголовка);

	ЭтаФорма.ТекущийЭлемент = ЭлементыФормы.ДокументРезультат;
	
КонецПроцедуры // ОбновитьОтчет()

Процедура КнопкаНастройкаПериодаНажатие(Элемент)

	РаботаСДиалогами.ОбработчикНастройкаПериодаНажатие(ДатаНач, ДатаКон);

КонецПроцедуры

//  Управляет выводом заголовка
//
// Параметры:
//	Нет.
//
Процедура ВыводЗаголовка()
	
	БухгалтерскиеОтчеты.ОбработатьВыводЗаголовка(ЭтаФорма, ЭтотОбъект, ВысотаЗаголовка, ПоказыватьЗаголовок);
    		
КонецПроцедуры // ВыводЗаголовка()

Процедура СформироватьЗаголовокФормы()

	ОписаниеПериода = БухгалтерскиеОтчеты.СформироватьСтрокуОграниченийПоДатамДляФормы(ДатаНач, ДатаКон);
	
	Заголовок = ЗаголовокОтчета()+" (" + ОписаниеПериода + ") " +  ?(ЗначениеЗаполнено(Организация.НаименованиеПолное), Организация.НаименованиеПолное, Организация);

КонецПроцедуры // СформироватьЗаголовокФормы()

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ НАЖАТИЯ КНОПОК КОМАНДНОЙ ПАНЕЛИ

Процедура КоманднаяПанельФормыОбновить(Кнопка)

	ОбновитьОтчет();

КонецПроцедуры

Процедура КоманднаяПанельФормыЗаголовок(Кнопка)

	ПоказыватьЗаголовок = Не ПоказыватьЗаголовок;
	ВыводЗаголовка();

КонецПроцедуры

Процедура КоманднаяПанельФормыНастройка(Кнопка)

	ФормаНастройка = ПолучитьФорму("ФормаНастройка", ЭтаФорма);
	
	Если ФормаНастройка.ОткрытьМодально() = Истина Тогда
		ОбновитьОтчет();
	КонецЕсли;

КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

Процедура СчетПриИзменении(Элемент)

	ЗаполнитьНачальныеНастройки();
	СформироватьЗаголовокФормы();

КонецПроцедуры // СчетПриИзменении()

Процедура ОбновлениеОтображения()

	СформироватьЗаголовокФормы();

КонецПроцедуры

Процедура ПередОткрытием(Отказ, СтандартнаяОбработка)
	
	ТиповыеОтчеты.НазначитьФормеУникальныйКлючИдентификации(ЭтаФорма);
	
	Если РегламентированнаяОтчетность.ИДКонфигурации() <> "БП" Тогда
		// Проверка ведения однофирменности
		УчетПоВсемОрганизациям = Истина;
	Иначе
		УчетПоВсемОрганизациям = глЗначениеПеременной("УчетПоВсемОрганизациям");		
	КонецЕсли;

	Если НЕ НеЗаполнятьНастройкиПриОткрытии Тогда
		
		ПоказыватьЗаголовок = Истина;
		
		СтруктураОбщихПараметров = ВосстановитьЗначение("ОбщиеПараметрыОтчетов"+ИмяРегистраБухгалтерии);
		Если ТипЗнч(СтруктураОбщихПараметров) = Тип("Структура") Тогда
			
			СтруктураОбщихПараметров.Свойство("Организация", Организация);
			СтруктураОбщихПараметров.Свойство("Счет", Счет);
			ЗаполнитьНачальныеНастройки();
			СтруктураОбщихПараметров.Свойство("ПоказыватьЗаголовок", ПоказыватьЗаголовок);
			СтруктураОбщихПараметров.Свойство("ДатаНач", ДатаНач);
			СтруктураОбщихПараметров.Свойство("ДатаКон", ДатаКон);
			Период = "ПЕРИОД";
			
		КонецЕсли;
		
		ВысотаЗаголовка = 0;
		
	КонецЕсли;
	
	БухгалтерскиеОтчеты.ПоддержкаОднофирменностиДляБухОтчетов(ЭтаФорма, Организация, УчетПоВсемОрганизациям, НЕ НеЗаполнятьНастройкиПриОткрытии);
    	
КонецПроцедуры

Процедура ПриОткрытии()
	
	Если НЕ НеЗаполнятьНастройкиПриОткрытии Тогда
		
		ВыводЗаголовка();
		СформироватьЗаголовокФормы();
		
	КонецЕсли;
	
	НеЗаполнятьНастройкиПриОткрытии=Ложь;
	
	БухгалтерскиеОтчеты.УправлениеПометкамиКнопокЗаголовковКоманднойПанели(ЭтаФорма, ПоказыватьЗаголовок);
    
КонецПроцедуры

Процедура ПриЗакрытии()
	
	СтруктураОбщихПараметров = Новый Структура;
	СтруктураОбщихПараметров.Вставить("Организация", Организация);
	СтруктураОбщихПараметров.Вставить("ПоказыватьЗаголовок", ПоказыватьЗаголовок);
	СтруктураОбщихПараметров.Вставить("ДатаНач", ДатаНач);
	СтруктураОбщихПараметров.Вставить("ДатаКон", Макс(ДатаНач, ДатаКон));
	СтруктураОбщихПараметров.Вставить("Счет",    Счет);
	
	СохранитьЗначение("ОбщиеПараметрыОтчетов"+ИмяРегистраБухгалтерии, СтруктураОбщихПараметров);
	
КонецПроцедуры

Процедура ПередСохранениемЗначений(Отказ)
	
	СохраняемыеРеквизиты = Новый Структура;

	СохраняемыеРеквизиты.Вставить("Организация", Организация);
	СохраняемыеРеквизиты.Вставить("Счет",   Счет);
	СохраняемыеРеквизиты.Вставить("НастройкиПостроителя",  ПостроительОтчета.ПолучитьНастройки());
	СохраняемыеРеквизиты.Вставить("Период", Период);
	
КонецПроцедуры

Процедура ПослеВосстановленияЗначений()
	
	Перем НастройкиПостроителя;
	
	// Если настройка восстанавливается, когда открывается форма сформированного отчета, игнорируем
	Если НеЗаполнятьНастройкиПриОткрытии Тогда
		Возврат;
	КонецЕсли;
	
	// Тип восстановленного значения должен быть Структура
	Если НЕ ТипЗнч(СохраняемыеРеквизиты) = Тип("Структура") Тогда 
		Возврат;
	КонецЕсли;
	
	СохраняемыеРеквизиты.Свойство("Счет",   Счет);
	
	ЗаполнитьНачальныеНастройки();

	СохраняемыеРеквизиты.Свойство("Организация", Организация);
	СохраняемыеРеквизиты.Свойство("НастройкиПостроителя", НастройкиПостроителя);
	ПостроительОтчета.УстановитьНастройки(НастройкиПостроителя);
	СохраняемыеРеквизиты.Свойство("Период", Период);
	
КонецПроцедуры

Процедура ДокументРезультатОбработкаРасшифровки(Элемент, Расшифровка, СтандартнаяОбработка)

	Если ТипЗнч(Расшифровка) = Тип("Структура") Тогда

		СтандартнаяОбработка = Ложь;
		//ФормаОперации = Расшифровка.ДокументОперации.ПолучитьОбъект().ПолучитьФорму();
		ФормаОперации = Расшифровка.ДокументОперации.ПолучитьФорму();
		ФормаОперации.Открыть();

	КонецЕсли;

КонецПроцедуры