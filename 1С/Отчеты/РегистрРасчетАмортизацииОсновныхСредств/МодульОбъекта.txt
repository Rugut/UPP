#Если Клиент Тогда
	
Перем НП Экспорт;

Перем ВидыРасходовПоСпособуАмортизации;
Перем CписокСчетов;
Перем ИтогСуммаАмортизации;
Перем СуммаАмортизацииПрямые экспорт;
Перем СуммаАмортизацииКосвенные Экспорт;
Перем СуммаАмортизацииПрочие Экспорт;
Перем СпособыРаспределенияРасходов Экспорт;
Перем СчетаПрямыхЗатрат;
Перем СчетаПрямыхИКосвенныхЗатрат;

// Возвращает полное наименование элемента справочника, если у этого вида 
// справочников есть реквизит НаименованиеПолное.
// 
// Параметры:      
//    Элемент - элемент справочника, для которого нужно вернуть полное наименование
//
// Возвращаемое значение: 
//  Строка с полным наименованием.
//
Функция ПолноеНаименование(Элемент) Экспорт
 
	Если НЕ ЗначениеЗаполнено(Элемент) Тогда
		Возврат "";
 
	ИначеЕсли Элемент.Метаданные().Реквизиты.Найти("НаименованиеПолное") = Неопределено Тогда
		Возврат Элемент.Наименование;;

	ИначеЕсли НЕ ЗначениеЗаполнено(Элемент.НаименованиеПолное) Тогда
		Возврат Элемент.Наименование;

	Иначе
		Возврат Элемент.НаименованиеПолное;
	КонецЕсли;
 
КонецФункции

Процедура ВывестиСтроку(Опер, ДокументРезультат, Макет)
	
	Если Опер.Амортизация = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Строка1 = Макет.ПолучитьОбласть("Строка|Общая1");
	Строка2 = Макет.ПолучитьОбласть("Строка|Объект");
	Строка3 = Макет.ПолучитьОбласть("Строка|Общая2");
	
	Строка1.Параметры.ДатаОперации = Формат(Опер.ПериодДействия, "ДФ=dd.MM.yyyy");
	Строка3.Параметры.СпециальныйКоэффициент = Опер.СпециальныйКоэффициент;
	Строка3.Параметры.МетодНачисленияАмортизации = Опер.МетодНачисленияАмортизации;
	Строка2.Параметры.НаименованиеОбъекта = ПолноеНаименование(Опер.ОсновноеСредство);
	
	Если Опер.МетодНачисленияАмортизации = Перечисления.МетодыНачисленияАмортизации.Линейный Тогда
		Стоимость = Опер.ПервоначальнаяСтоимость;
		
	Иначе
		Стоимость =?(Опер.РасчетПоБазовойСтоимости = Истина,  Опер.ПервоначальнаяСтоимость - Опер.АмортизацииДляБазовойСтоимости, Опер.ОстаточнаяСтоимость);
	КонецЕсли;
	
	КоличествоМесяцевПолезногоИспользованияНаНач = 0;
	ДатаПринятияКУчету = Опер.ДатаПринятияКУчету;
	
	Если НЕ ЗначениеЗаполнено(ДатаПринятияКУчету) Тогда
		КоличествоМесяцевПолезногоИспользованияНаНач = ?(Опер.СрокПолезногоИспользования = NUll, 0, Опер.СрокПолезногоИспользования);
	Иначе
		ТекДата = КонецМесяца(ДобавитьМесяц(ДатаПринятияКУчету, 1));
		ТекДатаНач = КонецМесяца(Опер.ПериодДействия);
		Пока ТекДата < ТекДатаНач Цикл
			КоличествоМесяцевПолезногоИспользованияНаНач = КоличествоМесяцевПолезногоИспользованияНаНач + 1;
			ТекДата = КонецМесяца(ДобавитьМесяц(ТекДата, 1));
		КонецЦикла;
	КонецЕсли;
	
	Способ = СпособыРаспределенияРасходов.Найти(Опер.СпособОтраженияРасходов,"Способ");
	Если Способ=Неопределено тогда
		ПрочиеРасходы = Опер.Амортизация;
		ПрямыеРасходы = 0;
		КосвенныеРасходы = 0;
	Иначе
		БазисРаспределения = Новый Массив(3);
		БазисРаспределения[0] = Способ.ПрямыеЗатраты;
		БазисРаспределения[1] = Способ.КосвенныеЗатраты;
		БазисРаспределения[2] = Способ.ПрочиеЗатраты;
		Распределение = ОбщегоНазначения.РаспределитьПропорционально(Опер.Амортизация,БазисРаспределения);
		Если Распределение=Неопределено тогда
			ПрочиеРасходы = Опер.Амортизация;
			ПрямыеРасходы = 0;
			КосвенныеРасходы = 0;
		Иначе
			 ПрямыеРасходы	= Распределение[0];
			 КосвенныеРасходы	= Распределение[1];
			 ПрочиеРасходы	= Распределение[2];
		КонецЕсли;
	КонецЕсли;
	СуммаАмортизацииПрямые = СуммаАмортизацииПрямые + ПрямыеРасходы;
	СуммаАмортизацииКосвенные = СуммаАмортизацииКосвенные + КосвенныеРасходы;
	СуммаАмортизацииПрочие = СуммаАмортизацииПрочие + ПрочиеРасходы;
	
	
	Строка3.Параметры.Прямые	= ПрямыеРасходы;
	Строка3.Параметры.Косвенные	= КосвенныеРасходы;
	Строка3.Параметры.Прочие	= ПрочиеРасходы;
	
	Строка3.Параметры.Стоимость = Стоимость;
	Строка3.Параметры.ПолезныйСрок = макс(0,?(Опер.СрокПолезногоИспользования = NULL, 0, Опер.СрокПолезногоИспользования) - КоличествоМесяцевПолезногоИспользованияНаНач);
	Строка3.Параметры.СуммаАмортизации = ?(Опер.Амортизация = NULL, 0, Опер.Амортизация);

	ДокументРезультат.Вывести(Строка1);
	Если Не ГруппироватьПоОбъектам Тогда
		ДокументРезультат.Присоединить(Строка2);
	КонецЕсли;
	ДокументРезультат.Присоединить(Строка3);
	
	ИтогСуммаАмортизации = ИтогСуммаАмортизации + Опер.Амортизация;
	
КонецПроцедуры

// Выполняет запрос и формирует табличный документ-результат отчета
// в соответствии с настройками, заданными значениями реквизитов отчета.
//
// Параметры:
//	ДокументРезультат - табличный документ, формируемый отчетом
//	ПоказыватьЗаголовок - признак видимости строк с заголовком отчета
//	ВысотаЗаголовка - параметр, через который возвращается высота заголовка в строках 
//
Процедура СформироватьОтчет(ДокументРезультат, ПоказыватьЗаголовок, ВысотаЗаголовка, ТолькоЗаголовок = Ложь) Экспорт

	ДокументРезультат.Очистить();

	Макет = ПолучитьМакет("Отчет");

	Если ГруппироватьПоОбъектам Тогда
		Заголовок1 = Макет.ПолучитьОбласть("Заголовок|Общая1");
		Заголовок2 = Макет.ПолучитьОбласть("Заголовок|Общая2");
		
	Иначе
		Заголовок1 = Макет.ПолучитьОбласть("Заголовок");
	КонецЕсли;
	
	Заголовок1.Параметры.НачалоПериода       = Формат(ДатаНач, "ДФ=dd.MM.yyyy");
	Заголовок1.Параметры.КонецПериода        = Формат(ДатаКон, "ДФ=dd.MM.yyyy");
	Заголовок1.Параметры.НазваниеОрганизации = Организация.НаименованиеПолное;
	Заголовок1.Параметры.ИННОрганизации      = "" + Организация.ИНН + "/" + Организация.КПП;
	Заголовок1.Параметры.НазваниеАмортизационнойГруппы = ?(НЕ ЗначениеЗаполнено(АмортизационнаяГруппа), "по всем", АмортизационнаяГруппа);
	
	Если ГруппироватьПоОбъектам Тогда
		ДокументРезультат.Вывести(Заголовок1);
		ДокументРезультат.Присоединить(Заголовок2);
		
	Иначе
		ДокументРезультат.Вывести(Заголовок1);
	КонецЕсли;

	// Параметр для показа заголовка
	ВысотаЗаголовка = ДокументРезультат.ВысотаТаблицы;

	// Когда нужен только заголовок:
	Если ТолькоЗаголовок Тогда
		Возврат;
	КонецЕсли;

	Если ЗначениеЗаполнено(ВысотаЗаголовка) Тогда
		ДокументРезультат.Область("R1:R" + ВысотаЗаголовка).Видимость = ПоказыватьЗаголовок;
	КонецЕсли;
	
	СуммаАмортизацииПрямые = 0;
	СуммаАмортизацииКосвенные = 0;
	СуммаАмортизацииПрочие = 0;
	// Шапка
	Если ГруппироватьПоОбъектам Тогда

		Шапка1 = Макет.ПолучитьОбласть("Шапка|Общая1");
		Шапка2 = Макет.ПолучитьОбласть("Шапка|Общая2");
		Шапка2.Параметры.НК2 = 2;
		Шапка2.Параметры.НК3 = 3;
		Шапка2.Параметры.НК4 = 4;
		Шапка2.Параметры.НК5 = 5;
		Шапка2.Параметры.НК6 = 6;

		ДокументРезультат.Вывести(Шапка1);
		ДокументРезультат.Присоединить(Шапка2);

	Иначе
		Шапка = Макет.ПолучитьОбласть("Шапка");
		Шапка.Параметры.НК2 = 3;
		Шапка.Параметры.НК3 = 4;
		Шапка.Параметры.НК4 = 5;
		Шапка.Параметры.НК5 = 6;
		Шапка.Параметры.НК6 = 7;
		ДокументРезультат.Вывести(Шапка);
	КонецЕсли;

	ТекстЗапроса =
	"ВЫБРАТЬ
	|	НалоговыйОбороты.регистратор,
	|	НалоговыйОбороты.СуммаОборотКт,
	|	НалоговыйОбороты.Период,
	|	НалоговыйОбороты.Субконто1 КАК ОсновноеСредство
	|ПОМЕСТИТЬ ХозрасчетныйОбороты
	|ИЗ
	|	РегистрБухгалтерии.Налоговый.Обороты(
	|			&ДатаНач,
	|			&ДатаКон,
	|			регистратор,
	|			Счет В ИЕРАРХИИ (&Счет02)
	|				И (НЕ Счет = &Счет09),
	|			&ВидСубконто,
	|			Организация = &Организация
	|				И ВидУчета = &ВидУчета
	|				И КорСубконто1.ВидРасходовНУ = &Амортизация,
	|			,
	|			&ВидКорСубконто) КАК НалоговыйОбороты
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ХозрасчетныйОбороты.ОсновноеСредство,
	|	ХозрасчетныйОбороты.Период,
	|	МАКСИМУМ(ПоБазовойСтоимости.Период) КАК Период1,
	|	МАКСИМУМ(ПоБазовойСтоимости.ПризнакНачисленияПоБазовойСтоимости) КАК ПризнакНачисленияПоБазовойСтоимости,
	|	МАКСИМУМ(ПоБазовойСтоимости.СуммаНакопленнойАмортизации) КАК СуммаНакопленнойАмортизации
	|ПОМЕСТИТЬ ПоБазовойСтоимости
	|ИЗ
	|	ХозрасчетныйОбороты КАК ХозрасчетныйОбороты
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.НачислениеАмортизацииОСПоБазовойСтоимостиНалоговыйУчет КАК ПоБазовойСтоимости
	|		ПО (НАЧАЛОПЕРИОДА(ХозрасчетныйОбороты.Период, МЕСЯЦ) > ПоБазовойСтоимости.Период)
	|			И ХозрасчетныйОбороты.ОсновноеСредство = ПоБазовойСтоимости.ОсновноеСредство
	|ГДЕ
	|	ПоБазовойСтоимости.Организация = &Организация
	|
	|СГРУППИРОВАТЬ ПО
	|	ХозрасчетныйОбороты.Период,
	|	ХозрасчетныйОбороты.ОсновноеСредство
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ХозрасчетныйОбороты.ОсновноеСредство,
	|	ХозрасчетныйОбороты.Период,
	|	МАКСИМУМ(ПервоначальныеСведения.Период) КАК Период1,
	|	МАКСИМУМ(ПервоначальныеСведения.МетодНачисленияАмортизации) КАК МетодНачисленияАмортизации,
	|	МАКСИМУМ(ПервоначальныеСведения.ПервоначальнаяСтоимостьНУ) КАК ПервоначальнаяСтоимостьНУ
	|ПОМЕСТИТЬ ПервоначальныеСведения
	|ИЗ
	|	ХозрасчетныйОбороты КАК ХозрасчетныйОбороты
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ПервоначальныеСведенияОСНалоговыйУчет КАК ПервоначальныеСведения
	|		ПО (НАЧАЛОПЕРИОДА(ХозрасчетныйОбороты.Период, МЕСЯЦ) > ПервоначальныеСведения.Период)
	|			И ХозрасчетныйОбороты.ОсновноеСредство = ПервоначальныеСведения.ОсновноеСредство
	|ГДЕ
	|	ПервоначальныеСведения.Организация = &Организация
	|
	|СГРУППИРОВАТЬ ПО
	|	ХозрасчетныйОбороты.Период,
	|	ХозрасчетныйОбороты.ОсновноеСредство
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОсновныеСредства.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ОсновныеСредстваНеЛин
	|ИЗ
	|	Справочник.ОсновныеСредства КАК ОсновныеСредства
	|ГДЕ
	|	(ОсновныеСредства.АмортизационнаяГруппа В (ЗНАЧЕНИЕ(Перечисление.АмортизационныеГруппы.ВосьмаяГруппа), ЗНАЧЕНИЕ(Перечисление.АмортизационныеГруппы.ДевятаяГруппа), ЗНАЧЕНИЕ(Перечисление.АмортизационныеГруппы.ДесятаяГруппа))
	|				И ОсновныеСредства.ГруппаОС В (ЗНАЧЕНИЕ(Перечисление.ГруппыОС.Здания), ЗНАЧЕНИЕ(Перечисление.ГруппыОС.Сооружения))
	|			ИЛИ ОсновныеСредства.АмортизационнаяГруппа = ЗНАЧЕНИЕ(Перечисление.АмортизационныеГруппы.ОтдельнаяГруппа))
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	АмортизацияОСПриПереходеКЛинейномуМетоду.Субконто1,
	|	АмортизацияОСПриПереходеКЛинейномуМетоду.СуммаОстаток
	|ПОМЕСТИТЬ АмортизацияОСПриПереходеКЛинейномуМетоду
	|ИЗ
	|	РегистрБухгалтерии.Налоговый.Остатки(
	|			&ДатаПереходаКЛинейномуМетоду,
	|			Счет В ИЕРАРХИИ (&Счет01)
	|				И (НЕ Счет = &Счет09),
	|			ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.ОсновныеСредства),
	|			Организация = &Организация
	|				И ВидУчета = ЗНАЧЕНИЕ(Перечисление.ВидыУчетаПоПБУ18.НУ)
	|				И (НЕ Субконто1 В
	|						(ВЫБРАТЬ
	|							ОсновныеСредстваНеЛин.Ссылка
	|						ИЗ
	|							ОсновныеСредстваНеЛин))
	|				И Субконто1 В
	|					(ВЫБРАТЬ
	|						ПервоначальныеСведения.ОсновноеСредство
	|					ИЗ
	|						ПервоначальныеСведения)) КАК АмортизацияОСПриПереходеКЛинейномуМетоду
	//|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПервоначальныеСведения КАК ПервоначальныеСведения
	//|		ПО (ПервоначальныеСведения.ОсновноеСредство = АмортизацияОСПриПереходеКЛинейномуМетоду.Субконто1)
	//|			И (ВЫБОР
	//|				КОГДА ГОД(&ДатаПереходаКЛинейномуМетоду) = 2009
	//|					ТОГДА ПервоначальныеСведения.МетодНачисленияАмортизации = ЗНАЧЕНИЕ(Перечисление.МетодыНачисленияАмортизации.Нелинейный)
	//|				ИНАЧЕ ИСТИНА
	//|			КОНЕЦ)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ХозрасчетныйОбороты.ОсновноеСредство,
	|	ХозрасчетныйОбороты.Период,
	|	МАКСИМУМ(Коэффициент.Период) КАК Период1,
	|	МАКСИМУМ(Коэффициент.СпециальныйКоэффициент) КАК СпециальныйКоэффициент
	|ПОМЕСТИТЬ Коэффициент
	|ИЗ
	|	ХозрасчетныйОбороты КАК ХозрасчетныйОбороты
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.НачислениеАмортизацииОССпециальныйКоэффициентНалоговыйУчет КАК Коэффициент
	|		ПО (НАЧАЛОПЕРИОДА(ХозрасчетныйОбороты.Период, МЕСЯЦ) > Коэффициент.Период)
	|			И ХозрасчетныйОбороты.ОсновноеСредство = Коэффициент.ОсновноеСредство
	|ГДЕ
	|	Коэффициент.Организация = &Организация
	|
	|СГРУППИРОВАТЬ ПО
	|	ХозрасчетныйОбороты.Период,
	|	ХозрасчетныйОбороты.ОсновноеСредство
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ХозрасчетныйОбороты.ОсновноеСредство,
	|	ХозрасчетныйОбороты.Период,
	|	МИНИМУМ(СостояниеОС.ДатаСостояния) КАК ДатаСостояния
	|ПОМЕСТИТЬ СостояниеОС
	|ИЗ
	|	ХозрасчетныйОбороты КАК ХозрасчетныйОбороты
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СостоянияОСОрганизаций КАК СостояниеОС
	|		ПО (НАЧАЛОПЕРИОДА(ХозрасчетныйОбороты.Период, МЕСЯЦ) > СостояниеОС.ДатаСостояния)
	|			И ХозрасчетныйОбороты.ОсновноеСредство = СостояниеОС.ОсновноеСредство
	|ГДЕ
	|	СостояниеОС.Организация = &Организация
	|	И СостояниеОС.Состояние = &ПринятоКУчету
	|
	|СГРУППИРОВАТЬ ПО
	|	ХозрасчетныйОбороты.Период,
	|	ХозрасчетныйОбороты.ОсновноеСредство
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ХозрасчетныйОбороты.ОсновноеСредство,
	|	ХозрасчетныйОбороты.Период,
	|	МАКСИМУМ(СпособОтражения.Период) КАК Период1,
	|	МАКСИМУМ(СпособОтражения.СпособыОтраженияРасходовПоАмортизации) КАК СпособыОтраженияРасходовПоАмортизации
	|ПОМЕСТИТЬ СпособОтражения
	|ИЗ
	|	ХозрасчетныйОбороты КАК ХозрасчетныйОбороты
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СпособыОтраженияРасходовПоАмортизацииОСНалоговыйУчет КАК СпособОтражения
	|		ПО (НАЧАЛОПЕРИОДА(ХозрасчетныйОбороты.Период, МЕСЯЦ) > СпособОтражения.Период)
	|			И ХозрасчетныйОбороты.ОсновноеСредство = СпособОтражения.ОсновноеСредство
	|ГДЕ
	|	СпособОтражения.Организация = &Организация
	|
	|СГРУППИРОВАТЬ ПО
	|	ХозрасчетныйОбороты.Период,
	|	ХозрасчетныйОбороты.ОсновноеСредство
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ХозрасчетныйОбороты.ОсновноеСредство,
	|	ХозрасчетныйОбороты.Период,
	|	МАКСИМУМ(Параметры.Период) КАК Период1,
	|	МАКСИМУМ(Параметры.СрокПолезногоИспользования) КАК СрокПолезногоИспользования
	|ПОМЕСТИТЬ Параметры
	|ИЗ
	|	ХозрасчетныйОбороты КАК ХозрасчетныйОбороты
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ПараметрыАмортизацииОСНалоговыйУчет КАК Параметры
	|		ПО (НАЧАЛОПЕРИОДА(ХозрасчетныйОбороты.Период, МЕСЯЦ) > Параметры.Период)
	|			И ХозрасчетныйОбороты.ОсновноеСредство = Параметры.ОсновноеСредство
	|ГДЕ
	|	Параметры.Организация = &Организация
	|
	|СГРУППИРОВАТЬ ПО
	|	ХозрасчетныйОбороты.Период,
	|	ХозрасчетныйОбороты.ОсновноеСредство
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ХозрасчетныйОбороты.Период,
	|	МАКСИМУМ(УчетнаяПолитикаОрганизаций.МетодНачисленияАмортизацииНУ) КАК МетодНачисленияАмортизацииНУ
	|ПОМЕСТИТЬ УчетнаяПолитикаОрганизаций
	|ИЗ
	|	ХозрасчетныйОбороты КАК ХозрасчетныйОбороты
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.УчетнаяПолитикаОрганизаций КАК УчетнаяПолитикаОрганизаций
	|		ПО (НАЧАЛОПЕРИОДА(ХозрасчетныйОбороты.Период, МЕСЯЦ) >= УчетнаяПолитикаОрганизаций.Период)
	|ГДЕ
	|	УчетнаяПолитикаОрганизаций.Организация = &Организация
	|СГРУППИРОВАТЬ ПО
	|	ХозрасчетныйОбороты.Период
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НалоговыйОстаткиИОбороты.регистратор КАК регистратор,
	|	НалоговыйОстаткиИОбороты.Субконто1 КАК ОсновноеСредство,
	|	НалоговыйОстаткиИОбороты.СуммаНачальныйОстатокДт,
	|	НалоговыйОстаткиИОбороты.СуммаНачальныйОстаток
	|ПОМЕСТИТЬ НалоговыйОстаткиИОбороты
	|ИЗ
	|	РегистрБухгалтерии.Налоговый.ОстаткиИОбороты(
	|			&ДатаНач,
	|			&ДатаКон,
	|			регистратор,
	|			,
	|			Счет В ИЕРАРХИИ (&Счет01)
	|				И (НЕ Счет = &Счет09),
	|			&ВидСубконто,
	|			Организация = &Организация
	|				И ВидУчета = &ВидУчета) КАК НалоговыйОстаткиИОбороты
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НалоговыйОбороты.регистратор,
	|	НалоговыйОбороты.СуммаОборотКт,
	|	НалоговыйОбороты.Период,
	|	НалоговыйОбороты.Субконто1 КАК ОсновноеСредство
	|ПОМЕСТИТЬ ХозрасчетныйОбороты2
	|ИЗ
	|	РегистрБухгалтерии.Налоговый.Обороты(
	|			&ДатаНач,
	|			&ДатаКон,
	|			регистратор,
	|			Счет В ИЕРАРХИИ (&Счет02)
	|				И (НЕ Счет = &Счет09),
	|			&ВидСубконто,
	|			Организация = &Организация
	|				И ВидУчета = &ВидУчета,
	|			,
	|			&ВидКорСубконто91) КАК НалоговыйОбороты
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ХозрасчетныйОбороты2.ОсновноеСредство,
	|	ХозрасчетныйОбороты2.Период,
	|	МАКСИМУМ(ПоБазовойСтоимости.Период) КАК Период1,
	|	МАКСИМУМ(ПоБазовойСтоимости.ПризнакНачисленияПоБазовойСтоимости) КАК ПризнакНачисленияПоБазовойСтоимости,
	|	МАКСИМУМ(ПоБазовойСтоимости.СуммаНакопленнойАмортизации) КАК СуммаНакопленнойАмортизации
	|ПОМЕСТИТЬ ПоБазовойСтоимости2
	|ИЗ
	|	ХозрасчетныйОбороты2 КАК ХозрасчетныйОбороты2
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.НачислениеАмортизацииОСПоБазовойСтоимостиНалоговыйУчет КАК ПоБазовойСтоимости
	|		ПО (НАЧАЛОПЕРИОДА(ХозрасчетныйОбороты2.Период, МЕСЯЦ) > ПоБазовойСтоимости.Период)
	|			И ХозрасчетныйОбороты2.ОсновноеСредство = ПоБазовойСтоимости.ОсновноеСредство
	|ГДЕ
	|	ПоБазовойСтоимости.Организация = &Организация
	|
	|СГРУППИРОВАТЬ ПО
	|	ХозрасчетныйОбороты2.Период,
	|	ХозрасчетныйОбороты2.ОсновноеСредство
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ХозрасчетныйОбороты2.ОсновноеСредство,
	|	ХозрасчетныйОбороты2.Период,
	|	МАКСИМУМ(ПервоначальныеСведения.Период) КАК Период1,
	|	МАКСИМУМ(ПервоначальныеСведения.МетодНачисленияАмортизации) КАК МетодНачисленияАмортизации,
	|	МАКСИМУМ(ПервоначальныеСведения.ПервоначальнаяСтоимостьНУ) КАК ПервоначальнаяСтоимостьНУ
	|ПОМЕСТИТЬ ПервоначальныеСведения2
	|ИЗ
	|	ХозрасчетныйОбороты2 КАК ХозрасчетныйОбороты2
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ПервоначальныеСведенияОСНалоговыйУчет КАК ПервоначальныеСведения
	|		ПО (НАЧАЛОПЕРИОДА(ХозрасчетныйОбороты2.Период, МЕСЯЦ) > ПервоначальныеСведения.Период)
	|			И ХозрасчетныйОбороты2.ОсновноеСредство = ПервоначальныеСведения.ОсновноеСредство
	|ГДЕ
	|	ПервоначальныеСведения.Организация = &Организация
	|
	|СГРУППИРОВАТЬ ПО
	|	ХозрасчетныйОбороты2.Период,
	|	ХозрасчетныйОбороты2.ОсновноеСредство
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	АмортизацияОСПриПереходеКЛинейномуМетоду.Субконто1,
	|	АмортизацияОСПриПереходеКЛинейномуМетоду.СуммаОстаток
	|ПОМЕСТИТЬ АмортизацияОСПриПереходеКЛинейномуМетоду2
	|ИЗ
	|	РегистрБухгалтерии.Налоговый.Остатки(
	|			&ДатаПереходаКЛинейномуМетоду,
	|			Счет В ИЕРАРХИИ (&Счет01)
	|				И (НЕ Счет = &Счет09),
	|			ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.ОсновныеСредства),
	|			Организация = &Организация
	|				И ВидУчета = ЗНАЧЕНИЕ(Перечисление.ВидыУчетаПоПБУ18.НУ)
	|				И (НЕ Субконто1 В
	|						(ВЫБРАТЬ
	|							ОсновныеСредстваНеЛин.Ссылка
	|						ИЗ
	|							ОсновныеСредстваНеЛин))
	|				И Субконто1 В
	|					(ВЫБРАТЬ
	|						ПервоначальныеСведения2.ОсновноеСредство
	|					ИЗ
	|						ПервоначальныеСведения2)) КАК АмортизацияОСПриПереходеКЛинейномуМетоду
	//|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПервоначальныеСведения2 КАК ПервоначальныеСведения2
	//|		ПО (ПервоначальныеСведения2.ОсновноеСредство = АмортизацияОСПриПереходеКЛинейномуМетоду.Субконто1)
	//|			И (ВЫБОР
	//|				КОГДА ГОД(&ДатаПереходаКЛинейномуМетоду) = 2009
	//|					ТОГДА ПервоначальныеСведения2.МетодНачисленияАмортизации = ЗНАЧЕНИЕ(Перечисление.МетодыНачисленияАмортизации.Нелинейный)
	//|				ИНАЧЕ ИСТИНА
	//|			КОНЕЦ)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ХозрасчетныйОбороты2.ОсновноеСредство,
	|	ХозрасчетныйОбороты2.Период,
	|	МАКСИМУМ(Коэффициент.Период) КАК Период1,
	|	МАКСИМУМ(Коэффициент.СпециальныйКоэффициент) КАК СпециальныйКоэффициент
	|ПОМЕСТИТЬ Коэффициент2
	|ИЗ
	|	ХозрасчетныйОбороты2 КАК ХозрасчетныйОбороты2
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.НачислениеАмортизацииОССпециальныйКоэффициентНалоговыйУчет КАК Коэффициент
	|		ПО (НАЧАЛОПЕРИОДА(ХозрасчетныйОбороты2.Период, МЕСЯЦ) > Коэффициент.Период)
	|			И ХозрасчетныйОбороты2.ОсновноеСредство = Коэффициент.ОсновноеСредство
	|ГДЕ
	|	Коэффициент.Организация = &Организация
	|
	|СГРУППИРОВАТЬ ПО
	|	ХозрасчетныйОбороты2.Период,
	|	ХозрасчетныйОбороты2.ОсновноеСредство
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ХозрасчетныйОбороты2.ОсновноеСредство,
	|	ХозрасчетныйОбороты2.Период,
	|	МИНИМУМ(СостояниеОС.ДатаСостояния) КАК ДатаСостояния
	|ПОМЕСТИТЬ СостояниеОС2
	|ИЗ
	|	ХозрасчетныйОбороты2 КАК ХозрасчетныйОбороты2
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СостоянияОСОрганизаций КАК СостояниеОС
	|		ПО (НАЧАЛОПЕРИОДА(ХозрасчетныйОбороты2.Период, МЕСЯЦ) > СостояниеОС.ДатаСостояния)
	|			И ХозрасчетныйОбороты2.ОсновноеСредство = СостояниеОС.ОсновноеСредство
	|ГДЕ
	|	СостояниеОС.Организация = &Организация
	|	И СостояниеОС.Состояние = &ПринятоКУчету
	|
	|СГРУППИРОВАТЬ ПО
	|	ХозрасчетныйОбороты2.Период,
	|	ХозрасчетныйОбороты2.ОсновноеСредство
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ХозрасчетныйОбороты2.ОсновноеСредство,
	|	ХозрасчетныйОбороты2.Период,
	|	МАКСИМУМ(СпособОтражения.Период) КАК Период1,
	|	МАКСИМУМ(СпособОтражения.СпособыОтраженияРасходовПоАмортизации) КАК СпособыОтраженияРасходовПоАмортизации
	|ПОМЕСТИТЬ СпособОтражения2
	|ИЗ
	|	ХозрасчетныйОбороты2 КАК ХозрасчетныйОбороты2
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СпособыОтраженияРасходовПоАмортизацииОСНалоговыйУчет КАК СпособОтражения
	|		ПО (НАЧАЛОПЕРИОДА(ХозрасчетныйОбороты2.Период, МЕСЯЦ) > СпособОтражения.Период)
	|			И ХозрасчетныйОбороты2.ОсновноеСредство = СпособОтражения.ОсновноеСредство
	|ГДЕ
	|	СпособОтражения.Организация = &Организация
	|
	|СГРУППИРОВАТЬ ПО
	|	ХозрасчетныйОбороты2.Период,
	|	ХозрасчетныйОбороты2.ОсновноеСредство
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ХозрасчетныйОбороты2.ОсновноеСредство,
	|	ХозрасчетныйОбороты2.Период,
	|	МАКСИМУМ(Параметры.Период) КАК Период1,
	|	МАКСИМУМ(Параметры.СрокПолезногоИспользования) КАК СрокПолезногоИспользования
	|ПОМЕСТИТЬ Параметры2
	|ИЗ
	|	ХозрасчетныйОбороты2 КАК ХозрасчетныйОбороты2
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ПараметрыАмортизацииОСНалоговыйУчет КАК Параметры
	|		ПО (НАЧАЛОПЕРИОДА(ХозрасчетныйОбороты2.Период, МЕСЯЦ) > Параметры.Период)
	|			И ХозрасчетныйОбороты2.ОсновноеСредство = Параметры.ОсновноеСредство
	|ГДЕ
	|	Параметры.Организация = &Организация
	|
	|СГРУППИРОВАТЬ ПО
	|	ХозрасчетныйОбороты2.Период,
	|	ХозрасчетныйОбороты2.ОсновноеСредство
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ХозрасчетныйОбороты2.Период,
	|	МАКСИМУМ(УчетнаяПолитикаОрганизаций.МетодНачисленияАмортизацииНУ) КАК МетодНачисленияАмортизацииНУ
	|ПОМЕСТИТЬ УчетнаяПолитикаОрганизаций2
	|ИЗ
	|	ХозрасчетныйОбороты2 КАК ХозрасчетныйОбороты2
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.УчетнаяПолитикаОрганизаций КАК УчетнаяПолитикаОрганизаций
	|		ПО (НАЧАЛОПЕРИОДА(ХозрасчетныйОбороты2.Период, МЕСЯЦ) >= УчетнаяПолитикаОрганизаций.Период)
	|ГДЕ
	|	УчетнаяПолитикаОрганизаций.Организация = &Организация
	|СГРУППИРОВАТЬ ПО
	|	ХозрасчетныйОбороты2.Период
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЕСТЬNULL(ТаблицаАмортизации.СуммаОборотКт, 0) КАК Амортизация,
	|	ТаблицаАмортизации.Период КАК ПериодДействия,
	|	ВЫБОР
	|		КОГДА ГОД(ТаблицаАмортизации.Период) < 2009
	|			ТОГДА ПервоначальныеСведения.МетодНачисленияАмортизации
	|		КОГДА ГОД(ТаблицаАмортизации.Период) >= 2009
	|					И ТаблицаАмортизации.ОсновноеСредство.АмортизационнаяГруппа В (ЗНАЧЕНИЕ(Перечисление.АмортизационныеГруппы.ВосьмаяГруппа), ЗНАЧЕНИЕ(Перечисление.АмортизационныеГруппы.ДевятаяГруппа), ЗНАЧЕНИЕ(Перечисление.АмортизационныеГруппы.ДесятаяГруппа))
	|					И ТаблицаАмортизации.ОсновноеСредство.ГруппаОС В (ЗНАЧЕНИЕ(Перечисление.ГруппыОС.Здания), ЗНАЧЕНИЕ(Перечисление.ГруппыОС.Сооружения))
	|				ИЛИ ТаблицаАмортизации.ОсновноеСредство.АмортизационнаяГруппа = ЗНАЧЕНИЕ(Перечисление.АмортизационныеГруппы.ОтдельнаяГруппа)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.МетодыНачисленияАмортизации.Линейный)
	|		ИНАЧЕ УчетнаяПолитикаОрганизаций.МетодНачисленияАмортизацииНУ
	|	КОНЕЦ КАК МетодНачисленияАмортизации,
	|	ЕСТЬNULL(АмортизацияОСПриПереходеКЛинейномуМетоду.СуммаОстаток, ПервоначальныеСведения.ПервоначальнаяСтоимостьНУ) КАК ПервоначальнаяСтоимость,
	|	Коэффициент.СпециальныйКоэффициент,
	|	ВЫБОР
	|		КОГДА СостояниеОС.ДатаСостояния < &ДатаНачалаНУ
	|			ТОГДА &ДатаНачалаНУ
	|		ИНАЧЕ СостояниеОС.ДатаСостояния
	|	КОНЕЦ КАК ДатаПринятияКУчету,
	|	СпособОтражения.СпособыОтраженияРасходовПоАмортизации КАК СпособОтраженияРасходов,
	|	ПоБазовойСтоимости.ПризнакНачисленияПоБазовойСтоимости КАК РасчетПоБазовойСтоимости,
	|	ПоБазовойСтоимости.СуммаНакопленнойАмортизации КАК АмортизацииДляБазовойСтоимости,
	|	ТаблицаАмортизации.ОсновноеСредство КАК ОсновноеСредство,
	|	ЕСТЬNULL(Параметры.СрокПолезногоИспользования, 0) КАК СрокПолезногоИспользования,
	|	НалоговыйОстаткиИОбороты.СуммаНачальныйОстатокДт КАК ПервоначальнаяСтоимостьДляАмортизации,
	|	НалоговыйОстаткиИОбороты.СуммаНачальныйОстаток КАК ОстаточнаяСтоимость
	|ПОМЕСТИТЬ РегистрАмортизацииОС
	|ИЗ
	|	ХозрасчетныйОбороты КАК ТаблицаАмортизации
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПоБазовойСтоимости КАК ПоБазовойСтоимости
	|		ПО ТаблицаАмортизации.ОсновноеСредство = ПоБазовойСтоимости.ОсновноеСредство
	|			И ТаблицаАмортизации.Период = ПоБазовойСтоимости.Период
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПервоначальныеСведения КАК ПервоначальныеСведения
	|		ПО ТаблицаАмортизации.ОсновноеСредство = ПервоначальныеСведения.ОсновноеСредство
	|			И ТаблицаАмортизации.Период = ПервоначальныеСведения.Период
	|			ЛЕВОЕ СОЕДИНЕНИЕ АмортизацияОСПриПереходеКЛинейномуМетоду КАК АмортизацияОСПриПереходеКЛинейномуМетоду
	|			ПО (ПервоначальныеСведения.ОсновноеСредство = АмортизацияОСПриПереходеКЛинейномуМетоду.Субконто1)
	|				И (ВЫБОР
	|					КОГДА ГОД(&ДатаПереходаКЛинейномуМетоду) = 2009
	|						ТОГДА ПервоначальныеСведения.МетодНачисленияАмортизации = ЗНАЧЕНИЕ(Перечисление.МетодыНачисленияАмортизации.Нелинейный)
	|					ИНАЧЕ ИСТИНА
	|				КОНЕЦ)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Коэффициент КАК Коэффициент
	|		ПО ТаблицаАмортизации.ОсновноеСредство = Коэффициент.ОсновноеСредство
	|			И ТаблицаАмортизации.Период = Коэффициент.Период
	|		ЛЕВОЕ СОЕДИНЕНИЕ СостояниеОС КАК СостояниеОС
	|		ПО ТаблицаАмортизации.ОсновноеСредство = СостояниеОС.ОсновноеСредство
	|			И ТаблицаАмортизации.Период = СостояниеОС.Период
	|		ЛЕВОЕ СОЕДИНЕНИЕ СпособОтражения КАК СпособОтражения
	|		ПО ТаблицаАмортизации.ОсновноеСредство = СпособОтражения.ОсновноеСредство
	|			И ТаблицаАмортизации.Период = СпособОтражения.Период
	|		ЛЕВОЕ СОЕДИНЕНИЕ Параметры КАК Параметры
	|		ПО ТаблицаАмортизации.ОсновноеСредство = Параметры.ОсновноеСредство
	|			И ТаблицаАмортизации.Период = Параметры.Период
	|		ЛЕВОЕ СОЕДИНЕНИЕ УчетнаяПолитикаОрганизаций КАК УчетнаяПолитикаОрганизаций
	|		ПО ТаблицаАмортизации.Период = УчетнаяПолитикаОрганизаций.Период
	|		ЛЕВОЕ СОЕДИНЕНИЕ НалоговыйОстаткиИОбороты КАК НалоговыйОстаткиИОбороты
	|		ПО ТаблицаАмортизации.ОсновноеСредство = НалоговыйОстаткиИОбороты.ОсновноеСредство
	|			И ТаблицаАмортизации.Регистратор = НалоговыйОстаткиИОбороты.Регистратор
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ЕСТЬNULL(ТаблицаАмортизации.СуммаОборотКт, 0),
	|	ТаблицаАмортизации.Период,
	|	ВЫБОР
	|		КОГДА ГОД(ТаблицаАмортизации.Период) < 2009
	|			ТОГДА ПервоначальныеСведения.МетодНачисленияАмортизации
	|		КОГДА ГОД(ТаблицаАмортизации.Период) >= 2009
	|					И ТаблицаАмортизации.ОсновноеСредство.АмортизационнаяГруппа В (ЗНАЧЕНИЕ(Перечисление.АмортизационныеГруппы.ВосьмаяГруппа), ЗНАЧЕНИЕ(Перечисление.АмортизационныеГруппы.ДевятаяГруппа), ЗНАЧЕНИЕ(Перечисление.АмортизационныеГруппы.ДесятаяГруппа))
	|					И ТаблицаАмортизации.ОсновноеСредство.ГруппаОС В (ЗНАЧЕНИЕ(Перечисление.ГруппыОС.Здания), ЗНАЧЕНИЕ(Перечисление.ГруппыОС.Сооружения))
	|				ИЛИ ТаблицаАмортизации.ОсновноеСредство.АмортизационнаяГруппа = ЗНАЧЕНИЕ(Перечисление.АмортизационныеГруппы.ОтдельнаяГруппа)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.МетодыНачисленияАмортизации.Линейный)
	|		ИНАЧЕ УчетнаяПолитикаОрганизаций.МетодНачисленияАмортизацииНУ
	|	КОНЕЦ,
	|	ЕСТЬNULL(АмортизацияОСПриПереходеКЛинейномуМетоду.СуммаОстаток, ПервоначальныеСведения.ПервоначальнаяСтоимостьНУ),
	|	Коэффициент.СпециальныйКоэффициент,
	|	ВЫБОР
	|		КОГДА СостояниеОС.ДатаСостояния < &ДатаНачалаНУ
	|			ТОГДА &ДатаНачалаНУ
	|		ИНАЧЕ СостояниеОС.ДатаСостояния
	|	КОНЕЦ,
	|	СпособОтражения.СпособыОтраженияРасходовПоАмортизации,
	|	ПоБазовойСтоимости.ПризнакНачисленияПоБазовойСтоимости,
	|	ПоБазовойСтоимости.СуммаНакопленнойАмортизации,
	|	ТаблицаАмортизации.ОсновноеСредство,
	|	ЕСТЬNULL(Параметры.СрокПолезногоИспользования, 0),
	|	НалоговыйОстаткиИОбороты.СуммаНачальныйОстатокДт,
	|	НалоговыйОстаткиИОбороты.СуммаНачальныйОстаток
	|ИЗ
	|	ХозрасчетныйОбороты2 КАК ТаблицаАмортизации
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПоБазовойСтоимости2 КАК ПоБазовойСтоимости
	|		ПО ТаблицаАмортизации.ОсновноеСредство = ПоБазовойСтоимости.ОсновноеСредство
	|			И ТаблицаАмортизации.Период = ПоБазовойСтоимости.Период
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПервоначальныеСведения2 КАК ПервоначальныеСведения
	|		ПО ТаблицаАмортизации.ОсновноеСредство = ПервоначальныеСведения.ОсновноеСредство
	|			И ТаблицаАмортизации.Период = ПервоначальныеСведения.Период
	|		ЛЕВОЕ СОЕДИНЕНИЕ АмортизацияОСПриПереходеКЛинейномуМетоду2 КАК АмортизацияОСПриПереходеКЛинейномуМетоду
	|		ПО (ПервоначальныеСведения.ОсновноеСредство = АмортизацияОСПриПереходеКЛинейномуМетоду.Субконто1)
	|			И (ВЫБОР
	|				КОГДА ГОД(&ДатаПереходаКЛинейномуМетоду) = 2009
	|					ТОГДА ПервоначальныеСведения.МетодНачисленияАмортизации = ЗНАЧЕНИЕ(Перечисление.МетодыНачисленияАмортизации.Нелинейный)
	|				ИНАЧЕ ИСТИНА
	|			КОНЕЦ)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Коэффициент2 КАК Коэффициент
	|		ПО ТаблицаАмортизации.ОсновноеСредство = Коэффициент.ОсновноеСредство
	|			И ТаблицаАмортизации.Период = Коэффициент.Период
	|		ЛЕВОЕ СОЕДИНЕНИЕ СостояниеОС2 КАК СостояниеОС
	|		ПО ТаблицаАмортизации.ОсновноеСредство = СостояниеОС.ОсновноеСредство
	|			И ТаблицаАмортизации.Период = СостояниеОС.Период
	|		ЛЕВОЕ СОЕДИНЕНИЕ СпособОтражения2 КАК СпособОтражения
	|		ПО ТаблицаАмортизации.ОсновноеСредство = СпособОтражения.ОсновноеСредство
	|			И ТаблицаАмортизации.Период = СпособОтражения.Период
	|		ЛЕВОЕ СОЕДИНЕНИЕ Параметры2 КАК Параметры
	|		ПО ТаблицаАмортизации.ОсновноеСредство = Параметры.ОсновноеСредство
	|			И ТаблицаАмортизации.Период = Параметры.Период
	|		ЛЕВОЕ СОЕДИНЕНИЕ УчетнаяПолитикаОрганизаций2 КАК УчетнаяПолитикаОрганизаций
	|		ПО ТаблицаАмортизации.Период = УчетнаяПолитикаОрганизаций.Период
	|		ЛЕВОЕ СОЕДИНЕНИЕ НалоговыйОстаткиИОбороты КАК НалоговыйОстаткиИОбороты
	|		ПО ТаблицаАмортизации.ОсновноеСредство = НалоговыйОстаткиИОбороты.ОсновноеСредство
	|			И ТаблицаАмортизации.Регистратор = НалоговыйОстаткиИОбороты.Регистратор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РегистрАмортизацииОС.ПериодДействия,
	|	МАКСИМУМ(УчетнаяПолитикаОрганизаций.Период) КАК ПериодСреза
	|ПОМЕСТИТЬ УчетнаяПолитикаОрганизацийСрезПоследнихПериодовОС
	|ИЗ
	|	РегистрАмортизацииОС КАК РегистрАмортизацииОС
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.УчетнаяПолитикаОрганизаций КАК УчетнаяПолитикаОрганизаций
	|		ПО РегистрАмортизацииОС.ПериодДействия >= УчетнаяПолитикаОрганизаций.Период
	|ГДЕ
	|	ВЫБОР
	|			КОГДА &ДатаКон = ДАТАВРЕМЯ(1, 1, 1)
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ УчетнаяПолитикаОрганизаций.Период < &ДатаКон
	|		КОНЕЦ
	|	И УчетнаяПолитикаОрганизаций.Организация = &Организация
	|
	|СГРУППИРОВАТЬ ПО
	|	РегистрАмортизацииОС.ПериодДействия
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	УчетнаяПолитикаОрганизацийСрезПоследнихПериодовОС.ПериодДействия,
	|	УчетнаяПолитикаОрганизаций.МетодНачисленияАмортизацииНУ КАК МетодНачисленияАмортизацииНУ
	|ПОМЕСТИТЬ УчетнаяПолитикаОрганизацийСрезПоследнихОС
	|ИЗ
	|	УчетнаяПолитикаОрганизацийСрезПоследнихПериодовОС КАК УчетнаяПолитикаОрганизацийСрезПоследнихПериодовОС
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.УчетнаяПолитикаОрганизаций КАК УчетнаяПолитикаОрганизаций
	|		ПО УчетнаяПолитикаОрганизацийСрезПоследнихПериодовОС.ПериодСреза = УчетнаяПолитикаОрганизаций.Период
	|ГДЕ
	|	ВЫБОР
	|			КОГДА &ДатаКон = ДАТАВРЕМЯ(1, 1, 1)
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ УчетнаяПолитикаОрганизаций.Период < &ДатаКон
	|		КОНЕЦ
	|	И УчетнаяПолитикаОрганизаций.Организация = &Организация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РегистрАмортизацииОС.Амортизация КАК Амортизация,
	|	РегистрАмортизацииОС.ПериодДействия КАК ПериодДействия,
	|	РегистрАмортизацииОС.МетодНачисленияАмортизации,
	|	РегистрАмортизацииОС.ПервоначальнаяСтоимость КАК ПервоначальнаяСтоимость,
	|	РегистрАмортизацииОС.СпециальныйКоэффициент,
	|	РегистрАмортизацииОС.ДатаПринятияКУчету,
	|	РегистрАмортизацииОС.СпособОтраженияРасходов,
	|	РегистрАмортизацииОС.РасчетПоБазовойСтоимости,
	|	РегистрАмортизацииОС.АмортизацииДляБазовойСтоимости КАК АмортизацииДляБазовойСтоимости,
	|	РегистрАмортизацииОС.ОсновноеСредство,
	|	РегистрАмортизацииОС.СрокПолезногоИспользования,
	|	РегистрАмортизацииОС.ПервоначальнаяСтоимостьДляАмортизации КАК ПервоначальнаяСтоимостьДляАмортизации,
	|	РегистрАмортизацииОС.ОстаточнаяСтоимость КАК ОстаточнаяСтоимость
	|ИЗ
	|	РегистрАмортизацииОС КАК РегистрАмортизацииОС
	|ГДЕ
	|	(РегистрАмортизацииОС.ОсновноеСредство.АмортизационнаяГруппа В (ЗНАЧЕНИЕ(Перечисление.АмортизационныеГруппы.ВосьмаяГруппа), ЗНАЧЕНИЕ(Перечисление.АмортизационныеГруппы.ДевятаяГруппа), ЗНАЧЕНИЕ(Перечисление.АмортизационныеГруппы.ДесятаяГруппа))
	|				И РегистрАмортизацииОС.ОсновноеСредство.ГруппаОС В (ЗНАЧЕНИЕ(Перечисление.ГруппыОС.Здания), ЗНАЧЕНИЕ(Перечисление.ГруппыОС.Сооружения))
	|			ИЛИ РегистрАмортизацииОС.ОсновноеСредство.АмортизационнаяГруппа = ЗНАЧЕНИЕ(Перечисление.АмортизационныеГруппы.ОтдельнаяГруппа)
	|			ИЛИ (НЕ РегистрАмортизацииОС.ПериодДействия В
	|					(ВЫБРАТЬ
	|						УчетнаяПолитикаОрганизацийСрезПоследнихОС.ПериодДействия
	|					ИЗ
	|						УчетнаяПолитикаОрганизацийСрезПоследнихОС
	|					ГДЕ
	|						УчетнаяПолитикаОрганизацийСрезПоследнихОС.МетодНачисленияАмортизацииНУ = ЗНАЧЕНИЕ(Перечисление.МетодыНачисленияАмортизации.Нелинейный))))
	|ИТОГИ
	|	СУММА(Амортизация),
	|	СУММА(ПервоначальнаяСтоимость),
	|	СУММА(АмортизацииДляБазовойСтоимости),
	|	СУММА(ПервоначальнаяСтоимостьДляАмортизации),
	|	СУММА(ОстаточнаяСтоимость)
	|ПО ПериодДействия
	|";

	
	
		Если ГруппироватьПоОбъектам Тогда
			 ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ПО ПериодДействия", "ПО ОсновноеСредство,ПериодДействия");
		 КонецЕсли;
		 
	Если Не АмортизационнаяГруппа.Пустая() Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "И ВидУчета = &ВидУчета", "И ВидУчета = &ВидУчета И Субконто1.АмортизационнаяГруппа = &АмортизационнаяГруппа");
	КонецЕсли;

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация",     Организация);
	Запрос.УстановитьПараметр("ДатаНач",         ?(НЕ ЗначениеЗаполнено(ДатаНач),ДатаНач,НачалоДня(ДатаНач)));
	Запрос.УстановитьПараметр("ДатаКон",         ?(НЕ ЗначениеЗаполнено(ДатаКон),ДатаКон,КонецДня(ДатаКон)));
	Запрос.УстановитьПараметр("ПринятоКУчету", 	 Перечисления.СостоянияОС.ВведеноВЭксплуатацию);
	Запрос.УстановитьПараметр("ДатаНачалаНУ",    '20020101000000');
	Запрос.УстановитьПараметр("ДатаПереходаКЛинейномуМетоду", УправлениеВнеоборотнымиАктивами.ПолучитьДатуПереходаКЛинейномуМетоду(ДатаНач, Организация));
	Массив = Новый Массив;
	Массив.Добавить(ПланыСчетов.Налоговый.ДоходныеВложенияВ_МЦ);
	Массив.Добавить(ПланыСчетов.Налоговый.ОсновныеСредства);
	Запрос.УстановитьПараметр("Счет",Массив);
	Массив = Новый Массив;
	Массив.Добавить(ПланыСчетов.Налоговый.ДоходныеВложенияВ_МЦ);
	Массив.Добавить(ПланыСчетов.Налоговый.ОсновныеСредства);
	Массив.Добавить(ПланыСчетов.Налоговый.АмортизацияОсновныхСредств);
	Запрос.УстановитьПараметр("Счет01",Массив);
	Запрос.УстановитьПараметр("Счет02",ПланыСчетов.Налоговый.АмортизацияОсновныхСредств);
	Запрос.УстановитьПараметр("Счет09",ПланыСчетов.Налоговый.ВыбытиеОС);
	Запрос.УстановитьПараметр("ВидУчета",Перечисления.ВидыУчетаПоПБУ18.НУ);
	Запрос.УстановитьПараметр("ВидСубконто",ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.ОсновныеСредства);
    Запрос.УстановитьПараметр("ВидКорСубконто",ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.СтатьиЗатрат);
    Запрос.УстановитьПараметр("ВидКорСубконто91",ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.ПрочиеДоходыИРасходы);
	Запрос.УстановитьПараметр("Амортизация",Перечисления.ВидыРасходовНУ.Амортизация);
	Запрос.УстановитьПараметр("АмортизационнаяГруппа",АмортизационнаяГруппа);
	
	Запрос.Текст = ТекстЗапроса;
	
	СтруктураПоиска = Новый Структура;
	СтруктураПоиска.Вставить("Организация", Организация);
	
	ИтогСуммаАмортизации = 0;
	
	Опер = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);

	Запрос.Текст = 
		"ВЫБРАТЬ
		|	РаспределениеАмортизации.ТекущееНаправление КАК Способ,
		|	СУММА(РаспределениеАмортизации.БазаРаспределения) КАК БазаРаспределения,
		|	СУММА(РаспределениеАмортизации.ПрямыеЗатраты) КАК ПрямыеЗатраты,
		|	СУММА(РаспределениеАмортизации.КосвенныеЗатраты) КАК КосвенныеЗатраты,
		|	СУММА(РаспределениеАмортизации.ПрочиеЗатраты) КАК ПрочиеЗатраты
		|ИЗ
		|	(ВЫБРАТЬ РАЗЛИЧНЫЕ
		|		СпособыОтраженияРасходовПоАмортизацииНалоговыйУчет.СпособыОтраженияРасходовПоАмортизации КАК СпособыОтраженияРасходовПоАмортизации
		|	ИЗ
		|		РегистрСведений.СпособыОтраженияРасходовПоАмортизацииОСНалоговыйУчет КАК СпособыОтраженияРасходовПоАмортизацииНалоговыйУчет
		|	ГДЕ
		|		СпособыОтраженияРасходовПоАмортизацииНалоговыйУчет.Организация = &Организация
		|		И СпособыОтраженияРасходовПоАмортизацииНалоговыйУчет.Период > &ДатаНач
		|		И СпособыОтраженияРасходовПоАмортизацииНалоговыйУчет.Период <= &ДатаКон
		|	
		|	ОБЪЕДИНИТЬ
		|	
		|	ВЫБРАТЬ РАЗЛИЧНЫЕ
		|		СпособыОтраженияРасходовПоАмортизацииНалоговыйУчет.СпособыОтраженияРасходовПоАмортизации
		|	ИЗ
		|		РегистрСведений.СпособыОтраженияРасходовПоАмортизацииОСНалоговыйУчет.СрезПоследних(&ДатаНач, Организация = &Организация) КАК СпособыОтраженияРасходовПоАмортизацииНалоговыйУчет) КАК СпособыОтраженияРасходовПоАмортизацииНалоговыйУчет
		|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
		|			НаправлениеНачисленияАмортизацииНаправления.Коэффициент КАК БазаРаспределения,
		|			ВЫБОР
		|				КОГДА НаправлениеНачисленияАмортизацииНаправления.СчетЗатратНУ В ИЕРАРХИИ (&СчетаПрямыхЗатрат)
		|					ТОГДА НаправлениеНачисленияАмортизацииНаправления.Коэффициент
		|				ИНАЧЕ 0
		|			КОНЕЦ КАК ПрямыеЗатраты,
		|			ВЫБОР
		|				КОГДА НаправлениеНачисленияАмортизацииНаправления.СчетЗатратНУ В ИЕРАРХИИ (&СчетаПрямыхИКосвенныхЗатрат)
		|						И (НЕ НаправлениеНачисленияАмортизацииНаправления.СчетЗатратНУ В ИЕРАРХИИ (&СчетаПрямыхЗатрат))
		|					ТОГДА НаправлениеНачисленияАмортизацииНаправления.Коэффициент
		|				ИНАЧЕ 0
		|			КОНЕЦ КАК КосвенныеЗатраты,
		|			ВЫБОР
		|				КОГДА (НЕ НаправлениеНачисленияАмортизацииНаправления.СчетЗатратНУ В ИЕРАРХИИ (&СчетаПрямыхИКосвенныхЗатрат))
		|						И (НЕ НаправлениеНачисленияАмортизацииНаправления.СчетЗатратНУ В ИЕРАРХИИ (&СчетаПрямыхЗатрат))
		|					ТОГДА НаправлениеНачисленияАмортизацииНаправления.Коэффициент
		|				ИНАЧЕ 0
		|			КОНЕЦ КАК ПрочиеЗатраты,
		|			НаправлениеНачисленияАмортизации.Ссылка КАК ТекущееНаправление,
		|			НаправлениеНачисленияАмортизации.Представление КАК Представление
		|		ИЗ
		|			Справочник.СпособыОтраженияРасходовПоАмортизации.Способы КАК НаправлениеНачисленияАмортизацииНаправления
		|				ЛЕВОЕ СОЕДИНЕНИЕ Справочник.СпособыОтраженияРасходовПоАмортизации КАК НаправлениеНачисленияАмортизации
		|				ПО НаправлениеНачисленияАмортизацииНаправления.Ссылка = НаправлениеНачисленияАмортизации.Ссылка) КАК РаспределениеАмортизации
		|		ПО СпособыОтраженияРасходовПоАмортизацииНалоговыйУчет.СпособыОтраженияРасходовПоАмортизации = РаспределениеАмортизации.ТекущееНаправление
		|
		|СГРУППИРОВАТЬ ПО
		|	РаспределениеАмортизации.ТекущееНаправление";
		Запрос.УстановитьПараметр("СчетаПрямыхЗатрат",СчетаПрямыхЗатрат);
		Запрос.УстановитьПараметр("СчетаПрямыхИКосвенныхЗатрат",СчетаПрямыхИКосвенныхЗатрат);
		СпособыРаспределенияРасходов = Запрос.Выполнить().Выгрузить();
		СпособыРаспределенияРасходов.Индексы.Добавить("Способ");
		
		Пока Опер.Следующий() Цикл
			
			Если ГруппироватьПоОбъектам Тогда
				
				Группировка1 = Макет.ПолучитьОбласть("Группировка|Общая1");
				Группировка3 = Макет.ПолучитьОбласть("Группировка|Общая2");
				
				Группировка1.Параметры.ЗначениеГруппировки = ПолноеНаименование(Опер.ОсновноеСредство);
				Группировка3.Параметры.ИтогСуммаПоГруппировке = Опер.Амортизация;
				
				ДокументРезультат.Вывести(Группировка1);
				ДокументРезультат.Присоединить(Группировка3);
				
				Опер2 = Опер.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
				Пока Опер2.Следующий() Цикл
					ОперПоОбъекту = Опер2.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
					Пока ОперПоОбъекту.Следующий() Цикл
						ВывестиСтроку(ОперПоОбъекту, ДокументРезультат, Макет);
					КонецЦикла;
				КонецЦикла;
				
			Иначе	
				
				ОперПоОбъекту = Опер.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
				Пока ОперПоОбъекту.Следующий() Цикл
					ВывестиСтроку(ОперПоОбъекту, ДокументРезультат, Макет);
				КонецЦикла;
			КонецЕсли;
			
		КонецЦикла;
	
	
	// Подвал
	Если ГруппироватьПоОбъектам Тогда
		Подвал1 = Макет.ПолучитьОбласть("Подвал1|Общая1");
		Подвал2 = Макет.ПолучитьОбласть("Подвал1|Общая2");
		Подвал2.Параметры.ИтогСуммаАмортизации     = ИтогСуммаАмортизации;
		Подвал2.Параметры.СуммаПрямых		= СуммаАмортизацииПрямые;
		Подвал2.Параметры.СуммаКосвенных	= СуммаАмортизацииКосвенные;
		Подвал2.Параметры.СуммаПрочих		= СуммаАмортизациипрочие;
		ДокументРезультат.Вывести(Подвал1);
		ДокументРезультат.Присоединить(Подвал2);

	Иначе
		Подвал = Макет.ПолучитьОбласть("Подвал1");
		Подвал.Параметры.ИтогСуммаАмортизации     = ИтогСуммаАмортизации;
		Подвал.Параметры.СуммаПрямых		= СуммаАмортизацииПрямые;
		Подвал.Параметры.СуммаКосвенных	= СуммаАмортизацииКосвенные;
		Подвал.Параметры.СуммаПрочих		= СуммаАмортизациипрочие;
		ДокументРезультат.Вывести(Подвал);
	КонецЕсли;

	ОбластьПодвал = Макет.ПолучитьОбласть("Подвал");
	СтруктураЛиц = РегламентированнаяОтчетность.ОтветственныеЛицаОрганизаций(Организация, ДатаКон);
	ОбластьПодвал.Параметры.ОтветственныйЗаРегистры = ОбщегоНазначения.ФамилияИнициалыФизЛица(СтруктураЛиц.ОтветственныйЗаРегистры);
	ДокументРезультат.Вывести(ОбластьПодвал);

КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОПЕРАТОРЫ ОСНОВНОЙ ПРОГРАММЫ
// 

НП = Новый НастройкаПериода;


СчетаПрямыхЗатрат =Новый Массив();
СчетаНалоговогоУчета = ПланыСчетов.Налоговый;
СчетаПрямыхЗатрат.Добавить(СчетаНалоговогоУчета.ПрямыеРасходыОсновногоПроизводства);
СчетаПрямыхЗатрат.Добавить(СчетаНалоговогоУчета.ПрямыеРасходыВспомогательныхПроизводств);
СчетаПрямыхЗатрат.Добавить(СчетаНалоговогоУчета.ПрямыеОбщепроизводственныеРасходы);
СчетаПрямыхЗатрат.Добавить(СчетаНалоговогоУчета.ПрямыеРасходыОбслуживающихПроизводств);
СчетаПрямыхЗатрат.Добавить(СчетаНалоговогоУчета.ПрямыеОбщехозяйственныеРасходы);

СчетаПрямыхИКосвенныхЗатрат = Новый Массив();
СчетаПрямыхИКосвенныхЗатрат.Добавить(СчетаНалоговогоУчета.ОсновноеПроизводство_);
СчетаПрямыхИКосвенныхЗатрат.Добавить(СчетаНалоговогоУчета.ВспомогательныеПроизводства);
СчетаПрямыхИКосвенныхЗатрат.Добавить(СчетаНалоговогоУчета.ОбщепроизводственныеРасходы);
СчетаПрямыхИКосвенныхЗатрат.Добавить(СчетаНалоговогоУчета.ОбщехозяйственныеРасходы);
СчетаПрямыхИКосвенныхЗатрат.Добавить(СчетаНалоговогоУчета.ОбслуживающиеПроизводства);
СчетаПрямыхИКосвенныхЗатрат.Добавить(СчетаНалоговогоУчета.РасходыНаПродажу);

#КонецЕсли