////////////////////////////////////////////////////////////////////////////////
// Вспомогательные процедуры, функции

////////////////////////////////////////////////////////////////////////////////
// Процедуры, функции объекта

Процедура ДополнитьСтруктуруПечатныхФорм(СтруктураПечатныхФорм, ДокументОбъект) Экспорт
	
	ПриемНаРаботуВОрганизациюДополнительный.ДополнитьСтруктуруПечатныхФорм(СтруктураПечатныхФорм, ДокументОбъект);
	
КонецПроцедуры

Функция ПечатьДополнительныхФорм(ИмяМакета, Объект) Экспорт
	
	Возврат ПриемНаРаботуВОрганизациюДополнительный.ПечатьДополнительныхФорм(ИмяМакета, Объект);
	
КонецФункции

Функция ВыполняетсяДополнительноеУсловиеИДействие(ИмяРегистра, Движение = Неопределено, ВыборкаПоРаботникиОрганизации, ВыборкаПоШапкеДокумента) Экспорт
	
	мДлинаСуток = 86400;
	
	Если НЕ ВыборкаПоРаботникиОрганизации.НапомнитьПоЗавершении Тогда
		
		Если ИмяРегистра = "РаботникиОрганизаций" Тогда
			// Свойства
			Движение.ПериодЗавершения						= ВыборкаПоРаботникиОрганизации.ДатаУвольнения + мДлинаСуток;
			// Ресурсы
			Движение.ПодразделениеОрганизацииЗавершения		= ВыборкаПоРаботникиОрганизации.ПодразделениеОрганизации;
			Движение.ДолжностьЗавершения					= ВыборкаПоРаботникиОрганизации.Должность;
			// Реквизиты
			Движение.ПричинаИзмененияСостоянияЗавершения	= Перечисления.ПричиныИзмененияСостояния.Увольнение;
			Движение.ОбособленноеПодразделениеЗавершения	= ВыборкаПоШапкеДокумента.Организация;
		ИначеЕсли ИмяРегистра = "ПериодыРаботыРаботниковОрганизацийПоСовместительству" Или ИмяРегистра = "ПериодыРаботыРаботниковОрганизацийПоОсновномуМестуРаботы" Тогда
			Движение.ДатаОкончания	= ВыборкаПоРаботникиОрганизации.ДатаУвольнения + мДлинаСуток;
		КонецЕсли;
		
		Возврат Истина;
		
	Иначе
		
		Возврат Ложь;
		
	КонецЕсли;
	
КонецФункции

Функция ВыполнятьДвижениеПоСостояниеРаботниковНаИспытательномСроке(ВыборкаПоРаботникиОрганизации) Экспорт
	
	РежимНабораПерсонала = ПроцедурыУправленияПерсоналомДополнительный.ПолучитьРежимНабораПерсонала();
	
	ВедетсяНаборПерсонала = РежимНабораПерсонала = Перечисления.ВидыОрганизационнойСтруктурыПредприятия.ПоСтруктуреЮридическихЛиц;

	Возврат ВедетсяНаборПерсонала И ВыборкаПоРаботникиОрганизации.ВидЗанятости <> Перечисления.ВидыЗанятостиВОрганизации.ВнутреннееСовместительство;
	
КонецФункции

Процедура ЗаполнитьПоОснованию(Основание, ЭтотОбъект) Экспорт
	
	ТипОснования = ТипЗнч(Основание);
	Если ТипОснования = Тип("ДокументСсылка.ПриемНаРаботу") Тогда
		
		// Заполним реквизиты из стандартного набора.
		ЗаполнениеДокументов.ЗаполнитьШапкуДокументаПоОснованию(ЭтотОбъект, Основание);
		
		Если Основание.Проведен Тогда
			
			ТекущийПользователь = глЗначениеПеременной("глТекущийПользователь");
			УчетнаяПолитикаПоПерсоналуОрганизации = глЗначениеПеременной("глУчетнаяПолитикаПоПерсоналуОрганизации");
			
			Если ЭтотОбъект.Организация.Пустая() Тогда
				ЭтотОбъект.Организация = УправлениеПользователями.ПолучитьЗначениеПоУмолчанию(ТекущийПользователь, "ОсновнаяОрганизация")
			КонецЕсли;
			
			Запрос = Новый Запрос;
			
			Запрос.УстановитьПараметр("Организация",			ЭтотОбъект.Организация);
			Запрос.УстановитьПараметр("Регистратор",			Основание);
			
			Запрос.Текст =
			"ВЫБРАТЬ РАЗРЕШЕННЫЕ
			|	ПриемНаРаботуРаботники.Сотрудник,
			|	ПриемНаРаботуРаботники.Сотрудник.Физлицо КАК ФизЛицо,
			|	СоответствиеПодразделенийИПодразделенийОрганизаций.ПодразделениеОрганизации,
			|	ПриемНаРаботуРаботники.Должность КАК Должность,
			|	ПриемНаРаботуРаботники.ЗанимаемыхСтавок,
			|	ПриемНаРаботуРаботники.ДатаПриема,
			|	ВЫБОР
			|		КОГДА ПриемНаРаботуРаботники.ИспытательныйСрок = ЗНАЧЕНИЕ(Перечисление.ИспытательныйСрок.СИспытательнымСроком)
			|			ТОГДА РАЗНОСТЬДАТ(ПриемНаРаботуРаботники.ДатаПриема, ПриемНаРаботуРаботники.ДатаУвольнения, МЕСЯЦ)
			|		ИНАЧЕ 0
			|	КОНЕЦ КАК ИспытательныйСрок,
			|	ПриемНаРаботуРаботники.ДатаУвольнения,
			|	ПриемНаРаботуРаботники.ГрафикРаботы,
			|	ИСТИНА КАК НапомнитьПоЗавершении
			|ИЗ
			|	Документ.ПриемНаРаботу.Работники КАК ПриемНаРаботуРаботники
			|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СоответствиеПодразделенийИПодразделенийОрганизаций КАК СоответствиеПодразделенийИПодразделенийОрганизаций
			|		ПО (СоответствиеПодразделенийИПодразделенийОрганизаций.Организация = &Организация)
			|			И ПриемНаРаботуРаботники.Подразделение = СоответствиеПодразделенийИПодразделенийОрганизаций.Подразделение
			|ГДЕ
			|	ПриемНаРаботуРаботники.Ссылка = &Регистратор";
			
			ЭтотОбъект.РаботникиОрганизации.Загрузить(Запрос.Выполнить().Выгрузить());
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

// Формирует запрос по таблице "ОсновныеНачисления" документа
//
// Параметры: 
//  нет
//
// Возвращаемое значение:
//  Результат запроса.
//
Функция СформироватьЗапросПоНачисления(КонтрольРазмераСтавокШтатногоРасписания, ЭтотОбъект) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("ДокументСсылка", ЭтотОбъект.Ссылка);
	
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ПриемНаРаботуВОрганизациюОсновныеНачисления.Ссылка,
	|	ПриемНаРаботуВОрганизациюОсновныеНачисления.НомерСтроки,
	|	ПриемНаРаботуВОрганизациюОсновныеНачисления.Сотрудник КАК Сотрудник,
	|	ПриемНаРаботуВОрганизациюОсновныеНачисления.ВидРасчета,
	|	ПриемНаРаботуВОрганизациюОсновныеНачисления.Показатель1,
	|	ПриемНаРаботуВОрганизациюОсновныеНачисления.Валюта1,
	|	ПриемНаРаботуВОрганизациюОсновныеНачисления.Показатель2,
	|	ПриемНаРаботуВОрганизациюОсновныеНачисления.Валюта2,
	|	ПриемНаРаботуВОрганизациюОсновныеНачисления.Показатель3,
	|	ПриемНаРаботуВОрганизациюОсновныеНачисления.Валюта3,
	|	ПриемНаРаботуВОрганизациюОсновныеНачисления.Показатель4,
	|	ПриемНаРаботуВОрганизациюОсновныеНачисления.Валюта4,
	|	ПриемНаРаботуВОрганизациюОсновныеНачисления.Показатель5,
	|	ПриемНаРаботуВОрганизациюОсновныеНачисления.Валюта5,
	|	ПриемНаРаботуВОрганизациюОсновныеНачисления.Показатель6,
	|	ПриемНаРаботуВОрганизациюОсновныеНачисления.Валюта6,
	|	ПриемНаРаботуВОрганизациюОсновныеНачисления.ТарифныйРазряд1,
	|	ПриемНаРаботуВОрганизациюОсновныеНачисления.ТарифныйРазряд2,
	|	ПриемНаРаботуВОрганизациюОсновныеНачисления.ТарифныйРазряд3,
	|	ПриемНаРаботуВОрганизациюОсновныеНачисления.ТарифныйРазряд4,
	|	ПриемНаРаботуВОрганизациюОсновныеНачисления.ТарифныйРазряд5,
	|	ПриемНаРаботуВОрганизациюОсновныеНачисления.ТарифныйРазряд6,
	|	ПриемНаРаботуВОрганизациюОсновныеНачисления.ВидРасчета.СпособРасчета,
	|	ПриемНаРаботуВОрганизациюОсновныеНачисления.ВидРасчета.ЗачетОтработанногоВремени,
	|	ПриемНаРаботуВОрганизациюОсновныеНачисления.ВидРасчета.ТребуетВводаТарифногоРазряда,
	|	ПриемНаРаботуВОрганизациюОсновныеНачисления.ВидРасчета.ПроизвольнаяФормулаРасчета
	|ПОМЕСТИТЬ ВТДанныеНачислений
	|ИЗ
	|	Документ.ПриемНаРаботуВОрганизацию.ОсновныеНачисления КАК ПриемНаРаботуВОрганизациюОсновныеНачисления
	|ГДЕ
	|	ПриемНаРаботуВОрганизациюОсновныеНачисления.Ссылка = &ДокументСсылка
	|	И (НЕ ПриемНаРаботуВОрганизациюОсновныеНачисления.Сторно)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Сотрудник
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТЧРаботникиОрганизации.Сотрудник КАК Сотрудник,
	|	ТЧРаботникиОрганизации.ПодразделениеОрганизации,
	|	ТЧРаботникиОрганизации.Должность,
	|	ТЧРаботникиОрганизации.ЗанимаемыхСтавок,
	|	ТЧРаботникиОрганизации.ГрафикРаботы,
	|	ТЧРаботникиОрганизации.ДатаПриема,
	|	ТЧРаботникиОрганизации.ДатаУвольнения,
	|	ТЧРаботникиОрганизации.НапомнитьПоЗавершении
	|ПОМЕСТИТЬ ВТДанныеДокумента
	|ИЗ
	|	Документ.ПриемНаРаботуВОрганизацию.РаботникиОрганизации КАК ТЧРаботникиОрганизации
	|ГДЕ
	|	ТЧРаботникиОрганизации.Ссылка = &ДокументСсылка
	|	И (НЕ ТЧРаботникиОрганизации.Сторно)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Сотрудник
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТЧНачисления.Ссылка КАК Ссылка,
	|	ТЧНачисления.НомерСтроки КАК НомерСтроки,
	|	ТЧНачисления.Сотрудник КАК Сотрудник,
	|	ТЧРаботникиОрганизации.ПодразделениеОрганизации,
	|	ТЧРаботникиОрганизации.Должность,
	|	ТЧРаботникиОрганизации.ЗанимаемыхСтавок,
	|	ТЧРаботникиОрганизации.ГрафикРаботы,
	|	ТЧРаботникиОрганизации.ДатаПриема КАК Период,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыДействияСНачислением.Начать) КАК Действие,
	|	ВЫБОР
	|		КОГДА ТЧРаботникиОрганизации.ДатаУвольнения <> ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|				И (НЕ ТЧРаботникиОрганизации.НапомнитьПоЗавершении)
	|			ТОГДА ДОБАВИТЬКДАТЕ(ТЧРаботникиОрганизации.ДатаУвольнения, ДЕНЬ, 1)
	|		ИНАЧЕ ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|	КОНЕЦ КАК ПериодЗавершения,
	|	ВЫБОР
	|		КОГДА ТЧРаботникиОрганизации.ДатаУвольнения <> ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|				И (НЕ ТЧРаботникиОрганизации.НапомнитьПоЗавершении)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыДействияСНачислением.Прекратить)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ВидыДействияСНачислением.ПустаяСсылка)
	|	КОНЕЦ КАК ДействиеЗавершения,
	|	ТЧНачисления.ВидРасчета КАК ВидРасчета,
	|	ВЫБОР
	|		КОГДА ТЧНачисления.ВидРасчетаЗачетОтработанногоВремени
	|			ТОГДА """"
	|		ИНАЧЕ ТЧНачисления.ВидРасчета
	|	КОНЕЦ КАК ВидРасчетаИзмерение,
	|	ТЧНачисления.ВидРасчетаСпособРасчета КАК СпособРасчета,
	|	ТЧНачисления.ВидРасчетаЗачетОтработанногоВремени КАК ЗачетОтработанногоВремени,
	|	ТЧНачисления.ВидРасчетаТребуетВводаТарифногоРазряда КАК ТребуетВводаТарифногоРазряда,
	|	ТЧНачисления.ВидРасчетаПроизвольнаяФормулаРасчета КАК ПроизвольнаяФормулаРасчета,
	|	ТЧНачисления.Показатель1 КАК Показатель1,
	|	ТЧНачисления.Валюта1 КАК Валюта1,
	|	ТЧНачисления.ТарифныйРазряд1 КАК ТарифныйРазряд1,
	|	ТЧНачисления.Показатель2 КАК Показатель2,
	|	ТЧНачисления.Валюта2 КАК Валюта2,
	|	ТЧНачисления.ТарифныйРазряд2 КАК ТарифныйРазряд2,
	|	ТЧНачисления.Показатель3 КАК Показатель3,
	|	ТЧНачисления.Валюта3 КАК Валюта3,
	|	ТЧНачисления.ТарифныйРазряд3 КАК ТарифныйРазряд3,
	|	ТЧНачисления.Показатель4 КАК Показатель4,
	|	ТЧНачисления.Валюта4 КАК Валюта4,
	|	ТЧНачисления.ТарифныйРазряд4 КАК ТарифныйРазряд4,
	|	ТЧНачисления.Показатель5 КАК Показатель5,
	|	ТЧНачисления.Валюта5 КАК Валюта5,
	|	ТЧНачисления.ТарифныйРазряд5 КАК ТарифныйРазряд5,
	|	ТЧНачисления.Показатель6 КАК Показатель6,
	|	ТЧНачисления.Валюта6 КАК Валюта6,
	|	ТЧНачисления.ТарифныйРазряд6 КАК ТарифныйРазряд6
	|ИЗ
	|	ВТДанныеНачислений КАК ТЧНачисления
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТДанныеДокумента КАК ТЧРаботникиОрганизации
	|		ПО ТЧНачисления.Сотрудник = ТЧРаботникиОрганизации.Сотрудник
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки";
	
	Возврат ПроцедурыУправленияПерсоналом.СформироватьЗапросПоПлановымНачислениям(Запрос.Выполнить().Выгрузить(), КонтрольРазмераСтавокШтатногоРасписания);
	
КонецФункции // СформироватьЗапросПоНачисления()

// Возвращает массив значений перечисления ВидыДоговоровСФизЛицами, для которых оформляется
// приказ о приеме на работу по форме Т-1
//
// Параметры
//  нет
//
// Возвращаемое значение:
//   массив значений типа ПеречислениеСсылка.ВидыДоговоровСФизЛицами
//
Функция ВидыТрудовыхДоговоров() Экспорт

	МассивВидов = Новый Массив;
	МассивВидов.Добавить(Перечисления.ВидыДоговоровСФизЛицами.ТрудовойДоговор);

	Возврат МассивВидов
	
КонецФункции // ВидыТрудовыхДоговоров()

Функция ДополнительноеПолеТабличнойЧасти() Экспорт

	Возврат "ТЧРаботникиОрганизации.НапомнитьПоЗавершении,"

КонецФункции // ДополнительноеПолеТабличнойЧасти()

Процедура ДобавитьДополнительноПоСтроке(ДокументОбъект, СтрокаТабличнойЧасти) Экспорт
	
	ПриемНаРаботуВОрганизациюДополнительный.ДобавитьДополнительноПоСтроке(ДокументОбъект, СтрокаТабличнойЧасти);
	
КонецПроцедуры

Процедура ДобавитьДополнительныеДвижения(ДокументОбъект, Отказ, Заголовок) Экспорт
	
	ПриемНаРаботуВОрганизациюДополнительный.ДобавитьДополнительныеДвижения(ДокументОбъект, Отказ, Заголовок);
	
КонецПроцедуры

Процедура ДозаполнитьСтрокуТаблицыРаботникиОрганизации(СтрокаТабличнойЧасти) Экспорт

	СтрокаТабличнойЧасти.НапомнитьПоЗавершении = Истина;

КонецПроцедуры

Процедура ДвиженияНаДатуУвольненияДополнительно(Движение) Экспорт
	
	ПриемНаРаботуВОрганизациюДополнительный.ДвиженияНаДатуУвольненияДополнительно(Движение);
	
КонецПроцедуры

Процедура ДополнитьДвиженияПоСостояниюРаботниковНаИспытательномСроке(Движение, ВыборкаПоРаботникиОрганизации) Экспорт
	
	ПриемНаРаботуВОрганизациюДополнительный.ДополнитьДвиженияПоСостояниюРаботниковНаИспытательномСроке(Движение, ВыборкаПоРаботникиОрганизации);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Процедуры, функции для работы формы документа

#Если ТолстыйКлиентОбычноеПриложение Тогда

Процедура ДополнитьКнопкиКоманднойПанели(КоманднаяПанель, Действие) Экспорт
	
	ИспользоватьУправленческийУчетЗарплаты = глЗначениеПеременной("глИспользоватьУправленческийУчетЗарплаты");
	
	Если ИспользоватьУправленческийУчетЗарплаты Тогда
	
		КнопкаЗаполнитьПринятымиВКомпанию = КоманднаяПанель.Кнопки.Добавить("ЗаполнитьПринятымиВКомпанию", ТипКнопкиКоманднойПанели.Действие, "Работниками, принятыми в компанию", Действие);
		КнопкаЗаполнитьПринятымиВКомпанию.Подсказка = "Работниками, принятыми в компанию";
		КнопкаЗаполнитьПринятымиВКомпанию.ИзменяетДанные = Истина;
		
	КонецЕсли;
		
КонецПроцедуры

Процедура ДополнитьСоставКолонок(Форма) Экспорт
	
	РаботаСДиалогами.ВставитьКолонкуВТабличноеПоле(8, Форма.ЭлементыФормы.РаботникиОрганизации, "НапомнитьПоЗавершении", "Регистрировать увольнение отдельным документом", , "НапомнитьПоЗавершении", , Ложь, , ПоложениеКолонки.ВТойЖеКолонке);
	
	ПриемНаРаботуВОрганизациюДополнительный.ДополнитьСоставКолонок(Форма);
	
КонецПроцедуры

// Процедура обеспечивает начальное значение реквизитов "ЗанимаемыхСтавок" 
//  и "ИспытательныйСрок"
//
// Параметры:
//  Элемент - табличное поле, которое отображает т.ч.
//  НоваяСтрока - булево, признак редактирования новой строки
//  
Процедура РаботникиОрганизацииПриНачалеРедактирования(Элемент, НоваяСтрока) Экспорт
	
	Если НоваяСтрока Тогда
		Элемент.ТекущиеДанные.НапомнитьПоЗавершении = Истина;
	КонецЕсли;
	
КонецПроцедуры // РаботникиОрганизацииПриНачалеРедактирования()

Процедура РаботникиОрганизацииПриПолученииДанных(Элемент, ОформленияСтрок, РаботникиОрганизации) Экспорт
	
	ТолькоПросмотрНапоминания = РаботникиОрганизации.Итог("НапомнитьПоЗавершении") = РаботникиОрганизации.Количество();
	
	Для Каждого ОформлениеСтроки Из ОформленияСтрок Цикл
		ОформлениеСтроки.Ячейки.НапомнитьПоЗавершении.ТолькоПросмотр = ТолькоПросмотрНапоминания;
	КонецЦикла;
	
	ПриемНаРаботуВОрганизациюДополнительный.РаботникиОрганизацийПриПолученииДанныхДополнительно(Элемент, ОформленияСтрок);
	
КонецПроцедуры

Процедура ВыполнитьДополнительныеДействия(Кнопка, Объект, НастройкаПериода, Форма) Экспорт
	
	Если Кнопка.Имя = "ЗаполнитьПринятымиВКомпанию" Тогда
		
		Если Объект.РаботникиОрганизации.Количество()>0 Тогда
			ТекстВопроса = "Имеющийся список сотрудников будет очищен. Продолжить?";
			Ответ  = Вопрос(ТекстВопроса, РежимДиалогаВопрос.ОКОтмена);
			Если Ответ = КодВозвратаДиалога.Отмена Тогда
				Возврат;
			КонецЕсли;
		КонецЕсли;
		
		Если НастройкаПериода.Редактировать() Тогда
			ПриемНаРаботуВОрганизациюДополнительный.ЗаполнитьТабличнуюЧастьРаботникиПринятымиВКомпанию(Объект, НастройкаПериода.ПолучитьДатуНачала(), НастройкаПериода.ПолучитьДатуОкончания());
		КонецЕсли;
	
	КонецЕсли;

	ПриемНаРаботуВОрганизациюДополнительный.ВыполнитьДополнительныеДействия(Форма, Кнопка);
	
КонецПроцедуры

Процедура ПриИзмененииПодразделенияДолжностиДополнительно(ЭтаФорма) Экспорт
	
	ДанныеСтроки = ЭтаФорма.ЭлементыФормы.РаботникиОрганизации.ТекущиеДанные;
	
	Если НЕ ЗначениеЗаполнено(ДанныеСтроки.ГрафикРаботы) Тогда
		ДанныеШР = ПроцедурыУправленияПерсоналомПереопределяемый.СведенияОПозицииШтатногоРасписания(ЭтаФорма.Дата, ДанныеСтроки.ПодразделениеОрганизации, ДанныеСтроки.Должность);
		Если ДанныеШР.Количество() > 0 Тогда
			ДанныеСтроки.ГрафикРаботы = ДанныеШР[0].ГрафикРаботы;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура ФормаДокументаПередОткрытиемДополнительно(Форма, ДополнительныеДействия, ДополнительныеОбработчики) Экспорт
	
	ПриемНаРаботуВОрганизациюДополнительный.ФормаДокументаПередОткрытиемДополнительно(Форма, ДополнительныеДействия, ДополнительныеОбработчики);
	
КонецПроцедуры

Процедура ОбработатьДополнительноПриИзменении(Форма, Элемент) Экспорт
	
	ПриемНаРаботуВОрганизациюДополнительный.ОбработатьДополнительноПриИзменении(Форма, Элемент);
	
КонецПроцедуры

Процедура УдалитьДополнительноПоСтроке(Форма, Сотрудник) Экспорт
	
	ПриемНаРаботуВОрганизациюДополнительный.УдалитьДополнительноПоСтроке(Форма, Сотрудник);
	
КонецПроцедуры

Процедура ФормаДокументаРаботникиПослеУдаленияСтрокиДополнительно(Форма) Экспорт
	
	ПриемНаРаботуВОрганизациюДополнительный.ФормаДокументаРаботникиПослеУдаленияСтрокиДополнительно(Форма);
		
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Процедуры, функции для работы формы списка

Процедура ФормаСпискаПередОткрытиемДополнительно(ФормаСписка) Экспорт
		
    ПриемНаРаботуВОрганизациюДополнительный.ФормаСпискаПередОткрытиемДополнительно(ФормаСписка);
	
КонецПроцедуры // ФормаСпискаПередОткрытиемДополнительно

#КонецЕсли