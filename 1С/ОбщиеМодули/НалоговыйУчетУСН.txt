///////////////////////////////////////////////////////////////////////////////
// ФУНКЦИИ РАБОТЫ С ЕНВД

//Определяет вид счета с точки зрения применения ЕНВД
//
Функция ОтноситсяКДеятельностиЕНВД(Счет, Распределение = "") Экспорт //~
	
	ВыборкаПоСчету = РегистрыСведений.СчетаУчетаПоДеятельностиЕНВД.Выбрать(новый структура("Счет",Счет));
	
	Если ВыборкаПоСчету.Следующий() тогда
		
		Если НЕ ЗначениеЗаполнено(Распределение) Тогда
			
			Возврат Истина;
			
		ИначеЕсли ВыборкаПоСчету.ПодлежитРаспределению Тогда
			
			Возврат Распределение;
			
		Иначе
			
			Возврат НЕ Распределение;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Ложь;
КонецФункции

//Возвращает массив счетов, на которых отражается выручка по деятельности, облагаемой ЕНВД
//
Функция МассивСчетовВыручкиЕНВД() Экспорт //~
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Счет90_01", Планысчетов.Хозрасчетный.Выручка);
	
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	Хозрасчетный.Ссылка КАК Счет
	|ИЗ
	|	РегистрСведений.СчетаУчетаПоДеятельностиЕНВД КАК СчетаУчетаПоДеятельностиЕНВД
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПланСчетов.Хозрасчетный КАК Хозрасчетный
	|		ПО СчетаУчетаПоДеятельностиЕНВД.Счет = Хозрасчетный.Ссылка
	|
	|ГДЕ
	|	Хозрасчетный.Ссылка В ИЕРАРХИИ(&Счет90_01)";
	
	ТаблицаСчетов = Запрос.Выполнить().Выгрузить();
	Возврат ТаблицаСчетов.ВыгрузитьКолонку("Счет");
	
КонецФункции

#Если Клиент Тогда
	
////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ РАБОТЫ С ФОРМАМИ

//Устанавливает видимость реквизитов формы относящихся к УСН
//
Процедура УстановитьВидимостьРеквизитовУСН(РеквизитыФормы, Организация, Дата, ПоказыватьВДокументахСчетаУчета=истина) Экспорт //~

	УСН = ПрименениеУСН(Организация, Дата);
	УСНДоходы = ПрименениеУСНДоходы(Организация, Дата);
	Если УСН Тогда
		Если УСНДоходы Тогда
			ВидимостьУСН = Ложь;
		Иначе
			ВидимостьУСН = Истина;
		КонецЕсли;
	Иначе
		ВидимостьУСН = Ложь;
	КонецЕсли;
	
	ВидимостьНУ  = НЕ УСН;

	Если НЕ РеквизитыФормы.Найти("ОтражатьВНалоговомУчете") = Неопределено Тогда

		Если НЕ РеквизитыФормы.Найти("ОтражатьВНалоговомУчете").Значение Тогда

			ВидимостьУСН = Ложь;
			ВидимостьНУ  = Ложь;

		КонецЕсли;

	КонецЕсли;

	Для Каждого Элемент Из РеквизитыФормы Цикл

		Если Найти(Элемент.Имя, "УСН") > 0 Тогда
			Если не ПоказыватьВДокументахСчетаУчета и Найти(Элемент.Имя, "Счет")>0 Тогда
				Элемент.Видимость = ложь;
			Иначе
				Элемент.Видимость = ВидимостьУСН;
			КонецЕсли;
			
		ИначеЕсли Найти(Элемент.Имя, "НУ") > 0 Тогда
			Если не ПоказыватьВДокументахСчетаУчета и Найти(Элемент.Имя, "Счет")>0 Тогда
				Элемент.Видимость = ложь;
			Иначе
				Элемент.Видимость = ВидимостьНУ;
			КонецЕсли;
		ИначеЕсли Тип(Элемент) = Тип("ТабличноеПоле") Тогда

			Для Каждого Колонка Из Элемент.Колонки Цикл

				Если Найти(Колонка.Имя, "УСН") > 0 Тогда
					Если не ПоказыватьВДокументахСчетаУчета и Найти(Колонка.Имя, "Счет")>0 Тогда
						Колонка.Видимость = ложь;
					Иначе
						Колонка.Видимость = ВидимостьУСН;
                    
					КонецЕсли;
				ИначеЕсли Найти(Колонка.Имя, "НУ") > 0 Тогда
					Если не ПоказыватьВДокументахСчетаУчета и Найти(Колонка.Имя, "Счет")>0 Тогда
						Колонка.Видимость = ложь;
					Иначе
						Колонка.Видимость = ВидимостьНУ;
                    КонецЕсли;
				КонецЕсли;

			КонецЦикла;

		ИначеЕсли Тип(Элемент) = Тип("Панель") Тогда

			Для Каждого Страница Из Элемент.Страницы Цикл

				Если Найти(Страница.Имя, "УСН") > 0 Тогда
					Страница.Видимость = ВидимостьУСН;

				ИначеЕсли Найти(Страница.Имя, "НУ") > 0 Тогда
					Страница.Видимость = ВидимостьНУ;

				КонецЕсли;

			КонецЦикла;

		КонецЕсли;

	КонецЦикла

КонецПроцедуры

Функция ВидимостьКнопкиКУДиР(ДокументОбъект) Экспорт //~
	
	Если НЕ ПрименениеУСН(ДокументОбъект.Организация, ДокументОбъект.Дата) Тогда
		Возврат Ложь;
	Конецесли;
	МетаданныеДокумента = ДокументОбъект.Метаданные();
	
	ОтражатьВНалоговомУчете = ?(МетаданныеДокумента.Реквизиты.Найти("ОтражатьВНалоговомУчете") <> Неопределено, ДокументОбъект.ОтражатьВНалоговомУчете, Истина);
	Если НЕ ОтражатьВНалоговомУчете Тогда
		Возврат Ложь;
	Конецесли;
	
	Параметры = Новый Структура("Ссылка, ВидОперации", ДокументОбъект, ДокументОбъект.ВидОперации);
	
	ВыручкаСНТТ = ?(МетаданныеДокумента.Реквизиты.Найти("ВыручкаСНТТ") <> Неопределено, ДокументОбъект.ВыручкаСНТТ, Ложь);
	Параметры.Вставить("ВыручкаСНТТ", ВыручкаСНТТ);
	
	ВидОперацииДДС = ВидОперацииДДС(Параметры);
	
	Если (ПрименениеУСНДоходы(ДокументОбъект.Организация, ДокументОбъект.Дата)) Тогда
        Возврат Истина;
	ИначеЕсли ВидОперацииДДС = "ПрочееДДС" Тогда
		Возврат Истина;
	Иначе
		Возврат Ложь;
	КонецЕсли;
	
КонецФункции
	
#КонецЕсли

////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ ЗАПОЛНЕНИЯ ФОРМЫ НАСТРОЙКИ КУДИР

// Процедура заполняет данные по УСН в платежных документах
//
Процедура ЗаполнитьНастройкуКУДиР(ДокументОбъект, ЗадатьВопрос = Ложь) Экспорт //~

	Если НЕ ПрименениеУСН(ДокументОбъект.Организация, ДокументОбъект.Дата) Тогда
		ДокументОбъект.Графа4_УСН			 = 0;
		ДокументОбъект.Графа5_УСН			 = 0;
		ДокументОбъект.Графа6_УСН			 = 0;
		ДокументОбъект.Графа7_УСН     		 = 0;
		ДокументОбъект.НДС_УСН        		 = 0;
		ДокументОбъект.ДоходыЕНВД_УСН 		 = Ложь;
		ДокументОбъект.РасходыЕНВД_УСН		 = Ложь;
		ДокументОбъект.РучнаяНастройка_УСН	 = Ложь;
		ДокументОбъект.Содержание_УСН 		 = "";
		Возврат;
	КонецЕсли;
	
	ВидОперации 	= ДокументОбъект.ВидОперации;
	ОбъектДоходы 	= ПрименениеУСНДоходы(ДокументОбъект.Организация, ДокументОбъект.Дата);
	ОбъектРасходы 	= НЕ ОбъектДоходы;
	
	СторнироватьДоходыПриВозврате = Истина;
	
	мВалютаРегламентированногоУчета = глЗначениеПеременной("ВалютаРегламентированногоУчета");
	
	Если ДокументОбъект.РучнаяНастройка_УСН Тогда
		Если НЕ ЗадатьВопрос Тогда
			Возврат;
		КонецЕсли;
		Если ОбщегоНазначения.ВопросПерезаполнитьКУДиР() Тогда
			ДокументОбъект.РучнаяНастройка_УСН = Ложь;
		Иначе
			Возврат;
		КонецЕсли;		
	КонецЕсли;
	
	Если ТипЗнч(ДокументОбъект) = Тип("ДокументОбъект.ПоступлениеДенежныхДокументов") Тогда
		СуммаДляКУДиР = ДокументОбъект.ДенежныеДокументы.Итог("Сумма");
	ИначеЕсли ТипЗнч(ДокументОбъект) = Тип("ДокументОбъект.ВыдачаДенежныхДокументов") Тогда
		СуммаДляКУДиР = ДокументОбъект.ДенежныеДокументы.Итог("Стоимость");
	Иначе
		СуммаДляКУДиР = ДокументОбъект.СуммаДокумента;
	КонецЕсли;
	Если НЕ ДокументОбъект.ВалютаДокумента = мВалютаРегламентированногоУчета Тогда
		КурсВалюты = МодульВалютногоУчета.ПолучитьКурсВалюты(ДокументОбъект.ВалютаДокумента, ДокументОбъект.Дата);
		СуммаДляКУДиР = МодульВалютногоУчета.ПересчитатьИзВалютыВВалюту(СуммаДляКУДиР, ДокументОбъект.ВалютаДокумента, глЗначениеПеременной("ВалютаРегламентированногоУчета"), КурсВалюты.Курс, 1, КурсВалюты.Кратность, 1);
	КонецЕсли;
	
	Графа4_УСН = 0;
	Графа5_УСН = 0;
	Графа6_УСН = 0;
	Графа7_УСН = 0;
	ДоходыЕНВД_УСН = Ложь;
	Содержание_УСН  = "" + ВидОперации + ".";

	Если ВидОперации = Перечисления.ВидыОперацийПКО.ПриходДенежныхСредствРозничнаяВыручка Тогда
		МетаданныеДокумента = ДокументОбъект.Метаданные();
		НТТ = ?(МетаданныеДокумента.Реквизиты.Найти("ВыручкаСНТТ") <> Неопределено, ДокументОбъект.ВыручкаСНТТ, Ложь);
	Иначе
		НТТ = Ложь;
	КонецЕсли;
	
	//Поступление
	Если (ВидОперации = Перечисления.ВидыОперацийПоступлениеБезналичныхДенежныхСредств.ВозвратДенежныхСредствПоставщиком) или //Доходы
		(ВидОперации = Перечисления.ВидыОперацийПКО.ПолучениеНаличныхДенежныхСредствВБанке) Тогда
		Графа4_УСН      = 0;
		Графа5_УСН      = 0;
		
	ИначеЕсли (ВидОперации = Перечисления.ВидыОперацийПКО.ВозвратДенежныхСредствПоставщиком ИЛИ
		ВидОперации = Перечисления.ВидыОперацийПКО.ВозвратДенежныхСредствПодотчетником) И ОбъектДоходы Тогда
		Содержание_УСН = "Возврат денежных средств. Основание: " + ДокументОбъект.Основание + ".";
		

	ИначеЕсли (ВидОперации = Перечисления.ВидыОперацийПоступлениеБезналичныхДенежныхСредств.ПоступленияОтПродажиИностраннойВалюты) или
		(ВидОперации = Перечисления.ВидыОперацийПоступлениеБезналичныхДенежныхСредств.ПриобретениеИностраннойВалюты) Тогда
		Графа4_УСН = СуммаДляКУДиР;
		Графа5_УСН      = 0;
		
	ИначеЕсли (ВидОперации = Перечисления.ВидыОперацийПоступлениеБезналичныхДенежныхСредств.ПрочееПоступлениеБезналичныхДенежныхСредств ИЛИ
		ВидОперации = Перечисления.ВидыОперацийПКО.ПриходДенежныхСредствПрочее) Тогда
		Содержание_УСН = "Прочий приход денежных средств: " + ДокументОбъект.СтатьяДвиженияДенежныхСредств + ".";
		Графа4_УСН = СуммаДляКУДиР;
		Графа5_УСН = СуммаДляКУДиР;
		
	ИначеЕсли (ВидОперации = Перечисления.ВидыОперацийПоступлениеБезналичныхДенежныхСредств.РасчетыПоКредитамИЗаймам 
		ИЛИ ВидОперации=Перечисления.ВидыОперацийПКО.РасчетыПоКредитамИЗаймамСКонтрагентами) Тогда
		Содержание_УСН = "Расчеты по кредитам и займам с """ + ПН(ДокументОбъект.Контрагент) + """";
		Если ДокументОбъект.РасшифровкаПлатежа.Количество() > 0 Тогда
			Содержание_УСН = Содержание_УСН + " по договору """ + ДокументОбъект.РасшифровкаПлатежа[0].ДоговорКонтрагента + """.";
		КонецЕсли;
		Графа4_УСН = СуммаДляКУДиР;
		
		СчетУчетаРасчетовСКонтрагентом = ДокументОбъект.РасшифровкаПлатежа[0].СчетУчетаРасчетовСКонтрагентом;
		
		Если ЗначениеЗаполнено(СчетУчетаРасчетовСКонтрагентом) И 
			((СчетУчетаРасчетовСКонтрагентом.ПринадлежитЭлементу(ПланыСчетов.Хозрасчетный.РасчетыПоКраткосрочнымКредитамИЗаймам)) ИЛИ
			(СчетУчетаРасчетовСКонтрагентом.ПринадлежитЭлементу(ПланыСчетов.Хозрасчетный.РасчетыПоДолгосрочнымКредитамИЗаймам))) Тогда
			
			Графа5_УСН = 0;
		Иначе
			
			Графа5_УСН = СуммаДляКУДиР;
		КонецЕсли;
		
		
	ИначеЕсли ВидОперации = Перечисления.ВидыОперацийПоступлениеБезналичныхДенежныхСредств.ПрочиеРасчетыСКонтрагентами Тогда
		Содержание_УСН = "Расчеты с """ + ПН(ДокументОбъект.Контрагент) + """";
		Если ДокументОбъект.РасшифровкаПлатежа.Количество() > 0 Тогда
			Содержание_УСН = Содержание_УСН + " по договору """ + ДокументОбъект.РасшифровкаПлатежа[0].ДоговорКонтрагента + """.";
		КонецЕсли;
		Графа4_УСН = СуммаДляКУДиР;
		Графа5_УСН = СуммаДляКУДиР;
		
	ИначеЕсли ВидОперации = Перечисления.ВидыОперацийПКО.ОплатаПокупателя
		ИЛИ ВидОперации = Перечисления.ВидыОперацийПоступлениеБезналичныхДенежныхСредств.ОплатаПокупателя
		ИЛИ ВидОперации = Перечисления.ВидыОперацийПоступлениеБезналичныхДенежныхСредств.ПоступлениеОплатыПоБанковскимКредитам 
		ИЛИ ВидОперации = Перечисления.ВидыОперацийПоступлениеБезналичныхДенежныхСредств.ПоступлениеОплатыПоПлатежнымКартам Тогда
		Содержание_УСН = "Оплата от покупателя """ + ПН(ДокументОбъект.Контрагент) + """.";
		Графа4_УСН = СуммаДляКУДиР;
		Графа5_УСН = СуммаДляКУДиР;
		
	ИначеЕсли ВидОперации = Перечисления.ВидыОперацийПКО.ПриходДенежныхСредствРозничнаяВыручка И (ОбъектДоходы ИЛИ НТТ) Тогда
		Содержание_УСН = "Прием розничной выручки с торговой точки """ + ДокументОбъект.Контрагент + """.";
				
		Графа4_УСН = СуммаДляКУДиР;
		Графа5_УСН = СуммаДляКУДиР;
		
	//денежные документы
	ИначеЕсли (ВидОперации = Перечисления.ВидыОперацийПоступлениеДенежныхДокументов.ПоступлениеОтПоставщика) Тогда
		
		Содержание_УСН = "Поступление денежных документов. Принято от """ + ПН(ДокументОбъект.Контрагент) + """.";
		Графа4_УСН = СуммаДляКУДиР;
		Графа5_УСН = 0;
		
	ИначеЕсли (ВидОперации = Перечисления.ВидыОперацийПоступлениеДенежныхДокументов.ПрочееПоступление) Тогда
		
		Содержание_УСН = "Поступление денежных документов. Принято от """ + ДокументОбъект.Контрагент + """.";
		Графа4_УСН = СуммаДляКУДиР;
		Графа5_УСН = 0;

	ИначеЕсли (ВидОперации = Перечисления.ВидыОперацийВыдачаДенежныхДокументов.ПрочаяВыдача) И ОбъектРасходы Тогда
		
		Содержание_УСН = "Выдача денежных документов. Выдано """ + ДокументОбъект.Контрагент + """.";
		Графа6_УСН = СуммаДляКУДиР;
			
	ИначеЕсли (ВидОперации = Перечисления.ВидыОперацийВыдачаДенежныхДокументов.ВозвратПоставщику) Тогда
		
		Содержание_УСН = "Возврат денежных документов контрагенту """ + ПН(ДокументОбъект.Контрагент) + """.";
		Графа4_УСН = - СуммаДляКУДиР;
		Графа5_УСН = 0;
		
	//Списание
	ИначеЕсли (ВидОперации = Перечисления.ВидыОперацийППИсходящее.ВозвратДенежныхСредствПокупателю
		ИЛИ ВидОперации = Перечисления.ВидыОперацийСписаниеБезналичныхДенежныхСредств.ВозвратДенежныхСредствПокупателю) 
		И ОбъектДоходы 
		И СторнироватьДоходыПриВозврате Тогда //Доходы
		Содержание_УСН = "Возврат денежных средств покупателю.";
		Графа4_УСН      = -СуммаДляКУДиР;
		Графа5_УСН      = -СуммаДляКУДиР;
		Графа6_УСН      = 0;
		
	ИначеЕсли (ВидОперации = Перечисления.ВидыОперацийРКО.ВозвратДенежныхСредствПокупателю) 
		И ОбъектДоходы 
		И СторнироватьДоходыПриВозврате Тогда
		Содержание_УСН = "Возврат денежных средств. Основание: " + ДокументОбъект.Основание + ".";
		Графа4_УСН      = -СуммаДляКУДиР;
		Графа5_УСН      = -СуммаДляКУДиР;
		
	ИначеЕсли (ВидОперации = Перечисления.ВидыОперацийППИсходящее.ПрочееСписаниеБезналичныхДенежныхСредств
		ИЛИ ВидОперации = Перечисления.ВидыОперацийСписаниеБезналичныхДенежныхСредств.ПрочееСписаниеБезналичныхДенежныхСредств
		ИЛИ ВидОперации = Перечисления.ВидыОперацийРКО.РасходДенежныхСредствПрочее) И ОбъектРасходы Тогда
		Содержание_УСН = "Прочий расход денежных средств: " + ДокументОбъект.СтатьяДвиженияДенежныхСредств + ".";
		Графа6_УСН      = СуммаДляКУДиР;
		
		//Если 91 счет, попробуем классифицировать расход
		Если ТипЗнч(ДокументОбъект.СубконтоДт1) = Тип("СправочникСсылка.ПрочиеДоходыИРасходы") Тогда
			Если ДокументОбъект.СубконтоДт1.ВидПрочихДоходовИРасходов = Перечисления.ВидыПрочихДоходовИРасходов.РасходыНаУслугиБанков Тогда
				Содержание_УСН = "Расходы, связанные с оплатой услуг, оказываемых кредитными организациями.";				
				Графа7_УСН = СуммаДляКУДиР;
			КонецЕсли;
		КонецЕсли;
		
	ИначеЕсли (ВидОперации=Перечисления.ВидыОперацийППИсходящее.РасчетыПоКредитамИЗаймамСКонтрагентами 
		ИЛИ ВидОперации=Перечисления.ВидыОперацийСписаниеБезналичныхДенежныхСредств.РасчетыПоКредитамИЗаймам
		ИЛИ ВидОперации=Перечисления.ВидыОперацийРКО.РасчетыПоКредитамИЗаймамСКонтрагентами) И ОбъектРасходы Тогда
		Содержание_УСН = "Расчеты по кредитам и займам с """ + ПН(ДокументОбъект.Контрагент) + """";
		Если ДокументОбъект.РасшифровкаПлатежа.Количество() > 0 Тогда
			Содержание_УСН = Содержание_УСН + " по договору """ + ДокументОбъект.РасшифровкаПлатежа[0].ДоговорКонтрагента + """.";
		КонецЕсли;
		Графа6_УСН      = СуммаДляКУДиР;
		
		СчетУчетаРасчетовСКонтрагентом = ДокументОбъект.РасшифровкаПлатежа[0].СчетУчетаРасчетовСКонтрагентом;
		
		Если (СчетУчетаРасчетовСКонтрагентом = ПланыСчетов.Хозрасчетный.ПроцентыПоДолгосрочнымЗаймам) ИЛИ
			(СчетУчетаРасчетовСКонтрагентом = ПланыСчетов.Хозрасчетный.ПроцентыПоДолгосрочнымЗаймамВал) ИЛИ
			(СчетУчетаРасчетовСКонтрагентом = ПланыСчетов.Хозрасчетный.ПроцентыПоКраткосрочнымЗаймам) ИЛИ
			(СчетУчетаРасчетовСКонтрагентом = ПланыСчетов.Хозрасчетный.ПроцентыПоКраткосрочнымЗаймамВал) ИЛИ
			(СчетУчетаРасчетовСКонтрагентом = ПланыСчетов.Хозрасчетный.ПроцентыПоКраткосрочнымКредитам) ИЛИ
			(СчетУчетаРасчетовСКонтрагентом = ПланыСчетов.Хозрасчетный.ПроцентыПоКраткосрочнымКредитамВал) ИЛИ
			(СчетУчетаРасчетовСКонтрагентом = ПланыСчетов.Хозрасчетный.ПроцентыПоДолгосрочнымКредитам) ИЛИ
			(СчетУчетаРасчетовСКонтрагентом = ПланыСчетов.Хозрасчетный.ПроцентыПоДолгосрочнымКредитамВал) ИЛИ
			(СчетУчетаРасчетовСКонтрагентом = ПланыСчетов.Хозрасчетный.ПроцентыПоКраткосрочнымКредитам) ИЛИ
			(СчетУчетаРасчетовСКонтрагентом = ПланыСчетов.Хозрасчетный.ПроцентыПоКраткосрочнымКредитамВал) Тогда
			
			Содержание_УСН = Содержание_УСН + " Уплата процентов.";
			Графа7_УСН      = СуммаДляКУДиР;
			
		КонецЕсли;
				
	ИначеЕсли (ВидОперации=Перечисления.ВидыОперацийППИсходящее.ПрочиеРасчетыСКонтрагентами
		ИЛИ ВидОперации=Перечисления.ВидыОперацийСписаниеБезналичныхДенежныхСредств.ПрочиеРасчетыСКонтрагентами) И ОбъектРасходы Тогда
		Содержание_УСН = "Расчеты с """ + ПН(ДокументОбъект.Контрагент) + """";
		Если ДокументОбъект.РасшифровкаПлатежа.Количество() > 0 Тогда
			Содержание_УСН = Содержание_УСН + " по договору """ + ДокументОбъект.РасшифровкаПлатежа[0].ДоговорКонтрагента + """.";
		КонецЕсли;
		Графа6_УСН      = СуммаДляКУДиР;
		
	КонецЕсли;
	
	ДокументОбъект.Графа4_УСН 		= Графа4_УСН;
	ДокументОбъект.Графа5_УСН 		= Графа5_УСН;
	ДокументОбъект.Графа6_УСН 		= Графа6_УСН;
	ДокументОбъект.Графа7_УСН      	= Графа7_УСН;
	ДокументОбъект.НДС_УСН         	= 0;
	ДокументОбъект.ДоходыЕНВД_УСН  	= ДоходыЕНВД_УСН;
	ДокументОбъект.РасходыЕНВД_УСН 	= Ложь;
	ДокументОбъект.Содержание_УСН  	= Содержание_УСН;
	//-
	
КонецПроцедуры // ЗаполнитьНастройкуКнигиУСН()

////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// ВСПОМОГАТЕЛЬНЫЕ ПРОЦЕДУРЫ

Функция ПН(Контрагент) //Полное наименование контрагента
	
	Возврат ?(ЗначениеЗаполнено(Контрагент.НаименованиеПолное), Контрагент.НаименованиеПолное, Контрагент.Наименование); 
	
КонецФункции

Функция СодержаниеКУДиР(СтруктураШапкиДокумента, ВидОперации, МетаданныеДокумента)
	
	СтрокаСодержание = "";
	ДокументСсылка = СтруктураШапкиДокумента.Ссылка;
	
	Если ВидОперации = "Поступление" Тогда
		
		Если ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.АвансовыйОтчет") Тогда
			СтрокаСодержание = "Представлен авансовый отчет: " + ДокументСсылка.ФизЛицо + 
			", назначение аванса """ + СокрЛП(ДокументСсылка.НазначениеАванса) + """. ";
		ИначеЕсли ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.ПоступлениеДопРасходов") Тогда
			СтрокаСодержание = "Контрагентом """ + ПН(ДокументСсылка.Контрагент) + """ по договору """ +
			ДокументСсылка.ДоговорКонтрагента +	""" оказаны услуги """ + СокрЛП(ДокументСсылка.Содержание) + """. ";
		ИначеЕсли ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.ПоступлениеТоваровУслуг") Тогда
			Если ДокументСсылка.Услуги.Количество() = 0 Тогда
				СтрокаСодержание = "Поступление ТМЦ от """ + ПН(ДокументСсылка.Контрагент) + """ по договору """ +
				ДокументСсылка.ДоговорКонтрагента +	""". ";
			ИначеЕсли ДокументСсылка.Товары.Количество() = 0 Тогда
				СтрокаСодержание = "Контрагентом """ + ПН(ДокументСсылка.Контрагент) + """ оказаны услуги по договору """ +
				ДокументСсылка.ДоговорКонтрагента +	""". ";
			Иначе
				СтрокаСодержание = "Поступление ТМЦ и услуг от """ + ПН(ДокументСсылка.Контрагент) + """ по договору """ +
				ДокументСсылка.ДоговорКонтрагента +	""". ";
			КонецЕсли;
		Иначе
			СтрокаСодержание = МетаданныеДокумента.Синоним + ". ";
		КонецЕсли;
		
	ИначеЕсли ВидОперации = "Оплата" Тогда
		
		Если ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.АвансовыйОтчет") Тогда
			СтрокаСодержание = "Подотчетным лицом: " + ДокументСсылка.ФизЛицо + 
			", назначение аванса """ + СокрЛП(ДокументСсылка.НазначениеАванса) + """. ";
			Если ДокументСсылка.ОплатаПоставщикам.Количество() = 1 Тогда
				СтрокаСодержание = СтрокаСодержание + " произведена оплата поставщику """ + 
				ПН(ДокументСсылка.ОплатаПоставщикам[0].Контрагент) + """ по договору """ + 
				ДокументСсылка.ОплатаПоставщикам[0].ДоговорКонтрагента + """. ";
			Иначе
				СтрокаСодержание = СтрокаСодержание + " произведена оплата поставщикам. ";
			КонецЕсли;
		ИначеЕсли Не МетаданныеДокумента.ТабличныеЧасти.Найти("РасшифровкаПлатежа") = Неопределено Тогда
			Если ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.РасходныйКассовыйОрдер") Тогда
				СтрокаСодержание = "Выдача из кассы: оплата поставщику";
			Иначе
				СтрокаСодержание = "Списание с р/с: оплата поставщику";
			КонецЕсли;
			Если ДокументСсылка.РасшифровкаПлатежа.Количество() > 0 Тогда
				СтрокаСодержание = СтрокаСодержание + " """ + 
				ПН(ДокументСсылка.Контрагент) + """ по договору """ + 
				ДокументСсылка.РасшифровкаПлатежа[0].ДоговорКонтрагента + """. ";
			Иначе
				СтрокаСодержание = СтрокаСодержание + " """ + ДокументСсылка.Контрагент + """. ";
			КонецЕсли;
		Иначе
			СтрокаСодержание = МетаданныеДокумента.Синоним + " (оплата поставщику). ";
		КонецЕсли;
		
	ИначеЕсли ВидОперации = "ОплатаОтПокупателя" Тогда
		
		Если Не МетаданныеДокумента.ТабличныеЧасти.Найти("РасшифровкаПлатежа") = Неопределено Тогда
			Если ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.ПриходныйКассовыйОрдер") Тогда
				СтрокаСодержание = "Поступление в кассу: оплата от покупателя";
			Иначе
				СтрокаСодержание = "Поступление на р/с: оплата от покупателя";
			КонецЕсли;
			Если ДокументСсылка.РасшифровкаПлатежа.Количество() > 0 Тогда
				СтрокаСодержание = СтрокаСодержание + " """ + 
				ПН(ДокументСсылка.Контрагент) + """ по договору """ + 
				ДокументСсылка.РасшифровкаПлатежа[0].ДоговорКонтрагента + """. ";
			Иначе
				СтрокаСодержание = СтрокаСодержание + " """ + ДокументСсылка.Контрагент + """. ";
			КонецЕсли;
		Иначе
			СтрокаСодержание = МетаданныеДокумента.Синоним + " (оплата от покупателя). ";
		КонецЕсли;
		
	ИначеЕсли ВидОперации = "ВозвратОтПодотчетника" Тогда
		
		Если ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.ПоступлениеДенежныхДокументов") Тогда
			СтрокаСодержание = "Поступление денежных документов: принято от подотчетного лица " + ДокументСсылка.Контрагент + ". ";
		Иначе
			СтрокаСодержание = "Поступление в кассу: возврат аванса подотчетным лицом " + ДокументСсылка.Контрагент + ". ";
		КонецЕсли;
		
	ИначеЕсли ВидОперации = "ВозвратОтРаботника" Тогда
		
		СтрокаСодержание = "Поступление в кассу: возврат аванса работником " + ДокументСсылка.Контрагент + ". ";
		                   
	ИначеЕсли ВидОперации = "ВозвратОтПоставщика" Тогда
		
		Если Не МетаданныеДокумента.ТабличныеЧасти.Найти("РасшифровкаПлатежа") = Неопределено Тогда
			Если ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.ПриходныйКассовыйОрдер") Тогда
				СтрокаСодержание = "Поступление в кассу: возврат денежных средств поставщиком";
			Иначе
				СтрокаСодержание = "Поступление на р/с: возврат денежных средств поставщиком";
			КонецЕсли;
			Если ДокументСсылка.РасшифровкаПлатежа.Количество() > 0 Тогда
				СтрокаСодержание = СтрокаСодержание + " """ + 
				ПН(ДокументСсылка.Контрагент) + """ по договору """ + 
				ДокументСсылка.РасшифровкаПлатежа[0].ДоговорКонтрагента + """. ";
			Иначе
				СтрокаСодержание = СтрокаСодержание + " """ + ДокументСсылка.Контрагент + """. ";
			КонецЕсли;
		Иначе
			СтрокаСодержание = МетаданныеДокумента.Синоним + " (возврат денежных средств поставщиком). ";
		КонецЕсли;
		
	ИначеЕсли (ВидОперации = "ВыручкаККМ") ИЛИ (ВидОперации = "ВыручкаСНТТ") Тогда
		
		Если ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.ПриходныйКассовыйОрдер") Тогда
			СтрокаСодержание = "Поступление в кассу: выручка в торговой точке  " + ДокументСсылка.Контрагент + ". ";
		Иначе
			СтрокаСодержание = МетаданныеДокумента.Синоним + ". ";
		КонецЕсли;
		
	ИначеЕсли ВидОперации = "ВыдачаПодотчетнику" Тогда
		
		Если ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.РасходныйКассовыйОрдер") Тогда
			СтрокаСодержание = "Выдача из кассы: выдано под отчет сотруднику " + ДокументСсылка.Контрагент + ". ";
		ИначеЕсли ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.ВыдачаДенежныхДокументов") Тогда
			СтрокаСодержание = "Выдача денежных документов: выдано под отчет сотруднику " + ДокументСсылка.Контрагент + ". ";	
		ИначеЕсли МетаданныеДокумента.Реквизиты.Найти("ФизЛицо") <> Неопределено Тогда
			СтрокаСодержание = "Списание с р/с: перечислено под отчет сотруднику " + ДокументСсылка.ФизЛицо + ". ";
		Иначе
			СтрокаСодержание = МетаданныеДокумента.Синоним + " (выдача под отчет сотруднику). ";			
		КонецЕсли;
		
	ИначеЕсли ВидОперации = "ВыплатаЗП" Тогда
		Если ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.РасходныйКассовыйОрдер") Тогда
			СтрокаСодержание = "Выдача из кассы: выплата заработной платы по ведомости. ";
		Иначе
			СтрокаСодержание = "Списание с р/с: перечисление заработной платы. ";
		КонецЕсли;
		
	ИначеЕсли ВидОперации = "ВыплатаЗПРаботнику" Тогда
		
		СтрокаСодержание = "Выдача из кассы: выплата заработной платы работику " + ДокументСсылка.Контрагент + ". ";
				
	ИначеЕсли ВидОперации = "ВыплатаДепонентов" Тогда
		
		СтрокаСодержание = "Выдача из кассы: выплата депонированной заработной платы. ";
		
	ИначеЕсли ВидОперации = "ПеречислениеНалога" Тогда
		
		Если ДокументСсылка.СчетУчетаРасчетовСКонтрагентом = ПланыСчетов.Хозрасчетный.НДФЛ Тогда
			СтрокаСодержание = "Списание с р/с: перечисление НДФЛ работников. ";
		Иначе
			СтрокаСодержание = "Списание с р/с: перечисление налога (" + ДокументСсылка.СчетУчетаРасчетовСКонтрагентом.Код + 
			" " + ДокументСсылка.СчетУчетаРасчетовСКонтрагентом.Наименование + "). ";
		КонецЕсли;
		
	ИначеЕсли ВидОперации = "ВозвратПокупателю" Тогда
		
		Если Не МетаданныеДокумента.ТабличныеЧасти.Найти("РасшифровкаПлатежа") = Неопределено Тогда
			Если ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.РасходныйКассовыйОрдер") Тогда
				СтрокаСодержание = "Выдача из кассы: возврат денежных средств покупателю";
			Иначе
				СтрокаСодержание = "Списание с р/с: возврат денежных средств покупателю";
			КонецЕсли;
			Если ДокументСсылка.РасшифровкаПлатежа.Количество() > 0 Тогда
				СтрокаСодержание = СтрокаСодержание + " """ + 
				ПН(ДокументСсылка.Контрагент) + """ по договору """ + 
				ДокументСсылка.РасшифровкаПлатежа[0].ДоговорКонтрагента + """. ";
			Иначе
				СтрокаСодержание = СтрокаСодержание + " """ + ДокументСсылка.Контрагент + """. ";
			КонецЕсли;
		Иначе
			СтрокаСодержание = МетаданныеДокумента.Синоним + " (возврат денежных средств покупателю). ";
		КонецЕсли;
		
	ИначеЕсли ВидОперации = "РеализацияУслуг" Тогда
		
		Если ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.АктОбОказанииПроизводственныхУслуг") Тогда
			СтрокаСодержание = "Акт об оказании производственных услуг; контрагент """ + ПН(ДокументСсылка.Контрагент) + """ по договору """ +
			ДокументСсылка.ДоговорКонтрагента +	""". ";
		ИначеЕсли ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.ПередачаНМА") Тогда 
			СтрокаСодержание = "Передача нематериальных активов контрагенту """ + ПН(ДокументСсылка.Контрагент) + """ по договору """ +
			ДокументСсылка.ДоговорКонтрагента +	""". ";
		ИначеЕсли ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.ПередачаОС") Тогда 
			СтрокаСодержание = "Передача основных средств контрагенту """ + ПН(ДокументСсылка.Контрагент) + """ по договору """ +
			ДокументСсылка.ДоговорКонтрагента +	""". ";
		ИначеЕсли ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.РеализацияУслугПоПереработке") Тогда 
			СтрокаСодержание = "Оказание услуг по переработке; контрагент """ + ПН(ДокументСсылка.Контрагент) + """ по договору """ +
			ДокументСсылка.ДоговорКонтрагента +	""". ";
		ИначеЕсли ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.ОтчетКомитентуОПродажах")  Тогда
			СтрокаСодержание = "Оказание посреднических услуг в торговле (комиссионное вознаграждение); комитент """ + ПН(ДокументСсылка.Контрагент) + """ по договору """ +
			ДокументСсылка.ДоговорКонтрагента +	""". ";
		Иначе
			СтрокаСодержание = МетаданныеДокумента.Синоним + ". ";
		КонецЕсли;
		
	ИначеЕсли ВидОперации = "ВозвратПоставщику" Тогда
		
		СтрокаСодержание = "Возврат товаров поставщику """ + ПН(ДокументСсылка.Контрагент) + """ по договору """ +
		ДокументСсылка.ДоговорКонтрагента +	""". ";
		
	ИначеЕсли ВидОперации = "НачислениеЗП" Тогда
		
		СтрокаСодержание = "Начисление заработной платы и налогов с ФОТ за " + Формат(ДокументСсылка.ПериодРегистрации,"ДФ=""ММММ гггг""") + ". ";
		
	ИначеЕсли (ВидОперации = "УслугаКомиссионера") ИЛИ (ВидОперации = "ЗачетВознагражденияКомиссионера") ИЛИ (ВидОперации = "ЗачетВознагражденияКомиссионераНДС") Тогда
		
		СтрокаСодержание = "Отчет комиссионера """ + ПН(ДокументСсылка.Контрагент) + """ по договору """ +
		ДокументСсылка.ДоговорКонтрагента +	"""; ";
		Если ВидОперации = "ЗачетВознагражденияКомиссионера" Тогда
			СтрокаСодержание = СтрокаСодержание + "вознаграждение комиссионера зачтено из выручки от реализации. ";
		ИначеЕсли ВидОперации = "ЗачетВознагражденияКомиссионераНДС" Тогда
			СтрокаСодержание = СтрокаСодержание + "НДС с вознагражденя комиссионера зачтен из выручки от реализации. ";
		Иначе
			СтрокаСодержание = СтрокаСодержание + "оказаны посреднические услуги в торговле. "
		КонецЕсли;
					
	ИначеЕсли ВидОперации = "РеализацияКомиссионером" Тогда
		
		Если ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.ОтчетКомиссионераОПродажах") Тогда
			СтрокаСодержание = "Отчет комиссионера """ + ПН(ДокументСсылка.Контрагент) + """ по договору """ +
			ДокументСсылка.ДоговорКонтрагента +	"""; реализованы товары. ";
		ИначеЕсли ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.РеализацияОтгруженныхТоваров") Тогда
			СтрокаСодержание = "Реализованы товары, отгруженные """ + ПН(ДокументСсылка.Контрагент) + """ по договору """ +
			ДокументСсылка.ДоговорКонтрагента +	""". ";
		КонецЕсли;		
		
	ИначеЕсли ВидОперации = "Розница" Тогда
		
		Если ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.ОтчетОРозничныхПродажах") Тогда
			СтрокаСодержание = "Продажа в розницу в торговой точке " + ДокументСсылка.Склад + ". ";
		КонецЕсли;
		
	ИначеЕсли ВидОперации = "ВПереработку" Тогда
		
		СтрокаСодержание = "Передача материалов в переаботку """ + ПН(ДокументСсылка.Контрагент) + """ по договору """ +
		ДокументСсылка.ДоговорКонтрагента +	""". ";
			
	ИначеЕсли ВидОперации = "УслугаПоПереработке" Тогда
		
		СтрокаСодержание = "Поступление из переработки; контрагентом """ + ПН(ДокументСсылка.Контрагент) + """ оказаны услуги по переаботке, договор """ +
		ДокументСсылка.ДоговорКонтрагента +	""". ";
		
	ИначеЕсли ВидОперации = "ИзПереработки" Тогда
		
		СтрокаСодержание = "Поступление из переработки; списаны материалы, переданные в переаботку контрагенту """ + 
		ПН(ДокументСсылка.Контрагент) + """ по договору """ + ДокументСсылка.ДоговорКонтрагента + """. ";
		
	ИначеЕсли ВидОперации = "Реализация" Тогда
		
		Если ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.РеализацияТоваровУслуг") Тогда
			Если ДокументСсылка.Услуги.Количество() = 0 Тогда
				СтрокаСодержание = "Реализация товаров покупателю """ + ПН(ДокументСсылка.Контрагент) + """ по договору """ +
				ДокументСсылка.ДоговорКонтрагента +	""". ";
			ИначеЕсли ДокументСсылка.Товары.Количество() = 0 Тогда
				СтрокаСодержание = "Реализация услуг покупателю """ + ПН(ДокументСсылка.Контрагент) + """ по договору """ +
				ДокументСсылка.ДоговорКонтрагента +	""". ";
			Иначе
				СтрокаСодержание = "Реализация товаров и услуг покупателю """ + ПН(ДокументСсылка.Контрагент) + """ по договору """ +
				ДокументСсылка.ДоговорКонтрагента +	""". ";
			КонецЕсли;
		КонецЕсли;	
		
	ИначеЕсли ВидОперации = "Списание" Тогда
		
		Если ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.СписаниеТоваров") Тогда
			СтрокаСодержание = "Списание МПЗ со склада """ + ДокументСсылка.Склад + """ на недостачи и потери от порчи ценностей. ";
		ИначеЕсли ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.ПередачаМатериаловВЭксплуатацию") Тогда
			СтрокаСодержание = "Передача спецодежды и спецоснастки в эксплуатацию со склада """ + ДокументСсылка.Склад + """. ";
		ИначеЕсли (ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.ТребованиеНакладная")) Тогда
			СтрокаСодержание = "Требование накладная на списание материалов со склада """ + ДокументСсылка.Склад + """";
			//Если Не ДокументСсылка.СчетаУчетаЗатратВТаблице Тогда
			//	СтрокаСодержание = СтрокаСодержание + "; счет затрат " + ДокументСсылка.СчетЗатрат.Код + " """ + 
			//	ДокументСсылка.СчетЗатрат.Наименование + """";
			//КонецЕсли;
			СтрокаСодержание = СтрокаСодержание + ". ";
		ИначеЕсли (ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.ОтчетПроизводстваЗаСмену")) Тогда
			СтрокаСодержание = "Требование накладная на списание материалов со склада """ + ДокументСсылка.Склад + """";
			СтрокаСодержание = СтрокаСодержание + "; счет затрат " + ДокументСсылка.СчетЗатрат.Код + " """ + 
				ДокументСсылка.СчетЗатрат.Наименование + """";
			СтрокаСодержание = СтрокаСодержание + ". ";
		КонецЕсли;
		
	ИначеЕсли ВидОперации = "ПереносЗадолженности" Тогда
		Если (ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.ОплатаОтПокупателяПлатежнойКартой")) Тогда
			СтрокаСодержание = "Оплата платежной картой от покупателя """ + ПН(ДокументСсылка.Контрагент) + """. "
		Иначе
			СтрокаСодержание = "Перенос задолженности контрагента """ + ПН(ДокументСсылка.КонтрагентДебитор) + """. "
		Конецесли;
		
	ИначеЕсли ВидОперации = "ПроведениеВзаимозачетаДебитор" Тогда
		СтрокаСодержание = "Проведение взаимозачета между """ + ПН(ДокументСсылка.КонтрагентДебитор) + 
		"""(дебитор) и """ + ПН(ДокументСсылка.КонтрагентКредитор) + """(кредитор); по дебиторской задолженности. ";
		
	ИначеЕсли ВидОперации = "ПроведениеВзаимозачетаКредитор" Тогда
		СтрокаСодержание = "Проведение взаимозачета между """ + ПН(ДокументСсылка.КонтрагентДебитор) + 
		"""(дебитор) и """ + ПН(ДокументСсылка.КонтрагентКредитор) + """(кредитор); по кредиторской задолженности. ";
		
	ИначеЕсли ВидОперации = "СписаниеДебиторскойЗадолженности" Тогда
		СтрокаСодержание = "Списание дебиторской задолженности контрагента """ + ПН(ДокументСсылка.КонтрагентДебитор) + """. ";
		
	ИначеЕсли ВидОперации = "СписаниеКредиторскойЗадолженности" Тогда
		СтрокаСодержание = "Списание кредиторской задолженности контрагента """ + ПН(ДокументСсылка.КонтрагентДебитор) + """. "
		
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(СтрокаСодержание) Тогда
		СтрокаСодержание = МетаданныеДокумента.Синоним + ". ";
	КонецЕсли;
	
	Возврат СтрокаСодержание;
	
КонецФункции 

Процедура СформироватьЗаписьРасшифровки(НаборЗаписейРасшифровки, Расход, ЗаписьКУДиР, ВидОперации)
	
	Если Расход.ВидРасхода = Перечисления.ВидыРасходовУСН.Номенклатура Тогда
		Расход.Строки.Свернуть("Организация, ВидРасхода, СчетУчета, Валюта, ДоговорКонтрагента, 
								|РасчетныйДокумент, СтатусыПартийУСН, Партия, ОтражениеВУСН, СтатусыОплатыРасходовУСН,
								|РеквизитыДокументаОплаты",
								"Сумма, НДС");
	ИначеЕсли Расход.ВидРасхода = Перечисления.ВидыРасходовУСН.НДС Тогда
        Расход.Строки.Свернуть("Организация, ВидРасхода, СчетУчета, Валюта, ДоговорКонтрагента, 
								|РасчетныйДокумент, СтатусыПартийУСН, Партия, ОтражениеВУСН, СтатусыОплатыРасходовУСН,
								|РеквизитыДокументаОплаты",
								"Сумма, НДС");
								
	ИначеЕсли Расход.ВидРасхода = Перечисления.ВидыРасходовУСН.Услуги Тогда
        Расход.Строки.Свернуть("Организация, ВидРасхода, ЭлементРасхода, СчетУчета, Валюта, ДоговорКонтрагента, 
								|РасчетныйДокумент, СтатусыПартийУСН, Партия, ОтражениеВУСН, СтатусыОплатыРасходовУСН,
								|РеквизитыДокументаОплаты",
								"Сумма, НДС");
								
	ИначеЕсли Расход.ВидРасхода = Перечисления.ВидыРасходовУСН.ДопРасходы Тогда
		Расход.Строки.Свернуть("Организация, ВидРасхода, ДоговорКонтрагента, Партия, 
								|РасчетныйДокумент, ОтражениеВУСН, СтатусыОплатыРасходовУСН,
								|РеквизитыДокументаОплаты",
								"Сумма, НДС");
								
	ИначеЕсли Расход.ВидРасхода = Перечисления.ВидыРасходовУСН.Зарплата Тогда
		Расход.Строки.Свернуть("ВидРасхода, ЭлементРасхода, ОтражениеВУСН, РеквизитыДокументаОплаты",
								"Сумма, НДС");
	
	ИначеЕсли Расход.ВидРасхода = Перечисления.ВидыРасходовУСН.Налоги Тогда
		Расход.Строки.Свернуть("ВидРасхода, СчетУчета, ОтражениеВУСН, РеквизитыДокументаОплаты",
								"Сумма, НДС");
								
	КонецЕсли;
	
	Счетчик = 1;
	Для Каждого Строка Из Расход.Строки Цикл
		Запись 					= НаборЗаписейРасшифровки.Добавить();
		Запись.Период 			= ЗаписьКУДиР.Период;
		Запись.Регистратор 		= ЗаписьКУДиР.Регистратор;
		Запись.Активность 		= Истина;
		Запись.Организация 		= ЗаписьКУДиР.Организация;
		Запись.ВидРасхода 		= Строка.ВидРасхода;
		Запись.СтрокаДокумента 	= ЗаписьКУДиР.СтрокаДокумента;
		Запись.ЭтапПроведения 	= ЗаписьКУДиР.ЭтапПроведения;
		Запись.Графа7			= Строка.Сумма;
		Запись.СтрокаРасхода	= Счетчик;
		Счетчик = Счетчик + 1;
		
		Запись.РеквизитыПервичногоДокумента = ЗаписьКУДиР.РеквизитыПервичногоДокумента;
		Запись.РеквизитыДокументаОплаты		= Строка.РеквизитыДокументаОплаты;
		
		Запись.Графа7			= Строка.Сумма;
		
		Описание = "";
		Если Строка.ВидРасхода = Перечисления.ВидыРасходовУСН.Номенклатура Тогда
			Описание = "ТМЦ, учитываемые на счете: """ + Строка.СчетУчета + """ .";
			Если ТипЗнч(Строка.ДоговорКонтрагента) = Тип("СправочникСсылка.ДоговорыКонтрагентов") Тогда
				Описание = Описание + " Оплачено поставщику:""" + ПН(Строка.ДоговорКонтрагента.Владелец) + """.";
			ИначеЕсли ТипЗнч(Строка.ДоговорКонтрагента) = Тип("СправочникСсылка.ФизическиеЛица") Тогда
				Если ТипЗнч(Строка.РасчетныйДокумент) = Тип("ДокументСсылка.АвансовыйОтчет") Тогда
					Описание = Описание + " Оплачено подотчетным лицом:""" + Строка.ДоговорКонтрагента + """.";
				КонецЕсли;
			КонецЕсли;
		ИначеЕсли Строка.ВидРасхода = Перечисления.ВидыРасходовУСН.ДопРасходы Тогда
			Описание = "Доп. расходы по ТМЦ, списанным документом: """ + РеквизитыПервичногоДокумента(Строка.Партия,,, Истина) + """ .";
			Если ТипЗнч(Строка.ДоговорКонтрагента) = Тип("СправочникСсылка.ДоговорыКонтрагентов") Тогда
				Описание = Описание + " Оплачено поставщику:""" + ПН(Строка.ДоговорКонтрагента.Владелец) + """.";
			ИначеЕсли ТипЗнч(Строка.ДоговорКонтрагента) = Тип("СправочникСсылка.ФизическиеЛица") Тогда
				Если ТипЗнч(Строка.РасчетныйДокумент) = Тип("ДокументСсылка.АвансовыйОтчет") Тогда
					Описание = Описание + " Оплачено подотчетным лицом:""" + Строка.ДоговорКонтрагента + """.";
				КонецЕсли;
			КонецЕсли;
		ИначеЕсли Строка.ВидРасхода = Перечисления.ВидыРасходовУСН.Услуги Тогда
			Описание = "Услуги сторонних организаций";
			Если ЗначениеЗаполнено(Строка.ЭлементРасхода) Тогда
				Описание = Описание + ": """ + Строка.ЭлементРасхода + """";				
			КонецЕсли;
			Если ЗначениеЗаполнено(Строка.СчетУчета) Тогда
				Описание = Описание + ", отнесенные на счет: """ + Строка.СчетУчета + """.";				
			Иначе
				Описание = Описание + ".";
			КонецЕсли;
			Если ТипЗнч(Строка.ДоговорКонтрагента) = Тип("СправочникСсылка.ДоговорыКонтрагентов") Тогда
				Описание = Описание + " Оплачено поставщику: """ + ПН(Строка.ДоговорКонтрагента.Владелец) + """.";
			ИначеЕсли ТипЗнч(Строка.ДоговорКонтрагента) = Тип("СправочникСсылка.ФизическиеЛица") Тогда
				Если ТипЗнч(Строка.РасчетныйДокумент) = Тип("ДокументСсылка.АвансовыйОтчет") Тогда
					Описание = Описание + " Оплачено подотчетным лицом:""" + Строка.ДоговорКонтрагента + """.";
				КонецЕсли;
			КонецЕсли;
		ИначеЕсли Строка.ВидРасхода = Перечисления.ВидыРасходовУСН.РБП Тогда
			Описание = "Статья РБП: """ + Строка.ЭлементРасхода + """, общая сумма:" + Строка.ЭлементРасхода.Сумма + ", период списания с:" + Строка.ЭлементРасхода.ДатаНачалаСписания + " по:" + Строка.ЭлементРасхода.ДатаОкончанияСписания + ".";
			Если ТипЗнч(Строка.ДоговорКонтрагента) = Тип("СправочникСсылка.ДоговорыКонтрагентов") Тогда
				Описание = Описание + " Оплачено поставщику:""" + ПН(Строка.ДоговорКонтрагента.Владелец) + """.";
			ИначеЕсли ТипЗнч(Строка.ДоговорКонтрагента) = Тип("СправочникСсылка.ФизическиеЛица") Тогда
				Если ТипЗнч(Строка.РасчетныйДокумент) = Тип("ДокументСсылка.АвансовыйОтчет") Тогда
					Описание = Описание + " Оплачено подотчетным лицом:""" + Строка.ДоговорКонтрагента + """.";
				КонецЕсли;
			КонецЕсли;
		ИначеЕсли Строка.ВидРасхода = Перечисления.ВидыРасходовУСН.Зарплата Тогда
			Описание = "Оплата труда сотрудника: """ + Строка.ЭлементРасхода + """.";
		ИначеЕсли Строка.ВидРасхода = Перечисления.ВидыРасходовУСН.Налоги Тогда
			Описание = "Налоги и сборы: """ + Строка.СчетУчета.Наименование + " (счет учета " + Строка.СчетУчета + ")"".";
		ИначеЕсли Строка.ВидРасхода = Перечисления.ВидыРасходовУСН.НМА Тогда
			Описание = "НМА: """ + Строка.ЭлементРасхода + """.";
			
		ИначеЕсли Строка.ВидРасхода = Перечисления.ВидыРасходовУСН.НДС Тогда
			Описание = "НДС, предъявленный поставщиком";
			Если ЗначениеЗаполнено(Строка.СчетУчета) Тогда
				Если Строка.СчетУчета.Количественный Тогда
					Описание = Описание + " по приобретенным ТМЦ, учитываемым на счете: """ + Строка.СчетУчета + """.";
				Иначе
					Описание = Описание + " по услугам, отнесенным на счет: """ + Строка.СчетУчета + """.";
				КонецЕсли;				
			Иначе
				Описание = Описание + ".";
			КонецЕсли;
			
			Если ТипЗнч(Строка.ДоговорКонтрагента) = Тип("СправочникСсылка.ДоговорыКонтрагентов") Тогда
				Описание = Описание + " Оплачено поставщику: """ + ПН(Строка.ДоговорКонтрагента.Владелец) + """.";
			ИначеЕсли ТипЗнч(Строка.ДоговорКонтрагента) = Тип("СправочникСсылка.ФизическиеЛица") Тогда
				Если ТипЗнч(Строка.РасчетныйДокумент) = Тип("ДокументСсылка.АвансовыйОтчет") Тогда
					Описание = Описание + " Оплачено подотчетным лицом: """ + Строка.ДоговорКонтрагента + """.";
				КонецЕсли;
			КонецЕсли;
			
		ИначеЕсли (НЕ ЗначениеЗаполнено(Расход.ВидРасхода)) И (ВидОперации = "РаспределениеЕНВД") Тогда
			Описание = "Скорректированы результаты распределения предыдущих отчетных периодов.";
			
		КонецЕсли;
		
		Запись.Содержание = Описание;
		
	КонецЦикла;
	
КонецПроцедуры

Функция ПолучитьКоэффРаспределенияЕНВД(СтруктураШапкиДокумента, Период = "Квартал", Способ = "БУ")
	
	Коэфф = 0;
	НачальнаяДата = ?(Период = "Квартал", НачалоКвартала(СтруктураШапкиДокумента.Дата), НачалоГода(СтруктураШапкиДокумента.Дата));
	
	Если Способ = "БУ" Тогда
		Коэфф = НалоговыйУчет.КоэффициентРаспределенияРасходовПоВидамДеятельности(СтруктураШапкиДокумента.Организация, СтруктураШапкиДокумента.Дата, НачальнаяДата);
	Иначе
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("НачДата", НачальнаяДата);
		Запрос.УстановитьПараметр("КонДата", КонецМесяца(СтруктураШапкиДокумента.Дата));
		Запрос.УстановитьПараметр("Организация", СтруктураШапкиДокумента.Организация);
		Запрос.Текст =
		"ВЫБРАТЬ
		|	КнигаУчетаДоходовИРасходовОбороты.Графа5Оборот,
		|	КнигаУчетаДоходовИРасходовОбороты.ДоходЕНВДОборот,
		|	КнигаУчетаДоходовИРасходовОбороты.Графа4Оборот
		|ИЗ
		|	РегистрНакопления.КнигаУчетаДоходовИРасходов.Обороты(&НачДата, &КонДата, , Организация = &Организация) КАК КнигаУчетаДоходовИРасходовОбороты";
		
		Результат = Запрос.Выполнить().Выбрать();
		Если Результат.Следующий() Тогда
			
			ДоходЕНВДОборот = ?(НЕ ЗначениеЗаполнено(Результат.ДоходЕНВДОборот), 0, Результат.ДоходЕНВДОборот);
			Графа4Оборот 	= ?(НЕ ЗначениеЗаполнено(Результат.Графа4Оборот), 0, Результат.Графа4Оборот);
			Графа5Оборот 	= ?(НЕ ЗначениеЗаполнено(Результат.Графа5Оборот), 0, Результат.Графа5Оборот);
			
			Если Способ = "НУ" Тогда
				Коэфф = ?(ДоходЕНВДОборот + Графа5Оборот = 0, 0, Результат.ДоходЕНВДОборот/(ДоходЕНВДОборот + Графа5Оборот));
			Иначе
				Коэфф = ?(Графа4Оборот = 0, 0, Результат.ДоходЕНВДОборот/Графа4Оборот);
			КонецЕсли;			
		КонецЕсли;
	КонецЕсли;
	
	Возврат Коэфф;
	
КонецФункции

Процедура СкорректироватьРаспределениеПредыдущихКварталов(СтруктураШапкиДокумента, Коэфф, ТаблицаПринятых)
	
	ЗапросРасходы = Новый Запрос;
	ЗапросРасходы.УстановитьПараметр("НачДата", НачалоГода(СтруктураШапкиДокумента.Дата));
	ЗапросРасходы.УстановитьПараметр("КонДата", НачалоМесяца(СтруктураШапкиДокумента.Дата));
	ЗапросРасходы.УстановитьПараметр("Организация", СтруктураШапкиДокумента.Организация);
	ЗапросРасходы.УстановитьПараметр("НеРаспределено", Перечисления.СтатусыРасходовУСН.НеРаспределено);
	ЗапросРасходы.Текст = 
	"ВЫБРАТЬ
	|	СУММА(РасходыПриУСНОбороты.СуммаРасход) КАК СуммаРасход,
	|	РасходыПриУСНОбороты.Валюта,
	|	РасходыПриУСНОбороты.Регистратор
	|ИЗ
	|	РегистрНакопления.РасходыПриУСН.Обороты(&НачДата, &КонДата, Регистратор, Организация = &Организация) КАК РасходыПриУСНОбороты
	|ГДЕ
	|	РасходыПриУСНОбороты.СтатусыОплатыРасходовУСН = &НеРаспределено
	|	И РасходыПриУСНОбороты.Регистратор ССЫЛКА Документ.РегламентныеОперацииНалоговогоУчетаПоУСН
	|
	|СГРУППИРОВАТЬ ПО
	|	РасходыПриУСНОбороты.Валюта,
	|	РасходыПриУСНОбороты.Регистратор";
	
	Результат = ЗапросРасходы.Выполнить().Выбрать();
	ВсегоРаспределено = 0;
	Пока Результат.Следующий() Цикл
		СуммаРасход 	= ?(НЕ ЗначениеЗаполнено(Результат.СуммаРасход), 0, Результат.СуммаРасход);
		Валюта 			= ?(НЕ ЗначениеЗаполнено(Результат.Валюта), СтруктураШапкиДокумента.ВалютаРегламентированногоУчета, Результат.Валюта);
		Регистратор 	= ?(НЕ ЗначениеЗаполнено(Результат.Регистратор), СтруктураШапкиДокумента.Ссылка, Результат.Регистратор);
		Если Результат.Валюта = СтруктураШапкиДокумента.ВалютаРегламентированногоУчета Тогда
			ВсегоРаспределено = ВсегоРаспределено + СуммаРасход;
		Иначе
			СтруктураКурса = МодульВалютногоУчета.ПолучитьКурсВалюты(Валюта, Регистратор.Дата);
			ВсегоРаспределено = ВсегоРаспределено + МодульВалютногоУчета.ПересчитатьИзВалютыВВалюту(СуммаРасход, Валюта,
																				СтруктураШапкиДокумента.ВалютаРегламентированногоУчета,
																				СтруктураКурса.Курс, 1,
																				СтруктураКурса.Кратность, 1);	
		КонецЕсли;			
	КонецЦикла;
	
	Если ВсегоРаспределено = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ЗапросПринято = Новый Запрос;
	ЗапросПринято.УстановитьПараметр("НачДата", НачалоГода(СтруктураШапкиДокумента.Дата));
	ЗапросПринято.УстановитьПараметр("КонДата", НачалоМесяца(СтруктураШапкиДокумента.Дата));
	ЗапросПринято.УстановитьПараметр("Организация", СтруктураШапкиДокумента.Организация);
	ЗапросПринято.УстановитьПараметр("ЭтапПроведения", 2);
	ЗапросПринято.Текст = 
	"ВЫБРАТЬ
	|	СУММА(КнигаУчетаДоходовИРасходов.Графа7) КАК Графа7
	|ИЗ
	|	РегистрНакопления.КнигаУчетаДоходовИРасходов КАК КнигаУчетаДоходовИРасходов
	|ГДЕ
	|	КнигаУчетаДоходовИРасходов.Период МЕЖДУ &НачДата И &КонДата
	|	И КнигаУчетаДоходовИРасходов.Регистратор ССЫЛКА Документ.РегламентныеОперацииНалоговогоУчетаПоУСН
	|	И КнигаУчетаДоходовИРасходов.ЭтапПроведения = &ЭтапПроведения";
	
	
	
	Результат = ЗапросПринято.Выполнить().Выбрать();
	ВсегоПринято = 0;
	Если Результат.Следующий() Тогда
		ВсегоПринято = ?(НЕ ЗначениеЗаполнено(Результат.Графа7), 0, Результат.Графа7);
	КонецЕсли;
	
	СуммаКорректировки = ВсегоРаспределено - Окр(ВсегоРаспределено*Коэфф, 2, 1) - ВсегоПринято;
	
	Если СуммаКорректировки <> 0 Тогда
		СтрокаРасхода 							= ТаблицаПринятых.Добавить();
		СтрокаРасхода.Организация 				= СтруктураШапкиДокумента.Организация;
		СтрокаРасхода.ОтражениеВУСН 			= Перечисления.ОтражениеВУСН.Принимаются;
		СтрокаРасхода.РасчетныйДокумент 		= СтруктураШапкиДокумента.Ссылка;
		СтрокаРасхода.Сумма 					= СуммаКорректировки;
		СтрокаРасхода.НДС 						= 0;
		СтрокаРасхода.Валюта 					= СтруктураШапкиДокумента.ВалютаРегламентированногоУчета;
	КонецЕсли;
	
КонецПроцедуры

// Преобразует таблицу значений в таблицу со вложенными таблицами, группируя значения по колонкам, формирование итога по строкам
Функция ТаблицуЗначенийВТаблицуСГруппировкой(ТаблицаРезультатов, знач ГруппировочныеКолонки= "",знач КолонкиИтогов = "", ВидОперации)
	
	ИтоговаяТаблица  = Новый ТаблицаЗначений();
	ПромежуточнаяТаблица = Новый ТаблицаЗначений();
	Для каждого Колонка из ТаблицаРезультатов.Колонки Цикл
		ИтоговаяТаблица.Колонки.Добавить(Колонка.Имя,Колонка.ТипЗначения,Колонка.Заголовок,Колонка.Ширина);
		ПромежуточнаяТаблица.Колонки.Добавить(Колонка.Имя,Колонка.ТипЗначения,Колонка.Заголовок,Колонка.Ширина);
	КонецЦикла;
	ИтоговаяТаблица.Колонки.Добавить("Строки");
	
	//Если ПустаяСтрока(ГруппировочныеКолонки) Тогда
	//	Для каждого СтрокаТаблицы Из ТаблицаРезультатов Цикл
	//		СтрокаДерева = ИтоговаяТаблица.Добавить();

	//		ЗаполнитьЗначенияСвойств(СтрокаДерева, СтрокаТаблицы);

	//	КонецЦикла; 
	//	
	//	Возврат ИтоговаяТаблица;
	//КонецЕсли;
	
	ТаблицаГруппировок = ТаблицаРезультатов.Скопировать();
	ТаблицаГруппировок.Свернуть(ГруппировочныеКолонки,КолонкиИтогов);

	Для каждого СтрокаТаблицы Из ТаблицаГруппировок Цикл
		СтрокаДерева = ИтоговаяТаблица.Добавить();
		
		ЗаполнитьЗначенияСвойств(СтрокаДерева, СтрокаТаблицы);
		
	КонецЦикла; 
	
	ЗначенияОтбора = Новый Структура(ГруппировочныеКолонки);
	Если НЕ ПустаяСтрока(ГруппировочныеКолонки) Тогда
		ТаблицаРезультатов.Индексы.Добавить(ГруппировочныеКолонки);
	КонецЕсли;
	Для каждого СтрокаДерева из ИтоговаяТаблица Цикл
		//Формирование структуры отбора
		Для каждого ПараметрОтбора Из ЗначенияОтбора Цикл
			ЗначенияОтбора.Вставить(ПараметрОтбора.Ключ, СтрокаДерева[ПараметрОтбора.Ключ]);
		КонецЦикла; 
		ПромежуточнаяТаблица.Очистить();
		//Поиск и заполнение подчиненными колонками
		Если ЗначенияОтбора.Количество() > 0 Тогда
			МассивПодчиненныйхСтрок = ТаблицаРезультатов.НайтиСтроки(ЗначенияОтбора);
		Иначе
			МассивПодчиненныйхСтрок = ТаблицаРезультатов;
		КонецЕсли;
		Для каждого СтрокаТаблицы Из МассивПодчиненныйхСтрок Цикл
			ПодчиненнаяСтрокаДерева = ПромежуточнаяТаблица.Добавить();
			
			ЗаполнитьЗначенияСвойств(ПодчиненнаяСтрокаДерева, СтрокаТаблицы);
			
		КонецЦикла;
		СтрокаДерева.Строки = ПромежуточнаяТаблица.Скопировать();
	КонецЦикла;
		
	Возврат ИтоговаяТаблица;
КонецФункции

//Возвращает представление вида операции платежного документа
//
Функция ВидОперацииДДС(СтруктураШапкиДокумента) //
	
	ПредставлениеВида = "";
	ВидОперации = СтруктураШапкиДокумента.ВидОперации;
	
	Если (ВидОперации = Перечисления.ВидыОперацийПКО.ОплатаПокупателя) ИЛИ
		(ВидОперации = Перечисления.ВидыОперацийПоступлениеБезналичныхДенежныхСредств.ОплатаПокупателя) ИЛИ
		(ВидОперации = Перечисления.ВидыОперацийПоступлениеБезналичныхДенежныхСредств.ПоступлениеОплатыПоБанковскимКредитам) ИЛИ
		(ВидОперации = Перечисления.ВидыОперацийПоступлениеБезналичныхДенежныхСредств.ПоступлениеОплатыПоПлатежнымКартам) Тогда
		
		ПредставлениеВида = "ОплатаОтПокупателя";
		
	ИначеЕсли (ВидОперации = Перечисления.ВидыОперацийПКО.ВозвратДенежныхСредствПодотчетником) ИЛИ
		(ВидОперации = Перечисления.ВидыОперацийПоступлениеДенежныхДокументов.ПоступлениеОтПодотчетногоЛица) Тогда
		
		ПредставлениеВида = "ВозвратОтПодотчетника";
		
	ИначеЕсли (ВидОперации = Перечисления.ВидыОперацийПКО.ВозвратДенежныхСредствРаботником) Тогда
		
		ПредставлениеВида = "ВозвратОтРаботника";
		
	ИначеЕсли (ВидОперации = Перечисления.ВидыОперацийПКО.ВозвратДенежныхСредствПоставщиком) ИЛИ
		(ВидОперации = Перечисления.ВидыОперацийПоступлениеБезналичныхДенежныхСредств.ВозвратДенежныхСредствПоставщиком) Тогда
		
		ПредставлениеВида = "ВозвратОтПоставщика";
		
	ИначеЕсли (ВидОперации = Перечисления.ВидыОперацийПКО.ПриходДенежныхСредствРозничнаяВыручка) Тогда
		
		ПредставлениеВида = "ВыручкаККМ";
		МетаданныеДокумента = СтруктураШапкиДокумента.Ссылка.Метаданные();
		Если МетаданныеДокумента.Реквизиты.Найти("ВидПриемаРозничнойВыручки") <> Неопределено Тогда //~
			ПредставлениеВида = ?(СтруктураШапкиДокумента.Ссылка.ВидПриемаРозничнойВыручки = Перечисления.ВидПриемаРозничнойВыручки.ИзНТТ, "ВыручкаСНТТ", "ВыручкаККМ"); //~
		КонецЕсли;
				
	ИначеЕсли 
		// Денежные документы
		(ВидОперации = Перечисления.ВидыОперацийПоступлениеДенежныхДокументов.ПрочееПоступление) ИЛИ
		(ВидОперации = Перечисления.ВидыОперацийВыдачаДенежныхДокументов.ПрочаяВыдача) ИЛИ
		(ВидОперации = Перечисления.ВидыОперацийПоступлениеДенежныхДокументов.ПоступлениеОтПоставщика) ИЛИ
		(ВидОперации = Перечисления.ВидыОперацийВыдачаДенежныхДокументов.ВозвратПоставщику) ИЛИ
		// Прочее
		(ВидОперации = Перечисления.ВидыОперацийПКО.ПриходДенежныхСредствПрочее) ИЛИ
		(ВидОперации = Перечисления.ВидыОперацийПоступлениеБезналичныхДенежныхСредств.ПрочееПоступлениеБезналичныхДенежныхСредств) ИЛИ
		(ВидОперации = Перечисления.ВидыОперацийРКО.РасходДенежныхСредствПрочее) ИЛИ
		(ВидОперации = Перечисления.ВидыОперацийСписаниеБезналичныхДенежныхСредств.ПрочееСписаниеБезналичныхДенежныхСредств) ИЛИ
		(ВидОперации = Перечисления.ВидыОперацийППИсходящее.ПрочееСписаниеБезналичныхДенежныхСредств) ИЛИ
		// РасчетыПоКредитамИЗаймамСКонтрагентами
		(ВидОперации = Перечисления.ВидыОперацийПКО.РасчетыПоКредитамИЗаймамСКонтрагентами) ИЛИ
		(ВидОперации = Перечисления.ВидыОперацийРКО.РасчетыПоКредитамИЗаймамСКонтрагентами) ИЛИ
		(ВидОперации = Перечисления.ВидыОперацийППИсходящее.РасчетыПоКредитамИЗаймамСКонтрагентами) ИЛИ
        // ВозвратДенежныхСредствРаботником
		(ВидОперации = Перечисления.ВидыОперацийПКО.ВозвратДенежныхСредствРаботником) ИЛИ //~
        // РасчетыПоКредитамИЗаймамСРаботниками
		(ВидОперации = Перечисления.ВидыОперацийПКО.РасчетыПоКредитамИЗаймамСРаботниками) ИЛИ //~
		(ВидОперации = Перечисления.ВидыОперацийРКО.РасчетыПоКредитамИЗаймамСРаботниками) ИЛИ //~
		(ВидОперации = Перечисления.ВидыОперацийППИсходящее.РасчетыПоКредитамИЗаймамСРаботниками) ИЛИ //~
		// ПрочиеРасчетыСКонтрагентами		
		(ВидОперации = Перечисления.ВидыОперацийПКО.ПрочиеРасчетыСКонтрагентами) ИЛИ //~
		(ВидОперации = Перечисления.ВидыОперацийРКО.ПрочиеРасчетыСКонтрагентами) ИЛИ //~
		(ВидОперации = Перечисления.ВидыОперацийПоступлениеБезналичныхДенежныхСредств.ПрочиеРасчетыСКонтрагентами) ИЛИ
		(ВидОперации = Перечисления.ВидыОперацийСписаниеБезналичныхДенежныхСредств.ПрочиеРасчетыСКонтрагентами) ИЛИ
		(ВидОперации = Перечисления.ВидыОперацийППИсходящее.ПрочиеРасчетыСКонтрагентами) ИЛИ
		// РасчетыПоКредитамИЗаймам		
		(ВидОперации = Перечисления.ВидыОперацийПоступлениеБезналичныхДенежныхСредств.РасчетыПоКредитамИЗаймам) ИЛИ
		(ВидОперации = Перечисления.ВидыОперацийСписаниеБезналичныхДенежныхСредств.РасчетыПоКредитамИЗаймам) Тогда
		
		ПредставлениеВида = "ПрочееДДС";
		
	ИначеЕсли (ВидОперации = Перечисления.ВидыОперацийРКО.ОплатаПоставщику) ИЛИ
		(ВидОперации = Перечисления.ВидыОперацийППИсходящее.ОплатаПоставщику) ИЛИ
		(ВидОперации = Перечисления.ВидыОперацийСписаниеБезналичныхДенежныхСредств.ОплатаПоставщику) Тогда
		
		ПредставлениеВида = "Оплата";
		
	ИначеЕсли (ВидОперации = Перечисления.ВидыОперацийРКО.ВыдачаДенежныхСредствПодотчетнику) ИЛИ
		(ВидОперации = Перечисления.ВидыОперацийППИсходящее.ПеречислениеДенежныхСредствПодотчетнику) ИЛИ
		(ВидОперации = Перечисления.ВидыОперацийСписаниеБезналичныхДенежныхСредств.ПеречислениеДенежныхСредствПодотчетнику) ИЛИ
		(ВидОперации = Перечисления.ВидыОперацийВыдачаДенежныхДокументов.ВыдачаПодотчетномуЛицу) Тогда
		
		ПредставлениеВида = "ВыдачаПодотчетнику";
		
	ИначеЕсли 
		(ВидОперации = Перечисления.ВидыОперацийРКО.ВыплатаЗаработнойПлатыПоВедомостям) ИЛИ //~
		(ВидОперации = Перечисления.ВидыОперацийППИсходящее.ПеречислениеЗП) ИЛИ
		(ВидОперации = Перечисления.ВидыОперацийСписаниеБезналичныхДенежныхСредств.ПеречислениеЗП) Тогда
		
		ПредставлениеВида = "ВыплатаЗП";
		
	ИначеЕсли (ВидОперации = Перечисления.ВидыОперацийРКО.ВыплатаЗаработнойПлатыРаботнику) Тогда
		
		ПредставлениеВида = "ВыплатаЗПРаботнику";
		
	ИначеЕсли (ВидОперации = Перечисления.ВидыОперацийРКО.ВыплатаДепонентов) Тогда
		
		ПредставлениеВида = "ВыплатаДепонентов";
		
	ИначеЕсли (ВидОперации = Перечисления.ВидыОперацийППИсходящее.ПеречислениеНалога) ИЛИ
		(ВидОперации = Перечисления.ВидыОперацийСписаниеБезналичныхДенежныхСредств.ПеречислениеНалога) Тогда
		
		ПредставлениеВида = "ПеречислениеНалога";
		
	ИначеЕсли (ВидОперации = Перечисления.ВидыОперацийРКО.ВозвратДенежныхСредствПокупателю) ИЛИ
		(ВидОперации = Перечисления.ВидыОперацийППИсходящее.ВозвратДенежныхСредствПокупателю) ИЛИ
		(ВидОперации = Перечисления.ВидыОперацийСписаниеБезналичныхДенежныхСредств.ВозвратДенежныхСредствПокупателю) Тогда
		
		ПредставлениеВида = "ВозвратПокупателю";
		
	Иначе
		
		ПредставлениеВида = "НеОтражатьВНУ";
		
	КонецЕсли;
	
	Возврат ПредставлениеВида;
	
КонецФункции

//Получить курс по документу оплаты (используется при зачете аванса поставщику
//
Функция ОпределитьКурсПоДокументу(Договор, Сделка, РасчетныйДокумент)
	
	Курс = 1;
	
	Если НЕ ЗначениеЗаполнено(РасчетныйДокумент) Тогда
		Возврат Курс;
	Конецесли;
	
	Если (ТипЗнч(Договор) = Тип("СправочникСсылка.ДоговорыКонтрагентов")) И (ЗначениеЗаполнено(Договор)) Тогда
		
		Если Договор.РасчетыВУсловныхЕдиницах Тогда
			
			МетаданныеДокумента = РасчетныйДокумент.Метаданные();
			
			Если ОбщегоНазначения.ЕстьРеквизитТабЧастиДокумента("СуммаПлатежа", МетаданныеДокумента, "РасшифровкаПлатежа") И
				ОбщегоНазначения.ЕстьРеквизитТабЧастиДокумента("СуммаВзаиморасчетов", МетаданныеДокумента, "РасшифровкаПлатежа") И
				ОбщегоНазначения.ЕстьРеквизитТабЧастиДокумента("ДоговорКонтрагента", МетаданныеДокумента, "РасшифровкаПлатежа") И
				ОбщегоНазначения.ЕстьРеквизитТабЧастиДокумента("Сделка", МетаданныеДокумента, "РасшифровкаПлатежа") Тогда
				
				Сумма = 0;
				СуммаВзаиморасчетов = 0;
				
				Для Каждого Строка ИЗ РасчетныйДокумент.РасшифровкаПлатежа Цикл
					
					Если Договор = Строка.ДоговорКонтрагента И Сделка = Строка.Сделка Тогда
						
						Сумма = Сумма + Строка.СуммаПлатежа;
						СуммаВзаиморасчетов = СуммаВзаиморасчетов + Строка.СуммаВзаиморасчетов;
						
					КонецЕсли;
					
				КонецЦикла;
				
				Курс = ?(СуммаВзаиморасчетов = 0, 1, Сумма/СуммаВзаиморасчетов);
				
			ИначеЕсли ОбщегоНазначения.ЕстьРеквизитТабЧастиДокумента("Сумма", МетаданныеДокумента, "ОплатаПоставщику") И
				ОбщегоНазначения.ЕстьРеквизитТабЧастиДокумента("СуммаВзаиморасчетов", МетаданныеДокумента, "ОплатаПоставщику") И
				ОбщегоНазначения.ЕстьРеквизитТабЧастиДокумента("ДоговорКонтрагента", МетаданныеДокумента, "ОплатаПоставщику") И
				ОбщегоНазначения.ЕстьРеквизитТабЧастиДокумента("Сделка", МетаданныеДокумента, "ОплатаПоставщику") Тогда
				
				Сумма = 0;
				СуммаВзаиморасчетов = 0;
				
				Для Каждого Строка ИЗ РасчетныйДокумент.ОплатаПоставщику Цикл
					
					Если Договор = Строка.ДоговорКонтрагента И Сделка = Строка.Сделка Тогда
						
						Сумма = Сумма + Строка.Сумма;
						СуммаВзаиморасчетов = СуммаВзаиморасчетов + Строка.СуммаВзаиморасчетов;
						
					КонецЕсли;
					
				КонецЦикла;
				
				Курс = ?(СуммаВзаиморасчетов = 0, 1, Сумма/СуммаВзаиморасчетов);
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Курс;		
КонецФункции

//Заменяет пустые ссылки в таблице фильтров на значения "Неопределено"
//
Процедура ОбработатьТаблицуФильтров(ТаблицаФильтров)
	
	Для Каждого Фильтр Из ТаблицаФильтров Цикл
		
		Если Тип("Массив") = ТипЗнч(Фильтр.ЗначениеПоля) Тогда
			
			Для Каждого Элемент Из Фильтр.ЗначениеПоля Цикл
				
				Если НЕ ЗначениеЗаполнено(Элемент) Тогда
					
					Элемент = Неопределено;
					
				КонецЕсли;
				
			КонецЦикла;
			
		Иначе
			
			Если НЕ ЗначениеЗаполнено(Фильтр.ЗначениеПоля) Тогда
				
				Фильтр.ЗначениеПоля = Неопределено;
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

//Возвращает реквизиты первичного документа для занесения в графу 2 КУДиР
//
Функция РеквизитыПервичногоДокумента(Документ, ТЧ = "", Строка = 0, УказыватьВид = Ложь) Экспорт
	
	НомерНаПечать = "";
	ДатаНаПечать = "";
	МетаданныеДокумента = Документ.Метаданные();
	
	//Проверим дату и номер первичного документа в табличной части
	Если (ЗначениеЗаполнено(ТЧ)) и (Строка > 0) Тогда
		
		Если ОбщегоНазначения.ЕстьРеквизитТабЧастиДокумента("НомерВходящегоДокумента", МетаданныеДокумента, ТЧ) И
			ОбщегоНазначения.ЕстьРеквизитТабЧастиДокумента("ДатаВходящегоДокумента", МетаданныеДокумента, ТЧ) Тогда
			
			Если НЕ Документ[ТЧ].Количество() < Строка-1 Тогда
				
				НомерНаПечать = Документ[ТЧ][Строка-1].НомерВходящегоДокумента;
				ДатаНаПечать = Формат(Документ[ТЧ][Строка-1].ДатаВходящегоДокумента, "ДФ=дд.ММ.гггг");
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
	//Проверим дату и номер первичного документа в шапке
	Если НЕ ЗначениеЗаполнено(НомерНаПечать) И НЕ ЗначениеЗаполнено(ДатаНаПечать) Тогда
		
		Если МетаданныеДокумента.Реквизиты.Найти("НомерВходящегоДокумента") <> Неопределено И
			МетаданныеДокумента.Реквизиты.Найти("ДатаВходящегоДокумента") <> Неопределено Тогда
			
			НомерНаПечать = Документ.НомерВходящегоДокумента;
			ДатаНаПечать = Формат(Документ.ДатаВходящегоДокумента, "ДФ=дд.ММ.гггг");
			
		КонецЕсли;
		
	КонецЕсли;
	
	//Если дата и номер первичного документа не указаны или не заполнены, используем дату и номер регистрации
	Если НЕ ЗначениеЗаполнено(НомерНаПечать) Тогда
		
		НомерНаПечать = ОбщегоНазначения.ПолучитьНомерНаПечать(Документ);
		
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ДатаНаПечать) Тогда
		
		ДатаНаПечать = Формат(Документ.Дата, "ДФ=дд.ММ.гггг");
		
	КонецЕсли;
	
	РеквизитыПервичногоДокумента = "№ "+ НомерНаПечать + " от " + ДатаНаПечать;
	
	Если УказыватьВид Тогда
		РеквизитыПервичногоДокумента = МетаданныеДокумента.Синоним + " " + РеквизитыПервичногоДокумента;
	КонецЕсли;
	
	Возврат РеквизитыПервичногоДокумента;
КонецФункции

//Определяет применяется ли упрощенная система налогообложения
//
Функция ПрименениеУСН(Организация, Знач Дата) Экспорт //~
	
	Если НЕ ЗначениеЗаполнено(Организация) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Дата) Тогда
		Дата = ТекущаяДата();
	КонецЕсли;

	УчетнаяПолитика = ОбщегоНазначения.ПолучитьПараметрыУчетнойПолитикиРегл(Дата, Организация, Ложь);
		
	Возврат ?(НЕ ЗначениеЗаполнено(УчетнаяПолитика), Ложь, (УчетнаяПолитика.СистемаНалогообложения = Перечисления.СистемыНалогообложения.Упрощенная));

КонецФункции

//Определяет применяется ли упрощенная система налогообложения
//
Функция ПрименениеУСНДоходы(Организация, Знач Дата) Экспорт //~

	Если НЕ ЗначениеЗаполнено(Организация) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Дата) Тогда
		Дата = ТекущаяДата();
	КонецЕсли;

	УчетнаяПолитика = ОбщегоНазначения.ПолучитьПараметрыУчетнойПолитикиРегл(Дата, Организация, Ложь);
	ПрименяетсяУСН = ?(НЕ ЗначениеЗаполнено(УчетнаяПолитика), Ложь, (УчетнаяПолитика.СистемаНалогообложения = Перечисления.СистемыНалогообложения.Упрощенная));
	
	Если НЕ (ПрименяетсяУСН) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Если УчетнаяПолитика.ОбъектНалогообложенияУСН = Перечисления.ОбъектыНалогообложенияПоУСН.Доходы Тогда
		Возврат Истина;
	Иначе
		Возврат Ложь;
	КонецЕсли;

КонецФункции

//Возвращает новый статус оплаты расхода, полученный из текущего статуса
//переданным событием
Функция ПолучитьНовыйСтатусОплаты(СтруктураШапкиДокумента, Расход, ТекСтатус, Событие = "Оплата", ПризнаватьРасход = Ложь) //
	
	НовыйСтатус = ТекСтатус;
	РасходыПоОплате = Ложь;
	РасходыПоОтгрузке = Ложь;
	Если ЗначениеЗаполнено(Расход.СчетУчета) И Расход.ВидРасхода = Перечисления.ВидыРасходовУСН.Номенклатура Тогда
		ТоварыПоОплате = (СтруктураШапкиДокумента.ПорядокПризнанияРасходовПоТоварам = Перечисления.ПорядокПризнанияРасходовПоТоварам.ПоОплатеПоставщику);
		РасходыПоОтгрузке = (СтруктураШапкиДокумента.ПорядокПризнанияРасходовПоТоварам = Перечисления.ПорядокПризнанияРасходовПоТоварам.ПоФактуРеализации);
		МатериалыПоОплате = (СтруктураШапкиДокумента.ПорядокПризнанияМатериальныхРасходов = Перечисления.ПорядокПризнанияМатериальныхРасходов.ПоОплатеПоставщику);
		ЭтоТовар = ((Расход.СчетУчета.ПринадлежитЭлементу(ПланыСчетов.Хозрасчетный.ТоварыОтгруженные)) ИЛИ 
					(Расход.СчетУчета.ПринадлежитЭлементу(ПланыСчетов.Хозрасчетный.Товары)));
	    ЭтоМатериал = Расход.СчетУчета.ПринадлежитЭлементу(ПланыСчетов.Хозрасчетный.Материалы);
		РасходыПоОплате = ((ТоварыПоОплате И ЭтоТовар) ИЛИ (МатериалыПоОплате И ЭтоМатериал));
	ИначеЕсли ЗначениеЗаполнено(Расход.СчетУчета) И Расход.ВидРасхода = Перечисления.ВидыРасходовУСН.ДопРасходы Тогда
		ТоварыПоОплате = (СтруктураШапкиДокумента.ПорядокПризнанияРасходовПоТоварам = Перечисления.ПорядокПризнанияРасходовПоТоварам.ПоОплатеПоставщику);
		МатериалыПоОплате = (СтруктураШапкиДокумента.ПорядокПризнанияМатериальныхРасходов = Перечисления.ПорядокПризнанияМатериальныхРасходов.ПоОплатеПоставщику);
		ЭтоТовар = ((Расход.СчетУчета.ПринадлежитЭлементу(ПланыСчетов.Хозрасчетный.ТоварыОтгруженные)) ИЛИ 
					(Расход.СчетУчета.ПринадлежитЭлементу(ПланыСчетов.Хозрасчетный.Товары)));
	    ЭтоМатериал = Расход.СчетУчета.ПринадлежитЭлементу(ПланыСчетов.Хозрасчетный.Материалы);
		РасходыПоОплате = (ЭтоТовар И ТоварыПоОплате) ИЛИ (МатериалыПоОплате И ЭтоМатериал);

	КонецЕсли;
    //~//
	Если (Событие = "ВПроизводство") И (СтруктураШапкиДокумента.ПорядокПризнанияМатериальныхРасходов <> Перечисления.ПорядокПризнанияМатериальныхРасходов.ПоФактуВыпускаПродукции) Тогда
		Событие = "Списание";
	КонецЕсли;
	//~\\
	Если Событие = "Оплата" Тогда
		
		Если ТекСтатус = Перечисления.СтатусыРасходовУСН.НеСписаноНеОплачено Тогда
			Если РасходыПоОплате Тогда
				Если Расход.ОтражениеВУСН = Перечисления.ОтражениеВУСН.Принимаются Тогда
					НовыйСтатус = Перечисления.СтатусыРасходовУСН.НеСписаноПринято;
					ПризнаватьРасход = Истина;
				ИначеЕсли Расход.ОтражениеВУСН = Перечисления.ОтражениеВУСН.Распределяются Тогда
					НовыйСтатус = Перечисления.СтатусыРасходовУСН.НеРаспределено;
				Иначе
					НовыйСтатус = Перечисления.СтатусыРасходовУСН.ПустаяСсылка();
				КонецЕсли;
			Иначе
				НовыйСтатус = Перечисления.СтатусыРасходовУСН.НеСписано;
			КонецЕсли;
		//~//
		ИначеЕсли ТекСтатус = Перечисления.СтатусыРасходовУСН.НеВыпущеноНеОплачено Тогда
			НовыйСтатус = Перечисления.СтатусыРасходовУСН.НеВыпущено;
		//~\\
		ИначеЕсли ТекСтатус = Перечисления.СтатусыРасходовУСН.НеОплаченоНеОплаченоПокупателем Тогда
			НовыйСтатус = Перечисления.СтатусыРасходовУСН.НеОплаченоПокупателем;
			Если РасходыПоОтгрузке ИЛИ РасходыПоОплате Тогда
				НовыйСтатус = Перечисления.СтатусыРасходовУСН.ПустаяСсылка();
				ПризнаватьРасход = Истина;
			КонецЕсли;
		ИначеЕсли ТекСтатус = Перечисления.СтатусыРасходовУСН.НеОплачено Тогда
			НовыйСтатус = Перечисления.СтатусыРасходовУСН.ПустаяСсылка();
			ПризнаватьРасход = Истина;
			//Если после 2006 г. отражается оплата НМА, поступивших до 2006 года.
			Если (Расход.ВидРасхода = Перечисления.ВидыРасходовУСН.НМА) И
				(СтруктураШапкиДокумента.ДатаОплаты >= Дата("20060101")) Тогда
				ПризнаватьРасход = Ложь;
			КонецЕсли;
		КонецЕсли;
		
	ИначеЕсли (Событие = "Списание") или (Событие = "ВозвратПоставщику") Тогда
		
		Если ТекСтатус = Перечисления.СтатусыРасходовУСН.НеСписаноНеОплачено Тогда
			НовыйСтатус = Перечисления.СтатусыРасходовУСН.НеОплачено;
		ИначеЕсли ТекСтатус = Перечисления.СтатусыРасходовУСН.НеСписано Тогда
			НовыйСтатус = Перечисления.СтатусыРасходовУСН.ПустаяСсылка();
			ПризнаватьРасход = Истина;
		ИначеЕсли ТекСтатус = Перечисления.СтатусыРасходовУСН.НеСписаноПринято Тогда
			НовыйСтатус = Перечисления.СтатусыРасходовУСН.ПустаяСсылка();
		ИначеЕсли ТекСтатус = Перечисления.СтатусыРасходовУСН.НеСписаноНеРаспределено Тогда
			НовыйСтатус = Перечисления.СтатусыРасходовУСН.НеРаспределено;
		КонецЕсли;
	//~//
	ИначеЕсли Событие = "ВПроизводство" Тогда
		
		Если ТекСтатус = Перечисления.СтатусыРасходовУСН.НеСписаноНеОплачено Тогда
			НовыйСтатус = Перечисления.СтатусыРасходовУСН.НеВыпущеноНеОплачено;
		ИначеЕсли ТекСтатус = Перечисления.СтатусыРасходовУСН.НеСписано Тогда
			НовыйСтатус = Перечисления.СтатусыРасходовУСН.НеВыпущено;
		ИначеЕсли ТекСтатус = Перечисления.СтатусыРасходовУСН.НеСписаноПринято Тогда
			НовыйСтатус = Перечисления.СтатусыРасходовУСН.НеВыпущено;
		ИначеЕсли ТекСтатус = Перечисления.СтатусыРасходовУСН.НеСписаноНеРаспределено Тогда
			НовыйСтатус = Перечисления.СтатусыРасходовУСН.НеВыпущено;
		КонецЕсли;
		
	ИначеЕсли Событие = "Выпуск" Тогда
		
		Если ТекСтатус = Перечисления.СтатусыРасходовУСН.НеВыпущеноНеОплачено Тогда
			НовыйСтатус = Перечисления.СтатусыРасходовУСН.НеОплачено;
		ИначеЕсли ТекСтатус = Перечисления.СтатусыРасходовУСН.НеВыпущено Тогда
			НовыйСтатус = Перечисления.СтатусыРасходовУСН.ПустаяСсылка();
			ПризнаватьРасход = Истина;
		КонецЕсли;
	//~\\
	ИначеЕсли Событие = "Реализация" Тогда
		
		Если ТекСтатус = Перечисления.СтатусыРасходовУСН.НеСписаноНеОплачено Тогда
			Если РасходыПоОтгрузке Тогда
				НовыйСтатус = Перечисления.СтатусыРасходовУСН.НеОплачено;
			Иначе
				НовыйСтатус = Перечисления.СтатусыРасходовУСН.НеОплаченоНеОплаченоПокупателем;
			КонецЕсли;
		ИначеЕсли ТекСтатус = Перечисления.СтатусыРасходовУСН.НеСписано Тогда
			Если РасходыПоОтгрузке Тогда
				НовыйСтатус = Перечисления.СтатусыРасходовУСН.ПустаяСсылка();
				ПризнаватьРасход = Истина;
			Иначе
				НовыйСтатус = Перечисления.СтатусыРасходовУСН.НеОплаченоПокупателем;
			КонецЕсли;
		ИначеЕсли ТекСтатус = Перечисления.СтатусыРасходовУСН.НеСписаноПринято Тогда
			НовыйСтатус = Перечисления.СтатусыРасходовУСН.ПустаяСсылка();
		ИначеЕсли ТекСтатус = Перечисления.СтатусыРасходовУСН.НеСписаноНеРаспределено Тогда
			НовыйСтатус = Перечисления.СтатусыРасходовУСН.НеРаспределено;
		КонецЕсли;
		
	ИначеЕсли Событие = "Розница" Тогда
		
		Если ТекСтатус = Перечисления.СтатусыРасходовУСН.НеСписаноНеОплачено Тогда
			НовыйСтатус = Перечисления.СтатусыРасходовУСН.НеОплачено;
		ИначеЕсли ТекСтатус = Перечисления.СтатусыРасходовУСН.НеСписано Тогда
			НовыйСтатус = Перечисления.СтатусыРасходовУСН.ПустаяСсылка();
			ПризнаватьРасход = Истина;
		ИначеЕсли ТекСтатус = Перечисления.СтатусыРасходовУСН.НеСписаноПринято Тогда
			НовыйСтатус = Перечисления.СтатусыРасходовУСН.ПустаяСсылка();
		ИначеЕсли ТекСтатус = Перечисления.СтатусыРасходовУСН.НеСписаноНеРаспределено Тогда
			НовыйСтатус = Перечисления.СтатусыРасходовУСН.НеРаспределено;
		КонецЕсли;
		
	ИначеЕсли Событие = "Доход" Тогда
		
		Если ТекСтатус = Перечисления.СтатусыРасходовУСН.НеОплаченоНеОплаченоПокупателем Тогда
			НовыйСтатус = Перечисления.СтатусыРасходовУСН.НеОплачено;
		ИначеЕсли ТекСтатус = Перечисления.СтатусыРасходовУСН.НеОплаченоПокупателем Тогда
			НовыйСтатус = Перечисления.СтатусыРасходовУСН.ПустаяСсылка();
			ПризнаватьРасход = Истина;
			Если РасходыПоОтгрузке ИЛИ РасходыПоОплате Тогда
				ПризнаватьРасход = Ложь;
			КонецЕсли;
		КонецЕсли;
		
	ИначеЕсли Событие = "РаспределениеЕНВД" Тогда
		
		НовыйСтатус = Перечисления.СтатусыРасходовУСН.ПустаяСсылка();
		ПризнаватьРасход = Истина;
		
	КонецЕсли;
	
	Возврат НовыйСтатус;
			
КонецФункции

//Возвращает текущий статус отражаения в НУ на основании комбинации двух статусов
//поступления и списания
Функция ПолучитьКомбнациюСтатусовОтражениеВНУ(Статус1, Статус2)

	СписокСтатусов = Новый СписокЗначений;
	СписокСтатусов.Добавить(Статус1);
	СписокСтатусов.Добавить(Статус2);

	Если НЕ СписокСтатусов.НайтиПоЗначению(Перечисления.ОтражениеВУСН.ПустаяСсылка()) = Неопределено Тогда
		Возврат Перечисления.ОтражениеВУСН.НеПринимаются;

	ИначеЕсли НЕ СписокСтатусов.НайтиПоЗначению(Перечисления.ОтражениеВУСН.НеПринимаются) = Неопределено Тогда
		Возврат Перечисления.ОтражениеВУСН.НеПринимаются;

	ИначеЕсли НЕ СписокСтатусов.НайтиПоЗначению(Перечисления.ОтражениеВУСН.Распределяются) = Неопределено Тогда
		Возврат Перечисления.ОтражениеВУСН.Распределяются;

	Иначе
		Возврат Перечисления.ОтражениеВУСН.Принимаются;
	КонецЕсли;

КонецФункции

//Собирает таблицы прихода и расхода в единую таблицу движений регистров
//Возвращает структуру, состоящую из трех таблиц (движения по трем регистрам
Функция СобратьТаблицыДвижений(СтруктураРасходов, ТаблицаВзаиморасчетов = Неопределено, ДопТаблицаРасходов = Неопределено, ДопСтруктураРасходов = Неопределено)
	
	ТаблицаКорректировкиРасход = СтруктураРасходов.ТаблицаРасход;
	ТаблицаКорректировкиПриход = СтруктураРасходов.ТаблицаПриход;
	ТаблицаПризнанныхРасходов  = СтруктураРасходов.ТаблицаПринятых;
	
	Если НЕ ТаблицаКорректировкиПриход.Колонки.Найти("СтатусСписания") = Неопределено Тогда
		ТаблицаКорректировкиПриход.ЗаполнитьЗначения(Перечисления.ОтражениеВУСН.ПустаяСсылка(), "СтатусСписания");
	КонецЕсли;
	
	Если ТаблицаВзаиморасчетов = Неопределено Тогда
		Если НЕ ДопСтруктураРасходов = Неопределено Тогда
			ТаблицаВзаиморасчетов = ДопСтруктураРасходов.ВзаиморасчетыРасход.СкопироватьКолонки();
		Иначе
			Рег = РегистрыНакопления.ВзаиморасчетыУСН;
			НаборЗаписей = Рег.СоздатьНаборЗаписей();
			ТаблицаВзаиморасчетов = НаборЗаписей.Выгрузить();
			ТаблицаВзаиморасчетов.Очистить();
		КонецЕсли;
	КонецЕсли;
	
	Если НЕ ДопСтруктураРасходов = Неопределено Тогда
		
		ТаблицаРезультат = ДопСтруктураРасходов.РасходыУСН.Скопировать();
		
		Для Каждого Строка Из ТаблицаКорректировкиРасход Цикл
			НоваяСтрока = ТаблицаРезультат.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);
			НоваяСтрока.ВидДвижения	 		= ВидДвиженияНакопления.Расход;
		КонецЦикла;
		
		Для Каждого Строка Из ДопСтруктураРасходов.КУДиР Цикл
			НоваяСтрока = ТаблицаПризнанныхРасходов.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);
		КонецЦикла;
		
		Для Каждого Строка Из ДопСтруктураРасходов.ВзаиморасчетыРасход Цикл
			НоваяСтрока = ТаблицаВзаиморасчетов.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);
		КонецЦикла;
		
	ИначеЕсли НЕ ДопТаблицаРасходов = Неопределено Тогда
		
		ТаблицаРезультат = ДопТаблицаРасходов.Скопировать();
		
		Если ТаблицаРезультат.Колонки.Найти("ВидДвижения") = Неопределено Тогда
			ТаблицаРезультат.Колонки.Добавить("ВидДвижения");
		КонецЕсли;
		
		ТаблицаРезультат.ЗаполнитьЗначения(ВидДвиженияНакопления.Приход,"ВидДвижения");
		
		Для Каждого Строка Из ТаблицаКорректировкиРасход Цикл
			НоваяСтрока = ТаблицаРезультат.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);
			НоваяСтрока.ВидДвижения	 		= ВидДвиженияНакопления.Расход;
		КонецЦикла;

	Иначе
		
		ТаблицаРезультат = ТаблицаКорректировкиРасход.Скопировать();
		
		Если ТаблицаРезультат.Колонки.Найти("ВидДвижения") = Неопределено Тогда
			ТаблицаРезультат.Колонки.Добавить("ВидДвижения");
		КонецЕсли;
		
		ТаблицаРезультат.ЗаполнитьЗначения(ВидДвиженияНакопления.Расход,"ВидДвижения");
		
	КонецЕсли;
	
	Для Каждого Строка Из ТаблицаКорректировкиПриход Цикл
		
		НоваяСтрока = ТаблицаРезультат.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);
		НоваяСтрока.ВидДвижения 		= ВидДвиженияНакопления.Приход;
		
	КонецЦикла;
		
	Возврат (Новый Структура("ВзаиморасчетыРасход, РасходыУСН, КУДиР",
								ТаблицаВзаиморасчетов, ТаблицаРезультат, ТаблицаПризнанныхРасходов));
КонецФункции

////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

//Определяет долю выручки, не включаемую в доходы разделяя ее на выручку комитента 
//и выручку по деятельности ЕНВД
Функция ДоляЕНВДиКомиссии(СтруктураШапкиДокумента, ТаблицаСписанныхПартий = Неопределено, ДвиженияРегистров = Неопределено) //
	
	ВыручкаВсего = 0;
	ВыручкаЕНВД = 0;
	ВыручкаКомиссии = 0;
	МетаданныеДокумента = СтруктураШапкиДокумента.Ссылка.Метаданные();
	ИмяВидаДокумента = МетаданныеДокумента.Имя;
	//В товарах
	
	Если (НЕ ТаблицаСписанныхПартий = НЕОПРЕДЕЛЕНО) И
		(НЕ МетаданныеДокумента.ТабличныеЧасти.Найти("Товары") = НЕОПРЕДЕЛЕНО) Тогда
		
		Если УправлениеЗапасами.ИспользуетсяРасширеннаяАналитикаУчета(СтруктураШапкиДокумента.Дата)
			И ОбщегоНазначения.ЕстьРеквизитТабЧастиДокумента("ПринадлежностьНоменклатуры", МетаданныеДокумента, "Товары") Тогда
			
			Запрос = Новый Запрос;
			Запрос.УстановитьПараметр("Ссылка", СтруктураШапкиДокумента.Ссылка);
			Всего = 0;
			
			Запрос.Текст = "
			|ВЫБРАТЬ
			|	Реализация.СчетДоходовБУ,
			|	Реализация.ПринадлежностьНоменклатуры,
			|	Сумма(Реализация.Сумма) КАК Сумма
			|ИЗ
			|	Документ."+ИмяВидаДокумента+".Товары КАК Реализация
			|
			|ГДЕ
			|	Реализация.Ссылка = &Ссылка
			|
			|СГРУППИРОВАТЬ ПО
			|	Реализация.СчетДоходовБУ,
			|	Реализация.ПринадлежностьНоменклатуры";
			
			Выборка = Запрос.Выполнить().Выбрать();		
			Пока Выборка.Следующий() Цикл
				ВыручкаВсего = ВыручкаВсего + Выборка.Сумма;
				Если Выборка.ПринадлежностьНоменклатуры = Перечисления.ПринадлежностьНоменклатуры.Принятый Тогда
					ВыручкаКомиссии = ВыручкаКомиссии + Выборка.Сумма;
				ИначеЕсли ОтноситсяКДеятельностиЕНВД(Выборка.СчетДоходовБУ) Тогда
					ВыручкаЕНВД = ВыручкаЕНВД + Выборка.Сумма;
				КонецЕсли;
			КонецЦикла;
			
		Иначе
			
			//Переданные = (НЕ ТаблицаСписанныхПартий.Колонки.Найти("ДокументПередачи") = Неопределено);
			
			Запрос = Новый Запрос;
			Запрос.УстановитьПараметр("Ссылка",		 СтруктураШапкиДокумента.Ссылка);
			Запрос.УстановитьПараметр("Организация", СтруктураШапкиДокумента.Организация);
			
			Если ОбщегоНазначения.ЕстьРеквизитТабЧастиДокумента("ПринятыеСчетУчетаБУ", МетаданныеДокумента, "Товары") Тогда
				ИмяСчетаПринятых = "ПринятыеСчетУчетаБУ";
			ИначеЕсли ОбщегоНазначения.ЕстьРеквизитТабЧастиДокумента("ПереданныеПринятыеБУ", МетаданныеДокумента, "Товары") Тогда
				ИмяСчетаПринятых = "ПереданныеПринятыеБУ";
			Иначе
				ИмяСчетаПринятых = "СчетУчетаБУ";
			КонецЕсли;
			
			Если СтруктураШапкиДокумента.Ссылка.СуммаВключаетНДС Тогда
				ТекстСуммы = "РеализацияТоваровУслугТовары.Сумма";			
			Иначе
				ТекстСуммы = "РеализацияТоваровУслугТовары.Сумма + РеализацияТоваровУслугТовары.СуммаНДС";
			КонецЕсли;
			
			Текст =
			
			"ВЫБРАТЬ
			|	СУММА(РеализацияТоваровУслугТовары.Количество*РеализацияТоваровУслугТовары.Коэффициент/РеализацияТоваровУслугТовары.Номенклатура.ЕдиницаХраненияОстатков.Коэффициент) КАК Количество,
			|	РеализацияТоваровУслугТовары.СчетДоходовБУ,
			|	СУММА(" + ТекстСуммы + ") КАК Сумма,
			|	РеализацияТоваровУслугТовары.Номенклатура,
			|	РеализацияТоваровУслугТовары.СчетУчетаБУ,
			|	РеализацияТоваровУслугТовары." + ИмяСчетаПринятых + " КАК ПринятыеСчетУчетаБУ
			|ИЗ
			|	Документ." + ИмяВидаДокумента + ".Товары КАК РеализацияТоваровУслугТовары
			|
			|ГДЕ
			|	РеализацияТоваровУслугТовары.Ссылка = &Ссылка
			|
			|СГРУППИРОВАТЬ ПО
			|	РеализацияТоваровУслугТовары.СчетДоходовБУ,
			|	РеализацияТоваровУслугТовары.Номенклатура,
			|	РеализацияТоваровУслугТовары.СчетУчетаБУ,
			|	РеализацияТоваровУслугТовары." + ИмяСчетаПринятых;
			
			Запрос.Текст = Текст;
			
			Выборка = Запрос.Выполнить().Выбрать();		
			Пока Выборка.Следующий() Цикл
				
				ВыручкаВсего = ВыручкаВсего + Выборка.Сумма;
				
				Цена = ?(ЗначениеЗаполнено(Выборка.Количество), Выборка.Сумма/Выборка.Количество, Выборка.Сумма);
				
				СтруктураОтбора = Новый Структура;
				СтруктураОтбора.Вставить("Номенклатура",				Выборка.Номенклатура);
				Если (Выборка.СчетУчетаБУ.Забалансовый) Тогда
					СтруктураОтбора.Вставить("СчетУчета",					Выборка.СчетУчетаБУ);
				Иначе
					СтруктураОтбора.Вставить("СчетУчета",					Выборка.ПринятыеСчетУчетаБУ);
				КонецЕсли;
				
				НайденныеСтроки = ТаблицаСписанныхПартий.НайтиСтроки(СтруктураОтбора);
				КоличествоПоСтроке = ?(НЕ ЗначениеЗаполнено(Выборка.Количество), 1, Выборка.Количество);
				
				Для Каждого СтрокаТаблицы Из НайденныеСтроки Цикл
					Если КоличествоПоСтроке = 0 Тогда
						Прервать;
					КонецЕсли;
					
					Учесть = Мин(КоличествоПоСтроке, СтрокаТаблицы.Количество);
					ВыручкаКомиссии = ВыручкаКомиссии + Окр(Цена*Учесть, 2,1);
					КоличествоПоСтроке = КоличествоПоСтроке - Учесть;
					
					//Отразим выручку комитента
					Если НЕ ДвиженияРегистров = Неопределено Тогда
						
						Если ДвиженияРегистров.ВзаиморасчетыРасход.Количество() = 0 Тогда
							НаборДвижений = РегистрыНакопления.ВзаиморасчетыУСН.СоздатьНаборЗаписей();
							ТаблицаДвижений = НаборДвижений.Выгрузить();
							ТаблицаДвижений.Очистить();
							
							ДвиженияРегистров.ВзаиморасчетыРасход = ТаблицаДвижений.Скопировать();
						КонецЕсли;
						
						Движение = ДвиженияРегистров.ВзаиморасчетыРасход.Добавить();
						Движение.Организация = СтруктураШапкиДокумента.Организация;
						
						Если (НЕ СтрокаТаблицы.ДокументОприходования.Метаданные().Реквизиты.Найти("ДоговорКонтрагента") = Неопределено) Тогда
							Договор = СтрокаТаблицы.ДокументОприходования.ДоговорКонтрагента;
							Движение.ДоговорКонтрагента = Договор;
							КурсВалюты = МодульВалютногоУчета.ПолучитьКурсВалюты(Договор.ВалютаВзаиморасчетов, СтруктураШапкиДокумента.ДатаОплаты);
							СуммаКомиссии = МодульВалютногоУчета.ПересчитатьИзВалютыВВалюту(Окр(Цена*Учесть, 2,1), СтруктураШапкиДокумента.ВалютаДокумента,
							Договор.ВалютаВзаиморасчетов,
							СтруктураШапкиДокумента.КурсДокумента, КурсВалюты.Курс,
							СтруктураШапкиДокумента.КратностьДокумента, КурсВалюты.Кратность);
						КонецЕсли;
						
						//Движение.РасчетныйДокумент 	= СтруктураШапкиДокумента.Ссылка;
						
						Движение.СуммаВзаиморасчетов = СуммаКомиссии;
						Движение.ДоходЕНВД 			= 0;
						Движение.ДоходКомитента 	= 0;
						Движение.ВидДвижения = ВидДвиженияНакопления.Расход;					
					КонецЕсли;
					
				КонецЦикла;
				
				Если ОтноситсяКДеятельностиЕНВД(Выборка.СчетДоходовБУ) Тогда
					ВыручкаЕНВД = ВыручкаЕНВД + Окр(Цена*КоличествоПоСтроке, 2,1);
				КонецЕсли;
				
				Если КоличествоПоСтроке = 0 Тогда
					Продолжить;
				КонецЕсли;
				
			КонецЦикла;
		КонецЕсли;
		
	КонецЕсли;
	
	//В услугах
	Если ОбщегоНазначения.ЕстьРеквизитТабЧастиДокумента("СчетДоходовБУ", МетаданныеДокумента, "Услуги") Тогда
		
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("Ссылка", СтруктураШапкиДокумента.Ссылка);
		Всего = 0;
		
		Запрос.Текст = "
		|ВЫБРАТЬ
		|	Реализация.СчетДоходовБУ,
		|	Сумма(Реализация.Сумма) КАК Сумма
		|ИЗ
		|	Документ."+ИмяВидаДокумента+".Услуги КАК Реализация
		|
		|ГДЕ
		|	Реализация.Ссылка = &Ссылка
		|
		|СГРУППИРОВАТЬ ПО
		|	Реализация.СчетДоходовБУ";
		
		Выборка = Запрос.Выполнить().Выбрать();		
		Пока Выборка.Следующий() Цикл
			ВыручкаВсего = ВыручкаВсего + Выборка.Сумма;
			Если ОтноситсяКДеятельностиЕНВД(Выборка.СчетДоходовБУ) Тогда
				ВыручкаЕНВД = ВыручкаЕНВД + Выборка.Сумма;
			КонецЕсли;
		КонецЦикла;
		
	КонецЕсли;
	
	//В услугах по переработке
	Если НЕ МетаданныеДокумента.ТабличныеЧасти.Найти("Продукция") = НЕОПРЕДЕЛЕНО Тогда
		
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("Ссылка", СтруктураШапкиДокумента.Ссылка);
		Всего = 0;
		
		Запрос.Текст = "
		|ВЫБРАТЬ
		|	Реализация.СчетДоходовБУ,
		|	Сумма(Реализация.Сумма) КАК Сумма
		|ИЗ
		|	Документ."+ИмяВидаДокумента+".Продукция КАК Реализация
		|
		|ГДЕ
		|	Реализация.Ссылка = &Ссылка
		|
		|СГРУППИРОВАТЬ ПО
		|	Реализация.СчетДоходовБУ";
		
		Выборка = Запрос.Выполнить().Выбрать();		
		Пока Выборка.Следующий() Цикл
			ВыручкаВсего = ВыручкаВсего + Выборка.Сумма;
			Если ОтноситсяКДеятельностиЕНВД(Выборка.СчетДоходовБУ) Тогда
				ВыручкаЕНВД = ВыручкаЕНВД + Выборка.Сумма;
			КонецЕсли;
		КонецЦикла;
		
	КонецЕсли;
	
	СтруктраВыручки = Новый Структура("Всего, ЕНВД, Комиссия", ВыручкаВсего, ВыручкаЕНВД, ВыручкаКомиссии);
	
	Возврат СтруктраВыручки;
	
КонецФункции

//Определяет долю выручки, не включаемую в доходы разделяя ее на выручку комитента 
//и выручку по деятельности ЕНВД
Функция ДоляЕНВДиКомиссииВозврат(СтруктураШапкиДокумента, ТаблицаСписанныхПартий = Неопределено, ДвиженияРегистров = Неопределено) //
	
	ВыручкаВсего = 0;
	ВыручкаЕНВД = 0;
	ВыручкаКомиссии = 0;
	
	ТаблицаВозврата = Новый ТаблицаЗначений();
	ТаблицаВозврата.Колонки.Добавить("Всего");
	ТаблицаВозврата.Колонки.Добавить("ЕНВД");
	ТаблицаВозврата.Колонки.Добавить("Комиссия");
	ТаблицаВозврата.Колонки.Добавить("ДокументПартии");
	
	МетаданныеДокумента = СтруктураШапкиДокумента.Ссылка.Метаданные();
	ИмяВидаДокумента = МетаданныеДокумента.Имя;
	//В товарах
	
	Если (НЕ ТаблицаСписанныхПартий = НЕОПРЕДЕЛЕНО) И
		(НЕ МетаданныеДокумента.ТабличныеЧасти.Найти("Товары") = НЕОПРЕДЕЛЕНО) Тогда
		
		Если УправлениеЗапасами.ИспользуетсяРасширеннаяАналитикаУчета(СтруктураШапкиДокумента.Дата)
			И ОбщегоНазначения.ЕстьРеквизитТабЧастиДокумента("ПринадлежностьНоменклатуры", МетаданныеДокумента, "Товары") Тогда
			
			Запрос = Новый Запрос;
			Запрос.УстановитьПараметр("Ссылка", СтруктураШапкиДокумента.Ссылка);
			Всего = 0;
			
			Запрос.Текст = "
			|ВЫБРАТЬ
			|	Реализация.СчетДоходовБУ,
			|	Реализация.ДокументПартии,
			|	Реализация.ПринадлежностьНоменклатуры,
			|	Сумма(Реализация.Сумма) КАК Сумма
			|ИЗ
			|	Документ."+ИмяВидаДокумента+".Товары КАК Реализация
			|
			|ГДЕ
			|	Реализация.Ссылка = &Ссылка
			|
			|СГРУППИРОВАТЬ ПО
			|	Реализация.СчетДоходовБУ,
			|	Реализация.ДокументПартии,
			|	Реализация.ПринадлежностьНоменклатуры";
			
			Выборка = Запрос.Выполнить().Выбрать();		
			Пока Выборка.Следующий() Цикл
				
				СтрокаВозврата = ТаблицаВозврата.Добавить();
				СтрокаВозврата.ДокументПартии = Выборка.ДокументПартии;
				СтрокаВозврата.Всего = Выборка.Сумма;
				СтрокаВозврата.Комиссия = 0;
				СтрокаВозврата.ЕНВД = 0;
				
				Если Выборка.ПринадлежностьНоменклатуры = Перечисления.ПринадлежностьНоменклатуры.Принятый Тогда
					СтрокаВозврата.Комиссия = Выборка.Сумма;
				ИначеЕсли ОтноситсяКДеятельностиЕНВД(Выборка.СчетДоходовБУ) Тогда
					СтрокаВозврата.ЕНВД = Выборка.Сумма;
				КонецЕсли;
			КонецЦикла;
			
		Иначе
			
			
			Запрос = Новый Запрос;
			Запрос.УстановитьПараметр("Ссылка",		 СтруктураШапкиДокумента.Ссылка);
			Запрос.УстановитьПараметр("Организация", СтруктураШапкиДокумента.Организация);
			
			Если СтруктураШапкиДокумента.Ссылка.СуммаВключаетНДС Тогда
				ТекстСуммы = "РеализацияТоваровУслугТовары.Сумма";			
			Иначе
				ТекстСуммы = "РеализацияТоваровУслугТовары.Сумма + РеализацияТоваровУслугТовары.СуммаНДС";
			КонецЕсли;
			
			Текст =
			
			"ВЫБРАТЬ
			|	СУММА(РеализацияТоваровУслугТовары.Количество*РеализацияТоваровУслугТовары.Коэффициент/РеализацияТоваровУслугТовары.Номенклатура.ЕдиницаХраненияОстатков.Коэффициент) КАК Количество,
			|	РеализацияТоваровУслугТовары.СчетДоходовБУ,
			|	СУММА(" + ТекстСуммы + ") КАК Сумма,
			|	РеализацияТоваровУслугТовары.Номенклатура,
			|	РеализацияТоваровУслугТовары.ДокументПартии,
			|	РеализацияТоваровУслугТовары.СчетУчетаБУ,
			|	РеализацияТоваровУслугТовары.ПринятыеСчетУчетаБУ КАК ПринятыеСчетУчетаБУ
			|ИЗ
			|	Документ." + ИмяВидаДокумента + ".Товары КАК РеализацияТоваровУслугТовары
			|
			|ГДЕ
			|	РеализацияТоваровУслугТовары.Ссылка = &Ссылка
			|
			|СГРУППИРОВАТЬ ПО
			|	РеализацияТоваровУслугТовары.СчетДоходовБУ,
			|	РеализацияТоваровУслугТовары.Номенклатура,
			|	РеализацияТоваровУслугТовары.ДокументПартии,
			|	РеализацияТоваровУслугТовары.СчетУчетаБУ,
			|	РеализацияТоваровУслугТовары.ПринятыеСчетУчетаБУ";
			
			Запрос.Текст = Текст;
			Выборка = Запрос.Выполнить().Выбрать();		
			Пока Выборка.Следующий() Цикл
				
				ВыручкаВсего = ВыручкаВсего + Выборка.Сумма;
				
				СтрокаВозврата = ТаблицаВозврата.Добавить();
				СтрокаВозврата.ДокументПартии = Выборка.ДокументПартии;
				СтрокаВозврата.Всего = Выборка.Сумма;
				СтрокаВозврата.Комиссия = 0;
				СтрокаВозврата.ЕНВД = 0;
				
				Цена = ?(ЗначениеЗаполнено(Выборка.Количество), Выборка.Сумма/Выборка.Количество, Выборка.Сумма);
				
				СтруктураОтбора = Новый Структура;
				СтруктураОтбора.Вставить("Номенклатура",				Выборка.Номенклатура);
				Если (Выборка.СчетУчетаБУ.Забалансовый) Тогда
					СтруктураОтбора.Вставить("СчетУчета",					Выборка.СчетУчетаБУ);
				Иначе
					СтруктураОтбора.Вставить("СчетУчета",					Выборка.ПринятыеСчетУчетаБУ);
				КонецЕсли;
				
				НайденныеСтроки = ТаблицаСписанныхПартий.НайтиСтроки(СтруктураОтбора);
				КоличествоПоСтроке = ?(НЕ ЗначениеЗаполнено(Выборка.Количество), 1, Выборка.Количество);
				
				Для Каждого СтрокаТаблицы Из НайденныеСтроки Цикл
					Если КоличествоПоСтроке = 0 Тогда
						Прервать;
					КонецЕсли;
					
					Учесть = Мин(КоличествоПоСтроке, - СтрокаТаблицы.Количество);
					ВыручкаКомиссии = ВыручкаКомиссии + Окр(Цена*Учесть, 2,1);
					СтрокаВозврата.Комиссия = СтрокаВозврата.Комиссия + Окр(Цена*Учесть, 2,1);
					КоличествоПоСтроке = КоличествоПоСтроке - Учесть;
					
					//Отразим выручку комитента
					Если НЕ ДвиженияРегистров = Неопределено Тогда
						
						Если ДвиженияРегистров.ВзаиморасчетыРасход.Количество() = 0 Тогда
							НаборДвижений = РегистрыНакопления.ВзаиморасчетыУСН.СоздатьНаборЗаписей();
							ТаблицаДвижений = НаборДвижений.Выгрузить();
							ТаблицаДвижений.Очистить();
							
							ДвиженияРегистров.ВзаиморасчетыРасход = ТаблицаДвижений.Скопировать();
						КонецЕсли;
						
						Движение = ДвиженияРегистров.ВзаиморасчетыРасход.Добавить();
						Движение.Организация = СтруктураШапкиДокумента.Организация;
						
						Если (НЕ СтрокаТаблицы.ДокументОприходования.Метаданные().Реквизиты.Найти("ДоговорКонтрагента") = Неопределено) Тогда
							Договор = СтрокаТаблицы.ДокументОприходования.ДоговорКонтрагента;
							Движение.ДоговорКонтрагента = Договор;
							КурсВалюты = МодульВалютногоУчета.ПолучитьКурсВалюты(Договор.ВалютаВзаиморасчетов, СтруктураШапкиДокумента.ДатаОплаты);
							СуммаКомиссии = МодульВалютногоУчета.ПересчитатьИзВалютыВВалюту(ВыручкаКомиссии, СтруктураШапкиДокумента.ВалютаДокумента,
							Договор.ВалютаВзаиморасчетов,
							СтруктураШапкиДокумента.КурсДокумента, КурсВалюты.Курс,
							СтруктураШапкиДокумента.КратностьДокумента, КурсВалюты.Кратность);
						КонецЕсли;
						
						Движение.РасчетныйДокумент 	= СтруктураШапкиДокумента.Ссылка;
						
						Движение.СуммаВзаиморасчетов = СуммаКомиссии;
						Движение.ДоходЕНВД 			= 0;
						Движение.ДоходКомитента 	= 0;
						Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
						
					КонецЕсли;
					
				КонецЦикла;
				
				Если ОтноситсяКДеятельностиЕНВД(Выборка.СчетДоходовБУ) Тогда
					ВыручкаЕНВД = ВыручкаЕНВД + Окр(Цена*КоличествоПоСтроке, 2,1);
					СтрокаВозврата.ЕНВД = СтрокаВозврата.ЕНВД + Окр(Цена*КоличествоПоСтроке, 2,1);
				КонецЕсли;
				
				Если КоличествоПоСтроке = 0 Тогда
					Продолжить;
				КонецЕсли;
				
			КонецЦикла;
		КонецЕсли;
		
	КонецЕсли;
	
	ТаблицаВозврата.Свернуть("ДокументПартии", "Всего, ЕНВД, Комиссия");
	
	Возврат ТаблицаВозврата;
	
КонецФункции

//Дополняет таблицу списанных партий колонкой, с новым статусом списания и количеством 
//списанным по новому статусу
Процедура ЗаполнитьОтражениеВНУпоСписанию(СтруктураШапкиДокумента, ТаблицаСписанныхПартий, ВидОперации)

	ВыручкаВсего = 0;
	ВыручкаЕНВД = 0;
	ВыручкаКомиссии = 0;
	МетаданныеДокумента = СтруктураШапкиДокумента.Ссылка.Метаданные();
	ИмяВидаДокумента = МетаданныеДокумента.Имя;
	
	Если ТаблицаСписанныхПартий.Колонки.Найти("СтатусСписания") = Неопределено Тогда
		ТаблицаСписанныхПартий.Колонки.Добавить("СтатусСписания");
	КонецЕсли;
	Если ТаблицаСписанныхПартий.Колонки.Найти("КоличествоПоСтатусуСписания") = Неопределено Тогда
		ТаблицаСписанныхПартий.Колонки.Добавить("КоличествоПоСтатусуСписания");
	КонецЕсли;
	ТаблицаСписанныхПартий.ЗаполнитьЗначения(Перечисления.ОтражениеВУСН.Принимаются, "СтатусСписания");
	ТаблицаСписанныхПартий.ЗаполнитьЗначения(0, "КоличествоПоСтатусуСписания");
	
	ТабличныеЧасти = Новый Массив;
	
	Если НЕ МетаданныеДокумента.ТабличныеЧасти.Найти("ИспользованныеМатериалы") = НЕОПРЕДЕЛЕНО Тогда
		ТабличныеЧасти.Добавить("ИспользованныеМатериалы");
	ИначеЕсли НЕ МетаданныеДокумента.ТабличныеЧасти.Найти("Материалы") = НЕОПРЕДЕЛЕНО Тогда
		ТабличныеЧасти.Добавить("Материалы");
	ИначеЕсли НЕ МетаданныеДокумента.ТабличныеЧасти.Найти("Товары") = НЕОПРЕДЕЛЕНО Тогда
		ТабличныеЧасти.Добавить("Товары");
	ИначеЕсли НЕ МетаданныеДокумента.ТабличныеЧасти.Найти("Спецодежда") = НЕОПРЕДЕЛЕНО Тогда
		ТабличныеЧасти.Добавить("Спецодежда");
		Если НЕ МетаданныеДокумента.ТабличныеЧасти.Найти("Спецоснастка") = НЕОПРЕДЕЛЕНО Тогда
			ТабличныеЧасти.Добавить("Спецоснастка");
		КонецЕсли;
	Иначе
		Возврат;
	КонецЕсли;
	
	Если ВидОперации = "ВозвратПоставщику" Тогда
		Для Каждого Строка Из ТаблицаСписанныхПартий Цикл
			Строка.СтатусСписания = Перечисления.ОтражениеВУСН.НеПринимаются;
			Строка.КоличествоПоСтатусуСписания = Строка.Количество;
		КонецЦикла;
		Возврат;
	КонецЕсли;
	
	ЕстьСерия 			= НЕ (ТаблицаСписанныхПартий.Колонки.Найти("СерияНоменклатуры") = Неопределено);
	ЕстьХарактеристика 	= НЕ (ТаблицаСписанныхПартий.Колонки.Найти("ХарактеристикаНоменклатуры") = Неопределено);
	Переданные 			= (ЕстьХарактеристика И (НЕ ЕстьСерия));
	
	Для Каждого ИмяТЧ Из ТабличныеЧасти Цикл
		//Если отражение в НУ указвается явно
		Если ОбщегоНазначения.ЕстьРеквизитТабЧастиДокумента("ОтражениеВУСН", МетаданныеДокумента, ИмяТЧ) Тогда
			
			Запрос = Новый Запрос;
			Запрос.УстановитьПараметр("Ссылка",		 СтруктураШапкиДокумента.Ссылка);
			Запрос.УстановитьПараметр("Организация", СтруктураШапкиДокумента.Организация);
			
			Текст =
			
			"ВЫБРАТЬ
			|	СУММА(РеализацияТоваровУслугТовары.Количество) КАК Количество,
			|	РеализацияТоваровУслугТовары.ОтражениеВУСН,
			|	РеализацияТоваровУслугТовары.Номенклатура";
			
			Если ЕстьХарактеристика Тогда
				Текст = Текст + ",
				|	РеализацияТоваровУслугТовары.ХарактеристикаНоменклатуры";
			КонецЕсли;
			
			Если ЕстьСерия Тогда
				Текст = Текст + ",
				|	РеализацияТоваровУслугТовары.СерияНоменклатуры";
			КонецЕсли;
			
			Текст = Текст + "
			|ИЗ
			|	Документ." + ИмяВидаДокумента + "." + ИмяТЧ + " КАК РеализацияТоваровУслугТовары
			|
			|ГДЕ
			|	РеализацияТоваровУслугТовары.Ссылка = &Ссылка
			|
			|СГРУППИРОВАТЬ ПО
			|	РеализацияТоваровУслугТовары.ОтражениеВУСН,
			|	РеализацияТоваровУслугТовары.Номенклатура";
			
			Если ЕстьХарактеристика Тогда
				Текст = Текст + ",
				|	РеализацияТоваровУслугТовары.ХарактеристикаНоменклатуры";
			КонецЕсли;
			
			Если ЕстьСерия Тогда
				Текст = Текст + ",
				|	РеализацияТоваровУслугТовары.СерияНоменклатуры";
			КонецЕсли;
			
			Запрос.Текст = Текст;
			Выборка = Запрос.Выполнить().Выбрать();		
			Пока Выборка.Следующий() Цикл
				Если Выборка.ОтражениеВУСН <> Перечисления.ОтражениеВУСН.Принимаются Тогда
					
					СтруктураОтбора = Новый Структура;
					СтруктураОтбора.Вставить("Номенклатура", Выборка.Номенклатура);
					
					Если ЕстьХарактеристика Тогда
						СтруктураОтбора.Вставить("ХарактеристикаНоменклатуры", Выборка.ХарактеристикаНоменклатуры);
					КонецЕсли;
					
					Если ЕстьСерия Тогда
						СтруктураОтбора.Вставить("СерияНоменклатуры", Выборка.СерияНоменклатуры);
					КонецЕсли;
					
					НайденныеСтроки = ТаблицаСписанныхПартий.НайтиСтроки(СтруктураОтбора);
					КоличествоПоСтроке = Выборка.Количество;
					
					Для Каждого СтрокаТаблицы Из НайденныеСтроки Цикл
						Если КоличествоПоСтроке = 0 Тогда
							Прервать;
						КонецЕсли;
						
						Учесть = Мин(КоличествоПоСтроке, СтрокаТаблицы.Количество - СтрокаТаблицы.КоличествоПоСтатусуСписания);
						
						Если Учесть = 0 Тогда
							Продолжить;
						КонецЕсли;
						
						Если НЕ ЗначениеЗаполнено(СтрокаТаблицы.СтатусСписания) 
							ИЛИ (СтрокаТаблицы.СтатусСписания = Выборка.ОтражениеВУСН) Тогда
							СтрокаТаблицы.СтатусСписания = Выборка.ОтражениеВУСН;
							СтрокаТаблицы.КоличествоПоСтатусуСписания = СтрокаТаблицы.КоличествоПоСтатусуСписания + Учесть;
							
						Иначе
							НоваяСтрока = ТаблицаСписанныхПартий.Добавить();
							ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицы);
							
							НоваяСтрока.Количество = Учесть;
							НоваяСтрока.КоличествоПоСтатусуСписания = Учесть;
							НоваяСтрока.СтатусСписания = ?(ЗначениеЗаполнено(Выборка.ОтражениеВУСН), Выборка.ОтражениеВУСН, Перечисления.ОтражениеВУСН.НеПринимаются);
							
							СтрокаТаблицы.Количество = СтрокаТаблицы.Количество - Учесть;
							
						КонецЕсли;
						КоличествоПоСтроке = КоличествоПоСтроке - Учесть;
					КонецЦикла;
					
					//Если серия партии не заполнена
					Если (ЕстьСерия) И (КоличествоПоСтроке > 0) Тогда
						СтруктураОтбора = Новый Структура;
						СтруктураОтбора.Вставить("Номенклатура", Выборка.Номенклатура);
						СтруктураОтбора.Вставить("СерияНоменклатуры", Справочники.СерииНоменклатуры.ПустаяСсылка());
						
						Если ЕстьХарактеристика Тогда
							СтруктураОтбора.Вставить("ХарактеристикаНоменклатуры", Выборка.ХарактеристикаНоменклатуры);
						КонецЕсли;
						
						НайденныеСтроки = ТаблицаСписанныхПартий.НайтиСтроки(СтруктураОтбора);
						
						Для Каждого СтрокаТаблицы Из НайденныеСтроки Цикл
							Если КоличествоПоСтроке = 0 Тогда
								Прервать;
							КонецЕсли;
							
							Учесть = Мин(КоличествоПоСтроке, СтрокаТаблицы.Количество - СтрокаТаблицы.КоличествоПоСтатусуСписания);
							
							Если Учесть = 0 Тогда
								Продолжить;
							КонецЕсли;
							
							Если НЕ ЗначениеЗаполнено(СтрокаТаблицы.СтатусСписания) 
								ИЛИ (СтрокаТаблицы.СтатусСписания = Выборка.ОтражениеВУСН) Тогда
								СтрокаТаблицы.СтатусСписания = Выборка.ОтражениеВУСН;
								СтрокаТаблицы.КоличествоПоСтатусуСписания = СтрокаТаблицы.КоличествоПоСтатусуСписания + Учесть;
								
							Иначе
								НоваяСтрока = ТаблицаСписанныхПартий.Добавить();
								ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицы);

								НоваяСтрока.Количество = Учесть;
								НоваяСтрока.КоличествоПоСтатусуСписания = Учесть;
								НоваяСтрока.СтатусСписания = ?(ЗначениеЗаполнено(Выборка.ОтражениеВУСН), Выборка.ОтражениеВУСН, Перечисления.ОтражениеВУСН.НеПринимаются);
							
								СтрокаТаблицы.Количество = СтрокаТаблицы.Количество - Учесть;
								
							КонецЕсли;
							КоличествоПоСтроке = КоличествоПоСтроке - Учесть;
						КонецЦикла;
					КонецЕсли;
					
				КонецЕсли;
			КонецЦикла;
			
			//Если реализация - выделяем реализованные по ЕНВД и устанавливаем им статус не принимаемых
		ИначеЕсли ОбщегоНазначения.ЕстьРеквизитТабЧастиДокумента("СчетДоходовБУ", МетаданныеДокумента, ИмяТЧ) Тогда
			
			Запрос = Новый Запрос;
			Запрос.УстановитьПараметр("Ссылка",		 СтруктураШапкиДокумента.Ссылка);
			Запрос.УстановитьПараметр("Организация", СтруктураШапкиДокумента.Организация);
			
			Текст =
			
			"ВЫБРАТЬ
			|	СУММА(РеализацияТоваровУслугТовары.Количество) КАК Количество,
			|	РеализацияТоваровУслугТовары.СчетДоходовБУ,
			|	РеализацияТоваровУслугТовары.Номенклатура";
			
			Если ЕстьХарактеристика Тогда
				Текст = Текст + ",
				|	РеализацияТоваровУслугТовары.ХарактеристикаНоменклатуры";
			КонецЕсли;
			
			Если ЕстьСерия Тогда
				Текст = Текст + ",
				|	РеализацияТоваровУслугТовары.СерияНоменклатуры";
			КонецЕсли;
			
			Текст = Текст + "
			|ИЗ
			|	Документ." + ИмяВидаДокумента + "." + ИмяТЧ + " КАК РеализацияТоваровУслугТовары
			|
			|ГДЕ
			|	РеализацияТоваровУслугТовары.Ссылка = &Ссылка
			|
			|СГРУППИРОВАТЬ ПО
			|	РеализацияТоваровУслугТовары.СчетДоходовБУ,
			|	РеализацияТоваровУслугТовары.Номенклатура";
			
			Если ЕстьХарактеристика Тогда
				Текст = Текст + ",
				|	РеализацияТоваровУслугТовары.ХарактеристикаНоменклатуры";
			КонецЕсли;
			
			Если ЕстьСерия Тогда
				Текст = Текст + ",
				|	РеализацияТоваровУслугТовары.СерияНоменклатуры";
			КонецЕсли;
			
			Запрос.Текст = Текст;
			Выборка = Запрос.Выполнить().Выбрать();		
			Пока Выборка.Следующий() Цикл
				Если ОтноситсяКДеятельностиЕНВД(Выборка.СчетДоходовБУ) Тогда
					СтруктураОтбора = Новый Структура;
					СтруктураОтбора.Вставить("Номенклатура",				Выборка.Номенклатура);
					
					Если ЕстьХарактеристика Тогда
						СтруктураОтбора.Вставить("ХарактеристикаНоменклатуры", Выборка.ХарактеристикаНоменклатуры);
					КонецЕсли;
					
					Если ЕстьСерия Тогда
						СтруктураОтбора.Вставить("СерияНоменклатуры", Выборка.СерияНоменклатуры);
					КонецЕсли;
					
					НайденныеСтроки = ТаблицаСписанныхПартий.НайтиСтроки(СтруктураОтбора);
					КоличествоПоСтроке = Выборка.Количество;
					
					Для Каждого СтрокаТаблицы Из НайденныеСтроки Цикл
						Если КоличествоПоСтроке = 0 Тогда
							Прервать;
						КонецЕсли;
						Учесть = Мин(КоличествоПоСтроке, СтрокаТаблицы.Количество - СтрокаТаблицы.КоличествоПоСтатусуСписания);
						СтрокаТаблицы.СтатусСписания = Перечисления.ОтражениеВУСН.НеПринимаются;
						СтрокаТаблицы.КоличествоПоСтатусуСписания = СтрокаТаблицы.КоличествоПоСтатусуСписания + Учесть;
						КоличествоПоСтроке = КоличествоПоСтроке - Учесть;
					КонецЦикла;
					
					//Если серия партии не заполнена
					Если (ЕстьСерия) И (КоличествоПоСтроке > 0) Тогда
						СтруктураОтбора = Новый Структура;
						СтруктураОтбора.Вставить("Номенклатура", Выборка.Номенклатура);
						СтруктураОтбора.Вставить("СерияНоменклатуры", Справочники.СерииНоменклатуры.ПустаяСсылка());
						
						Если ЕстьХарактеристика Тогда
							СтруктураОтбора.Вставить("ХарактеристикаНоменклатуры", Выборка.ХарактеристикаНоменклатуры);
						КонецЕсли;
						
						НайденныеСтроки = ТаблицаСписанныхПартий.НайтиСтроки(СтруктураОтбора);
						
						Для Каждого СтрокаТаблицы Из НайденныеСтроки Цикл
							Если КоличествоПоСтроке = 0 Тогда
								Прервать;
							КонецЕсли;
							Учесть = Мин(КоличествоПоСтроке, СтрокаТаблицы.Количество - СтрокаТаблицы.КоличествоПоСтатусуСписания);
							СтрокаТаблицы.СтатусСписания = Перечисления.ОтражениеВУСН.НеПринимаются;
							СтрокаТаблицы.КоличествоПоСтатусуСписания = СтрокаТаблицы.КоличествоПоСтатусуСписания + Учесть;
							КоличествоПоСтроке = КоличествоПоСтроке - Учесть;
						КонецЦикла;
					КонецЕсли;

				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
	КонецЦикла;

КонецПроцедуры

//Формирует таблицы движений регистра "Расходы при УСН" по таблице остатков расходов
//на заданную сумму по установленному виду операции
Функция ИзменениеСтатусовРасходов(СтруктураШапкиДокумента, ТаблицаРасходов, Сумма = 0, ВидОперации = "Оплата", КонтролироватьСумму = Истина, ДокументОплаты = Неопределено, ВходящаяСтруктура = Неопределено)
	
	ВыделятьНДСУСН = (СтруктураШапкиДокумента.ПорядокПризнанияРасходовПоНДС = Перечисления.ПорядокПризнанияРасходовПоНДС.ПоОплатеПоставщику);
	
	ТаблицаКорректировкиРасход = ТаблицаРасходов.СкопироватьКолонки();
	ТаблицаКорректировкиПриход = ТаблицаРасходов.СкопироватьКолонки();
	ТаблицаПризнанныхРасходов  = ТаблицаРасходов.СкопироватьКолонки();
	
	Если ТаблицаРасходов.Колонки.Найти("НДС") = Неопределено Тогда
		ТаблицаРасходов.Колонки.Добавить("НДС");
		ТаблицаРасходов.ЗаполнитьЗначения(0, "НДС");
	КонецЕсли;
	Если ТаблицаРасходов.Колонки.Найти("Количество") = Неопределено Тогда
		ТаблицаРасходов.Колонки.Добавить("Количество");
		ТаблицаРасходов.ЗаполнитьЗначения(0, "Количество");
	КонецЕсли;
	Если ТаблицаРасходов.Колонки.Найти("РеквизитыДокументаОплаты") = Неопределено Тогда
		ТаблицаРасходов.Колонки.Добавить("РеквизитыДокументаОплаты");
	КонецЕсли;
	
	Для Каждого Расход Из ТаблицаРасходов Цикл
		
		Если ЗначениеЗаполнено(Расход.РеквизитыДокументаОплаты) Тогда
			УчетнаяПолитикаДокумент = ОбщегоНазначения.ПолучитьПараметрыУчетнойПолитикиРегл(Расход.РеквизитыДокументаОплаты.Дата, СтруктураШапкиДокумента.Организация, Ложь);
			ВыделятьНДСУСНДокумент = ?(НЕ ЗначениеЗаполнено(УчетнаяПолитикаДокумент), ВыделятьНДСУСН, (УчетнаяПолитикаДокумент.ПорядокПризнанияРасходовПоНДС = Перечисления.ПорядокПризнанияРасходовПоНДС.ПоОплатеПоставщику));
		ИначеЕсли ЗначениеЗаполнено(Расход.РасчетныйДокумент) Тогда
			УчетнаяПолитикаДокумент = ОбщегоНазначения.ПолучитьПараметрыУчетнойПолитикиРегл(Расход.РасчетныйДокумент.Дата, СтруктураШапкиДокумента.Организация, Ложь);
			ВыделятьНДСУСНДокумент = ?(НЕ ЗначениеЗаполнено(УчетнаяПолитикаДокумент), ВыделятьНДСУСН, (УчетнаяПолитикаДокумент.ПорядокПризнанияРасходовПоНДС = Перечисления.ПорядокПризнанияРасходовПоНДС.ПоОплатеПоставщику));
		Иначе
			ВыделятьНДСУСНДокумент = ВыделятьНДСУСН;
		КонецЕсли;
		
		Если КонтролироватьСумму Тогда
			Если Сумма = 0 Тогда
				Прервать;
			КонецЕсли;
			Если Расход.Сумма = 0 Тогда
				Продолжить;
			КонецЕсли;
			Если ВыделятьНДСУСНДокумент И 
				(Расход.ВидРасхода <> Перечисления.ВидыРасходовУСН.ОС) И
				(Расход.ВидРасхода <> Перечисления.ВидыРасходовУСН.НМА) Тогда
				СуммаКорректировки = Мин(Расход.Сумма + Расход.НДС, Сумма);
				Сумма = Макс(0, Сумма - СуммаКорректировки);
				К = СуммаКорректировки / (Расход.Сумма + Расход.НДС);
			Иначе
				СуммаКорректировки = Мин(Расход.Сумма, Сумма);
				Сумма = Сумма - СуммаКорректировки;
				К = СуммаКорректировки / Расход.Сумма;
			КонецЕсли;
		Иначе
			К = 1;
			СуммаКорректировки = Расход.Сумма;
		КонецЕсли;
		ПризнаватьРасход = Ложь;
		НовыйСтатусОплаты = ПолучитьНовыйСтатусОплаты(СтруктураШапкиДокумента, Расход, Расход.СтатусыОплатыРасходовУСН, ВидОперации, ПризнаватьРасход);
		Если НЕ ВидОперации = "РаспределениеЕНВД" Тогда
			Если НЕ ЗначениеЗаполнено(НовыйСтатусОплаты) И Расход.ОтражениеВУСН = Перечисления.ОтражениеВУСН.Распределяются Тогда
				НовыйСтатусОплаты = Перечисления.СтатусыРасходовУСН.НеРаспределено;
			КонецЕсли;
			Если (НЕ ЗначениеЗаполнено(НовыйСтатусОплаты)) И (НЕ ТаблицаРасходов.Колонки.Найти("СтатусСписания") = Неопределено) Тогда
				НовыйСтатусРасхода = ПолучитьКомбнациюСтатусовОтражениеВНУ(Расход.ОтражениеВУСН, Расход.СтатусСписания);
				
				Если НовыйСтатусРасхода = Перечисления.ОтражениеВУСН.Распределяются Тогда
					НовыйСтатусОплаты = Перечисления.СтатусыРасходовУСН.НеРаспределено;
				КонецЕсли;
				
			КонецЕсли;
		КонецЕсли;	
		СтрокаКорректировкиРасход = ТаблицаКорректировкиРасход.Добавить();
		
		НомерСтрокиРасходов = 0;
		Если ЗначениеЗаполнено(НовыйСтатусОплаты) Тогда
			СтрокаКорректировкиПриход = ТаблицаКорректировкиПриход.Добавить();
		Иначе
			СтрокаКорректировкиПриход = ТаблицаПризнанныхРасходов.Добавить();
			НомерСтрокиРасходов = ТаблицаПризнанныхРасходов.Количество();
		КонецЕсли;
		
		ЗаполнитьЗначенияСвойств(СтрокаКорректировкиРасход, Расход);
		ЗаполнитьЗначенияСвойств(СтрокаКорректировкиПриход, Расход);
		
		Если ВидОперации = "РаспределениеЕНВД" Тогда
			Если НЕ ЗначениеЗаполнено(НовыйСтатусОплаты) И Расход.ОтражениеВУСН = Перечисления.ОтражениеВУСН.Распределяются Тогда
				СтрокаКорректировкиПриход.ОтражениеВУСН = Перечисления.ОтражениеВУСН.Принимаются;
			КонецЕсли;
		КонецЕсли;	

		СтрокаКорректировкиПриход.СтатусыОплатыРасходовУСН = НовыйСтатусОплаты; 
		Если НЕ К = 1 Тогда
			СтрокаКорректировкиРасход.Сумма 		= К*СтрокаКорректировкиРасход.Сумма;
			СтрокаКорректировкиРасход.Количество	= К*СтрокаКорректировкиРасход.Количество;
			СтрокаКорректировкиРасход.НДС			= К*СтрокаКорректировкиРасход.НДС;
			СтрокаКорректировкиПриход.Сумма 		= К*СтрокаКорректировкиПриход.Сумма;
			СтрокаКорректировкиПриход.Количество	= К*СтрокаКорректировкиПриход.Количество;
			СтрокаКорректировкиПриход.НДС			= К*СтрокаКорректировкиПриход.НДС;
		КонецЕсли;
		Если НЕ ТаблицаРасходов.Колонки.Найти("СтруктураКурса") = Неопределено Тогда
			Если ЗначениеЗаполнено(Расход.СтруктураКурса) Тогда
				Курс = Расход.СтруктураКурса.Курс;
				Кратность = ?(Расход.СтруктураКурса.Кратность = 0, 1, Расход.СтруктураКурса.Кратность);
				СтрокаКорректировкиПриход.Сумма 		= СтрокаКорректировкиПриход.Сумма*Курс/Кратность;
				СтрокаКорректировкиПриход.НДС			= СтрокаКорректировкиПриход.НДС*Курс/Кратность;
				Если Курс <> 1 Тогда
					СтрокаКорректировкиПриход.Валюта = глЗначениеПеременной("ВалютаРегламентированногоУчета");
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
		//Признаем расходы в части входящего НДС
		Если (ВидОперации = "Оплата") И (СтрокаКорректировкиПриход.НДС > 0) И 
			(СтрокаКорректировкиРасход.ВидРасхода <> Перечисления.ВидыРасходовУСН.ОС) И
			(СтрокаКорректировкиРасход.ВидРасхода <> Перечисления.ВидыРасходовУСН.НМА) И
			(СтрокаКорректировкиРасход.ОтражениеВУСН <> Перечисления.ОтражениеВУСН.ПустаяСсылка()) И
			(СтрокаКорректировкиРасход.ОтражениеВУСН <> Перечисления.ОтражениеВУСН.НеПринимаются) Тогда
			
			Если (ВыделятьНДСУСН) Тогда
				Если СтрокаКорректировкиРасход.ОтражениеВУСН = Перечисления.ОтражениеВУСН.Принимаются Тогда
					СтрокаНДС = ТаблицаПризнанныхРасходов.Добавить();
				Иначе
					СтрокаНДС = ТаблицаКорректировкиПриход.Добавить();
				КонецЕсли;
				ЗаполнитьЗначенияСвойств(СтрокаНДС, Расход);
				
				Если НЕ СтрокаКорректировкиРасход.ОтражениеВУСН = Перечисления.ОтражениеВУСН.Принимаются Тогда
					СтрокаНДС.СтатусыОплатыРасходовУСН = Перечисления.СтатусыРасходовУСН.НеРаспределено;
				КонецЕсли;
				
				СтрокаНДС.ВидРасхода	= Перечисления.ВидыРасходовУСН.НДС;
				СтрокаНДС.Сумма 		= СтрокаКорректировкиПриход.НДС;
				СтрокаНДС.Валюта		= СтрокаКорректировкиПриход.Валюта;
				СтрокаНДС.Количество	= 0;
				СтрокаНДС.НДС			= 0;
				
				Если Не ВыделятьНДСУСНДокумент Тогда
					СтрокаКорректировкиПриход.Сумма = СтрокаКорректировкиПриход.Сумма - СтрокаКорректировкиПриход.НДС;
				КонецЕсли;
				
			ИначеЕсли (НЕ ВыделятьНДСУСН) И (ВыделятьНДСУСНДокумент) Тогда
				
				СтрокаКорректировкиПриход.Сумма = СтрокаКорректировкиПриход.Сумма + СтрокаКорректировкиПриход.НДС;
			КонецЕсли;
		КонецЕсли;
		
		//Признаем расходы, отражанные по статусу "Не списано, принято"
		Если (ВидОперации = "Оплата") И 
			(СтрокаКорректировкиПриход.СтатусыОплатыРасходовУСН = Перечисления.СтатусыРасходовУСН.НеСписаноПринято) И 
			(СтрокаКорректировкиРасход.ОтражениеВУСН = Перечисления.ОтражениеВУСН.Принимаются) Тогда
			
			СтрокаРасходов = ТаблицаПризнанныхРасходов.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаРасходов, СтрокаКорректировкиПриход);
		КонецЕсли;
				
		// Измемнение статуса расхода по списанию
		Если НЕ ТаблицаРасходов.Колонки.Найти("СтатусСписания") = Неопределено Тогда
			НовыйСтатусРасхода = ПолучитьКомбнациюСтатусовОтражениеВНУ(Расход.ОтражениеВУСН, Расход.СтатусСписания);
			Если НЕ ЗначениеЗаполнено(Расход.СтатусСписания) Тогда
				
			ИначеЕсли Расход.ОтражениеВУСН = НовыйСтатусРасхода Тогда
				
			ИначеЕсли Расход.Количество > Расход.КоличествоПоСтатусуСписания Тогда
				
				К = Расход.КоличествоПоСтатусуСписания / Расход.Количество;
				СумКорректировки = К*СтрокаКорректировкиПриход.Сумма;
				КолКорректировки = К*СтрокаКорректировкиПриход.Количество;
				НДСКорректировки = К*СтрокаКорректировкиПриход.НДС;
				СтрокаКорректировкиПриход.Сумма 		= СтрокаКорректировкиПриход.Сумма - СумКорректировки;
				СтрокаКорректировкиПриход.Количество 	= СтрокаКорректировкиПриход.Количество - КолКорректировки;
				СтрокаКорректировкиПриход.НДС 			= СтрокаКорректировкиПриход.НДС - НДСКорректировки;
				
				Если НЕ ЗначениеЗаполнено(НовыйСтатусОплаты) Тогда
					СтрокаКорректировкиПриход = ТаблицаПризнанныхРасходов.Добавить();
				Иначе
					СтрокаКорректировкиПриход = ТаблицаКорректировкиПриход.Добавить();
				КонецЕсли;
				
				ЗаполнитьЗначенияСвойств(СтрокаКорректировкиРасход, Расход);
				ЗаполнитьЗначенияСвойств(СтрокаКорректировкиПриход, Расход);
				
				Если НЕ ЗначениеЗаполнено(НовыйСтатусОплаты) И НовыйСтатусРасхода = Перечисления.ОтражениеВУСН.Распределяются Тогда
					НовыйСтатусОплаты = Перечисления.СтатусыРасходовУСН.НеРаспределено;
				КонецЕсли;
				Если НЕ ЗначениеЗаполнено(НовыйСтатусОплаты) И НовыйСтатусРасхода = Перечисления.ОтражениеВУСН.НеПринимаются Тогда
					ПризнаватьРасход = Ложь;
				КонецЕсли;
				СтрокаКорректировкиПриход.СтатусыОплатыРасходовУСН = НовыйСтатусОплаты; 
				СтрокаКорректировкиПриход.ОтражениеВУСН  = НовыйСтатусРасхода;
				СтрокаКорректировкиПриход.Сумма 		= СумКорректировки;
				СтрокаКорректировкиПриход.Количество 	= КолКорректировки;
				СтрокаКорректировкиПриход.НДС 			= НДСКорректировки;
								
				ПроверитьСторноРасходовПОСписаню(СтруктураШапкиДокумента, НомерСтрокиРасходов, ВыделятьНДСУСНДокумент,
												ТаблицаКорректировкиРасход, ТаблицаКорректировкиПриход, ТаблицаПризнанныхРасходов,
												СтрокаКорректировкиРасход, СтрокаКорректировкиПриход);
			Иначе
                Если НЕ ЗначениеЗаполнено(НовыйСтатусОплаты) И НовыйСтатусРасхода = Перечисления.ОтражениеВУСН.Распределяются Тогда
					НовыйСтатусОплаты = Перечисления.СтатусыРасходовУСН.НеРаспределено;
				КонецЕсли;
				Если НЕ ЗначениеЗаполнено(НовыйСтатусОплаты) И НовыйСтатусРасхода = Перечисления.ОтражениеВУСН.НеПринимаются Тогда
					ПризнаватьРасход = Ложь;
				КонецЕсли;
				СтрокаКорректировкиПриход.СтатусыОплатыРасходовУСН = НовыйСтатусОплаты;
				СтрокаКорректировкиПриход.ОтражениеВУСН = НовыйСтатусРасхода;
				
				ПроверитьСторноРасходовПОСписаню(СтруктураШапкиДокумента, НомерСтрокиРасходов, ВыделятьНДСУСНДокумент,
											ТаблицаКорректировкиРасход, ТаблицаКорректировкиПриход, ТаблицаПризнанныхРасходов,
											СтрокаКорректировкиРасход, СтрокаКорректировкиПриход);
			КонецЕсли;
		КонецЕсли;
		
		Если (НомерСтрокиРасходов > 0) И (СтрокаКорректировкиПриход.НДС > 0) И
			(НЕ ВыделятьНДСУСН) И (ВыделятьНДСУСНДокумент) Тогда
			
			СтрокаКорректировкиПриход.НДС = 0;		
		КонецЕсли;
	
		Если (НомерСтрокиРасходов > 0) и (НЕ ПризнаватьРасход) Тогда
			ТаблицаПризнанныхРасходов.Удалить(НомерСтрокиРасходов-1);
		КонецЕсли;
        
		Если К = 1 Тогда
			Расход.Сумма = 0;
			Расход.Количество = 0;
			Расход.НДС = 0;
		Иначе
			Расход.Сумма = Расход.Сумма - К*Расход.Сумма;
			Расход.Количество = Расход.Количество - К*Расход.Количество;
			Расход.НДС = Расход.НДС - К*Расход.НДС;
		КонецЕсли;

	КонецЦикла;

	Если ВидОперации = "Оплата" Тогда
		Если ТаблицаКорректировкиПриход.Колонки.Найти("РеквизитыДокументаОплаты") = Неопределено Тогда
			ТаблицаКорректировкиПриход.Колонки.Добавить("РеквизитыДокументаОплаты");
		КонецЕсли;
		Если ТаблицаПризнанныхРасходов.Колонки.Найти("РеквизитыДокументаОплаты") = Неопределено Тогда
			ТаблицаПризнанныхРасходов.Колонки.Добавить("РеквизитыДокументаОплаты");
		КонецЕсли;
		ТаблицаКорректировкиПриход.ЗаполнитьЗначения(?(ЗначениеЗаполнено(ДокументОплаты), ДокументОплаты, СтруктураШапкиДокумента.Ссылка),"РеквизитыДокументаОплаты");
		ТаблицаПризнанныхРасходов.ЗаполнитьЗначения(?(ЗначениеЗаполнено(ДокументОплаты), ДокументОплаты, СтруктураШапкиДокумента.Ссылка),"РеквизитыДокументаОплаты");
	КонецЕсли;
	
	
	Если ЗначениеЗаполнено(ВходящаяСтруктура) Тогда
		Если ВходящаяСтруктура.ТаблицаПриход.Количество() > 0 Тогда
			Для Каждого Строка Из ТаблицаКорректировкиПриход Цикл
				НоваяСтрока = ВходящаяСтруктура.ТаблицаПриход.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);
			КонецЦикла;	
		Иначе
			ВходящаяСтруктура.ТаблицаПриход = ТаблицаКорректировкиПриход;
		КонецЕсли;		
		Если ВходящаяСтруктура.ТаблицаРасход.Количество() > 0 Тогда
			Для Каждого Строка Из ТаблицаКорректировкиРасход Цикл
				НоваяСтрока = ВходящаяСтруктура.ТаблицаРасход.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);
			КонецЦикла;
		Иначе
			ВходящаяСтруктура.ТаблицаРасход = ТаблицаКорректировкиРасход;
		КонецЕсли;
		Если ВходящаяСтруктура.ТаблицаПринятых.Количество() > 0 Тогда
			Для Каждого Строка Из ТаблицаПризнанныхРасходов Цикл
				НоваяСтрока = ВходящаяСтруктура.ТаблицаПринятых.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);
			КонецЦикла;
		Иначе
			ВходящаяСтруктура.ТаблицаПринятых = ТаблицаПризнанныхРасходов;
		КонецЕсли;
		ИтоговаяСтруктура = ВходящаяСтруктура;
	Иначе
		ИтоговаяСтруктура = Новый Структура("ТаблицаПриход, ТаблицаРасход, ТаблицаПринятых", ТаблицаКорректировкиПриход, ТаблицаКорректировкиРасход, ТаблицаПризнанныхРасходов);
	КонецЕсли;

	Возврат ИтоговаяСтруктура;
	
КонецФункции

Процедура ПроверитьСторноРасходовПОСписаню(СтруктураШапкиДокумента, НомерСтрокиРасходов, ВыделятьНДСУСНДокумент,
											ТаблицаКорректировкиРасход, ТаблицаКорректировкиПриход, ТаблицаПризнанныхРасходов,
											СтрокаКорректировкиРасход, СтрокаКорректировкиПриход)
											
	Если СтрокаКорректировкиРасход.ОтражениеВУСН = Перечисления.ОтражениеВУСН.НеПринимаются Тогда
		Возврат;
	КонецЕсли;
	
	ВыделятьНДСУСН = ВыделятьНДСУСНДокумент;
	//ВыделятьНДСУСН = (СтруктураШапкиДокумента.ПорядокПризнанияРасходовПоНДС = Перечисления.ПорядокПризнанияРасходовПоНДС.ПоОплатеПоставщику);
	
	Если (СтрокаКорректировкиПриход.ОтражениеВУСН = Перечисления.ОтражениеВУСН.Распределяются) И 
		(СтрокаКорректировкиРасход.ОтражениеВУСН = Перечисления.ОтражениеВУСН.Принимаются) Тогда
		
		//В части входящего НДС
		Если (ВыделятьНДСУСН) И (СтрокаКорректировкиПриход.НДС > 0) И 
			(СтрокаКорректировкиРасход.ВидРасхода <> Перечисления.ВидыРасходовУСН.ОС) И
			(СтрокаКорректировкиРасход.ВидРасхода <> Перечисления.ВидыРасходовУСН.НМА) И
			(СтрокаКорректировкиПриход.СтатусыОплатыРасходовУСН <> Перечисления.СтатусыРасходовУСН.НеОплачено) И
			(СтрокаКорректировкиПриход.СтатусыОплатыРасходовУСН <> Перечисления.СтатусыРасходовУСН.НеОплаченоНеОплаченоПокупателем) Тогда
			
			//Сторно
			СтрокаНДС = ТаблицаПризнанныхРасходов.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаНДС, СтрокаКорректировкиРасход);
			
			СтрокаНДС.ВидРасхода	= Перечисления.ВидыРасходовУСН.НДС;
			СтрокаНДС.Сумма 		= - СтрокаКорректировкиПриход.НДС;
			СтрокаНДС.Количество	= 0;
			СтрокаНДС.НДС			= 0;
			
			//Распределяемые расходы
			СтрокаНДС = ТаблицаКорректировкиПриход.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаНДС, СтрокаКорректировкиПриход);
			
			СтрокаНДС.СтатусыОплатыРасходовУСН = Перечисления.СтатусыРасходовУСН.НеРаспределено;
			СтрокаНДС.ВидРасхода	= Перечисления.ВидыРасходовУСН.НДС;
			СтрокаНДС.Сумма 		= СтрокаКорректировкиПриход.НДС;
			СтрокаНДС.Количество	= 0;
			СтрокаНДС.НДС			= 0;
		КонецЕсли;
		
		Если СтрокаКорректировкиРасход.СтатусыОплатыРасходовУСН = Перечисления.СтатусыРасходовУСН.НеСписаноПринято Тогда
			
			//Сторно
			СтрокаСторно = ТаблицаПризнанныхРасходов.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаСторно, СтрокаКорректировкиРасход);
			
			СтрокаСторно.Сумма 		= - СтрокаКорректировкиПриход.Сумма;
			СтрокаСторно.Количество	= - СтрокаКорректировкиПриход.Количество;
			СтрокаСторно.НДС		= - СтрокаКорректировкиПриход.НДС;
			
			//Распределяемые расходы
			СтрокаКорректировкиПриход.СтатусыОплатыРасходовУСН = Перечисления.СтатусыРасходовУСН.НеРаспределено;
			
		КонецЕсли;
		
	ИначеЕсли (СтрокаКорректировкиПриход.ОтражениеВУСН = Перечисления.ОтражениеВУСН.НеПринимаются) И 
		(СтрокаКорректировкиРасход.ОтражениеВУСН = Перечисления.ОтражениеВУСН.Принимаются) Тогда
		
		//В части входящего НДС
		Если (ВыделятьНДСУСН) И (СтрокаКорректировкиПриход.НДС > 0) И 
			(СтрокаКорректировкиРасход.ВидРасхода <> Перечисления.ВидыРасходовУСН.ОС) И
			(СтрокаКорректировкиРасход.ВидРасхода <> Перечисления.ВидыРасходовУСН.НМА) И
			(СтрокаКорректировкиПриход.СтатусыОплатыРасходовУСН <> Перечисления.СтатусыРасходовУСН.НеОплачено) И
			(СтрокаКорректировкиПриход.СтатусыОплатыРасходовУСН <> Перечисления.СтатусыРасходовУСН.НеОплаченоНеОплаченоПокупателем) Тогда
			
			//Сторно
			СтрокаНДС = ТаблицаПризнанныхРасходов.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаНДС, СтрокаКорректировкиРасход);
			
			СтрокаНДС.ВидРасхода	= Перечисления.ВидыРасходовУСН.НДС;
			СтрокаНДС.Сумма 		= - СтрокаКорректировкиПриход.НДС;
			СтрокаНДС.Количество	= 0;
			СтрокаНДС.НДС			= 0;
			
		КонецЕсли;
		
		Если СтрокаКорректировкиРасход.СтатусыОплатыРасходовУСН = Перечисления.СтатусыРасходовУСН.НеСписаноПринято Тогда
			
			//Сторно
			НомерСтрокиРасходов = 0;
			СтрокаКорректировкиПриход.Сумма 		= - СтрокаКорректировкиПриход.Сумма;
			СтрокаКорректировкиПриход.Количество	= - СтрокаКорректировкиПриход.Количество;
			СтрокаКорректировкиПриход.НДС			= - СтрокаКорректировкиПриход.НДС;
		КонецЕсли;
		
	ИначеЕсли (СтрокаКорректировкиПриход.ОтражениеВУСН = Перечисления.ОтражениеВУСН.Распределяются) И 
		(СтрокаКорректировкиРасход.ОтражениеВУСН = Перечисления.ОтражениеВУСН.НеПринимаются) Тогда //Не обрабатываем
		
	КонецЕсли;
	
КонецПроцедуры

//Отражает прочее (настроенное вручную) движение денежных средств в КУДиР
//
Процедура ПрочееДДС(Документ) Экспорт

	Период = ?(Документ.Метаданные().Реквизиты.Найти("ДатаОплаты") = Неопределено, Документ.Дата, Документ.ДатаОплаты);
	КУДиР = Документ.Движения.КнигаУчетаДоходовИРасходов;
    	
	Если (Документ.Графа4_УСН = 0) И
		(Документ.Графа5_УСН = 0) И
		(Документ.Графа6_УСН = 0) И
		(Документ.Графа7_УСН = 0) И
		(Документ.НДС_УСН = 0) Тогда
		
		Возврат;
	КонецЕсли;
	МетаданныеДокумента = Документ.Метаданные();
	Если (МетаданныеДокумента.Реквизиты.Найти("ОтражатьВНалоговомУчете") <> Неопределено) И НЕ Документ.ОтражатьВНалоговомУчете Тогда
		Возврат;
	КонецЕсли;
	
	СтрокаКниги  = КУДиР.Добавить();

	СтрокаКниги.Организация		= Документ.Организация;
	СтрокаКниги.СтрокаДокумента = 0;
	СтрокаКниги.Период			= Период;
	СтрокаКниги.Графа4			= Документ.Графа4_УСН;

	Если Документ.ДоходыЕНВД_УСН Тогда
        СтрокаКниги.ДоходЕНВД = Документ.Графа4_УСН - Документ.Графа5_УСН;
	Иначе

		СтрокаКниги.Графа5 = Документ.Графа5_УСН;

	КонецЕсли;

	СтрокаКниги.Графа6 = Документ.Графа6_УСН;

	Если Документ.РасходыЕНВД_УСН Тогда

		Расход = Документ.Движения.РасходыПриУСН;
		СтрокаРасхода = Расход.ДобавитьПриход();
		СтрокаРасхода.Период					= Период;
		СтрокаРасхода.Организация 				= Документ.Организация;
		СтрокаРасхода.ОтражениеВУСН 			= Перечисления.ОтражениеВУСН.Распределяются;
		СтрокаРасхода.СтатусыОплатыРасходовУСН 	= Перечисления.СтатусыРасходовУСН.НеРаспределено;
		СтрокаРасхода.РасчетныйДокумент 		= Документ.Ссылка;
		СтрокаРасхода.Сумма 					= Документ.Графа7_УСН;
		СтрокаРасхода.НДС 						= Документ.НДС_УСН;
		СтрокаРасхода.Валюта 					= глЗначениеПеременной("ВалютаРегламентированногоУчета");
		Расход.Записать();
		
	Иначе

		СтрокаКниги.Графа7 = Документ.Графа7_УСН;
		СтрокаКниги.НДС    = Документ.НДС_УСН;

	КонецЕсли;

	СтрокаКниги.Содержание = Документ.Содержание_УСН;
	СтрокаКниги.РеквизитыПервичногоДокумента = РеквизитыПервичногоДокумента(Документ);
	КУДиР.Записать();
КонецПроцедуры

//Собирает таблицу остатков взаиморасчетов по установленным фильтрам
//Таблица фильтров содержит колонки:
// - Имя поля (строка),  
// - Значение поля (произвольный),  
// - Условие (строка)
Функция ВыполнитьЗапросПоВзаиморасчетамУСН(Граница, Организация, ТаблицаФильтров, ДополнитьДоходами = Ложь)
	
	ОбработатьТаблицуФильтров(ТаблицаФильтров);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("Граница",	 Граница);
	Для каждого Фильтр Из ТаблицаФильтров Цикл
		Запрос.УстановитьПараметр(Фильтр.ИмяПоля,	 Фильтр.ЗначениеПоля);
	КонецЦикла;
	Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ВзаиморасчетыУСНОстатки.Организация,
	|	ВзаиморасчетыУСНОстатки.ДоговорКонтрагента,
	|	ВзаиморасчетыУСНОстатки.Работник,
	|	ВзаиморасчетыУСНОстатки.Сделка,
	|	ВзаиморасчетыУСНОстатки.РасчетныйДокумент,
	|	ВзаиморасчетыУСНОстатки.РасчетныйДокумент.Дата КАК РасчетныйДокументДата,
	|	СУММА(ВзаиморасчетыУСНОстатки.СуммаВзаиморасчетовОстаток) КАК СуммаВзаиморасчетов,
	|	СУММА(ВзаиморасчетыУСНОстатки.ДоходЕНВДОстаток) КАК ДоходЕНВД,
	|	СУММА(ВзаиморасчетыУСНОстатки.ДоходКомитентаОстаток) КАК ДоходКомитента,
	|	СУММА(КнигаУчетаДоходовИРасходов.Графа5Оборот) КАК Графа5,
	|	СУММА(КнигаУчетаДоходовИРасходов.Графа4Оборот) КАК Графа4
	|ИЗ
	|	РегистрНакопления.ВзаиморасчетыУСН.Остатки(&Граница, Организация = &Организация";
	
	Для каждого Фильтр Из ТаблицаФильтров Цикл
		Если Найти(Фильтр.Условие, "В") = 0 Тогда
			Текст = Текст + " И " + Фильтр.ИмяПоля + " " + Фильтр.Условие + " &" + Фильтр.ИмяПоля; 
		Иначе
			Текст = Текст + " И " + Фильтр.ИмяПоля + " " + Фильтр.Условие + " (&" + Фильтр.ИмяПоля + ")";
		КонецЕсли;
	КонецЦикла;
	
	Текст = Текст + ") КАК ВзаиморасчетыУСНОстатки
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.КнигаУчетаДоходовИРасходов.Обороты(,,Регистратор,Организация = &Организация) КАК КнигаУчетаДоходовИРасходов
	|		ПО ВзаиморасчетыУСНОстатки.РасчетныйДокумент = КнигаУчетаДоходовИРасходов.Регистратор
	|
	|СГРУППИРОВАТЬ ПО
	|	ВзаиморасчетыУСНОстатки.Организация,
	|	ВзаиморасчетыУСНОстатки.ДоговорКонтрагента,
	|	ВзаиморасчетыУСНОстатки.Работник,
	|	ВзаиморасчетыУСНОстатки.Сделка,
	|	ВзаиморасчетыУСНОстатки.РасчетныйДокумент
	|
	|УПОРЯДОЧИТЬ ПО
	|	РасчетныйДокументДата,
	|	ДоходКомитента УБЫВ,
	|	ДоходЕНВД УБЫВ";
	
	Запрос.Текст = Текст;
	Возврат Запрос.Выполнить().Выгрузить();
	
	
КонецФункции

//Собирает таблицу остатков расходов по установленным фильтрам
//Таблица фильтров содержит колонки:
// - Имя поля (строка),  
// - Значение поля (произвольный),  
// - Условие (строка)  
Функция ВыполнитьЗапросПоРасходам(Граница, Организация, ТаблицаФильтров)
	
	ОбработатьТаблицуФильтров(ТаблицаФильтров);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("Граница",	 Граница);
	Для каждого Фильтр Из ТаблицаФильтров Цикл
		Запрос.УстановитьПараметр(Фильтр.ИмяПоля,	 Фильтр.ЗначениеПоля);
	КонецЦикла;
	Текст = 
	"ВЫБРАТЬ
	|	РасходыУСН.Организация,
	|	РасходыУСН.ВидРасхода,
	|	РасходыУСН.ЭлементРасхода,
	|	РасходыУСН.СчетУчета,
	|	РасходыУСН.Валюта,
	|	РасходыУСН.ДоговорКонтрагента,
	|	РасходыУСН.РасчетныйДокумент,
	|	РасходыУСН.СтатусыПартийУСН,
	|	РасходыУСН.Партия,
	|	РасходыУСН.ОтражениеВУСН,
	|	РасходыУСН.СтатусыОплатыРасходовУСН,
	|	РасходыУСН.СтатусыОплатыРасходовУСН.Порядок КАК СтатусыОплатыРасходовУСНПорядок,
	|	СУММА(РасходыУСН.КоличествоОстаток) КАК Количество,
	|	СУММА(РасходыУСН.СуммаОстаток) КАК Сумма,
	|	СУММА(РасходыУСН.НДСОстаток) КАК НДС,
	|	РасходыУСН.НомерСтрокиДокумента КАК НомерСтрокиДокумента,
	|	РасходыУСН.РеквизитыДокументаОплаты КАК РеквизитыДокументаОплаты,
	|	РасходыУСН.Партия.Дата КАК ПартияДата,
	|	РасходыУСН.РасчетныйДокумент.Дата КАК РасчетныйДокументДата
	|ИЗ
	|	РегистрНакопления.РасходыПриУСН.Остатки(&Граница, Организация = &Организация";
	
	Для каждого Фильтр Из ТаблицаФильтров Цикл
		Если Найти(Фильтр.Условие, "В") = 0 Тогда
			Текст = Текст + " И " + Фильтр.ИмяПоля + " " + Фильтр.Условие + " &" + Фильтр.ИмяПоля; 
		Иначе
			Текст = Текст + " И " + Фильтр.ИмяПоля + " " + Фильтр.Условие + " (&" + Фильтр.ИмяПоля + ")";
		КонецЕсли;
	КонецЦикла;
	
	Текст = Текст + ") КАК РасходыУСН
	|
	|СГРУППИРОВАТЬ ПО
	|	РасходыУСН.Организация,
	|	РасходыУСН.ВидРасхода,
	|	РасходыУСН.ЭлементРасхода,
	|	РасходыУСН.СчетУчета,
	|	РасходыУСН.Валюта,
	|	РасходыУСН.ДоговорКонтрагента,
	|	РасходыУСН.РасчетныйДокумент,
	|	РасходыУСН.СтатусыПартийУСН,
	|	РасходыУСН.Партия,
	|	РасходыУСН.ОтражениеВУСН,
	|	РасходыУСН.СтатусыОплатыРасходовУСН,
	|	РасходыУСН.НомерСтрокиДокумента,
	|	РасходыУСН.РеквизитыДокументаОплаты
	|
	|УПОРЯДОЧИТЬ ПО
	|	СтатусыОплатыРасходовУСНПорядок,
	|	ПартияДата,
	|	РасчетныйДокументДата,
	|	НомерСтрокиДокумента";
	
	Запрос.Текст = Текст;
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// ДВИЖЕНИЯ РЕГИСТРОВ

//Добавить движение по регистру взаиморасчетов УСН
//
Процедура ДвижениеВзаиморасчетовУСН(ТаблицаДвижений, Организация, Договор = Неопределено, РасчетныйДокумент, Сделка, СуммаДокумента, СуммаЕНВД=0, СуммаКомиссии=0, Работник = Неопределено)
	
	Движение = ТаблицаДвижений.Добавить();
	Движение.Организация = Организация;
	Движение.ДоговорКонтрагента = Договор;
	Движение.РасчетныйДокумент 	= РасчетныйДокумент;
	Если ТипЗнч(Договор) = Тип("СправочникСсылка.ДоговорыКонтрагентов") Тогда
		Если НЕ Договор.ВедениеВзаиморасчетов = Перечисления.ВедениеВзаиморасчетовПоДоговорам.ПоДоговоруВЦелом Тогда
			Движение.Сделка = Сделка;
		КонецЕсли;
		Если (Договор.ВидДоговора = Перечисления.ВидыДоговоровКонтрагентов.СКомитентом) Тогда
			Движение.РасчетныйДокумент = Неопределено;
		КонецЕсли;
	ИначеЕсли ТипЗнч(РасчетныйДокумент) = Тип("ДокументСсылка.АвансовыйОтчет") Тогда
		Движение.Сделка = РасчетныйДокумент;
	КонецЕсли;
	Движение.Работник 			= Работник;
	Движение.СуммаВзаиморасчетов = СуммаДокумента;
	Движение.ДоходЕНВД 			= СуммаЕНВД;
	Движение.ДоходКомитента 	= СуммаКомиссии;
	
КонецПроцедуры

Процедура ВыделитьБезналичнуюОплату(СтруктураШапкиДокумента, ТаблицаДвижений, СуммаВзаиморасчетов, СуммаЕНВД, СуммаКомиссии)
	
	Если (НЕ СтруктураШапкиДокумента.Ссылка.Метаданные().ТабличныеЧасти.Найти("ОплатаПлатежнымиКартами") = НЕОПРЕДЕЛЕНО) И
		(НЕ СтруктураШапкиДокумента.Ссылка.Метаданные().ТабличныеЧасти.Найти("ОплатаБанковскимиКредитами") = НЕОПРЕДЕЛЕНО) Тогда
		Запрос = Новый Запрос();
		Запрос.УстановитьПараметр("Ссылка", СтруктураШапкиДокумента.Ссылка);
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ОтчетОРозничныхПродажахОплата.ДоговорВзаиморасчетовБанкаКредитора КАК ДоговорКонтрагента,
		|	СУММА(ОтчетОРозничныхПродажахОплата.Сумма) КАК СуммаОплаты
		|ИЗ
		|	Документ.ОтчетОРозничныхПродажах.ОплатаБанковскимиКредитами КАК ОтчетОРозничныхПродажахОплата
		|ГДЕ
		|	ОтчетОРозничныхПродажахОплата.Ссылка = &Ссылка
		|
		|СГРУППИРОВАТЬ ПО
		|	ОтчетОРозничныхПродажахОплата.ДоговорВзаиморасчетовБанкаКредитора
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ОтчетОРозничныхПродажахОплатаПлатежнымиКартами.Ссылка.ДоговорВзаиморасчетовЭквайрера,
		|	СУММА(ОтчетОРозничныхПродажахОплатаПлатежнымиКартами.Сумма)
		|ИЗ
		|	Документ.ОтчетОРозничныхПродажах.ОплатаПлатежнымиКартами КАК ОтчетОРозничныхПродажахОплатаПлатежнымиКартами
		|ГДЕ
		|	ОтчетОРозничныхПродажахОплатаПлатежнымиКартами.Ссылка = &Ссылка
		|
		|СГРУППИРОВАТЬ ПО
		|	ОтчетОРозничныхПродажахОплатаПлатежнымиКартами.Ссылка.ДоговорВзаиморасчетовЭквайрера";
		
		Результат = Запрос.Выполнить().Выбрать();
		
		Если ТаблицаДвижений.Колонки.Количество() = 0 Тогда
			ТаблицаДвижений = РегистрыНакопления.ВзаиморасчетыУСН.СоздатьНаборЗаписей().ВыгрузитьКолонки();
		КонецЕсли;
		Если ТаблицаДвижений.Колонки.Найти("ВидДвижения") = Неопределено Тогда
			ТаблицаДвижений.Колонки.Добавить("ВидДвижения");
		КонецЕсли;
		
		Пока Результат.Следующий() Цикл
			
			КоэффБН = ?(СуммаВзаиморасчетов = 0, 0, Результат.СуммаОплаты / СуммаВзаиморасчетов);
			СуммаВзаиморасчетов = СуммаВзаиморасчетов - Результат.СуммаОплаты;
			
			СуммаЕНВД_БН = Окр(КоэффБН*СуммаЕНВД, 2, 1);
			СуммаЕНВД = СуммаЕНВД - СуммаЕНВД_БН;
			
			СуммаКомиссии_БН = Окр(КоэффБН*СуммаКомиссии, 2, 1);
			СуммаКомиссии = СуммаКомиссии - СуммаКомиссии_БН;
					
			ДвижениеВзаиморасчетовУСН(ТаблицаДвижений, СтруктураШапкиДокумента.Организация, Результат.ДоговорКонтрагента, СтруктураШапкиДокумента.Ссылка, СтруктураШапкиДокумента.Ссылка, Результат.СуммаОплаты, СуммаЕНВД_БН, СуммаКомиссии_БН);
			ТаблицаДвижений[ТаблицаДвижений.Количество()-1].ВидДвижения = ВидДвиженияНакопления.Приход;
		КонецЦикла;
		
	КонецЕсли;
		
КонецПроцедуры

//Отражает поступление расходов по регистру расходов УСН
//
Процедура ПоступлениеРасходовУСН(СтруктураШапкиДокумента, ТаблицаРасходов, ТаблицаДвижений, ВидРасхода, Договор = Неопределено, СтатусПартии, СуммаВключаетНДС = Истина, ВозвратБезУказанияПартии = Ложь, СторноРасхода = 0)  Экспорт //
	
	НС = 0;
	мВалютаРегламентированногоУчета = глЗначениеПеременной("ВалютаРегламентированногоУчета");
	УчетнаяПолитика = ОбщегоНазначения.ПолучитьПараметрыУчетнойПолитикиРегл(СтруктураШапкиДокумента.Дата, СтруктураШапкиДокумента.Организация);
	СтруктураШапкиДокумента.Вставить("ПорядокПризнанияРасходовПоНДС",УчетнаяПолитика.ПорядокПризнанияРасходовПоНДС);
	СтруктураШапкиДокумента.Вставить("ПорядокПризнанияРасходовПоТоварам",УчетнаяПолитика.ПорядокПризнанияРасходовПоТоварам);
	СтруктураШапкиДокумента.Вставить("ПорядокПризнанияМатериальныхРасходов",УчетнаяПолитика.ПорядокПризнанияМатериальныхРасходов);
	ВыделятьНДСУСН = (СтруктураШапкиДокумента.ПорядокПризнанияРасходовПоНДС = Перечисления.ПорядокПризнанияРасходовПоНДС.ПоОплатеПоставщику);
	Валюта = мВалютаРегламентированногоУчета;
	ВалютаДокумента = мВалютаРегламентированногоУчета;
	КурсВзаиморасчетов = 1;
	КратностьВзаиморасчетов = 1;
	КурсДокумента = 1;
	КратностьДокумента = 1;
	УчетАгентскогоНДС = Ложь;
	
	Если ЗначениеЗаполнено(Договор) И (ТипЗнч(Договор) = Тип("СправочникСсылка.ДоговорыКонтрагентов")) Тогда
		УчетАгентскогоНДС = Договор.УчетАгентскогоНДС;
	КонецЕсли;
	
	СтруктураШапкиДокумента.Свойство("ВалютаДокумента", ВалютаДокумента);
	СтруктураШапкиДокумента.Свойство("КурсВзаиморасчетов", КурсВзаиморасчетов);
	СтруктураШапкиДокумента.Свойство("КратностьВзаиморасчетов", КратностьВзаиморасчетов);
	
	СтруктураШапкиДокумента.Свойство("ВалютаВзаиморасчетов", Валюта);
	СтруктураШапкиДокумента.Свойство("КурсДокумента", КурсДокумента);
	СтруктураШапкиДокумента.Свойство("КратностьДокумента", КратностьДокумента);
	
	ВалютаДокумента 		= ?(ВалютаДокумента = Неопределено, мВалютаРегламентированногоУчета, ВалютаДокумента);
	КурсВзаиморасчетов 		= ?(КурсВзаиморасчетов = Неопределено, 1, КурсВзаиморасчетов);
	КратностьВзаиморасчетов = ?(КратностьВзаиморасчетов = Неопределено, 1, КратностьВзаиморасчетов);
	Валюта 					= ?(Валюта = Неопределено, ВалютаДокумента, Валюта);
	КурсДокумента 			= ?(КурсДокумента = Неопределено, 1, КурсДокумента);
	КратностьДокумента 		= ?(КратностьДокумента = Неопределено, 1, КратностьДокумента);
	
	Если ВидРасхода = Перечисления.ВидыРасходовУСН.Номенклатура Тогда
		ТоварыПоОплате = (СтруктураШапкиДокумента.ПорядокПризнанияРасходовПоТоварам = Перечисления.ПорядокПризнанияРасходовПоТоварам.ПоОплатеПоставщику);
		РасходыПоОтгрузке = (СтруктураШапкиДокумента.ПорядокПризнанияРасходовПоТоварам = Перечисления.ПорядокПризнанияРасходовПоТоварам.ПоФактуРеализации);
		МатериалыПоОплате = (СтруктураШапкиДокумента.ПорядокПризнанияМатериальныхРасходов = Перечисления.ПорядокПризнанияМатериальныхРасходов.ПоОплатеПоставщику);
	КонецЕсли;
	
	Для Каждого Расход из ТаблицаРасходов Цикл
		НС = НС+1;
		
		//Если поступление на комиссию - не отражаем в расходах
		Если ВидРасхода = Перечисления.ВидыРасходовУСН.Номенклатура Тогда
			Если НЕ ТаблицаРасходов.Колонки.Найти("СчетУчетаБУ") = Неопределено Тогда
				Если Расход.СчетУчетаБУ.Забалансовый Тогда
					Продолжить;
				КонецЕсли;
			Конецесли;
		ИначеЕсли ВидРасхода = Перечисления.ВидыРасходовУСН.ДопРасходы Тогда
			//Не отражаем непринимаемые расходы
			Если НЕ ТаблицаРасходов.Колонки.Найти("ОтражениеВУСН") = Неопределено Тогда 
				Если Расход.ОтражениеВУСН = Перечисления.ОтражениеВУСН.НеПринимаются Тогда
					Продолжить;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
		СтрокаДвижений = ТаблицаДвижений.Добавить();
		СтрокаДвижений.Организация			= СтруктураШапкиДокумента.Организация;
		СтрокаДвижений.Валюта 				= Валюта;
		СтрокаДвижений.ДоговорКонтрагента 	= Договор;
		СтрокаДвижений.СтатусыПартийУСН		= СтатусПартии;
		СтрокаДвижений.РасчетныйДокумент 	= СтруктураШапкиДокумента.Ссылка;
		СтрокаДвижений.ВидРасхода 			= ВидРасхода;
		СтрокаДвижений.НомерСтрокиДокумента = НС;
		СтрокаДвижений.ЭтапПроведения 		= -1; //~
		
		Если ВидРасхода = Перечисления.ВидыРасходовУСН.Услуги Тогда
			
			Если Не ТаблицаРасходов.Колонки.Найти("Номенклатура") = Неопределено Тогда
				СтрокаДвижений.ЭлементРасхода 	= Расход.Номенклатура;
			Конецесли;
			
			Если НЕ ТаблицаРасходов.Колонки.Найти("СчетЗатрат") = Неопределено Тогда
				//Из услуг выделим РБП
				Если БухгалтерскийУчет.ЭтоСубсчет(Расход.СчетЗатрат, ПланыСчетов.Хозрасчетный.РасходыБудущихПериодов) Тогда
					СтрокаДвижений.ВидРасхода = Перечисления.ВидыРасходовУСН.РБП;
					Если НЕ ТаблицаРасходов.Колонки.Найти("Субконто1") = Неопределено Тогда
						СтрокаДвижений.ЭлементРасхода = Расход.Субконто1;
					КонецЕсли;
				Иначе
					СтрокаДвижений.СчетУчета = Расход.СчетЗатрат;
				КонецЕсли;
			КонецЕсли;
			
		ИначеЕсли ВидРасхода = Перечисления.ВидыРасходовУСН.Номенклатура ИЛИ 
			(ВидРасхода = Перечисления.ВидыРасходовУСН.ДопРасходы) Тогда
			
			Если Не ТаблицаРасходов.Колонки.Найти("Номенклатура") = Неопределено Тогда
				СтрокаДвижений.ЭлементРасхода 	= Расход.Номенклатура;
			Конецесли;
			Если НЕ ТаблицаРасходов.Колонки.Найти("Количество") = Неопределено Тогда
				СтрокаДвижений.Количество 		= Расход.Количество;
			КонецЕсли;
			Если НЕ ТаблицаРасходов.Колонки.Найти("КоличествоДок") = Неопределено Тогда
				СтрокаДвижений.Количество 		= Расход.КоличествоДок;
			КонецЕсли;
			
			//Для доп. расходов пропишем партию, указанную в документе
			Если НЕ ТаблицаРасходов.Колонки.Найти("ДокументОприходования") = Неопределено Тогда
				Если НЕ ЗначениеЗаполнено(Расход.ДокументОприходования) Тогда
					СтрокаДвижений.Партия 		= СтруктураШапкиДокумента.Ссылка;
				Иначе
					СтрокаДвижений.Партия 		= Расход.ДокументОприходования;
				КонецЕсли;
			Иначе
				СтрокаДвижений.Партия 			= СтруктураШапкиДокумента.Ссылка;
			КонецЕсли;
			
		ИначеЕсли ВидРасхода = Перечисления.ВидыРасходовУСН.НМА Тогда
			Если НЕ ТаблицаРасходов.Колонки.Найти("НематериальныйАктив") = Неопределено Тогда
				СтрокаДвижений.ЭлементРасхода = Расход.НематериальныйАктив;
			Конецесли;
			
		КонецЕсли;
		
		Если ВозвратБезУказанияПартии Тогда
			СтрокаДвижений.СтатусыОплатыРасходовУСН = Перечисления.СтатусыРасходовУСН.НеСписано;
		ИначеЕсли (СтрокаДвижений.ВидРасхода = Перечисления.ВидыРасходовУСН.Номенклатура)
			ИЛИ (СтрокаДвижений.ВидРасхода = Перечисления.ВидыРасходовУСН.ДопРасходы)
			ИЛИ (СтрокаДвижений.ВидРасхода = Перечисления.ВидыРасходовУСН.РБП) Тогда
			СтрокаДвижений.СтатусыОплатыРасходовУСН = Перечисления.СтатусыРасходовУСН.НеСписаноНеОплачено;
		Иначе
			СтрокаДвижений.СтатусыОплатыРасходовУСН = Перечисления.СтатусыРасходовУСН.НеОплачено;
		КонецЕсли;
		
		Если (НЕ ТаблицаРасходов.Колонки.Найти("СчетУчетаБУ") = Неопределено) Тогда
			СтрокаДвижений.СчетУчета 		= Расход.СчетУчетаБУ;
		ИначеЕсли НЕ ТаблицаРасходов.Колонки.Найти("Счет") = Неопределено Тогда
			СтрокаДвижений.СчетУчета 		= Расход.Счет;
		КонецЕсли;
		
		Сумма = 0;
		СуммаНДС = 0;
		Если НЕ ТаблицаРасходов.Колонки.Найти("НДСВал") = Неопределено Тогда
			Если Валюта = мВалютаРегламентированногоУчета Тогда
				СуммаНДС = Расход.НДС;
			Иначе
				СуммаНДС = Расход.НДСВал;
			КонецЕсли;
		ИначеЕсли НЕ ТаблицаРасходов.Колонки.Найти("ПроводкаСуммаНДС") = Неопределено Тогда
			СуммаНДС = МодульВалютногоУчета.ПересчитатьИзВалютыВВалюту(Расход.ПроводкаСуммаНДС, мВалютаРегламентированногоУчета, Валюта,
										   1, КурсВзаиморасчетов,
										   1, КратностьВзаиморасчетов);
		ИначеЕсли НЕ ТаблицаРасходов.Колонки.Найти("СуммаНДС") = Неопределено Тогда
			СуммаНДС = МодульВалютногоУчета.ПересчитатьИзВалютыВВалюту(Расход.СуммаНДС, ВалютаДокумента, Валюта,
										   КурсДокумента, КурсВзаиморасчетов,
										   КратностьДокумента, КратностьВзаиморасчетов);
		КонецЕсли;
		Если НЕ ТаблицаРасходов.Колонки.Найти("СуммаБезНДСВал") = Неопределено Тогда
			Если Валюта = мВалютаРегламентированногоУчета Тогда
				СтрокаДвижений.Сумма = Расход.СуммаБезНДС + Расход.НДС;
			Иначе
				СтрокаДвижений.Сумма = Расход.СуммаБезНДСВал + Расход.НДСВал;
			КонецЕсли;
			
		ИначеЕсли НЕ ТаблицаРасходов.Колонки.Найти("СуммаВал") = Неопределено Тогда
			Если Валюта = мВалютаРегламентированногоУчета Тогда
				СтрокаДвижений.Сумма = Расход.Сумма;
			Иначе
				СтрокаДвижений.Сумма = Расход.СуммаВал;
			КонецЕсли;
			
		ИначеЕсли НЕ ТаблицаРасходов.Колонки.Найти("Сумма") = Неопределено Тогда
			СтрокаДвижений.Сумма = Расход.Сумма;
			
		КонецЕсли;
		СтрокаДвижений.НДС 					= СуммаНДС;
		
		Если УчетАгентскогоНДС Тогда
			СтрокаДвиженийНДС = ТаблицаДвижений.Добавить();
			СтрокаДвиженийНДС.Организация			= СтруктураШапкиДокумента.Организация;
			СтрокаДвиженийНДС.Валюта 				= мВалютаРегламентированногоУчета;
			СтрокаДвиженийНДС.РасчетныйДокумент 	= СтруктураШапкиДокумента.Ссылка;
			СтрокаДвиженийНДС.ВидРасхода 			= Перечисления.ВидыРасходовУСН.Налоги;
			СтрокаДвиженийНДС.СтатусыОплатыРасходовУСН = Перечисления.СтатусыРасходовУСН.НеОплачено;
			СтрокаДвиженийНДС.СчетУчета 			= ПланыСчетов.Хозрасчетный.НДС;
			СтрокаДвиженийНДС.НомерСтрокиДокумента 	= НС;
			СтрокаДвиженийНДС.ОтражениеВУСН 		= Перечисления.ОтражениеВУСН.Принимаются;
			Если Валюта <>  мВалютаРегламентированногоУчета Тогда
				СтрокаДвиженийНДС.Сумма	= МодульВалютногоУчета.ПересчитатьИзВалютыВВалюту(СтрокаДвижений.НДС, Валюта, мВалютаРегламентированногоУчета,
														   			КурсВзаиморасчетов, 1,
										   			  				КратностьВзаиморасчетов, 1);
			Иначе
                СтрокаДвиженийНДС.Сумма	= СтрокаДвижений.НДС;
			КонецЕсли;
													  
			СтрокаДвижений.Сумма = СтрокаДвижений.Сумма - СтрокаДвижений.НДС;
			СтрокаДвижений.НДС = 0;
		КонецЕсли;
		
		Если ВозвратБезУказанияПартии Тогда
			Если НЕ ТаблицаРасходов.Колонки.Найти("Себестоимость") = Неопределено Тогда
				СтрокаДвижений.Сумма = Расход.Себестоимость*СтрокаДвижений.Количество;
				СтрокаДвижений.НДС = 0;
			Иначе
				СтрокаДвижений.Сумма = МодульВалютногоУчета.ПересчитатьИзВалютыВВалюту(СтрокаДвижений.Сумма, Валюта, мВалютаРегламентированногоУчета,
										   							КурсВзаиморасчетов, 1,
										   							КратностьВзаиморасчетов, 1);
				СтрокаДвижений.НДС = 0;
			КонецЕсли;
		КонецЕсли;
		
		Если ВыделятьНДСУСН И
			(ВидРасхода <> Перечисления.ВидыРасходовУСН.ОС) И
			(ВидРасхода <> Перечисления.ВидыРасходовУСН.НМА) Тогда
			СтрокаДвижений.Сумма = СтрокаДвижений.Сумма - СтрокаДвижений.НДС;
		КонецЕсли;			
		
		Если НЕ ТаблицаРасходов.Колонки.Найти("ОтражениеВУСН") = Неопределено Тогда
			СтрокаДвижений.ОтражениеВУСН 		= Расход.ОтражениеВУСН;
			Если ВозвратБезУказанияПартии И (СтрокаДвижений.ОтражениеВУСН = Перечисления.ОтражениеВУСН.Принимаются) Тогда
				Если НЕ ТаблицаРасходов.Колонки.Найти("СчетУчетаБУ") = Неопределено Тогда
					ЭтоТовар = ((Расход.СчетУчетаБУ.ПринадлежитЭлементу(ПланыСчетов.Хозрасчетный.ТоварыОтгруженные)) ИЛИ 
								(Расход.СчетУчетаБУ.ПринадлежитЭлементу(ПланыСчетов.Хозрасчетный.Товары)));
					ЭтоМатериал = Расход.СчетУчетаБУ.ПринадлежитЭлементу(ПланыСчетов.Хозрасчетный.Материалы);
					РасходыПоОплате = ((ТоварыПоОплате И ЭтоТовар) ИЛИ (МатериалыПоОплате И ЭтоМатериал));
					Если НЕ РасходыПоОплате Тогда
						СторноРасхода = СторноРасхода + СтрокаДвижений.Сумма;
					Иначе
						СтрокаДвижений.СтатусыОплатыРасходовУСН = Перечисления.СтатусыРасходовУСН.НеСписаноПринято;
					КонецЕсли;
				ИначеЕсли НЕ ТаблицаРасходов.Колонки.Найти("Счет") = Неопределено Тогда
					ЭтоТовар = ((Расход.Счет.ПринадлежитЭлементу(ПланыСчетов.Хозрасчетный.ТоварыОтгруженные)) ИЛИ 
								(Расход.Счет.ПринадлежитЭлементу(ПланыСчетов.Хозрасчетный.Товары)));
					ЭтоМатериал = Расход.Счет.ПринадлежитЭлементу(ПланыСчетов.Хозрасчетный.Материалы);
					РасходыПоОплате = ((ТоварыПоОплате И ЭтоТовар) ИЛИ (МатериалыПоОплате И ЭтоМатериал));
					Если НЕ РасходыПоОплате Тогда
						СторноРасхода = СторноРасхода + СтрокаДвижений.Сумма;
					Иначе
						СтрокаДвижений.СтатусыОплатыРасходовУСН = Перечисления.СтатусыРасходовУСН.НеСписаноПринято;
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
		Иначе
			СтрокаДвижений.ОтражениеВУСН 		= Перечисления.ОтражениеВУСН.НеПринимаются;
		Конецесли;
				
	КонецЦикла;
	
КонецПроцедуры

//Отражает поступление расходов по регистру расходов УСН
//
Процедура ПоступлениеПоПартиямУСН(СтруктураШапкиДокумента, ТаблицаРасходов, ТаблицаДвижений, ВидРасхода)  Экспорт
	
	Склад = Неопределено;
	СтруктураШапкиДокумента.Свойство("Склад", Склад);
	
	Если НЕ СтруктураШапкиДокумента.ОтражатьВНалоговомУчетеУСН Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого Расход из ТаблицаРасходов Цикл
		
		СтрокаДвижений = ТаблицаДвижений.Добавить();
		СтрокаДвижений.Организация = СтруктураШапкиДокумента.Организация;
		
		Если ВидРасхода = Перечисления.ВидыРасходовУСН.Номенклатура Тогда
			Если Не ТаблицаРасходов.Колонки.Найти("Номенклатура") = Неопределено Тогда
				СтрокаДвижений.Номенклатура = Расход.Номенклатура;
			Конецесли;
			Если Не ТаблицаРасходов.Колонки.Найти("ХарактеристикаНоменклатуры") = Неопределено Тогда
				СтрокаДвижений.ХарактеристикаНоменклатуры = Расход.ХарактеристикаНоменклатуры;
			Конецесли;
			Если Не ТаблицаРасходов.Колонки.Найти("СерияНоменклатуры") = Неопределено Тогда
				СтрокаДвижений.СерияНоменклатуры = Расход.СерияНоменклатуры;
			Конецесли;
			Если НЕ ТаблицаРасходов.Колонки.Найти("СчетУчетаБУ") = Неопределено Тогда
				СтрокаДвижений.СчетУчета = Расход.СчетУчетаБУ;
			ИначеЕсли НЕ ТаблицаРасходов.Колонки.Найти("Счет") = Неопределено Тогда
				СтрокаДвижений.СчетУчета = Расход.Счет;
			Конецесли;
			Если НЕ ТаблицаРасходов.Колонки.Найти("Количество") = Неопределено Тогда
				СтрокаДвижений.Количество = Расход.Количество;
			КонецЕсли;
			Если НЕ ТаблицаРасходов.Колонки.Найти("КоличествоДок") = Неопределено Тогда
				СтрокаДвижений.Количество = Расход.КоличествоДок;
			КонецЕсли;
			Если НЕ ТаблицаРасходов.Колонки.Найти("Склад") = Неопределено Тогда
				СтрокаДвижений.Склад = Расход.Склад;
			КонецЕсли;
			
			//Для доп. расходов пропишем партию, указанную в документе
			Если НЕ ТаблицаРасходов.Колонки.Найти("ДокументПартии") = Неопределено Тогда
				Если НЕ ЗначениеЗаполнено(Расход.ДокументПартии) Тогда
					СтрокаДвижений.ДокументОприходования = СтруктураШапкиДокумента.Ссылка;
				Иначе
					СтрокаДвижений.ДокументОприходования = Расход.ДокументПартии;
				КонецЕсли;
				
				Если НЕ ЗначениеЗаполнено(СтрокаДвижений.Склад) Тогда
					МетаданныеДокумента = Расход.ДокументПартии.Метаданные();
					Склад = ?(МетаданныеДокумента.Реквизиты.Найти("Склад") <> Неопределено, Расход.ДокументПартии.Склад, Неопределено);
				КонецЕсли;
			Иначе
				СтрокаДвижений.ДокументОприходования = СтруктураШапкиДокумента.Ссылка;
			КонецЕсли;
			
			Если НЕ ЗначениеЗаполнено(СтрокаДвижений.Склад) Тогда
				СтрокаДвижений.Склад = Склад;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

//Отражает событие "списание" по регистру расходов при УСН
//ТаблицаПартий - таблица движений регистра партионного учета НУ
Процедура СписаниеРасходовУСН(СтруктураШапкиДокумента, ТаблицаПартий, ДвиженияРегистров, ВидРасхода, Договор, СтатусПартии, ВидОперации = "Списание") //
	
	//Подготовка наборов фильтров
	МассивНоменклатуры = ТаблицаПартий.ВыгрузитьКолонку("Номенклатура");
	ОбщегоНазначения.УдалитьПовторяющиесяЭлементы(МассивНоменклатуры);
	
	МассивСчетов = ТаблицаПартий.ВыгрузитьКолонку("СчетУчета");
	ОбщегоНазначения.УдалитьПовторяющиесяЭлементы(МассивСчетов);
	
	МассивПартий = ТаблицаПартий.ВыгрузитьКолонку("ДокументОприходования");
	ОбщегоНазначения.УдалитьПовторяющиесяЭлементы(МассивПартий);
	
	Статусы = Новый Массив;
	Статусы.Добавить(Перечисления.СтатусыРасходовУСН.НеСписаноНеОплачено);
	Статусы.Добавить(Перечисления.СтатусыРасходовУСН.НеСписано);
	Статусы.Добавить(Перечисления.СтатусыРасходовУСН.НеСписаноПринято);
	Статусы.Добавить(Перечисления.СтатусыРасходовУСН.НеСписаноНеРаспределено);
	
	//Формирование таблицы фильтров
	ТаблицаФильтров = Новый ТаблицаЗначений;
	ТаблицаФильтров.Колонки.Добавить("ИмяПоля");
	ТаблицаФильтров.Колонки.Добавить("ЗначениеПоля");
	ТаблицаФильтров.Колонки.Добавить("Условие");
		
	Фильтр = ТаблицаФильтров.Добавить();
	Фильтр.ИмяПоля			= "ВидРасхода";
	Фильтр.ЗначениеПоля	= ВидРасхода;
	Фильтр.Условие			= "=";
	
	Фильтр = ТаблицаФильтров.Добавить();
	Фильтр.ИмяПоля			= "СтатусыПартийУСН";
	Фильтр.ЗначениеПоля	= СтатусПартии;
	Фильтр.Условие			= "=";
	
	Фильтр = ТаблицаФильтров.Добавить();
	Фильтр.ИмяПоля			= "СчетУчета";
	Фильтр.ЗначениеПоля	= МассивСчетов;
	Фильтр.Условие			= "В";
	
	Фильтр = ТаблицаФильтров.Добавить();
	Фильтр.ИмяПоля			= "ЭлементРасхода";
	Фильтр.ЗначениеПоля	= МассивНоменклатуры;
	Фильтр.Условие			= "В";
	
	Фильтр = ТаблицаФильтров.Добавить();
	Фильтр.ИмяПоля			= "Партия";
	Фильтр.ЗначениеПоля	= МассивПартий;
	Фильтр.Условие			= "В";
	
	Фильтр = ТаблицаФильтров.Добавить();
	Фильтр.ИмяПоля			= "СтатусыОплатыРасходовУСН";
	Фильтр.ЗначениеПоля	= Статусы;
	Фильтр.Условие			= "В";
	
	//Из таблицы расходов выбираем соответствующие партии и элементы расходов
	ТаблицаРасходов = ВыполнитьЗапросПоРасходам(Новый Граница(СтруктураШапкиДокумента.Ссылка.МоментВремени(), ВидГраницы.Включая), СтруктураШапкиДокумента.Организация, ТаблицаФильтров);
	
	ТаблицаДляСписания = ТаблицаРасходов.СкопироватьКолонки();
	ТаблицаДляСписания.Колонки.Добавить("СтатусСписания");
	ТаблицаДляСписания.Колонки.Добавить("КоличествоПоСтатусуСписания");
	
	//Заполняем таблицу списываемых расходов по таблице расходов и фильтрам партионного учета
	Для Каждого СтрокаПартии Из ТаблицаПартий Цикл
		СтруктураОтбора = Новый Структура;
		СтруктураОтбора.Вставить("ЭлементРасхода",	СтрокаПартии.Номенклатура);
		СтруктураОтбора.Вставить("Партия",			СтрокаПартии.ДокументОприходования);
		
		НайденныеСтроки = ТаблицаРасходов.НайтиСтроки(СтруктураОтбора);
		
		// Подлежащее погашению при списании количество
		КоличествоОсталосьПогасить = СтрокаПартии.Количество;
		Если НЕ ТаблицаПартий.Колонки.Найти("СтатусСписания") = Неопределено Тогда
			КоличествоОсталосьПоСтСписания = СтрокаПартии.КоличествоПоСтатусуСписания;
		Иначе
			КоличествоОсталосьПоСтСписания = 0;
		КонецЕсли;
		Для Каждого Строка Из НайденныеСтроки Цикл
			Если КоличествоОсталосьПогасить <= 0 Тогда
				Прервать;
			КонецЕсли;
			Если Строка.Количество <= 0 Тогда
				Продолжить;
			КонецЕсли;
			
			КоличествоСписания = Мин(Строка.Количество, КоличествоОсталосьПогасить);
			КоличествоОсталосьПогасить = КоличествоОсталосьПогасить - КоличествоСписания;
						
			НоваяСтрока = ТаблицаДляСписания.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);
			
			Если НЕ ТаблицаПартий.Колонки.Найти("СтатусСписания") = Неопределено Тогда
				НовыйСтатус = ПолучитьКомбнациюСтатусовОтражениеВНУ(Строка.ОтражениеВУСН, СтрокаПартии.СтатусСписания);
				НоваяСтрока.СтатусСписания = НовыйСтатус;
				НоваяСтрока.КоличествоПоСтатусуСписания = НоваяСтрока.Количество;
				Если НЕ ЗначениеЗаполнено(СтрокаПартии.СтатусСписания) Тогда
					
				ИначеЕсли Строка.ОтражениеВУСН = НовыйСтатус Тогда
					КоличествоСписанияПоСтатусу = Мин(Строка.Количество, КоличествоОсталосьПоСтСписания);
					КоличествоОсталосьПоСтСписания = КоличествоОсталосьПоСтСписания - КоличествоСписанияПоСтатусу;
					
				ИначеЕсли КоличествоОсталосьПогасить < КоличествоОсталосьПоСтСписания Тогда
					КоличествоСписанияПоСтатусу = Мин(Строка.Количество, КоличествоОсталосьПоСтСписания - КоличествоОсталосьПогасить);
					КоличествоОсталосьПоСтСписания = КоличествоОсталосьПоСтСписания - КоличествоСписанияПоСтатусу;
										
					НоваяСтрока.КоличествоПоСтатусуСписания = КоличествоСписанияПоСтатусу;
				КонецЕсли;
			КонецЕсли;
			
			Если Строка.Количество > КоличествоСписания Тогда
				К = КоличествоСписания / Строка.Количество;
				НоваяСтрока.Количество 	= КоличествоСписания;
				НоваяСтрока.Сумма	 	= К*НоваяСтрока.Сумма;
				НоваяСтрока.НДС 		= К*НоваяСтрока.НДС;
			КонецЕсли;
			
			Строка.Количество 	= Строка.Количество - НоваяСтрока.Количество;
			Строка.Сумма 		= Строка.Сумма - НоваяСтрока.Сумма;
			Строка.НДС 			= Строка.НДС - НоваяСтрока.НДС;
			
		КонецЦикла;
	КонецЦикла;
	
	//Разложим таблицу списываемых расходов на движения корректировки статуса и принятые расходы
	ТаблицыДвижений = ИзменениеСтатусовРасходов(СтруктураШапкиДокумента, ТаблицаДляСписания, , ВидОперации, Ложь);
	
	Если ВидОперации = "НаРеализацию" Тогда
		ТаблицыДвижений.ТаблицаПриход.ЗаполнитьЗначения(Перечисления.СтатусыПартийУСН.НаРеализации,"СтатусыПартийУСН");
	ИначеЕсли ВидОперации = "ВПереработку" Тогда
		ТаблицыДвижений.ТаблицаПриход.ЗаполнитьЗначения(Перечисления.СтатусыПартийУСН.ВПереработке,"СтатусыПартийУСН");
	ИначеЕсли (ВидОперации = "ВозвратСРеализации") ИЛИ (ВидОперации = "ВозвратИзПереработки") Тогда
		ТаблицыДвижений.ТаблицаПриход.ЗаполнитьЗначения(Перечисления.СтатусыПартийУСН.Купленные,"СтатусыПартийУСН");
	//~//
	ИначеЕсли (ВидОперации = "ВПроизводство") Тогда
		ТаблицыДвижений.ТаблицаПриход.ЗаполнитьЗначения(Перечисления.СтатусыПартийУСН.ВПроизводстве,"СтатусыПартийУСН");
	//~\\
	ИначеЕсли ВидОперации = "ВозвратПоставщику" Тогда
		ТаблицыДвижений.ТаблицаПриход.Очистить();
	Иначе
		ТаблицыДвижений.ТаблицаПриход.ЗаполнитьЗначения(Перечисления.СтатусыПартийУСН.Списанные,"СтатусыПартийУСН");
		ТаблицыДвижений.ТаблицаПриход.ЗаполнитьЗначения(СтруктураШапкиДокумента.Ссылка,"Партия");
	КонецЕсли;
		 
	ДвиженияРегистров = СобратьТаблицыДвижений(ТаблицыДвижений,,,);
	ДвиженияРегистров.Вставить("ТаблицаПриход", ТаблицыДвижений.ТаблицаПриход);
										
КонецПроцедуры

//Отражает изменения по регистру расходов при УСН не влияющие на статус расхода
//Такие как изменение счета учета или статуса партии.
//
//ТаблицаПартийПриход - таблица движений "Приход" регистра партионного учета НУ
//ТаблицаПартийРасход - таблица движений "Расход" регистра партионного учета НУ
Процедура ПеремещениеРасходовУСН(СтруктураШапкиДокумента, ТаблицаПартийПриход, ТаблицаПартийРасход, ДвиженияРегистров, ВидРасхода, Договор, СтатусПартии, ВидОперации = "Списание")
	
	Если НЕ ТаблицаПартийПриход.Количество() = ТаблицаПартийРасход.Количество() Тогда
		Возврат;
	КонецЕсли;
	
	//Подготовка наборов фильтров
	МассивНоменклатуры = ТаблицаПартийРасход.ВыгрузитьКолонку("Номенклатура");
	ОбщегоНазначения.УдалитьПовторяющиесяЭлементы(МассивНоменклатуры);
	
	МассивСчетов = ТаблицаПартийРасход.ВыгрузитьКолонку("СчетУчета");
	ОбщегоНазначения.УдалитьПовторяющиесяЭлементы(МассивСчетов);
	
	МассивПартий = ТаблицаПартийРасход.ВыгрузитьКолонку("ДокументОприходования");
	ОбщегоНазначения.УдалитьПовторяющиесяЭлементы(МассивПартий);
	
	Статусы = Новый Массив;
	Статусы.Добавить(Перечисления.СтатусыРасходовУСН.НеСписаноНеОплачено);
	Статусы.Добавить(Перечисления.СтатусыРасходовУСН.НеСписано);
	Статусы.Добавить(Перечисления.СтатусыРасходовУСН.НеСписаноПринято);
	Статусы.Добавить(Перечисления.СтатусыРасходовУСН.НеСписаноНеРаспределено);
	
	//Формирование таблицы фильтров
	ТаблицаФильтров = Новый ТаблицаЗначений;
	ТаблицаФильтров.Колонки.Добавить("ИмяПоля");
	ТаблицаФильтров.Колонки.Добавить("ЗначениеПоля");
	ТаблицаФильтров.Колонки.Добавить("Условие");
		
	Фильтр = ТаблицаФильтров.Добавить();
	Фильтр.ИмяПоля			= "ВидРасхода";
	Фильтр.ЗначениеПоля	= ВидРасхода;
	Фильтр.Условие			= "=";
	
	Фильтр = ТаблицаФильтров.Добавить();
	Фильтр.ИмяПоля			= "СтатусыПартийУСН";
	Фильтр.ЗначениеПоля	= СтатусПартии;
	Фильтр.Условие			= "=";
	
	Фильтр = ТаблицаФильтров.Добавить();
	Фильтр.ИмяПоля			= "СчетУчета";
	Фильтр.ЗначениеПоля	= МассивСчетов;
	Фильтр.Условие			= "В";
	
	Фильтр = ТаблицаФильтров.Добавить();
	Фильтр.ИмяПоля			= "ЭлементРасхода";
	Фильтр.ЗначениеПоля	= МассивНоменклатуры;
	Фильтр.Условие			= "В";
	
	Фильтр = ТаблицаФильтров.Добавить();
	Фильтр.ИмяПоля			= "Партия";
	Фильтр.ЗначениеПоля	= МассивПартий;
	Фильтр.Условие			= "В";
	
	Фильтр = ТаблицаФильтров.Добавить();
	Фильтр.ИмяПоля			= "СтатусыОплатыРасходовУСН";
	Фильтр.ЗначениеПоля	= Статусы;
	Фильтр.Условие			= "В";
	
	//Из таблицы расходов выбираем соответствующие партии и элементы расходов
	ТаблицаРасходов = ВыполнитьЗапросПоРасходам(Новый Граница(СтруктураШапкиДокумента.Ссылка.МоментВремени(), ВидГраницы.Включая), СтруктураШапкиДокумента.Организация, ТаблицаФильтров);
	ТаблицаРасходов.Индексы.Добавить("ЭлементРасхода,Партия, СчетУчета");
	
	ТаблицаДляСписания = ТаблицаРасходов.СкопироватьКолонки();
	
	//Соберем допрасходы для переноса на другую партию/номенклатуру
	Если ВидОперации = "Комплектация" Тогда
		//Подготовка наборов фильтров
		МассивНоменклатуры = ТаблицаПартийРасход.ВыгрузитьКолонку("Номенклатура");
		ОбщегоНазначения.УдалитьПовторяющиесяЭлементы(МассивНоменклатуры);
		
		МассивПартий = ТаблицаПартийРасход.ВыгрузитьКолонку("ДокументОприходования");
		ОбщегоНазначения.УдалитьПовторяющиесяЭлементы(МассивПартий);
		
		Статусы = Новый Массив;
		Статусы.Добавить(Перечисления.СтатусыРасходовУСН.НеСписаноНеОплачено);
		Статусы.Добавить(Перечисления.СтатусыРасходовУСН.НеСписано);
		Статусы.Добавить(Перечисления.СтатусыРасходовУСН.НеСписаноПринято);
		Статусы.Добавить(Перечисления.СтатусыРасходовУСН.НеСписаноНеРаспределено);
		
		//Формирование таблицы фильтров
		ТаблицаФильтров = Новый ТаблицаЗначений;
		ТаблицаФильтров.Колонки.Добавить("ИмяПоля");
		ТаблицаФильтров.Колонки.Добавить("ЗначениеПоля");
		ТаблицаФильтров.Колонки.Добавить("Условие");
		
		Фильтр = ТаблицаФильтров.Добавить();
		Фильтр.ИмяПоля			= "ВидРасхода";
		Фильтр.ЗначениеПоля	= Перечисления.ВидыРасходовУСН.ДопРасходы;
		Фильтр.Условие			= "=";
		
		Фильтр = ТаблицаФильтров.Добавить();
		Фильтр.ИмяПоля			= "ЭлементРасхода";
		Фильтр.ЗначениеПоля	= МассивНоменклатуры;
		Фильтр.Условие			= "В";
		
		Фильтр = ТаблицаФильтров.Добавить();
		Фильтр.ИмяПоля			= "Партия";
		Фильтр.ЗначениеПоля	= МассивПартий;
		Фильтр.Условие			= "В";
		
		Фильтр = ТаблицаФильтров.Добавить();
		Фильтр.ИмяПоля			= "СтатусыОплатыРасходовУСН";
		Фильтр.ЗначениеПоля	= Статусы;
		Фильтр.Условие			= "В";
		
		//Из таблицы расходов выбираем соответствующие партии и элементы расходов
		ТаблицаДопРасходов = ВыполнитьЗапросПоРасходам(Новый Граница(СтруктураШапкиДокумента.Ссылка.МоментВремени(), ВидГраницы.Включая), СтруктураШапкиДокумента.Организация, ТаблицаФильтров);
		ТаблицаДопРасходов.Индексы.Добавить("ЭлементРасхода,Партия");
		
		ТаблицаРасчетныхДокументов = ТаблицаДопРасходов.Скопировать();
		ТаблицаРасчетныхДокументов.Свернуть("ЭлементРасхода, Партия, РасчетныйДокумент", "Количество"); 
		
		ТаблицаДопРасходов.Индексы.Добавить("ЭлементРасхода,Партия,РасчетныйДокумент");
		ТаблицаРасчетныхДокументов.Индексы.Добавить("ЭлементРасхода,Партия");
	КонецЕсли;
	
	ТаблицаДляСписания.Колонки.Добавить("СчетПриход");
	ТаблицаДляСписания.Колонки.Добавить("НоменклатураПриход");
	ТаблицаДляСписания.Колонки.Добавить("ПартияПриход");
	ТаблицаДляСписания.Колонки.Добавить("КоличествоПриход");
	
	//Заполняем таблицу списываемых расходов по таблице расходов и фильтрам партионного учета
	КолПартий = ТаблицаПартийРасход.Количество();
	Для НомерСтр = 0 по КолПартий - 1 Цикл
		
		СтрокаПартии = ТаблицаПартийРасход[НомерСтр];
		СтрокаПартииПриход = ТаблицаПартийПриход[НомерСтр];
		
		Если (ВидОперации = "Перемещение") и (СтрокаПартии.СчетУчета = СтрокаПартииПриход.СчетУчета) Тогда
			Продолжить;
		КонецЕсли;
		
		СтруктураОтбора = Новый Структура;
		СтруктураОтбора.Вставить("ЭлементРасхода",	СтрокаПартии.Номенклатура);
		СтруктураОтбора.Вставить("Партия",			СтрокаПартии.ДокументОприходования);
		СтруктураОтбора.Вставить("СчетУчета",		СтрокаПартии.СчетУчета);
		
		НайденныеСтроки = ТаблицаРасходов.НайтиСтроки(СтруктураОтбора);
		
		// Подлежащее погашению при списании количество
		КоличествоОсталосьПогасить = СтрокаПартии.Количество;
		Для Каждого Строка Из НайденныеСтроки Цикл
			Если КоличествоОсталосьПогасить <= 0 Тогда
				Прервать;
			КонецЕсли;
			Если Строка.Количество <= 0 Тогда
				Продолжить;
			КонецЕсли;
			
			КоличествоСписания = Мин(Строка.Количество, КоличествоОсталосьПогасить);
			КоличествоОсталосьПогасить = КоличествоОсталосьПогасить - КоличествоСписания;
						
			НоваяСтрока = ТаблицаДляСписания.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);
			
			НоваяСтрока.СчетПриход 			= СтрокаПартииПриход.СчетУчета;
			НоваяСтрока.НоменклатураПриход 	= СтрокаПартииПриход.Номенклатура;
			НоваяСтрока.ПартияПриход 		= СтрокаПартииПриход.ДокументОприходования;
								
			Если Строка.Количество > КоличествоСписания Тогда
				К = КоличествоСписания / Строка.Количество;
				НоваяСтрока.Количество 	= КоличествоСписания;
				НоваяСтрока.Сумма	 	= К*НоваяСтрока.Сумма;
				НоваяСтрока.НДС 		= К*НоваяСтрока.НДС;
			КонецЕсли;
			
			НоваяСтрока.КоличествоПриход = ?(СтрокаПартии.Количество = 0, 0, НоваяСтрока.Количество * СтрокаПартииПриход.Количество / СтрокаПартии.Количество);
			
			Строка.Количество = Строка.Количество - НоваяСтрока.Количество;
			Строка.Сумма = Строка.Сумма - НоваяСтрока.Сумма;
			Строка.НДС = Строка.НДС - НоваяСтрока.НДС;
		КонецЦикла;
		
		//Обработаем доп. расходы по партии
		Если ВидОперации = "Комплектация" Тогда
			КоличествоСписанияПоПартииТМЦ = СтрокаПартии.Количество - КоличествоОсталосьПогасить;
			
			//Пропустим операции поступления и перемещения
			Если НЕ КоличествоСписанияПоПартииТМЦ > 0 Тогда
				Продолжить;
			КонецЕсли;
			
			//Выберем расчетные документы доп.расходов
			СтруктураОтбора = Новый Структура;
			СтруктураОтбора.Вставить("ЭлементРасхода",	СтрокаПартии.Номенклатура);
			СтруктураОтбора.Вставить("Партия",			СтрокаПартии.ДокументОприходования);
			
			НайденныеСтрокиРД = ТаблицаРасчетныхДокументов.НайтиСтроки(СтруктураОтбора);
			
			Для Каждого СтрокаРасчетныйДокумент Из НайденныеСтрокиРД Цикл
				
				КоличествоСписанияРД = Мин(СтрокаРасчетныйДокумент.Количество, КоличествоСписанияПоПартииТМЦ);
				
				//Выберем доп. расходы данной номенклатуры/партии
				СтруктураОтбора = Новый Структура;
				СтруктураОтбора.Вставить("ЭлементРасхода",	СтрокаПартии.Номенклатура);
				СтруктураОтбора.Вставить("Партия",			СтрокаПартии.ДокументОприходования);
				СтруктураОтбора.Вставить("РасчетныйДокумент",СтрокаРасчетныйДокумент.РасчетныйДокумент);
				
				НайденныеСтроки = ТаблицаДопРасходов.НайтиСтроки(СтруктураОтбора);
				
				Для Каждого Строка Из НайденныеСтроки Цикл
					
					Если Строка.Количество <= 0 Тогда
						Продолжить;
					КонецЕсли;
					
					Если КоличествоСписанияРД <= 0 Тогда
						Прервать;
					КонецЕсли;
					
					КоличествоСписания = Мин(Строка.Количество, КоличествоСписанияРД);
					
					НоваяСтрока = ТаблицаДляСписания.Добавить();
					ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);
					
					НоваяСтрока.НоменклатураПриход 	= СтрокаПартииПриход.Номенклатура;
					НоваяСтрока.ПартияПриход 		= СтрокаПартииПриход.ДокументОприходования;
															
					Если Строка.Количество <> КоличествоСписания Тогда
						К = КоличествоСписания / Строка.Количество;
						НоваяСтрока.Количество 	= КоличествоСписания;
						НоваяСтрока.Сумма	 	= К*НоваяСтрока.Сумма;
						НоваяСтрока.НДС 		= К*НоваяСтрока.НДС;
					КонецЕсли;
					
					НоваяСтрока.КоличествоПриход = ?(СтрокаПартии.Количество = 0, 0, НоваяСтрока.Количество * СтрокаПартииПриход.Количество / СтрокаПартии.Количество);
					
					Строка.Количество 	=  Строка.Количество - НоваяСтрока.Количество;
					Строка.Сумма 		=  Строка.Сумма - НоваяСтрока.Сумма;
					Строка.НДС 			=  Строка.НДС - НоваяСтрока.НДС;
					
					КоличествоСписанияРД = КоличествоСписанияРД - КоличествоСписания;
					СтрокаРасчетныйДокумент.Количество = СтрокаРасчетныйДокумент.Количество - КоличествоСписания;
				КонецЦикла;
				
			КонецЦикла;
		КонецЕсли;	
	КонецЦикла;
	
	//Разложим таблицу списываемых расходов на движения корректировки статуса и принятые расходы
	ТаблицаКорректировкиРасход = ТаблицаДляСписания.СкопироватьКолонки();
	ТаблицаКорректировкиПриход = ТаблицаДляСписания.СкопироватьКолонки();
	ТаблицаПризнанныхРасходов  = ТаблицаДляСписания.СкопироватьКолонки();
	
	Для Каждого Расход Из ТаблицаДляСписания Цикл
		СтрокаКорректировкиРасход = ТаблицаКорректировкиРасход.Добавить();
		СтрокаКорректировкиПриход = ТаблицаКорректировкиПриход.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаКорректировкиРасход, Расход);
		ЗаполнитьЗначенияСвойств(СтрокаКорректировкиПриход, Расход);
		
		Если НЕ Расход.ВидРасхода = Перечисления.ВидыРасходовУСН.ДопРасходы Тогда
			СтрокаКорректировкиПриход.СчетУчета 	= Расход.СчетПриход;
		КонецЕсли;
		СтрокаКорректировкиПриход.ЭлементРасхода= Расход.НоменклатураПриход;
		СтрокаКорректировкиПриход.Партия 		= Расход.ПартияПриход;
		СтрокаКорректировкиПриход.Количество 	= Расход.КоличествоПриход;
	КонецЦикла;
	
	ТаблицыДвижений = Новый Структура("ТаблицаПриход, ТаблицаРасход, ТаблицаПринятых", ТаблицаКорректировкиПриход, ТаблицаКорректировкиРасход, ТаблицаПризнанныхРасходов);
	
	Если ВидОперации = "НаРеализацию" Тогда
		ТаблицыДвижений.ТаблицаПриход.ЗаполнитьЗначения(Перечисления.СтатусыПартийУСН.НаРеализации,"СтатусыПартийУСН");
	ИначеЕсли ВидОперации = "ВПереработку" Тогда
		ТаблицыДвижений.ТаблицаПриход.ЗаполнитьЗначения(Перечисления.СтатусыПартийУСН.ВПереработке,"СтатусыПартийУСН");
	ИначеЕсли (ВидОперации = "ВозвратСРеализации") ИЛИ (ВидОперации = "ВозвратИзПереработки") Тогда
		ТаблицыДвижений.ТаблицаПриход.ЗаполнитьЗначения(Перечисления.СтатусыПартийУСН.Купленные,"СтатусыПартийУСН");
	ИначеЕсли ВидОперации = "ВозвратПоставщику" Тогда
		ТаблицыДвижений.ТаблицаПриход.Очистить();
	КонецЕсли;
		 
	ДвиженияРегистров = СобратьТаблицыДвижений(ТаблицыДвижений,,,);
	ДвиженияРегистров.Вставить("ТаблицаПриход", ТаблицыДвижений.ТаблицаПриход);
										
КонецПроцедуры

//Операция зачета аванса поставщику
//ТаблицаДвижений - Таблица движений приход регистра Расходы УСН
Процедура ЗачетАвансаПоставщикуУСН(СтруктураШапкиДокумента, ТаблицаДвижений, ДвиженияРегистров, Договор, Сделка, ТаблицаРД, СуммаДокумента, ПриходОтражен = Ложь)
	
	КЗ = 1; //Коэффициент знака взаиморасчетов (+1 или -1)
	
	Если ТаблицаРД.Количество() = 0 Тогда
		Строка = ТаблицаРД.Добавить();
		Строка.ДокументРасчетовСКонтрагентом = Неопределено;
		Строка.СуммаВзаиморасчетов = СуммаДокумента;
		Строка.Сделка = Сделка;
	ИначеЕсли (ТаблицаРД.Итог("СуммаВзаиморасчетов") <> СуммаДокумента) и (ТаблицаРД.Итог("СуммаВзаиморасчетов") <> 0) Тогда
		КР = СуммаДокумента / ТаблицаРД.Итог("СуммаВзаиморасчетов");
		Для Каждого СтрокаРД Из ТаблицаРД Цикл
			СтрокаРД.СуммаВзаиморасчетов = КР * СтрокаРД.СуммаВзаиморасчетов;
		КонецЦикла;
		Если (ТаблицаРД.Итог("СуммаВзаиморасчетов") <> СуммаДокумента) Тогда
			ПоследняяСтрока = ТаблицаРД[ТаблицаРД.Количество()-1];
			ПоследняяСтрока.СуммаВзаиморасчетов = ПоследняяСтрока.СуммаВзаиморасчетов + (СуммаДокумента - ТаблицаРД.Итог("СуммаВзаиморасчетов"));
		КонецЕсли;
	КонецЕсли;
	
	Если (ТипЗнч(Договор) = Тип("СправочникСсылка.ДоговорыКонтрагентов")) И (ЗначениеЗаполнено(Договор)) Тогда
		УчетПоЗаказам = (Договор.ВедениеВзаиморасчетов <> Перечисления.ВедениеВзаиморасчетовПоДоговорам.ПоДоговоруВЦелом);
		УчетПоРД = (Договор.ВестиПоДокументамРасчетовСКонтрагентом);
	Иначе
		УчетПоЗаказам 	= Ложь;
		УчетПоРД 		= Ложь;
	КонецЕсли;
	
	МассивРД = ТаблицаРД.ВыгрузитьКолонку("ДокументРасчетовСКонтрагентом");
	ОбщегоНазначения.УдалитьПовторяющиесяЭлементы(МассивРД);
	
	МассивСделок = ТаблицаРД.ВыгрузитьКолонку("Сделка");
	ОбщегоНазначения.УдалитьПовторяющиесяЭлементы(МассивСделок);
	
	//Формирование таблицы фильтров
	ТаблицаФильтров = Новый ТаблицаЗначений;
	ТаблицаФильтров.Колонки.Добавить("ИмяПоля");
	ТаблицаФильтров.Колонки.Добавить("ЗначениеПоля");
	ТаблицаФильтров.Колонки.Добавить("Условие");
		
	Фильтр = ТаблицаФильтров.Добавить();
	Фильтр.ИмяПоля			= "ДоговорКонтрагента";
	Фильтр.ЗначениеПоля	= Договор;
	Фильтр.Условие			= "=";
	
	Если УчетПоЗаказам Тогда
		Фильтр = ТаблицаФильтров.Добавить();
		Фильтр.ИмяПоля			= "Сделка";
		Фильтр.ЗначениеПоля		= МассивСделок;
		Фильтр.Условие			= "В";
	КонецЕсли;
	
	Если УчетПоРД Тогда
		Фильтр = ТаблицаФильтров.Добавить();
		Фильтр.ИмяПоля			= "РасчетныйДокумент";
		Фильтр.ЗначениеПоля		= МассивРД;
		Фильтр.Условие			= "В";
    КонецЕсли;
	
	ТаблицаПлатежей = ВыполнитьЗапросПоВзаиморасчетамУСН(Новый Граница(СтруктураШапкиДокумента.Ссылка.МоментВремени(), ВидГраницы.Исключая), СтруктураШапкиДокумента.Организация, ТаблицаФильтров);
	
	ТаблицаДвиженияВзаиморасчетов = ТаблицаПлатежей.СкопироватьКолонки();
	ТаблицаДвиженияВзаиморасчетов.Колонки.Добавить("СтруктураКурса");
	
	СуммаРегл = 0;	
	СуммаОсталосьПогасить = СуммаДокумента;
	
	СтрокаРеквизитовПлДок = "";
	ИтоговаяСтруктура = Новый Структура("ТаблицаПриход, ТаблицаРасход, ТаблицаПринятых", Новый ТаблицаЗначений(), Новый ТаблицаЗначений(), Новый ТаблицаЗначений());

	ТаблицаРасходов = ТаблицаДвижений.Скопировать();
	
	Для Каждого СтрокаРД Из ТаблицаРД Цикл
		
		СтруктураОтбора = Новый Структура;
		СтруктураОтбора.Вставить("ДоговорКонтрагента",  Договор);
		Если УчетПоЗаказам Тогда
			СтруктураОтбора.Вставить("Сделка",  СтрокаРД.Сделка);
		КонецЕсли;
		Если УчетПоРД Тогда
			СтруктураОтбора.Вставить("РасчетныйДокумент",  СтрокаРД.ДокументРасчетовСКонтрагентом);
		КонецЕсли;
				
		НайденныеСтроки = ТаблицаПлатежей.НайтиСтроки(СтруктураОтбора);
		СуммаОсталосьПогаситьПоСтроке = СтрокаРД.СуммаВзаиморасчетов;
		
		Для Каждого Строка Из НайденныеСтроки Цикл
			Если СуммаОсталосьПогасить <= 0 Тогда
				Прервать;
			КонецЕсли;
			Если СуммаОсталосьПогаситьПоСтроке <= 0 Тогда
				Прервать;
			КонецЕсли;
			Если КЗ*(Строка.СуммаВзаиморасчетов)<= 0 Тогда
				Продолжить;
			КонецЕсли;
			
			СуммаСписания = Мин(КЗ*(Строка.СуммаВзаиморасчетов), СуммаОсталосьПогаситьПоСтроке);
			СуммаОсталосьПогасить = СуммаОсталосьПогасить - СуммаСписания;
			СуммаОсталосьПогаситьПоСтроке = СуммаОсталосьПогаситьПоСтроке - СуммаСписания;
			
			НоваяСтрока = ТаблицаДвиженияВзаиморасчетов.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);
			НоваяСтрока.ДоходЕНВД	 		= 0;
			НоваяСтрока.ДоходКомитента 		= 0;
			Если КЗ*(Строка.СуммаВзаиморасчетов) > СуммаСписания Тогда
				НоваяСтрока.СуммаВзаиморасчетов = Мин(КЗ*(Строка.СуммаВзаиморасчетов), СуммаСписания);
			КонецЕсли;
			СуммаРегл = СуммаРегл + НоваяСтрока.СуммаВзаиморасчетов * ОпределитьКурсПоДокументу(Договор, Сделка, Строка.РасчетныйДокумент);
			
			Если ТаблицаРасходов.Колонки.Найти("СтруктураКурса") = Неопределено Тогда
				ТаблицаРасходов.Колонки.Добавить("СтруктураКурса");
			КонецЕсли;
			
			ТаблицаРасходов.ЗаполнитьЗначения(Новый Структура("Курс, Кратность",ОпределитьКурсПоДокументу(Договор, Сделка, Строка.РасчетныйДокумент),1), "СтруктураКурса");
			ИтоговаяСтруктура = ИзменениеСтатусовРасходов(СтруктураШапкиДокумента, ТаблицаРасходов, СуммаСписания, "Оплата",, Строка.РасчетныйДокумент, ИтоговаяСтруктура);
			
		КонецЦикла;
		Если СуммаОсталосьПогаситьПоСтроке > 0 Тогда
			ДвижениеВзаиморасчетовУСН(ТаблицаДвиженияВзаиморасчетов, СтруктураШапкиДокумента.Организация, Договор, СтруктураШапкиДокумента.Ссылка, СтрокаРД.Сделка, СуммаОсталосьПогаситьПоСтроке);
			ИтоговаяСтруктура = ИзменениеСтатусовРасходов(СтруктураШапкиДокумента, ТаблицаРасходов, 0, "Оплата",, СтруктураШапкиДокумента.Ссылка, ИтоговаяСтруктура);
		КонецЕсли;
	КонецЦикла;
	
	Если ТаблицаДвиженияВзаиморасчетов.Колонки.Найти("ВидДвижения") = Неопределено Тогда
		ТаблицаДвиженияВзаиморасчетов.Колонки.Добавить("ВидДвижения");
	КонецЕсли;
	ТаблицаДвиженияВзаиморасчетов.ЗаполнитьЗначения(ВидДвиженияНакопления.Расход,"ВидДвижения");
	
	Если НЕ ПриходОтражен Тогда
		Для Каждого Строка ИЗ ТаблицаДвижений Цикл
			НоваяСтрока = ИтоговаяСтруктура.ТаблицаПриход.Вставить(0);
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);
		КонецЦикла;
	КонецЕсли;
		
	ДвиженияРегистров = СобратьТаблицыДвижений(ИтоговаяСтруктура, ТаблицаДвиженияВзаиморасчетов, , ДвиженияРегистров);
КонецПроцедуры

//Операция зачета аванса поставщику
//ТаблицаДвижений - Таблица движений приход регистра Расходы УСН (сформированная процедурой списание расходов)
Процедура ЗачетАвансаОтПокупателяУСН(СтруктураШапкиДокумента, ТаблицаДвижений, ДвиженияРегистровВх, ДвиженияРегистров, Договор, Сделка, ТаблицаРД, СуммаДокумента, СуммаЕНВД, СуммаКомиссии, КУДиР)
	
	КЗ = -1; //Коэффициент знака взаиморасчетов (+1 или -1)
	
	Если ТаблицаРД.Количество() = 0 Тогда
		Строка = ТаблицаРД.Добавить();
		Строка.ДокументРасчетовСКонтрагентом = Неопределено;
		Строка.СуммаВзаиморасчетов = СуммаДокумента;
		Строка.Сделка = Сделка;
	ИначеЕсли (ТаблицаРД.Итог("СуммаВзаиморасчетов") <> СуммаДокумента) и (ТаблицаРД.Итог("СуммаВзаиморасчетов") <> 0) Тогда
		КР = СуммаДокумента / ТаблицаРД.Итог("СуммаВзаиморасчетов");
		Для Каждого СтрокаРД Из ТаблицаРД Цикл
			СтрокаРД.СуммаВзаиморасчетов = КР * СтрокаРД.СуммаВзаиморасчетов;
		КонецЦикла;
		Если (ТаблицаРД.Итог("СуммаВзаиморасчетов") <> СуммаДокумента) Тогда
			ПоследняяСтрока = ТаблицаРД[ТаблицаРД.Количество()-1];
			ПоследняяСтрока.СуммаВзаиморасчетов = ПоследняяСтрока.СуммаВзаиморасчетов + (СуммаДокумента - ТаблицаРД.Итог("СуммаВзаиморасчетов"));
		КонецЕсли;
	КонецЕсли;
	
	УчетПоЗаказам = (Договор.ВедениеВзаиморасчетов <> Перечисления.ВедениеВзаиморасчетовПоДоговорам.ПоДоговоруВЦелом);
	УчетПоРД = (Договор.ВестиПоДокументамРасчетовСКонтрагентом);
	
	МассивРД = ТаблицаРД.ВыгрузитьКолонку("ДокументРасчетовСКонтрагентом");
	ОбщегоНазначения.УдалитьПовторяющиесяЭлементы(МассивРД);
	
	МассивСделок = ТаблицаРД.ВыгрузитьКолонку("Сделка");
	ОбщегоНазначения.УдалитьПовторяющиесяЭлементы(МассивСделок);
	
	//Формирование таблицы фильтров
	ТаблицаФильтров = Новый ТаблицаЗначений;
	ТаблицаФильтров.Колонки.Добавить("ИмяПоля");
	ТаблицаФильтров.Колонки.Добавить("ЗначениеПоля");
	ТаблицаФильтров.Колонки.Добавить("Условие");
		
	Фильтр = ТаблицаФильтров.Добавить();
	Фильтр.ИмяПоля			= "ДоговорКонтрагента";
	Фильтр.ЗначениеПоля	= Договор;
	Фильтр.Условие			= "=";
	
	Если УчетПоЗаказам Тогда
		Фильтр = ТаблицаФильтров.Добавить();
		Фильтр.ИмяПоля			= "Сделка";
		Фильтр.ЗначениеПоля		= МассивСделок;
		Фильтр.Условие			= "В";
	КонецЕсли;
	
	Если УчетПоРД Тогда
		Фильтр = ТаблицаФильтров.Добавить();
		Фильтр.ИмяПоля			= "РасчетныйДокумент";
		Фильтр.ЗначениеПоля		= МассивРД;
		Фильтр.Условие			= "В";
    КонецЕсли;
	
	ТаблицаПлатежей = ВыполнитьЗапросПоВзаиморасчетамУСН(Новый Граница(СтруктураШапкиДокумента.Ссылка.МоментВремени(), ВидГраницы.Исключая), СтруктураШапкиДокумента.Организация, ТаблицаФильтров, Истина);
	
	ТаблицаДвиженияВзаиморасчетов = ТаблицаПлатежей.СкопироватьКолонки();
			
	СуммаОсталосьПогасить = СуммаДокумента;
	СуммаЕНВДОсталосьПогасить = СуммаЕНВД;
	СуммаКомиссииОсталосьПогасить = СуммаКомиссии;
	
	Для Каждого СтрокаРД Из ТаблицаРД Цикл
		
		СтруктураОтбора = Новый Структура;
		СтруктураОтбора.Вставить("ДоговорКонтрагента",  Договор);
		Если УчетПоЗаказам Тогда
			СтруктураОтбора.Вставить("Сделка",  СтрокаРД.Сделка);
		КонецЕсли;
		Если УчетПоРД Тогда
			СтруктураОтбора.Вставить("РасчетныйДокумент",  СтрокаРД.ДокументРасчетовСКонтрагентом);
		КонецЕсли;
				
		НайденныеСтроки = ТаблицаПлатежей.НайтиСтроки(СтруктураОтбора);
		СуммаОсталосьПогаситьПоСтроке = СтрокаРД.СуммаВзаиморасчетов;
		
		К = ?(СуммаОсталосьПогасить = 0, 0, СуммаОсталосьПогаситьПоСтроке/СуммаОсталосьПогасить);
		СуммаЕНВДОсталосьПогаситьПоСтроке = К*СуммаЕНВД;
		СуммаКомиссииОсталосьПогаситьПоСтроке = К*СуммаКомиссии;
		
		Для Каждого Строка Из НайденныеСтроки Цикл
			Если СуммаОсталосьПогасить <= 0 Тогда
				Прервать;
			КонецЕсли;
			Если СуммаОсталосьПогаситьПоСтроке <= 0 Тогда
				Прервать;
			КонецЕсли;
			Если КЗ*(Строка.СуммаВзаиморасчетов)<= 0 Тогда
				Продолжить;
			КонецЕсли;
			Если НЕ ЗначениеЗаполнено(Строка.Графа4) Тогда
				Строка.Графа4 = 0;
			КонецЕсли;
			Если НЕ ЗначениеЗаполнено(Строка.Графа5) Тогда
				Строка.Графа5 = 0;
			КонецЕсли;
			
			КурсДокумента = ОпределитьКурсПоДокументу(Строка.ДоговорКонтрагента, Строка.Сделка, Строка.РасчетныйДокумент);
			КурсДокумента = ?(КурсДокумента = 0, 1, КурсДокумента);
		
			Строка.Графа5 = Мин(Строка.Графа5, Окр(КЗ*(Строка.СуммаВзаиморасчетов)*КурсДокумента, 2, 1));
			Строка.Графа4 = Мин(Строка.Графа4, Окр(КЗ*(Строка.СуммаВзаиморасчетов)*КурсДокумента, 2, 1));
		
			СуммаСписания = Мин(КЗ*(Строка.СуммаВзаиморасчетов), СуммаОсталосьПогаситьПоСтроке);
			СуммаОсталосьПогасить = СуммаОсталосьПогасить - СуммаСписания;
			СуммаОсталосьПогаситьПоСтроке = СуммаОсталосьПогаситьПоСтроке - СуммаСписания;
			
			СуммаСписанияКомиссии = Мин(КЗ*(Строка.СуммаВзаиморасчетов), СуммаКомиссииОсталосьПогаситьПоСтроке);
			СуммаКомиссииОсталосьПогасить = СуммаКомиссииОсталосьПогасить - СуммаСписанияКомиссии;
			СуммаКомиссииОсталосьПогаситьПоСтроке = СуммаКомиссииОсталосьПогаситьПоСтроке - СуммаСписанияКомиссии;
			
			СуммаСписанияЕНВД = Мин(КЗ*(Строка.СуммаВзаиморасчетов)-СуммаСписанияКомиссии, СуммаЕНВДОсталосьПогаситьПоСтроке);
			СуммаЕНВДОсталосьПогасить = СуммаЕНВДОсталосьПогасить - СуммаСписанияЕНВД;
			СуммаЕНВДОсталосьПогаситьПоСтроке = СуммаЕНВДОсталосьПогаситьПоСтроке - СуммаСписанияЕНВД;
			
			Если СуммаСписанияКомиссии > 0 И Строка.Графа5 > 0 Тогда
				СуммаКоррДоходов  = Строка.Графа5 * СуммаСписанияКомиссии / (КЗ*(Строка.СуммаВзаиморасчетов));
				КУДиР.Содержание = КУДиР.Содержание + "Аванс на сумму " + СуммаКоррДоходов + " руб. определен как выручка комитента. ";
				КУДиР.Графа5 = КУДиР.Графа5 - СуммаКоррДоходов;
			КонецЕсли;
			Если СуммаСписанияЕНВД > 0 И Строка.Графа5 > 0 Тогда
				СуммаКоррДоходов  = Строка.Графа5 * СуммаСписанияЕНВД / (КЗ*(Строка.СуммаВзаиморасчетов));
				КУДиР.Содержание = КУДиР.Содержание + "Аванс на сумму " + СуммаКоррДоходов + " руб. отнесен к деятельности ЕНВД. ";
				КУДиР.Графа5 = КУДиР.Графа5 - СуммаКоррДоходов;
				КУДиР.ДоходЕНВД	 = КУДиР.ДоходЕНВД + СуммаКоррДоходов;
			КонецЕсли;
			
			НоваяСтрока = ТаблицаДвиженияВзаиморасчетов.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);
			
			НоваяСтрока.ДоходЕНВД	 		= 0;
			НоваяСтрока.ДоходКомитента 		= 0;
			Если КЗ*(Строка.СуммаВзаиморасчетов) > СуммаСписания Тогда
				НоваяСтрока.СуммаВзаиморасчетов = Мин(КЗ*(Строка.СуммаВзаиморасчетов), СуммаСписания);
			Иначе
				НоваяСтрока.СуммаВзаиморасчетов = КЗ*(Строка.СуммаВзаиморасчетов);
			КонецЕсли;
		КонецЦикла;
		Если СуммаОсталосьПогаситьПоСтроке > 0 Тогда
			СуммаВзаиморасчетов = СуммаОсталосьПогаситьПоСтроке;
			ДвижениеВзаиморасчетовУСН(ТаблицаДвиженияВзаиморасчетов, СтруктураШапкиДокумента.Организация, Договор, СтруктураШапкиДокумента.Ссылка, СтрокаРД.Сделка, СуммаВзаиморасчетов, СуммаЕНВДОсталосьПогаситьПоСтроке, СуммаКомиссииОсталосьПогаситьПоСтроке);
		КонецЕсли;
		
	КонецЦикла;
	
	Зачтено = СуммаДокумента - СуммаОсталосьПогасить;
	ЗачтеноЕНВД = СуммаЕНВД - СуммаЕНВДОсталосьПогасить;
	ЗачтеноКомиссии = СуммаКомиссии - СуммаКомиссииОсталосьПогасить;
	
	КоэффОбщий = ?(СуммаДокумента - СуммаКомиссии <= 0, 0, (Зачтено - ЗачтеноКомиссии) / (СуммаДокумента - СуммаКомиссии));
	КоэффДоходы = ?(СуммаДокумента - СуммаКомиссии - СуммаЕНВД <= 0, 0, (Зачтено - ЗачтеноКомиссии - ЗачтеноЕНВД) / (СуммаДокумента - СуммаКомиссии - СуммаЕНВД));
	
	ТаблицаДляСписания = ТаблицаДвижений.СкопироватьКолонки();
	
	Для Каждого Строка Из ТаблицаДвижений Цикл
		Если Строка.СтатусыОплатыРасходовУСН = Перечисления.СтатусыРасходовУСН.НеРаспределено Тогда
			Продолжить;
		КонецЕсли;
		Если Строка.ОтражениеВУСН = Перечисления.ОтражениеВУСН.НеПринимаются Тогда
			Сумма = КоэффОбщий * Строка.Сумма;
			Количество = КоэффОбщий * Строка.Количество;
			НДС = КоэффОбщий * Строка.НДС;
		Иначе
			Сумма = КоэффДоходы * Строка.Сумма;
			Количество = КоэффДоходы * Строка.Количество;
			НДС = КоэффДоходы * Строка.НДС;
		КонецЕсли;
		Если Сумма = 0 Тогда
			Продолжить;
		Конецесли;
		НоваяСтрока = ТаблицаДляСписания.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);
		
		НоваяСтрока.Сумма = Сумма;
		НоваяСтрока.Количество = Количество;
		НоваяСтрока.НДС = НДС;
	КонецЦикла;

	//Разложим таблицу расходов на движения корректировки статуса и принятые расходы
	НовыеТаблицыДвижений = ИзменениеСтатусовРасходов(СтруктураШапкиДокумента, ТаблицаДляСписания, , "Доход", Ложь);
	
	Если ТаблицаДвиженияВзаиморасчетов.Колонки.Найти("ВидДвижения") = Неопределено Тогда
		ТаблицаДвиженияВзаиморасчетов.Колонки.Добавить("ВидДвижения");
	КонецЕсли;
	ТаблицаДвиженияВзаиморасчетов.ЗаполнитьЗначения(ВидДвиженияНакопления.Приход,"ВидДвижения");
	
	ДвиженияРегистров = СобратьТаблицыДвижений(НовыеТаблицыДвижений, ТаблицаДвиженияВзаиморасчетов, ,ДвиженияРегистровВх);
					
КонецПроцедуры

//Операция зачета аванса поставщику
//ТаблицаДвижений - Таблица движений приход регистра Расходы УСН (сформированная процедурой списание расходов)
Процедура ЗачетАвансаОтПокупателяБезУчетаРасходовУСН(СтруктураШапкиДокумента, ДвиженияРегистров, Договор, Сделка, ТаблицаРД, СуммаДокумента, СуммаЕНВД, СуммаКомиссии, КУДиР, ЗачитыватьАванс, УдержатьВознаграждение)
	
	КЗ = -1; //Коэффициент знака взаиморасчетов (+1 или -1)
	
	СуммаОсталосьПогасить = СуммаДокумента;
	СуммаЕНВДОсталосьПогасить = СуммаЕНВД;
	СуммаКомиссииОсталосьПогасить = СуммаКомиссии;
	ДоходПоУдержанномуВознаграждению = 0;
	
	Если ТаблицаРД.Количество() = 0 Тогда
		Строка = ТаблицаРД.Добавить();
		Строка.ДокументРасчетовСКонтрагентом = Неопределено;
		Строка.СуммаВзаиморасчетов = СуммаДокумента;
		Строка.Сделка = Сделка;
	ИначеЕсли (ТаблицаРД.Итог("СуммаВзаиморасчетов") <> СуммаДокумента) и (ТаблицаРД.Итог("СуммаВзаиморасчетов") <> 0) Тогда
		КР = СуммаДокумента / ТаблицаРД.Итог("СуммаВзаиморасчетов");
		Для Каждого СтрокаРД Из ТаблицаРД Цикл
			СтрокаРД.СуммаВзаиморасчетов = КР * СтрокаРД.СуммаВзаиморасчетов;
		КонецЦикла;
		Если (ТаблицаРД.Итог("СуммаВзаиморасчетов") <> СуммаДокумента) Тогда
			ПоследняяСтрока = ТаблицаРД[ТаблицаРД.Количество()-1];
			ПоследняяСтрока.СуммаВзаиморасчетов = ПоследняяСтрока.СуммаВзаиморасчетов + (СуммаДокумента - ТаблицаРД.Итог("СуммаВзаиморасчетов"));
		КонецЕсли;
	КонецЕсли;
	
	УчетПоЗаказам = (Договор.ВедениеВзаиморасчетов <> Перечисления.ВедениеВзаиморасчетовПоДоговорам.ПоДоговоруВЦелом);
	УчетПоРД = (Договор.ВестиПоДокументамРасчетовСКонтрагентом И ЗачитыватьАванс);
	
	МассивРД = ТаблицаРД.ВыгрузитьКолонку("ДокументРасчетовСКонтрагентом");
	ОбщегоНазначения.УдалитьПовторяющиесяЭлементы(МассивРД);
	
	МассивСделок = ТаблицаРД.ВыгрузитьКолонку("Сделка");
	ОбщегоНазначения.УдалитьПовторяющиесяЭлементы(МассивСделок);
	
	//Формирование таблицы фильтров
	ТаблицаФильтров = Новый ТаблицаЗначений;
	ТаблицаФильтров.Колонки.Добавить("ИмяПоля");
	ТаблицаФильтров.Колонки.Добавить("ЗначениеПоля");
	ТаблицаФильтров.Колонки.Добавить("Условие");
		
	Фильтр = ТаблицаФильтров.Добавить();
	Фильтр.ИмяПоля			= "ДоговорКонтрагента";
	Фильтр.ЗначениеПоля		= Договор;
	Фильтр.Условие			= "=";
	
	Если УчетПоЗаказам Тогда
		Фильтр = ТаблицаФильтров.Добавить();
		Фильтр.ИмяПоля			= "Сделка";
		Фильтр.ЗначениеПоля		= МассивСделок;
		Фильтр.Условие			= "В";
	КонецЕсли;
			
	Если НЕ ЗачитыватьАванс Тогда
		Фильтр = ТаблицаФильтров.Добавить();
		Фильтр.ИмяПоля			= "РасчетныйДокумент";
		Фильтр.ЗначениеПоля		= СтруктураШапкиДокумента.Ссылка;
		Фильтр.Условие			= "=";
	ИначеЕсли УчетПоРД Тогда
		Фильтр = ТаблицаФильтров.Добавить();
		Фильтр.ИмяПоля			= "РасчетныйДокумент";
		Фильтр.ЗначениеПоля		= МассивРД;
		Фильтр.Условие			= "В";
    КонецЕсли;
	
	ТаблицаПлатежей = ВыполнитьЗапросПоВзаиморасчетамУСН(Новый Граница(СтруктураШапкиДокумента.Ссылка.МоментВремени(), ВидГраницы.Включая), СтруктураШапкиДокумента.Организация, ТаблицаФильтров, Истина);
	
	ТаблицаДвиженияВзаиморасчетов = ТаблицаПлатежей.СкопироватьКолонки();
	
	Для Каждого СтрокаРД Из ТаблицаРД Цикл
		
		СтруктураОтбора = Новый Структура;
		СтруктураОтбора.Вставить("ДоговорКонтрагента",  Договор);
		Если УчетПоЗаказам Тогда
			СтруктураОтбора.Вставить("Сделка",  СтрокаРД.Сделка);
		КонецЕсли;
		Если УчетПоРД Тогда
			СтруктураОтбора.Вставить("РасчетныйДокумент",  СтрокаРД.ДокументРасчетовСКонтрагентом);
		КонецЕсли;
				
		НайденныеСтроки = ТаблицаПлатежей.НайтиСтроки(СтруктураОтбора);
		СуммаОсталосьПогаситьПоСтроке = СтрокаРД.СуммаВзаиморасчетов;
		
		К = ?(СуммаОсталосьПогасить = 0, 0, СуммаОсталосьПогаситьПоСтроке/СуммаОсталосьПогасить);
		СуммаЕНВДОсталосьПогаситьПоСтроке = К*СуммаЕНВД;
		СуммаКомиссииОсталосьПогаситьПоСтроке = К*СуммаКомиссии;
		
		Для Каждого Строка Из НайденныеСтроки Цикл
			Если СуммаОсталосьПогасить <= 0 Тогда
				Прервать;
			КонецЕсли;
			Если СуммаОсталосьПогаситьПоСтроке <= 0 Тогда
				Прервать;
			КонецЕсли;
			Если КЗ*(Строка.СуммаВзаиморасчетов)<= 0 Тогда
				Продолжить;
			КонецЕсли;
			Если НЕ ЗначениеЗаполнено(Строка.Графа4) Тогда
				Строка.Графа4 = 0;
			КонецЕсли;
			Если НЕ ЗначениеЗаполнено(Строка.Графа5) Тогда
				Строка.Графа5 = 0;
			КонецЕсли;
			
			КурсДокумента = ОпределитьКурсПоДокументу(Строка.ДоговорКонтрагента, Строка.Сделка, Строка.РасчетныйДокумент);
			КурсДокумента = ?(КурсДокумента = 0, 1, КурсДокумента);
		
			Строка.Графа5 = Мин(Строка.Графа5, Окр(КЗ*(Строка.СуммаВзаиморасчетов)*КурсДокумента, 2, 1));
			Строка.Графа4 = Мин(Строка.Графа4, Окр(КЗ*(Строка.СуммаВзаиморасчетов)*КурсДокумента, 2, 1));
			
			СуммаСписания = Мин(КЗ*(Строка.СуммаВзаиморасчетов), СуммаОсталосьПогаситьПоСтроке);
			СуммаОсталосьПогасить = СуммаОсталосьПогасить - СуммаСписания;
			СуммаОсталосьПогаситьПоСтроке = СуммаОсталосьПогаситьПоСтроке - СуммаСписания;
			
			СуммаСписанияКомиссии = Мин(КЗ*(Строка.СуммаВзаиморасчетов), СуммаКомиссииОсталосьПогаситьПоСтроке);
			СуммаКомиссииОсталосьПогасить = СуммаКомиссииОсталосьПогасить - СуммаСписанияКомиссии;
			СуммаКомиссииОсталосьПогаситьПоСтроке = СуммаКомиссииОсталосьПогаситьПоСтроке - СуммаСписанияКомиссии;
			
			СуммаСписанияЕНВД = Мин(КЗ*(Строка.СуммаВзаиморасчетов)-СуммаСписанияКомиссии, СуммаЕНВДОсталосьПогаситьПоСтроке);
			СуммаЕНВДОсталосьПогасить = СуммаЕНВДОсталосьПогасить - СуммаСписанияЕНВД;
			СуммаЕНВДОсталосьПогаситьПоСтроке = СуммаЕНВДОсталосьПогаситьПоСтроке - СуммаСписанияЕНВД;
			
			Если СуммаСписанияКомиссии > 0 И Строка.Графа5 > 0 Тогда
				СуммаКоррДоходов  = Строка.Графа5 * СуммаСписанияКомиссии / (КЗ*(Строка.СуммаВзаиморасчетов));
				КУДиР.Содержание = КУДиР.Содержание + "Аванс на сумму " + СуммаКоррДоходов + " руб. определен как выручка комитента. ";
				КУДиР.Графа5 = КУДиР.Графа5 - СуммаКоррДоходов;
			КонецЕсли;
			Если СуммаСписанияЕНВД > 0 И Строка.Графа5 > 0 Тогда
				СуммаКоррДоходов  = Строка.Графа5 * СуммаСписанияЕНВД / (КЗ*(Строка.СуммаВзаиморасчетов));
				КУДиР.Содержание = КУДиР.Содержание + "Аванс на сумму " + СуммаКоррДоходов + " руб. отнесен к деятельности ЕНВД. ";
				КУДиР.Графа5 	 = КУДиР.Графа5 - СуммаКоррДоходов;
				КУДиР.ДоходЕНВД	 = КУДиР.ДоходЕНВД + СуммаКоррДоходов;
			КонецЕсли;
			
			НоваяСтрока = ТаблицаДвиженияВзаиморасчетов.Добавить();
			Для Каждого Кол Из ТаблицаПлатежей.Колонки Цикл
				Если ПустаяСтрока(Кол.Имя) ИЛИ Кол.Имя = "QuieryId" Тогда
					Продолжить;
				КонецЕсли; 
				НоваяСтрока[Кол.Имя] = Строка[Кол.Имя];
			КонецЦикла;
			НоваяСтрока.ДоходЕНВД	 		= 0;
			НоваяСтрока.ДоходКомитента 		= 0;
			Если КЗ*(Строка.СуммаВзаиморасчетов) > СуммаСписания Тогда
				НоваяСтрока.СуммаВзаиморасчетов = Мин(КЗ*(Строка.СуммаВзаиморасчетов), СуммаСписания);
			Иначе
				НоваяСтрока.СуммаВзаиморасчетов = КЗ*(Строка.СуммаВзаиморасчетов);
			КонецЕсли;
			
			Если УдержатьВознаграждение Тогда
				
				СуммаВознаграждения = НоваяСтрока.СуммаВзаиморасчетов;
				
				//Формирование таблицы фильтров
				ТаблицаФильтров = Новый ТаблицаЗначений;
				ТаблицаФильтров.Колонки.Добавить("ИмяПоля");
				ТаблицаФильтров.Колонки.Добавить("ЗначениеПоля");
				ТаблицаФильтров.Колонки.Добавить("Условие");
				
				Фильтр = ТаблицаФильтров.Добавить();
				Фильтр.ИмяПоля			= "РасчетныйДокумент";
				Фильтр.ЗначениеПоля		= НоваяСтрока.РасчетныйДокумент;
				Фильтр.Условие			= "=";
				
				ТаблицаПродаж = ВыполнитьЗапросПоВзаиморасчетамУСН(Новый Граница(СтруктураШапкиДокумента.Ссылка.МоментВремени(), ВидГраницы.Включая), СтруктураШапкиДокумента.Организация, ТаблицаФильтров, Истина);
				
				Для Каждого Продажа из ТаблицаПродаж Цикл
					Если СуммаВознаграждения <= 0 Тогда
						Прервать;
					КонецЕсли;
					Если Продажа.ДоходКомитента <= 0 Тогда
						Продолжить;
					КонецЕсли;
					
					Если (ЗначениеЗаполнено(Продажа.ДоговорКонтрагента)) Тогда
						Договор = Продажа.ДоговорКонтрагента;
						КурсВалюты = МодульВалютногоУчета.ПолучитьКурсВалюты(Договор.ВалютаВзаиморасчетов, СтруктураШапкиДокумента.ДатаОплаты);
						
						ДоходКомитента = МодульВалютногоУчета.ПересчитатьИзВалютыВВалюту(Продажа.ДоходКомитента, Договор.ВалютаВзаиморасчетов,
						СтруктураШапкиДокумента.ВалютаВзаиморасчетов,
						КурсВалюты.Курс, СтруктураШапкиДокумента.КурсДокумента,
						КурсВалюты.Кратность, СтруктураШапкиДокумента.КратностьДокумента);
						ЗачетВознаграждения = Мин(ДоходКомитента, СуммаВознаграждения);
						СуммаВознаграждения = СуммаВознаграждения - ЗачетВознаграждения;
						
						КоррДоходаКомитента = МодульВалютногоУчета.ПересчитатьИзВалютыВВалюту(ЗачетВознаграждения, СтруктураШапкиДокумента.ВалютаВзаиморасчетов,
						Договор.ВалютаВзаиморасчетов,																
						СтруктураШапкиДокумента.КурсДокумента, КурсВалюты.Курс, 
						СтруктураШапкиДокумента.КратностьДокумента, КурсВалюты.Кратность);
					Иначе
						ЗачетВознаграждения = Мин(ДоходКомитента, СуммаВознаграждения);
						СуммаВознаграждения = СуммаВознаграждения - ЗачетВознаграждения;
						КоррДоходаКомитента = ЗачетВознаграждения;
					КонецЕсли;
					
					
					НоваяСтрока = ТаблицаДвиженияВзаиморасчетов.Добавить();
					ЗаполнитьЗначенияСвойств(НоваяСтрока, Продажа);
					НоваяСтрока.ДоходЕНВД	 		= 0;
					НоваяСтрока.СуммаВзаиморасчетов = 0;
					НоваяСтрока.ДоходКомитента 		= - КоррДоходаКомитента;
					
				КонецЦикла;
				
				Если СуммаВознаграждения > 0 Тогда
					ДоходПоУдержанномуВознаграждению = ДоходПоУдержанномуВознаграждению + СуммаВознаграждения;
				КонецЕсли;
			КонецЕсли;	
			
		КонецЦикла;
		Если СуммаОсталосьПогаситьПоСтроке > 0 Тогда
			СуммаВзаиморасчетов = СуммаОсталосьПогаситьПоСтроке;
			ДвижениеВзаиморасчетовУСН(ТаблицаДвиженияВзаиморасчетов, СтруктураШапкиДокумента.Организация, Договор, СтруктураШапкиДокумента.Ссылка, СтрокаРД.Сделка, СуммаВзаиморасчетов, СуммаЕНВДОсталосьПогаситьПоСтроке, СуммаКомиссииОсталосьПогаситьПоСтроке);
			Если УдержатьВознаграждение Тогда
				ОбщегоНазначения.СообщитьОбОшибке("Учет УСН: Не достаточно выручки комитента. Удержано из выручки комитента " + Строка(СуммаДокумента - СуммаОсталосьПогасить) + " из " + Строка(СуммаДокумента));
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	Если ДоходПоУдержанномуВознаграждению > 0 Тогда
		КУДиР.Содержание = КУДиР.Содержание + "Комиссионное вознаграждение удержано из выручки комитента. ";
		КУДиР.Графа5 = КУДиР.Графа5 + МодульВалютногоУчета.ПересчитатьИзВалютыВВалюту(ДоходПоУдержанномуВознаграждению, СтруктураШапкиДокумента.ВалютаВзаиморасчетов,
																СтруктураШапкиДокумента.ВалютаРегламентированногоУчета,
																СтруктураШапкиДокумента.КурсДокумента, 1,
																СтруктураШапкиДокумента.КратностьДокумента, 1);
	КонецЕсли;
	
	Зачтено = СуммаДокумента - СуммаОсталосьПогасить;
	ЗачтеноЕНВД = СуммаЕНВД - СуммаЕНВДОсталосьПогасить;
	ЗачтеноКомиссии = СуммаКомиссии - СуммаКомиссииОсталосьПогасить;
	
	Если ТаблицаДвиженияВзаиморасчетов.Колонки.Найти("ВидДвижения") = Неопределено Тогда
		ТаблицаДвиженияВзаиморасчетов.Колонки.Добавить("ВидДвижения");
	КонецЕсли;
	ТаблицаДвиженияВзаиморасчетов.ЗаполнитьЗначения(ВидДвиженияНакопления.Приход,"ВидДвижения");
	
	ТаблицаПризнанныхРасходов = Новый ТаблицаЗначений;
	ТаблицаРезультат = Новый ТаблицаЗначений;
	
	ДвиженияРегистров = Новый Структура("ВзаиморасчетыРасход, РасходыУСН, КУДиР", ТаблицаДвиженияВзаиморасчетов, ТаблицаРезультат, ТаблицаПризнанныхРасходов);
					
КонецПроцедуры

//Отразить оплату поставщику по взаиморасчетам УСН и расходам УСН
//
Процедура ОплатаПоставщикуУСН(СтруктураШапкиДокумента, ТаблицаОплат, ДвиженияРегистров, КУДиР, ВидОперации = "Оплата", ИмяДопПоля = "") //
	
	КЗ = -1; //Коэффициент знака взаиморасчетов (+1 или -1)
	
	ВыделятьНДСУСН = (СтруктураШапкиДокумента.ПорядокПризнанияРасходовПоНДС = Перечисления.ПорядокПризнанияРасходовПоНДС.ПоОплатеПоставщику);
		
	//Подготовка наборов фильтров
	МассивСделок = ТаблицаОплат.ВыгрузитьКолонку("Сделка");
	ОбщегоНазначения.УдалитьПовторяющиесяЭлементы(МассивСделок);
	
	//Формирование таблицы фильтров
	ТаблицаФильтров = Новый ТаблицаЗначений;
	ТаблицаФильтров.Колонки.Добавить("ИмяПоля");
	ТаблицаФильтров.Колонки.Добавить("ЗначениеПоля");
	ТаблицаФильтров.Колонки.Добавить("Условие");
	
	Фильтр = ТаблицаФильтров.Добавить();
	Если НЕ ТаблицаОплат.Колонки.Найти("ДоговорКонтрагента") = Неопределено Тогда
		МассивДоговоров = ТаблицаОплат.ВыгрузитьКолонку("ДоговорКонтрагента");
		ТаблицаОплат.Колонки.Добавить("Работник");
		ТаблицаОплат.ЗаполнитьЗначения(Справочники.ФизическиеЛица.ПустаяСсылка(), "Работник");
		ИмяИзмерения = "ДоговорКонтрагента";
	ИначеЕсли НЕ ТаблицаОплат.Колонки.Найти("Работник") = Неопределено Тогда
		МассивДоговоров = ТаблицаОплат.ВыгрузитьКолонку("Работник");
		ТаблицаОплат.Колонки.Добавить("ДоговорКонтрагента");
		ТаблицаОплат.ЗаполнитьЗначения(Неопределено, "ДоговорКонтрагента");
		ИмяИзмерения = "Работник";
	КонецЕсли;
	ОбщегоНазначения.УдалитьПовторяющиесяЭлементы(МассивДоговоров);
	Фильтр.ИмяПоля			= ИмяИзмерения;
	Фильтр.ЗначениеПоля	= МассивДоговоров;
	Фильтр.Условие			= "В";
	
	Если МассивСделок.Количество() > 0 Тогда
		Фильтр = ТаблицаФильтров.Добавить();
		Фильтр.ИмяПоля		= "Сделка";
		Фильтр.ЗначениеПоля	= МассивСделок;
		Фильтр.Условие		= "В";
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(СтруктураШапкиДокумента.ДатаОплаты) Тогда
		МоментДокумента = СтруктураШапкиДокумента.Ссылка.МоментВремени();
	Иначе
		МоментДокумента = Новый МоментВремени(СтруктураШапкиДокумента.ДатаОплаты, СтруктураШапкиДокумента.Ссылка);
	КонецЕсли;
	
	ТаблицаПлатежей = ВыполнитьЗапросПоВзаиморасчетамУСН(МоментДокумента, СтруктураШапкиДокумента.Организация, ТаблицаФильтров);
	
	ТаблицаДвиженияВзаиморасчетов = ТаблицаПлатежей.СкопироватьКолонки();
	ТаблицаДвиженияВзаиморасчетов.Колонки.Добавить("СтруктураКурса");
	
	//Заполняем таблицу списываемых расходов по таблице расходов и фильтрам партионного учета
	Для Каждого СтрокаПлатеж Из ТаблицаОплат Цикл
		
		Если СтрокаПлатеж.Сумма = 0 Тогда
			Продолжить;
		КонецЕсли;
		
		СтруктураОтбора = Новый Структура;
		Если НЕ ТаблицаОплат.Колонки.Найти("ДоговорКонтрагента") = Неопределено Тогда
			СтруктураОтбора.Вставить("ДоговорКонтрагента",  СтрокаПлатеж.ДоговорКонтрагента);
		КонецЕсли;
		Если НЕ ТаблицаОплат.Колонки.Найти("Работник") = Неопределено Тогда
			СтруктураОтбора.Вставить("Работник",  СтрокаПлатеж.Работник);
		КонецЕсли;
		
		Если ТипЗнч(СтрокаПлатеж.ДоговорКонтрагента) = Тип("СправочникСсылка.ДоговорыКонтрагентов") Тогда
			Если СтрокаПлатеж.ДоговорКонтрагента.ВедениеВзаиморасчетов <> Перечисления.ВедениеВзаиморасчетовПоДоговорам.ПоДоговоруВЦелом Тогда
				СтруктураОтбора.Вставить("Сделка", ?(ЗначениеЗаполнено(СтрокаПлатеж.Сделка), СтрокаПлатеж.Сделка, Неопределено));					
			КонецЕсли;
			Если СтрокаПлатеж.ДоговорКонтрагента.ВестиПоДокументамРасчетовСКонтрагентом И ИмяДопПоля = "РасчетныйДокумент" Тогда
				СтруктураОтбора.Вставить("РасчетныйДокумент", ?(ЗначениеЗаполнено(СтрокаПлатеж.РасчетныйДокумент), СтрокаПлатеж.РасчетныйДокумент, Неопределено));
			КонецЕсли;
		КонецЕсли;
		
		НайденныеСтроки = ТаблицаПлатежей.НайтиСтроки(СтруктураОтбора);
		
		Курс = 1;
		Кратность = 1;
		
		Если Не ТаблицаОплат.Колонки.Найти("КурсВзаиморасчетов") = Неопределено Тогда
			Курс = СтрокаПлатеж.КурсВзаиморасчетов;
		КонецЕсли;
		Если Не ТаблицаОплат.Колонки.Найти("КратностьВзаиморасчетов") = Неопределено Тогда
			Кратность = СтрокаПлатеж.КратностьВзаиморасчетов;
		КонецЕсли;
		СтруктураКурса = Новый Структура("Курс, Кратность",Курс,Кратность);
		
		Если НЕ ВидОперации = "ОплатаАО" Тогда
			КУДиР.Графа6 = КУДиР.Графа6 + СтрокаПлатеж.Сумма*Курс/Кратность;
		КонецЕсли;
		
		// Подлежащее погашению при списании количество
		СуммаОсталосьПогасить = СтрокаПлатеж.Сумма;
		Для Каждого Строка Из НайденныеСтроки Цикл
			Если СуммаОсталосьПогасить <= 0 Тогда
				Прервать;
			КонецЕсли;
			Если КЗ*(Строка.СуммаВзаиморасчетов)<= 0 Тогда
				Продолжить;
			КонецЕсли;
			
			СуммаСписания = Мин(КЗ*(Строка.СуммаВзаиморасчетов), СуммаОсталосьПогасить);
			СуммаОсталосьПогасить = СуммаОсталосьПогасить - СуммаСписания;
			
			НоваяСтрока = ТаблицаДвиженияВзаиморасчетов.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);
			
			НоваяСтрока.ДоходЕНВД	 		= 0;
			НоваяСтрока.ДоходКомитента 		= 0;
			Если КЗ*(Строка.СуммаВзаиморасчетов) > СуммаСписания Тогда
				НоваяСтрока.СуммаВзаиморасчетов = Мин(КЗ*(Строка.СуммаВзаиморасчетов), СуммаСписания);
			Иначе
				НоваяСтрока.СуммаВзаиморасчетов = КЗ*(Строка.СуммаВзаиморасчетов);
			КонецЕсли;
			НоваяСтрока.СтруктураКурса = СтруктураКурса;
			Строка.СуммаВзаиморасчетов = Строка.СуммаВзаиморасчетов - НоваяСтрока.СуммаВзаиморасчетов;
		КонецЦикла;
		Если СуммаОсталосьПогасить > 0 Тогда
			ДвижениеВзаиморасчетовУСН(ТаблицаДвиженияВзаиморасчетов, СтруктураШапкиДокумента.Организация, СтрокаПлатеж.ДоговорКонтрагента, СтруктураШапкиДокумента.Ссылка, СтрокаПлатеж.Сделка, СуммаОсталосьПогасить,,,СтрокаПлатеж.Работник);
		КонецЕсли;
	КонецЦикла;
	
	Если ТаблицаДвиженияВзаиморасчетов.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	//Подготовка наборов фильтров
	Если ТипЗнч(ТаблицаДвиженияВзаиморасчетов[0].ДоговорКонтрагента) = Тип("ПланСчетовСсылка.Хозрасчетный") Тогда
		УсловиеПоПолюРасходы = "СчетУчета";
		УсловиеПоПолюРасчеты = "ДоговорКонтрагента";
		МассивУсловия = ТаблицаДвиженияВзаиморасчетов.ВыгрузитьКолонку("ДоговорКонтрагента");
		ОбщегоНазначения.УдалитьПовторяющиесяЭлементы(МассивУсловия);
	ИначеЕсли ЗначениеЗаполнено(ТаблицаДвиженияВзаиморасчетов[0].Работник) Тогда
		УсловиеПоПолюРасходы = "ЭлементРасхода";
		УсловиеПоПолюРасчеты = "Работник";
		МассивУсловия = ТаблицаДвиженияВзаиморасчетов.ВыгрузитьКолонку("Работник");
		ОбщегоНазначения.УдалитьПовторяющиесяЭлементы(МассивУсловия);
	Иначе
		УсловиеПоПолюРасходы = "РасчетныйДокумент";
		УсловиеПоПолюРасчеты = "РасчетныйДокумент";
		МассивУсловия = ТаблицаДвиженияВзаиморасчетов.ВыгрузитьКолонку("РасчетныйДокумент");
		ОбщегоНазначения.УдалитьПовторяющиесяЭлементы(МассивУсловия);
	КонецЕсли;
	
	ТаблицаФильтров = Новый ТаблицаЗначений;
	ТаблицаФильтров.Колонки.Добавить("ИмяПоля");
	ТаблицаФильтров.Колонки.Добавить("ЗначениеПоля");
	ТаблицаФильтров.Колонки.Добавить("Условие");
		
	Статусы = Новый Массив;
	Статусы.Добавить(Перечисления.СтатусыРасходовУСН.НеСписаноНеОплачено);
	Статусы.Добавить(Перечисления.СтатусыРасходовУСН.НеВыпущеноНеОплачено); //~
	Статусы.Добавить(Перечисления.СтатусыРасходовУСН.НеОплачено);
	Статусы.Добавить(Перечисления.СтатусыРасходовУСН.НеОплаченоНеОплаченоПокупателем);
	
	Фильтр = ТаблицаФильтров.Добавить();
	Фильтр.ИмяПоля			= УсловиеПоПолюРасходы;
	Фильтр.ЗначениеПоля		= МассивУсловия;
	Фильтр.Условие			= "В";
	
	Фильтр = ТаблицаФильтров.Добавить();
	Фильтр.ИмяПоля			= "СтатусыОплатыРасходовУСН";
	Фильтр.ЗначениеПоля	= Статусы;
	Фильтр.Условие			= "В";
	
	Если НЕ ЗначениеЗаполнено(СтруктураШапкиДокумента.ДатаОплаты) Тогда
		МоментДокумента = СтруктураШапкиДокумента.Ссылка.МоментВремени();
	Иначе
		МоментДокумента = Новый МоментВремени(СтруктураШапкиДокумента.ДатаОплаты, СтруктураШапкиДокумента.Ссылка);
	КонецЕсли;
	ТаблицаРасходов = ВыполнитьЗапросПоРасходам(Новый Граница(МоментДокумента, ВидГраницы.Включая), СтруктураШапкиДокумента.Организация, ТаблицаФильтров);
	
	ТаблицаДляСписания = ТаблицаРасходов.СкопироватьКолонки();
	ТаблицаДляСписания.Колонки.Добавить("СтруктураКурса");
		
	//Заполняем таблицу списываемых расходов по таблице расходов и фильтрам партионного учета
	Для Каждого СтрокаПартии Из ТаблицаДвиженияВзаиморасчетов Цикл
		
		Если СтрокаПартии.РасчетныйДокумент =  СтруктураШапкиДокумента.Ссылка Тогда
			Продолжить;
		КонецЕсли;
		СтруктураОтбора = Новый Структура;
		СтруктураОтбора.Вставить(УсловиеПоПолюРасходы, СтрокаПартии[УсловиеПоПолюРасчеты]);
		
		НайденныеСтроки = ТаблицаРасходов.НайтиСтроки(СтруктураОтбора);
		СтруктураКурса = СтрокаПартии.СтруктураКурса;
		// Подлежащее погашению при списании количество
		СуммаОсталосьПогасить = СтрокаПартии.СуммаВзаиморасчетов;
		Для Каждого Строка Из НайденныеСтроки Цикл
			Если СуммаОсталосьПогасить <= 0 Тогда
				Прервать;
			КонецЕсли;
			Если Строка.Сумма <= 0 Тогда
				Продолжить;
			КонецЕсли;
			
			//Исключим агентский НДС, зарегистрированный по тому же расчетному документу
			Если УсловиеПоПолюРасходы = "РасчетныйДокумент" 
				И УсловиеПоПолюРасчеты = "РасчетныйДокумент"
				И Строка.ВидРасхода = Перечисления.ВидыРасходовУСН.Налоги Тогда
				Продолжить;
			КонецЕсли;
			
			Если ЗначениеЗаполнено(Строка.РасчетныйДокумент) Тогда
				УчетнаяПолитикаДокумент = ОбщегоНазначения.ПолучитьПараметрыУчетнойПолитикиРегл(Строка.РасчетныйДокумент.Дата, СтруктураШапкиДокумента.Организация, Ложь);
				ВыделятьНДСУСНДокумент = ?(НЕ ЗначениеЗаполнено(УчетнаяПолитикаДокумент), ВыделятьНДСУСН, (УчетнаяПолитикаДокумент.ПорядокПризнанияРасходовПоНДС = Перечисления.ПорядокПризнанияРасходовПоНДС.ПоОплатеПоставщику));
			Иначе
				ВыделятьНДСУСНДокумент = ВыделятьНДСУСН;
			КонецЕсли;
			
			Если ВыделятьНДСУСНДокумент И 
				(Строка.ВидРасхода <> Перечисления.ВидыРасходовУСН.ОС) И
				(Строка.ВидРасхода <> Перечисления.ВидыРасходовУСН.НМА) Тогда
				
				СуммаСписания = Мин(Строка.Сумма + Строка.НДС, СуммаОсталосьПогасить);
				СуммаОсталосьПогасить = Макс(0, СуммаОсталосьПогасить - СуммаСписания);
				К = СуммаСписания / (Строка.Сумма + Строка.НДС);
			Иначе
				СуммаСписания = Мин(Строка.Сумма, СуммаОсталосьПогасить);
				СуммаОсталосьПогасить = СуммаОсталосьПогасить - СуммаСписания;
				К = СуммаСписания / (Строка.Сумма);
			КонецЕсли;
			
			НоваяСтрока = ТаблицаДляСписания.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);
			
			Строка.Сумма = Строка.Сумма - СуммаСписания;
			
			Если НЕ К = 1 Тогда
				НоваяСтрока.Сумма	 	= К*НоваяСтрока.Сумма;
				НоваяСтрока.Количество 	= К*НоваяСтрока.Количество;
				НоваяСтрока.НДС 		= К*НоваяСтрока.НДС;
			КонецЕсли;
			НоваяСтрока.СтруктураКурса = СтруктураКурса;
		КонецЦикла;
	КонецЦикла;
	
	ТаблицыДвижений = ИзменениеСтатусовРасходов(СтруктураШапкиДокумента, ТаблицаДляСписания, , ВидОперации, Ложь);
	
	Если ВидОперации = "ОплатаАО" Тогда
		ТаблицыДвижений.ТаблицаПриход.ЗаполнитьЗначения(СтруктураШапкиДокумента.Ссылка,"РасчетныйДокумент");
		ТаблицыДвижений.ТаблицаПриход.ЗаполнитьЗначения(СтруктураШапкиДокумента.Ссылка.ФизЛицо,"ДоговорКонтрагента");
	КонецЕсли;
	
	Если ТаблицаДвиженияВзаиморасчетов.Колонки.Найти("ВидДвижения") = Неопределено Тогда
		ТаблицаДвиженияВзаиморасчетов.Колонки.Добавить("ВидДвижения");
	КонецЕсли;
	ТаблицаДвиженияВзаиморасчетов.ЗаполнитьЗначения(ВидДвиженияНакопления.Приход,"ВидДвижения");
	
	ДвиженияРегистров = СобратьТаблицыДвижений(ТаблицыДвижений, ТаблицаДвиженияВзаиморасчетов,, ДвиженияРегистров);
	
КонецПроцедуры

//Отразить оплату от покупателя по взаиморасчетам УСН и расходам УСН
//
Процедура ОплатаОтПокупателяУСН(СтруктураШапкиДокумента, ТаблицаОплат, ДвиженияРегистров, КУДиР, ИмяДопПоля = "")
	
	//Подготовка движений регистра взаиморасчетов
	УчетнаяПолитика = ОбщегоНазначения.ПолучитьПараметрыУчетнойПолитикиРегл(СтруктураШапкиДокумента.ДатаОплаты, СтруктураШапкиДокумента.Организация);
	
	КЗ = 1; //Коэффициент знака взаиморасчетов (+1 или -1)
	
	//Услуги банка, удержанные из оплаты покупателя
	СуммаУслуг = 0;
	Если СтруктураШапкиДокумента.Ссылка.Метаданные().Реквизиты.Найти("СуммаУслуг") <> Неопределено Тогда
		СуммаУслуг = СтруктураШапкиДокумента.Ссылка.СуммаУслуг;
	КонецЕсли;
	
	МассивСделок = ТаблицаОплат.ВыгрузитьКолонку("Сделка");
	ОбщегоНазначения.УдалитьПовторяющиесяЭлементы(МассивСделок);
	
	//Формирование таблицы фильтров
	ТаблицаФильтров = Новый ТаблицаЗначений;
	ТаблицаФильтров.Колонки.Добавить("ИмяПоля");
	ТаблицаФильтров.Колонки.Добавить("ЗначениеПоля");
	ТаблицаФильтров.Колонки.Добавить("Условие");
		
	Фильтр = ТаблицаФильтров.Добавить();
	Если НЕ ТаблицаОплат.Колонки.Найти("ДоговорКонтрагента") = Неопределено Тогда
		МассивДоговоров = ТаблицаОплат.ВыгрузитьКолонку("ДоговорКонтрагента");
		ТаблицаОплат.Колонки.Добавить("Работник");
		ТаблицаОплат.ЗаполнитьЗначения(Справочники.ФизическиеЛица.ПустаяСсылка(), "Работник");
		ИмяИзмерения = "ДоговорКонтрагента";
	ИначеЕсли НЕ ТаблицаОплат.Колонки.Найти("Работник") = Неопределено Тогда
		МассивДоговоров = ТаблицаОплат.ВыгрузитьКолонку("Работник");
		ТаблицаОплат.Колонки.Добавить("ДоговорКонтрагента");
		ТаблицаОплат.ЗаполнитьЗначения(Неопределено, "ДоговорКонтрагента");
		ИмяИзмерения = "Работник";
	КонецЕсли;
	ОбщегоНазначения.УдалитьПовторяющиесяЭлементы(МассивДоговоров);
	Фильтр.ИмяПоля			= ИмяИзмерения;
	Фильтр.ЗначениеПоля		= МассивДоговоров;
	Фильтр.Условие			= "В";
	
	Если МассивСделок.Количество() > 0 Тогда
		Фильтр = ТаблицаФильтров.Добавить();
		Фильтр.ИмяПоля		= "Сделка";
		Фильтр.ЗначениеПоля	= МассивСделок;
		Фильтр.Условие		= "В";
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(СтруктураШапкиДокумента.ДатаОплаты) Тогда
		МоментДокумента = СтруктураШапкиДокумента.Ссылка.МоментВремени();
	Иначе
		МоментДокумента = Новый МоментВремени(СтруктураШапкиДокумента.ДатаОплаты, СтруктураШапкиДокумента.Ссылка);
	КонецЕсли;

	ТаблицаПлатежей = ВыполнитьЗапросПоВзаиморасчетамУСН(МоментДокумента, СтруктураШапкиДокумента.Организация, ТаблицаФильтров);
	
	ТаблицаДвиженияВзаиморасчетов = ТаблицаПлатежей.СкопироватьКолонки();
	ТаблицаДвиженияВзаиморасчетов.Колонки.Добавить("СуммаВзаиморасчетовПолная", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15,2)));
	ТаблицаДвиженияВзаиморасчетов.Колонки.Добавить("СуммаДоходЕНВДПолная", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15,2)));
	ТаблицаДвиженияВзаиморасчетов.Колонки.Добавить("СуммаДоходКомПолная", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15,2)));
	
	СуммаКоррДоходовКомитента 	= 0;
	СуммаКоррДоходовЕНВД		= 0;
	СуммаУслугВсего				= СуммаУслуг;
	
	//Заполняем таблицу списываемых расходов по таблице расходов и фильтрам партионного учета
	Для Каждого СтрокаПлатеж Из ТаблицаОплат Цикл
		СтруктураОтбора = Новый Структура;
		
		Если НЕ ТаблицаОплат.Колонки.Найти("ДоговорКонтрагента") = Неопределено Тогда
			СтруктураОтбора.Вставить("ДоговорКонтрагента",  СтрокаПлатеж.ДоговорКонтрагента);
		КонецЕсли;
		Если НЕ ТаблицаОплат.Колонки.Найти("Работник") = Неопределено Тогда
			СтруктураОтбора.Вставить("Работник",  СтрокаПлатеж.Работник);
		КонецЕсли;
		
		Если ТипЗнч(СтрокаПлатеж.ДоговорКонтрагента) = Тип("СправочникСсылка.ДоговорыКонтрагентов") Тогда
			Если СтрокаПлатеж.ДоговорКонтрагента.ВедениеВзаиморасчетов <> Перечисления.ВедениеВзаиморасчетовПоДоговорам.ПоДоговоруВЦелом Тогда
				СтруктураОтбора.Вставить("Сделка", ?(ЗначениеЗаполнено(СтрокаПлатеж.Сделка), СтрокаПлатеж.Сделка, Неопределено));					
			КонецЕсли;
			Если СтрокаПлатеж.ДоговорКонтрагента.ВестиПоДокументамРасчетовСКонтрагентом И ИмяДопПоля = "РасчетныйДокумент" Тогда
				СтруктураОтбора.Вставить("РасчетныйДокумент", ?(ЗначениеЗаполнено(СтрокаПлатеж.РасчетныйДокумент), СтрокаПлатеж.РасчетныйДокумент, Неопределено));
			КонецЕсли;
		КонецЕсли;
		
		НайденныеСтроки = ТаблицаПлатежей.НайтиСтроки(СтруктураОтбора);
		
		Курс = 1;
		Кратность = 1;
		
		Если Не ТаблицаОплат.Колонки.Найти("КурсВзаиморасчетов") = Неопределено Тогда
			Курс = СтрокаПлатеж.КурсВзаиморасчетов;
		КонецЕсли;
		Если Не ТаблицаОплат.Колонки.Найти("КратностьВзаиморасчетов") = Неопределено Тогда
			Кратность = СтрокаПлатеж.КратностьВзаиморасчетов;
		КонецЕсли;
		
		КУДиР.Графа4 = КУДиР.Графа4 + СтрокаПлатеж.Сумма*Курс/Кратность;
				
		// Подлежащее погашению при списании количество
		СуммаОсталосьПогасить = СтрокаПлатеж.Сумма + СуммаУслуг; //Списание задоленности по регистру производится на полную сумму
		Для Каждого Строка Из НайденныеСтроки Цикл
			Если СуммаОсталосьПогасить <= 0 Тогда
				Прервать;
			КонецЕсли;
			Если КЗ*(Строка.СуммаВзаиморасчетов)<= 0 Тогда
				Продолжить;
			КонецЕсли;
			
			СуммаСписания = Мин(КЗ*(Строка.СуммаВзаиморасчетов), СуммаОсталосьПогасить);
			СуммаОсталосьПогасить = СуммаОсталосьПогасить - СуммаСписания;
			
			НоваяСтрока = ТаблицаДвиженияВзаиморасчетов.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);
			
			НоваяСтрока.СуммаВзаиморасчетовПолная = КЗ*Строка.СуммаВзаиморасчетов;
			НоваяСтрока.СуммаДоходКомПолная	  	  = КЗ*Строка.ДоходКомитента;
			НоваяСтрока.СуммаДоходЕНВДПолная	  = КЗ*Строка.ДоходЕНВД;
			
			Если КЗ*(Строка.СуммаВзаиморасчетов) > СуммаСписания Тогда
				НоваяСтрока.СуммаВзаиморасчетов = Мин(КЗ*(Строка.СуммаВзаиморасчетов), СуммаСписания);
				НоваяСтрока.ДоходЕНВД	 		= Мин(КЗ*(Строка.ДоходЕНВД), НоваяСтрока.СуммаВзаиморасчетов);
				НоваяСтрока.ДоходКомитента 		= Мин(КЗ*(Строка.ДоходКомитента), НоваяСтрока.СуммаВзаиморасчетов - НоваяСтрока.ДоходЕНВД);
			КонецЕсли;
			
			ДоПереходаНаУСН = Ложь;
			Если (УчетнаяПолитика.ПоложенияПереходногоПериодаУСН) Тогда
				Если ЗначениеЗаполнено(НоваяСтрока.РасчетныйДокумент) Тогда
					Если УчетнаяПолитика.ДатаПереходаНаУСН > НоваяСтрока.РасчетныйДокумент.Дата Тогда
						//Не включается в доходы в соответствии с положениями переходного 
						//периода ст.346.25 НК РФ
						ДоПереходаНаУСН = Истина;
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
			
			СуммаВзаиморасчетов = НоваяСтрока.СуммаВзаиморасчетов;
			ДоходЕНВД 			= НоваяСтрока.ДоходЕНВД;
			ДоходКомитента 		= НоваяСтрока.ДоходКомитента;
			
			//Оражение платежа в КУДиР производиться только в части фактически поступивших средств
			Если СуммаУслуг > 0 Тогда
				СписатьУслуг = Мин(СуммаВзаиморасчетов, СуммаУслуг);
				СуммаУслуг = СуммаУслуг - СписатьУслуг;
				
				КоэффУслуг = ?(СуммаВзаиморасчетов = 0, 0, СписатьУслуг / СуммаВзаиморасчетов); 
				СуммаВзаиморасчетов  = СуммаВзаиморасчетов - СписатьУслуг;
				
				СуммаЕНВД_БН = Окр(КоэффУслуг*ДоходЕНВД, 2, 1);
				ДоходЕНВД = ДоходЕНВД - СуммаЕНВД_БН;
				
				//Уменьшим сумму расходов на услуги банка в части ЕНВД
				СуммаУслугВсего = СуммаУслугВсего - СуммаЕНВД_БН;
			
				СуммаКомиссии_БН = Окр(КоэффУслуг*ДоходКомитента, 2, 1);
				ДоходКомитента = ДоходКомитента - СуммаКомиссии_БН;
			КонецЕсли;
			
			Если НЕ ДоПереходаНаУСН Тогда
				
				КУДиР.Графа5 = КУДиР.Графа5 + (СуммаВзаиморасчетов - ДоходЕНВД - ДоходКомитента)*Курс/Кратность;
				
				Если ДоходКомитента > 0 Тогда
					СуммаКоррДоходовКомитента  = СуммаКоррДоходовКомитента + ДоходКомитента * Курс/Кратность;
				КонецЕсли;
				Если ДоходЕНВД > 0 Тогда
					СуммаКоррДоходов = ДоходЕНВД * Курс/Кратность;
					СуммаКоррДоходовЕНВД  = СуммаКоррДоходовЕНВД + СуммаКоррДоходов;
					КУДиР.ДоходЕНВД	 = КУДиР.ДоходЕНВД + СуммаКоррДоходов;
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
		Если СуммаОсталосьПогасить > 0 Тогда
			КУДиР.Графа5 = КУДиР.Графа5 + СуммаОсталосьПогасить*Курс/Кратность;
			ДвижениеВзаиморасчетовУСН(ТаблицаДвиженияВзаиморасчетов, СтруктураШапкиДокумента.Организация, СтрокаПлатеж.ДоговорКонтрагента, СтруктураШапкиДокумента.Ссылка, СтрокаПлатеж.Сделка, СуммаОсталосьПогасить, 0, 0, СтрокаПлатеж.Работник);
		КонецЕсли;
	КонецЦикла;
	
	Если СуммаКоррДоходовКомитента > 0 Тогда
		КУДиР.Содержание = КУДиР.Содержание + "Оплата на сумму " + СуммаКоррДоходовКомитента + " руб. определена как выручка комитента. ";
	КонецЕсли;
	Если СуммаКоррДоходовЕНВД > 0 Тогда
		КУДиР.Содержание = КУДиР.Содержание + "Оплата на сумму " + СуммаКоррДоходовЕНВД + " руб. отнесена к деятельности ЕНВД. ";
	КонецЕсли;
	Если (СуммаУслугВсего) > 0 Тогда
		КУДиР.Содержание = КУДиР.Содержание + "Удержана комиссия банка на сумму " + СуммаУслугВсего + " руб. ";
		КУДиР.Графа7 = КУДиР.Графа7 + СуммаУслугВсего;
	КонецЕсли;

//Подготовка движений регистра расходов	
	
	//Подготовка наборов фильтров
	МассивПартий = ТаблицаДвиженияВзаиморасчетов.ВыгрузитьКолонку("РасчетныйДокумент");
	ОбщегоНазначения.УдалитьПовторяющиесяЭлементы(МассивПартий);
	
	Статусы = Новый Массив;
	Статусы.Добавить(Перечисления.СтатусыРасходовУСН.НеОплаченоНеОплаченоПокупателем);
	Статусы.Добавить(Перечисления.СтатусыРасходовУСН.НеОплаченоПокупателем);
	
	//Формирование таблицы фильтров
	ТаблицаФильтров = Новый ТаблицаЗначений;
	ТаблицаФильтров.Колонки.Добавить("ИмяПоля");
	ТаблицаФильтров.Колонки.Добавить("ЗначениеПоля");
	ТаблицаФильтров.Колонки.Добавить("Условие");
	
	Фильтр = ТаблицаФильтров.Добавить();
	Фильтр.ИмяПоля			= "ВидРасхода";
	Фильтр.ЗначениеПоля	= Перечисления.ВидыРасходовУСН.Номенклатура;
	Фильтр.Условие			= "=";
	
	Фильтр = ТаблицаФильтров.Добавить();
	Фильтр.ИмяПоля			= "СтатусыПартийУСН";
	Фильтр.ЗначениеПоля	= Перечисления.СтатусыПартийУСН.Списанные;
	Фильтр.Условие			= "=";
	
	Фильтр = ТаблицаФильтров.Добавить();
	Фильтр.ИмяПоля			= "Партия";
	Фильтр.ЗначениеПоля	= МассивПартий;
	Фильтр.Условие			= "В";
	
	Фильтр = ТаблицаФильтров.Добавить();
	Фильтр.ИмяПоля			= "СтатусыОплатыРасходовУСН";
	Фильтр.ЗначениеПоля	= Статусы;
	Фильтр.Условие			= "В";
	
	//Из таблицы расходов выбираем соответствующие партии и элементы расходов
	Если НЕ ЗначениеЗаполнено(СтруктураШапкиДокумента.ДатаОплаты) Тогда
		МоментДокумента = СтруктураШапкиДокумента.Ссылка.МоментВремени();
	Иначе
		МоментДокумента = Новый МоментВремени(СтруктураШапкиДокумента.ДатаОплаты, СтруктураШапкиДокумента.Ссылка);
	КонецЕсли;
	ТаблицаРасходов = ВыполнитьЗапросПоРасходам(Новый Граница(МоментДокумента, ВидГраницы.Включая), СтруктураШапкиДокумента.Организация, ТаблицаФильтров);
	
	ТаблицаДляСписания = ТаблицаРасходов.СкопироватьКолонки();
	
	//Заполняем таблицу списываемых расходов по таблице расходов и фильтрам партионного учета
	Для Каждого СтрокаПлатеж Из ТаблицаДвиженияВзаиморасчетов Цикл
		СтруктураОтбора = Новый Структура;
		СтруктураОтбора.Вставить("Партия",  СтрокаПлатеж.РасчетныйДокумент);
		
		//При расчете коэффициента оплаты не учитываются расчеты за комиссионные товары
		Если СтрокаПлатеж.СуммаВзаиморасчетовПолная  - СтрокаПлатеж.СуммаДоходКомПолная = 0 Тогда
			Продолжить;
		Конецесли;
		КоэффОбщий = (СтрокаПлатеж.СуммаВзаиморасчетов - СтрокаПлатеж.ДоходКомитента) / (СтрокаПлатеж.СуммаВзаиморасчетовПолная - СтрокаПлатеж.СуммаДоходКомПолная);
		КоэффДоходы = ?(СтрокаПлатеж.СуммаВзаиморасчетовПолная - СтрокаПлатеж.СуммаДоходКомПолная - СтрокаПлатеж.СуммаДоходЕНВДПолная = 0, 0, 
						(СтрокаПлатеж.СуммаВзаиморасчетов - СтрокаПлатеж.ДоходКомитента - СтрокаПлатеж.ДоходЕНВД )/ 
						(СтрокаПлатеж.СуммаВзаиморасчетовПолная - СтрокаПлатеж.СуммаДоходКомПолная - СтрокаПлатеж.СуммаДоходЕНВДПолная));
				
		НайденныеСтроки = ТаблицаРасходов.НайтиСтроки(СтруктураОтбора);
		
		Для Каждого Строка Из НайденныеСтроки Цикл
			Если Строка.ОтражениеВУСН = Перечисления.ОтражениеВУСН.НеПринимаются Тогда
				Сумма = КоэффОбщий * Строка.Сумма;
				Количество = КоэффОбщий * Строка.Количество;
				НДС = КоэффОбщий * Строка.НДС;
			Иначе
				Сумма = КоэффДоходы * Строка.Сумма;
				Количество = КоэффДоходы * Строка.Количество;
				НДС = КоэффДоходы * Строка.НДС;
			КонецЕсли;
			Если Сумма = 0 Тогда
				Продолжить;
			Конецесли;
			НоваяСтрока = ТаблицаДляСписания.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);
			
			НоваяСтрока.Сумма = Сумма;
			НоваяСтрока.Количество = Количество;
			НоваяСтрока.НДС = НДС;
		КонецЦикла;
	КонецЦикла;

	//Разложим таблицу списываемых расходов на движения корректировки статуса и принятые расходы
	ТаблицыДвижений = ИзменениеСтатусовРасходов(СтруктураШапкиДокумента, ТаблицаДляСписания, , "Доход", Ложь);
	
	Если ТаблицаДвиженияВзаиморасчетов.Колонки.Найти("ВидДвижения") = Неопределено Тогда
		ТаблицаДвиженияВзаиморасчетов.Колонки.Добавить("ВидДвижения");
	КонецЕсли;
	ТаблицаДвиженияВзаиморасчетов.ЗаполнитьЗначения(ВидДвиженияНакопления.Расход,"ВидДвижения");
	
	ДвиженияРегистров = СобратьТаблицыДвижений(ТаблицыДвижений, ТаблицаДвиженияВзаиморасчетов,,);
	
КонецПроцедуры

//Отражает перенос задолженности с одного контрагента на другого.
//
Процедура ПереносЗадолженностиУСН(СтруктураШапкиДокумента, ТаблицаОплат, ДвиженияРегистров, ВидДвижения, ИмяДопПоля = "")
	
	Если ВидДвижения = ВидДвиженияНакопления.Приход Тогда
		КЗ = -1; //Кредиторская задолженность
	ИначеЕсли ВидДвижения = ВидДвиженияНакопления.Расход Тогда
		КЗ = 1; //Дебиторская задолженность
	КонецЕсли;	
		
	ВыделятьНДСУСН = (СтруктураШапкиДокумента.ПорядокПризнанияРасходовПоНДС = Перечисления.ПорядокПризнанияРасходовПоНДС.ПоОплатеПоставщику);
		
	//Подготовка наборов фильтров
	МассивСделок = ТаблицаОплат.ВыгрузитьКолонку("Сделка");
	ОбщегоНазначения.УдалитьПовторяющиесяЭлементы(МассивСделок);
	
	//Формирование таблицы фильтров
	ТаблицаФильтров = Новый ТаблицаЗначений;
	ТаблицаФильтров.Колонки.Добавить("ИмяПоля");
	ТаблицаФильтров.Колонки.Добавить("ЗначениеПоля");
	ТаблицаФильтров.Колонки.Добавить("Условие");
	
	Фильтр = ТаблицаФильтров.Добавить();
	Если НЕ ТаблицаОплат.Колонки.Найти("ДоговорКонтрагента") = Неопределено Тогда
		МассивДоговоров = ТаблицаОплат.ВыгрузитьКолонку("ДоговорКонтрагента");
		ТаблицаОплат.Колонки.Добавить("Работник");
		ТаблицаОплат.ЗаполнитьЗначения(Неопределено, "Работник");
		ИмяИзмерения = "ДоговорКонтрагента";
	ИначеЕсли НЕ ТаблицаОплат.Колонки.Найти("Работник") = Неопределено Тогда
		МассивДоговоров = ТаблицаОплат.ВыгрузитьКолонку("Работник");
		ТаблицаОплат.Колонки.Добавить("ДоговорКонтрагента");
		ТаблицаОплат.ЗаполнитьЗначения(Неопределено, "ДоговорКонтрагента");
		ИмяИзмерения = "Работник";
	КонецЕсли;
	ОбщегоНазначения.УдалитьПовторяющиесяЭлементы(МассивДоговоров);
	Фильтр.ИмяПоля			= ИмяИзмерения;
	Фильтр.ЗначениеПоля	= МассивДоговоров;
	Фильтр.Условие			= "В";
	
	Если МассивСделок.Количество() > 0 Тогда
		Фильтр = ТаблицаФильтров.Добавить();
		Фильтр.ИмяПоля		= "Сделка";
		Фильтр.ЗначениеПоля	= МассивСделок;
		Фильтр.Условие		= "В";
	КонецЕсли;	
	
	Если НЕ ЗначениеЗаполнено(СтруктураШапкиДокумента.ДатаОплаты) Тогда
		МоментДокумента = СтруктураШапкиДокумента.Ссылка.МоментВремени();
	Иначе
		МоментДокумента = Новый МоментВремени(СтруктураШапкиДокумента.ДатаОплаты, СтруктураШапкиДокумента.Ссылка);
	КонецЕсли;
	
	ТаблицаПлатежей = ВыполнитьЗапросПоВзаиморасчетамУСН(МоментДокумента, СтруктураШапкиДокумента.Организация, ТаблицаФильтров);
	
	ТаблицаДвиженияВзаиморасчетов = ТаблицаПлатежей.СкопироватьКолонки();
	ТаблицаДвиженияВзаиморасчетов.Колонки.Добавить("СтруктураКурса");
	
	//Заполняем таблицу списываемых расходов по таблице расходов и фильтрам партионного учета
	Для Каждого СтрокаПлатеж Из ТаблицаОплат Цикл
		СтруктураОтбора = Новый Структура;
		Если НЕ ТаблицаОплат.Колонки.Найти("ДоговорКонтрагента") = Неопределено Тогда
			СтруктураОтбора.Вставить("ДоговорКонтрагента",  СтрокаПлатеж.ДоговорКонтрагента);
		ИначеЕсли НЕ ТаблицаОплат.Колонки.Найти("Работник") = Неопределено Тогда
			СтруктураОтбора.Вставить("Работник",  СтрокаПлатеж.Работник);
		КонецЕсли;
		
		Если ТипЗнч(СтрокаПлатеж.ДоговорКонтрагента) = Тип("СправочникСсылка.ДоговорыКонтрагентов") Тогда
			Если СтрокаПлатеж.ДоговорКонтрагента.ВедениеВзаиморасчетов <> Перечисления.ВедениеВзаиморасчетовПоДоговорам.ПоДоговоруВЦелом Тогда
				СтруктураОтбора.Вставить("Сделка", ?(ЗначениеЗаполнено(СтрокаПлатеж.Сделка), СтрокаПлатеж.Сделка, Неопределено));					
			КонецЕсли;
			Если СтрокаПлатеж.ДоговорКонтрагента.ВестиПоДокументамРасчетовСКонтрагентом И ИмяДопПоля = "РасчетныйДокумент" Тогда
				СтруктураОтбора.Вставить("РасчетныйДокумент", ?(ЗначениеЗаполнено(СтрокаПлатеж.РасчетныйДокумент), СтрокаПлатеж.РасчетныйДокумент, Неопределено));
			КонецЕсли;
		КонецЕсли;	
		
		НайденныеСтроки = ТаблицаПлатежей.НайтиСтроки(СтруктураОтбора);
		
		Курс = 1;
		Кратность = 1;
		
		Если Не ТаблицаОплат.Колонки.Найти("КурсВзаиморасчетов") = Неопределено Тогда
			Курс = СтрокаПлатеж.КурсВзаиморасчетов;
		КонецЕсли;
		Если Не ТаблицаОплат.Колонки.Найти("КратностьВзаиморасчетов") = Неопределено Тогда
			Кратность = СтрокаПлатеж.КратностьВзаиморасчетов;
		КонецЕсли;
		СтруктураКурса = Новый Структура("Курс, Кратность",Курс,Кратность);
		
		// Подлежащее погашению при списании количество
		СуммаОсталосьПогасить = СтрокаПлатеж.Сумма;
		Для Каждого Строка Из НайденныеСтроки Цикл
			Если СуммаОсталосьПогасить <= 0 Тогда
				Прервать;
			КонецЕсли;
			Если КЗ*(Строка.СуммаВзаиморасчетов)<= 0 Тогда
				Продолжить;
			КонецЕсли;
			
			СуммаСписания = Мин(КЗ*(Строка.СуммаВзаиморасчетов), СуммаОсталосьПогасить);
			СуммаОсталосьПогасить = СуммаОсталосьПогасить - СуммаСписания;
			
			НоваяСтрока = ТаблицаДвиженияВзаиморасчетов.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);
			
			Если КЗ*(Строка.СуммаВзаиморасчетов) > СуммаСписания Тогда
				НоваяСтрока.СуммаВзаиморасчетов = Мин(КЗ*(Строка.СуммаВзаиморасчетов), СуммаСписания);
				НоваяСтрока.ДоходЕНВД	 		= Мин(КЗ*(Строка.ДоходЕНВД), НоваяСтрока.СуммаВзаиморасчетов);
				НоваяСтрока.ДоходКомитента 		= Мин(КЗ*(Строка.ДоходКомитента), НоваяСтрока.СуммаВзаиморасчетов - НоваяСтрока.ДоходЕНВД);
			КонецЕсли;
			
			НоваяСтрока.СтруктураКурса = СтруктураКурса;
		КонецЦикла;
		
		Если  СуммаОсталосьПогасить > 0 Тогда
			ДвижениеВзаиморасчетовУСН(ТаблицаДвиженияВзаиморасчетов, СтруктураШапкиДокумента.Организация, СтрокаПлатеж.ДоговорКонтрагента, СтруктураШапкиДокумента.Ссылка, СтрокаПлатеж.Сделка, СуммаОсталосьПогасить, 0, 0);
		КонецЕсли;
	КонецЦикла;
	
	Если ТаблицаДвиженияВзаиморасчетов.Колонки.Найти("ВидДвижения") = Неопределено Тогда
		ТаблицаДвиженияВзаиморасчетов.Колонки.Добавить("ВидДвижения");
	КонецЕсли;
	ТаблицаДвиженияВзаиморасчетов.ЗаполнитьЗначения(ВидДвижения,"ВидДвижения");
	
	//Отразим в таблице долг "новому" контрагенту.
	Если ВидДвижения = ВидДвиженияНакопления.Расход Тогда
		НовыйВидДвижения = ВидДвиженияНакопления.Приход;
	ИначеЕсли ВидДвижения = ВидДвиженияНакопления.Приход Тогда
		НовыйВидДвижения = ВидДвиженияНакопления.Расход;
	КонецЕсли;	
		
	ТаблицаВзаиморасчетов = ТаблицаДвиженияВзаиморасчетов.Скопировать();
	ТаблицаВзаиморасчетов.ЗаполнитьЗначения(СтруктураШапкиДокумента.ДоговорКонтрагента,"ДоговорКонтрагента");
    ТаблицаВзаиморасчетов.ЗаполнитьЗначения(НовыйВидДвижения,"ВидДвижения");
	ОбщегоНазначения.ЗагрузитьВТаблицуЗначений(ТаблицаВзаиморасчетов,ТаблицаДвиженияВзаиморасчетов);
	
	ТаблицыДвижений = ИзменениеСтатусовРасходов(СтруктураШапкиДокумента, Новый ТаблицаЗначений, , Неопределено, Ложь);

	ДвиженияРегистров = СобратьТаблицыДвижений(ТаблицыДвижений, ТаблицаДвиженияВзаиморасчетов,,);
    	
Конецпроцедуры	

//Отражает списание задолженности
Процедура СписаниеЗадолженностиУСН(СтруктураШапкиДокумента, ТаблицаОплат, ДвиженияРегистров, КУДиР, ВидДвижения)
	
	Если ВидДвижения = ВидДвиженияНакопления.Приход Тогда
		КЗ = -1; //Кредиторская задолженность
	ИначеЕсли ВидДвижения = ВидДвиженияНакопления.Расход Тогда
		КЗ = 1; //Дебиторская задолженность
	КонецЕсли;	
		
	ВыделятьНДСУСН = (СтруктураШапкиДокумента.ПорядокПризнанияРасходовПоНДС = Перечисления.ПорядокПризнанияРасходовПоНДС.ПоОплатеПоставщику);
		
	//Подготовка наборов фильтров
	МассивСделок = ТаблицаОплат.ВыгрузитьКолонку("Сделка");
	ОбщегоНазначения.УдалитьПовторяющиесяЭлементы(МассивСделок);
	
	//Формирование таблицы фильтров
	ТаблицаФильтров = Новый ТаблицаЗначений;
	ТаблицаФильтров.Колонки.Добавить("ИмяПоля");
	ТаблицаФильтров.Колонки.Добавить("ЗначениеПоля");
	ТаблицаФильтров.Колонки.Добавить("Условие");
	
	Фильтр = ТаблицаФильтров.Добавить();
	Если НЕ ТаблицаОплат.Колонки.Найти("ДоговорКонтрагента") = Неопределено Тогда
		МассивДоговоров = ТаблицаОплат.ВыгрузитьКолонку("ДоговорКонтрагента");
		ТаблицаОплат.Колонки.Добавить("Работник");
		ТаблицаОплат.ЗаполнитьЗначения(Неопределено, "Работник");
		ИмяИзмерения = "ДоговорКонтрагента";
	ИначеЕсли НЕ ТаблицаОплат.Колонки.Найти("Работник") = Неопределено Тогда
		МассивДоговоров = ТаблицаОплат.ВыгрузитьКолонку("Работник");
		ТаблицаОплат.Колонки.Добавить("ДоговорКонтрагента");
		ТаблицаОплат.ЗаполнитьЗначения(Неопределено, "ДоговорКонтрагента");
		ИмяИзмерения = "Работник";
	КонецЕсли;
	ОбщегоНазначения.УдалитьПовторяющиесяЭлементы(МассивДоговоров);
	Фильтр.ИмяПоля			= ИмяИзмерения;
	Фильтр.ЗначениеПоля	= МассивДоговоров;
	Фильтр.Условие			= "В";
	
	Если МассивСделок.Количество() > 0 Тогда
		Фильтр = ТаблицаФильтров.Добавить();
		Фильтр.ИмяПоля		= "Сделка";
		Фильтр.ЗначениеПоля	= МассивСделок;
		Фильтр.Условие		= "В";
	КонецЕсли;	
	
	Если НЕ ЗначениеЗаполнено(СтруктураШапкиДокумента.ДатаОплаты) Тогда
		МоментДокумента = СтруктураШапкиДокумента.Ссылка.МоментВремени();
	Иначе
		МоментДокумента = Новый МоментВремени(СтруктураШапкиДокумента.ДатаОплаты, СтруктураШапкиДокумента.Ссылка);
	КонецЕсли;
	
	ТаблицаПлатежей = ВыполнитьЗапросПоВзаиморасчетамУСН(МоментДокумента, СтруктураШапкиДокумента.Организация, ТаблицаФильтров);
	
	ТаблицаДвиженияВзаиморасчетов = ТаблицаПлатежей.СкопироватьКолонки();
	ТаблицаДвиженияВзаиморасчетов.Колонки.Добавить("СтруктураКурса");
	
	//Заполняем таблицу списываемых расходов по таблице расходов и фильтрам партионного учета
	Для Каждого СтрокаПлатеж Из ТаблицаОплат Цикл
		СтруктураОтбора = Новый Структура;
		Если НЕ ТаблицаОплат.Колонки.Найти("ДоговорКонтрагента") = Неопределено Тогда
			СтруктураОтбора.Вставить("ДоговорКонтрагента",  СтрокаПлатеж.ДоговорКонтрагента);
		ИначеЕсли НЕ ТаблицаОплат.Колонки.Найти("Работник") = Неопределено Тогда
			СтруктураОтбора.Вставить("Работник",  СтрокаПлатеж.Работник);
		КонецЕсли;
		
		Если ТипЗнч(СтрокаПлатеж.ДоговорКонтрагента) = Тип("СправочникСсылка.ДоговорыКонтрагентов") Тогда
			Если СтрокаПлатеж.ДоговорКонтрагента.ВедениеВзаиморасчетов <> Перечисления.ВедениеВзаиморасчетовПоДоговорам.ПоДоговоруВЦелом Тогда
				СтруктураОтбора.Вставить("Сделка", ?(ЗначениеЗаполнено(СтрокаПлатеж.Сделка), СтрокаПлатеж.Сделка, Неопределено));					
			КонецЕсли;
			Если СтрокаПлатеж.ДоговорКонтрагента.ВестиПоДокументамРасчетовСКонтрагентом Тогда
				СтруктураОтбора.Вставить("РасчетныйДокумент", ?(ЗначениеЗаполнено(СтрокаПлатеж.РасчетныйДокумент), СтрокаПлатеж.РасчетныйДокумент, Неопределено));
			КонецЕсли;
		КонецЕсли;
				
		НайденныеСтроки = ТаблицаПлатежей.НайтиСтроки(СтруктураОтбора);
		
		Курс = 1;
		Кратность = 1;
		
		Если Не ТаблицаОплат.Колонки.Найти("КурсВзаиморасчетов") = Неопределено Тогда
			Курс = СтрокаПлатеж.КурсВзаиморасчетов;
		КонецЕсли;
		Если Не ТаблицаОплат.Колонки.Найти("КратностьВзаиморасчетов") = Неопределено Тогда
			Кратность = СтрокаПлатеж.КратностьВзаиморасчетов;
		КонецЕсли;
		СтруктураКурса = Новый Структура("Курс, Кратность",Курс,Кратность);
		
		// Подлежащее погашению при списании количество
		СуммаОсталосьПогасить = СтрокаПлатеж.Сумма;
		Для Каждого Строка Из НайденныеСтроки Цикл
			Если СуммаОсталосьПогасить <= 0 Тогда
				Прервать;
			КонецЕсли;
			Если КЗ*(Строка.СуммаВзаиморасчетов)<= 0 Тогда
				Продолжить;
			КонецЕсли;
			
			СуммаСписания = Мин(КЗ*(Строка.СуммаВзаиморасчетов), СуммаОсталосьПогасить);
			СуммаОсталосьПогасить = СуммаОсталосьПогасить - СуммаСписания;
			
			НоваяСтрока = ТаблицаДвиженияВзаиморасчетов.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);
			
			Если КЗ*(Строка.СуммаВзаиморасчетов) > СуммаСписания Тогда
				НоваяСтрока.СуммаВзаиморасчетов = Мин(КЗ*(Строка.СуммаВзаиморасчетов), СуммаСписания);
				НоваяСтрока.ДоходЕНВД	 		= Мин(КЗ*(Строка.ДоходЕНВД), НоваяСтрока.СуммаВзаиморасчетов);
				НоваяСтрока.ДоходКомитента 		= Мин(КЗ*(Строка.ДоходКомитента), НоваяСтрока.СуммаВзаиморасчетов - НоваяСтрока.ДоходЕНВД);
			Иначе
				НоваяСтрока.СуммаВзаиморасчетов = КЗ*НоваяСтрока.СуммаВзаиморасчетов;
				НоваяСтрока.ДоходЕНВД	 		= КЗ*НоваяСтрока.ДоходЕНВД;
				НоваяСтрока.ДоходКомитента 		= КЗ*НоваяСтрока.ДоходКомитента;
			КонецЕсли;
			
			Если  ВидДвижения = ВидДвиженияНакопления.Приход Тогда
				КУДиР.Графа4 = КУДиР.Графа4 + (НоваяСтрока.СуммаВзаиморасчетов)*Курс/Кратность;
			КонецЕсли;
			
			НоваяСтрока.СтруктураКурса = СтруктураКурса;
		КонецЦикла;
		
		Если  СуммаОсталосьПогасить > 0 Тогда
			ОбщегоНазначения.СообщитьОбОшибке("Сумма по договору " + СтрокаПлатеж.ДоговорКонтрагента + " превышает остаток по взаиморасчетам");
		КонецЕсли;
	КонецЦикла;
	
	Если ТаблицаДвиженияВзаиморасчетов.Колонки.Найти("ВидДвижения") = Неопределено Тогда
		ТаблицаДвиженияВзаиморасчетов.Колонки.Добавить("ВидДвижения");
	КонецЕсли;
	ТаблицаДвиженияВзаиморасчетов.ЗаполнитьЗначения(ВидДвижения,"ВидДвижения");

	КЗ = 1; 
	
	МассивПартий = ТаблицаДвиженияВзаиморасчетов.ВыгрузитьКолонку("РасчетныйДокумент");
	ОбщегоНазначения.УдалитьПовторяющиесяЭлементы(МассивПартий);
	
	// По регистру Расходы УСН
	Статусы = Новый Массив;
	Статусы.Добавить(Перечисления.СтатусыРасходовУСН.НеОплачено);
	Статусы.Добавить(Перечисления.СтатусыРасходовУСН.НеОплаченоНеОплаченоПокупателем);
	Статусы.Добавить(Перечисления.СтатусыРасходовУСН.НеСписаноНеОплачено);
	Статусы.Добавить(Перечисления.СтатусыРасходовУСН.НеОплаченоПокупателем);
		
	//Формирование таблицы фильтров
	ТаблицаФильтров = Новый ТаблицаЗначений;
	ТаблицаФильтров.Колонки.Добавить("ИмяПоля");
	ТаблицаФильтров.Колонки.Добавить("ЗначениеПоля");
	ТаблицаФильтров.Колонки.Добавить("Условие");
	
	Фильтр = ТаблицаФильтров.Добавить();
	Фильтр.ИмяПоля			= "РасчетныйДокумент";
	Фильтр.ЗначениеПоля	= МассивПартий;
	Фильтр.Условие			= "В";
	
	Фильтр = ТаблицаФильтров.Добавить();
	Фильтр.ИмяПоля			= "СтатусыОплатыРасходовУСН";
	Фильтр.ЗначениеПоля	= Статусы;
	Фильтр.Условие			= "В";
	
	//Из таблицы расходов выбираем соответствующие партии и элементы расходов
	ТаблицаРасходов = ВыполнитьЗапросПоРасходам(Новый Граница(СтруктураШапкиДокумента.Ссылка.МоментВремени(), ВидГраницы.Включая), СтруктураШапкиДокумента.Организация, ТаблицаФильтров);
	
	ТаблицаДвиженияРасходов = ТаблицаРасходов.СкопироватьКолонки();
	ТаблицаДвиженияРасходов.Колонки.Добавить("СтруктураКурса");
	
	//Заполняем таблицу списываемых расходов по таблице расходов и фильтрам партионного учета
	Для Каждого СтрокаПлатеж Из ТаблицаДвиженияВзаиморасчетов Цикл
		СтруктураОтбора = Новый Структура;
		СтруктураОтбора.Вставить("РасчетныйДокумент",  СтрокаПлатеж.РасчетныйДокумент);
		
		НайденныеСтроки = ТаблицаРасходов.НайтиСтроки(СтруктураОтбора);
		
		// Подлежащее погашению при списании количество
		СуммаОсталосьПогасить = СтрокаПлатеж.СуммаВзаиморасчетов;
		Для Каждого Строка Из НайденныеСтроки Цикл
			Если СуммаОсталосьПогасить <= 0 Тогда
				Прервать;
			КонецЕсли;
			Если КЗ*(Строка.Сумма)<= 0 Тогда
				Продолжить;
			КонецЕсли;
			
			СуммаСписания = Мин(КЗ*(Строка.Сумма), СуммаОсталосьПогасить);
			СуммаОсталосьПогасить = СуммаОсталосьПогасить - СуммаСписания;
			
			НоваяСтрока = ТаблицаДвиженияРасходов.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);
			
			Если КЗ*(Строка.Сумма) > СуммаСписания Тогда
				НоваяСтрока.Сумма				= Мин(КЗ*(Строка.Сумма), СуммаСписания);
				НоваяСтрока.Количество	 		= СуммаСписания/НоваяСтрока.Сумма*Строка.Количество;
				НоваяСтрока.НДС			 		= СуммаСписания/НоваяСтрока.Сумма*Строка.НДС;
			Иначе
				НоваяСтрока.Сумма 				= КЗ*(Строка.Сумма);
				НоваяСтрока.Количество	 		= Строка.Количество;
				НоваяСтрока.НДС 				= Строка.НДС;
			КонецЕсли;
			НоваяСтрока.СтруктураКурса = СтруктураКурса;
		КонецЦикла;
	КонецЦикла;
	
	Если ТаблицаДвиженияРасходов.Колонки.Найти("ВидДвижения") = Неопределено Тогда
		ТаблицаДвиженияРасходов.Колонки.Добавить("ВидДвижения");
	КонецЕсли;
	ТаблицаДвиженияРасходов.ЗаполнитьЗначения(ВидДвижения,"ВидДвижения");

	//КУДиР.Содержание = "Доходы от списания кредиторской задолженности";
	
	ТаблицаРасходов.Очистить();
	
    ТаблицыДвижений = ИзменениеСтатусовРасходов(СтруктураШапкиДокумента, ТаблицаРасходов, , Неопределено, Ложь);
	ТаблицыДвижений.Вставить("ТаблицаРасход",ТаблицаДвиженияРасходов);
	
	ДвиженияРегистров = СобратьТаблицыДвижений(ТаблицыДвижений, ТаблицаДвиженияВзаиморасчетов,,);
	
КонецПроцедуры	

//Начисление ЗП документом отражение зарплаты в регл. учете
//
Процедура НачислениеЗП(СтруктураШапкиДокумента, ДвиженияРегистров, КУДиР) //
	
	МассивСчетов = Новый Массив;
	МассивСчетов.Добавить(ПланыСчетов.Хозрасчетный.РасчетыПоНалогам);
	МассивСчетов.Добавить(ПланыСчетов.Хозрасчетный.РасчетыПоСоциальномуСтрахованию);
	
	//~~//
	
	ПериодРегистрации = СтруктураШапкиДокумента.Ссылка.ПериодРегистрации;
	ДатаНач = НачалоМесяца(ПериодРегистрации);
	ДатаКон = СтруктураШапкиДокумента.Дата;
	
	//Определим период обработки сдельной зарплаты
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Истина", Истина);
	Запрос.УстановитьПараметр("ПериодРегистрации", 	ПериодРегистрации);
	Запрос.УстановитьПараметр("Организация", 		СтруктураШапкиДокумента.Организация);
	Запрос.УстановитьПараметр("Дата", 	 			СтруктураШапкиДокумента.Дата);
	Запрос.УстановитьПараметр("ДатаКон", 	 		ДатаКон);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	МАКСИМУМ(ОтражениеЗарплатыВРеглУчете.Дата) КАК Дата
	|ИЗ
	|	Документ.ОтражениеЗарплатыВРеглУчете КАК ОтражениеЗарплатыВРеглУчете
	|ГДЕ
	|	ОтражениеЗарплатыВРеглУчете.Проведен = &Истина
	|	И ОтражениеЗарплатыВРеглУчете.ПериодРегистрации = &ПериодРегистрации
	|	И ОтражениеЗарплатыВРеглУчете.Организация = &Организация
	|	И ОтражениеЗарплатыВРеглУчете.Дата < &Дата";
	
	Выборка = Запрос.Выполнить().Выбрать();

	Если Выборка.Следующий() тогда
		Если ЗначениеЗаполнено(Выборка.Дата) Тогда
			ДатаНач = Мин(КонецДня(Выборка.Дата)+1, ДатаКон);
		КонецЕсли;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Счет70", 		ПланыСчетов.Хозрасчетный.РасчетыСПерсоналомПоОплатеТруда);
	Запрос.УстановитьПараметр("СчетаНалогов", 	МассивСчетов);
	Запрос.УстановитьПараметр("Ссылка", 		СтруктураШапкиДокумента.Ссылка);
	Запрос.УстановитьПараметр("Зарплата", 		Перечисления.ВидыРасходовУСН.Зарплата);
	Запрос.УстановитьПараметр("Налоги", 		Перечисления.ВидыРасходовУСН.Налоги);
	Запрос.УстановитьПараметр("Сводно", 		Справочники.ФизическиеЛица.ПустаяСсылка());
	Запрос.УстановитьПараметр("Неопределено", 	Неопределено);
	Запрос.УстановитьПараметр("Организация", 	СтруктураШапкиДокумента.Организация);
	Запрос.УстановитьПараметр("ДатаНач", 	 	ДатаНач);
	Запрос.УстановитьПараметр("ДатаКон", 	 	ДатаКон);
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	&Зарплата КАК ВидРасхода,
	|	ОтражениеЗарплатыВРеглУчетеОтражениеВУчете.СубконтоКт1 КАК ЭлементРасхода,
	|	&Неопределено КАК СчетУчета,
	|	ОтражениеЗарплатыВРеглУчетеОтражениеВУчете.Сумма КАК Сумма,
	|	ОтражениеЗарплатыВРеглУчетеОтражениеВУчете.ОтражениеВУСН КАК ОтражениеВУСН
	|ИЗ
	|	Документ.ОтражениеЗарплатыВРеглУчете.ОтражениеВУчете КАК ОтражениеЗарплатыВРеглУчетеОтражениеВУчете
	|ГДЕ
	|	ОтражениеЗарплатыВРеглУчетеОтражениеВУчете.Ссылка = &Ссылка
	|	И ОтражениеЗарплатыВРеглУчетеОтражениеВУчете.СчетКт В ИЕРАРХИИ(&Счет70)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&Зарплата,
	|	БУОсновныеНачисления.ФизЛицо,
	|	&Неопределено,
	|	БУОсновныеНачисления.Результат,
	|	БУОсновныеНачисления.ОтражениеВУСН
	|ИЗ
	|	РегистрРасчета.БУОсновныеНачисления КАК БУОсновныеНачисления
	|ГДЕ
	|	БУОсновныеНачисления.Организация = &Организация
	|	И БУОсновныеНачисления.Регистратор.Дата МЕЖДУ &ДатаНач И &ДатаКон
	|	И (НЕ БУОсновныеНачисления.Регистратор Ссылка Документ.ОтражениеЗарплатыВРеглУчете)
	|	И (НЕ БУОсновныеНачисления.Регистратор Ссылка Документ.НачислениеПоБольничномуЛисту)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&Зарплата,
	|	ОтражениеЗарплатыВРеглУчетеОтражениеВУчете.СубконтоДт1,
	|	&Неопределено,
	|	-ОтражениеЗарплатыВРеглУчетеОтражениеВУчете.Сумма,
	|	ОтражениеЗарплатыВРеглУчетеОтражениеВУчете.ОтражениеВУСН
	|ИЗ
	|	Документ.ОтражениеЗарплатыВРеглУчете.ОтражениеВУчете КАК ОтражениеЗарплатыВРеглУчетеОтражениеВУчете
	|ГДЕ
	|	ОтражениеЗарплатыВРеглУчетеОтражениеВУчете.Ссылка = &Ссылка
	|	И ОтражениеЗарплатыВРеглУчетеОтражениеВУчете.СчетДт В ИЕРАРХИИ(&Счет70)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&Налоги,
	|	&Неопределено,
	|	ОтражениеЗарплатыВРеглУчетеОтражениеВУчете.СчетКт,
	|	ОтражениеЗарплатыВРеглУчетеОтражениеВУчете.Сумма,
	|	ОтражениеЗарплатыВРеглУчетеОтражениеВУчете.ОтражениеВУСН
	|ИЗ
	|	Документ.ОтражениеЗарплатыВРеглУчете.ОтражениеВУчете КАК ОтражениеЗарплатыВРеглУчетеОтражениеВУчете
	|ГДЕ
	|	ОтражениеЗарплатыВРеглУчетеОтражениеВУчете.Ссылка = &Ссылка
	|	И ОтражениеЗарплатыВРеглУчетеОтражениеВУчете.СчетКт В ИЕРАРХИИ(&СчетаНалогов)";
	
	//~~\\
	
	ТаблицаРасходов = Запрос.Выполнить().Выгрузить();
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ОтражениеЗарплатыВРеглУчетеОтражениеВУчете.СчетДт КАК СчетУчета,
	|	СУММА(ОтражениеЗарплатыВРеглУчетеОтражениеВУчете.Сумма) КАК Сумма
	|ИЗ
	|	Документ.ОтражениеЗарплатыВРеглУчете.ОтражениеВУчете КАК ОтражениеЗарплатыВРеглУчетеОтражениеВУчете
	|ГДЕ
	|	ОтражениеЗарплатыВРеглУчетеОтражениеВУчете.Ссылка = &Ссылка
	|	И ОтражениеЗарплатыВРеглУчетеОтражениеВУчете.СчетДт В ИЕРАРХИИ(&СчетаНалогов)
	|
	|СГРУППИРОВАТЬ ПО
	|	ОтражениеЗарплатыВРеглУчетеОтражениеВУчете.СчетДт";
	ТаблицаЗачетаНалогов = Запрос.Выполнить().Выгрузить();
	ТаблицаЗачетаНалогов.Индексы.Добавить("СчетУчета");
	
	//Обработать удержания с незаполненным порядком признания расходов
	
	КоличествоСтрок = ТаблицаРасходов.Количество();
	Для НС = 0 по КоличествоСтрок - 1 Цикл
		
		ТекСтрока = ТаблицаРасходов[НС];
		
		Если (ТекСтрока.ВидРасхода = Перечисления.ВидыРасходовУСН.Зарплата) И
			(ТекСтрока.Сумма < 0) И
			(НЕ ЗначениеЗаполнено(ТекСтрока.ОтражениеВУСН)) Тогда
			
			СуммаКРаспределению = ТекСтрока.Сумма;
			
			//Распределим удержание
			СтруктураОтбора = Новый Структура;
			СтруктураОтбора.Вставить("ВидРасхода",  	ТекСтрока.ВидРасхода);
			СтруктураОтбора.Вставить("ЭлементРасхода", 	ТекСтрока.ЭлементРасхода);
								
			НайденныеСтроки = ТаблицаРасходов.НайтиСтроки(СтруктураОтбора);
			
			БазаПринимаемых = 0;
			БазаНеПринимаемых = 0;
			Для Каждого НайденнаяСтрока ИЗ НайденныеСтроки Цикл
				
				Если НайденнаяСтрока.Сумма < 0 Тогда
					Продолжить;
				КонецЕсли;
				
				Если НайденнаяСтрока.ОтражениеВУСН = Перечисления.ОтражениеВУСН.Принимаются Тогда
					БазаПринимаемых = БазаПринимаемых + НайденнаяСтрока.Сумма;
				Иначе
					БазаНеПринимаемых = БазаНеПринимаемых + НайденнаяСтрока.Сумма;
				КонецЕсли;
				
			КонецЦикла;
			
			СуммаПринимаемых = ?(БазаПринимаемых = 0, 0, СуммаКРаспределению * БазаПринимаемых / (БазаПринимаемых + БазаНеПринимаемых));
			СуммаНеПринимаемых = СуммаКРаспределению - СуммаПринимаемых;
			
			Если СуммаПринимаемых = 0 Тогда
				ТекСтрока.ОтражениеВУСН = Перечисления.ОтражениеВУСН.НеПринимаются;
			ИначеЕсли СуммаНеПринимаемых = 0 Тогда
				ТекСтрока.ОтражениеВУСН = Перечисления.ОтражениеВУСН.Принимаются;
			ИначеЕсли НЕ СуммаКРаспределению = 0 Тогда
				ТекСтрока.ОтражениеВУСН = Перечисления.ОтражениеВУСН.Принимаются;
				ТекСтрока.Сумма = СуммаПринимаемых;
				
				НоваяСтрока 				= ТаблицаРасходов.Добавить();
				НоваяСтрока.ВидРасхода 		= ТекСтрока.ВидРасхода;
				НоваяСтрока.ЭлементРасхода	= ТекСтрока.ЭлементРасхода;
				НоваяСтрока.СчетУчета 		= ТекСтрока.СчетУчета;
				НоваяСтрока.ОтражениеВУСН 	= Перечисления.ОтражениеВУСН.НеПринимаются;
				НоваяСтрока.Сумма	 		= СуммаНеПринимаемых;
			КонецЕсли;
			
			//Если это НДФЛ - ищем среди налогов
			
			СтруктураОтбора = Новый Структура;
			СтруктураОтбора.Вставить("ВидРасхода",  	Перечисления.ВидыРасходовУСН.Налоги);
			СтруктураОтбора.Вставить("ЭлементРасхода", 	Неопределено);
			СтруктураОтбора.Вставить("СчетУчета", 		ПланыСчетов.Хозрасчетный.НДФЛ);
			СтруктураОтбора.Вставить("ОтражениеВУСН",  	Перечисления.ОтражениеВУСН.ПустаяСсылка());
			СтруктураОтбора.Вставить("Сумма", 			- СуммаКРаспределению);
								
			НайденныеСтроки = ТаблицаРасходов.НайтиСтроки(СтруктураОтбора);
			
			Если НайденныеСтроки.Количество() > 0 Тогда
				СтрокаНДС = НайденныеСтроки[0];
				
				Если СуммаПринимаемых = 0 Тогда
					СтрокаНДС.ОтражениеВУСН = Перечисления.ОтражениеВУСН.НеПринимаются;
				ИначеЕсли СуммаНеПринимаемых = 0 Тогда
					СтрокаНДС.ОтражениеВУСН = Перечисления.ОтражениеВУСН.Принимаются;
				ИначеЕсли НЕ СуммаКРаспределению = 0 Тогда
					СтрокаНДС.ОтражениеВУСН = Перечисления.ОтражениеВУСН.Принимаются;
					СтрокаНДС.Сумма = - СуммаПринимаемых;
					
					НоваяСтрока 				= ТаблицаРасходов.Добавить();
					НоваяСтрока.ВидРасхода 		= СтрокаНДС.ВидРасхода;
					НоваяСтрока.ЭлементРасхода	= СтрокаНДС.ЭлементРасхода;
					НоваяСтрока.СчетУчета 		= СтрокаНДС.СчетУчета;
					НоваяСтрока.ОтражениеВУСН 	= Перечисления.ОтражениеВУСН.НеПринимаются;
					НоваяСтрока.Сумма	 		= - СуммаНеПринимаемых;
				КонецЕсли;
				
			КонецЕсли;
						
		ИначеЕсли (ТекСтрока.ВидРасхода = Перечисления.ВидыРасходовУСН.Зарплата) И
			(НЕ ЗначениеЗаполнено(ТекСтрока.ОтражениеВУСН)) Тогда
			
			ТекСтрока.ОтражениеВУСН = Перечисления.ОтражениеВУСН.НеПринимаются;
			
		КонецЕсли;
		
	КонецЦикла;
	
	ТаблицаРасходов.Свернуть("ВидРасхода, ЭлементРасхода, СчетУчета, ОтражениеВУСН", "Сумма");
	ТаблицаРасходов.Колонки.Добавить("Валюта");
	ТаблицаРасходов.Колонки.Добавить("РасчетныйДокумент");
	ТаблицаРасходов.Колонки.Добавить("СтатусыОплатыРасходовУСН");
	ТаблицаРасходов.Колонки.Добавить("НДС");
	
	ТаблицаРасходов.ЗаполнитьЗначения(СтруктураШапкиДокумента.ВалютаВзаиморасчетов, "Валюта");
	ТаблицаРасходов.ЗаполнитьЗначения(СтруктураШапкиДокумента.Ссылка, "РасчетныйДокумент");
	ТаблицаРасходов.ЗаполнитьЗначения(Перечисления.СтатусыРасходовУСН.НеОплачено, "СтатусыОплатыРасходовУСН");
	ТаблицаРасходов.ЗаполнитьЗначения(0, "НДС");
	
	МассивРаботников = ТаблицаРасходов.ВыгрузитьКолонку("ЭлементРасхода");
	ОбщегоНазначения.УдалитьПовторяющиесяЭлементы(МассивРаботников);
	
	//Формирование таблицы фильтров
	ТаблицаФильтров = Новый ТаблицаЗначений;
	ТаблицаФильтров.Колонки.Добавить("ИмяПоля");
	ТаблицаФильтров.Колонки.Добавить("ЗначениеПоля");
	ТаблицаФильтров.Колонки.Добавить("Условие");
	
	//Расчеты с работниками
	Фильтр = ТаблицаФильтров.Добавить();
	Фильтр.ИмяПоля			= "Работник";
	Фильтр.ЗначениеПоля	= МассивРаботников;
	Фильтр.Условие			= "В";
	
	ТаблицаПлатежей = ВыполнитьЗапросПоВзаиморасчетамУСН(Новый Граница(СтруктураШапкиДокумента.Ссылка.МоментВремени(), ВидГраницы.Исключая), СтруктураШапкиДокумента.Организация, ТаблицаФильтров);
	
	ТаблицаФильтров.Очистить();
	
	МассивСчетов = ТаблицаРасходов.ВыгрузитьКолонку("СчетУчета");
	ОбщегоНазначения.УдалитьПовторяющиесяЭлементы(МассивСчетов);
	
	//Расчеты по налогам
	Фильтр = ТаблицаФильтров.Добавить();
	Фильтр.ИмяПоля			= "ДоговорКонтрагента";
	Фильтр.ЗначениеПоля		= МассивСчетов;
	Фильтр.Условие			= "В";
	
	ТаблицаПлатежейНалогов = ВыполнитьЗапросПоВзаиморасчетамУСН(Новый Граница(СтруктураШапкиДокумента.Ссылка.МоментВремени(), ВидГраницы.Исключая), СтруктураШапкиДокумента.Организация, ТаблицаФильтров);
	
	ТаблицаДвиженияВзаиморасчетов 	= ТаблицаПлатежей.СкопироватьКолонки();
	ТаблицаДвиженияРасходов 		= ТаблицаРасходов.СкопироватьКолонки();
	ТаблицаДвиженияРасходов.Колонки.Добавить("КлючУникальности");
	
	//Заполняем таблицу списываемых расходов по таблице расходов
	Для Каждого Расход Из ТаблицаРасходов Цикл
		
		СуммаОсталосьПогасить = Расход.Сумма;
		
		//Учтем начисления за счет ФСС
		Если Расход.ВидРасхода = Перечисления.ВидыРасходовУСН.Налоги
			И ТаблицаЗачетаНалогов.Количество() > 0 Тогда
			
			НайденныеСтроки = ТаблицаЗачетаНалогов.НайтиСтроки(Новый Структура("СчетУчета",  Расход.СчетУчета));
			
			Для Каждого Строка Из НайденныеСтроки Цикл
				Если СуммаОсталосьПогасить <= 0 Тогда
					Прервать;
				КонецЕсли;
				Если Строка.Сумма <= 0 Тогда
					Продолжить;
				КонецЕсли;
				
				СуммаСписания = Мин(Строка.Сумма, СуммаОсталосьПогасить);
				СуммаОсталосьПогасить = СуммаОсталосьПогасить - СуммаСписания;
				
				НоваяСтрокаРасходов = ТаблицаДвиженияРасходов.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрокаРасходов, Расход);
				
				НоваяСтрокаРасходов.КлючУникальности = Расход.СчетУчета;
				НоваяСтрокаРасходов.Сумма = Мин(Расход.Сумма, СуммаСписания);
				
				Строка.Сумма = Строка.Сумма - СуммаСписания;
			КонецЦикла;
		КонецЕсли;
		
		СтруктураОтбора = Новый Структура;
		Если Расход.ВидРасхода = Перечисления.ВидыРасходовУСН.Зарплата Тогда
			СтруктураОтбора.Вставить("Работник",  Расход.ЭлементРасхода);
			НайденныеСтроки = ТаблицаПлатежей.НайтиСтроки(СтруктураОтбора);
		Иначе
			СтруктураОтбора.Вставить("ДоговорКонтрагента",  Расход.СчетУчета);
			НайденныеСтроки = ТаблицаПлатежейНалогов.НайтиСтроки(СтруктураОтбора);
		КонецЕсли;
				
		Для Каждого Строка Из НайденныеСтроки Цикл
			Если СуммаОсталосьПогасить <= 0 Тогда
				Прервать;
			КонецЕсли;
			Если Строка.СуммаВзаиморасчетов <= 0 Тогда
				Продолжить;
			КонецЕсли;
			
			СуммаСписания = Мин(Строка.СуммаВзаиморасчетов, СуммаОсталосьПогасить);
			СуммаОсталосьПогасить = СуммаОсталосьПогасить - СуммаСписания;
			
			НоваяСтрока = ТаблицаДвиженияВзаиморасчетов.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);
			
			НоваяСтрокаРасходов = ТаблицаДвиженияРасходов.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрокаРасходов, Расход);
			
			Если НЕ Расход.ВидРасхода = Перечисления.ВидыРасходовУСН.Зарплата Тогда
				НоваяСтрокаРасходов.КлючУникальности = Расход.СчетУчета;
			КонецЕсли;
			
			НоваяСтрока.ДоходЕНВД	 		= 0;
			НоваяСтрока.ДоходКомитента 		= 0;
			НоваяСтрока.СуммаВзаиморасчетов = Мин(Строка.СуммаВзаиморасчетов, СуммаСписания);
			НоваяСтрокаРасходов.Сумма = Мин(Расход.Сумма, СуммаСписания);
			
			Строка.СуммаВзаиморасчетов = Строка.СуммаВзаиморасчетов - СуммаСписания;
			
		КонецЦикла;
		Если СуммаОсталосьПогасить > 0 Тогда
			Если Расход.ВидРасхода = Перечисления.ВидыРасходовУСН.Зарплата Тогда
				ДвижениеВзаиморасчетовУСН(ТаблицаДвиженияВзаиморасчетов, СтруктураШапкиДокумента.Организация, Неопределено, СтруктураШапкиДокумента.Ссылка, Неопределено, СуммаОсталосьПогасить, , ,Расход.ЭлементРасхода);
			Иначе
				ДвижениеВзаиморасчетовУСН(ТаблицаДвиженияВзаиморасчетов, СтруктураШапкиДокумента.Организация, Расход.СчетУчета, СтруктураШапкиДокумента.Ссылка, Неопределено, СуммаОсталосьПогасить);
			КонецЕсли;
		КонецЕсли;
		
	КонецЦикла;
	
	ТаблицыДвиженийРез = ИзменениеСтатусовРасходов(СтруктураШапкиДокумента, ТаблицаДвиженияРасходов, , "Оплата", Ложь);
			
	Если ТаблицаДвиженияВзаиморасчетов.Колонки.Найти("ВидДвижения") = Неопределено Тогда
		ТаблицаДвиженияВзаиморасчетов.Колонки.Добавить("ВидДвижения");
	КонецЕсли;
	ТаблицаДвиженияВзаиморасчетов.ЗаполнитьЗначения(ВидДвиженияНакопления.Расход,"ВидДвижения");
	
	ДвиженияРегистров = СобратьТаблицыДвижений(ТаблицыДвиженийРез, ТаблицаДвиженияВзаиморасчетов, ТаблицаРасходов, );
	
	Если ТаблицаЗачетаНалогов.Итог("Сумма") > 0 Тогда
		ТаблицаЗачетаНалогов.Колонки.СчетУчета.Имя = "ДоговорКонтрагента";
		ТаблицаЗачетаНалогов.Колонки.Добавить("Сделка");
		КУДиР_Оплаты = Новый Структура("ДоходЕНВД, Графа4, Графа5, Графа6, Графа7, НДС, Содержание", 0,0,0,0,0,0,"");
		ОплатаПоставщикуУСН(СтруктураШапкиДокумента, ТаблицаЗачетаНалогов, ДвиженияРегистров, КУДиР_Оплаты, "Налог");
	КонецЕсли;
	
КонецПроцедуры

//Расчет и списание расходов будущих периодов
//
Процедура СписаниеРБПУСН(СтруктураШапкиДокумента, ДвиженияРегистров, КУДиР)
	
	Заголовок = "Списание РБП для целей УСН";

	Статусы = Новый Массив;
	Статусы.Добавить(Перечисления.СтатусыРасходовУСН.НеСписаноНеОплачено);
	Статусы.Добавить(Перечисления.СтатусыРасходовУСН.НеСписано);
	
	//Формирование таблицы фильтров
	ТаблицаФильтров = Новый ТаблицаЗначений;
	ТаблицаФильтров.Колонки.Добавить("ИмяПоля");
	ТаблицаФильтров.Колонки.Добавить("ЗначениеПоля");
	ТаблицаФильтров.Колонки.Добавить("Условие");
		
	Фильтр = ТаблицаФильтров.Добавить();
	Фильтр.ИмяПоля			= "ВидРасхода";
	Фильтр.ЗначениеПоля		= Перечисления.ВидыРасходовУСН.РБП;
	Фильтр.Условие			= "=";
	
	Фильтр = ТаблицаФильтров.Добавить();
	Фильтр.ИмяПоля			= "СтатусыОплатыРасходовУСН";
	Фильтр.ЗначениеПоля	= Статусы;
	Фильтр.Условие			= "В";
	
	//Из таблицы расходов выбираем соответствующие партии и элементы расходов
	ТаблицаРасходов = ВыполнитьЗапросПоРасходам(Новый Граница(СтруктураШапкиДокумента.Ссылка.МоментВремени(), ВидГраницы.Включая), СтруктураШапкиДокумента.Организация, ТаблицаФильтров);
	
	ТаблицаДляСписания = ТаблицаРасходов.СкопироватьКолонки();
	
	НачМесяца = НачалоМесяца(СтруктураШапкиДокумента.Дата);
	КонМесяца = КонецМесяца(СтруктураШапкиДокумента.Дата);
	
	//Заполняем таблицу списываемых расходов по таблице расходов и фильтрам партионного учета
	Для Каждого Строка Из ТаблицаРасходов Цикл
		Если Строка.Сумма = 0 Тогда
			Продолжить;
		КонецЕсли;
		РБП = Строка.ЭлементРасхода;
		
		Если Не ЗначениеЗаполнено(РБП) Тогда
			Продолжить;
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(РБП.ДатаНачалаСписания) Тогда
			ТекстСообщения = "В справочнике ""Расходы будущих периодов"" для статьи """+ РБП + """ не заполнен дата начала списания !";
			ОбщегоНазначения.СообщитьОбОшибке(ТекстСообщения, Ложь, Заголовок);
			Продолжить;
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(РБП.ДатаОкончанияСписания) Тогда
			ТекстСообщения = "В справочнике ""Расходы будущих периодов"" для статьи """+ РБП + """ не заполнен дата окончания списания !";
			ОбщегоНазначения.СообщитьОбОшибке(ТекстСообщения, Ложь, Заголовок);
			Продолжить;
		КонецЕсли;
		
		ВспомогательныеДанныеРасчета = Новый Структура("КоличествоМесяцевДней, КоличествоМесяцевДнейТекущегоПериода", 0, 0);
		ДатаНачала = ?(РБП.ДатаНачалаСписания > НачМесяца, РБП.ДатаНачалаСписания, НачМесяца);
		СуммаСписания = БухгалтерскийУчет.СуммаСписанияЗаМесяц(НачМесяца, КонМесяца, РБП, ДатаНачала, РБП.ДатаОкончанияСписания, Строка.Сумма, ВспомогательныеДанныеРасчета);
		
		Если НЕ (СуммаСписания > 0.05 Или СуммаСписания < -0.05) Тогда
			Продолжить;
		КонецЕсли;
		
		НоваяСтрока = ТаблицаДляСписания.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);
		
		Если Строка.Сумма > СуммаСписания Тогда
			К = СуммаСписания / Строка.Сумма;
			НоваяСтрока.Количество 	= К*НоваяСтрока.Количество;
			НоваяСтрока.Сумма	 	= К*НоваяСтрока.Сумма;
			НоваяСтрока.НДС 		= К*НоваяСтрока.НДС;
		КонецЕсли;
	КонецЦикла;
	
	//Разложим таблицу списываемых расходов на движения корректировки статуса и принятые расходы
	ТаблицыДвижений = ИзменениеСтатусовРасходов(СтруктураШапкиДокумента, ТаблицаДляСписания, , "Списание", Ложь);
		     
	ДвиженияРегистров = СобратьТаблицыДвижений(ТаблицыДвижений,,,);
	
КонецПроцедуры

//Списывает материалы переданные в производство на основании движений по регистру 
//ЗатратыНаВыпускПродукцииБухгалтерскийУчет
Процедура ВыпускПродукции(СтруктураШапкиДокумента, ДвиженияРегистров, КУДиР) //~
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация", СтруктураШапкиДокумента.Организация);
	Запрос.УстановитьПараметр("ДатаНач", НачалоМесяца(СтруктураШапкиДокумента.Дата));
	Запрос.УстановитьПараметр("ДатаКон", КонецМесяца(СтруктураШапкиДокумента.Дата));
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ЗатратыНаВыпускПродукцииБухгалтерскийУчетОбороты.Затрата,
	|	СУММА(ЗатратыНаВыпускПродукцииБухгалтерскийУчетОбороты.КоличествоОборот) КАК Количество
	|ИЗ
	|	РегистрНакопления.ЗатратыНаВыпускПродукцииБухгалтерскийУчет.Обороты(&ДатаНач, &ДатаКон, Месяц, ) КАК ЗатратыНаВыпускПродукцииБухгалтерскийУчетОбороты
	|ГДЕ
	|	(ЗатратыНаВыпускПродукцииБухгалтерскийУчетОбороты.Организация = &Организация) И
	|	(ЗатратыНаВыпускПродукцииБухгалтерскийУчетОбороты.Затрата Ссылка Справочник.Номенклатура)
	|
	|СГРУППИРОВАТЬ ПО
	|	ЗатратыНаВыпускПродукцииБухгалтерскийУчетОбороты.Затрата";
	
	ИспользованныеМатериалы = Запрос.Выполнить().Выгрузить();
	
	//Подготовка наборов фильтров
	МассивНоменклатуры = ИспользованныеМатериалы.ВыгрузитьКолонку("Затрата");
	ОбщегоНазначения.УдалитьПовторяющиесяЭлементы(МассивНоменклатуры);
	
	Статусы = Новый Массив;
	Статусы.Добавить(Перечисления.СтатусыРасходовУСН.НеВыпущеноНеОплачено);
	Статусы.Добавить(Перечисления.СтатусыРасходовУСН.НеВыпущено);
			
	//Формирование таблицы фильтров
	ТаблицаФильтров = Новый ТаблицаЗначений;
	ТаблицаФильтров.Колонки.Добавить("ИмяПоля");
	ТаблицаФильтров.Колонки.Добавить("ЗначениеПоля");
	ТаблицаФильтров.Колонки.Добавить("Условие");
		
	Фильтр = ТаблицаФильтров.Добавить();
	Фильтр.ИмяПоля			= "ВидРасхода";
	Фильтр.ЗначениеПоля	= Перечисления.ВидыРасходовУСН.Номенклатура;
	Фильтр.Условие			= "=";
	
	Фильтр = ТаблицаФильтров.Добавить();
	Фильтр.ИмяПоля			= "СтатусыПартийУСН";
	Фильтр.ЗначениеПоля	= Перечисления.СтатусыПартийУСН.ВПроизводстве;
	Фильтр.Условие			= "=";
	
	Фильтр = ТаблицаФильтров.Добавить();
	Фильтр.ИмяПоля			= "ЭлементРасхода";
	Фильтр.ЗначениеПоля	= МассивНоменклатуры;
	Фильтр.Условие			= "В";
	
	Фильтр = ТаблицаФильтров.Добавить();
	Фильтр.ИмяПоля			= "СтатусыОплатыРасходовУСН";
	Фильтр.ЗначениеПоля	= Статусы;
	Фильтр.Условие			= "В";
	
	//Из таблицы расходов выбираем соответствующие партии и элементы расходов
	ТаблицаРасходов = ВыполнитьЗапросПоРасходам(Новый Граница(СтруктураШапкиДокумента.Ссылка.МоментВремени(), ВидГраницы.Включая), СтруктураШапкиДокумента.Организация, ТаблицаФильтров);
	
	ТаблицаДляСписания = ТаблицаРасходов.СкопироватьКолонки();	
	ТаблицаДляСписания.Колонки.Добавить("СтатусСписания");
	ТаблицаДляСписания.Колонки.Добавить("КоличествоПоСтатусуСписания");
	
	//Заполняем таблицу списываемых расходов по таблице расходов и фильтрам партионного учета
	Для Каждого СтрокаМатериал Из ИспользованныеМатериалы Цикл
		СтруктураОтбора = Новый Структура;
		СтруктураОтбора.Вставить("ЭлементРасхода",	СтрокаМатериал.Затрата);
		
		НайденныеСтроки = ТаблицаРасходов.НайтиСтроки(СтруктураОтбора);
		
		// Подлежащее погашению при списании количество
		КоличествоОсталосьПогасить = СтрокаМатериал.Количество;
		
		Для Каждого Строка Из НайденныеСтроки Цикл
			Если КоличествоОсталосьПогасить <= 0 Тогда
				Прервать;
			КонецЕсли;
			Если Строка.Количество <= 0 Тогда
				Продолжить;
			КонецЕсли;
			
			КоличествоСписания = Мин(Строка.Количество, КоличествоОсталосьПогасить);
			КоличествоОсталосьПогасить = КоличествоОсталосьПогасить - КоличествоСписания;
						
			НоваяСтрока = ТаблицаДляСписания.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);
						
			Если Строка.Количество > КоличествоСписания Тогда
				К = КоличествоСписания / Строка.Количество;
				НоваяСтрока.Количество 	= КоличествоСписания;
				НоваяСтрока.Сумма	 	= К*НоваяСтрока.Сумма;
				НоваяСтрока.НДС 		= К*НоваяСтрока.НДС;
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
	//Разложим таблицу списываемых расходов на движения корректировки статуса и принятые расходы
	ТаблицыДвижений = ИзменениеСтатусовРасходов(СтруктураШапкиДокумента, ТаблицаДляСписания, , "Выпуск", Ложь);
	
	ТаблицыДвижений.ТаблицаПриход.ЗаполнитьЗначения(Перечисления.СтатусыПартийУСН.Списанные,"СтатусыПартийУСН");
	     
	ДвиженияРегистров = СобратьТаблицыДвижений(ТаблицыДвижений,,,);
	ДвиженияРегистров.Вставить("ТаблицаПриход", ТаблицыДвижений.ТаблицаПриход);
	
КонецПроцедуры

//Расчет и списание расходов будущих периодов
//
Процедура РаспределениеЕНВД(СтруктураШапкиДокумента, ДвиженияРегистров, КУДиР)
	
	//Формирование таблицы фильтров
	ТаблицаФильтров = Новый ТаблицаЗначений;
	ТаблицаФильтров.Колонки.Добавить("ИмяПоля");
	ТаблицаФильтров.Колонки.Добавить("ЗначениеПоля");
	ТаблицаФильтров.Колонки.Добавить("Условие");
	
	Фильтр = ТаблицаФильтров.Добавить();
	Фильтр.ИмяПоля			= "СтатусыОплатыРасходовУСН";
	Фильтр.ЗначениеПоля		= Перечисления.СтатусыРасходовУСН.НеРаспределено;
	Фильтр.Условие			= "=";
	МетаданныеДокумента = СтруктураШапкиДокумента.Ссылка.Метаданные();
	СтруктураШапкиДокумента.Дата = ?(МетаданныеДокумента.Реквизиты.Найти("ПериодРегистрации") <> Неопределено, КонецКвартала(СтруктураШапкиДокумента.Ссылка.ПериодРегистрации), КонецКвартала(СтруктураШапкиДокумента.Дата));
	
	//Из таблицы расходов выбираем соответствующие партии и элементы расходов
	ТаблицаРасходов = ВыполнитьЗапросПоРасходам(Новый Граница(СтруктураШапкиДокумента.Ссылка.МоментВремени(), ВидГраницы.Включая), СтруктураШапкиДокумента.Организация, ТаблицаФильтров);
	ТаблицаДляСписания = ТаблицаРасходов.СкопироватьКолонки();
			
	//Разложим таблицу списываемых расходов на движения корректировки статуса и принятые расходы
	ТаблицыДвижений = ИзменениеСтатусовРасходов(СтруктураШапкиДокумента, ТаблицаРасходов, , "РаспределениеЕНВД", Ложь);
			 
	Метод = ?(СтруктураШапкиДокумента.МетодРаспределения = Перечисления.МетодыРаспределенияРасходовУСНПоВидамДеятельности.НарастающимИтогомСНачалаГода, "Год", "Квартал");
	База = ?(СтруктураШапкиДокумента.БазаРаспределения = Перечисления.БазаРаспределенияРасходовУСНПоВидамДеятельности.ДоходыВсегоНУ, "НВ",
			?(СтруктураШапкиДокумента.БазаРаспределения = Перечисления.БазаРаспределенияРасходовУСНПоВидамДеятельности.ДоходыПринимаемыеНУ, "НУ", "БУ"));
	
	Коэфф = ПолучитьКоэффРаспределенияЕНВД(СтруктураШапкиДокумента, Метод, База);
	
	ИтогРаспределения = ТаблицыДвижений.ТаблицаПринятых.Итог("Сумма")*Коэфф;
	Распределено = 0;
	МаксПозиция = Неопределено;
	МаксСумма = 0;
	
	Если (Коэфф = 0) ИЛИ (ТаблицыДвижений.ТаблицаПринятых.Количество() = 0) Тогда
		//Все расходы осносятся к УСН или нет распределенных расходов
	Иначе
		Для Каждого Расход Из ТаблицыДвижений.ТаблицаПринятых Цикл
			Если Расход.Сумма > 0 Тогда
				Расход.Количество 	= Расход.Количество - Окр(Коэфф*Расход.Количество,3,1);
				
				Если МаксСумма < Расход.Сумма Тогда
					МаксПозиция = Расход;
					МаксСумма = Расход.Сумма;
				КонецЕсли;
				Распределено = Распределено + Окр(Коэфф*Расход.Сумма,2,1);
				
				Расход.Сумма	 	= Расход.Сумма - Окр(Коэфф*Расход.Сумма,2,1);
				Расход.НДС 			= Расход.НДС - Окр(Коэфф*Расход.НДС,2,1);
			КонецЕсли;
		КонецЦикла;
		
		Если (Распределено <> ИтогРаспределения) И (МаксПозиция <> Неопределено) Тогда
			МаксПозиция.Сумма = МаксПозиция.Сумма - (ИтогРаспределения - Распределено);
		КонецЕсли;
	КонецЕсли;
	
	Если (НЕ Метод = "Квартал") И (НЕ НачалоГода(СтруктураШапкиДокумента.Дата) = НачалоКвартала(СтруктураШапкиДокумента.Дата)) Тогда
		СкорректироватьРаспределениеПредыдущихКварталов(СтруктураШапкиДокумента, Коэфф, ТаблицыДвижений.ТаблицаПринятых);
	КонецЕсли;

	ДвиженияРегистров = СобратьТаблицыДвижений(ТаблицыДвижений,,,);
	
КонецПроцедуры

Процедура ВозвратПоДокументуУСН(СтруктураШапкиДокумента, ТаблицаПоТоварам, ДвиженияРегистров, КУДиР)
	
	мВалютаРегламентированногоУчета = СтруктураШапкиДокумента.ВалютаРегламентированногоУчета;
	УчетнаяПолитика = ОбщегоНазначения.ПолучитьПараметрыУчетнойПолитикиРегл(СтруктураШапкиДокумента.Дата, СтруктураШапкиДокумента.Организация);
	СтруктураШапкиДокумента.Вставить("ПорядокПризнанияРасходовПоНДС",УчетнаяПолитика.ПорядокПризнанияРасходовПоНДС);
	СтруктураШапкиДокумента.Вставить("ПорядокПризнанияРасходовПоТоварам",УчетнаяПолитика.ПорядокПризнанияРасходовПоТоварам);
	СтруктураШапкиДокумента.Вставить("ПорядокПризнанияМатериальныхРасходов",УчетнаяПолитика.ПорядокПризнанияМатериальныхРасходов);
	ВыделятьНДСУСН = (СтруктураШапкиДокумента.ПорядокПризнанияРасходовПоНДС = Перечисления.ПорядокПризнанияРасходовПоНДС.ПоОплатеПоставщику);
	
	ВыделятьНДСУСНРеализация = ВыделятьНДСУСН;
	УчетнаяПолитикаРализация = ОбщегоНазначения.ПолучитьПараметрыУчетнойПолитикиРегл(СтруктураШапкиДокумента.Сделка.Дата, СтруктураШапкиДокумента.Организация, Ложь);
	ВыделятьНДСУСНРеализация = (УчетнаяПолитикаРализация.ПорядокПризнанияРасходовПоНДС = Перечисления.ПорядокПризнанияРасходовПоНДС.ПоОплатеПоставщику);

	
	ТоварыПоОплате = (СтруктураШапкиДокумента.ПорядокПризнанияРасходовПоТоварам = Перечисления.ПорядокПризнанияРасходовПоТоварам.ПоОплатеПоставщику);
	ТоварыПоОтгрузке = (СтруктураШапкиДокумента.ПорядокПризнанияРасходовПоТоварам = Перечисления.ПорядокПризнанияРасходовПоТоварам.ПоФактуРеализации);
	МатериалыПоОплате = (СтруктураШапкиДокумента.ПорядокПризнанияМатериальныхРасходов = Перечисления.ПорядокПризнанияМатериальныхРасходов.ПоОплатеПоставщику);
		
	//Соберем данные о списанных партиях
	НаборЗаписей = РегистрыНакопления.ПартииТоваровНаСкладахНалоговыйУчет.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Регистратор.Установить(СтруктураШапкиДокумента.Сделка);
	НаборЗаписей.Прочитать();
	ТаблицаПартийРасход = НаборЗаписей.Выгрузить();
	
	//Удалим движения прихода
	КолСтрок = ТаблицаПартийРасход.Количество();
	Для Н = 1 по КолСтрок Цикл
		Если ТаблицаПартийРасход[КолСтрок - Н].ВидДвижения = ВидДвиженияНакопления.Приход Тогда
			ТаблицаПартийРасход.Удалить(ТаблицаПартийРасход[КолСтрок - Н]);
		КонецЕсли;
	КонецЦикла;
	
	//Добавим в партии информацию об изменении ОтражениеВУСН
	СтруктураШапкиДокументаРеализации = ОбщегоНазначения.СформироватьСтруктуруШапкиДокумента(СтруктураШапкиДокумента.Сделка.ПолучитьОбъект());
	ЗаполнитьОтражениеВНУпоСписанию(СтруктураШапкиДокументаРеализации, ТаблицаПартийРасход, "Реализация");
	
	//Соберем данные о списанных расходах
	НаборЗаписей = РегистрыНакопления.РасходыПриУСН.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Регистратор.Установить(СтруктураШапкиДокумента.Сделка);
	НаборЗаписей.Прочитать();
	ТаблицаРасходов = НаборЗаписей.Выгрузить();
	
	//Удалим движения прихода
	КолСтрок = ТаблицаРасходов.Количество();
	Для Н = 1 по КолСтрок Цикл
		Если ТаблицаРасходов[КолСтрок - Н].ВидДвижения = ВидДвиженияНакопления.Приход Тогда
			ТаблицаРасходов.Удалить(ТаблицаРасходов[КолСтрок - Н]);
		КонецЕсли;
	КонецЦикла;
	
	//Формирование таблицы фильтров
	ТаблицаФильтров = Новый ТаблицаЗначений;
	ТаблицаФильтров.Колонки.Добавить("ИмяПоля");
	ТаблицаФильтров.Колонки.Добавить("ЗначениеПоля");
	ТаблицаФильтров.Колонки.Добавить("Условие");
		
	Фильтр = ТаблицаФильтров.Добавить();
	Фильтр.ИмяПоля			= "ВидРасхода";
	Фильтр.ЗначениеПоля		= Перечисления.ВидыРасходовУСН.Номенклатура;
	Фильтр.Условие			= "=";
	
	Фильтр = ТаблицаФильтров.Добавить();
	Фильтр.ИмяПоля			= "СтатусыПартийУСН";
	Фильтр.ЗначениеПоля		= Перечисления.СтатусыПартийУСН.Списанные;
	Фильтр.Условие			= "=";
	
	МассивНоменклатуры = ТаблицаПоТоварам.ВыгрузитьКолонку("Номенклатура");
	ОбщегоНазначения.УдалитьПовторяющиесяЭлементы(МассивНоменклатуры);
	
	Фильтр = ТаблицаФильтров.Добавить();
	Фильтр.ИмяПоля			= "ЭлементРасхода";
	Фильтр.ЗначениеПоля		= МассивНоменклатуры;
	Фильтр.Условие			= "В";
	
	Фильтр = ТаблицаФильтров.Добавить();
	Фильтр.ИмяПоля			= "Партия";
	Фильтр.ЗначениеПоля		= СтруктураШапкиДокумента.Сделка;
	Фильтр.Условие			= "=";
	
	//Из таблицы расходов выбираем соответствующие партии и элементы расходов
	ТаблицаОстатковРасходов = ВыполнитьЗапросПоРасходам(Новый Граница(СтруктураШапкиДокумента.Ссылка.МоментВремени(), ВидГраницы.Включая), СтруктураШапкиДокумента.Организация, ТаблицаФильтров);
	
	ТаблицаКорректировкиПриход = ТаблицаОстатковРасходов.СкопироватьКолонки();
	
	ТаблицаКорректировкиРасход = ТаблицаКорректировкиПриход.Скопировать();
	ТаблицаПризнанныхРасходов = ТаблицаКорректировкиПриход.Скопировать();
	
	Для Каждого Строка Из ТаблицаПоТоварам Цикл
		
		СтруктураОтбора = Новый Структура;
		СтруктураОтбора.Вставить("Номенклатура",	Строка.Номенклатура);
		
		НайденныеСтроки = ТаблицаПартийРасход.НайтиСтроки(СтруктураОтбора);
		
		СтатусСписания = Перечисления.ОтражениеВУСН.ПустаяСсылка();
		Если НайденныеСтроки.Количество() > 0 Тогда
			СтатусСписания = НайденныеСтроки[0].СтатусСписания;
		КонецЕсли;
			
		//Добавить строки поступления по партиям
		КоличествоОсталосьСписать = Строка.Количество;
		Для Каждого СтрокаПартии Из НайденныеСтроки Цикл
			Если НЕ КоличествоОсталосьСписать > 0 Тогда
				Прервать;
			КонецЕсли;
			Если НЕ СтрокаПартии.Количество > 0 Тогда
				Продолжить;
			КонецЕсли;
			
			КоличествоСписать = Мин(КоличествоОсталосьСписать, СтрокаПартии.Количество);
			КоличествоОсталосьСписать 	= КоличествоОсталосьСписать - КоличествоСписать;
			СтрокаПартии.Количество 	= СтрокаПартии.Количество - КоличествоСписать;
			
			СтруктураОтбора = Новый Структура;
			СтруктураОтбора.Вставить("ЭлементРасхода",	Строка.Номенклатура);

			СтруктураОтбораПоПартиям = Новый Структура;
			СтруктураОтбораПоПартиям.Вставить("ЭлементРасхода",	Строка.Номенклатура);
			СтруктураОтбораПоПартиям.Вставить("Партия",			СтрокаПартии.ДокументОприходования);
			
			НайденныеСтрокиСписано = ТаблицаРасходов.НайтиСтроки(СтруктураОтбораПоПартиям);
			НайденныеСтрокиОстаток = ТаблицаОстатковРасходов.НайтиСтроки(СтруктураОтбора);
			
			КоличествоОсталосьСписатьРасход = КоличествоСписать;
			Для Каждого СтрокаСписания Из НайденныеСтрокиСписано Цикл
				Если НЕ СтрокаСписания.Количество > 0 Тогда
					Продолжить;
				КонецЕсли;
				Для Каждого СтрокаОстатка Из НайденныеСтрокиОстаток Цикл
					Если НЕ СтрокаСписания.Количество > 0 Тогда
						Продолжить;
					КонецЕсли;
					Если НЕ КоличествоОсталосьСписатьРасход > 0 Тогда
						Прервать;
					КонецЕсли;
					Если НЕ СтрокаОстатка.Количество > 0 Тогда
						Продолжить;
					КонецЕсли;
					
					КоличествоСписатьРасход = Мин(КоличествоОсталосьСписатьРасход, СтрокаСписания.Количество, СтрокаОстатка.Количество);
					Коэфф = ?(СтрокаОстатка.Количество = 0, 0, КоличествоСписатьРасход/СтрокаОстатка.Количество);
					//Добавить строки поступления по расходам
					НоваяСтрокаРасход = ТаблицаКорректировкиРасход.Добавить();
					НоваяСтрока 	  = ТаблицаКорректировкиПриход.Добавить();
					Для Каждого Кол Из ТаблицаОстатковРасходов.Колонки Цикл
						Если ПустаяСтрока(Кол.Имя) ИЛИ Кол.Имя = "QuieryId" ИЛИ Кол.Имя = "ПартияДата" ИЛИ Кол.Имя = "РасчетныйДокументДата" Тогда
							Продолжить;
						КонецЕсли; 
						НоваяСтрокаРасход[Кол.Имя] = СтрокаОстатка[Кол.Имя];
						НоваяСтрока[Кол.Имя] = СтрокаОстатка[Кол.Имя];
					КонецЦикла;
					НоваяСтрока.СтатусыПартийУСН 	= СтрокаСписания.СтатусыПартийУСН;
					НоваяСтрока.Партия 				= СтрокаСписания.Партия;
					Если (НоваяСтрока.СтатусыОплатыРасходовУСН = Перечисления.СтатусыРасходовУСН.НеОплаченоПокупателем) или
						(НоваяСтрока.СтатусыОплатыРасходовУСН = Перечисления.СтатусыРасходовУСН.НеРаспределено) Тогда
						НоваяСтрока.СтатусыОплатыРасходовУСН = Перечисления.СтатусыРасходовУСН.НеСписано;
					Иначе
						НоваяСтрока.СтатусыОплатыРасходовУСН = Перечисления.СтатусыРасходовУСН.НеСписаноНеОплачено;
					КонецЕсли;
					//Движение расход
					НоваяСтрокаРасход.Сумма 		= Коэфф*СтрокаОстатка.Сумма;
					НоваяСтрокаРасход.НДС 			= Коэфф*СтрокаОстатка.НДС;
					НоваяСтрокаРасход.Количество 	= КоличествоСписатьРасход;
					//Движение приход
					НоваяСтрока.Сумма 		= Коэфф*СтрокаОстатка.Сумма;
					НоваяСтрока.НДС 		= Коэфф*СтрокаОстатка.НДС;
					НоваяСтрока.Количество 	= КоличествоСписатьРасход;
					
					КоличествоОсталосьСписатьРасход 	= КоличествоОсталосьСписатьРасход - КоличествоСписатьРасход;
					СтрокаСписания.Количество 	= СтрокаСписания.Количество - НоваяСтрока.Количество;
					СтрокаСписания.Сумма 		= СтрокаСписания.Сумма - НоваяСтрока.Сумма;
					СтрокаСписания.НДС 			= СтрокаСписания.НДС - НоваяСтрока.НДС;
					
					СтрокаОстатка.Количество 	= СтрокаОстатка.Количество - НоваяСтрока.Количество;
					СтрокаОстатка.Сумма 		= СтрокаОстатка.Сумма - НоваяСтрока.Сумма;
					СтрокаОстатка.НДС 			= СтрокаОстатка.НДС - НоваяСтрока.НДС;
					
					Если (СтруктураШапкиДокумента.Дата >= Дата("20060101")) И (ТоварыПоОтгрузке) И
						(НоваяСтрокаРасход.СтатусыОплатыРасходовУСН = Перечисления.СтатусыРасходовУСН.НеОплаченоПокупателем) И 
						(НоваяСтрока.ОтражениеВУСН = Перечисления.ОтражениеВУСН.Принимаются) И 
						(НЕ СтатусСписания = Перечисления.ОтражениеВУСН.НеПринимаются) Тогда
						
						СтрокаРасходов = ТаблицаПризнанныхРасходов.Добавить();
						ЗаполнитьЗначенияСвойств(СтрокаРасходов, НоваяСтрока);
						СтрокаРасходов.Сумма = - СтрокаРасходов.Сумма;
						СтрокаРасходов.НДС = - СтрокаРасходов.НДС;
						
					КонецЕсли;
					
				КонецЦикла;
			КонецЦикла;
			
			Если КоличествоОсталосьСписатьРасход > 0 Тогда
				Для Каждого СтрокаСписания Из НайденныеСтрокиСписано Цикл
					Если НЕ СтрокаСписания.Количество > 0 Тогда
						Продолжить;
					КонецЕсли;
					КоличествоСписатьРасход = Мин(КоличествоОсталосьСписатьРасход, СтрокаСписания.Количество);
					Коэфф = ?(СтрокаСписания.Количество = 0, 0, КоличествоСписатьРасход/СтрокаСписания.Количество);
					
					//Добавить строки поступления по расходам
					НоваяСтрока = ТаблицаКорректировкиПриход.Добавить();
					ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаСписания);
										
					//Определим сумму сторнируемых расходов
					ЭтоТовар = ((Строка.СчетУчетаБУ.ПринадлежитЭлементу(ПланыСчетов.Хозрасчетный.ТоварыОтгруженные)) ИЛИ 
								(Строка.СчетУчетаБУ.ПринадлежитЭлементу(ПланыСчетов.Хозрасчетный.Товары)));
					ЭтоМатериал = Строка.СчетУчетаБУ.ПринадлежитЭлементу(ПланыСчетов.Хозрасчетный.Материалы);
					РасходыПоОплате = ((ТоварыПоОплате И ЭтоТовар) ИЛИ (МатериалыПоОплате И ЭтоМатериал));
					
					Если (РасходыПоОплате) И (НоваяСтрока.ОтражениеВУСН = Перечисления.ОтражениеВУСН.Принимаются) Тогда
						НоваяСтрока.СтатусыОплатыРасходовУСН = Перечисления.СтатусыРасходовУСН.НеСписаноПринято;
					Иначе
						НоваяСтрока.СтатусыОплатыРасходовУСН = Перечисления.СтатусыРасходовУСН.НеСписано;
					КонецЕсли;
					
					//Движение приход
					НоваяСтрока.Сумма 		= Коэфф*СтрокаСписания.Сумма;
					НоваяСтрока.НДС 		= Коэфф*СтрокаСписания.НДС;
					НоваяСтрока.Количество 	= КоличествоСписатьРасход;
					
					КоличествоОсталосьСписатьРасход = КоличествоОсталосьСписатьРасход - КоличествоСписатьРасход;
					СтрокаСписания.Количество 	= СтрокаСписания.Количество - НоваяСтрока.Количество;
					СтрокаСписания.Сумма 		= СтрокаСписания.Сумма - НоваяСтрока.Сумма;
					СтрокаСписания.НДС 			= СтрокаСписания.НДС - НоваяСтрока.НДС;
					
					Если (НЕ ВыделятьНДСУСНРеализация) И (ВыделятьНДСУСН) Тогда
						НоваяСтрока.Сумма = НоваяСтрока.Сумма - НоваяСтрока.НДС;
					КонецЕсли;
					
					Если (НЕ РасходыПоОплате) И (НоваяСтрока.ОтражениеВУСН = Перечисления.ОтражениеВУСН.Принимаются)
						И (НЕ СтатусСписания = Перечисления.ОтражениеВУСН.НеПринимаются) Тогда
						
						СтрокаРасходов = ТаблицаПризнанныхРасходов.Добавить();
						ЗаполнитьЗначенияСвойств(СтрокаРасходов, НоваяСтрока);
						СтрокаРасходов.Сумма = - СтрокаРасходов.Сумма;
						СтрокаРасходов.НДС = - СтрокаРасходов.НДС;
												
					КонецЕсли;
									
				КонецЦикла;
			КонецЕсли;
		КонецЦикла;
		
		Если КоличествоОсталосьСписать > 0 Тогда
			
			//Оприходовать по документу возврата
			
		КонецЕсли;
	
	КонецЦикла;
	
	СтруктураРасходов = Новый Структура("ТаблицаПриход, ТаблицаРасход, ТаблицаПринятых", ТаблицаКорректировкиПриход, ТаблицаКорректировкиРасход, ТаблицаПризнанныхРасходов);
	ДвиженияРегистров = СобратьТаблицыДвижений(СтруктураРасходов, , , ДвиженияРегистров);
		
КонецПроцедуры

Процедура СписаниеТЗР(СтруктураШапкиДокумента, ДвиженияРегистров)
	
	//Подготовка наборов фильтров
	Статусы = Новый Массив;
	Статусы.Добавить(Перечисления.СтатусыРасходовУСН.НеСписаноНеОплачено);
	Статусы.Добавить(Перечисления.СтатусыРасходовУСН.НеСписано);
	Статусы.Добавить(Перечисления.СтатусыРасходовУСН.НеСписаноПринято);
	Статусы.Добавить(Перечисления.СтатусыРасходовУСН.НеСписаноНеРаспределено);
	
	//Формирование таблицы фильтров
	ТаблицаФильтров = Новый ТаблицаЗначений;
	ТаблицаФильтров.Колонки.Добавить("ИмяПоля");
	ТаблицаФильтров.Колонки.Добавить("ЗначениеПоля");
	ТаблицаФильтров.Колонки.Добавить("Условие");
		
	Фильтр = ТаблицаФильтров.Добавить();
	Фильтр.ИмяПоля			= "ВидРасхода";
	Фильтр.ЗначениеПоля		= Перечисления.ВидыРасходовУСН.ДопРасходы;
	Фильтр.Условие			= "=";
	
	Фильтр = ТаблицаФильтров.Добавить();
	Фильтр.ИмяПоля			= "СтатусыОплатыРасходовУСН";
	Фильтр.ЗначениеПоля	= Статусы;
	Фильтр.Условие			= "В";
	
	//Из таблицы расходов выбираем соответствующие партии и элементы расходов
	ТаблицаРасходов = ВыполнитьЗапросПоРасходам(Новый Граница(СтруктураШапкиДокумента.Ссылка.МоментВремени(), ВидГраницы.Включая), СтруктураШапкиДокумента.Организация, ТаблицаФильтров);
	
	ТаблицаРасчетныхДокументов = ТаблицаРасходов.Скопировать();
	ТаблицаРасчетныхДокументов.Свернуть("ЭлементРасхода, Партия, РасчетныйДокумент", "Количество"); 
	
	ТаблицаРасходов.Индексы.Добавить("ЭлементРасхода,Партия,РасчетныйДокумент");
	ТаблицаРасчетныхДокументов.Индексы.Добавить("ЭлементРасхода,Партия");
	
	ТаблицаДляСписания = ТаблицаРасходов.СкопироватьКолонки();
	ТаблицаДляСписания.Колонки.Добавить("СтатусСписания");
	ТаблицаДляСписания.Колонки.Добавить("КоличествоПоСтатусуСписания");
	
	МассивНоменклатуры = ТаблицаРасходов.ВыгрузитьКолонку("ЭлементРасхода");
	ОбщегоНазначения.УдалитьПовторяющиесяЭлементы(МассивНоменклатуры);
	ОбщегоНазначения.УдалитьНеЗаполненныеЭлементыМассива(МассивНоменклатуры);
	
	МассивПартий = ТаблицаРасходов.ВыгрузитьКолонку("Партия");
	ОбщегоНазначения.УдалитьПовторяющиесяЭлементы(МассивПартий);
	ОбщегоНазначения.УдалитьНеЗаполненныеЭлементыМассива(МассивПартий);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("НачДата", НачалоКвартала(СтруктураШапкиДокумента.Дата));
	Запрос.УстановитьПараметр("КонДата", КонецКвартала(СтруктураШапкиДокумента.Дата));
	Запрос.УстановитьПараметр("Организация", 		СтруктураШапкиДокумента.Организация);
	Запрос.УстановитьПараметр("ВидРасхода", 		Перечисления.ВидыРасходовУСН.Номенклатура);
	Запрос.УстановитьПараметр("СтатусСписания", 	Перечисления.ОтражениеВУСН.ПустаяСсылка());
	Запрос.УстановитьПараметр("Номенклатура", МассивНоменклатуры);
	Запрос.УстановитьПараметр("Партии", МассивПартий);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	РасходыПриУСН.СтатусСписания,
	|	СУММА(РасходыПриУСН.Количество) КАК КоличествоСписания,
	|	РасходыПриУСН.ЭлементРасхода КАК Номенклатура,
	|	РасходыПриУСН.Партия КАК ДокументОприходования,
	|	РасходыПриУСН.Регистратор
	|ИЗ
	|	РегистрНакопления.РасходыПриУСН КАК РасходыПриУСН
	|ГДЕ
	|	РасходыПриУСН.ВидРасхода = &ВидРасхода
	|	И РасходыПриУСН.СтатусСписания <> &СтатусСписания
	|	И РасходыПриУСН.Организация = &Организация
	|	И РасходыПриУСН.Период МЕЖДУ &НачДата И &КонДата
	|	И РасходыПриУСН.Партия В(&Партии)
	|	И РасходыПриУСН.ЭлементРасхода В(&Номенклатура)
	|
	|СГРУППИРОВАТЬ ПО
	|	РасходыПриУСН.СтатусСписания,
	|	РасходыПриУСН.ЭлементРасхода,
	|	РасходыПриУСН.Партия,
	|	РасходыПриУСН.Регистратор
	|
	|УПОРЯДОЧИТЬ ПО
	|	РасходыПриУСН.Регистратор.Дата";
	
	ТаблицаПартий = Запрос.Выполнить().Выгрузить();
	
	Для Каждого СтрокаПартии Из ТаблицаПартий Цикл
		КоличествоСписанияПоРегистратору = СтрокаПартии.КоличествоСписания;
		
		ТаблицаДляСписания.Очистить();
		
		//Пропустим операции поступления и перемещения
		Если НЕ КоличествоСписанияПоРегистратору > 0 Тогда
			Продолжить;
		КонецЕсли;
		
		//Выберем расчетные документы доп.расходов
		СтруктураОтбора = Новый Структура;
		СтруктураОтбора.Вставить("ЭлементРасхода",	СтрокаПартии.Номенклатура);
		СтруктураОтбора.Вставить("Партия",			СтрокаПартии.ДокументОприходования);
		
		НайденныеСтрокиРД = ТаблицаРасчетныхДокументов.НайтиСтроки(СтруктураОтбора);
		
		Для Каждого СтрокаРасчетныйДокумент Из НайденныеСтрокиРД Цикл
			
			КоличествоСписанияРД = Мин(СтрокаРасчетныйДокумент.Количество, КоличествоСписанияПоРегистратору);
			
			//Выберем доп. расходы данной номенклатуры/партии
			СтруктураОтбора = Новый Структура;
			СтруктураОтбора.Вставить("ЭлементРасхода",	СтрокаПартии.Номенклатура);
			СтруктураОтбора.Вставить("Партия",			СтрокаПартии.ДокументОприходования);
			СтруктураОтбора.Вставить("РасчетныйДокумент",СтрокаРасчетныйДокумент.РасчетныйДокумент);
			
			НайденныеСтроки = ТаблицаРасходов.НайтиСтроки(СтруктураОтбора);
			
			Для Каждого Строка Из НайденныеСтроки Цикл
				
				Если Строка.Количество <= 0 Тогда
					Продолжить;
				КонецЕсли;
				
				Если КоличествоСписанияРД <= 0 Тогда
					Прервать;
				КонецЕсли;
				
				КоличествоСписания = Мин(Строка.Количество, КоличествоСписанияРД);
				
				НоваяСтрока = ТаблицаДляСписания.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);
				
				НовыйСтатус = ПолучитьКомбнациюСтатусовОтражениеВНУ(Строка.ОтражениеВУСН, СтрокаПартии.СтатусСписания);
				Если ЗначениеЗаполнено(СтрокаПартии.СтатусСписания) Тогда
					НоваяСтрока.СтатусСписания = НовыйСтатус;
					НоваяСтрока.КоличествоПоСтатусуСписания = КоличествоСписания;
				КонецЕсли;
				
				Если Строка.Количество <> КоличествоСписания Тогда
					К = КоличествоСписания / Строка.Количество;
					НоваяСтрока.Количество 	= Окр(КоличествоСписания,3,1);
					НоваяСтрока.Сумма	 	= Окр(К*НоваяСтрока.Сумма,3,1);
					НоваяСтрока.НДС 		= Окр(К*НоваяСтрока.НДС,3,1);
				КонецЕсли;
				
				Строка.Количество 	=  Строка.Количество - НоваяСтрока.Количество;
				Строка.Сумма 		=  Строка.Сумма - НоваяСтрока.Сумма;
				Строка.НДС 			=  Строка.НДС - НоваяСтрока.НДС;
				
				КоличествоСписанияРД = КоличествоСписанияРД - КоличествоСписания;
				СтрокаРасчетныйДокумент.Количество = СтрокаРасчетныйДокумент.Количество - КоличествоСписания;
			КонецЦикла;
						
		КонецЦикла;
		
		//Разложим таблицу списываемых расходов на движения корректировки статуса и принятые расходы.
		ТаблицыДвижений = ИзменениеСтатусовРасходов(СтруктураШапкиДокумента, ТаблицаДляСписания, , "Списание", Ложь);
		ТаблицыДвижений.ТаблицаПриход.ЗаполнитьЗначения(Перечисления.СтатусыПартийУСН.Списанные,"СтатусыПартийУСН");
		ТаблицыДвижений.ТаблицаПриход.ЗаполнитьЗначения(СтрокаПартии.Регистратор,"Партия");
		ТаблицыДвижений.ТаблицаПринятых.ЗаполнитьЗначения(СтрокаПартии.Регистратор,"Партия");
		ДвиженияРегистров = СобратьТаблицыДвижений(ТаблицыДвижений,,,ДвиженияРегистров);
		
	КонецЦикла;
												
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// МЕНЕДЖЕР УСН (вход документов)

//Основная процедура - собирает необходимые данные и вызвывает
//ситуационные процедуры в зависимости от вида операции. Выполняет движения по регистрам
Процедура ОтразитьВУСН(ДокументСсылка, ВидОперации = Неопределено, Параметры = Неопределено, ЭтапПроведения = 0) Экспорт //
	
	ВалютаРегламентированногоУчета = глЗначениеПеременной("ВалютаРегламентированногоУчета");
	МетаданныеДокумента = ДокументСсылка.Метаданные();
	
	ПустаяТаблицаРД = Новый ТаблицаЗначений;
	ПустаяТаблицаРД.Колонки.Добавить("ДокументРасчетовСКонтрагентом");
	ПустаяТаблицаРД.Колонки.Добавить("СуммаВзаиморасчетов");
	ПустаяТаблицаРД.Колонки.Добавить("Сделка");
	
	//Сбор информации для структуры шапки
	Договор = ?(МетаданныеДокумента.Реквизиты.Найти("ДоговорКонтрагента") <> Неопределено, ДокументСсылка.ДоговорКонтрагента, ?(МетаданныеДокумента.Реквизиты.Найти("ФизЛицо") <> Неопределено,ДокументСсылка.ФизЛицо,Неопределено));
	ВалютаДокумента = ?(МетаданныеДокумента.Реквизиты.Найти("ВалютаДокумента") <> Неопределено, ДокументСсылка.ВалютаДокумента, ВалютаРегламентированногоУчета);
	ВалютаВзаиморасчетов = ?(МетаданныеДокумента.Реквизиты.Найти("ДоговорКонтрагента") <> Неопределено, ДокументСсылка.ДоговорКонтрагента.ВалютаВзаиморасчетов, ВалютаДокумента);
	КурсВзаиморасчетов = ?(МетаданныеДокумента.Реквизиты.Найти("КурсВзаиморасчетов") <> Неопределено, ДокументСсылка.КурсВзаиморасчетов, ?(ВалютаДокумента = ВалютаРегламентированногоУчета, 1, ЗаполнениеДокументов.КурсДокумента(ДокументСсылка, ВалютаРегламентированногоУчета)));
	КратностьВзаиморасчетов = ?(МетаданныеДокумента.Реквизиты.Найти("КратностьВзаиморасчетов") <> Неопределено, ДокументСсылка.КратностьВзаиморасчетов, ?(ВалютаДокумента = ВалютаРегламентированногоУчета, 1, ЗаполнениеДокументов.КратностьДокумента(ДокументСсылка, ВалютаРегламентированногоУчета)));
	ВедениеВзаиморасчетов = ?(ТипЗнч(Договор) = Тип("СправочникСсылка.ДоговорыКонтрагентов"), Договор.ВедениеВзаиморасчетов, Перечисления.ВедениеВзаиморасчетовПоДоговорам.ПоДоговоруВЦелом);
	
	Сделка = ?(МетаданныеДокумента.Реквизиты.Найти("Сделка") <> Неопределено, ?(НЕ ЗначениеЗаполнено(ДокументСсылка.Сделка), Неопределено, ДокументСсылка.Сделка), Неопределено);
		
	ДатаОплаты = ?(МетаданныеДокумента.Реквизиты.Найти("ДатаОплаты") <> Неопределено, ?(ДокументСсылка.Оплачено,УправлениеДенежнымиСредствами.ПолучитьДатуДвижений(ДокументСсылка.Дата,ДокументСсылка.ДатаОплаты),ДокументСсылка.Дата), ДокументСсылка.Дата);
	ВидОперацииДок = ?(МетаданныеДокумента.Реквизиты.Найти("ВидОперации") <> Неопределено, ДокументСсылка.ВидОперации, Неопределено);
	
	//Сбор информации о заказах и расчетных документах
	ТаблицаРД = ПустаяТаблицаРД;
	Если МетаданныеДокумента.ТабличныеЧасти.Найти("ДокументыРасчетовСКонтрагентом") <> Неопределено Тогда
		ТаблицаРД = ДокументСсылка.ДокументыРасчетовСКонтрагентом.Выгрузить();
	КонецЕсли;
	
	Если ТаблицаРД.Колонки.Найти("Сделка") = Неопределено Тогда
		ТаблицаРД.Колонки.Добавить("Сделка");
		ТаблицаРД.ЗаполнитьЗначения(Сделка, "Сделка");
	Иначе
		ТаблицаРД.Колонки.Сделка.Имя = "СделкаИсточника";
		ТаблицаРД.Колонки.Добавить("Сделка");
		Для Каждого Строка Из ТаблицаРД Цикл
			Строка.Сделка = ?(НЕ ЗначениеЗаполнено(Строка.СделкаИсточника), Неопределено, Строка.СделкаИсточника);
		КонецЦикла;
	КонецЕсли;
	
	Если ВедениеВзаиморасчетов = Перечисления.ВедениеВзаиморасчетовПоДоговорам.ПоСчетам Тогда
		ТаблицаРД.ЗаполнитьЗначения(Сделка, "Сделка");
	ИначеЕсли ТаблицаРД.Количество() = 0 Тогда
		Если МетаданныеДокумента.ТабличныеЧасти.Найти("Товары") <> Неопределено Тогда
			Если ОбщегоНазначения.ЕстьРеквизитТабЧастиДокумента("ЗаказПокупателя", МетаданныеДокумента,"Товары") И (ВидОперации <> "Поступление") Тогда
				ИмяРеквизитаСделки = "ЗаказПокупателя";
			ИначеЕсли ОбщегоНазначения.ЕстьРеквизитТабЧастиДокумента("ЗаказПоставщику", МетаданныеДокумента,"Товары") Тогда
				ИмяРеквизитаСделки = "ЗаказПоставщику";
			Иначе
				ИмяРеквизитаСделки = "";
			КонецЕсли;
			Если НЕ ИмяРеквизитаСделки = "" Тогда
				ДополнятьНДС = ?(МетаданныеДокумента.Реквизиты.Найти("СуммаВключаетНДС") <> Неопределено, НЕ ДокументСсылка.СуммаВключаетНДС, Ложь);
				Для Каждого ТЧ из МетаданныеДокумента.ТабличныеЧасти Цикл
					Если ОбщегоНазначения.ЕстьРеквизитТабЧастиДокумента(ИмяРеквизитаСделки, МетаданныеДокумента,ТЧ.Имя) Тогда
						Если ОбщегоНазначения.ЕстьРеквизитТабЧастиДокумента("Сумма", МетаданныеДокумента,ТЧ.Имя) Тогда
							ДополнятьНДСВТЧ = ?(ДополнятьНДС, МетаданныеДокумента.Реквизиты.Найти("СуммаНДС") <> Неопределено, Ложь);
							ТаблицаРДТЧ = ДокументСсылка[ТЧ.Имя].Выгрузить();
							ТаблицаРДТЧ.Свернуть(ИмяРеквизитаСделки,"Сумма" + ?(ДополнятьНДСВТЧ, ", СуммаНДС", "")); 
							Для Каждого СтрокаРДТЧ Из ТаблицаРДТЧ Цикл
								СтрокаРД = ТаблицаРД.Найти(СтрокаРДТЧ[ИмяРеквизитаСделки], "Сделка");
								Если СтрокаРД = Неопределено Тогда
									СтрокаРД = ТаблицаРД.Добавить();
									СтрокаРД.Сделка = СтрокаРДТЧ[ИмяРеквизитаСделки];
									СтрокаРД.СуммаВзаиморасчетов = ?(ДополнятьНДСВТЧ, СтрокаРДТЧ.Сумма + СтрокаРДТЧ.СуммаНДС, СтрокаРДТЧ.Сумма);
								Иначе
									СтрокаРД.СуммаВзаиморасчетов = СтрокаРД.СуммаВзаиморасчетов + ?(ДополнятьНДСВТЧ, СтрокаРДТЧ.Сумма + СтрокаРДТЧ.СуммаНДС, СтрокаРДТЧ.Сумма);
								КонецЕсли;
							КонецЦикла;
						КонецЕсли;
					КонецЕсли;
				КонецЦикла;				
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
		
	УчетнаяПолитика = ОбщегоНазначения.ПолучитьПараметрыУчетнойПолитикиРегл(ДатаОплаты, ДокументСсылка.Организация);
	
	Если НЕ (УчетнаяПолитика.СистемаНалогообложения = Перечисления.СистемыНалогообложения.Упрощенная) Тогда
		Возврат;
	ИначеЕсли УчетнаяПолитика.ОбъектНалогообложенияУСН = Перечисления.ОбъектыНалогообложенияПоУСН.Доходы Тогда
		Возврат;
	КонецЕсли;
	
	ДвиженияРегистров = Неопределено;
	
	Если Параметры = Неопределено Тогда
		СтруктураШапкиДокумента = Новый Структура("Ссылка, Организация, Дата", ДокументСсылка, ДокументСсылка.Организация, ДокументСсылка.Дата);
	Иначе
		СтруктураШапкиДокумента = Параметры;
	КонецЕсли;
	
	СтруктураШапкиДокумента.Вставить("ВалютаВзаиморасчетов",ВалютаВзаиморасчетов);
	СтруктураШапкиДокумента.Вставить("ВалютаРегламентированногоУчета",ВалютаРегламентированногоУчета);
	СтруктураШапкиДокумента.Вставить("ВалютаДокумента",ВалютаДокумента);
	СтруктураШапкиДокумента.Вставить("ДатаОплаты",ДатаОплаты);
	СтруктураШапкиДокумента.Вставить("ВидОперации",ВидОперацииДок);
	СтруктураШапкиДокумента.Вставить("ДоговорКонтрагента",Договор);
	СтруктураШапкиДокумента.Вставить("ПорядокПризнанияРасходовПоНДС",УчетнаяПолитика.ПорядокПризнанияРасходовПоНДС);
	СтруктураШапкиДокумента.Вставить("ПорядокПризнанияРасходовПоТоварам",УчетнаяПолитика.ПорядокПризнанияРасходовПоТоварам);
	СтруктураШапкиДокумента.Вставить("ПорядокПризнанияМатериальныхРасходов",УчетнаяПолитика.ПорядокПризнанияМатериальныхРасходов);
	СтруктураШапкиДокумента.Вставить("МетодРаспределения",УчетнаяПолитика.МетодРаспределенияРасходовУСНПоВидамДеятельности);
	СтруктураШапкиДокумента.Вставить("БазаРаспределения",УчетнаяПолитика.БазаРаспределенияРасходовУСНПоВидамДеятельности);
	СтруктураШапкиДокумента.Вставить("Сделка",Сделка);
	
	Если ВидОперации = Неопределено Тогда
		ВидОперации = ВидОперацииДДС(СтруктураШапкиДокумента);
	КонецЕсли;
	
	Если ВидОперации = "НеОтражатьВНУ" Тогда
		Возврат;
	КонецЕсли;
		
	КУДиР = Новый Структура("ДоходЕНВД, Графа4, Графа5, Графа6, Графа7, НДС, Содержание", 0,0,0,0,0,0,"");
	
	КУДиР.Содержание = СодержаниеКУДиР(СтруктураШапкиДокумента, ВидОперации, МетаданныеДокумента);
	
	Если МетаданныеДокумента.Реквизиты.Найти("ВалютаДокумента") <> Неопределено Тогда
		СтруктураШапкиДокумента.Вставить("КурсДокумента", 		ЗаполнениеДокументов.КурсДокумента(ДокументСсылка, ВалютаРегламентированногоУчета));
		СтруктураШапкиДокумента.Вставить("КратностьДокумента", 	ЗаполнениеДокументов.КратностьДокумента(ДокументСсылка, ВалютаРегламентированногоУчета));
	Иначе
		СтруктураШапкиДокумента.Вставить("КурсДокумента", 		1);
		СтруктураШапкиДокумента.Вставить("КратностьДокумента", 	1);
	КонецЕсли;

	
	Если (ВидОперации = "Поступление")
		ИЛИ (ВидОперации = "УслугаКомиссионера")
		ИЛИ (ВидОперации = "УслугаПоПереработке") Тогда
		
		НаборЗаписей = РегистрыНакопления.РасходыПриУСН.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Регистратор.Установить(ДокументСсылка);
		НаборЗаписей.Прочитать();
		ТаблицаПрихода = НаборЗаписей.Выгрузить();
		
		Статусы = Новый СписокЗначений;
		Статусы.Добавить(Перечисления.СтатусыРасходовУСН.НеСписаноНеОплачено);
		Статусы.Добавить(Перечисления.СтатусыРасходовУСН.НеОплачено);
		Статусы.Добавить(Перечисления.СтатусыРасходовУСН.НеОплаченоНеОплаченоПокупателем);
		Если ЭтапПроведения > 0 Тогда
			Статусы.Добавить(Перечисления.СтатусыРасходовУСН.НеОплачено);
		КонецЕсли;
		
		КолвоЭлементовКоллекции = ТаблицаПрихода.Количество(); 
		Для ОбратныйИндекс = 1 По КолвоЭлементовКоллекции Цикл
			ЭлементКоллекции = ТаблицаПрихода[КолвоЭлементовКоллекции - ОбратныйИндекс];
			Если (Статусы.НайтиПоЗначению(ЭлементКоллекции.СтатусыОплатыРасходовУСН)=Неопределено)
				ИЛИ (ЭлементКоллекции.ВидДвижения = ВидДвиженияНакопления.Расход) Тогда
				ТаблицаПрихода.Удалить(ЭлементКоллекции);
			КонецЕсли;
		КонецЦикла;
		
		УчетАгентскогоНДС = Ложь;
	    	
		Если ТипЗнч(Договор) = Тип("СправочникСсылка.ДоговорыКонтрагентов") Тогда
			УчетАгентскогоНДС = Договор.УчетАгентскогоНДС;
		КонецЕсли;
		
		Если ВидОперации = "УслугаКомиссионера" Тогда
			СуммаВзаиморасчетов   = МодульВалютногоУчета.ПересчитатьИзВалютыВВалюту(ДокументСсылка.Товары.Итог("СуммаВознаграждения"), ВалютаДокумента,
														   СтруктураШапкиДокумента.ВалютаВзаиморасчетов,
														   СтруктураШапкиДокумента.КурсДокумента, КурсВзаиморасчетов,
														   СтруктураШапкиДокумента.КратностьДокумента, КратностьВзаиморасчетов); 
		ИначеЕсли ВидОперации = "УслугаПоПереработке" Тогда
			СуммаДокумента = ДокументСсылка.Товары.Итог("Сумма") + ДокументСсылка.Услуги.Итог("Сумма"); //~
			СуммаВзаиморасчетов   = МодульВалютногоУчета.ПересчитатьИзВалютыВВалюту(СуммаДокумента, ВалютаДокумента,
														   СтруктураШапкиДокумента.ВалютаВзаиморасчетов,
														   СтруктураШапкиДокумента.КурсДокумента, КурсВзаиморасчетов,
														   СтруктураШапкиДокумента.КратностьДокумента, КратностьВзаиморасчетов); 
		Иначе
			СуммаВзаиморасчетов   = МодульВалютногоУчета.ПересчитатьИзВалютыВВалюту(УчетНДС.ПолучитьСуммуДокументаСНДС(ДокументСсылка), ВалютаДокумента,
														   СтруктураШапкиДокумента.ВалютаВзаиморасчетов,
														   СтруктураШапкиДокумента.КурсДокумента, КурсВзаиморасчетов,
														   СтруктураШапкиДокумента.КратностьДокумента, КратностьВзаиморасчетов); 
														   
			//Учтем агентский НДС
			Если УчетАгентскогоНДС Тогда
				СуммаВзаиморасчетовНДС   = МодульВалютногоУчета.ПересчитатьИзВалютыВВалюту(УчетНДС.ПолучитьНДСДокумента(ДокументСсылка), ВалютаДокумента,
														   СтруктураШапкиДокумента.ВалютаВзаиморасчетов,
														   СтруктураШапкиДокумента.КурсДокумента, КурсВзаиморасчетов,
														   СтруктураШапкиДокумента.КратностьДокумента, КратностьВзаиморасчетов);
														   
				СуммаВзаиморасчетов = СуммаВзаиморасчетов - СуммаВзаиморасчетовНДС;
				ТаблицаПриходаНДС = ТаблицаПрихода.СкопироватьКолонки();
				
				СтруктураОтбора = Новый Структура;
				СтруктураОтбора.Вставить("ВидРасхода",  Перечисления.ВидыРасходовУСН.Налоги);
				
				НайденныеСтроки = ТаблицаПрихода.НайтиСтроки(СтруктураОтбора);
				КоличествоСтрок = НайденныеСтроки.Количество();
				Для Индекс = 1 По КоличествоСтрок Цикл
					Строка = НайденныеСтроки[КоличествоСтрок - Индекс];
					СтрокаНДС = ТаблицаПриходаНДС.Добавить();
					ЗаполнитьЗначенияСвойств(СтрокаНДС, Строка);
					ТаблицаПрихода.Удалить(Строка);
				КонецЦикла;
				
				ПустаяТаблицаРД = ТаблицаРД.СкопироватьКолонки();
				ЗачетАвансаПоставщикуУСН(СтруктураШапкиДокумента, ТаблицаПриходаНДС, ДвиженияРегистров, ПланыСчетов.Хозрасчетный.НДС, Неопределено, ПустаяТаблицаРД, СуммаВзаиморасчетовНДС, (ЭтапПроведения > 0));
		    КонецЕсли;
		КонецЕсли;												   
		
		ЗачетАвансаПоставщикуУСН(СтруктураШапкиДокумента, ТаблицаПрихода, ДвиженияРегистров, Договор, Сделка, ТаблицаРД, СуммаВзаиморасчетов, (ЭтапПроведения > 0));
		//КУДиР.Содержание = "Поступление в счет аванса. ";
		
	ИначеЕсли (ВидОперации = "ВозвратОтПокупателя") Тогда
		
		НаборЗаписей = РегистрыНакопления.ПартииТоваровНаСкладахНалоговыйУчет.СоздатьНаборЗаписей();
		
		НаборЗаписей.Отбор.Регистратор.Установить(ДокументСсылка);
		НаборЗаписей.Прочитать();
		ТаблицаПартий = НаборЗаписей.Выгрузить();
		
		//Расходы по указанным документам реализации
		ТаблицаПоТоварам = Новый ТаблицаЗначений();
		Заголовок 				= Неопределено;
		СтруктураШапкиВозврата 	= Неопределено;
		
		ДокументОбъект = ДокументСсылка.ПолучитьОбъект();
		ДокументОбъект.ПодготовитьСтруктуруШапкиДокумента(Заголовок, СтруктураШапкиВозврата);
		ДокументОбъект.ПодготовитьТаблицыДокумента(СтруктураШапкиВозврата, ТаблицаПоТоварам);
		УправлениеЗапасами.СформироватьТаблицуКомплектующих(ТаблицаПоТоварам, ДокументОбъект, СтруктураШапкиВозврата);
		
		Таблица = ТаблицаПоТоварам.Скопировать();
		Если НЕ Таблица.Колонки.Найти("КоличествоДок") = Неопределено Тогда
			Таблица.Колонки.КоличествоДок.Имя = "Количество";
		КонецЕсли;
		
		МассивДокументовРеализации = Таблица.ВыгрузитьКолонку("ДокументПартии");
		ОбщегоНазначения.УдалитьПовторяющиесяЭлементы(МассивДокументовРеализации);
						
		Для Каждого ДокументРеализации ИЗ МассивДокументовРеализации Цикл
			Если НЕ ЗначениеЗаполнено(ДокументРеализации) Тогда
				Продолжить;
			КонецЕсли;
			Таблица.Очистить();
			СтруктураШапкиДокумента.Вставить("Сделка", ДокументРеализации);
			
			СтруктураОтбора = Новый Структура;
			СтруктураОтбора.Вставить("ДокументПартии",	ДокументРеализации);
		    НайденныеСтроки = ТаблицаПоТоварам.НайтиСтроки(СтруктураОтбора);
			
			Для Каждого Строка Из НайденныеСтроки Цикл
				НоваяСтрока = Таблица.Добавить();
				НоваяСтрока.Номенклатура 				= Строка.Номенклатура;
				НоваяСтрока.ХарактеристикаНоменклатуры 	= Строка.ХарактеристикаНоменклатуры;
				НоваяСтрока.СерияНоменклатуры 			= Строка.СерияНоменклатуры;
				НоваяСтрока.СчетУчетаБУ 				= Строка.СчетУчетаБУ;
				Если НЕ ТаблицаПоТоварам.Колонки.Найти("Количество") = Неопределено Тогда
					НоваяСтрока.Количество 				= Строка.Количество;
				ИначеЕсли НЕ ТаблицаПоТоварам.Колонки.Найти("КоличествоДок") = Неопределено Тогда
					НоваяСтрока.Количество 			    = Строка.КоличествоДок;
				КонецЕсли;
			КонецЦикла;
			
			ВозвратПоДокументуУСН(СтруктураШапкиДокумента, Таблица, ДвиженияРегистров, КУДиР);
		КонецЦикла;
		
		ТаблицаВозврата	= ДоляЕНВДиКомиссииВозврат(СтруктураШапкиДокумента, ТаблицаПартий, ДвиженияРегистров);
		
		КЗ = 1; //Коэффициент знака взаиморасчетов (+1 или -1)
		
		//Формирование таблицы фильтров
		ТаблицаФильтров = Новый ТаблицаЗначений;
		ТаблицаФильтров.Колонки.Добавить("ИмяПоля");
		ТаблицаФильтров.Колонки.Добавить("ЗначениеПоля");
		ТаблицаФильтров.Колонки.Добавить("Условие");
		
		Фильтр = ТаблицаФильтров.Добавить();
		Фильтр.ИмяПоля			= "ДоговорКонтрагента";
		Фильтр.ЗначениеПоля	= СтруктураШапкиДокумента.ДоговорКонтрагента;
		Фильтр.Условие			= "=";
			
		МоментДокумента = СтруктураШапкиДокумента.Ссылка.МоментВремени();
		ТаблицаПлатежей = ВыполнитьЗапросПоВзаиморасчетамУСН(МоментДокумента, СтруктураШапкиДокумента.Организация, ТаблицаФильтров);
		
		Если ДвиженияРегистров = Неопределено Тогда
			ТаблицаВзаиморасчетов = РегистрыНакопления.ВзаиморасчетыУСН.СоздатьНаборЗаписей().Выгрузить();
			ТаблицаВзаиморасчетов.Очистить();
			ТаблицаРезультат = РегистрыНакопления.РасходыПриУСН.СоздатьНаборЗаписей().Выгрузить();
			ТаблицаРезультат.Очистить();
			
			ДвиженияРегистров = Новый Структура("ВзаиморасчетыРасход, РасходыУСН, КУДиР",
								ТаблицаВзаиморасчетов, ТаблицаРезультат, ТаблицаРезультат);
		КонецЕсли; 
							
		Если ДвиженияРегистров.ВзаиморасчетыРасход.Колонки.Найти("ВидДвижения") = Неопределено Тогда
			ДвиженияРегистров.ВзаиморасчетыРасход.Колонки.Добавить("ВидДвижения");
		КонецЕсли;
	
		//Взаиморасчеты для указанных реализаций
		Для Каждого Строка Из ТаблицаВозврата Цикл
			Если НЕ ЗначениеЗаполнено(Строка.ДокументПартии) Тогда
				Продолжить;
			КонецЕсли;
			СтруктураОтбора = Новый Структура;
			СтруктураОтбора.Вставить("РасчетныйДокумент",  Строка.ДокументПартии);
			НайденныеСтроки = ТаблицаПлатежей.НайтиСтроки(СтруктураОтбора);
			
			Для Каждого СтрокаРД Из НайденныеСтроки Цикл
				ЕНВДПоСтроке = Мин(Строка.ЕНВД, СтрокаРД.ДоходЕНВД);
				КомиссияПоСтроке = Мин(Строка.Комиссия, СтрокаРД.ДоходКомитента);
				ОстатокПоСтроке = Мин((Строка.Всего - Строка.ЕНВД - Строка.Комиссия), (СтрокаРД.СуммаВзаиморасчетов - СтрокаРД.ДоходЕНВД - СтрокаРД.ДоходКомитента));
				
				ВсегоПоСтроке = ЕНВДПоСтроке + КомиссияПоСтроке + ОстатокПоСтроке;
				
				Если ВсегоПоСтроке > 0 Тогда
					НоваяСтрока = ДвиженияРегистров.ВзаиморасчетыРасход.Добавить();
					ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаРД);
					
					НоваяСтрока.СуммаВзаиморасчетов = ВсегоПоСтроке;
					НоваяСтрока.ДоходКомитента	  	= КомиссияПоСтроке;
					НоваяСтрока.ДоходЕНВД	  		= ЕНВДПоСтроке;
					НоваяСтрока.ВидДвижения			= ВидДвиженияНакопления.Расход;
					
					Строка.ЕНВД 					= Строка.ЕНВД - ЕНВДПоСтроке;
					Строка.Комиссия 				= Строка.Комиссия - КомиссияПоСтроке;
					Строка.Всего 					= Строка.Всего - ВсегоПоСтроке;
					СтрокаРД.СуммаВзаиморасчетов 	= СтрокаРД.СуммаВзаиморасчетов - ВсегоПоСтроке;
					СтрокаРД.ДоходКомитента	  		= СтрокаРД.ДоходКомитента - КомиссияПоСтроке;
					СтрокаРД.ДоходЕНВД	  			= СтрокаРД.ДоходЕНВД - ЕНВДПоСтроке;
				КонецЕсли;
			КонецЦикла;
		КонецЦикла;
		
		//Взаиморасчеты для не указанных реализаций
		Для Каждого Строка Из ТаблицаВозврата Цикл
			Если ЗначениеЗаполнено(Строка.ДокументПартии) Тогда
				Продолжить;
			КонецЕсли;
			
			Для Каждого СтрокаРД Из ТаблицаПлатежей Цикл
				ЕНВДПоСтроке = Мин(Строка.ЕНВД, СтрокаРД.ДоходЕНВД);
				КомиссияПоСтроке = Мин(Строка.Комиссия, СтрокаРД.ДоходКомитента);
				ОстатокПоСтроке = Мин((Строка.Всего - Строка.ЕНВД - Строка.Комиссия), (СтрокаРД.СуммаВзаиморасчетов - СтрокаРД.ДоходЕНВД - СтрокаРД.ДоходКомитента));
				
				ВсегоПоСтроке = ЕНВДПоСтроке + КомиссияПоСтроке + ОстатокПоСтроке;
				
				Если ВсегоПоСтроке > 0 Тогда
					НоваяСтрока = ДвиженияРегистров.ВзаиморасчетыРасход.Добавить();
					ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаРД);
					
					НоваяСтрока.СуммаВзаиморасчетов = ВсегоПоСтроке;
					НоваяСтрока.ДоходКомитента	  	= КомиссияПоСтроке;
					НоваяСтрока.ДоходЕНВД	  		= ЕНВДПоСтроке;
					НоваяСтрока.ВидДвижения			= ВидДвиженияНакопления.Расход;
					
					Строка.ЕНВД 					= Строка.ЕНВД - ЕНВДПоСтроке;
					Строка.Комиссия 				= Строка.Комиссия - КомиссияПоСтроке;
					Строка.Всего 					= Строка.Всего - ВсегоПоСтроке;
					СтрокаРД.СуммаВзаиморасчетов 	= СтрокаРД.СуммаВзаиморасчетов - ВсегоПоСтроке;
					СтрокаРД.ДоходКомитента	  		= СтрокаРД.ДоходКомитента - КомиссияПоСтроке;
					СтрокаРД.ДоходЕНВД	  			= СтрокаРД.ДоходЕНВД - ЕНВДПоСтроке;
				КонецЕсли;
			КонецЦикла;
		КонецЦикла;
		
		СуммаВзаиморасчетов  = МодульВалютногоУчета.ПересчитатьИзВалютыВВалюту(ТаблицаВозврата.Итог("Всего"), ВалютаДокумента, СтруктураШапкиДокумента.ВалютаВзаиморасчетов,
																СтруктураШапкиДокумента.КурсДокумента, КурсВзаиморасчетов,
																СтруктураШапкиДокумента.КратностьДокумента, КратностьВзаиморасчетов);
		СуммаЕНВД		= МодульВалютногоУчета.ПересчитатьИзВалютыВВалюту(ТаблицаВозврата.Итог("ЕНВД"), ВалютаДокумента, СтруктураШапкиДокумента.ВалютаВзаиморасчетов,
																СтруктураШапкиДокумента.КурсДокумента, КурсВзаиморасчетов,
																СтруктураШапкиДокумента.КратностьДокумента, КратностьВзаиморасчетов); 
		СуммаКомиссии	= МодульВалютногоУчета.ПересчитатьИзВалютыВВалюту(ТаблицаВозврата.Итог("Комиссия"), ВалютаДокумента, СтруктураШапкиДокумента.ВалютаВзаиморасчетов,
																СтруктураШапкиДокумента.КурсДокумента, КурсВзаиморасчетов,
																СтруктураШапкиДокумента.КратностьДокумента, КратностьВзаиморасчетов);
																
		//КУДиР.Содержание = "Возврат товаров от покупателя. ";
		
		Если (СуммаЕНВД <> 0) ИЛИ (СуммаКомиссии <> 0) ИЛИ (СуммаВзаиморасчетов <> 0) Тогда

			Если СуммаКомиссии > 0 Тогда
				СуммаКоррДоходов  = СуммаКомиссии * СтруктураШапкиДокумента.КурсДокумента/СтруктураШапкиДокумента.КратностьДокумента;
				КУДиР.Графа5 = КУДиР.Графа5 + СуммаКоррДоходов;
				КУДиР.Содержание = КУДиР.Содержание + "Задолженность по возврату комиссинных товаров " + СуммаКоррДоходов + " руб. определена как аванс, ранее не включенный в доходы. ";
			КонецЕсли;
			Если СуммаЕНВД > 0 Тогда
				СуммаКоррДоходов  = СуммаЕНВД * СтруктураШапкиДокумента.КурсДокумента/СтруктураШапкиДокумента.КратностьДокумента;
				КУДиР.Графа5 = КУДиР.Графа5 + СуммаКоррДоходов;
				КУДиР.ДоходЕНВД	 = КУДиР.ДоходЕНВД - СуммаКоррДоходов;
				КУДиР.Содержание = КУДиР.Содержание + "Задолженность по возврату товаров ЕНВД " + СуммаКоррДоходов + " руб. определена как аванс, ранее не включенный в доходы. ";
			КонецЕсли;
				
			ДвижениеВзаиморасчетовУСН(ДвиженияРегистров.ВзаиморасчетыРасход, СтруктураШапкиДокумента.Организация, СтруктураШапкиДокумента.ДоговорКонтрагента, СтруктураШапкиДокумента.Ссылка, СтруктураШапкиДокумента.Ссылка, - СуммаВзаиморасчетов, 0, 0);
		КонецЕсли;															
		
	ИначеЕсли (ВидОперации = "Перемещение") 
		ИЛИ (ВидОперации = "Комплектация")
		ИЛИ (ВидОперации = "НаРеализацию")
		ИЛИ (ВидОперации = "ВПереработку")
		ИЛИ (ВидОперации = "ВозвратСРеализации")
		ИЛИ (ВидОперации = "ВозвратИзПереработки") Тогда
		
		Если ВидОперации = "ВозвратСРеализации" Тогда
			НаборЗаписей = РегистрыНакопления.ПартииТоваровПереданныеНалоговыйУчет.СоздатьНаборЗаписей();
		ИначеЕсли ВидОперации = "ВозвратИзПереработки" Тогда
			НаборЗаписей = РегистрыНакопления.ПартииТоваровПереданныеНалоговыйУчет.СоздатьНаборЗаписей();
		Иначе
			НаборЗаписей = РегистрыНакопления.ПартииТоваровНаСкладахНалоговыйУчет.СоздатьНаборЗаписей();
		КонецЕсли;
		
		НаборЗаписей.Отбор.Регистратор.Установить(ДокументСсылка);
		НаборЗаписей.Прочитать();
		ТаблицаПартийРасход = НаборЗаписей.Выгрузить();
		
		//Удалим движения прихода
		КолСтрок = ТаблицаПартийРасход.Количество();
		Для Номер = 1 по КолСтрок Цикл
			Если ТаблицаПартийРасход[КолСтрок - Номер].ВидДвижения = ВидДвиженияНакопления.Приход Тогда
				ТаблицаПартийРасход.Удалить(ТаблицаПартийРасход[КолСтрок - Номер]);
			КонецЕсли;
		КонецЦикла;
		
		Если ВидОперации = "НаРеализацию" Тогда
			НаборЗаписей = РегистрыНакопления.ПартииТоваровПереданныеНалоговыйУчет.СоздатьНаборЗаписей();
		ИначеЕсли ВидОперации = "ВПереработку" Тогда
			НаборЗаписей = РегистрыНакопления.ПартииТоваровПереданныеНалоговыйУчет.СоздатьНаборЗаписей();
		Иначе
			НаборЗаписей = РегистрыНакопления.ПартииТоваровНаСкладахНалоговыйУчет.СоздатьНаборЗаписей();
		КонецЕсли;
		
		НаборЗаписей.Отбор.Регистратор.Установить(ДокументСсылка);
		НаборЗаписей.Прочитать();
		ТаблицаПартийПриход = НаборЗаписей.Выгрузить();
		
		//Удалим движения расхода
		КолСтрок = ТаблицаПартийПриход.Количество();
		Для Номер = 1 по КолСтрок Цикл
			Если ТаблицаПартийПриход[КолСтрок - Номер].ВидДвижения = ВидДвиженияНакопления.Расход Тогда
				ТаблицаПартийПриход.Удалить(ТаблицаПартийПриход[КолСтрок - Номер]);
			КонецЕсли;
		КонецЦикла;
		
		Договор = ?(МетаданныеДокумента.Реквизиты.Найти("ДоговорКонтрагента") <> Неопределено, ДокументСсылка.ДоговорКонтрагента, Неопределено);
		
		ВидРасхода = Перечисления.ВидыРасходовУСН.Номенклатура;
		Если ВидОперации = "ВозвратСРеализации" Тогда
			СтатусПартии = Перечисления.СтатусыПартийУСН.НаРеализации;
		ИначеЕсли ВидОперации = "ВозвратИзПереработки" Тогда
			СтатусПартии = Перечисления.СтатусыПартийУСН.ВПереработке;
		Иначе
			СтатусПартии = Перечисления.СтатусыПартийУСН.Купленные;
		КонецЕсли;
		
		ДвиженияРегистровВх = Неопределено;
		
		ПеремещениеРасходовУСН(СтруктураШапкиДокумента, ТаблицаПартийПриход, ТаблицаПартийРасход, ДвиженияРегистровВх, ВидРасхода, Договор, СтатусПартии, ВидОперации);
		
		ДвиженияРегистров = ДвиженияРегистровВх;
		
	ИначеЕсли (ВидОперации = "Списание") 
		ИЛИ (ВидОперации = "Реализация")
		ИЛИ (ВидОперации = "ВПроизводство") //~
		ИЛИ (ВидОперации = "Розница") 
		ИЛИ (ВидОперации = "ВозвратПоставщику") Тогда
		
		НаборЗаписей = РегистрыНакопления.ПартииТоваровНаСкладахНалоговыйУчет.СоздатьНаборЗаписей();
		
		НаборЗаписей.Отбор.Регистратор.Установить(ДокументСсылка);
		НаборЗаписей.Прочитать();
		ТаблицаПартий = НаборЗаписей.Выгрузить();
		
		//Удалим движения прихода
		КолСтрок = ТаблицаПартий.Количество();
		Для Номер = 1 по КолСтрок Цикл
			Если ТаблицаПартий[КолСтрок - Номер].ВидДвижения = ВидДвиженияНакопления.Приход Тогда
				ТаблицаПартий.Удалить(ТаблицаПартий[КолСтрок - Номер]);
			КонецЕсли;
		КонецЦикла;
		
		Договор = ?(МетаданныеДокумента.Реквизиты.Найти("ДоговорКонтрагента") <> Неопределено, ДокументСсылка.ДоговорКонтрагента, Неопределено);
		
		ВидРасхода = Перечисления.ВидыРасходовУСН.Номенклатура;
		СтатусПартии = Перечисления.СтатусыПартийУСН.Купленные;
		
		ДвиженияРегистровВх = Неопределено;
		
		//Дополним таблицу партий колонкой со статусами расходов по списанию
		ЗаполнитьОтражениеВНУпоСписанию(СтруктураШапкиДокумента, ТаблицаПартий, ВидОперации);
		
		СписаниеРасходовУСН(СтруктураШапкиДокумента, ТаблицаПартий, ДвиженияРегистровВх, ВидРасхода, Договор, СтатусПартии, ВидОперации);
		
		Если ВидОперации = "Реализация" Тогда
			Выручка 			 = ДоляЕНВДиКомиссии(СтруктураШапкиДокумента, ТаблицаПартий, ДвиженияРегистровВх);
			СуммаЕНВД			 = МодульВалютногоУчета.ПересчитатьИзВалютыВВалюту(Выручка.ЕНВД, ВалютаДокумента,
																СтруктураШапкиДокумента.ВалютаВзаиморасчетов,
																СтруктураШапкиДокумента.КурсДокумента, КурсВзаиморасчетов,
																СтруктураШапкиДокумента.КратностьДокумента, КратностьВзаиморасчетов); 
			СуммаКомиссии		 = МодульВалютногоУчета.ПересчитатьИзВалютыВВалюту(Выручка.Комиссия, ВалютаДокумента,
																СтруктураШапкиДокумента.ВалютаВзаиморасчетов,
																СтруктураШапкиДокумента.КурсДокумента, КурсВзаиморасчетов,
																СтруктураШапкиДокумента.КратностьДокумента, КратностьВзаиморасчетов); 
			СуммаВзаиморасчетов  = МодульВалютногоУчета.ПересчитатьИзВалютыВВалюту(УчетНДС.ПолучитьСуммуДокументаСНДС(ДокументСсылка), ВалютаДокумента,
																СтруктураШапкиДокумента.ВалютаВзаиморасчетов,
																СтруктураШапкиДокумента.КурсДокумента, КурсВзаиморасчетов,
																СтруктураШапкиДокумента.КратностьДокумента, КратностьВзаиморасчетов); 
																
			//Если (Договор.ВедениеВзаиморасчетов = Перечисления.ВедениеВзаиморасчетовПоДоговорам.ПоРасчетнымДокументам) И (Сделка = Неопределено) Тогда
			//	Сделка = СтруктураШапкиДокумента.Ссылка;
			//КонецЕсли;
			
			ЗачетАвансаОтПокупателяУСН(СтруктураШапкиДокумента, ДвиженияРегистровВх.ТаблицаПриход, ДвиженияРегистровВх, ДвиженияРегистров, Договор, Сделка, ТаблицаРД, СуммаВзаиморасчетов, СуммаЕНВД, СуммаКомиссии, КУДиР);
			//КУДиР.Содержание = "Реализация товаров. ";
			
		ИначеЕсли (ВидОперации = "Списание") ИЛИ (ВидОперации = "ВПроизводство") Тогда
			
			ДвиженияРегистров = ДвиженияРегистровВх;
			Если ТипЗнч(СтруктураШапкиДокумента.Ссылка) = Тип("ДокументСсылка.СписаниеТоваров") Тогда
				//КУДиР.Содержание = "Списание ТМЦ. ";
			Иначе
				//КУДиР.Содержание = "Передача материалов в производство. ";
			КонецЕсли;
			
		ИначеЕсли ВидОперации = "Розница" Тогда
			
			ДвиженияРегистров = ДвиженияРегистровВх;
			//КУДиР.Содержание = "Продажа в розницу. ";
			Выручка 			 = ДоляЕНВДиКомиссии(СтруктураШапкиДокумента, ТаблицаПартий, ДвиженияРегистровВх);
			СуммаЕНВД			 = МодульВалютногоУчета.ПересчитатьИзВалютыВВалюту(Выручка.ЕНВД, ВалютаДокумента,
																СтруктураШапкиДокумента.ВалютаВзаиморасчетов,
																СтруктураШапкиДокумента.КурсДокумента, КурсВзаиморасчетов,
																СтруктураШапкиДокумента.КратностьДокумента, КратностьВзаиморасчетов); 
			СуммаКомиссии		 = МодульВалютногоУчета.ПересчитатьИзВалютыВВалюту(Выручка.Комиссия, ВалютаДокумента,
																СтруктураШапкиДокумента.ВалютаВзаиморасчетов,
																СтруктураШапкиДокумента.КурсДокумента, КурсВзаиморасчетов,
																СтруктураШапкиДокумента.КратностьДокумента, КратностьВзаиморасчетов);
			СуммаВзаиморасчетов  = МодульВалютногоУчета.ПересчитатьИзВалютыВВалюту(УчетНДС.ПолучитьСуммуДокументаСНДС(ДокументСсылка), ВалютаДокумента,
																СтруктураШапкиДокумента.ВалютаВзаиморасчетов,
																СтруктураШапкиДокумента.КурсДокумента, КурсВзаиморасчетов,
																СтруктураШапкиДокумента.КратностьДокумента, КратностьВзаиморасчетов); 
																
			ВыделитьБезналичнуюОплату(СтруктураШапкиДокумента, ДвиженияРегистров.ВзаиморасчетыРасход, СуммаВзаиморасчетов, СуммаЕНВД, СуммаКомиссии);
			
			Если СтруктураШапкиДокумента.ВидОперации = Перечисления.ВидыОперацийОтчетОРозничныхПродажах.ОтчетНТТОПродажах Тогда
				КУДиР.Графа5 = КУДиР.Графа5  - СуммаЕНВД - СуммаКомиссии;
				
				Если СуммаКомиссии > 0 Тогда
					КУДиР.Содержание = КУДиР.Содержание + "Розничная выручка на сумму " + СуммаКомиссии + " руб. определена как выручка комитента. ";
				КонецЕсли;
				Если СуммаЕНВД > 0 Тогда
					КУДиР.ДоходЕНВД	 = КУДиР.ДоходЕНВД + СуммаЕНВД;
					КУДиР.Содержание = КУДиР.Содержание + "Розничная выручка на сумму " + СуммаЕНВД + " руб. отнесена к деятельности ЕНВД. ";
				КонецЕсли;
			Иначе
				КУДиР.Графа4 = КУДиР.Графа4  + СуммаВзаиморасчетов;
				КУДиР.Графа5 = КУДиР.Графа5  + СуммаВзаиморасчетов - СуммаЕНВД - СуммаКомиссии;
				
				Если СуммаКомиссии > 0 Тогда
					КУДиР.Содержание = КУДиР.Содержание + "Розничная выручка на сумму " + СуммаКомиссии + " руб. определена как выручка комитента. ";
				КонецЕсли;
				Если СуммаЕНВД > 0 Тогда
					КУДиР.ДоходЕНВД	 = КУДиР.ДоходЕНВД + СуммаЕНВД;
					КУДиР.Содержание = КУДиР.Содержание + "Розничная выручка на сумму " + СуммаЕНВД + " руб. отнесена к деятельности ЕНВД. ";
				КонецЕсли;
			КонецЕсли;
		ИначеЕсли ВидОперации = "ВозвратПоставщику" Тогда
						
			Если НЕ Договор.ВидДоговора = Перечисления.ВидыДоговоровКонтрагентов.СКомитентом Тогда
				СуммаВзаиморасчетов  = МодульВалютногоУчета.ПересчитатьИзВалютыВВалюту(УчетНДС.ПолучитьСуммуДокументаСНДС(ДокументСсылка), ВалютаДокумента,
				СтруктураШапкиДокумента.ВалютаВзаиморасчетов,
				СтруктураШапкиДокумента.КурсДокумента, КурсВзаиморасчетов,
				СтруктураШапкиДокумента.КратностьДокумента, КратностьВзаиморасчетов);
				
				ТаблицаОплат = Новый ТаблицаЗначений;
				ТаблицаОплат.Колонки.Добавить("ДоговорКонтрагента");
				ТаблицаОплат.Колонки.Добавить("Сделка");
				ТаблицаОплат.Колонки.Добавить("Сумма");
				
				НоваяСтрока = ТаблицаОплат.Добавить();
				НоваяСтрока.ДоговорКонтрагента = Договор;
				НоваяСтрока.Сделка = ?(НЕ ЗначениеЗаполнено(ДокументСсылка.Сделка), Неопределено, ДокументСсылка.Сделка);
				НоваяСтрока.Сумма = СуммаВзаиморасчетов;
				
				ОплатаПоставщикуУСН(СтруктураШапкиДокумента, ТаблицаОплат, ДвиженияРегистров, КУДиР);
				ДвиженияРегистров.РасходыУСН = ДвиженияРегистровВх.РасходыУСН.Скопировать();
				ДвиженияРегистров.КУДиР = ДвиженияРегистровВх.КУДиР.Скопировать();
				
				КУДиР.Графа6 = 0;
			Иначе
				ДвиженияРегистров = ДвиженияРегистровВх;
			КонецЕсли;
			
		Иначе
			ДвиженияРегистров = ДвиженияРегистровВх;
			
		КонецЕсли;
		
	ИначеЕсли (ВидОперации = "РеализацияКомиссионером") 
		ИЛИ (ВидОперации = "ИзПереработки") Тогда
		
		НаборЗаписей = РегистрыНакопления.ПартииТоваровПереданныеНалоговыйУчет.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Регистратор.Установить(ДокументСсылка);
		НаборЗаписей.Прочитать();
		ТаблицаПартий = НаборЗаписей.Выгрузить();
		
		Договор = ?(МетаданныеДокумента.Реквизиты.Найти("ДоговорКонтрагента") <> Неопределено, ДокументСсылка.ДоговорКонтрагента, Неопределено);
		
		ВидРасхода = Перечисления.ВидыРасходовУСН.Номенклатура;
		Если ВидОперации = "РеализацияКомиссионером" Тогда
			СтатусПартии = Перечисления.СтатусыПартийУСН.НаРеализации;
			ВидОперации = "Реализация";
		ИначеЕсли ВидОперации = "ИзПереработки" Тогда
			СтатусПартии = Перечисления.СтатусыПартийУСН.ВПереработке;
			ВидОперации = "Списание";
		КонецЕсли;
		ДвиженияРегистровВх = Неопределено;
		
		//Дополним таблицу партий колонкой со статусами расходов по списанию
		ЗаполнитьОтражениеВНУпоСписанию(СтруктураШапкиДокумента, ТаблицаПартий, ВидОперации);
		
		СписаниеРасходовУСН(СтруктураШапкиДокумента, ТаблицаПартий, ДвиженияРегистровВх, ВидРасхода, Договор, СтатусПартии, ВидОперации);
		
		Если ВидОперации = "Реализация" Тогда
			Выручка 			 = ДоляЕНВДиКомиссии(СтруктураШапкиДокумента, ТаблицаПартий, ДвиженияРегистровВх);
			СуммаЕНВД			 = МодульВалютногоУчета.ПересчитатьИзВалютыВВалюту(Выручка.ЕНВД, ВалютаДокумента,
																СтруктураШапкиДокумента.ВалютаВзаиморасчетов,
																СтруктураШапкиДокумента.КурсДокумента, КурсВзаиморасчетов,
																СтруктураШапкиДокумента.КратностьДокумента, КратностьВзаиморасчетов); 
			СуммаКомиссии		 = МодульВалютногоУчета.ПересчитатьИзВалютыВВалюту(Выручка.Комиссия, ВалютаДокумента,
																СтруктураШапкиДокумента.ВалютаВзаиморасчетов,
																СтруктураШапкиДокумента.КурсДокумента, КурсВзаиморасчетов,
																СтруктураШапкиДокумента.КратностьДокумента, КратностьВзаиморасчетов); 
			СуммаВзаиморасчетов  = МодульВалютногоУчета.ПересчитатьИзВалютыВВалюту(УчетНДС.ПолучитьСуммуДокументаСНДС(ДокументСсылка), ВалютаДокумента,
																СтруктураШапкиДокумента.ВалютаВзаиморасчетов,
																СтруктураШапкиДокумента.КурсДокумента, КурсВзаиморасчетов,
																СтруктураШапкиДокумента.КратностьДокумента, КратностьВзаиморасчетов); 
			
			Если (МетаданныеДокумента.ТабличныеЧасти.Количество() > 0) Тогда
				Если ДокументСсылка.УдержатьКомиссионноеВознаграждение Тогда
													
					СуммаВознаграждения = МодульВалютногоУчета.ПересчитатьИзВалютыВВалюту(ДокументСсылка.Товары.Итог("СуммаВознаграждения"), ВалютаДокумента,
														   		СтруктураШапкиДокумента.ВалютаВзаиморасчетов,
														   		СтруктураШапкиДокумента.КурсДокумента, КурсВзаиморасчетов,
																СтруктураШапкиДокумента.КратностьДокумента, КратностьВзаиморасчетов);
																
					КоэффВознагр = ?(СуммаВзаиморасчетов = 0, 0, СуммаВознаграждения/СуммаВзаиморасчетов);
					СуммаЕНВД = Окр(СуммаЕНВД - СуммаЕНВД*КоэффВознагр, 2, 1);
					СуммаКомиссии = Окр(СуммаКомиссии - СуммаКомиссии*КоэффВознагр, 2, 1);
					СуммаВзаиморасчетов = СуммаВзаиморасчетов - СуммаВознаграждения;
				КонецЕсли;
			КонецЕсли;
			
			ЗачетАвансаОтПокупателяУСН(СтруктураШапкиДокумента, ДвиженияРегистровВх.ТаблицаПриход, ДвиженияРегистровВх, ДвиженияРегистров, Договор, Сделка, ТаблицаРД, СуммаВзаиморасчетов, СуммаЕНВД, СуммаКомиссии, КУДиР);
			//КУДиР.Содержание = "Реализация товаров. ";
			
		ИначеЕсли ВидОперации = "Списание" Тогда
			
			ДвиженияРегистров = ДвиженияРегистровВх;
			//КУДиР.Содержание = "Передача материалов в производство. ";
													
		КонецЕсли;
		
	ИначеЕсли ВидОперации = "ЗачетВознагражденияКомиссионера" Тогда
		
		//Проверим наличие доходов
		НаборЗаписей = РегистрыНакопления.ПартииТоваровПереданныеНалоговыйУчет.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Регистратор.Установить(ДокументСсылка);
		НаборЗаписей.Прочитать();
		ТаблицаПартий = НаборЗаписей.Выгрузить();
		Выручка = ДоляЕНВДиКомиссии(СтруктураШапкиДокумента, ТаблицаПартий);
		СуммаПринимаемойВыручки = Выручка.Всего - Выручка.ЕНВД - Выручка.Комиссия;
		
		ВыделятьНДСУСН = (СтруктураШапкиДокумента.ПорядокПризнанияРасходовПоНДС = Перечисления.ПорядокПризнанияРасходовПоНДС.ПоОплатеПоставщику);
		Если ВыделятьНДСУСН Тогда
			СуммаНДСВознаграждения = ДокументСсылка.Товары.Итог("СуммаНДСВознаграждения"); 
			СуммаВознаграждения = ДокументСсылка.СуммаВознаграждения - СуммаНДСВознаграждения;
			Если ДокументСсылка.СуммаВознаграждения > 0 Тогда
				СуммаПринимаемойВыручки = СуммаПринимаемойВыручки - СуммаПринимаемойВыручки * СуммаНДСВознаграждения / ДокументСсылка.СуммаВознаграждения;
			КонецЕсли;
		Иначе
			СуммаВознаграждения = ДокументСсылка.СуммаВознаграждения;
		КонецЕсли;
			
		СуммаДоходов = Мин(СуммаПринимаемойВыручки, СуммаВознаграждения);
		СуммаДоходов   = МодульВалютногоУчета.ПересчитатьИзВалютыВВалюту(СуммаДоходов, ВалютаДокумента,
														   ВалютаРегламентированногоУчета,
														   СтруктураШапкиДокумента.КурсДокумента, 1,
														   СтруктураШапкиДокумента.КратностьДокумента, 1);
		СуммаВзаиморасчетов   = МодульВалютногоУчета.ПересчитатьИзВалютыВВалюту(СуммаВознаграждения, ВалютаДокумента,
														   ВалютаРегламентированногоУчета,
														   СтруктураШапкиДокумента.КурсДокумента, 1,
														   СтруктураШапкиДокумента.КратностьДокумента, 1);
														   
		КУДиР.Графа4 = СуммаДоходов;
		КУДиР.Графа5 = СуммаДоходов;
		КУДиР.Графа7 = СуммаВзаиморасчетов;
		
		Если НЕ ВыделятьНДСУСН Тогда
			КУДиР.НДС = ДокументСсылка.Товары.Итог("СуммаНДСВознаграждения");
		КонецЕсли;
		
	ИначеЕсли ВидОперации = "ЗачетВознагражденияКомиссионераНДС" Тогда
		
		//Проверим наличие доходов
		НаборЗаписей = РегистрыНакопления.ПартииТоваровПереданныеНалоговыйУчет.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Регистратор.Установить(ДокументСсылка);
		НаборЗаписей.Прочитать();
		ТаблицаПартий = НаборЗаписей.Выгрузить();
		Выручка = ДоляЕНВДиКомиссии(СтруктураШапкиДокумента, ТаблицаПартий);
		СуммаПринимаемойВыручки = Выручка.Всего - Выручка.ЕНВД - Выручка.Комиссия;
		СуммаНДСВознаграждения = ДокументСсылка.Товары.Итог("СуммаНДСВознаграждения"); 
		Если ДокументСсылка.СуммаВознаграждения > 0 Тогда
			СуммаПринимаемойВыручки = СуммаПринимаемойВыручки * СуммаНДСВознаграждения / ДокументСсылка.СуммаВознаграждения;
		КонецЕсли;
		СуммаДоходов = Мин(СуммаПринимаемойВыручки, СуммаНДСВознаграждения);
		СуммаДоходов   = МодульВалютногоУчета.ПересчитатьИзВалютыВВалюту(СуммаДоходов, ВалютаДокумента,
														   ВалютаРегламентированногоУчета,
														   СтруктураШапкиДокумента.КурсДокумента, 1,
														   СтруктураШапкиДокумента.КратностьДокумента, 1);
		СуммаВзаиморасчетов   = МодульВалютногоУчета.ПересчитатьИзВалютыВВалюту(СуммаНДСВознаграждения, ВалютаДокумента,
														   ВалютаРегламентированногоУчета,
														   СтруктураШапкиДокумента.КурсДокумента, 1,
														   СтруктураШапкиДокумента.КратностьДокумента, 1);
														   
		КУДиР.Графа4 = СуммаДоходов;
		КУДиР.Графа5 = СуммаДоходов;
		КУДиР.Графа7 = СуммаВзаиморасчетов;
	
	ИначеЕсли (ВидОперации = "РеализацияУслуг") Тогда
		
		Выручка 			 = ДоляЕНВДиКомиссии(СтруктураШапкиДокумента, );
		СуммаЕНВД			 = МодульВалютногоУчета.ПересчитатьИзВалютыВВалюту(Выручка.ЕНВД, ВалютаДокумента,
																СтруктураШапкиДокумента.ВалютаВзаиморасчетов,
																СтруктураШапкиДокумента.КурсДокумента, КурсВзаиморасчетов,
																СтруктураШапкиДокумента.КратностьДокумента, КратностьВзаиморасчетов); 
		СуммаКомиссии		 = 0;
		СуммаУслугПоДокументу = 0;
		
		ЗачитыватьАванс = Истина;
		УдержатьВознаграждение = Ложь;
		
		Если МетаданныеДокумента.ТабличныеЧасти.Найти("Услуги") <> Неопределено Тогда
			СуммаУслугПоДокументу = УчетНДС.ПолучитьСуммуДокументаСНДС(ДокументСсылка);
			
		ИначеЕсли МетаданныеДокумента.ТабличныеЧасти.Найти("Продукция") <> Неопределено 
			И МетаданныеДокумента.ТабличныеЧасти.Найти("Услуги") <> Неопределено Тогда
			
			СуммаУслугПоДокументу = УчетНДС.ПолучитьСуммуДокументаСНДС(ДокументСсылка, "Продукция") + УчетНДС.ПолучитьСуммуДокументаСНДС(ДокументСсылка, "Услуги");
			
		ИначеЕсли МетаданныеДокумента.ТабличныеЧасти.Найти("Товары") <> Неопределено Тогда
			Если ОбщегоНазначения.ЕстьРеквизитТабЧастиДокумента("СуммаВознаграждения", МетаданныеДокумента, "Товары") Тогда
				СуммаУслугПоДокументу = ДокументСсылка.Товары.Итог("СуммаВознаграждения");
				Если ДокументСсылка.УдержатьКомиссионноеВознаграждение Тогда
					УдержатьВознаграждение = Истина;
				Иначе
					ЗачитыватьАванс = Ложь;
				КонецЕсли;
			Конецесли;
		ИначеЕсли МетаданныеДокумента.ТабличныеЧасти.Найти("ОС") <> Неопределено Тогда
			СуммаУслугПоДокументу = УчетНДС.ПолучитьСуммуДокументаСНДС(ДокументСсылка, "ОС");
		ИначеЕсли МетаданныеДокумента.Реквизиты.Найти("НематериальныйАктив") <> Неопределено Тогда
			СуммаУслугПоДокументу = ?(ДокументСсылка.СуммаВключаетНДС, ДокументСсылка.Сумма, ДокументСсылка.Сумма + ДокументСсылка.СуммаНДС); 
		КонецЕсли;
		
		Если НЕ СуммаУслугПоДокументу = 0 Тогда
			СуммаВзаиморасчетов  = МодульВалютногоУчета.ПересчитатьИзВалютыВВалюту(СуммаУслугПоДокументу, ВалютаДокумента,
																СтруктураШапкиДокумента.ВалютаВзаиморасчетов,
																СтруктураШапкиДокумента.КурсДокумента, КурсВзаиморасчетов,
																СтруктураШапкиДокумента.КратностьДокумента, КратностьВзаиморасчетов); 
			
			Если УдержатьВознаграждение Тогда
				СделкаВзаиморасчетов = Неопределено;
			Иначе
				СделкаВзаиморасчетов = Сделка;
			КонецЕсли;
			ЗачетАвансаОтПокупателяБезУчетаРасходовУСН(СтруктураШапкиДокумента, ДвиженияРегистров, Договор, СделкаВзаиморасчетов, ТаблицаРД, СуммаВзаиморасчетов, СуммаЕНВД, СуммаКомиссии, КУДиР, ЗачитыватьАванс, УдержатьВознаграждение);
		Конецесли;
	ИначеЕсли (ВидОперации = "Оплата") Тогда	
		Если НЕ МетаданныеДокумента.ТабличныеЧасти.Найти("РасшифровкаПлатежа") = Неопределено Тогда
			Запрос = Новый Запрос;
			Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
			Запрос.Текст = 
			"ВЫБРАТЬ
			|	ТЧРасшифровкаПлатежа.ДоговорКонтрагента,
			|	ТЧРасшифровкаПлатежа.Сделка,
			|	ТЧРасшифровкаПлатежа.КурсВзаиморасчетов,
			|	ТЧРасшифровкаПлатежа.КратностьВзаиморасчетов,
			|	СУММА(ТЧРасшифровкаПлатежа.СуммаВзаиморасчетов) КАК Сумма,
			|	СУММА(ТЧРасшифровкаПлатежа.СуммаПлатежа) КАК СуммаПлатежа
			|ИЗ
			|	Документ." + МетаданныеДокумента.Имя + ".РасшифровкаПлатежа КАК ТЧРасшифровкаПлатежа
			|ГДЕ
			|	ТЧРасшифровкаПлатежа.Ссылка = &Ссылка
			|
			|СГРУППИРОВАТЬ ПО
			|	ТЧРасшифровкаПлатежа.ДоговорКонтрагента,
			|	ТЧРасшифровкаПлатежа.Сделка,
			|	ТЧРасшифровкаПлатежа.КурсВзаиморасчетов,
			|	ТЧРасшифровкаПлатежа.КратностьВзаиморасчетов";
			
			ТаблицаОплат = Запрос.Выполнить().Выгрузить();
			Если ТаблицаОплат.Количество() > 0 Тогда
				ОплатаПоставщикуУСН(СтруктураШапкиДокумента, ТаблицаОплат, ДвиженияРегистров, КУДиР);
				//КУДиР.Содержание = "Оплата поставщику. ";
			Конецесли;
		КонецЕсли;
		
		Если НЕ МетаданныеДокумента.ТабличныеЧасти.Найти("ОплатаПоставщикам") = Неопределено Тогда
			Запрос = Новый Запрос;
			Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
			Запрос.Текст = 
			"ВЫБРАТЬ
			|	ТЧРасшифровкаПлатежа.ДоговорКонтрагента,
			|	ТЧРасшифровкаПлатежа.Сделка,
			|	ТЧРасшифровкаПлатежа.КурсВзаиморасчетов,
			|	ТЧРасшифровкаПлатежа.КратностьВзаиморасчетов,
			|	СУММА(ТЧРасшифровкаПлатежа.СуммаВзаиморасчетов) КАК Сумма,
			|	СУММА(ТЧРасшифровкаПлатежа.Сумма) КАК СуммаПлатежа
			|ИЗ
			|	Документ." + МетаданныеДокумента.Имя + ".ОплатаПоставщикам КАК ТЧРасшифровкаПлатежа
			|ГДЕ
			|	ТЧРасшифровкаПлатежа.Ссылка = &Ссылка
			|
			|СГРУППИРОВАТЬ ПО
			|	ТЧРасшифровкаПлатежа.ДоговорКонтрагента,
			|	ТЧРасшифровкаПлатежа.Сделка,
			|	ТЧРасшифровкаПлатежа.КурсВзаиморасчетов,
			|	ТЧРасшифровкаПлатежа.КратностьВзаиморасчетов";
			
			ТаблицаОплат = Запрос.Выполнить().Выгрузить();
			Если ТаблицаОплат.Количество() > 0 Тогда
				ОплатаПоставщикуУСН(СтруктураШапкиДокумента, ТаблицаОплат, ДвиженияРегистров, КУДиР, "ОплатаАО");
				//КУДиР.Содержание = "Оплата поставщику. ";
			Конецесли;
		КонецЕсли;
		
	ИначеЕсли ВидОперации = "ВыплатаЗП" Тогда
		
		Если Не МетаданныеДокумента.ТабличныеЧасти.Найти("ВыплатаЗаработнойПлаты") = Неопределено Тогда
			Запрос = Новый Запрос;
			Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
			Запрос.УстановитьПараметр("Организация", СтруктураШапкиДокумента.Организация);
			Запрос.УстановитьПараметр("Сделка", Неопределено);
			Запрос.УстановитьПараметр("Выплачено",Перечисления.ВыплаченностьЗарплаты.Выплачено);
			Запрос.УстановитьПараметр("Сводно", Справочники.ФизическиеЛица.ПустаяСсылка());
			Запрос.Текст = 
			"ВЫБРАТЬ
			|	ЗарплатаКВыплатеОрганизацийЗарплата.Физлицо КАК Работник,
			|	&Сделка КАК Сделка,
			|	1 КАК КурсВзаиморасчетов,
			|	1 КАК КратностьВзаиморасчетов,
			|	СУММА(ЗарплатаКВыплатеОрганизацийЗарплата.Сумма) КАК Сумма
			|ИЗ
			|	Документ.ЗарплатаКВыплатеОрганизаций.Зарплата КАК ЗарплатаКВыплатеОрганизацийЗарплата
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ." + МетаданныеДокумента.Имя + ".ВыплатаЗаработнойПлаты КАК РасходныйКассовыйОрдерВыплатаЗаработнойПлаты
			|		ПО ЗарплатаКВыплатеОрганизацийЗарплата.Ссылка = РасходныйКассовыйОрдерВыплатаЗаработнойПлаты.Ведомость
			|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СведенияОВыплатахРаботникамОрганизацийПоПлатежнымВедомостям КАК РанееВыплаченныеСуммы
			|		ПО ЗарплатаКВыплатеОрганизацийЗарплата.Ссылка = РанееВыплаченныеСуммы.Ведомость
			|			И ЗарплатаКВыплатеОрганизацийЗарплата.Физлицо = РанееВыплаченныеСуммы.Физлицо
			|			И (РанееВыплаченныеСуммы.Регистратор <> &Ссылка)
			|
			|ГДЕ
			|	РасходныйКассовыйОрдерВыплатаЗаработнойПлаты.Ссылка = &Ссылка И
			|	(НЕ ЗарплатаКВыплатеОрганизацийЗарплата.Физлицо = &Сводно) И
			|	ЗарплатаКВыплатеОрганизацийЗарплата.ВыплаченностьЗарплаты = &Выплачено И
			|	ЗарплатаКВыплатеОрганизацийЗарплата.Ссылка.Организация = &Организация И
			|	РанееВыплаченныеСуммы.Физлицо ЕСТЬ NULL 
			|
			|СГРУППИРОВАТЬ ПО
			|	ЗарплатаКВыплатеОрганизацийЗарплата.Физлицо
			|
			|ИМЕЮЩИЕ
			|	СУММА(ЗарплатаКВыплатеОрганизацийЗарплата.Сумма) <> 0";
			
			ТаблицаОплат = Запрос.Выполнить().Выгрузить();
			Если ТаблицаОплат.Количество() > 0 Тогда
				ОплатаПоставщикуУСН(СтруктураШапкиДокумента, ТаблицаОплат, ДвиженияРегистров, КУДиР);
				//КУДиР.Содержание = "Выплата зарплаты. ";
			Конецесли;
		КонецЕсли;
		
		Если Не МетаданныеДокумента.ТабличныеЧасти.Найти("ПеречислениеЗаработнойПлаты") = Неопределено Тогда
			Запрос = Новый Запрос;
			Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
			Запрос.УстановитьПараметр("Организация", СтруктураШапкиДокумента.Организация);
			Запрос.УстановитьПараметр("Сделка", Неопределено);
			Запрос.УстановитьПараметр("Сводно", Справочники.ФизическиеЛица.ПустаяСсылка());
			Запрос.Текст = 
			"ВЫБРАТЬ
			|	ЗарплатаКВыплатеОрганизацийЗарплата.Физлицо КАК Работник,
			|	&Сделка КАК Сделка,
			|	1 КАК КурсВзаиморасчетов,
			|	1 КАК КратностьВзаиморасчетов,
			|	ЗарплатаКВыплатеОрганизацийЗарплата.Сумма
			|ИЗ
			|	Документ.ЗарплатаКВыплатеОрганизаций.Зарплата КАК ЗарплатаКВыплатеОрганизацийЗарплата
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ." + МетаданныеДокумента.Имя + ".ПеречислениеЗаработнойПлаты КАК РасходныйКассовыйОрдерВыплатаЗаработнойПлаты
			|		ПО ЗарплатаКВыплатеОрганизацийЗарплата.Ссылка = РасходныйКассовыйОрдерВыплатаЗаработнойПлаты.Ведомость
			|
			|ГДЕ
			|	РасходныйКассовыйОрдерВыплатаЗаработнойПлаты.Ссылка = &Ссылка И
			|	(НЕ ЗарплатаКВыплатеОрганизацийЗарплата.Физлицо = &Сводно) И
			|	ЗарплатаКВыплатеОрганизацийЗарплата.Ссылка.Организация = &Организация";
			
			ТаблицаОплат = Запрос.Выполнить().Выгрузить();
			Если ТаблицаОплат.Количество() > 0 Тогда
				ОплатаПоставщикуУСН(СтруктураШапкиДокумента, ТаблицаОплат, ДвиженияРегистров, КУДиР);
				//КУДиР.Содержание = "Перечисление зарплаты. ";
			Конецесли;
		КонецЕсли;
		
	ИначеЕсли ВидОперации = "ВыплатаДепонентов" Тогда
		
		Если Не МетаданныеДокумента.ТабличныеЧасти.Найти("ВыплатаДепонентов") = Неопределено Тогда
			Запрос = Новый Запрос;
			Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
			Запрос.УстановитьПараметр("Организация", СтруктураШапкиДокумента.Организация);
			Запрос.УстановитьПараметр("Сделка", Неопределено);
			Запрос.УстановитьПараметр("Выплачено",Перечисления.ВыплаченностьЗарплаты.Выплачено);
			Запрос.УстановитьПараметр("Сводно", Справочники.ФизическиеЛица.ПустаяСсылка());
			Запрос.Текст = 
			"ВЫБРАТЬ
			|	ЗарплатаКВыплатеОрганизацийЗарплата.Физлицо КАК Работник,
			|	&Сделка КАК Сделка,
			|	1 КАК КурсВзаиморасчетов,
			|	1 КАК КратностьВзаиморасчетов,
			|	ЗарплатаКВыплатеОрганизацийЗарплата.Сумма КАК Сумма
			|ИЗ
			|	Документ.ЗарплатаКВыплатеОрганизаций.Зарплата КАК ЗарплатаКВыплатеОрганизацийЗарплата
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ." + МетаданныеДокумента.Имя + ".ВыплатаДепонентов КАК РКОВыплатаДепонентов
			|		ПО (ЗарплатаКВыплатеОрганизацийЗарплата.Ссылка = РКОВыплатаДепонентов.Ведомость) И
			|		   (ЗарплатаКВыплатеОрганизацийЗарплата.Физлицо = РКОВыплатаДепонентов.Физлицо)
			|
			|ГДЕ
			|	РКОВыплатаДепонентов.Ссылка = &Ссылка И
			|	(НЕ РКОВыплатаДепонентов.Физлицо = &Сводно) И
			|	ЗарплатаКВыплатеОрганизацийЗарплата.Ссылка.Организация = &Организация";
			
			ТаблицаОплат = Запрос.Выполнить().Выгрузить();
			Если ТаблицаОплат.Количество() > 0 Тогда
				ОплатаПоставщикуУСН(СтруктураШапкиДокумента, ТаблицаОплат, ДвиженияРегистров, КУДиР);
				//КУДиР.Содержание = "Выплата депонированной зарплаты. ";
			Конецесли;
		КонецЕсли;
		
	ИначеЕсли (ВидОперации = "ВыплатаЗПРаботнику") Тогда
		
		ТаблицаОплат = Новый ТаблицаЗначений;
		ТаблицаОплат.Колонки.Добавить("Работник");
		ТаблицаОплат.Колонки.Добавить("Сделка");
		ТаблицаОплат.Колонки.Добавить("Сумма");
		ТаблицаОплат.Колонки.Добавить("КурсВзаиморасчетов");
		ТаблицаОплат.Колонки.Добавить("КратностьВзаиморасчетов");
		
		НоваяСтрока = ТаблицаОплат.Добавить();
		НоваяСтрока.Работник = ДокументСсылка.Контрагент;
		НоваяСтрока.Сделка = Неопределено;
		НоваяСтрока.Сумма = ДокументСсылка.СуммаДокумента;
		НоваяСтрока.КурсВзаиморасчетов =  1;
		НоваяСтрока.КратностьВзаиморасчетов = 1;
		
		ОплатаПоставщикуУСН(СтруктураШапкиДокумента, ТаблицаОплат, ДвиженияРегистров, КУДиР);
		//КУДиР.Содержание = "Выплата зарплаты. ";
		
	ИначеЕсли (ВидОперации = "ОплатаОтПокупателя") Тогда	
		Если Не МетаданныеДокумента.ТабличныеЧасти.Найти("РасшифровкаПлатежа") = Неопределено Тогда
			Запрос = Новый Запрос;
			Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
			Запрос.Текст = 
			"ВЫБРАТЬ
			|	ТЧРасшифровкаПлатежа.ДоговорКонтрагента,
			|	ТЧРасшифровкаПлатежа.Сделка,
			|	ТЧРасшифровкаПлатежа.КурсВзаиморасчетов,
			|	ТЧРасшифровкаПлатежа.КратностьВзаиморасчетов,
			|	СУММА(ТЧРасшифровкаПлатежа.СуммаВзаиморасчетов) КАК Сумма,
			|	СУММА(ТЧРасшифровкаПлатежа.СуммаПлатежа) КАК СуммаПлатежа
			|ИЗ
			|	Документ." + МетаданныеДокумента.Имя + ".РасшифровкаПлатежа КАК ТЧРасшифровкаПлатежа
			|ГДЕ
			|	ТЧРасшифровкаПлатежа.Ссылка = &Ссылка
			|
			|СГРУППИРОВАТЬ ПО
			|	ТЧРасшифровкаПлатежа.ДоговорКонтрагента,
			|	ТЧРасшифровкаПлатежа.Сделка,
			|	ТЧРасшифровкаПлатежа.КурсВзаиморасчетов,
			|	ТЧРасшифровкаПлатежа.КратностьВзаиморасчетов";
			
			ТаблицаОплат = Запрос.Выполнить().Выгрузить();
			Если ТаблицаОплат.Количество() > 0 Тогда
				//КУДиР.Содержание = "Поступление оплаты от покупателя. ";
				ОплатаОтПокупателяУСН(СтруктураШапкиДокумента, ТаблицаОплат, ДвиженияРегистров, КУДиР);
			Конецесли;
		КонецЕсли;
		
	ИначеЕсли (ВидОперации = "НачислениеЗП") Тогда
		НачислениеЗП(СтруктураШапкиДокумента, ДвиженияРегистров, КУДиР);
		//КУДиР.Содержание = "Отражение зарплаты в регламентированном учете. ";
		
	ИначеЕсли (ВидОперации = "СписаниеРБП") Тогда
		СписаниеРБПУСН(СтруктураШапкиДокумента, ДвиженияРегистров, КУДиР);
		
	ИначеЕсли (ВидОперации = "Выпуск") Тогда
		ВыпускПродукции(СтруктураШапкиДокумента, ДвиженияРегистров, КУДиР);
		
	ИначеЕсли (ВидОперации = "РаспределениеЕНВД") Тогда
		РаспределениеЕНВД(СтруктураШапкиДокумента, ДвиженияРегистров, КУДиР);
		
	ИначеЕсли (ВидОперации = "СписаниеТЗР") Тогда
		СписаниеТЗР(СтруктураШапкиДокумента, ДвиженияРегистров);
		
	ИначеЕсли (ВидОперации = "ПеречислениеНалога") Тогда	
		ТаблицаОплат = Новый ТаблицаЗначений;
		ТаблицаОплат.Колонки.Добавить("ДоговорКонтрагента");
		ТаблицаОплат.Колонки.Добавить("Сделка");
		ТаблицаОплат.Колонки.Добавить("Сумма");
		
		НоваяСтрока = ТаблицаОплат.Добавить();
		НоваяСтрока.ДоговорКонтрагента = ДокументСсылка.СчетУчетаРасчетовСКонтрагентом;
		НоваяСтрока.Сделка = Неопределено;
		НоваяСтрока.Сумма = ДокументСсылка.СуммаДокумента;
		
		//КУДиР.Содержание = "Перечисление налога (" + ДокументСсылка.СчетУчетаРасчетовСКонтрагентом.Наименование + "). ";
		ОплатаПоставщикуУСН(СтруктураШапкиДокумента, ТаблицаОплат, ДвиженияРегистров, КУДиР);
		ДвиженияРегистров.КУДиР.Колонки.Добавить("КлючУникальности");
		ДвиженияРегистров.КУДиР.ЗаполнитьЗначения(ДокументСсылка.СчетУчетаРасчетовСКонтрагентом, "КлючУникальности");
		
	ИначеЕсли (ВидОперации = "ВыдачаПодотчетнику") Тогда	
		ТаблицаОплат = Новый ТаблицаЗначений;
		ТаблицаОплат.Колонки.Добавить("ДоговорКонтрагента");
		ТаблицаОплат.Колонки.Добавить("Сделка");
		ТаблицаОплат.Колонки.Добавить("Сумма");
		ТаблицаОплат.Колонки.Добавить("КурсВзаиморасчетов");
		ТаблицаОплат.Колонки.Добавить("КратностьВзаиморасчетов");
		
		НоваяСтрока = ТаблицаОплат.Добавить();
		Если НЕ МетаданныеДокумента.Реквизиты.Найти("ФизЛицо") = Неопределено Тогда
			НоваяСтрока.ДоговорКонтрагента = ДокументСсылка.ФизЛицо;
		Иначе
			НоваяСтрока.ДоговорКонтрагента = ДокументСсылка.Контрагент;
		КонецЕсли;
		Если НЕ МетаданныеДокумента.Реквизиты.Найти("РасчетныйДокументРаботника") = Неопределено Тогда
			РасчетныйДокументРаботника = ДокументСсылка.РасчетныйДокументРаботника;
		ИначеЕсли НЕ МетаданныеДокумента.Реквизиты.Найти("РасчетныйДокумент") = Неопределено Тогда
			РасчетныйДокументРаботника = ДокументСсылка.РасчетныйДокумент;
		Иначе
			РасчетныйДокументРаботника = Неопределено;
		КонецЕсли;
		НоваяСтрока.Сделка = ?(НЕ ЗначениеЗаполнено(РасчетныйДокументРаботника), Неопределено, РасчетныйДокументРаботника); //~~
		НоваяСтрока.Сумма = ДокументСсылка.СуммаДокумента;
		НоваяСтрока.КурсВзаиморасчетов =  ЗаполнениеДокументов.КурсДокумента(ДокументСсылка, глЗначениеПеременной("ВалютаРегламентированногоУчета"));
		НоваяСтрока.КратностьВзаиморасчетов =  ЗаполнениеДокументов.КратностьДокумента(ДокументСсылка, глЗначениеПеременной("ВалютаРегламентированногоУчета"));
				
		//КУДиР.Содержание = "Выдача под отчет. ";
		ОплатаПоставщикуУСН(СтруктураШапкиДокумента, ТаблицаОплат, ДвиженияРегистров, КУДиР);
		
	ИначеЕсли (ВидОперации = "ВозвратОтПодотчетника") Тогда	
		ТаблицаОплат = Новый ТаблицаЗначений;
		ТаблицаОплат.Колонки.Добавить("ДоговорКонтрагента");
		ТаблицаОплат.Колонки.Добавить("Сделка");
		ТаблицаОплат.Колонки.Добавить("Сумма");
		ТаблицаОплат.Колонки.Добавить("КурсВзаиморасчетов");
		ТаблицаОплат.Колонки.Добавить("КратностьВзаиморасчетов");
		
		НоваяСтрока = ТаблицаОплат.Добавить();
		НоваяСтрока.ДоговорКонтрагента = ДокументСсылка.Контрагент;
		НоваяСтрока.Сделка = ?(НЕ ЗначениеЗаполнено(ДокументСсылка.РасчетныйДокумент), Неопределено, ДокументСсылка.РасчетныйДокумент); //~~
		НоваяСтрока.Сумма = ДокументСсылка.СуммаДокумента;
		НоваяСтрока.КурсВзаиморасчетов =  ЗаполнениеДокументов.КурсДокумента(ДокументСсылка, глЗначениеПеременной("ВалютаРегламентированногоУчета"));
		НоваяСтрока.КратностьВзаиморасчетов =  ЗаполнениеДокументов.КратностьДокумента(ДокументСсылка, глЗначениеПеременной("ВалютаРегламентированногоУчета"));
				
		ОплатаОтПокупателяУСН(СтруктураШапкиДокумента, ТаблицаОплат, ДвиженияРегистров, КУДиР);
		//КУДиР.Содержание = "Возврат денежных средств подотчетником. ";
		КУДиР.Графа6 = - КУДиР.Графа4;
		КУДиР.Графа4 = 0;
		КУДиР.Графа5 = 0;
		
	ИначеЕсли (ВидОперации = "ВозвратОтРаботника") Тогда	
		ТаблицаОплат = Новый ТаблицаЗначений;
		ТаблицаОплат.Колонки.Добавить("Работник");
		ТаблицаОплат.Колонки.Добавить("Сделка");
		ТаблицаОплат.Колонки.Добавить("Сумма");
		ТаблицаОплат.Колонки.Добавить("КурсВзаиморасчетов");
		ТаблицаОплат.Колонки.Добавить("КратностьВзаиморасчетов");
		
		НоваяСтрока = ТаблицаОплат.Добавить();
		НоваяСтрока.Работник = ДокументСсылка.Контрагент;
		НоваяСтрока.Сделка = Неопределено; //~~
		НоваяСтрока.Сумма = ДокументСсылка.СуммаДокумента;
		НоваяСтрока.КурсВзаиморасчетов =  ЗаполнениеДокументов.КурсДокумента(ДокументСсылка, глЗначениеПеременной("ВалютаРегламентированногоУчета"));
		НоваяСтрока.КратностьВзаиморасчетов =  ЗаполнениеДокументов.КратностьДокумента(ДокументСсылка, глЗначениеПеременной("ВалютаРегламентированногоУчета"));
				
		ОплатаОтПокупателяУСН(СтруктураШапкиДокумента, ТаблицаОплат, ДвиженияРегистров, КУДиР);
		//КУДиР.Содержание = "Возврат денежных средств подотчетником. ";
		КУДиР.Графа6 = - КУДиР.Графа4;
		КУДиР.Графа4 = 0;
		КУДиР.Графа5 = 0;
		
	ИначеЕсли (ВидОперации = "ВозвратОтПоставщика") Тогда
		
		Если НЕ МетаданныеДокумента.ТабличныеЧасти.Найти("РасшифровкаПлатежа") = Неопределено Тогда
			Запрос = Новый Запрос;
			Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
			Запрос.Текст = 
			"ВЫБРАТЬ
			|	ТЧРасшифровкаПлатежа.ДоговорКонтрагента,
			|	ТЧРасшифровкаПлатежа.Сделка,
			|	ТЧРасшифровкаПлатежа.КурсВзаиморасчетов,
			|	ТЧРасшифровкаПлатежа.КратностьВзаиморасчетов,
			|	СУММА(ТЧРасшифровкаПлатежа.СуммаВзаиморасчетов) КАК Сумма,
			|	СУММА(ТЧРасшифровкаПлатежа.СуммаПлатежа) КАК СуммаПлатежа
			|ИЗ
			|	Документ." + МетаданныеДокумента.Имя + ".РасшифровкаПлатежа КАК ТЧРасшифровкаПлатежа
			|ГДЕ
			|	ТЧРасшифровкаПлатежа.Ссылка = &Ссылка
			|
			|СГРУППИРОВАТЬ ПО
			|	ТЧРасшифровкаПлатежа.ДоговорКонтрагента,
			|	ТЧРасшифровкаПлатежа.Сделка,
			|	ТЧРасшифровкаПлатежа.КурсВзаиморасчетов,
			|	ТЧРасшифровкаПлатежа.КратностьВзаиморасчетов";
			
			ТаблицаОплат = Запрос.Выполнить().Выгрузить();
			Если ТаблицаОплат.Количество() > 0 Тогда
				ОплатаОтПокупателяУСН(СтруктураШапкиДокумента, ТаблицаОплат, ДвиженияРегистров, КУДиР);
				//КУДиР.Содержание = "Возврат денежных средств от поставщика. ";
				
				КУДиР.Графа6 = - КУДиР.Графа4;
				КУДиР.Графа4 = 0;
				КУДиР.Графа5 = 0;
			Конецесли;
		КонецЕсли;
		
	ИначеЕсли (ВидОперации = "ВозвратПокупателю") Тогда
		
		Если НЕ МетаданныеДокумента.ТабличныеЧасти.Найти("РасшифровкаПлатежа") = Неопределено Тогда
			Запрос = Новый Запрос;
			Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
			Запрос.Текст = 
			"ВЫБРАТЬ
			|	ТЧРасшифровкаПлатежа.ДоговорКонтрагента,
			|	ТЧРасшифровкаПлатежа.Сделка,
			|	ТЧРасшифровкаПлатежа.КурсВзаиморасчетов,
			|	ТЧРасшифровкаПлатежа.КратностьВзаиморасчетов,
			|	СУММА(ТЧРасшифровкаПлатежа.СуммаВзаиморасчетов) КАК Сумма,
			|	СУММА(ТЧРасшифровкаПлатежа.СуммаПлатежа) КАК СуммаПлатежа
			|ИЗ
			|	Документ." + МетаданныеДокумента.Имя + ".РасшифровкаПлатежа КАК ТЧРасшифровкаПлатежа
			|ГДЕ
			|	ТЧРасшифровкаПлатежа.Ссылка = &Ссылка
			|
			|СГРУППИРОВАТЬ ПО
			|	ТЧРасшифровкаПлатежа.ДоговорКонтрагента,
			|	ТЧРасшифровкаПлатежа.Сделка,
			|	ТЧРасшифровкаПлатежа.КурсВзаиморасчетов,
			|	ТЧРасшифровкаПлатежа.КратностьВзаиморасчетов";
			
			ТаблицаОплат = Запрос.Выполнить().Выгрузить();
			Если ТаблицаОплат.Количество() > 0 Тогда
				ОплатаПоставщикуУСН(СтруктураШапкиДокумента, ТаблицаОплат, ДвиженияРегистров, КУДиР);
				//КУДиР.Содержание = "Возврат денежных средств покупателю. ";
								
				КУДиР.Графа4 = - КУДиР.Графа6;
				КУДиР.Графа5 = - КУДиР.Графа6;
				КУДиР.Графа6 = 0;
			Конецесли;
		КонецЕсли;
				
	ИначеЕсли (ВидОперации = "ВыручкаСНТТ") Тогда
		//КУДиР.Содержание = "Прием розничной выручки. ";
		Если ДокументСсылка.РучнаяНастройка_УСН Тогда
			ПрочееДДС(ДокументСсылка.ПолучитьОбъект());
			
		Иначе
			СуммаДокумента = ДокументСсылка.РасшифровкаПлатежа.Итог("СуммаПлатежа");
			КУДиР.Графа4 = КУДиР.Графа4 + СуммаДокумента;
			КУДиР.Графа5 = КУДиР.Графа5 + СуммаДокумента;
			
		КонецЕсли;
		
	ИначеЕсли (ВидОперации = "ПереносЗадолженности") Тогда	
		
		Если НЕ МетаданныеДокумента.ТабличныеЧасти.Найти("СуммыДолга") = Неопределено Тогда
			Запрос = Новый Запрос;
			Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
			Запрос.УстановитьПараметр("ВидЗадолженности", Перечисления.ВидыЗадолженности.Дебиторская);
			Запрос.Текст = 
			"ВЫБРАТЬ
			|	ТЧСуммыДолга.НомерСтроки,
			|	ТЧСуммыДолга.ДоговорКонтрагента,
			|	ТЧСуммыДолга.Сделка КАК Сделка,
			|	ТЧСуммыДолга.ДокументРасчетовСКонтрагентом КАК РасчетныйДокумент,
			|	ТЧСуммыДолга.КурсВзаиморасчетов,
			|	ТЧСуммыДолга.КратностьВзаиморасчетов,
			|	ТЧСуммыДолга.Сумма КАК Сумма
			|ИЗ
			|	Документ." + МетаданныеДокумента.Имя + ".СуммыДолга КАК ТЧСуммыДолга
			|
			|ГДЕ
			|	ТЧСуммыДолга.Ссылка = &Ссылка
			|	И ТЧСуммыДолга.ВидЗадолженности = &ВидЗадолженности
			|";
			
			ТаблицаОплат = Запрос.Выполнить().Выгрузить();
			Если ТаблицаОплат.Количество() > 0 Тогда
				ПереносЗадолженностиУСН(СтруктураШапкиДокумента, ТаблицаОплат, ДвиженияРегистров, ВидДвиженияНакопления.Расход, "РасчетныйДокумент");
			Конецесли;
				
			Запрос = Новый Запрос;
			Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
			Запрос.УстановитьПараметр("ВидЗадолженности", Перечисления.ВидыЗадолженности.Кредиторская);
			Запрос.Текст = 
			"ВЫБРАТЬ
			|	ТЧСуммыДолга.НомерСтроки,
			|	ТЧСуммыДолга.ДоговорКонтрагента,
			|	ТЧСуммыДолга.Сделка КАК Сделка,
			|	ТЧСуммыДолга.ДокументРасчетовСКонтрагентом КАК РасчетныйДокумент,
			|	ТЧСуммыДолга.КурсВзаиморасчетов,
			|	ТЧСуммыДолга.КратностьВзаиморасчетов,
			|	ТЧСуммыДолга.Сумма КАК Сумма
			|ИЗ
			|	Документ." + МетаданныеДокумента.Имя + ".СуммыДолга КАК ТЧСуммыДолга
			|
			|ГДЕ
			|	ТЧСуммыДолга.Ссылка = &Ссылка
			|	И ТЧСуммыДолга.ВидЗадолженности = &ВидЗадолженности
			|";
			
			ТаблицаОплат = Запрос.Выполнить().Выгрузить();
			Если ТаблицаОплат.Количество() > 0 Тогда
				ПереносЗадолженностиУСН(СтруктураШапкиДокумента, ТаблицаОплат, ДвиженияРегистров, ВидДвиженияНакопления.Приход, "РасчетныйДокумент");
			Конецесли;
			
		ИначеЕсли НЕ МетаданныеДокумента.ТабличныеЧасти.Найти("РасшифровкаПлатежа") = Неопределено Тогда	
			
			Запрос = Новый Запрос;
			Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
			Запрос.Текст = 
			"ВЫБРАТЬ
			|	ТЧРасшифровкаПлатежа.ДоговорКонтрагента,
			|	ТЧРасшифровкаПлатежа.Сделка,
			|	ТЧРасшифровкаПлатежа.КурсВзаиморасчетов,
			|	ТЧРасшифровкаПлатежа.КратностьВзаиморасчетов,
			|	СУММА(ТЧРасшифровкаПлатежа.СуммаВзаиморасчетов) КАК Сумма,
			|	СУММА(ТЧРасшифровкаПлатежа.СуммаПлатежа) КАК СуммаПлатежа
			|ИЗ
			|	Документ." + МетаданныеДокумента.Имя + ".РасшифровкаПлатежа КАК ТЧРасшифровкаПлатежа
			|ГДЕ
			|	ТЧРасшифровкаПлатежа.Ссылка = &Ссылка
			|
			|СГРУППИРОВАТЬ ПО
			|	ТЧРасшифровкаПлатежа.ДоговорКонтрагента,
			|	ТЧРасшифровкаПлатежа.Сделка,
			|	ТЧРасшифровкаПлатежа.КурсВзаиморасчетов,
			|	ТЧРасшифровкаПлатежа.КратностьВзаиморасчетов";
			
			СтруктураШапкиДокумента.Вставить("ДоговорКонтрагента", ДокументСсылка.ДоговорВзаиморасчетовЭквайрера);
			
			ТаблицаОплат = Запрос.Выполнить().Выгрузить();
			Если ТаблицаОплат.Количество() > 0 Тогда
				ПереносЗадолженностиУСН(СтруктураШапкиДокумента, ТаблицаОплат, ДвиженияРегистров, ВидДвиженияНакопления.Расход);
			Конецесли;
			
		КонецЕсли;

	ИначеЕсли (ВидОперации = "ПроведениеВзаимозачетаДебитор") Тогда
		
		Если НЕ МетаданныеДокумента.ТабличныеЧасти.Найти("СуммыДолга") = Неопределено Тогда
			
			Запрос = Новый Запрос;
			Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
			Запрос.УстановитьПараметр("ВидЗадолженности", Перечисления.ВидыЗадолженности.Дебиторская);
			Запрос.Текст = 
			"ВЫБРАТЬ
			|	ТЧСуммыДолга.НомерСтроки,
			|	ТЧСуммыДолга.ДоговорКонтрагента,
			|	ТЧСуммыДолга.Сделка КАК Сделка,
			|	ТЧСуммыДолга.ДокументРасчетовСКонтрагентом КАК РасчетныйДокумент,
			|	ТЧСуммыДолга.КурсВзаиморасчетов,
			|	ТЧСуммыДолга.КратностьВзаиморасчетов,
			|	ТЧСуммыДолга.Сумма КАК Сумма
			|ИЗ
			|	Документ." + МетаданныеДокумента.Имя + ".СуммыДолга КАК ТЧСуммыДолга
			|
			|ГДЕ
			|	ТЧСуммыДолга.Ссылка = &Ссылка
			|	И ТЧСуммыДолга.ВидЗадолженности = &ВидЗадолженности
			|";
			
			ТаблицаОплат = Запрос.Выполнить().Выгрузить();
			Если ТаблицаОплат.Количество() > 0 Тогда
				//КУДиР.Содержание = "Взаимозачет, погашение дебиторской задолженности. ";
				ОплатаОтПокупателяУСН(СтруктураШапкиДокумента, ТаблицаОплат, ДвиженияРегистров, КУДиР, "РасчетныйДокумент");
			Конецесли;
			
			//Проверим второй договор
			Запрос = Новый Запрос;
			Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
			Запрос.УстановитьПараметр("ВидЗадолженности", Перечисления.ВидыЗадолженности.Кредиторская);
			Запрос.Текст = 
			"ВЫБРАТЬ
			|	ТЧСуммыДолга.НомерСтроки,
			|	ТЧСуммыДолга.ДоговорКонтрагента,
			|	ТЧСуммыДолга.Сделка КАК Сделка,
			|	ТЧСуммыДолга.ДокументРасчетовСКонтрагентом КАК РасчетныйДокумент,
			|	ТЧСуммыДолга.КурсВзаиморасчетов,
			|	ТЧСуммыДолга.КратностьВзаиморасчетов,
			|	ТЧСуммыДолга.Сумма КАК Сумма
			|ИЗ
			|	Документ." + МетаданныеДокумента.Имя + ".СуммыДолга КАК ТЧСуммыДолга
			|
			|ГДЕ
			|	ТЧСуммыДолга.Ссылка = &Ссылка
			|	И ТЧСуммыДолга.ВидЗадолженности = &ВидЗадолженности
			|";
			
			ТаблицаОплатКр = Запрос.Выполнить().Выгрузить();
			Если ТаблицаОплатКр.Количество() > 0 И ТаблицаОплат.Количество() > 0 Тогда
				Если ТаблицаОплатКр[0].ДоговорКонтрагента.ВидДоговора = ТаблицаОплат[0].ДоговорКонтрагента.ВидДоговора Тогда
					КУДиР.Графа4 = 0;
					КУДиР.Графа5 = 0;
					КУДиР.Графа6 = 0;
				КонецЕсли;
			КонецЕсли;
			
		КонецЕсли;
			
	ИначеЕсли (ВидОперации = "ПроведениеВзаимозачетаКредитор") Тогда
		
		Если НЕ МетаданныеДокумента.ТабличныеЧасти.Найти("СуммыДолга") = Неопределено Тогда
			
			Запрос = Новый Запрос;
			Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
			Запрос.УстановитьПараметр("ВидЗадолженности", Перечисления.ВидыЗадолженности.Кредиторская);
			Запрос.Текст = 
			"ВЫБРАТЬ
			|	ТЧСуммыДолга.НомерСтроки,
			|	ТЧСуммыДолга.ДоговорКонтрагента,
			|	ТЧСуммыДолга.Сделка КАК Сделка,
			|	ТЧСуммыДолга.ДокументРасчетовСКонтрагентом КАК РасчетныйДокумент,
			|	ТЧСуммыДолга.КурсВзаиморасчетов,
			|	ТЧСуммыДолга.КратностьВзаиморасчетов,
			|	ТЧСуммыДолга.Сумма КАК Сумма
			|ИЗ
			|	Документ." + МетаданныеДокумента.Имя + ".СуммыДолга КАК ТЧСуммыДолга
			|
			|ГДЕ
			|	ТЧСуммыДолга.Ссылка = &Ссылка
			|	И ТЧСуммыДолга.ВидЗадолженности = &ВидЗадолженности
			|";
			
			ТаблицаОплат = Запрос.Выполнить().Выгрузить();
			Если ТаблицаОплат.Количество() > 0 Тогда
				//КУДиР.Содержание = "Взаимозачет, погашение кредиторской задолженности. ";
				ОплатаПоставщикуУСН(СтруктураШапкиДокумента, ТаблицаОплат, ДвиженияРегистров, КУДиР, , "РасчетныйДокумент");
			Конецесли;
			
			//Проверим второй договор
			Запрос = Новый Запрос;
			Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
			Запрос.УстановитьПараметр("ВидЗадолженности", Перечисления.ВидыЗадолженности.Дебиторская);
			Запрос.Текст = 
			"ВЫБРАТЬ
			|	ТЧСуммыДолга.НомерСтроки,
			|	ТЧСуммыДолга.ДоговорКонтрагента,
			|	ТЧСуммыДолга.Сделка КАК Сделка,
			|	ТЧСуммыДолга.ДокументРасчетовСКонтрагентом КАК РасчетныйДокумент,
			|	ТЧСуммыДолга.КурсВзаиморасчетов,
			|	ТЧСуммыДолга.КратностьВзаиморасчетов,
			|	ТЧСуммыДолга.Сумма КАК Сумма
			|ИЗ
			|	Документ." + МетаданныеДокумента.Имя + ".СуммыДолга КАК ТЧСуммыДолга
			|
			|ГДЕ
			|	ТЧСуммыДолга.Ссылка = &Ссылка
			|	И ТЧСуммыДолга.ВидЗадолженности = &ВидЗадолженности
			|";
			
			ТаблицаОплатДт = Запрос.Выполнить().Выгрузить();
			Если ТаблицаОплатДт.Количество() > 0 И ТаблицаОплат.Количество() > 0 Тогда
				Если ТаблицаОплатДт[0].ДоговорКонтрагента.ВидДоговора = ТаблицаОплат[0].ДоговорКонтрагента.ВидДоговора Тогда
					КУДиР.Графа4 = 0;
					КУДиР.Графа5 = 0;
					КУДиР.Графа6 = 0;
				КонецЕсли;
			КонецЕсли;

		КонецЕсли;
		
	ИначеЕсли (ВидОперации = "СписаниеДебиторскойЗадолженности") Тогда
		
		Если НЕ МетаданныеДокумента.ТабличныеЧасти.Найти("СуммыДолга") = Неопределено Тогда
			
			Запрос = Новый Запрос;
			Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
			Запрос.УстановитьПараметр("ВидЗадолженности", Перечисления.ВидыЗадолженности.Дебиторская);
			Запрос.Текст = 
			"ВЫБРАТЬ
			|	ТЧСуммыДолга.НомерСтроки,
			|	ТЧСуммыДолга.ДоговорКонтрагента,
			|	ТЧСуммыДолга.Сделка КАК Сделка,
			|	ТЧСуммыДолга.ДокументРасчетовСКонтрагентом КАК РасчетныйДокумент,
			|	ТЧСуммыДолга.КурсВзаиморасчетов,
			|	ТЧСуммыДолга.КратностьВзаиморасчетов,
			|	ТЧСуммыДолга.Сумма КАК Сумма
			|ИЗ
			|	Документ." + МетаданныеДокумента.Имя + ".СуммыДолга КАК ТЧСуммыДолга
			|
			|ГДЕ
			|	ТЧСуммыДолга.Ссылка = &Ссылка
			|	И ТЧСуммыДолга.ВидЗадолженности = &ВидЗадолженности
			|";
			
			ТаблицаОплат = Запрос.Выполнить().Выгрузить();
			Если ТаблицаОплат.Количество() > 0 Тогда
				СписаниеЗадолженностиУСН(СтруктураШапкиДокумента, ТаблицаОплат, ДвиженияРегистров, КУДиР,ВидДвиженияНакопления.Расход);
			Конецесли;
			
		КонецЕсли;

	ИначеЕсли (ВидОперации = "СписаниеКредиторскойЗадолженности") Тогда
		
		Если НЕ МетаданныеДокумента.ТабличныеЧасти.Найти("СуммыДолга") = Неопределено Тогда
			
			Запрос = Новый Запрос;
			Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
			Запрос.УстановитьПараметр("ВидЗадолженности", Перечисления.ВидыЗадолженности.Кредиторская);
			Запрос.Текст = 
			"ВЫБРАТЬ
			|	ТЧСуммыДолга.НомерСтроки,
			|	ТЧСуммыДолга.ДоговорКонтрагента,
			|	ТЧСуммыДолга.Сделка КАК Сделка,
			|	ТЧСуммыДолга.ДокументРасчетовСКонтрагентом КАК РасчетныйДокумент,
			|	ТЧСуммыДолга.КурсВзаиморасчетов,
			|	ТЧСуммыДолга.КратностьВзаиморасчетов,
			|	ТЧСуммыДолга.Сумма КАК Сумма
			|ИЗ
			|	Документ." + МетаданныеДокумента.Имя + ".СуммыДолга КАК ТЧСуммыДолга
			|
			|ГДЕ
			|	ТЧСуммыДолга.Ссылка = &Ссылка
			|	И ТЧСуммыДолга.ВидЗадолженности = &ВидЗадолженности
			|";
			
			ТаблицаОплат = Запрос.Выполнить().Выгрузить();
			Если ТаблицаОплат.Количество() > 0 Тогда
				СписаниеЗадолженностиУСН(СтруктураШапкиДокумента, ТаблицаОплат, ДвиженияРегистров, КУДиР, ВидДвиженияНакопления.Приход);
			Конецесли;
			
		КонецЕсли;
		
	ИначеЕсли (ВидОперации = "ПрочееДДС") Тогда
		ПрочееДДС(ДокументСсылка.ПолучитьОбъект());
		
	КонецЕсли;
	
	Если (ВидОперации = "НачислениеЗП") ИЛИ (ВидОперации = "Выпуск") Тогда
		МоментДокумента = СтруктураШапкиДокумента.Ссылка.МоментВремени().Дата;
	ИначеЕсли НЕ ЗначениеЗаполнено(СтруктураШапкиДокумента.ДатаОплаты) Тогда
		МоментДокумента = ?(МетаданныеДокумента.Реквизиты.Найти("ПериодРегистрации") <> Неопределено, КонецКвартала(СтруктураШапкиДокумента.Ссылка.ПериодРегистрации), СтруктураШапкиДокумента.Ссылка.МоментВремени().Дата);
	Иначе
		МоментДокумента = СтруктураШапкиДокумента.ДатаОплаты;
	КонецЕсли;
	
	Если НЕ ДвиженияРегистров = Неопределено Тогда
		Если ДвиженияРегистров.ВзаиморасчетыРасход.Количество() > 0 Тогда
			
			НаборЗаписей = РегистрыНакопления.ВзаиморасчетыУСН.СоздатьНаборЗаписей();
			НаборЗаписей.Отбор.Регистратор.Установить(ДокументСсылка);
			
			ТаблицаДвижений = НаборЗаписей.Выгрузить();
			ТаблицаДвижений.Очистить();
			// Заполним таблицу движений.
			ОбщегоНазначения.ЗагрузитьВТаблицуЗначений(ДвиженияРегистров.ВзаиморасчетыРасход, ТаблицаДвижений);
			// Недостающие поля.
			ТаблицаДвижений.ЗаполнитьЗначения(ДокументСсылка.Организация, "Организация");
			ТаблицаДвижений.ЗаполнитьЗначения(МоментДокумента, "Период");
			ТаблицаДвижений.ЗаполнитьЗначения(ДокументСсылка, "Регистратор");
			ТаблицаДвижений.ЗаполнитьЗначения(Истина, "Активность");
				
			Если ЭтапПроведения = 0 Тогда
				ОбщегоНазначения.ДобавитьСтрокиВНаборЗаписей(НаборЗаписей, ТаблицаДвижений);
				НаборЗаписей.Записать(Истина);
			Иначе
				НаборЗаписей.Прочитать();
				
				Инд=0;
				Пока Инд < НаборЗаписей.Количество() Цикл
					Если НЕ НаборЗаписей[Инд].ЭтапПроведения < ЭтапПроведения Тогда
						НаборЗаписей.Удалить(Инд);
					Иначе
						Инд=Инд+1;
					КонецЕсли;
				КонецЦикла;
				
				ТаблицаДвижений.ЗаполнитьЗначения(ЭтапПроведения, "ЭтапПроведения");
				ОбщегоНазначения.ДобавитьСтрокиВНаборЗаписей(НаборЗаписей, ТаблицаДвижений);
				НаборЗаписей.Записать(Истина);
				
			КонецЕсли;
		КонецЕсли;
		Если ДвиженияРегистров.РасходыУСН.Количество() > 0 Тогда
			НаборЗаписей = РегистрыНакопления.РасходыПриУСН.СоздатьНаборЗаписей();
			НаборЗаписей.Отбор.Регистратор.Установить(ДокументСсылка);
			
			ТаблицаДвижений = НаборЗаписей.Выгрузить();
			ТаблицаДвижений.Очистить();
			// Заполним таблицу движений.
			ОбщегоНазначения.ЗагрузитьВТаблицуЗначений(ДвиженияРегистров.РасходыУСН, ТаблицаДвижений);
			// Недостающие поля.
			ТаблицаДвижений.ЗаполнитьЗначения(ДокументСсылка.Организация, "Организация");
			ТаблицаДвижений.ЗаполнитьЗначения(МоментДокумента, "Период");
			ТаблицаДвижений.ЗаполнитьЗначения(ДокументСсылка, "Регистратор");
			ТаблицаДвижений.ЗаполнитьЗначения(Истина, "Активность");
			
			Если ЭтапПроведения = 0 Тогда
				ОбщегоНазначения.ДобавитьСтрокиВНаборЗаписей(НаборЗаписей, ТаблицаДвижений);
				НаборЗаписей.Записать(Истина);
			Иначе
				НаборЗаписей.Прочитать();
				
				Инд=0;
				Пока Инд < НаборЗаписей.Количество() Цикл
					Если НЕ НаборЗаписей[Инд].ЭтапПроведения < ЭтапПроведения Тогда
						НаборЗаписей.Удалить(Инд);
					Иначе
						Инд=Инд+1;
					КонецЕсли;
				КонецЦикла;
				
				ТаблицаДвижений.ЗаполнитьЗначения(ЭтапПроведения, "ЭтапПроведения");
				ОбщегоНазначения.ДобавитьСтрокиВНаборЗаписей(НаборЗаписей, ТаблицаДвижений);
				НаборЗаписей.Записать(Истина);
				
			КонецЕсли;
		КонецЕсли;
		
		Если (ДвиженияРегистров.КУДиР.Количество() > 0)
			ИЛИ (КУДиР.Графа4 <> 0) 
			ИЛИ (КУДиР.Графа5 <> 0) 
			ИЛИ (КУДиР.Графа6 <> 0)
			ИЛИ (КУДиР.Графа7 <> 0) Тогда
			
			ИмяДокумента = МетаданныеДокумента.Имя;
			ТаблицаРасходов = ДвиженияРегистров.КУДиР.Скопировать();
			Если ТаблицаРасходов.Колонки.Найти("ВидРасхода") = Неопределено Тогда
				ТаблицаРасходов.Колонки.Добавить("ВидРасхода");
			КонецЕсли;
			Если ТаблицаРасходов.Колонки.Найти("ОтражениеВУСН") = Неопределено Тогда
				ТаблицаРасходов.Колонки.Добавить("ОтражениеВУСН");
			КонецЕсли;
			Если ТаблицаРасходов.Колонки.Найти("Валюта") = Неопределено Тогда
				ТаблицаРасходов.Колонки.Добавить("Валюта");
				ТаблицаРасходов.ЗаполнитьЗначения(ВалютаРегламентированногоУчета, "Валюта");
			КонецЕсли;
			Если ТаблицаРасходов.Колонки.Найти("Сумма") = Неопределено Тогда
				ТаблицаРасходов.Колонки.Добавить("Сумма");
				ТаблицаРасходов.ЗаполнитьЗначения(0, "Сумма");
			КонецЕсли;
			Если ТаблицаРасходов.Колонки.Найти("НДС") = Неопределено Тогда
				ТаблицаРасходов.Колонки.Добавить("НДС");
				ТаблицаРасходов.ЗаполнитьЗначения(0, "НДС");
			КонецЕсли;
			//ТаблицаРасходов.Свернуть("ВидРасхода, ОтражениеВУСН, Валюта", "Сумма, НДС");
			
			КолвоЭлементовКоллекции = ТаблицаРасходов.Количество(); 
			Для ОбратныйИндекс = 1 По КолвоЭлементовКоллекции Цикл
				ЭлементКоллекции = ТаблицаРасходов[КолвоЭлементовКоллекции - ОбратныйИндекс];
				Если (ЭлементКоллекции.ОтражениеВУСН <> Перечисления.ОтражениеВУСН.Принимаются) И (ЭлементКоллекции.Сумма > 0) Тогда
					ТаблицаРасходов.Удалить(ЭлементКоллекции);
				ИначеЕсли ЭлементКоллекции.Валюта <> ВалютаРегламентированногоУчета Тогда
					СтруктураКурса = МодульВалютногоУчета.ПолучитьКурсВалюты(ЭлементКоллекции.Валюта, СтруктураШапкиДокумента.ДатаОплаты);
					ЭлементКоллекции.Сумма = МодульВалютногоУчета.ПересчитатьИзВалютыВВалюту(ЭлементКоллекции.Сумма, ЭлементКоллекции.Валюта,
																							ВалютаРегламентированногоУчета,
																							СтруктураКурса.Курс, 1,
																							СтруктураКурса.Кратность, 1);
					ЭлементКоллекции.НДС   = МодульВалютногоУчета.ПересчитатьИзВалютыВВалюту(ЭлементКоллекции.НДС, ЭлементКоллекции.Валюта,
																							ВалютаРегламентированногоУчета,
																							СтруктураКурса.Курс, 1,
																							СтруктураКурса.Кратность, 1);
				КонецЕсли;
			КонецЦикла;
			Если ВидОперации = "РаспределениеЕНВД" Тогда
				ДеревоРасходов = ТаблицуЗначенийВТаблицуСГруппировкой(ТаблицаРасходов, "", "Сумма, НДС", ВидОперации);
			ИначеЕсли НЕ ТаблицаРасходов.Колонки.Найти("КлючУникальности") = НЕОПРЕДЕЛЕНО Тогда
				ДеревоРасходов = ТаблицуЗначенийВТаблицуСГруппировкой(ТаблицаРасходов, "ВидРасхода, КлючУникальности", "Сумма, НДС", ВидОперации);
			Иначе
				ДеревоРасходов = ТаблицуЗначенийВТаблицуСГруппировкой(ТаблицаРасходов, "ВидРасхода", "Сумма, НДС", ВидОперации);
			КонецЕсли;
			//ТаблицаРасходов.Свернуть("ВидРасхода", "Сумма, НДС");
			
			ДеревоРасходов.Колонки.Добавить("Графа4", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15,2)));
			ДеревоРасходов.Колонки.Добавить("Графа5", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15,2)));
			ДеревоРасходов.Колонки.Добавить("Графа6", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15,2)));
			ДеревоРасходов.Колонки.Добавить("ДоходЕНВД", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15,2)));
			ДеревоРасходов.Колонки.Добавить("Описание");
				
			Если ДеревоРасходов.Итог("Сумма") <> 0 Тогда
												
				К4 = КУДиР.Графа4 / ДеревоРасходов.Итог("Сумма");
				К5 = КУДиР.Графа5 / ДеревоРасходов.Итог("Сумма");
				К6 = КУДиР.Графа6 / ДеревоРасходов.Итог("Сумма");
				КЕ = КУДиР.ДоходЕНВД / ДеревоРасходов.Итог("Сумма");
				Для каждого Расход Из ДеревоРасходов Цикл
					
					Если Расход.Сумма > 0 Тогда
						Описание = "Признаны";
					Иначе
						Описание = "Сторнированы";
					КонецЕсли;
					
					Расход.Строки.Колонки.Добавить("Графа4", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15,2)));
					Расход.Строки.Колонки.Добавить("Графа5", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15,2)));
					Расход.Строки.Колонки.Добавить("Графа6", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15,2)));
					Расход.Строки.Колонки.Добавить("ДоходЕНВД", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15,2)));
					Расход.Строки.Колонки.Добавить("Описание");
					
					Если ВидОперации = "РаспределениеЕНВД" Тогда
						Описание = Описание + " расходы по результатам распределения (УСН/ЕНВД), ";
					ИначеЕсли Расход.ВидРасхода = Перечисления.ВидыРасходовУСН.Номенклатура Тогда
						Если ВидОперации = "Реализация" Тогда
							Описание = Описание + " расходы на приобретение товаров, ";
						Иначе
							Описание = Описание + " расходы на приобретение ТМЦ, ";
						Конецесли;
					ИначеЕсли Расход.ВидРасхода = Перечисления.ВидыРасходовУСН.ДопРасходы Тогда
						Описание = Описание + " транспортно-заготовительные расходы, ";
					ИначеЕсли Расход.ВидРасхода = Перечисления.ВидыРасходовУСН.Услуги Тогда
						Описание = Описание + " расходы на услуги сторонних организаций, ";
					ИначеЕсли Расход.ВидРасхода = Перечисления.ВидыРасходовУСН.РБП Тогда
						Описание = Описание + " РБП, ";
					ИначеЕсли Расход.ВидРасхода = Перечисления.ВидыРасходовУСН.Зарплата Тогда
						Описание = Описание + " расходы на оплату труда, ";
					ИначеЕсли Расход.ВидРасхода = Перечисления.ВидыРасходовУСН.Налоги Тогда
						
						Если ДеревоРасходов.Колонки.Найти("КлючУникальности") <> НЕОПРЕДЕЛЕНО 
							И ЗначениеЗаполнено(Расход.КлючУникальности) Тогда
							Если Расход.КлючУникальности = ПланыСчетов.Хозрасчетный.НДФЛ Тогда
								Описание = Описание + " расходы на оплату труда в части НДФЛ, ";
							Иначе
								Описание = Описание + " расходы на уплату налогов (взносов), счет " + 
								Расход.КлючУникальности.Код + " """ + Расход.КлючУникальности.Наименование + """ , ";
							КонецЕсли;
						Иначе
							Описание = Описание + " расходы на уплату налогов (взносов), ";
						КонецЕсли;						
								
					ИначеЕсли Расход.ВидРасхода = Перечисления.ВидыРасходовУСН.НМА Тогда
						Описание = Описание + " расходы на приобретение НМА, ";
					ИначеЕсли Расход.ВидРасхода = Перечисления.ВидыРасходовУСН.НДС Тогда
						Описание = Описание + " расходы на уплату НДС, предъявленного поставщиком, ";
					Иначе
						Описание = Описание + " расходы, ";
					КонецЕсли;
					
					Расход.Описание =  Лев(КУДиР.Содержание + Описание,(СтрДлина(КУДиР.Содержание + Описание)-2)) + ".";
					
					Если Расход.Сумма > 0 Тогда
						Расход.Графа4 = Мин(К4* Расход.Сумма, Расход.Сумма);
						Расход.Графа5 = Мин(К5* Расход.Сумма, Расход.Сумма);
						Расход.Графа6 = Мин(К6* Расход.Сумма, Расход.Сумма);
						Расход.ДоходЕНВД = Мин(КЕ* Расход.Сумма, Расход.Сумма);
					Иначе
						Расход.Графа4 = Макс(К4* Расход.Сумма, Расход.Сумма);
						Расход.Графа5 = Макс(К5* Расход.Сумма, Расход.Сумма);
						Расход.Графа6 = Макс(К6* Расход.Сумма, Расход.Сумма);
						Расход.ДоходЕНВД = Макс(КЕ* Расход.Сумма, Расход.Сумма);
					КонецЕсли;
					
				КонецЦикла;
				Если ДеревоРасходов.Количество() > 0 Тогда
					ДеревоРасходов[0].Графа4 = ДеревоРасходов[0].Графа4 + (КУДиР.Графа4 - ДеревоРасходов.Итог("Графа4"));
					ДеревоРасходов[0].Графа5 = ДеревоРасходов[0].Графа5 + (КУДиР.Графа5 - ДеревоРасходов.Итог("Графа5")); 
					ДеревоРасходов[0].Графа6 = ДеревоРасходов[0].Графа6 + (КУДиР.Графа6 - ДеревоРасходов.Итог("Графа6")); 
					ДеревоРасходов[0].ДоходЕНВД = ДеревоРасходов[0].ДоходЕНВД + (КУДиР.ДоходЕНВД - ДеревоРасходов.Итог("ДоходЕНВД")); 
				КонецЕсли;
			КонецЕсли;
			
			Если (ДеревоРасходов.Количество() = 0) И ((КУДир.Графа4 <> 0) ИЛИ (КУДир.Графа5 <> 0) ИЛИ (КУДир.Графа6 <> 0) ИЛИ (КУДир.Графа7 <> 0)) Тогда
				Запись = ДеревоРасходов.Добавить();
				Запись.Описание = КУДиР.Содержание;
				Запись.Графа4	= КУДиР.Графа4;
				Запись.Графа5	= КУДиР.Графа5;
				Запись.Графа6	= КУДиР.Графа6;
				Запись.Сумма	= КУДиР.Графа7;
				Запись.НДС		= КУДиР.НДС;
				Запись.ДоходЕНВД= КУДиР.ДоходЕНВД;
			КонецЕсли;
			
			НаборЗаписей = РегистрыНакопления.КнигаУчетаДоходовИРасходов.СоздатьНаборЗаписей();
			НаборЗаписей.Отбор.Регистратор.Установить(ДокументСсылка);
			НаборЗаписей.Прочитать();
			
			Инд=0;
			Пока Инд < НаборЗаписей.Количество() Цикл
				Если НЕ НаборЗаписей[Инд].ЭтапПроведения < ЭтапПроведения Тогда
					НаборЗаписей.Удалить(Инд);
				Иначе
					Инд=Инд+1;
				КонецЕсли;
			КонецЦикла;
			
			НаборЗаписейРасшифровки = РегистрыСведений.РасшифровкаКУДиР.СоздатьНаборЗаписей();
			НаборЗаписейРасшифровки.Отбор.Регистратор.Установить(ДокументСсылка);
			НаборЗаписейРасшифровки.Прочитать();
			
			Инд=0;
			Пока Инд < НаборЗаписейРасшифровки.Количество() Цикл
				Если НЕ НаборЗаписейРасшифровки[Инд].ЭтапПроведения < ЭтапПроведения Тогда
					НаборЗаписейРасшифровки.Удалить(Инд);
				Иначе
					Инд=Инд+1;
				КонецЕсли;
			КонецЦикла;
			
			Для каждого Расход Из ДеревоРасходов Цикл
				ЗаписьКУДиР = НаборЗаписей.Добавить();
				ЗаписьКУДиР.Период = МоментДокумента;
				ЗаписьКУДиР.Регистратор = ДокументСсылка;
				ЗаписьКУДиР.Активность = Истина;
				ЗаписьКУДиР.Организация = ДокументСсылка.Организация;
				ЗаписьКУДиР.Содержание = Расход.Описание;
				ЗаписьКУДиР.ВидРасхода = Расход.ВидРасхода;
				ЗаписьКУДиР.СтрокаДокумента = НаборЗаписей.Количество();
				ЗаписьКУДиР.РеквизитыПервичногоДокумента = РеквизитыПервичногоДокумента(ДокументСсылка);
				ЗаписьКУДиР.Графа4 	= Расход.Графа4;
				ЗаписьКУДиР.Графа5 	= Расход.Графа5;
				ЗаписьКУДиР.ДоходЕНВД = Расход.ДоходЕНВД;
				ЗаписьКУДиР.Графа6 	= Расход.Графа6;
				ЗаписьКУДиР.Графа7 	= Расход.Сумма;
				ЗаписьКУДиР.НДС		= Расход.НДС;
				ЗаписьКУДиР.ЭтапПроведения = ЭтапПроведения;
				Если (ЗначениеЗаполнено(Расход.ВидРасхода)) или (ВидОперации = "РаспределениеЕНВД") Тогда
					СформироватьЗаписьРасшифровки(НаборЗаписейРасшифровки, Расход, ЗаписьКУДиР, ВидОперации);
				КонецЕсли;
			КонецЦикла;
			
			НаборЗаписей.Записать(Истина);
			НаборЗаписейРасшифровки.Записать(Истина);
				
		КонецЕсли;
	ИначеЕсли (КУДиР.Графа4 <> 0) 
		ИЛИ (КУДиР.Графа5 <> 0) 
		ИЛИ (КУДиР.Графа6 <> 0) 
		ИЛИ (КУДиР.Графа7 <> 0) Тогда
		
		Если ЭтапПроведения = 0 Тогда
			НаборЗаписей = РегистрыНакопления.КнигаУчетаДоходовИРасходов.СоздатьНаборЗаписей();
			НаборЗаписей.Отбор.Регистратор.Установить(ДокументСсылка);
			ЗаписьКУДиР = НаборЗаписей.Добавить();
			ЗаписьКУДиР.Период = МоментДокумента;
			ЗаписьКУДиР.Регистратор = ДокументСсылка;
			ЗаписьКУДиР.Активность = Истина;
			ЗаписьКУДиР.Организация = ДокументСсылка.Организация;
			ЗаписьКУДиР.Содержание = КУДиР.Содержание;
			ЗаписьКУДиР.РеквизитыПервичногоДокумента = РеквизитыПервичногоДокумента(ДокументСсылка);
			ЗаписьКУДиР.Графа4 = КУДиР.Графа4;
			ЗаписьКУДиР.Графа5 = КУДиР.Графа5;
			ЗаписьКУДиР.Графа6 = КУДиР.Графа6;
			ЗаписьКУДиР.Графа7 = КУДиР.Графа7;
			ЗаписьКУДиР.НДС	   = КУДиР.НДС;
			ЗаписьКУДиР.ДоходЕНВД = КУДиР.ДоходЕНВД;
			
			НаборЗаписей.Записать(Истина);
		Иначе
			НаборЗаписей = РегистрыНакопления.КнигаУчетаДоходовИРасходов.СоздатьНаборЗаписей();
			НаборЗаписей.Отбор.Регистратор.Установить(ДокументСсылка);
			НаборЗаписей.Прочитать();
			
			Инд=0;
			Пока Инд < НаборЗаписей.Количество() Цикл
				Если НЕ НаборЗаписей[Инд].ЭтапПроведения < ЭтапПроведения Тогда
					НаборЗаписей.Удалить(Инд);
				Иначе
					Инд=Инд+1;
				КонецЕсли;
			КонецЦикла;
			
			ЗаписьКУДиР = НаборЗаписей.Добавить();
			ЗаписьКУДиР.Период = МоментДокумента;
			ЗаписьКУДиР.Регистратор = ДокументСсылка;
			ЗаписьКУДиР.Активность = Истина;
			ЗаписьКУДиР.Организация = ДокументСсылка.Организация;
			ЗаписьКУДиР.Содержание = КУДиР.Содержание;
			ЗаписьКУДиР.РеквизитыПервичногоДокумента = РеквизитыПервичногоДокумента(ДокументСсылка);
			ЗаписьКУДиР.Графа4 = КУДиР.Графа4;
			ЗаписьКУДиР.Графа5 = КУДиР.Графа5;
			ЗаписьКУДиР.Графа6 = КУДиР.Графа6;
			ЗаписьКУДиР.Графа7 = КУДиР.Графа7;
			ЗаписьКУДиР.НДС	   = КУДиР.НДС;
			ЗаписьКУДиР.ДоходЕНВД = КУДиР.ДоходЕНВД;
			ЗаписьКУДиР.ЭтапПроведения = ЭтапПроведения;
			
			НаборЗаписей.Записать(Истина);
		КонецЕсли;
	КонецЕсли;		
КонецПроцедуры

//Определяет по виду документов вид движения и вызывает основную процедуру
//
Процедура ДвиженияУСН(ДокументСсылка, РежимПроведения = Неопределено) Экспорт //
	
	//Получение реквизитов шапки
	СтруктураШапкиДокумента = Новый Структура("Ссылка, ВидДокумента, Дата,  Организация,ДатаОплаты,Оплачено,ОтражатьВНалоговомУчете,ОтражатьВУправленческомУчете", ДокументСсылка, ДокументСсылка.Метаданные().Имя, ДокументСсылка.Дата);

	МетаданныеДокумента = ДокументСсылка.Метаданные().Реквизиты;
	Для Каждого Реквизит из СтруктураШапкиДокумента Цикл
		Если МетаданныеДокумента.найти(Реквизит.Ключ)=Неопределено тогда
			Продолжить;
		КонецЕсли;
		СтруктураШапкиДокумента.Вставить(Реквизит.Ключ,ДокументСсылка[Реквизит.Ключ]);
	КонецЦикла;
	
	Если СтруктураШапкиДокумента.ОтражатьВНалоговомУчете=Ложь Тогда
		Возврат;
	КонецЕсли;
		
	Если НЕ ЗначениеЗаполнено(СтруктураШапкиДокумента["ДатаОплаты"]) тогда
		СтруктураШапкиДокумента.Вставить("ДатаОплаты",СтруктураШапкиДокумента.Дата);
	Иначе
		//Особый контроль для документов оплаты
		СтруктураШапкиДокумента.Вставить("ДатаОплаты",?(СтруктураШапкиДокумента.Оплачено,УправлениеДенежнымиСредствами.ПолучитьДатуДвижений(СтруктураШапкиДокумента.Дата,СтруктураШапкиДокумента.ДатаОплаты),СтруктураШапкиДокумента.Дата));
		Если СтруктураШапкиДокумента.ВидДокумента = "ПриходныйКассовыйОрдер" или
			СтруктураШапкиДокумента.ВидДокумента = "РасходныйКассовыйОрдер" Тогда
		//Оплачено или не отражается в упр. учете
			Если СтруктураШапкиДокумента.Оплачено=Истина или СтруктураШапкиДокумента.ОтраженоВОперУчете=Ложь Тогда
				//Формируются движения по регламентированному учету
			Иначе
				//Документ по регламентированному учету не проводится			
				Возврат;
			КонецЕсли; 
		ИначеЕсли СтруктураШапкиДокумента.ВидДокумента = "ПлатежныйОрдерПоступлениеДенежныхСредств" или
			СтруктураШапкиДокумента.ВидДокумента = "ПлатежныйОрдерСписаниеДенежныхСредств" Тогда
			
			СтруктураШапкиДокумента.Вставить("Оплачено", Истина);
			//Формируются движения по регламентированному учету	
		Иначе
			Если СтруктураШапкиДокумента.Оплачено=Ложь тогда
				//Документ по регламентированному учету не проводится			
				Возврат;
			КонецЕсли;
		КонецЕсли; 
	Конецесли;
	
	Если НЕ ПрименениеУСН(СтруктураШапкиДокумента.Организация, СтруктураШапкиДокумента.ДатаОплаты) Тогда
		Возврат;
	КонецЕсли;
	
	Если ПрименениеУСНДоходы(СтруктураШапкиДокумента.Организация, СтруктураШапкиДокумента.ДатаОплаты) Тогда
		Возврат;
	КонецЕсли;
	
	//~//
	ПараметрыПартионногоУчета = глЗначениеПеременной("ПараметрыПартионногоУчета");
	СтруктураШапкиДокумента.Вставить("ПроводитьПоПартиям",ПараметрыПартионногоУчета.СписыватьПартииПриПроведенииДокументовНал);
	
	Если РежимПроведения = Неопределено Тогда
		//Вызвано обработкой вовстановления последовательности. Выполняются все действия (крооме регистрации в последовательности)
		
		//Очистим лишние движения под документами, что бы они не повлияли на результаты запросов
		
		НаборЗаписей = РегистрыНакопления.ВзаиморасчетыУСН.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Регистратор.Установить(ДокументСсылка);
		Если ЗначениеЗаполнено(НаборЗаписей.Отбор.Регистратор.Значение) Тогда
			НаборЗаписей.Записать(Истина);
		КонецЕсли;
		
		Если (НЕ ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.ОтчетКомиссионераОПродажах")) И
			(НЕ ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.ПолучениеУслугПоПереработке")) И
			(НЕ ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.АвансовыйОтчет")) И
			(НЕ ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.ВозвратТоваровОтПокупателя")) И
			(НЕ ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.ПоступлениеТоваровУслуг")) И
			(НЕ ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.ПоступлениеТоваровУслугВНТТ")) И
			(НЕ ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.РегламентныеОперацииНалоговогоУчетаПоУСН")) Тогда
			
			НаборЗаписей = РегистрыНакопления.РасходыПриУСН.СоздатьНаборЗаписей();
			НаборЗаписей.Отбор.Регистратор.Установить(ДокументСсылка);
			Если ЗначениеЗаполнено(НаборЗаписей.Отбор.Регистратор.Значение) Тогда
				
				НаборЗаписей.Прочитать();
				
				Инд=0;
				Пока Инд < НаборЗаписей.Количество() Цикл
					Если НаборЗаписей[Инд].ЭтапПроведения < 0 Тогда
						Инд=Инд+1;
					Иначе
						НаборЗаписей.Удалить(Инд);
					КонецЕсли;
				КонецЦикла;
				
				НаборЗаписей.Записать(Истина);
			КонецЕсли;
		КонецЕсли;
		
	Иначе
		//Вызов из документа. Необходимо зарегистрировать в последовательности
		РегистрацияВПоследовательностиНУ_УСН(СтруктураШапкиДокумента, РежимПроведения);
		
		Если не СтруктураШапкиДокумента.ПроводитьПоПартиям Тогда
			//Дополнительная обработка не производится.
			Возврат;  
		КонецЕсли;
	КонецЕсли; 
	//~\\
    	
	Если ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.АвансовыйОтчет") Тогда
		
		ОтразитьВУСН(ДокументСсылка, "Оплата", СтруктураШапкиДокумента,1);
		ОтразитьВУСН(ДокументСсылка, "Поступление", СтруктураШапкиДокумента,2);
		
	ИначеЕсли 
		ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.АккредитивПереданный") ИЛИ //~
		ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.АккредитивПолученный") ИЛИ //~
		ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.ИнкассовоеПоручениеПереданное") ИЛИ //~
		ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.ИнкассовоеПоручениеПолученное") ИЛИ //~
		ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.ПлатежноеТребованиеВыставленное") ИЛИ //~
		ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.ПлатежноеТребованиеПолученное") ИЛИ //~
		ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.ПлатежноеПоручениеВходящее") ИЛИ
		ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.ПлатежноеПоручениеИсходящее") ИЛИ
		ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.ПлатежныйОрдерПоступлениеДенежныхСредств") ИЛИ
		ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.ПлатежныйОрдерСписаниеДенежныхСредств") ИЛИ
		ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.ПриходныйКассовыйОрдер") ИЛИ
		ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.РасходныйКассовыйОрдер") ИЛИ
		ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.ПоступлениеДенежныхДокументов") ИЛИ
		ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.ВыдачаДенежныхДокументов") Тогда
		
		ОтразитьВУСН(ДокументСсылка, ,СтруктураШапкиДокумента);
		
	ИначеЕсли ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.АктОбОказанииПроизводственныхУслуг") ИЛИ
		ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.ПередачаНМА") ИЛИ  
		ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.РеализацияУслугПоПереработке") Тогда
		
		ОтразитьВУСН(ДокументСсылка, "РеализацияУслуг", СтруктураШапкиДокумента);
		
	ИначеЕсли ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.ПередачаОС") Тогда
		Если НЕ ДокументСсылка.ПравоСобственностиПереходитПослеГосРегистрации Тогда
			ОтразитьВУСН(ДокументСсылка, "РеализацияУслуг", СтруктураШапкиДокумента);
		КонецЕсли;
		
	//~//
	ИначеЕсли ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.ОтчетКомитентуОПродажах") Тогда
		
		Если УправлениеЗапасами.ИспользуетсяРасширеннаяАналитикаУчета(СтруктураШапкиДокумента.Дата) Тогда
			ОтразитьВУСН(ДокументСсылка, "Поступление", СтруктураШапкиДокумента, 0);
			ОтразитьВУСН(ДокументСсылка, "РеализацияУслуг", СтруктураШапкиДокумента, 1);
		Иначе
			ОтразитьВУСН(ДокументСсылка, "РеализацияУслуг", СтруктураШапкиДокумента);
		КонецЕсли;
			
	ИначеЕсли ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.ВозвратПереданныхТоваров") Тогда
		
		Если ДокументСсылка.ВидОперации = Перечисления.ВидыОперацийВозвратТоваровПоставщику.ИзПереработки Тогда
			ОтразитьВУСН(ДокументСсылка, "ВозвратИзПереработки", СтруктураШапкиДокумента);
		КонецЕсли;
		
	//~\\
	
	ИначеЕсли ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.ВозвратТоваровОтПокупателя") Тогда
		
		Если ДокументСсылка.ДоговорКонтрагента.ВидДоговора = Перечисления.ВидыДоговоровКонтрагентов.СКомиссионером Тогда
			ОтразитьВУСН(ДокументСсылка, "ВозвратСРеализации", СтруктураШапкиДокумента);
		Иначе
			ОтразитьВУСН(ДокументСсылка, "ВозвратОтПокупателя", СтруктураШапкиДокумента, 1);
		КонецЕсли;

	ИначеЕсли
		ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.ВозвратТоваровПоставщикуИзНТТ") ИЛИ //~
		ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.ВозвратТоваровПоставщику") Тогда
		
		Если ДокументСсылка.Товары.Количество() > 0 Тогда
			ОтразитьВУСН(ДокументСсылка, "ВозвратПоставщику", СтруктураШапкиДокумента);
		КонецЕсли;
		
	ИначеЕсли ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.ОтражениеЗарплатыВРеглУчете") Тогда
		
		ОтразитьВУСН(ДокументСсылка, "НачислениеЗП", СтруктураШапкиДокумента);
		
	ИначеЕсли ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.ОтчетКомиссионераОПродажах") Тогда
		
		Если НЕ ДокументСсылка.УдержатьКомиссионноеВознаграждение Тогда
			ОтразитьВУСН(ДокументСсылка, "УслугаКомиссионера", СтруктураШапкиДокумента,0);
			ОтразитьВУСН(ДокументСсылка, "РеализацияКомиссионером", СтруктураШапкиДокумента,1);
		Иначе
			ОтразитьВУСН(ДокументСсылка, "ЗачетВознагражденияКомиссионера", СтруктураШапкиДокумента,0);
			
			Отказ = Ложь;
			УчетнаяПолитика = ОбщегоНазначения.ПолучитьПараметрыУчетнойПолитики(СтруктураШапкиДокумента.Дата, Отказ, СтруктураШапкиДокумента.Организация, "Нал");
			ВыделятьНДСУСН = ?(Отказ, Истина, УчетнаяПолитика.ПорядокПризнанияРасходовПоНДС = Перечисления.ПорядокПризнанияРасходовПоНДС.ПоОплатеПоставщику);
			
			Если ВыделятьНДСУСН Тогда
				ОтразитьВУСН(ДокументСсылка, "ЗачетВознагражденияКомиссионераНДС", СтруктураШапкиДокумента,1);
				ОтразитьВУСН(ДокументСсылка, "РеализацияКомиссионером", СтруктураШапкиДокумента,2);
			Иначе
				ОтразитьВУСН(ДокументСсылка, "РеализацияКомиссионером", СтруктураШапкиДокумента,1);
			КонецЕсли;
		КонецЕсли;
	ИначеЕсли ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.РеализацияОтгруженныхТоваров") Тогда
		
		ОтразитьВУСН(ДокументСсылка, "РеализацияКомиссионером", СтруктураШапкиДокумента,1);
	
	ИначеЕсли ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.ОтчетОРозничныхПродажах") Тогда
		
		ОтразитьВУСН(ДокументСсылка, "Розница", СтруктураШапкиДокумента);
		
	ИначеЕсли ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.ПередачаТоваров") Тогда
		
		Если НЕ ДокументСсылка.ВидОперации = Перечисления.ВидыОперацийПередачаТоваров.ИзПереработки Тогда
			
			ОтразитьВУСН(ДокументСсылка, "ВПереработку", СтруктураШапкиДокумента);
			
		КонецЕсли;
				
	ИначеЕсли ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.ПолучениеУслугПоПереработке") Тогда
	//ИначеЕсли ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.ПоступлениеИзПереработки") Тогда // БП
		
		ОтразитьВУСН(ДокументСсылка, "УслугаПоПереработке", СтруктураШапкиДокумента);
		ОтразитьВУСН(ДокументСсылка, "ИзПереработки", СтруктураШапкиДокумента , 1);

	ИначеЕсли ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.ПоступлениеДопРасходов") ИЛИ
		ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.ПоступлениеНМА") Тогда
		
		ОтразитьВУСН(ДокументСсылка, "Поступление", СтруктураШапкиДокумента);
		
	ИначеЕсли ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.ПоступлениеТоваровУслуг") Тогда
		
		Если (ДокументСсылка.ДоговорКонтрагента.ВидДоговора = Перечисления.ВидыДоговоровКонтрагентов.СПоставщиком) И
			ДокументСсылка.ВидОперации <> Перечисления.ВидыОперацийПоступлениеТоваровУслуг.ВПереработку Тогда
			
			ОтразитьВУСН(ДокументСсылка, "Поступление", СтруктураШапкиДокумента);
			
		КонецЕсли;
		
	//~//
	ИначеЕсли ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.ПоступлениеТоваровУслугВНТТ") Тогда
		
		Если ДокументСсылка.ДоговорКонтрагента.ВидДоговора = Перечисления.ВидыДоговоровКонтрагентов.СПоставщиком И 
			ДокументСсылка.ВидОперации = Перечисления.ВидыОперацийПоступлениеТоваровУслугВНТТ.ОтПоставщика Тогда
			
			ОтразитьВУСН(ДокументСсылка, "Поступление", СтруктураШапкиДокумента);
			
		КонецЕсли;
	//~\\
	ИначеЕсли ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.РеализацияТоваровУслуг") Тогда
		
		Если ДокументСсылка.ДоговорКонтрагента.ВидДоговора = Перечисления.ВидыДоговоровКонтрагентов.СПокупателем
			И (НЕ ДокументСсылка.ВидОперации = Перечисления.ВидыОперацийРеализацияТоваров.ОтгрузкаБезПереходаПраваСобственности) Тогда
			
			ОтразитьВУСН(ДокументСсылка, "Реализация", СтруктураШапкиДокумента);
			
		Иначе
			
			ОтразитьВУСН(ДокументСсылка, "НаРеализацию", СтруктураШапкиДокумента);
			
		КонецЕсли;
		
	ИначеЕсли ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.СписаниеТоваров") ИЛИ
		ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.ПередачаМатериаловВЭксплуатацию") ИЛИ
		ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.ТребованиеНакладная") Тогда
		
		ОтразитьВУСН(ДокументСсылка, "Списание", СтруктураШапкиДокумента);
	
	ИначеЕсли ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.ПеремещениеТоваров") Тогда
		
		ОтразитьВУСН(ДокументСсылка, "Перемещение", СтруктураШапкиДокумента);
		
	ИначеЕсли ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.КомплектацияНоменклатуры") Тогда
		
		ОтразитьВУСН(ДокументСсылка, "Комплектация", СтруктураШапкиДокумента);
		
	ИначеЕсли ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.КорректировкаДолга") Тогда
		
		Если ДокументСсылка.ВидОперации = Перечисления.ВидыОперацийКорректировкаДолга.ПереносЗадолженности Тогда
		
			ОтразитьВУСН(ДокументСсылка, "ПереносЗадолженности", СтруктураШапкиДокумента);
			
		ИначеЕсли ДокументСсылка.ВидОперации = Перечисления.ВидыОперацийКорректировкаДолга.ПроведениеВзаимозачета Тогда	
			
			ОтразитьВУСН(ДокументСсылка, "ПроведениеВзаимозачетаДебитор", СтруктураШапкиДокумента,0);
			ОтразитьВУСН(ДокументСсылка, "ПроведениеВзаимозачетаКредитор", СтруктураШапкиДокумента,1);
			
		ИначеЕсли ДокументСсылка.ВидОперации = Перечисления.ВидыОперацийКорректировкаДолга.СписаниеЗадолженности Тогда		

			ОтразитьВУСН(ДокументСсылка, "СписаниеДебиторскойЗадолженности", СтруктураШапкиДокумента,0);
			ОтразитьВУСН(ДокументСсылка, "СписаниеКредиторскойЗадолженности", СтруктураШапкиДокумента,1);
			
		КонецЕсли;
	//~//
	ИначеЕсли ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.РасчетСебестоимостиВыпуска") Тогда
		
		ОтразитьВУСН(ДокументСсылка, "Выпуск", СтруктураШапкиДокумента);
		
	ИначеЕсли ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.ОплатаОтПокупателяПлатежнойКартой") Тогда
		
		ОтразитьВУСН(ДокументСсылка, "ПереносЗадолженности", СтруктураШапкиДокумента);
	//~\\
	КонецЕсли;
	
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// ОПЕРАЦИИ С ПОСЛЕДОВАТЕЛЬНОСТЬЮ

Процедура РегистрацияВПоследовательностиНУ_УСН(СтруктураШапкиДокумента, РежимПроведения) //~
	
	Если СтруктураШапкиДокумента.ПроводитьПоПартиям Тогда
		//Непосредственная регистрация в последовательности
		ПоследовательностьДляРегистрации = Последовательности.НалоговыйУчетУСН.СоздатьНаборЗаписей();
		ПоследовательностьДляРегистрации.Отбор.Регистратор.Установить(СтруктураШапкиДокумента.Ссылка);
		ПоследовательностьДляРегистрации.Прочитать();
		Если ПоследовательностьДляРегистрации.Количество()>0 Тогда
			ПоследовательностьДляРегистрации.Очистить();
		КонецЕсли; 
		
		ЗаписьРегистрации = ПоследовательностьДляРегистрации.Добавить();
		ЗаписьРегистрации.Период 	  = СтруктураШапкиДокумента.ДатаОплаты;
		ЗаписьРегистрации.Организация = СтруктураШапкиДокумента.Организация;
		// При этом должна соответствующим образом подвинуться граница (выполняется средствами платформы)
		
		Если ПоследовательностьДляРегистрации.Модифицированность() Тогда
			ПоследовательностьДляРегистрации.Записать();
		КонецЕсли;
	Иначе
		// В неоперативном режиме границы последовательностей сдвигаются назад, если они позже документа.
		Если РежимПроведения = РежимПроведенияДокумента.Неоперативный Тогда
			СдвигГраницыПоследовательностиНалоговогоУчетаУСННазад(СтруктураШапкиДокумента.ДатаОплаты,СтруктураШапкиДокумента.Ссылка, СтруктураШапкиДокумента.Организация);
		КонецЕсли;
	КонецЕсли; 
	

КонецПроцедуры
 
// Сдвиг ГП НУ УСН назад, используемый в неоперативных документах.
//
Процедура СдвигГраницыПоследовательностиНалоговогоУчетаУСННазад(Дата, Ссылка, Организация) //~
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	НУУСН_Границы.МоментВремени
	|ИЗ
	|	Последовательность.НалоговыйУчетУСН.Границы КАК НУУСН_Границы
	|ГДЕ
	|	НУУСН_Границы.Организация = &Организация
	|
	|	ДЛЯ ИЗМЕНЕНИЯ";
		
	Запрос.УстановитьПараметр("Организация", Организация);
	
	Если глЗначениеПеременной("ИспользоватьБлокировкуДанных") Тогда
		СтруктураПараметровБлокировки = Новый Структура(
			"ТипТаблицы,ИмяТаблицы"
			,"Последовательность"
			,"НалоговыйУчетУСН");
		СтруктураЗначенийБлокировки = Новый Структура("Организация", Организация);
		
		ОбщегоНазначения.УстановитьУправляемуюБлокировку(СтруктураПараметровБлокировки,СтруктураЗначенийБлокировки,, Ложь, "");
	КонецЕсли;
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	МоментВремениДокумента = Новый МоментВремени(Дата, Ссылка);
	
	Если Выборка.Следующий() Тогда
		
		// Граница переносится назад, если документ проводится задним числом
		Если МоментВремениДокумента.Сравнить(Выборка.МоментВремени) = -1 Тогда
			Последовательности.НалоговыйУчетУСН.УстановитьГраницу(МоментВремениДокумента, Новый Структура("Организация", Организация));
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры