// Отображение макета обновления

Функция ДокументОписаниеОбновлений(СтартовыйНомерВерсии) Экспорт
	
	МакетОписаниеОбновлений = Обработки.ОбновлениеИнформационнойБазы.ПолучитьМакет("ОписаниеОбновлений");
	
	ОписаниеИзмененийСистемыПереопределяемый.ПриПодготовкеМакетаОписанияОбновлений(МакетОписаниеОбновлений);
	
	Если НЕ ЗначениеЗаполнено(СтартовыйНомерВерсии) Тогда
		Возврат МакетОписаниеОбновлений;
	КонецЕсли;
	
	// Выведем описание только "новых" версий (на которые обновились)
	// Получим список "новых" версий, для которых есть описание
	ТаблицаОписаний = Новый ТаблицаЗначений();
	ТаблицаОписаний.Колонки.Добавить("Версия");
	
	ИдентификаторОбласти = "Версия";
	ДлинаИдентификатора = СтрДлина(ИдентификаторОбласти);
	
	Для Каждого Область Из МакетОписаниеОбновлений.Области Цикл
		Если Лев(Область.Имя, ДлинаИдентификатора) = ИдентификаторОбласти Тогда
			
			НомерВерсии = Сред(Область.Имя, ДлинаИдентификатора + 1);
			НомерВерсии = СтрЗаменить(НомерВерсии, "_", ".");
			
			Попытка
				НадоВыводитьВерсию = СтроковыеФункцииКлиентСервер.СравнитьВерсии(НомерВерсии, СтартовыйНомерВерсии) > 0;
			Исключение
				// Исключение возникнет, если в макете номер версии указан неверно.
				НадоВыводитьВерсию = Ложь;
			КонецПопытки;
			
			Если НадоВыводитьВерсию Тогда
				ТаблицаОписаний.Добавить().Версия = НомерВерсии;
			КонецЕсли;
			
		КонецЕсли;
	КонецЦикла;
	
	// Упорядочим список версий
	ОбновлениеИнформационнойБазы.УпорядочитьСписокВерсий(ТаблицаОписаний);
	
	// Выведем версии
	ДокументОписаниеОбновлений = Новый ТабличныйДокумент();
	Для Каждого Версия Из ТаблицаОписаний Цикл
		ВывестиОписаниеИзменений(Версия.Версия, ДокументОписаниеОбновлений, МакетОписаниеОбновлений);
	КонецЦикла;
	
	Возврат ДокументОписаниеОбновлений;
	
КонецФункции

// Вывести описания изменений в указанной версии
//
// Параметры
//  НомерВерсии  – Строка - номер версии, для которого выводится описание из макета
//                          табличного документа МакетОписаниеОбновлений в табличный документ 
//                          ДокументОписаниеОбновлений
//
Процедура ВывестиОписаниеИзменений(Знач НомерВерсии, ДокументОписаниеОбновлений, МакетОписаниеОбновлений)
	
	Номер = СтрЗаменить(НомерВерсии, ".", "_");
	
	Если МакетОписаниеОбновлений.Области.Найти("Шапка" + Номер) = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ДокументОписаниеОбновлений.Вывести(МакетОписаниеОбновлений.ПолучитьОбласть("Шапка" + Номер));
	ДокументОписаниеОбновлений.НачатьГруппуСтрок("Версия" + Номер);
	ДокументОписаниеОбновлений.Вывести(МакетОписаниеОбновлений.ПолучитьОбласть("Версия" + Номер));
	ДокументОписаниеОбновлений.ЗакончитьГруппуСтрок();
	ДокументОписаниеОбновлений.Вывести(МакетОписаниеОбновлений.ПолучитьОбласть("Отступ"));
	
КонецПроцедуры