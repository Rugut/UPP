///////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ МЕХАНИЗМА АНКЕТИРОВАНИЕ
///////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////

// Функция возвращает выборку по вопросам анкеты
//
Функция ВыполнитьЗапросПоАнкете(Анкета)
	
	Запрос = Новый Запрос();
	
	Запрос.УстановитьПараметр("Ссылка",	Анкета);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТиповыеАнкетыВопросыАнкеты.НомерСтроки КАК НомерСтроки,
	|	ТиповыеАнкетыВопросыАнкеты.Вопрос КАК Вопрос,
	|	ТиповыеАнкетыВопросыАнкеты.Раздел КАК Раздел,
	|	ТиповыеАнкетыВопросыАнкеты.Раздел.Код КАК КодРаздела,
	|	"". "" + ТиповыеАнкетыВопросыАнкеты.Раздел.Наименование КАК РазделНаименование,
	|	ВопросыДляАнкетированияКолонкиТаблицы.КолонкаТаблицы КАК КолонкаТаблицы,
	|	ВариантыОтветовОпросов.Наименование КАК ВариантыОтветовНаименование,
	|	ВариантыОтветовОпросовТабличные.Наименование КАК КолонкаТаблицыВариантыОтветовНаименование,
	|	ТиповыеАнкетыВопросыАнкеты.Вопрос.Наименование КАК Наименование,
	|	ТиповыеАнкетыВопросыАнкеты.Вопрос.ТипОтветаНаВопрос КАК ТипОтветаНаВопрос,
	|	ТиповыеАнкетыВопросыАнкеты.Вопрос.Длина КАК Длина,
	|	ТиповыеАнкетыВопросыАнкеты.Вопрос.Код КАК Код,
	|	ТиповыеАнкетыВопросыАнкеты.Вопрос.Родитель КАК Родитель,
	|	ТиповыеАнкетыВопросыАнкеты.Вопрос.ПолнаяФормулировка КАК ПолнаяФормулировка,
	|	ТиповыеАнкетыВопросыАнкеты.Вопрос.ТипЗначения КАК ТипЗначения,
	|	ВЫБОР
	|		КОГДА ТиповыеАнкетыВопросыАнкеты.Обязательный В (ЗНАЧЕНИЕ(Перечисление.ОбязательностьЗаполненияОтветаНаВопрос.НеОбязателенКЗаполнению), ЗНАЧЕНИЕ(Перечисление.ОбязательностьЗаполненияОтветаНаВопрос.ПустаяСсылка))
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК ОбязателенДляЗаполнения,
	|	ТиповыеАнкетыВопросыАнкеты.Вопрос.КоличествоСтрокТаблицы КАК СтрокВТаблице,
	|	ТиповыеАнкетыВопросыАнкеты.Вопрос.Предопределенный КАК Предопределенный,
	|	ТиповыеАнкетыВопросыАнкеты.Вопрос.ВидКонтактнойИнформации КАК ВидКонтактнойИнформации,
	|	ТиповыеАнкетыВопросыАнкеты.Вопрос.ВидКонтактнойИнформации.Тип КАК ТипКонтактнойИнформации,
	|	ТиповыеАнкетыВопросыАнкеты.Вопрос.ВидКонтактнойИнформации.ВидОбъектаКонтактнойИнформации КАК ВидОбъектаКонтактнойИнформации,
	|	ВопросыДляАнкетированияКолонкиТаблицы.КолонкаТаблицы.ТипЗначения,
	|	ВопросыДляАнкетированияКолонкиТаблицы.КолонкаТаблицы.ТипОтветаНаВопрос,
	|	ВопросыДляАнкетированияКолонкиТаблицы.КолонкаТаблицы.Длина,
	|	ВопросыДляАнкетированияКолонкиТаблицы.КолонкаТаблицы.ПолнаяФормулировка,
	|	ВопросыДляАнкетированияКолонкиТаблицы.КолонкаТаблицы.Код КАК КолонкаТаблицыКод,
	|	ВопросыДляАнкетированияКолонкиТаблицы.КолонкаТаблицы.Родитель,
	|	ВопросыДляАнкетированияКолонкиТаблицы.КолонкаТаблицы.Наименование,
	|	ВопросыДляАнкетированияКолонкиТаблицы.КолонкаТаблицы.КоличествоСтрокТаблицы,
	|	ВопросыДляАнкетированияКолонкиТаблицы.КолонкаТаблицы.Предопределенный,
	|	ВопросыДляАнкетированияКолонкиТаблицы.КолонкаТаблицы.ВидКонтактнойИнформации,
	|	ВопросыДляАнкетированияКолонкиТаблицы.КолонкаТаблицы.ВидКонтактнойИнформации.Тип КАК КолонкаТаблицыТипКонтактнойИнформации,
	|	ВопросыДляАнкетированияКолонкиТаблицы.КолонкаТаблицы.ВидКонтактнойИнформации.ВидОбъектаКонтактнойИнформации КАК КолонкаТаблицыВидОбъектаКонтактнойИнформации,
	|	ВопросыДляАнкетированияКолонкиТаблицы.НомерСтроки КАК КолонкаТаблицыНомерСтроки,
	|	ВариантыОтветовОпросовТабличные.Код КАК КолонкаТаблицыВариантыОтветовКод,
	|	ВариантыОтветовОпросовТабличные.Ссылка КАК КолонкаТаблицыВариантыОтветовСсылка,
	|	ВариантыОтветовОпросовТабличные.ТребуетРазвернутыйОтвет КАК КолонкаТаблицыВариантыОтветовТребуетРазвернутыйОтвет,
	|	ЛОЖЬ КАК КолонкаТаблицыОбязателенДляЗаполнения,
	|	ВариантыОтветовОпросов.Код КАК ВариантыОтветовКод,
	|	ВариантыОтветовОпросов.Ссылка КАК ВариантыОтветовСсылка,
	|	ВариантыОтветовОпросов.ТребуетРазвернутыйОтвет КАК ВариантыОтветовТребуетРазвернутыйОтвет
	|ИЗ
	|	Справочник.ТиповыеАнкеты.ВопросыАнкеты КАК ТиповыеАнкетыВопросыАнкеты
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовХарактеристик.ВопросыДляАнкетирования.КолонкиТаблицы КАК ВопросыДляАнкетированияКолонкиТаблицы
	|			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВариантыОтветовОпросов КАК ВариантыОтветовОпросовТабличные
	|			ПО ВопросыДляАнкетированияКолонкиТаблицы.КолонкаТаблицы = ВариантыОтветовОпросовТабличные.Владелец
	|		ПО ТиповыеАнкетыВопросыАнкеты.Вопрос = ВопросыДляАнкетированияКолонкиТаблицы.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВариантыОтветовОпросов КАК ВариантыОтветовОпросов
	|		ПО ТиповыеАнкетыВопросыАнкеты.Вопрос = ВариантыОтветовОпросов.Владелец
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовХарактеристик.ВопросыДляАнкетирования КАК ВопросыДляАнкетирования
	|		ПО ТиповыеАнкетыВопросыАнкеты.Вопрос = ВопросыДляАнкетирования.Ссылка
	|ГДЕ
	|	ТиповыеАнкетыВопросыАнкеты.Ссылка = &Ссылка
	|	И ТиповыеАнкетыВопросыАнкеты.Вопрос <> ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВопросыДляАнкетирования.ПустаяСсылка)
	|
	|УПОРЯДОЧИТЬ ПО
	|	КодРаздела,
	|	НомерСтроки,
	|	ВариантыОтветовКод,
	|	КолонкаТаблицыНомерСтроки,
	|	КолонкаТаблицыВариантыОтветовКод
	|ИТОГИ
	|	МАКСИМУМ(НомерСтроки),
	|	МАКСИМУМ(КодРаздела),
	|	МАКСИМУМ(ОбязателенДляЗаполнения),
	|	МАКСИМУМ(КолонкаТаблицыНомерСтроки),
	|	МАКСИМУМ(КолонкаТаблицыВариантыОтветовКод),
	|	МАКСИМУМ(ВариантыОтветовКод)
	|ПО
	|	Раздел,
	|	Вопрос,
	|	КолонкаТаблицы";
	
	Возврат Запрос.Выполнить();
	
КонецФункции // ВыполнитьЗапросПоАнкете()

Функция ВыполнитьЗапросПоОпросу(ДокументОпрос)
	
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("Опрос",	ДокументОпрос.Ссылка);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ОпросВопросы.Вопрос КАК Вопрос,
	|	ОпросВопросы.Ответ,
	|	ОпросВопросы.ТиповойОтвет КАК ТиповойОтвет,
	|	ОпросВопросы.Вопрос КАК КолонкаТаблицы,
	|	NULL КАК НомерСтрокиВТаблице,
	|	ОпросВопросы.Вопрос КАК ВопросКоличество
	|ИЗ
	|	Документ.Опрос.Вопросы КАК ОпросВопросы
	|ГДЕ
	|	ОпросВопросы.Ссылка.Ссылка = &Опрос
	|	И (НЕ ОпросВопросы.Вопрос.ТипОтветаНаВопрос В (ЗНАЧЕНИЕ(Перечисление.ТипыОтветаНаВопросАнкеты.Табличный), ЗНАЧЕНИЕ(Перечисление.ТипыОтветаНаВопросАнкеты.НесколькоВариантовОтвета)))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ОпросСоставнойОтвет.ВопросВладелец,
	|	ОпросСоставнойОтвет.Ответ,
	|	ОпросСоставнойОтвет.ТиповойОтвет,
	|	ОпросСоставнойОтвет.Вопрос,
	|	ОпросСоставнойОтвет.НомерСтрокиВТаблице,
	|	ОпросСоставнойОтвет.ВопросВладелец
	|ИЗ
	|	Документ.Опрос.СоставнойОтвет КАК ОпросСоставнойОтвет
	|ГДЕ
	|	ОпросСоставнойОтвет.Ссылка.Ссылка = &Опрос
	|ИТОГИ
	|	КОЛИЧЕСТВО(ВопросКоличество)
	|ПО
	|	Вопрос";
	
	Возврат Запрос.Выполнить();
	
КонецФункции

Функция ВыполнитьЗапросПоДанным(ОпрашиваемоеЛицо, ТиповаяАнкета)
	
	Если ТипЗнч(ОпрашиваемоеЛицо) = Тип("СправочникСсылка.ФизическиеЛица") Тогда
	
		Запрос = Новый Запрос;
		
		Запрос.УстановитьПараметр("Ссылка",	ОпрашиваемоеЛицо);
		Запрос.УстановитьПараметр("Анкета",	ТиповаяАнкета);
		
		Запрос.Текст =
		"ВЫБРАТЬ
		|	ВопросыДляАнкетированияКолонкиТаблицы.Ссылка КАК Вопрос,
		|	ВопросыДляАнкетированияКолонкиТаблицы.КолонкаТаблицы КАК КолонкаТаблицы,
		|	ФизическиеЛицаСоставСемьи.НомерСтроки КАК НомерСтрокиВТаблице,
		|	"""" КАК Ответ,
		|	ВЫБОР
		|		КОГДА ВопросыДляАнкетированияКолонкиТаблицы.КолонкаТаблицы = ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВопросыДляАнкетирования.СоставСемьиСтепеньРодства)
		|			ТОГДА ФизическиеЛицаСоставСемьи.СтепеньРодства
		|		КОГДА ВопросыДляАнкетированияКолонкиТаблицы.КолонкаТаблицы = ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВопросыДляАнкетирования.СоставСемьиРодственник)
		|			ТОГДА ФизическиеЛицаСоставСемьи.Имя
		|		КОГДА ВопросыДляАнкетированияКолонкиТаблицы.КолонкаТаблицы = ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВопросыДляАнкетирования.СоставСемьиДатаРождения)
		|			ТОГДА ФизическиеЛицаСоставСемьи.ДатаРождения
		|	КОНЕЦ КАК ТиповойОтвет
		|ИЗ
		|	ПланВидовХарактеристик.ВопросыДляАнкетирования.КолонкиТаблицы КАК ВопросыДляАнкетированияКолонкиТаблицы
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ФизическиеЛица.СоставСемьи КАК ФизическиеЛицаСоставСемьи
		|		ПО (ФизическиеЛицаСоставСемьи.Ссылка = &Ссылка)
		|			И (ВопросыДляАнкетированияКолонкиТаблицы.Ссылка = ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВопросыДляАнкетирования.ТСоставСемьи))
		|			И ((НЕ ВопросыДляАнкетированияКолонкиТаблицы.Ссылка.ЭтоГруппа))
		|			И (ВопросыДляАнкетированияКолонкиТаблицы.Ссылка В
		|				(ВЫБРАТЬ
		|					ТиповыеАнкетыВопросыАнкеты.Вопрос
		|				ИЗ
		|					Справочник.ТиповыеАнкеты.ВопросыАнкеты КАК ТиповыеАнкетыВопросыАнкеты
		|				ГДЕ
		|					ТиповыеАнкетыВопросыАнкеты.Ссылка = &Анкета))
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВопросыДляАнкетирования.Ссылка,
		|	NULL,
		|	NULL,
		|	"""",
		|	ВЫБОР
		|		КОГДА ВопросыДляАнкетирования.Ссылка = ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВопросыДляАнкетирования.Фамилия)
		|			ТОГДА ФИОФизЛицСрезПоследних.Фамилия
		|		КОГДА ВопросыДляАнкетирования.Ссылка = ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВопросыДляАнкетирования.Имя)
		|			ТОГДА ФИОФизЛицСрезПоследних.Имя
		|		КОГДА ВопросыДляАнкетирования.Ссылка = ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВопросыДляАнкетирования.Отчество)
		|			ТОГДА ФИОФизЛицСрезПоследних.Отчество
		|		КОГДА ВопросыДляАнкетирования.Ссылка = ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВопросыДляАнкетирования.ДатаРождения)
		|			ТОГДА ФизическиеЛица.ДатаРождения
		|		КОГДА ВопросыДляАнкетирования.Ссылка = ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВопросыДляАнкетирования.ИНН)
		|			ТОГДА ФизическиеЛица.ИНН
		|		КОГДА ВопросыДляАнкетирования.Ссылка = ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВопросыДляАнкетирования.Пол)
		|			ТОГДА ФизическиеЛица.Пол
		|		КОГДА ВопросыДляАнкетирования.Ссылка = ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВопросыДляАнкетирования.КодИМНС)
		|			ТОГДА ФизическиеЛица.КодИМНС
		|		КОГДА ВопросыДляАнкетирования.Ссылка = ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВопросыДляАнкетирования.СтраховойНомерПФР)
		|			ТОГДА ФизическиеЛица.СтраховойНомерПФР
		|		КОГДА ВопросыДляАнкетирования.Ссылка = ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВопросыДляАнкетирования.Гражданство)
		|			ТОГДА ГражданствоФизЛицСрезПоследних.Страна
		|		КОГДА ВопросыДляАнкетирования.Ссылка = ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВопросыДляАнкетирования.СемейноеПоложение)
		|			ТОГДА СемейноеПоложениеФизЛицСрезПоследних.СемейноеПоложение
		|		КОГДА ВопросыДляАнкетирования.Ссылка = ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВопросыДляАнкетирования.ВидДокументаУдостоверяющегоЛичность)
		|			ТОГДА ПаспортныеДанныеФизЛицСрезПоследних.ДокументВид
		|		КОГДА ВопросыДляАнкетирования.Ссылка = ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВопросыДляАнкетирования.СерияДокумента)
		|			ТОГДА ПаспортныеДанныеФизЛицСрезПоследних.ДокументСерия
		|		КОГДА ВопросыДляАнкетирования.Ссылка = ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВопросыДляАнкетирования.НомерДокумента)
		|			ТОГДА ПаспортныеДанныеФизЛицСрезПоследних.ДокументНомер
		|		КОГДА ВопросыДляАнкетирования.Ссылка = ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВопросыДляАнкетирования.ДатаВыдачиДокумента)
		|			ТОГДА ПаспортныеДанныеФизЛицСрезПоследних.ДокументДатаВыдачи
		|		КОГДА ВопросыДляАнкетирования.Ссылка = ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВопросыДляАнкетирования.КемВыданДокумент)
		|			ТОГДА ВЫРАЗИТЬ(ПаспортныеДанныеФизЛицСрезПоследних.ДокументКемВыдан КАК СТРОКА(1000))
		|	КОНЕЦ
		|ИЗ
		|	ПланВидовХарактеристик.ВопросыДляАнкетирования КАК ВопросыДляАнкетирования
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ФизическиеЛица КАК ФизическиеЛица
		|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ФИОФизЛиц.СрезПоследних(, ФизЛицо = &Ссылка) КАК ФИОФизЛицСрезПоследних
		|			ПО ФИОФизЛицСрезПоследних.ФизЛицо = ФизическиеЛица.Ссылка
		|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ГражданствоФизЛиц.СрезПоследних(, ФизЛицо = &Ссылка) КАК ГражданствоФизЛицСрезПоследних
		|			ПО ГражданствоФизЛицСрезПоследних.ФизЛицо = ФизическиеЛица.Ссылка
		|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СемейноеПоложениеФизЛиц.СрезПоследних(, ФизЛицо = &Ссылка) КАК СемейноеПоложениеФизЛицСрезПоследних
		|			ПО СемейноеПоложениеФизЛицСрезПоследних.ФизЛицо = ФизическиеЛица.Ссылка
		|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПаспортныеДанныеФизЛиц.СрезПоследних(, ФизЛицо = &Ссылка) КАК ПаспортныеДанныеФизЛицСрезПоследних
		|			ПО ПаспортныеДанныеФизЛицСрезПоследних.ФизЛицо = ФизическиеЛица.Ссылка
		|		ПО (ФизическиеЛица.Ссылка = &Ссылка)
		|			И ((НЕ ВопросыДляАнкетирования.ТипОтветаНаВопрос В (ЗНАЧЕНИЕ(Перечисление.ТипыОтветаНаВопросАнкеты.Табличный), ЗНАЧЕНИЕ(Перечисление.ТипыОтветаНаВопросАнкеты.КонтактнаяИнформация))))
		|			И ((НЕ ВопросыДляАнкетирования.ЭтоГруппа))
		|			И (ВопросыДляАнкетирования.Ссылка В
		|				(ВЫБРАТЬ
		|					ТиповыеАнкетыВопросыАнкеты.Вопрос
		|				ИЗ
		|					Справочник.ТиповыеАнкеты.ВопросыАнкеты КАК ТиповыеАнкетыВопросыАнкеты
		|				ГДЕ
		|					ТиповыеАнкетыВопросыАнкеты.Ссылка = &Анкета))
		|
		|ОБЪЕДИНИТЬ
		|
		|ВЫБРАТЬ
		|	ВопросыДляАнкетирования.Ссылка,
		|	NULL,
		|	NULL,
		|	"""",
		|	ВЫБОР
		|		КОГДА КонтактнаяИнформация.Тип = ЗНАЧЕНИЕ(Перечисление.ТипыКонтактнойИнформации.Телефон)
		|			ТОГДА КонтактнаяИнформация.Поле1 + ""¤"" + КонтактнаяИнформация.Поле2 + ""¤"" + КонтактнаяИнформация.Поле3 + ""¤"" + КонтактнаяИнформация.Поле4
		|		КОГДА КонтактнаяИнформация.Тип = ЗНАЧЕНИЕ(Перечисление.ТипыКонтактнойИнформации.Адрес)
		|			ТОГДА КонтактнаяИнформация.Поле1 + ""¤"" + КонтактнаяИнформация.Поле2 + ""¤"" + КонтактнаяИнформация.Поле3 + ""¤"" + КонтактнаяИнформация.Поле4 + ""¤"" + КонтактнаяИнформация.Поле5 + ""¤"" + КонтактнаяИнформация.Поле6 + ""¤"" + КонтактнаяИнформация.Поле7 + ""¤"" + КонтактнаяИнформация.Поле8 + ""¤"" + КонтактнаяИнформация.Поле9
		|	КОНЕЦ
		|ИЗ
		|	ПланВидовХарактеристик.ВопросыДляАнкетирования КАК ВопросыДляАнкетирования
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.КонтактнаяИнформация КАК КонтактнаяИнформация
		|		ПО ВопросыДляАнкетирования.ВидКонтактнойИнформации = КонтактнаяИнформация.Вид
		|			И ((НЕ ВопросыДляАнкетирования.ЭтоГруппа))
		|			И (ВопросыДляАнкетирования.Ссылка В
		|				(ВЫБРАТЬ
		|					ТиповыеАнкетыВопросыАнкеты.Вопрос
		|				ИЗ
		|					Справочник.ТиповыеАнкеты.ВопросыАнкеты КАК ТиповыеАнкетыВопросыАнкеты
		|				ГДЕ
		|					ТиповыеАнкетыВопросыАнкеты.Ссылка = &Анкета))
		|			И (КонтактнаяИнформация.Объект = &Ссылка)
		|			И ВопросыДляАнкетирования.ВидКонтактнойИнформации.Тип = КонтактнаяИнформация.Тип
		|
		|УПОРЯДОЧИТЬ ПО
		|	Вопрос,
		|	КолонкаТаблицы,
		|	НомерСтрокиВТаблице";
		
		Возврат Запрос.Выполнить().Выбрать();
	
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

Процедура ЗаполнитьТабличнуюЧастьПоОпросу(ФизическоеЛицо, Опрос, Вопрос, ИмяСправочника, ИмяТЧ)
	
	СтруктураОтбора = Новый Структура;
	СтруктураОтбора.Вставить("ВопросВладелец",	Вопрос);
	
	ФизическоеЛицо[ИмяТЧ].Очистить();
	
	КоличествоКолонок = Метаданные.Справочники[ИмяСправочника].ТабличныеЧасти[ИмяТЧ].Реквизиты.Количество();
	
	Для ИндексНомераСтроки = 1 по Вопрос.КоличествоСтрокТаблицы Цикл
		
		ПараметрыПоиска = Новый Структура("ВопросВладелец, НомерСтрокиВТаблице");
		ПараметрыПоиска.ВопросВладелец		= Вопрос;
		ПараметрыПоиска.НомерСтрокиВТаблице	= ИндексНомераСтроки;
		
		ОтветНаТабличныйВопрос = Опрос.СоставнойОтвет.НайтиСтроки(ПараметрыПоиска);
		
		Если ОтветНаТабличныйВопрос.Количество() = 0 Тогда
			Продолжить;
		КонецЕсли;
		
		СтрокаОтвет = ФизическоеЛицо[ИмяТЧ].Добавить();
		НомерКолонки = 0;
		
		Для Каждого КолонкаОтвет Из ОтветНаТабличныйВопрос Цикл
			ИмяКолонки = Метаданные.Справочники[ИмяСправочника].ТабличныеЧасти[ИмяТЧ].Реквизиты[НомерКолонки].Имя;
			СтрокаОтвет[ИмяКолонки] = КолонкаОтвет.ТиповойОтвет;
			НомерКолонки = НомерКолонки + 1;
			Если НомерКолонки >= КоличествоКолонок тогда
				Прервать;
			КонецЕсли;
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗаполнитьКонтактнуюИнформациюПоОпросу(ФизическоеЛицо, Опрос, Вопрос, Раздел, ВидКИ, ТипКИ)
	
	// Из объекта нет доступа к его КИ
	// Следовательно, при создании сотрудника, нет возможности записать КИ
	// А в форме доступ к набору записей есть
	Если ТипЗнч(ФизическоеЛицо) <> Тип("Форма") Тогда
		Возврат;
	КонецЕсли;
	
	НайденныеСтроки = Опрос.Вопросы.НайтиСтроки(Новый Структура("Раздел, Вопрос",	Раздел, Вопрос));
	
	// найденная строка всегда одна
	СтрокаСОтветом = НайденныеСтроки[0];
	
	КИОбъекта = Новый ТаблицаЗначений;
	КИОбъекта.Колонки.Добавить("Поле1КИ");
	КИОбъекта.Колонки.Добавить("Поле2КИ");
	КИОбъекта.Колонки.Добавить("Поле3КИ");
	КИОбъекта.Колонки.Добавить("Поле4КИ");
	КИОбъекта.Колонки.Добавить("Поле5КИ");
	КИОбъекта.Колонки.Добавить("Поле6КИ");
	КИОбъекта.Колонки.Добавить("Поле7КИ");
	КИОбъекта.Колонки.Добавить("Поле8КИ");
	КИОбъекта.Колонки.Добавить("Поле9КИ");
	КИОбъекта.Колонки.Добавить("Поле10КИ");
	КИОбъекта.Колонки.Добавить("КомментарийКИ");
	КИОбъекта.Колонки.Добавить("ВидКИ");
	КИОбъекта.Колонки.Добавить("ТипКИ");
	КИОбъекта.Колонки.Добавить("ПредставлениеКИ");
	
	Если	ТипКИ = Перечисления.ТипыКонтактнойИнформации.Адрес	тогда
		СтрокаКИ =	КИОбъекта.Добавить();
		СтрокаКИ.ВидКИ	= ВидКИ;
		СтрокаКИ.ТипКИ	= ТипКИ;
		МассивПолей	=	ОбщегоНазначения.РазложитьСтрокуВМассивПодстрок(СтрокаСОтветом.ТиповойОтвет,	"¤");
		Если	МассивПолей.Количество()	>	0	тогда
			СтрокаКИ.Поле1КИ = МассивПолей[0];
		КонецЕсли;
		Если	МассивПолей.Количество()	>	1	тогда
			СтрокаКИ.Поле2КИ = МассивПолей[1];
		КонецЕсли;
		Если	МассивПолей.Количество()	>	2	тогда
			СтрокаКИ.Поле3КИ = МассивПолей[2];
		КонецЕсли;
		Если	МассивПолей.Количество()	>	3	тогда
			СтрокаКИ.Поле4КИ = МассивПолей[3];
		КонецЕсли;
		Если	МассивПолей.Количество()	>	4	тогда
			СтрокаКИ.Поле5КИ = МассивПолей[4];
		КонецЕсли;
		Если	МассивПолей.Количество()	>	5	тогда
			СтрокаКИ.Поле6КИ = МассивПолей[5];
		КонецЕсли;
		Если	МассивПолей.Количество()	>	6	тогда
			СтрокаКИ.Поле7КИ = МассивПолей[6];
		КонецЕсли;
		Если	МассивПолей.Количество()	>	7	тогда
			СтрокаКИ.Поле8КИ = МассивПолей[7];
		КонецЕсли;
		Если	МассивПолей.Количество()	>	8	тогда
			СтрокаКИ.Поле9КИ = МассивПолей[8];
		КонецЕсли;
		Если	МассивПолей.Количество()	>	9	тогда
			СтрокаКИ.КомментарийКИ	= МассивПолей[9];
		КонецЕсли;
		Представление = "";
		
		Если СокрЛП(СтрокаКИ.Поле1КИ) <> "" Тогда
			Представление = Представление + ", " + СокрЛП(СтрокаКИ.Поле1КИ);
		КонецЕсли;
		
		Если СокрЛП(СтрокаКИ.Поле2КИ) <> "" Тогда
			Представление = Представление + ", " + СокрЛП(СтрокаКИ.Поле2КИ);
		КонецЕсли;
		
		Если СокрЛП(СтрокаКИ.Поле3КИ) <> "" Тогда
			Представление = Представление + ", " + СокрЛП(СтрокаКИ.Поле3КИ);
		КонецЕсли;
		
		Если СокрЛП(СтрокаКИ.Поле4КИ) <> "" Тогда
			Представление = Представление + ", " + СокрЛП(СтрокаКИ.Поле4КИ);
		КонецЕсли;
		
		Если СокрЛП(СтрокаКИ.Поле5КИ) <> "" Тогда
			Представление = Представление + ", " + СокрЛП(СтрокаКИ.Поле5КИ);
		КонецЕсли;
		
		Если СокрЛП(СтрокаКИ.Поле6КИ) <> "" Тогда
			Представление = Представление + ", " + СокрЛП(СтрокаКИ.Поле6КИ);
		КонецЕсли;
		
		Если СокрЛП(СтрокаКИ.Поле7КИ) <> "" Тогда
			Представление = Представление + ", дом № " + СокрЛП(СтрокаКИ.Поле7КИ);
		КонецЕсли;
		
		Если СокрЛП(СтрокаКИ.Поле8КИ) <> "" Тогда
			Представление = Представление + ", корпус " + СокрЛП(СтрокаКИ.Поле8КИ);
		КонецЕсли;
		
		Если СокрЛП(СтрокаКИ.Поле9КИ) <> "" Тогда
			Представление = Представление + ", кв." + СокрЛП(СтрокаКИ.Поле9КИ);
		КонецЕсли;
		
		Если СтрДлина(Представление) > 2 Тогда
			Представление = Сред(Представление, 3);
		КонецЕсли;
		СтрокаКИ.ПредставлениеКИ = Представление
	ИначеЕсли	ТипКИ = Перечисления.ТипыКонтактнойИнформации.Телефон	тогда
		СтрокаКИ	=	КИОбъекта.Добавить();
		СтрокаКИ.ВидКИ	= ВидКИ;
		СтрокаКИ.ТипКИ	= ТипКИ;
		МассивПолей	=	ОбщегоНазначения.РазложитьСтрокуВМассивПодстрок(СтрокаСОтветом.ТиповойОтвет,	"¤");
		Если	МассивПолей.Количество()	>	0	тогда
			СтрокаКИ.Поле1КИ	= МассивПолей[0];
		КонецЕсли;
		Если	МассивПолей.Количество()	>	1	тогда
			СтрокаКИ.Поле2КИ	= МассивПолей[1];
		КонецЕсли;
		Если	МассивПолей.Количество()	>	2	тогда
			СтрокаКИ.Поле3КИ	= МассивПолей[2];
		КонецЕсли;
		Если	МассивПолей.Количество()	>	3	тогда
			СтрокаКИ.Поле4КИ	= МассивПолей[3];
		КонецЕсли;
		Если	МассивПолей.Количество()	>	4	тогда
			СтрокаКИ.КомментарийКИ	= МассивПолей[4];
		КонецЕсли;
		Представление = ?(Не ПустаяСтрока(СтрокаКИ.Поле1КИ),("+"+СтрокаКИ.Поле1КИ),"");
		Представление = Представление + ?((Не ПустаяСтрока(СтрокаКИ.Поле2КИ)),(УправлениеКонтактнойИнформацией.ПроверкаПустойСтроки(Представление,Ложь)+"(" + СтрокаКИ.Поле2КИ + ")"),"");
		Представление = Представление + ?((Не ПустаяСтрока(СтрокаКИ.Поле3КИ)),(УправлениеКонтактнойИнформацией.ПроверкаПустойСтроки(Представление,ПустаяСтрока(СтрокаКИ.Поле2КИ))+СтрокаКИ.Поле3КИ),"");
		Представление = Представление + ?((Не ПустаяСтрока(СтрокаКИ.Поле4КИ)),(УправлениеКонтактнойИнформацией.ПроверкаПустойСтроки(Представление)+"доб. " + СтрокаКИ.Поле4КИ),"");
		СтрокаКИ.ПредставлениеКИ	= Представление
	ИначеЕсли ТипКИ = Перечисления.ТипыКонтактнойИнформации.АдресЭлектроннойПочты или 
		ТипКИ = Перечисления.ТипыКонтактнойИнформации.ВебСтраница или 
		ТипКИ = Перечисления.ТипыКонтактнойИнформации.Другое тогда
		СтрокаКИ					= КИОбъекта.Добавить();
		СтрокаКИ.ВидКИ				= ВидКИ;
		СтрокаКИ.ТипКИ				= ТипКИ;
		СтрокаКИ.ПредставлениеКИ	= СтрокаСОтветом.ТиповойОтвет;
	КонецЕсли;
	
	Для Каждого СтрокаКИ Из КИОбъекта Цикл
		Поле1КИ	= СтрокаКИ.Поле1КИ;
		Поле2КИ	= СтрокаКИ.Поле2КИ;
		Поле3КИ	= СтрокаКИ.Поле3КИ;
		Поле4КИ	= СтрокаКИ.Поле4КИ;
		Поле5КИ	= СтрокаКИ.Поле5КИ;
		Поле6КИ	= СтрокаКИ.Поле6КИ;
		Поле7КИ	= СтрокаКИ.Поле7КИ;
		Поле8КИ	= СтрокаКИ.Поле8КИ;
		Поле9КИ	= СтрокаКИ.Поле9КИ;
		Поле10КИ		= СтрокаКИ.Поле10КИ;
		КомментарийКИ	= СтрокаКИ.КомментарийКИ;
		ПредставлениеКИ	= СтрокаКИ.ПредставлениеКИ;
		ВидКИ		= СтрокаКИ.ВидКИ;
		ТипКИ		= СтрокаКИ.ТипКИ;
		Если
			ЗначениеЗаполнено(Поле1КИ)	ИЛИ
			ЗначениеЗаполнено(Поле2КИ)	ИЛИ
			ЗначениеЗаполнено(Поле3КИ)	ИЛИ
			ЗначениеЗаполнено(Поле4КИ)	ИЛИ
			ЗначениеЗаполнено(Поле5КИ)	ИЛИ
			ЗначениеЗаполнено(Поле6КИ)	ИЛИ
			ЗначениеЗаполнено(Поле7КИ)	ИЛИ
			ЗначениеЗаполнено(Поле8КИ)	ИЛИ
			ЗначениеЗаполнено(Поле9КИ)	ИЛИ
			ЗначениеЗаполнено(Поле10КИ)	ИЛИ
			ЗначениеЗаполнено(ПредставлениеКИ) тогда
			
			НоваяЗапись			= ФизическоеЛицо.НаборКонтактнойИнформации.Добавить();
			НоваяЗапись.Объект	= ФизическоеЛицо.Ссылка;
			НоваяЗапись.Тип		= ТипКИ;
			НоваяЗапись.Вид		= ВидКИ;
			НоваяЗапись.Поле1	= Поле1КИ;
			НоваяЗапись.Поле2	= Поле2КИ;
			НоваяЗапись.Поле3	= Поле3КИ;
			НоваяЗапись.Поле4	= Поле4КИ;
			НоваяЗапись.Поле5	= Поле5КИ;
			НоваяЗапись.Поле6	= Поле6КИ;
			НоваяЗапись.Поле7	= Поле7КИ;
			НоваяЗапись.Поле8	= Поле8КИ;
			НоваяЗапись.Поле9	= Поле9КИ;
			НоваяЗапись.Поле10	= Поле10КИ;
			НоваяЗапись.Комментарий		= КомментарийКИ;
			Если ТипКИ = Перечисления.ТипыКонтактнойИнформации.Адрес тогда
				НоваяЗапись.Представление	= УправлениеКонтактнойИнформацией.ПолучитьПредставлениеАдреса(НоваяЗапись);
			ИначеЕсли ТипКИ = Перечисления.ТипыКонтактнойИнформации.Телефон тогда
				УправлениеКонтактнойИнформацией.СформироватьПредставлениеТелефона(НоваяЗапись);
			Иначе
				НоваяЗапись.Представление	= ПредставлениеКИ;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла; // по строкам объекта
	
КонецПроцедуры

// Процедура заполняет физическое лицо данными из опроса
//
Процедура ЗаполнитьФизическоеЛицоПоОпросу(Форма, ФизическоеЛицо, Опрос) Экспорт
	
	ПВХ = ПланыВидовХарактеристик.ВопросыДляАнкетирования;
	
	// Выгрузим вопросы и определим, какие из них являются КИ
	МассивВопросов = Опрос.Вопросы.ВыгрузитьКолонку("Вопрос");
	
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("СписокВопросов",	МассивВопросов);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВопросыДляАнкетирования.Ссылка КАК Вопрос,
	|	ВопросыДляАнкетирования.ВидКонтактнойИнформации КАК ВидКИ,
	|	ВопросыДляАнкетирования.ВидКонтактнойИнформации.Тип КАК ТипКИ
	|ИЗ
	|	ПланВидовХарактеристик.ВопросыДляАнкетирования КАК ВопросыДляАнкетирования
	|ГДЕ
	|	ВопросыДляАнкетирования.Ссылка В(&СписокВопросов)
	|	И ВопросыДляАнкетирования.ВидКонтактнойИнформации.Тип <> ЗНАЧЕНИЕ(Перечисление.ТипыКонтактнойИнформации.ПустаяСсылка)";
	
	ТаблицаКИ = Запрос.Выполнить().Выгрузить();
	ТаблицаКИ.Индексы.Добавить("Вопрос");
	
	Для Каждого ТекущийВопрос Из Опрос.Вопросы Цикл
		
		Ответ = ТекущийВопрос.ТиповойОтвет;
		
		Если ТекущийВопрос.Вопрос = ПланыВидовХарактеристик.ВопросыДляАнкетирования.Гражданство Тогда
			// ничего не делать, в гражданстве спрашивается только страна
			
		// в качестве условий можно добавлять разбор табличных вопросов
		ИначеЕсли ТекущийВопрос.Вопрос = ПВХ.ТЗнаниеЯзыков Тогда
			ЗаполнитьТабличнуюЧастьПоОпросу(ФизическоеЛицо, Опрос, ТекущийВопрос.Вопрос, "ФизическиеЛица", "ЗнаниеЯзыков");
			
		ИначеЕсли ТекущийВопрос.Вопрос = ПВХ.ТОбразование Тогда
			ЗаполнитьТабличнуюЧастьПоОпросу(ФизическоеЛицо, Опрос, ТекущийВопрос.Вопрос, "ФизическиеЛица", "Образование");
			
		ИначеЕсли ТекущийВопрос.Вопрос = ПВХ.ТСоставСемьи Тогда
			ЗаполнитьТабличнуюЧастьПоОпросу(ФизическоеЛицо, Опрос, ТекущийВопрос.Вопрос, "ФизическиеЛица", "СоставСемьи");
			
		ИначеЕсли ТекущийВопрос.Вопрос = ПВХ.ТТрудоваяДеятельность Тогда
			ЗаполнитьТабличнуюЧастьПоОпросу(ФизическоеЛицо, Опрос, ТекущийВопрос.Вопрос, "ФизическиеЛица", "ТрудоваяДеятельность");
			
		Иначе
			НайденнаяСтрока = ТаблицаКИ.Найти(ТекущийВопрос.Вопрос, "Вопрос");
			
			Если НайденнаяСтрока <> Неопределено Тогда // Вопрос - КИ
				ЗаполнитьКонтактнуюИнформациюПоОпросу(ФизическоеЛицо, Опрос, ТекущийВопрос.Вопрос, ТекущийВопрос.Раздел, НайденнаяСтрока.ВидКИ, НайденнаяСтрока.ТипКИ);
				
			Иначе
				Элемент = Форма.ЭлементыФормы.Найти(ПланыВидовХарактеристик.ВопросыДляАнкетирования.ПолучитьИмяПредопределенного(ТекущийВопрос.Вопрос));
				Если ТекущийВопрос.Вопрос.Предопределенный И Элемент <> Неопределено Тогда
					Элемент.Значение = Ответ;
				КонецЕсли;
				
			КонецЕсли;
		КонецЕсли;
		
	КонецЦикла;
	
	Если Не ПустаяСтрока(Форма.Фамилия) И Не ПустаяСтрока(Форма.Имя) Тогда
		Форма.Наименование		= Форма.Фамилия + " " + Форма.Имя + " " + Форма.Отчество;
	КонецЕсли;
	
КонецПроцедуры


Функция ПолучитьПредставлениеКИ(ТипКИ, ЗначениеКИ) Экспорт
	
	Если ТипКИ = Перечисления.ТипыКонтактнойИнформации.Адрес тогда
		МассивПолей = ОбщегоНазначения.РазложитьСтрокуВМассивПодстрок(ЗначениеКИ, "¤");
		
		Представление = УправлениеКонтактнойИнформацией.ПолучитьПредставлениеАдресаПоСтрока(СтрЗаменить(ЗначениеКИ, "¤", ","));
		
		Возврат Представление;
		
	ИначеЕсли ТипКИ = Перечисления.ТипыКонтактнойИнформации.Телефон тогда
		МассивПолей = ОбщегоНазначения.РазложитьСтрокуВМассивПодстрок(ЗначениеКИ, "¤");
		Если МассивПолей.Количество() > 0 тогда
			Поле1 = МассивПолей[0];
		КонецЕсли;
		Если МассивПолей.Количество() > 1 тогда
			Поле2 = МассивПолей[1];
		КонецЕсли;
		Если МассивПолей.Количество() > 2 тогда
			Поле3 = МассивПолей[2];
		КонецЕсли;
		Если МассивПолей.Количество() > 3 тогда
			Поле4 = МассивПолей[3];
		КонецЕсли;
		Если МассивПолей.Количество() > 4 тогда
			Коммент = МассивПолей[4];
		КонецЕсли;
		Предст = ""+?(Не ПустаяСтрока(Поле1),("+"+Поле1),"");
		Предст = Предст + ?((Не ПустаяСтрока(Поле2)),(УправлениеКонтактнойИнформацией.ПроверкаПустойСтроки(Предст,Ложь)+"(" + Поле2 + ")"),"");
		Предст = Предст + ?((Не ПустаяСтрока(Поле3)),(УправлениеКонтактнойИнформацией.ПроверкаПустойСтроки(Предст,ПустаяСтрока(Поле2))+Поле3),"");
		Предст = Предст + ?((Не ПустаяСтрока(Поле4)),(УправлениеКонтактнойИнформацией.ПроверкаПустойСтроки(Предст)+"доб. " + Поле4),"");
		Предст = Предст + ?((Не ПустаяСтрока(Коммент)),(УправлениеКонтактнойИнформацией.ПроверкаПустойСтроки(Предст)+" " + Коммент),"");
		
		Возврат Предст;
		
	КонецЕсли;
	
	Возврат "";
	
КонецФункции

///////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ МЕХАНИЗМА УДАЛЕННОЕ АНКЕТИРОВАНИЕ
///////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////

// Функция формирует XSL файл
//
Функция СформироватьXSL(ИмяФайлаАнкеты, КаталогАнкеты, КаталогОпроса, ДобавлятьКнопкуЗаписи, ДобавлятьКнопкуОтправки, ЭлектронныйАдресОтветов) Экспорт
	
	ИмяФайлаПреобразования = "style.xsl";
	
	XSLМакет = ПолучитьОбщийМакет("МакетТиповойАнкеты");
	
	Если Не ПустаяСтрока(КаталогАнкеты) тогда
		ТекстXSL = XSLМакет.ПолучитьОбласть("ДоКнопок");
		ТекстXSL.УдалитьСтроку(1);
		ТекстXSL.УдалитьСтроку(ТекстXSL.КоличествоСтрок());
		СтрокаПреобразования = ТекстXSL.ПолучитьТекст();
		ТекстXSL = XSLМакет.ПолучитьОбласть("КнопкаОтправить2");
		ТекстXSL.УдалитьСтроку(1);
		ТекстXSL.УдалитьСтроку(ТекстXSL.КоличествоСтрок());
		СтрокаПреобразования = СтрокаПреобразования + ТекстXSL.ПолучитьТекст();
		ТекстXSL = XSLМакет.ПолучитьОбласть("ПослеКнопок");
		ТекстXSL.УдалитьСтроку(1);
		ТекстXSL.УдалитьСтроку(ТекстXSL.КоличествоСтрок());
		СтрокаПреобразования = СтрокаПреобразования + ТекстXSL.ПолучитьТекст();
		СтрокаПреобразования = СтрЗаменить(СтрокаПреобразования, "//comment11;", "LoadPath = '"+СтрЗаменить(КаталогАнкеты+ИмяФайлаАнкеты, "\", "\\")+"';");
		СтрокаПреобразования = СтрЗаменить(СтрокаПреобразования, "//comment22;", "FName	= '" + СтрЗаменить(КаталогОпроса, "\", "\\")+"opros.xml';");
		
	ИначеЕсли Не ДобавлятьКнопкуОтправки И ДобавлятьКнопкуЗаписи тогда
		// когда открываем анкету просто для просмотра то она уже точно не адресная
		// в нее помещаем только кнопку "Сохранить в файл"
		ТекстXSL = XSLМакет.ПолучитьОбласть("ДоКнопок");
		ТекстXSL.УдалитьСтроку(1);
		ТекстXSL.УдалитьСтроку(ТекстXSL.КоличествоСтрок());
		СтрокаПреобразования = ТекстXSL.ПолучитьТекст();
		ТекстXSL = XSLМакет.ПолучитьОбласть("КнопкаСохранить");
		ТекстXSL.УдалитьСтроку(1);
		ТекстXSL.УдалитьСтроку(ТекстXSL.КоличествоСтрок());
		СтрокаПреобразования = СтрокаПреобразования + ТекстXSL.ПолучитьТекст();
		ТекстXSL = XSLМакет.ПолучитьОбласть("ПослеКнопок");
		ТекстXSL.УдалитьСтроку(1);
		ТекстXSL.УдалитьСтроку(ТекстXSL.КоличествоСтрок());
		СтрокаПреобразования = СтрокаПреобразования + ТекстXSL.ПолучитьТекст();
		
	ИначеЕсли ДобавлятьКнопкуОтправки И ДобавлятьКнопкуЗаписи тогда
		// когда открываем адресную анкету просто для просмотра 
		// в нее помещаем обе кнопки
		ТекстXSL = XSLМакет.ПолучитьОбласть("ДоКнопок");
		ТекстXSL.УдалитьСтроку(1);
		ТекстXSL.УдалитьСтроку(ТекстXSL.КоличествоСтрок());
		СтрокаПреобразования = ТекстXSL.ПолучитьТекст();
		ТекстXSL = XSLМакет.ПолучитьОбласть("Кнопки");
		ТекстXSL.УдалитьСтроку(1);
		ТекстXSL.УдалитьСтроку(ТекстXSL.КоличествоСтрок());
		СтрокаПреобразования = СтрокаПреобразования + ТекстXSL.ПолучитьТекст();
		ТекстXSL = XSLМакет.ПолучитьОбласть("ПослеКнопок");
		ТекстXSL.УдалитьСтроку(1);
		ТекстXSL.УдалитьСтроку(ТекстXSL.КоличествоСтрок());
		СтрокаПреобразования = СтрокаПреобразования + ТекстXSL.ПолучитьТекст();
		
	ИначеЕсли Не ДобавлятьКнопкуЗаписи тогда
		ТекстXSL = XSLМакет.ПолучитьОбласть("ДоКнопок");
		ТекстXSL.УдалитьСтроку(1);
		ТекстXSL.УдалитьСтроку(ТекстXSL.КоличествоСтрок());
		СтрокаПреобразования = ТекстXSL.ПолучитьТекст();
		ТекстXSL = XSLМакет.ПолучитьОбласть("КнопкаСохранить");
		ТекстXSL.УдалитьСтроку(1);
		ТекстXSL.УдалитьСтроку(ТекстXSL.КоличествоСтрок());
		СтрокаПреобразования = СтрокаПреобразования + ТекстXSL.ПолучитьТекст();
		ТекстXSL = XSLМакет.ПолучитьОбласть("ПослеКнопок");
		ТекстXSL.УдалитьСтроку(1);
		ТекстXSL.УдалитьСтроку(ТекстXSL.КоличествоСтрок());
		СтрокаПреобразования = СтрокаПреобразования + ТекстXSL.ПолучитьТекст();
		
	КонецЕсли;
	СтрокаПреобразования = СтрЗаменить(СтрокаПреобразования, "###@###", ЭлектронныйАдресОтветов);
	ТекстXSL.УстановитьТекст(СтрокаПреобразования);
	
	Возврат ТекстXSL;
	
КонецФункции

Процедура ДобавитьПоле(ОбъектXML, Имя, Значение)
	
	ОбъектXML.ЗаписатьАтрибут(Имя, XMLСтрока(Значение));
	
КонецПроцедуры // ДобавитьПоле()

Функция ПолучитьОтветы(Выборка, Тип, ФлагКолонки = Ложь)
	
	Если НЕ ФлагКолонки Тогда
		Вопрос	= Выборка.Вопрос;
		Префикс	= "";
		
	Иначе
		Вопрос	= Выборка.КолонкаТаблицы;
		Префикс	= "КолонкаТаблицы";
		
	КонецЕсли;
	
	Запрос = Новый Запрос();
	Запрос.УстановитьПараметр("Вопрос", Вопрос);
	
	ТипЗначения = Выборка[Префикс + "ТипЗначения"].Типы().Получить(0);
	
	Если (ТипЗначения = Тип("Булево")) Тогда
		
		ВыборкаЭлементов = Новый ТаблицаЗначений();
		ВыборкаЭлементов.Колонки.Добавить("Код");
		ВыборкаЭлементов.Колонки.Добавить("Ссылка");
		ВыборкаЭлементов.Колонки.Добавить("Наименование");
		ВыборкаЭлементов.Колонки.Добавить("Комментарий");
		ВыборкаЭлементов.Колонки.Добавить("ТребуетРазвернутыйОтвет");
		ВыборкаЭлементов.Колонки.Добавить("ГУИД");
		
		СтрокаТаблицы = ВыборкаЭлементов.Добавить();
		СтрокаТаблицы.Код						= ЗначениеВСтрокуВнутр(Ложь);
		СтрокаТаблицы.Ссылка					= Ложь;
		СтрокаТаблицы.Наименование				= Строка("Нет");
		СтрокаТаблицы.Комментарий				= "";
		СтрокаТаблицы.ТребуетРазвернутыйОтвет	= Ложь;
		СтрокаТаблицы.ГУИД = СтрокаТаблицы.Код;
		
		СтрокаТаблицы = ВыборкаЭлементов.Добавить();
		СтрокаТаблицы.Код						= ЗначениеВСтрокуВнутр(Истина);
		СтрокаТаблицы.Ссылка					= Истина;
		СтрокаТаблицы.Наименование				= Строка("Да");
		СтрокаТаблицы.Комментарий				= "";
		СтрокаТаблицы.ТребуетРазвернутыйОтвет	= Ложь;
		СтрокаТаблицы.ГУИД = СтрокаТаблицы.Код;
		
	ИначеЕсли (ТипЗначения = Тип("Строка")) ИЛИ
		(ТипЗначения = Тип("Число")) ИЛИ
		(ТипЗначения = Тип("Дата")) Тогда
		
		ВыборкаЭлементов = Новый ТаблицаЗначений();
		ВыборкаЭлементов.Колонки.Добавить("Код");
		ВыборкаЭлементов.Колонки.Добавить("Ссылка");
		ВыборкаЭлементов.Колонки.Добавить("Наименование");
		ВыборкаЭлементов.Колонки.Добавить("Комментарий");
		ВыборкаЭлементов.Колонки.Добавить("ТребуетРазвернутыйОтвет");
		
	ИначеЕсли (ТипЗначения = Тип("СправочникСсылка.ВариантыОтветовОпросов")) Тогда
		
		ВыборкаЭлементов = Новый ТаблицаЗначений;
		ВыборкаЭлементов.Колонки.Добавить("Код");
		ВыборкаЭлементов.Колонки.Добавить("Ссылка", Новый ОписаниеТипов("СправочникСсылка.ВариантыОтветовОпросов"));
		ВыборкаЭлементов.Колонки.Добавить("Наименование");
		ВыборкаЭлементов.Колонки.Добавить("ТребуетРазвернутыйОтвет");
		
		ВариантыОтветов = Выборка.Выбрать(ОбходРезультатаЗапроса.Прямой);
		Пока ВариантыОтветов.Следующий() Цикл
			НоваяСтрока = ВыборкаЭлементов.Добавить();
			НоваяСтрока.Код						= ВариантыОтветов[Префикс + "ВариантыОтветовКод"];
			НоваяСтрока.Ссылка					= ВариантыОтветов[Префикс + "ВариантыОтветовСсылка"];
			НоваяСтрока.Наименование			= ВариантыОтветов[Префикс + "ВариантыОтветовНаименование"];
			НоваяСтрока.ТребуетРазвернутыйОтвет	= ВариантыОтветов[Префикс + "ВариантыОтветовТребуетРазвернутыйОтвет"];
		КонецЦикла;
		
		Если ВыборкаЭлементов.Количество() > 0 И НЕ ВыборкаЭлементов[0].Ссылка.Пустая() Тогда
			ПустаяСтрокаТаблицы			= ВыборкаЭлементов.Вставить(0);
			ПустаяСтрокаТаблицы.Ссылка	= Справочники.ВариантыОтветовОпросов.ПустаяСсылка();
		КонецЕсли;
		
		ВыборкаЭлементов.Колонки.Добавить("ГУИД");
		Для Каждого СтрокаТаблицы Из ВыборкаЭлементов Цикл
			СтрокаТаблицы.ГУИД = СтрокаТаблицы.Ссылка.УникальныйИдентификатор();
		КонецЦикла;
		
	Иначе
		Объект = Метаданные.НайтиПоТипу(ТипЗначения);
		
		Если Перечисления.ТипВсеСсылки().СодержитТип(ТипЗначения) Тогда
			Значения = Объект.ЗначенияПеречисления;
			
			ВыборкаЭлементов = Новый ТаблицаЗначений();
			ВыборкаЭлементов.Колонки.Добавить("Код");
			ВыборкаЭлементов.Колонки.Добавить("Ссылка");
			ВыборкаЭлементов.Колонки.Добавить("Наименование");
			ВыборкаЭлементов.Колонки.Добавить("Комментарий");
			ВыборкаЭлементов.Колонки.Добавить("ТребуетРазвернутыйОтвет");
			
			Менеджер = Перечисления[Объект.Имя];
			Для Индекс = 0 По Менеджер.Количество() - 1 Цикл
				СтрокаТаблицы = ВыборкаЭлементов.Добавить();
				Значение = Менеджер.Получить(Индекс);
				СтрокаТаблицы.Код						= ЗначениеВСтрокуВнутр(Значение);
				СтрокаТаблицы.Ссылка					= Значение;
				СтрокаТаблицы.Наименование				= Строка(Значение);
				СтрокаТаблицы.Комментарий				= Значение.Метаданные().Комментарий;
				СтрокаТаблицы.ТребуетРазвернутыйОтвет	= Ложь;
			КонецЦикла;
			ПустаяСтрокаТаблицы = ВыборкаЭлементов.Вставить(0);
			ПустаяСтрокаТаблицы.Ссылка = Менеджер.ПустаяСсылка();
			
		Иначе
			Запрос.Текст =
			"ВЫБРАТЬ
			|	"+Объект.Имя+".Ссылка       КАК Ссылка,
			|	"+Объект.Имя+".Наименование КАК Наименование,
			|	Ложь КАК ТребуетРазвернутыйОтвет
			|ИЗ
			|	Справочник."+Объект.Имя+" КАК "+Объект.Имя+"
			|";
			
			ВыборкаЭлементов =  Запрос.Выполнить().Выгрузить();
			ВыборкаЭлементов.Колонки.Добавить("Код");
			ВыборкаЭлементов.ЗаполнитьЗначения(NULL, "Код");
			
			Если ВыборкаЭлементов.Количество() > 0 тогда
				ПустойЭлемент = ВыборкаЭлементов.Вставить(0);
				ПустойЭлемент.Ссылка = Справочники[Объект.Имя].ПустаяСсылка();
				
			Иначе
				ОбщегоНазначения.СообщитьОбОшибке("Справочник """+Объект.Синоним+""" не имеет ни одного элемента. Рекомендуется заполнить его и сформировать анкету еще раз.");
				
			КонецЕсли;
			
		КонецЕсли;
		
		ВыборкаЭлементов.Колонки.Добавить("ГУИД");
		Для Каждого СтрокаТаблицы Из ВыборкаЭлементов Цикл
			Если СтрокаТаблицы.Код = NULL Тогда
				СтрокаТаблицы.ГУИД = СтрокаТаблицы.Ссылка.УникальныйИдентификатор();
				
			Иначе
				СтрокаТаблицы.ГУИД = СтрокаТаблицы.Код;
				
			КонецЕсли;
		КонецЦикла;
		
	КонецЕсли;
	
	Возврат ВыборкаЭлементов;
	
КонецФункции // ПолучитьОтветы()

Функция ПолучитьТипЗначенияОтвета(Выборка, ФлагКолонки)
	
	Если НЕ ФлагКолонки тогда
		Вопрос	= Выборка.Вопрос;
		Префикс	= "";
		
	Иначе
		Вопрос	= Выборка.КолонкаТаблицы;
		Префикс	= "КолонкаТаблицы";
		
	КонецЕсли;
	
	Параметры = Новый Структура("Представление, Тип, Длина, СпособОтвета, СтрокВТаблице, Ответы");
	
	ТипЗначения = Выборка[Префикс + "ТипЗначения"];
	ДлинаТипа   = Выборка[Префикс + "Длина"];
	
	Если (ТипЗначения.СодержитТип(Тип("Булево"))) Тогда
		Параметры.Тип = Строка(ТипЗначения);
		
	ИначеЕсли Выборка[Префикс + "ТипОтветаНаВопрос"] = Перечисления.ТипыОтветаНаВопросАнкеты.Текст тогда
		Параметры.Тип 			= "Строка";
		Параметры.Длина = Строка(ДлинаТипа);
		
	ИначеЕсли (ТипЗначения.СодержитТип(Тип("Строка"))) ИЛИ
		(ТипЗначения.СодержитТип(Тип("Число"))) ИЛИ
		(ТипЗначения.СодержитТип(Тип("Дата"))) Тогда
		
		Параметры.Тип = Строка(ТипЗначения);
		Параметры.Длина = Строка(ДлинаТипа);
		
	Иначе
		Тип = "Строка";
		
	КонецЕсли;
	
	Ответы = ПолучитьОтветы(Выборка, Тип, ФлагКолонки);
	
	ЕстьОтветы = Ответы.Количество() > 0;
	
	Если Выборка[Префикс + "ТипОтветаНаВопрос"]  = Перечисления.ТипыОтветаНаВопросАнкеты.КонтактнаяИнформация Тогда
		СтрокаТипКИ = ОбщегоНазначения.ПолучитьИмяЭлементаПеречисленияПоЗначению(Выборка[Префикс + "ТипКонтактнойИнформации"]);
		СтрокаВидКИ = ОбщегоНазначения.ПолучитьИмяЭлементаПеречисленияПоЗначению(Выборка[Префикс + "ВидОбъектаКонтактнойИнформации"]);
		
		Параметры.Тип			= ""+СтрокаТипКИ+"¤"+СтрокаВидКИ;
		Параметры.СтрокВТаблице	= 0; // вопрос не табличный, значит строк - 0!
		Параметры.Представление	= "ПолеВвода";
		Параметры.СпособОтвета	= "ВСвободнойФорме";
		
	ИначеЕсли Выборка[Префикс + "ТипОтветаНаВопрос"] = Перечисления.ТипыОтветаНаВопросАнкеты.Табличный Тогда
		Параметры.Тип			= "Таблица";
		Параметры.СтрокВТаблице	= Выборка.СтрокВТаблице; // вложенные табличные вопросы быть не могут, поэтому можно обойтись без префикса
		Параметры.Представление	= "Таблица";
		Параметры.СпособОтвета	= "";
		
	Иначе
		Параметры.СтрокВТаблице = 0;
		
		Если ЕстьОтветы Тогда
			
			Если Выборка[Префикс + "ТипОтветаНаВопрос"] <> Перечисления.ТипыОтветаНаВопросАнкеты.НесколькоВариантовОтвета Тогда
				Если Ответы.Количество() > 5 Тогда
					Параметры.Тип				= Тип;
					Если Ответы.Найти(Истина, "ТребуетРазвернутыйОтвет") = Неопределено Тогда
						Параметры.Представление	= "СписокВыбора";
						
					Иначе
						// если есть хоть один предопределенный	ответ
						Параметры.Представление	= "Переключатель";
						
					КонецЕсли;
					Параметры.СпособОтвета		= "ВыборОдногоВарианта";
					
				Иначе
					Параметры.Тип				= Тип;
					Если ФлагКолонки Тогда
						Параметры.Представление	= "СписокВыбора";
						
					Иначе
						Параметры.Представление	= "Переключатель";
						
					КонецЕсли;
					Параметры.СпособОтвета		= "ВыборОдногоВарианта";
				КонецЕсли;
			Иначе
				Параметры.Тип			= Тип;
				Параметры.Представление	= "Флажок";
				Параметры.СпособОтвета	= "ВыборНесколькихВариантов";
				
			КонецЕсли;
			
		Иначе
			
			Если (ТипЗначения.СодержитТип(Тип("Булево"))) Тогда
				Параметры.Тип				= Строка(Выборка[Префикс + "ТипЗначения"]);
				Параметры.Представление		= "СписокВыбора";
				Параметры.СпособОтвета		= "ВыборОдногоВарианта";
				
			ИначеЕсли (ТипЗначения.СодержитТип(Тип("Строка"))) ИЛИ
				(ТипЗначения.СодержитТип(Тип("Число"))) ИЛИ
				(ТипЗначения.СодержитТип(Тип("Дата"))) Тогда
				
				Параметры.Тип				= Строка(Выборка[Префикс + "ТипЗначения"]);
				
				Если Выборка[Префикс + "ТипОтветаНаВопрос"] = Перечисления.ТипыОтветаНаВопросАнкеты.Текст тогда
					// не все строковые поля одинаковые. Бывает еще и текст
					Параметры.Представление	= "Текст";
					
				Иначе
					Параметры.Представление	= "ПолеВвода";
					
				КонецЕсли;
				
				Параметры.СпособОтвета		= "ВСвободнойФорме";
				
			Иначе
				Параметры.Тип				= "Строка";
				Параметры.Представление		= "ПолеВвода";
				Параметры.СпособОтвета		= "ВСвободнойФорме";
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Параметры.Ответы = Ответы;
	
	Возврат Параметры;
	
КонецФункции // ПолучитьТипЗначенияОтвета()

Процедура ЗаписатьВопрос(ОбъектXML, Выборка, ФлагКолонки)
	
	Если НЕ ФлагКолонки тогда
		Вопрос	= Выборка.Вопрос;
		Префикс	= "";
		
	Иначе
		Вопрос	= Выборка.КолонкаТаблицы;
		Префикс	= "КолонкаТаблицы";
		
	КонецЕсли;
	
	Параметры	= ПолучитьТипЗначенияОтвета(Выборка, ФлагКолонки);
	
	Если Выборка.ТипОтветаНаВопрос = Перечисления.ТипыОтветаНаВопросАнкеты.Табличный и НЕ ФлагКолонки тогда
		ТабличныйВопрос = Выборка.Вопрос;
		// значит имеем дело с табличным вопросом
		Параметры = ПолучитьТипЗначенияОтвета(Выборка, Ложь);
		
		ОбъектXML.ЗаписатьНачалоЭлемента("Вопрос");
			
			ДобавитьПоле(ОбъектXML,"Ид",						ТабличныйВопрос.УникальныйИдентификатор());
			ДобавитьПоле(ОбъектXML,"Наименование",				?(ПустаяСтрока(Выборка.ПолнаяФормулировка), Выборка.Наименование, Выборка.ПолнаяФормулировка));
			ДобавитьПоле(ОбъектXML,"Комментарий",				Выборка.Наименование);
			ДобавитьПоле(ОбъектXML,"Обязательный",				Выборка.ОбязателенДляЗаполнения);
			ДобавитьПоле(ОбъектXML,"СпособОтвета",				Параметры.СпособОтвета);
			ДобавитьПоле(ОбъектXML,"ТипЗначенийОтвета",			Параметры.Тип);
			ДобавитьПоле(ОбъектXML,"Представление",				Параметры.Представление);
			ДобавитьПоле(ОбъектXML,"КоличествоСтрокТаблицы",	Параметры.СтрокВТаблице);
			
			КолонкиТаблицы = Выборка.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			Пока КолонкиТаблицы.Следующий() Цикл
				ЗаписатьВопрос(ОбъектXML, КолонкиТаблицы, Истина);
			КонецЦикла;
			
		ОбъектXML.ЗаписатьКонецЭлемента(); // Вопрос
		
	Иначе
		Если НЕ ФлагКолонки тогда
			ОбъектXML.ЗаписатьНачалоЭлемента("Вопрос");
		Иначе
			ОбъектXML.ЗаписатьНачалоЭлемента("КолонкаТаблицы");
		КонецЕсли;
		
			ДобавитьПоле(ОбъектXML,"Ид",						Вопрос.УникальныйИдентификатор());
			ДобавитьПоле(ОбъектXML,"Наименование",				?(ПустаяСтрока(Выборка[Префикс + "ПолнаяФормулировка"]), Выборка[Префикс + "Наименование"], Выборка[Префикс + "ПолнаяФормулировка"]));
			ДобавитьПоле(ОбъектXML,"Комментарий",				Выборка[Префикс + "Наименование"]);
			ДобавитьПоле(ОбъектXML,"Обязательный",				Выборка[Префикс + "ОбязателенДляЗаполнения"]);
			ДобавитьПоле(ОбъектXML,"СпособОтвета",				Параметры.СпособОтвета);
			ДобавитьПоле(ОбъектXML,"ТипЗначенийОтвета",			Параметры.Тип);
			ДобавитьПоле(ОбъектXML,"Длина",						?(Параметры.Длина = "0", "60", Параметры.Длина));
			ДобавитьПоле(ОбъектXML,"Представление",				Параметры.Представление);
			ДобавитьПоле(ОбъектXML,"КоличествоСтрокТаблицы",	Параметры.СтрокВТаблице);
			Ответы = Параметры.Ответы;
			
			Для Каждого СтрокаТаблицы Из Ответы Цикл
				Если НЕ ЗначениеЗаполнено(СтрокаТаблицы.Ссылка) и Параметры.Представление <> "СписокВыбора" тогда
					// пустые ссылки будем записывать только выпадающие списки
					Продолжить;
				КонецЕсли;
				
				ОбъектXML.ЗаписатьНачалоЭлемента("ВариантОтвета");
				
					ДобавитьПоле(ОбъектXML,"Ид",						СтрокаТаблицы.ГУИД);
					ДобавитьПоле(ОбъектXML,"Наименование",				СтрокаТаблицы.Наименование);
					ДобавитьПоле(ОбъектXML,"Комментарий",				"");
					ДобавитьПоле(ОбъектXML,"ТребуетРазвернутыйОтвет",	СтрокаТаблицы.ТребуетРазвернутыйОтвет);
				
				ОбъектXML.ЗаписатьКонецЭлемента(); // ВариантОтвета
			КонецЦикла;
		
		ОбъектXML.ЗаписатьКонецЭлемента(); // Вопрос ИЛИ КолонкаТаблицы
		
	КонецЕсли;
	
КонецПроцедуры // ЗаписатьВопрос()

Процедура ЗаписатьРаздел(ОбъектXML, РазделДляЗаписи, РазделНаименование)
	
	ОбъектXML.ЗаписатьНачалоЭлемента("Группа");
	Если РазделДляЗаписи = NULL тогда
		ДобавитьПоле(ОбъектXML,"Ид",			"");
		ДобавитьПоле(ОбъектXML,"Комментарий",	"");
		
	Иначе
		ДобавитьПоле(ОбъектXML,"Ид",			РазделДляЗаписи.Ссылка.УникальныйИдентификатор());
		ДобавитьПоле(ОбъектXML,"Комментарий",	РазделДляЗаписи.Комментарий);
		
	КонецЕсли;
	ДобавитьПоле(ОбъектXML,"Наименование", РазделНаименование);
	
КонецПроцедуры // ЗаписатьРаздел()

Процедура ДобавитьОтвет(ОбъектXML, Вопрос, ТипОтветаНаВопрос, ВыборкаОтветовИзОпроса, ТабличныйВопрос = Неопределено, НомерСтрокиВТаблице = Неопределено)
	
	Если ТабличныйВопрос = Неопределено Тогда
		ВопросПоиска = Новый Структура("Вопрос");
		ВопросПоиска.Вопрос				= Вопрос;				// выберем ответы по указанному вопросу
		
	Иначе
		ВопросПоиска = Новый Структура("Вопрос, КолонкаТаблицы, НомерСтрокиВТаблице");
		ВопросПоиска.Вопрос				= ТабличныйВопрос;		// выберем ответы по указанному табличному вопросу
		ВопросПоиска.КолонкаТаблицы		= Вопрос;				// выберем ответы по указанной колонке
		ВопросПоиска.НомерСтрокиВТаблице= НомерСтрокиВТаблице;	// НомерСтрокиВТаблице
		
	КонецЕсли;
	
	Если ВыборкаОтветовИзОпроса <> Неопределено Тогда
		ВыборкаОтветовИзОпроса.Сбросить();
	КонецЕсли;
	
	ОбъектXML.ЗаписатьНачалоЭлемента("Вопрос");
	
		ДобавитьПоле(ОбъектXML,"Ид", Вопрос.УникальныйИдентификатор());
		Если (Вопрос.Предопределенный И 
			(Вопрос.ТипЗначения.СодержитТип(Тип("Строка")) ИЛИ
			Вопрос.ТипЗначения.СодержитТип(Тип("Дата")) ИЛИ 
			Вопрос.ТипЗначения.СодержитТип(Тип("Число")) ) ) ИЛИ
			(НЕ Вопрос.Предопределенный И
			(ТипОтветаНаВопрос = Перечисления.ТипыОтветаНаВопросАнкеты.Строка ИЛИ
			ТипОтветаНаВопрос = Перечисления.ТипыОтветаНаВопросАнкеты.Число ИЛИ
			ТипОтветаНаВопрос = Перечисления.ТипыОтветаНаВопросАнкеты.Дата ИЛИ
			ТипОтветаНаВопрос = Перечисления.ТипыОтветаНаВопросАнкеты.КонтактнаяИнформация ИЛИ
			ТипОтветаНаВопрос = Перечисления.ТипыОтветаНаВопросАнкеты.Текст)) Тогда
			
			ОбъектXML.ЗаписатьНачалоЭлемента("Значение");
			
				Если ВыборкаОтветовИзОпроса <> Неопределено Тогда
					Пока ВыборкаОтветовИзОпроса.НайтиСледующий(ВопросПоиска) Цикл
						Если Вопрос.ТипЗначения.СодержитТип(Тип("Дата")) ИЛИ
							Вопрос.ТипЗначения.СодержитТип(Тип("Число")) Тогда
							Если ЗначениеЗаполнено(ВыборкаОтветовИзОпроса.ТиповойОтвет) Тогда
								ОбъектXML.ЗаписатьТекст(XMLСтрока(ВыборкаОтветовИзОпроса.ТиповойОтвет));
								
							Иначе
								ОбъектXML.ЗаписатьТекст(" ");
								
							КонецЕсли;
							
						Иначе
							ОбъектXML.ЗаписатьТекст(Строка(ВыборкаОтветовИзОпроса.ТиповойОтвет));
							
						КонецЕсли;
					КонецЦикла;
					
				Иначе
					ОбъектXML.ЗаписатьТекст(" ");
					
				КонецЕсли;
			
			ОбъектXML.ЗаписатьКонецЭлемента(); // Значение
			
		ИначеЕсли ТипОтветаНаВопрос = Перечисления.ТипыОтветаНаВопросАнкеты.ОдинИзВариантовОтвета ИЛИ
			ТипОтветаНаВопрос = Перечисления.ТипыОтветаНаВопросАнкеты.Булево ИЛИ
			ТипОтветаНаВопрос = Перечисления.ТипыОтветаНаВопросАнкеты.НесколькоВариантовОтвета ИЛИ
			ТипОтветаНаВопрос = Перечисления.ТипыОтветаНаВопросАнкеты.Ссылка ИЛИ
			Вопрос.Предопределенный Тогда
			
			Если ВыборкаОтветовИзОпроса <> Неопределено Тогда
				
				Пока ВыборкаОтветовИзОпроса.НайтиСледующий(ВопросПоиска) Цикл
					ОбъектXML.ЗаписатьНачалоЭлемента("ВариантОтвета");
					
						Если ПустаяСтрока(ВыборкаОтветовИзОпроса.ТиповойОтвет) тогда
							ДобавитьПоле(ОбъектXML,"Ид", "00000000-0000-0000-0000-000000000000");
							
						Иначе
							Попытка
								ДобавитьПоле(ОбъектXML,"Ид", ВыборкаОтветовИзОпроса.ТиповойОтвет.УникальныйИдентификатор());
									
							Исключение
								ДобавитьПоле(ОбъектXML,"Ид", ЗначениеВСтрокуВнутр(ВыборкаОтветовИзОпроса.ТиповойОтвет));
									
							КонецПопытки
							
						КонецЕсли;
						
						Если ПустаяСтрока(ВыборкаОтветовИзОпроса.Ответ) Тогда
							ДобавитьПоле(ОбъектXML,"РазвернутыйОтвет", " ");
							
						Иначе
							ДобавитьПоле(ОбъектXML,"РазвернутыйОтвет", ВыборкаОтветовИзОпроса.Ответ);
							
						КонецЕсли;
					
					ОбъектXML.ЗаписатьКонецЭлемента(); // ВариантОтвета
				КонецЦикла;
				
			Иначе
				ОбъектXML.ЗаписатьНачалоЭлемента("ВариантОтвета");
				
					ДобавитьПоле(ОбъектXML,"Ид",				"00000000-0000-0000-0000-000000000000");
					ДобавитьПоле(ОбъектXML,"РазвернутыйОтвет",	" ");
				
				ОбъектXML.ЗаписатьКонецЭлемента(); // ВариантОтвета
				
			КонецЕсли;
			
		КонецЕсли;
	
	ОбъектXML.ЗаписатьКонецЭлемента(); // Вопрос
	
КонецПроцедуры // ДобавитьОтвет

Процедура ОбработкаЗапроса(Выборка, ОбъектXML);
	
	Разделы = Выборка.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока Разделы.Следующий() Цикл
		Вопросы = Разделы.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Если Разделы.КодРаздела = NULL Тогда
			РазделНаименование	= "";
			
		Иначе
			КодРаздела			= Строка(Число(Разделы.КодРаздела));
			РазделНаименование	= КодРаздела + Разделы.РазделНаименование;
			
		КонецЕсли;
		
		ЗаписатьРаздел(ОбъектXML, Разделы.Раздел, РазделНаименование);
		
		// теперь обходим вопросы
		Пока Вопросы.Следующий() Цикл
			ЗаписатьВопрос(ОбъектXML, Вопросы, Ложь);
		КонецЦикла;
		
		ОбъектXML.ЗаписатьКонецЭлемента();
	КонецЦикла;
	
КонецПроцедуры // ОбработкаЗапроса()

Функция ВыгрузитьАнкетуВXML(Анкета, ИмяФайла = "", ОпрашиваемоеЛицо, ДокРассылки, ДокОпрос = Неопределено) 
	
	Если Анкета = Неопределено ИЛИ Анкета.Пустая() Тогда
		Возврат ""; // Нечего выгружать!
	КонецЕсли;
	
	Выборка		= ВыполнитьЗапросПоАнкете(Анкета);
	XML			= Новый ЗаписьXML();
	XML.Отступ	= Истина;
	
	Если ПустаяСтрока(ИмяФайла) Тогда
		XML.УстановитьСтроку("UTF-8"); //16
		
	Иначе
		XML.ОткрытьФайл(ИмяФайла, "UTF-8");
		
	КонецЕсли;
	
	XML.ЗаписатьОбъявлениеXML();
	
	XML.ЗаписатьИнструкциюОбработки("xml-stylesheet","type=""text/xsl"" href=""style.xsl""");
	
	XML.ЗаписатьНачалоЭлемента("Заголовок");
	
		ДобавитьПоле(XML,"ДатаФормирования",	XMLСтрока(ТекущаяДата()));
		ДобавитьПоле(XML,"ВерсияФормата",		"1.1");
		ДобавитьПоле(XML,"ИдОпрашиваемого",		ЗначениеВСтрокуВнутр(ОпрашиваемоеЛицо));
		
		XML.ЗаписатьНачалоЭлемента("Анкета");
		
			ДобавитьПоле(XML,"Ид",					Анкета.УникальныйИдентификатор());
			ДобавитьПоле(XML,"Наименование",		Анкета.НаименованиеАнкеты);
			ДобавитьПоле(XML,"Комментарий",			Анкета.Вступление);
			Если ДокРассылки <> Неопределено Тогда
				ДобавитьПоле(XML,"ИдРассылки",		ДокРассылки.УникальныйИдентификатор());
			КонецЕсли;
			ОбработкаЗапроса(Выборка, XML);
		
		XML.ЗаписатьКонецЭлемента(); // Анкета
		
		XML.ЗаписатьНачалоЭлемента("РезультатОпроса");
		
			ДобавитьПоле(XML,"ДатаЗаполненияАнкеты",	XMLСтрока(?(ЗначениеЗаполнено(ДокОпрос), ДокОпрос.Дата, "")));
			ДобавитьПоле(XML,"ИдАнкеты",				Анкета.УникальныйИдентификатор());
			ДобавитьПоле(XML,"Наименование",			Анкета.НаименованиеАнкеты);
			Если ДокРассылки <> Неопределено Тогда
				ДобавитьПоле(XML,"ИдРассылки",			ДокРассылки.УникальныйИдентификатор());
			КонецЕсли;
			
			Если ЗначениеЗаполнено(ДокОпрос) Тогда
				
				ВыборкаОтветовИзОпроса = ВыполнитьЗапросПоОпросу(ДокОпрос).Выбрать();
				
			Иначе
				Если Анкета.ЗаполнятьАнкетуДанными Тогда
					ВыборкаОтветовИзОпроса = ВыполнитьЗапросПоДанным(ОпрашиваемоеЛицо, Анкета);
					
				Иначе
					ВыборкаОтветовИзОпроса = Неопределено;
					
				КонецЕсли;
				
			КонецЕсли;
			
			ВыборкаВопросовДляОтветов = Выборка.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "Вопрос");
			Пока ВыборкаВопросовДляОтветов.Следующий() Цикл
				
				Если ВыборкаВопросовДляОтветов.ТипОтветаНаВопрос <> Перечисления.ТипыОтветаНаВопросАнкеты.Табличный Тогда
					ДобавитьОтвет(XML, ВыборкаВопросовДляОтветов.Вопрос, ВыборкаВопросовДляОтветов.ТипОтветаНаВопрос, ВыборкаОтветовИзОпроса);
					
				Иначе
					XML.ЗаписатьНачалоЭлемента("Вопрос");
					
						ДобавитьПоле(XML,"Ид", ВыборкаВопросовДляОтветов.Вопрос.УникальныйИдентификатор());
						Для НомерСтрокиТаблицы = 1 По ВыборкаВопросовДляОтветов.СтрокВТаблице Цикл
							
							XML.ЗаписатьНачалоЭлемента("СтрокаТаблицы");
							
								ДобавитьПоле(XML,"НомерСтроки", НомерСтрокиТаблицы);
								ВыборкаКолонокДляОтветов = ВыборкаВопросовДляОтветов.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "КолонкаТаблицы");
								Пока ВыборкаКолонокДляОтветов.Следующий() Цикл
									ДобавитьОтвет(XML, ВыборкаКолонокДляОтветов.КолонкаТаблицы, ВыборкаКолонокДляОтветов.КолонкаТаблицыТипОтветаНаВопрос, ВыборкаОтветовИзОпроса, ВыборкаВопросовДляОтветов.Вопрос, НомерСтрокиТаблицы);
								КонецЦикла;
							
							XML.ЗаписатьКонецЭлемента(); // СтрокаТаблицы
							
						КонецЦикла;
					
					XML.ЗаписатьКонецЭлемента(); // Вопрос
					
				КонецЕсли;
			КонецЦикла;
		
		XML.ЗаписатьКонецЭлемента(); // РезультатОпроса
	
	XML.ЗаписатьКонецЭлемента(); // Заголовок
	
	Если ЗначениеЗаполнено(ИмяФайла) Тогда
		XML.Закрыть();
		
	Иначе
		Возврат (XML.Закрыть());
		
	КонецЕсли;
	
КонецФункции // ВыгрузитьАнкетуВXML()

Функция ПолучитьИмяФайлаАнкеты(ТиповаяАнкета) Экспорт
	
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("Анкета",	ТиповаяАнкета);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТиповыеАнкеты.Код КАК Номер,
	|	ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка) КАК Организация
	|ИЗ
	|	Справочник.ТиповыеАнкеты КАК ТиповыеАнкеты
	|ГДЕ
	|	ТиповыеАнкеты.Ссылка = &Анкета";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Выборка.Следующий();
	
	КодАнкеты = ОбщегоНазначения.ПолучитьНомерНаПечать(Выборка);
	
	Возврат "anketa" + КодАнкеты+".xml";
	
КонецФункции // ПолучитьИмяФайлаАнкеты()

Функция СформироватьВложение(ТиповаяАнкета, ОпрашиваемоеЛицо = Неопределено, ЭлектронныйАдресОтветов = "", ДокРассылки = Неопределено, ДокОпрос = Неопределено, ИмяФайла = "", ФормироватьXSL = Ложь, КаталогАнкеты = "", КаталогОпроса = "") Экспорт
	
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("Анкета",	ТиповаяАнкета);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	КОЛИЧЕСТВО(*) КАК КоличествоВопросовВРазделе,
	|	ТиповыеАнкетыВопросыАнкеты.Раздел,
	|	ТиповыеАнкетыВопросыАнкеты.Вопрос
	|ИЗ
	|	Справочник.ТиповыеАнкеты.ВопросыАнкеты КАК ТиповыеАнкетыВопросыАнкеты
	|ГДЕ
	|	ТиповыеАнкетыВопросыАнкеты.Ссылка = &Анкета
	|
	|СГРУППИРОВАТЬ ПО
	|	ТиповыеАнкетыВопросыАнкеты.Раздел,
	|	ТиповыеАнкетыВопросыАнкеты.Вопрос
	|
	|ИМЕЮЩИЕ
	|	КОЛИЧЕСТВО(*) > 1";
		
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Сообщить("Вопрос """+Выборка.Вопрос + """ встречается в разделе """ + Выборка.Раздел + """ более одного раза!");
	КонецЦикла;
	
	Если НЕ Выборка.Количество() = 0 Тогда
		Возврат Ложь;
	КонецЕсли;
		
	Если НЕ ЗначениеЗаполнено(ИмяФайла) тогда
		ИмяФайла = ПолучитьИмяФайлаАнкеты(ТиповаяАнкета);
	КонецЕсли;
	
	ИмяФайлаПреобразования = "style.xsl";
	ФайлАнкеты			= Новый Файл(ИмяФайла);
	ТекстXSL			= Новый ТекстовыйДокумент;
	Преобразование		= Новый ПреобразованиеXSL;
	
	Если ФормироватьXSL Тогда
		ТекстXSL = Анкетирование.СформироватьXSL(ИмяФайла, КаталогАнкеты, КаталогОпроса, ДокОпрос = Неопределено, ЭлектронныйАдресОтветов <> "e-mail ответов", ЭлектронныйАдресОтветов);
		ТекстXSL.Записать(КаталогВременныхФайлов()+ИмяФайлаПреобразования);
	КонецЕсли;
	
	ВыгрузитьАнкетуВXML(ТиповаяАнкета, КаталогВременныхФайлов() + ИмяФайла, ОпрашиваемоеЛицо, ДокРассылки, ДокОпрос);
	
	Возврат Истина;
	
КонецФункции // СформироватьВложение()

#Если ТолстыйКлиентОбычноеПриложение Тогда

Процедура ОткрытьВложение(ТиповаяАнкета) Экспорт
	
	Если СформироватьВложение(ТиповаяАнкета, , "e-mail ответов", , , , Истина) тогда
		ИмяФайла = ПолучитьИмяФайлаАнкеты(ТиповаяАнкета);
		ЗапуститьПриложение(КаталогВременныхФайлов() + ИмяФайла);
	КонецЕсли;
	
КонецПроцедуры // ОткрытьВложение()

Процедура СоздатьОпрос(УчетнаяЗапись, ТекущийПредмет) Экспорт
	
	Если Не УправлениеЭлектроннойПочтой.ПолучитьЗначениеРеквизитаУчетнойЗаписи(УчетнаяЗапись, "ИспользоватьКлассификациюПисемПоПредметам") Тогда
		Предупреждение("Для выбранной учетной записи электронной почты не используется классифицикация писем по предметам. Создание опроса невозможно!");
		Возврат;
	КонецЕсли;
	
	ЗапросПоПредмету = Новый Запрос;
	ЗапросПоПредмету.УстановитьПараметр("ПредметКонтакта",	ТекущийПредмет);
	ЗапросПоПредмету.УстановитьПараметр("Отправленное",		Перечисления.СтатусыПисем.Отправленное);
	ЗапросПоПредмету.УстановитьПараметр("Полученное",		Перечисления.СтатусыПисем.Полученное);
	ЗапросПоПредмету.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ПредметыЭлектронныхПисем.Регистратор КАК ЭП,
	|	ВЫБОР
	|		КОГДА ВложенияЭлектронныхПисем.ИмяФайла ПОДОБНО ""%.xml""
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ЕстьРезультатОпроса,
	|	ВЫБОР
	|		КОГДА ВложенияЭлектронныхПисем.ИмяФайла ПОДОБНО ""%.xml""
	|			ТОГДА ВложенияЭлектронныхПисем.Хранилище
	|	КОНЕЦ КАК Хранилище,
	|	ПредметыЭлектронныхПисем.Регистратор.КомуТЧ.(
	|		ВЫБОР
	|			КОГДА ПредметыЭлектронныхПисем.Регистратор.СтатусПисьма = &Отправленное
	|				ТОГДА ПредметыЭлектронныхПисем.Регистратор.КомуТЧ.АдресЭлектроннойПочты
	|			ИНАЧЕ """"
	|		КОНЕЦ КАК АдресКому
	|	) КАК Кому,
	|	ВЫБОР
	|		КОГДА ПредметыЭлектронныхПисем.Регистратор.СтатусПисьма = &Полученное
	|			ТОГДА ПредметыЭлектронныхПисем.Регистратор.ОтправительАдресЭлектроннойПочты
	|		ИНАЧЕ """"
	|	КОНЕЦ КАК АдресОтКого
	|ИЗ
	|	РегистрСведений.ПредметыЭлектронныхПисем КАК ПредметыЭлектронныхПисем
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВложенияЭлектронныхПисем КАК ВложенияЭлектронныхПисем
	|		ПО ПредметыЭлектронныхПисем.Регистратор = ВложенияЭлектронныхПисем.Объект
	|ГДЕ
	|	ПредметыЭлектронныхПисем.Предмет = &ПредметКонтакта
	|	И ВложенияЭлектронныхПисем.ИДФайлаПочтовогоПисьма = """"
	|	И ВложенияЭлектронныхПисем.Объект.Ссылка ССЫЛКА Документ.ЭлектронноеПисьмо
	|	И ВложенияЭлектронныхПисем.ИмяФайла ПОДОБНО ""%.xml""";
	РезультатЗапроса = ЗапросПоПредмету.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		Предупреждение("Cоздание опроса возможно только по файлам-вложениям с расширением xml!");
		Возврат;
	КонецЕсли;
	
	ВыборкаЗапроса 	 = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.Прямой);
	ОбработкаРезультатовАнкетирования = Обработки.ЗагрузкаРезультатовАнкетирования.Создать();
	
	Пока ВыборкаЗапроса.Следующий() Цикл
		ХранилищеИзЗапроса = ВыборкаЗапроса.Хранилище.Получить();
		//Попытка
		ИмяВрФайла = ПолучитьИмяВременногоФайла();
		ХранилищеИзЗапроса.Записать(ИмяВрФайла);
		Файл = Новый ЧтениеXML();
		Файл.ОткрытьФайл(ИмяВрФайла);
		ОбъектЗагрузки 	= ОбработкаРезультатовАнкетирования.НайтиОбъектДляЗагрузки(Файл);
		Анкета 			= ОбработкаРезультатовАнкетирования.ПолучитьАнкетуИзФайла(Файл);
		ДатаОпроса 		= ОбработкаРезультатовАнкетирования.ПолучитьДатуФормированияАнкеты(Файл);
		
		Если Анкета = Неопределено тогда
			Продолжить;
		КонецЕсли;
		Если ОбъектЗагрузки = Неопределено тогда
			// если объект загрузки найти по ИдОпрашиваемого найти не удалось, тогда пробуем его найти
			// по ФИО и дате рождения, если это физлицо(конт. лицо), либо по ИНН, если это контрагент
			Файл.ОткрытьФайл(ИмяВрФайла);
			ОбъектЗагрузки = ОбработкаРезультатовАнкетирования.НайтиОбъектПоАнкетнымДанным(Файл, Анкета);
		КонецЕсли;
		ДокОпрос = ОбработкаРезультатовАнкетирования.НайтиДокументОпрос(ДатаОпроса, Анкета);
		Если НЕ ЗначениеЗаполнено(ОбъектЗагрузки) тогда
			ОбъектЗагрузки = ОбработкаРезультатовАнкетирования.НайтиОбъектПоАнкетнымДанным(Файл, Анкета);
		КонецЕсли;
		Если ЗначениеЗаполнено(ДокОпрос) тогда
			
			Ответ = Вопрос("Найден документ опрос по данной анкете. Возможно такой документ уже есть в ИБ. Продолжить зарузку данных?", РежимДиалогаВопрос.ДаНетОтмена, , КодВозвратаДиалога.Нет);
			Если Ответ = КодВозвратаДиалога.Нет тогда
				ФормаОЗ = ДокОпрос.ПолучитьФорму("ФормаДокумента");
				ФормаОЗ.Открыть();
				Сообщить("Найден документ опрос по данной анкете.");
				Возврат;
			ИначеЕсли Ответ = КодВозвратаДиалога.Отмена тогда
				Возврат;
			КонецЕсли;   
			
		КонецЕсли;
		
		Файл.ОткрытьФайл(ИмяВрФайла);
		ДокРассылки 		= ОбработкаРезультатовАнкетирования.ПолучитьДокРассылкиИзФайла(Файл);
		Если НЕ ЗначениеЗаполнено(Анкета) тогда
			Продолжить;
		КонецЕсли;
		// загружаем в документ Опрос
		ДокОпроса = Документы.Опрос.СоздатьДокумент();
		
		Пока Файл.Прочитать() Цикл
			Если (Файл.ТипУзла  = ТипУзлаXML.НачалоЭлемента) и ((Файл.Имя = "Вопрос")) Тогда
				Значение 		= Файл.ПолучитьАтрибут("Ид");
				Если Значение 	= "" Тогда
					Файл.Прочитать();
					Продолжить; // Пустые вопросы пропускаем
				КонецЕсли;
				ОбработкаРезультатовАнкетирования.ПрочитатьВопрос(Файл, ДокОпроса);
			КонецЕсли;
		КонецЦикла;
		Файл.Закрыть();
		
		ДокОпроса.ОпрашиваемоеЛицо = ОбъектЗагрузки;
		ДокОпроса.Ответственный    = ПараметрыСеанса.ТекущийПользователь;
		ДокОпроса.ТиповаяАнкета	   = Анкета;
		ДокОпроса.Рассылка 		   = ДокРассылки;
		ДокОпроса.Дата			   = ДатаОпроса;
		ДокОпроса.ПолучитьФорму("ФормаДокумента").ОткрытьМодально();
		ОбработкаПрерыванияПользователя();
	КонецЦикла;
	
КонецПроцедуры

Процедура ВставитьКнопкуСоздатьОпросВКоманднуюПанель(КоманднаяПанель, ОбработчикКоманды)
	
	Кнопки = КоманднаяПанель.Кнопки.Подменю.Кнопки;
	
	// ищем разделитель
	МестоВставки = 0;
	Для Каждого Кнопка Из Кнопки Цикл
		Если Кнопка.ТипКнопки = ТипКнопкиКоманднойПанели.Разделитель Тогда
			МестоВставки = Кнопки.Индекс(Кнопка);
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Кнопки.Вставить(МестоВставки, "СоздатьОпрос", ТипКнопкиКоманднойПанели.Действие, "Создать опрос", ОбработчикКоманды);
	Кнопки.Вставить(МестоВставки, "РазделительСоздатьОпрос", ТипКнопкиКоманднойПанели.Разделитель);
	
КонецПроцедуры

Процедура ДобавитьКомандыВФормуЭлектронногоПисьма(ФормаДокумента, ДополнительныеДействия) Экспорт
	
	// команду нужно добавить в подменю Подменю командных панелей:
	// - ДействияФормыПисьмоКОтправке
	// - ДействияФормыПисьмоКПросмотру
	// сделать это нужно с разделителем перед первым разделителем
	
	ВставитьКнопкуСоздатьОпросВКоманднуюПанель(ФормаДокумента.ЭлементыФормы.ДействияФормыПисьмоКОтправке, ДополнительныеДействия);
	ВставитьКнопкуСоздатьОпросВКоманднуюПанель(ФормаДокумента.ЭлементыФормы.ДействияФормыПисьмоКПросмотру, ДополнительныеДействия);
	
КонецПроцедуры

Процедура ВыполнитьКомандыФормыЭлектронногоПисьма(ИмяКоманды, ФормаДокумента) Экспорт
	
	Если ИмяКоманды = "СоздатьОпрос" Тогда
		ТекущийПредмет = ФормаДокумента.ПредметКонтакта;
		СоздатьОпрос(ФормаДокумента.УчетнаяЗапись, ТекущийПредмет);
	КонецЕсли;
	
КонецПроцедуры

#КонецЕсли