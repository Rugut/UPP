////////////////////////////////////////////////////////////////////////////////
// ПУСКАЛЬЩИК ИНТЕРАКТИВНОГО ОБНОВЛЕНИЯ

// Проверить, есть ли необходимость выполнять обновление информационной базы, 
// и если необходимо - выполняется обновление.
// Если обновление не удалось выполнить, то процедура предлагает завершить работу системы.
//
Процедура ВыполнитьОбновлениеИнформационнойБазы() Экспорт
	
	Если НЕ ОбновлениеИнформационнойБазы.НеобходимоОбновлениеИнформационнойБазы() Тогда
		Возврат;
	КонецЕсли;
	
	// Выполняем обновление, если оно получено легально
	Если НЕ ПроверкаЛегальностиПолученияОбновленияКлиент.ПодтвердитьЛегальностьПолученияОбновления() Тогда
		Возврат;
	КонецЕсли;

	Состояние(НСтр("ru = 'Пожалуйста, подождите, выполняется обновление информационной базы...'"));
	
	// Обработка данных, связанная с изменением конфигурации
	Ошибка                 = Ложь;
	ТекстСообщенияОбОшибке = "";
	Попытка
		СтартоваяВерсия = ОбновлениеИнформационнойБазы.ВыполнитьОбновлениеИнформационнойБазы();
	Исключение
		
		ТекстСообщенияОбОшибке = НСтр(
		"ru = 'При обновлении информационной базы возникла ошибка:
		|
		|%ПодробноеПредставлениеОшибки
		|
		|Подробности см. в Журнале регистрации.'");
		
		ТекстСообщенияОбОшибке = СтрЗаменить(ТекстСообщенияОбОшибке, "%ПодробноеПредставлениеОшибки", ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			
		Сообщить(ТекстСообщенияОбОшибке); // Не нужно писать в журнал или в панель комментариев
			
		Ошибка = Истина;
			
	КонецПопытки;
	
	// Интерактивные действия после обработки данных
	Если Ошибка Тогда
		
		Состояние("");
		
		ТекстВопроса = НСтр("ru = 'Не выполнено обновление информационной базы! 
		|Завершить работу системы?'");
		Заголовок    = НСтр("ru = 'Ошибка при обновлении информационной базы'");
		Ответ = Вопрос(ТекстВопроса, РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Да, Заголовок);
		
		Если Ответ = КодВозвратаДиалога.Да Тогда
			ВызватьИсключение ТекстСообщенияОбОшибке;
		КонецЕсли;
		
	Иначе
		
		Состояние(НСтр("ru = 'Обновление информационной базы выполнено успешно.'"));
		
		#Если ТолстыйКлиентОбычноеПриложение Тогда
		// В обычную форму передаем номер версии через реквизиты формы
		ФормаОписанияОбновлений = Обработки.ОбновлениеИнформационнойБазы.ПолучитьФорму();
		ФормаОписанияОбновлений.СтартоваяВерсия = СтартоваяВерсия;
		ФормаОписанияОбновлений.Открыть();
		#Иначе
		// В управляемую форму передаем номер версии через параметры формы
		ОткрытьФорму("ОбщаяФорма.ОписаниеИзмененийСистемы", Новый Структура("СтартоваяВерсия", СтартоваяВерсия));
		#КонецЕсли
		
	КонецЕсли;
	
КонецПроцедуры

// Проверить, возможно ли выполнять обновление информационной базы.
//
Функция ВозможноВыполнитьОбновлениеИнформационнойБазы() Экспорт
	
	Результат = СтандартныеПодсистемыКлиентПовтИсп.ПараметрыРаботыКлиента().ИнформационнаяБазаЗаблокированаДляОбновления;
	
	Если Результат Тогда
		Сообщение = НСтр("ru = 'Информационная база заблокирована в связи с обновлением версии конфигурации. Работа системы будет завершена.
		                       |Обратитесь к системному администратору за подробностями.'");
		Предупреждение(Сообщение);
	КонецЕсли;
	
	Возврат НЕ Результат;
	
КонецФункции