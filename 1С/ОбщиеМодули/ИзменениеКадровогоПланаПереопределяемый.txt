////////////////////////////////////////////////////////////////////////////////
// Вспомогательные процедуры, функции

////////////////////////////////////////////////////////////////////////////////
// Процедуры, функции объекта

Процедура ДополнитьДвижение(Движение, ВыборкаПоШапкеДокумента, ВыборкаПоСтрокамДокумента) Экспорт
	
	Если ВыборкаПоШапкеДокумента.РежимНабораПерсонала = Перечисления.ВидыОрганизационнойСтруктурыПредприятия.ПоСтруктуреЮридическихЛиц Тогда
		Движение.ПодразделениеОрганизации	= ВыборкаПоСтрокамДокумента.Подразделение;
	Иначе
		Движение.Подразделение				= ВыборкаПоСтрокамДокумента.Подразделение;
	КонецЕсли;
	
КонецПроцедуры

Функция СформироватьЗапросПоРасхождениям(ЭтотОбъект) Экспорт
	
	РежимНабораПерсонала = ПроцедурыУправленияПерсоналомДополнительный.ПолучитьРежимНабораПерсонала();
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ТекущаяДата",	ОбщегоНазначения.ПолучитьРабочуюДату());
	
	Если РежимНабораПерсонала = Перечисления.ВидыОрганизационнойСтруктурыПредприятия.ПоСтруктуреЮридическихЛиц Тогда
		Запрос.УстановитьПараметр("Организация",	ЭтотОбъект.Организация);
		
		Запрос.Текст =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	КадровоеПланирование.Подразделение КАК Подразделение,
		|	КадровоеПланирование.Должность КАК Должность,
		|	КадровоеПланирование.ЗанятоСтавок КАК Количество
		|ИЗ
		|	(ВЫБРАТЬ
		|		КадровоеПланирование.Подразделение КАК Подразделение,
		|		КадровоеПланирование.Должность КАК Должность,
		|		МАКСИМУМ(КадровоеПланирование.КоличествоСтавок) КАК КоличествоСтавок,
		|		МАКСИМУМ(КадровоеПланирование.ЗанятоСтавок) КАК ЗанятоСтавок
		|	ИЗ
		|		(ВЫБРАТЬ
		|			КадровыйПланСрезПоследних.ПодразделениеОрганизации КАК Подразделение,
		|			КадровыйПланСрезПоследних.Должность КАК Должность,
		|			СУММА(КадровыйПланСрезПоследних.Количество) КАК КоличествоСтавок,
		|			СУММА(0) КАК ЗанятоСтавок
		|		ИЗ
		|			РегистрСведений.КадровыйПлан.СрезПоследних(
		|					&ТекущаяДата,
		|					Подразделение = ЗНАЧЕНИЕ(Справочник.Подразделения.ПустаяСсылка)
		|						И ПодразделениеОрганизации.Владелец = &Организация) КАК КадровыйПланСрезПоследних
		|		
		|		СГРУППИРОВАТЬ ПО
		|			КадровыйПланСрезПоследних.ПодразделениеОрганизации,
		|			КадровыйПланСрезПоследних.Должность
		|		
		|		ОБЪЕДИНИТЬ ВСЕ
		|		
		|		ВЫБРАТЬ
		|			ЗанятыеШтатныеЕдиницыОрганизацийОстатки.ПодразделениеОрганизации,
		|			ЗанятыеШтатныеЕдиницыОрганизацийОстатки.Должность.Должность,
		|			СУММА(0),
		|			СУММА(ЗанятыеШтатныеЕдиницыОрганизацийОстатки.КоличествоСтавокОстаток)
		|		ИЗ
		|			РегистрНакопления.ЗанятыеШтатныеЕдиницыОрганизаций.Остатки(
		|					&ТекущаяДата,
		|					ПодразделениеОрганизации.Владелец = &Организация
		|						И Должность.Должность <> ЗНАЧЕНИЕ(Справочник.Должности.ПустаяСсылка)) КАК ЗанятыеШтатныеЕдиницыОрганизацийОстатки
		|		
		|		СГРУППИРОВАТЬ ПО
		|			ЗанятыеШтатныеЕдиницыОрганизацийОстатки.ПодразделениеОрганизации,
		|			ЗанятыеШтатныеЕдиницыОрганизацийОстатки.Должность.Должность
		|		
		|		ОБЪЕДИНИТЬ ВСЕ
		|		
		|		ВЫБРАТЬ
		|			Вакансии.Подразделение,
		|			Вакансии.Должность,
		|			СУММА(1),
		|			СУММА(0)
		|		ИЗ
		|			Справочник.Вакансии КАК Вакансии
		|		ГДЕ
		|			(НЕ Вакансии.Закрыта)
		|			И Вакансии.Подразделение ССЫЛКА Справочник.ПодразделенияОрганизаций
		|			И Вакансии.Подразделение.Владелец = &Организация
		|		
		|		СГРУППИРОВАТЬ ПО
		|			Вакансии.Подразделение,
		|			Вакансии.Должность
		|		
		|		ОБЪЕДИНИТЬ ВСЕ
		|		
		|		ВЫБРАТЬ
		|			ШтатноеРасписаниеОрганизацийСрезПоследних.ПодразделениеОрганизации,
		|			ШтатноеРасписаниеОрганизацийСрезПоследних.Должность.Должность,
		|			СУММА(ШтатноеРасписаниеОрганизацийСрезПоследних.КоличествоСтавок),
		|			СУММА(0)
		|		ИЗ
		|			РегистрСведений.ШтатноеРасписаниеОрганизаций.СрезПоследних(
		|					&ТекущаяДата,
		|					ПодразделениеОрганизации.Владелец = &Организация
		|						И Должность.Должность <> ЗНАЧЕНИЕ(Справочник.Должности.ПустаяСсылка)
		|						И (НЕ (ПодразделениеОрганизации, Должность.Должность) В
		|								(ВЫБРАТЬ РАЗЛИЧНЫЕ
		|									КадровыйПлан.ПодразделениеОрганизации,
		|									КадровыйПлан.Должность
		|								ИЗ
		|									РегистрСведений.КадровыйПлан.СрезПоследних(&ТекущаяДата, Подразделение = ЗНАЧЕНИЕ(Справочник.Подразделения.ПустаяСсылка)
		|										И ПодразделениеОрганизации.Владелец = &Организация) КАК КадровыйПлан
		|								ГДЕ
		|									КадровыйПлан.Количество <> 0))) КАК ШтатноеРасписаниеОрганизацийСрезПоследних
		|		
		|		СГРУППИРОВАТЬ ПО
		|			ШтатноеРасписаниеОрганизацийСрезПоследних.ПодразделениеОрганизации,
		|			ШтатноеРасписаниеОрганизацийСрезПоследних.Должность.Должность) КАК КадровоеПланирование
		|	
		|	СГРУППИРОВАТЬ ПО
		|		КадровоеПланирование.Подразделение,
		|		КадровоеПланирование.Должность) КАК КадровоеПланирование
		|ГДЕ
		|	КадровоеПланирование.КоличествоСтавок - КадровоеПланирование.ЗанятоСтавок <> 0
		|
		|УПОРЯДОЧИТЬ ПО
		|	Должность,
		|	Подразделение
		|АВТОУПОРЯДОЧИВАНИЕ";
		
	Иначе
		Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	КадровоеПланирование.Подразделение КАК Подразделение,
		|	КадровоеПланирование.Должность КАК Должность,
		|	КадровоеПланирование.ЗанятоСтавок КАК Количество
		|ИЗ
		|	(ВЫБРАТЬ
		|		КадровоеПланирование.Подразделение КАК Подразделение,
		|		КадровоеПланирование.Должность КАК Должность,
		|		МАКСИМУМ(КадровоеПланирование.ЗанятоСтавок) КАК ЗанятоСтавок,
		|		МАКСИМУМ(КадровоеПланирование.КоличествоСтавок) КАК КоличествоСтавок
		|	ИЗ
		|		(ВЫБРАТЬ
		|			КадровыйПланСрезПоследних.Подразделение КАК Подразделение,
		|			КадровыйПланСрезПоследних.Должность КАК Должность,
		|			СУММА(0) КАК ЗанятоСтавок,
		|			СУММА(КадровыйПланСрезПоследних.Количество) КАК КоличествоСтавок
		|		ИЗ
		|			РегистрСведений.КадровыйПлан.СрезПоследних(
		|					&ТекущаяДата,
		|					ПодразделениеОрганизации = ЗНАЧЕНИЕ(Справочник.ПодразделенияОрганизаций.ПустаяСсылка)
		|						И Подразделение <> ЗНАЧЕНИЕ(Справочник.Подразделения.ПустаяСсылка)) КАК КадровыйПланСрезПоследних
		|		
		|		СГРУППИРОВАТЬ ПО
		|			КадровыйПланСрезПоследних.Подразделение,
		|			КадровыйПланСрезПоследних.Должность
		|		
		|		ОБЪЕДИНИТЬ ВСЕ
		|		
		|		ВЫБРАТЬ
		|			ЗанятыеРабочиеМестаОстатки.Подразделение,
		|			ЗанятыеРабочиеМестаОстатки.Должность.Должность,
		|			СУММА(ЗанятыеРабочиеМестаОстатки.КоличествоОстаток),
		|			СУММА(0)
		|		ИЗ
		|			РегистрНакопления.ЗанятыеРабочиеМеста.Остатки(&ТекущаяДата, Должность.Должность <> ЗНАЧЕНИЕ(Справочник.Должности.ПустаяСсылка)) КАК ЗанятыеРабочиеМестаОстатки
		|		
		|		СГРУППИРОВАТЬ ПО
		|			ЗанятыеРабочиеМестаОстатки.Подразделение,
		|			ЗанятыеРабочиеМестаОстатки.Должность.Должность
		|		
		|		ОБЪЕДИНИТЬ ВСЕ
		|		
		|		ВЫБРАТЬ
		|			Вакансии.Подразделение,
		|			Вакансии.Должность,
		|			СУММА(0),
		|			СУММА(1)
		|		ИЗ
		|			Справочник.Вакансии КАК Вакансии
		|		ГДЕ
		|			(НЕ Вакансии.Закрыта)
		|			И Вакансии.Подразделение ССЫЛКА Справочник.Подразделения
		|		
		|		СГРУППИРОВАТЬ ПО
		|			Вакансии.Подразделение,
		|			Вакансии.Должность) КАК КадровоеПланирование
		|	
		|	СГРУППИРОВАТЬ ПО
		|		КадровоеПланирование.Подразделение,
		|		КадровоеПланирование.Должность) КАК КадровоеПланирование
		|ГДЕ
		|	КадровоеПланирование.КоличествоСтавок - КадровоеПланирование.ЗанятоСтавок <> 0
		|
		|УПОРЯДОЧИТЬ ПО
		|	Должность,
		|	Подразделение
		|АВТОУПОРЯДОЧИВАНИЕ";
		
	КонецЕсли;
	
	Возврат Запрос.Выполнить();
	
КонецФункции

Функция СформироватьЗапросПоТекущемуСостоянию(ЭтотОбъект) Экспорт
	
	РежимНабораПерсонала = ПроцедурыУправленияПерсоналомДополнительный.ПолучитьРежимНабораПерсонала();
	
	Запрос = Новый Запрос;
	
	Если РежимНабораПерсонала = Перечисления.ВидыОрганизационнойСтруктурыПредприятия.ПоСтруктуреЮридическихЛиц Тогда
		
		Запрос.УстановитьПараметр("Организация",	ЭтотОбъект.Организация);
		
		Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	РаботникиСрезПоследних.ПодразделениеОрганизации КАК Подразделение,
		|	РаботникиСрезПоследних.Должность.Должность КАК Должность,
		|	СУММА(РаботникиСрезПоследних.ЗанимаемыхСтавок) КАК Количество,
		|	ВЫБОР
		|		КОГДА РаботникиСрезПоследних.Должность.Должность = ЗНАЧЕНИЕ(Справочник.Должности.ПустаяСсылка)
		|			ТОГДА ЛОЖЬ
		|		ИНАЧЕ ИСТИНА
		|	КОНЕЦ КАК ДолжностьЗаполнена,
		|	ВЫБОР
		|		КОГДА РаботникиСрезПоследних.Должность.Должность = ЗНАЧЕНИЕ(Справочник.Должности.ПустаяСсылка)
		|			ТОГДА РаботникиСрезПоследних.Должность.Наименование
		|		ИНАЧЕ """"
		|	КОНЕЦ КАК ДолжностьОрганизации
		|ИЗ
		|	РегистрСведений.РаботникиОрганизаций.СрезПоследних(, ОбособленноеПодразделение = &Организация) КАК РаботникиСрезПоследних
		|ГДЕ
		|	РаботникиСрезПоследних.ПричинаИзмененияСостояния <> ЗНАЧЕНИЕ(Перечисление.ПричиныИзмененияСостояния.Увольнение)
		|
		|СГРУППИРОВАТЬ ПО
		|	РаботникиСрезПоследних.ПодразделениеОрганизации,
		|	РаботникиСрезПоследних.Должность.Должность,
		|	ВЫБОР
		|		КОГДА РаботникиСрезПоследних.Должность.Должность = ЗНАЧЕНИЕ(Справочник.Должности.ПустаяСсылка)
		|			ТОГДА РаботникиСрезПоследних.Должность.Наименование
		|		ИНАЧЕ """"
		|	КОНЕЦ";
		
	Иначе
		
		Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	РаботникиСрезПоследних.Подразделение КАК Подразделение,
		|	РаботникиСрезПоследних.Должность.Должность КАК Должность,
		|	СУММА(РаботникиСрезПоследних.ЗанимаемыхСтавок) КАК Количество,
		|	ВЫБОР
		|		КОГДА РаботникиСрезПоследних.Должность.Должность = ЗНАЧЕНИЕ(Справочник.Должности.ПустаяСсылка)
		|			ТОГДА ЛОЖЬ
		|		ИНАЧЕ ИСТИНА
		|	КОНЕЦ КАК ДолжностьЗаполнена,
		|	ВЫБОР
		|		КОГДА РаботникиСрезПоследних.Должность.Должность = ЗНАЧЕНИЕ(Справочник.Должности.ПустаяСсылка)
		|			ТОГДА РаботникиСрезПоследних.Должность.Наименование
		|		ИНАЧЕ """"
		|	КОНЕЦ КАК ДолжностьОрганизации
		|ИЗ
		|	РегистрСведений.Работники.СрезПоследних КАК РаботникиСрезПоследних
		|ГДЕ
		|	РаботникиСрезПоследних.ПричинаИзмененияСостояния <> ЗНАЧЕНИЕ(Перечисление.ПричиныИзмененияСостояния.Увольнение)
		|
		|СГРУППИРОВАТЬ ПО
		|	РаботникиСрезПоследних.Подразделение,
		|	РаботникиСрезПоследних.Должность.Должность,
		|	ВЫБОР
		|		КОГДА РаботникиСрезПоследних.Должность.Должность = ЗНАЧЕНИЕ(Справочник.Должности.ПустаяСсылка)
		|			ТОГДА РаботникиСрезПоследних.Должность.Наименование
		|		ИНАЧЕ """"
		|	КОНЕЦ";
		
	КонецЕсли;
	
	Возврат Запрос.Выполнить();
	
КонецФункции

Процедура ПроверитьЗначенияРеквизитов(ВыборкаПоШапкеДокумента, ВыборкаПоСтрокамДокумента, Отказ, СтрокаНачалаСообщенияОбОшибке) Экспорт
	
	Если ВыборкаПоШапкеДокумента.РежимНабораПерсонала = Перечисления.ВидыОрганизационнойСтруктурыПредприятия.ПоСтруктуреЮридическихЛиц Тогда
		Если ВыборкаПоСтрокамДокумента.ЭтоУправленческоеПодразделение Тогда
			СтрокаНачалаСообщенияОбОшибке = СтрокаНачалаСообщенияОбОшибке + "недопустимо указывать управленческие подразделения при текущем режиме кадрового планирования!";
			Отказ = Истина;
		КонецЕсли;
	Иначе
		Если Не ВыборкаПоСтрокамДокумента.ЭтоУправленческоеПодразделение Тогда
			СтрокаНачалаСообщенияОбОшибке = СтрокаНачалаСообщенияОбОшибке + "недопустимо указывать подразделения организаций при текущем режиме кадрового планирования!";
			Отказ = Истина;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

// Формирует запрос по шапке документа
//
// Параметры: 
//  Режим - режим проведения
//
// Возвращаемое значение:
//  Результат запроса
//
Функция СформироватьЗапросПоШапке(Режим, Ссылка) Экспорт

	Запрос = Новый Запрос;

	// Установим параметры запроса
	Запрос.УстановитьПараметр("ДокументСсылка",	Ссылка);

	Запрос.Текст =
	"ВЫБРАТЬ
	|	ИзменениеКадровогоПлана.ДатаИзменений,
	|	ИзменениеКадровогоПлана.Ссылка,
	|	Константы.РежимНабораПерсонала
	|ИЗ
	|	Документ.ИзменениеКадровогоПлана КАК ИзменениеКадровогоПлана,
	|	Константы КАК Константы
	|ГДЕ
	|	ИзменениеКадровогоПлана.Ссылка = &ДокументСсылка";

	Возврат Запрос.Выполнить();
	
КонецФункции // СформироватьЗапросПоШапке()

// Формирует запрос по таблице "Штатные единицы" документа
//
// Параметры: 
//  Режим - режим проведения
//
// Возвращаемое значение:
//  Результат запроса
//
Функция СформироватьЗапросПоРабочиеМеста(ВыборкаПоШапкеДокумента, Режим, Ссылка) Экспорт
	
	Запрос = Новый Запрос;
	
	// Установим параметры запроса
	Запрос.УстановитьПараметр("ДокументСсылка",			Ссылка);
	Запрос.УстановитьПараметр("РежимНабораПерсонала",	ВыборкаПоШапкеДокумента.РежимНабораПерсонала);

	Запрос.Текст =
	"ВЫБРАТЬ
	|	""РабочиеМеста"" КАК ВидСтрокиЗапроса,
	|	Док.НомерСтроки КАК НомерСтроки,
	|	Док.Подразделение КАК Подразделение,
	|	Док.Должность,
	|	Док.Количество,
	|	NULL КАК КонфликтныйДокумент,
	|	ВЫБОР
	|		КОГДА Док.Подразделение ССЫЛКА Справочник.Подразделения
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ЭтоУправленческоеПодразделение
	|ИЗ
	|	Документ.ИзменениеКадровогоПлана.РабочиеМеста КАК Док
	|ГДЕ
	|	Док.Ссылка = &ДокументСсылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	""КонфликтныйДокумент"",
	|	ИзменениеКадровогоПланаРабочиеМеста.НомерСтроки,
	|	NULL,
	|	NULL,
	|	NULL,
	|	ПРЕДСТАВЛЕНИЕ(КадровыйПлан.Регистратор),
	|	NULL
	|ИЗ
	|	Документ.ИзменениеКадровогоПлана.РабочиеМеста КАК ИзменениеКадровогоПланаРабочиеМеста
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.КадровыйПлан КАК КадровыйПлан
	|		ПО ИзменениеКадровогоПланаРабочиеМеста.Должность = КадровыйПлан.Должность
	|			И ИзменениеКадровогоПланаРабочиеМеста.Ссылка.ДатаИзменений = КадровыйПлан.Период
	|			И (ВЫБОР
	|				КОГДА &РежимНабораПерсонала = ЗНАЧЕНИЕ(Перечисление.ВидыОрганизационнойСтруктурыПредприятия.ПоСтруктуреЮридическихЛиц)
	|					ТОГДА ИзменениеКадровогоПланаРабочиеМеста.Подразделение = КадровыйПлан.ПодразделениеОрганизации
	|				ИНАЧЕ ИзменениеКадровогоПланаРабочиеМеста.Подразделение = КадровыйПлан.Подразделение
	|			КОНЕЦ)
	|ГДЕ
	|	ИзменениеКадровогоПланаРабочиеМеста.Ссылка = &ДокументСсылка";
	
	Возврат Запрос.Выполнить();

КонецФункции // СформироватьЗапросПоРабочиеМеста()

////////////////////////////////////////////////////////////////////////////////
// Процедуры, функции для работы формы документа

#Если ТолстыйКлиентОбычноеПриложение Тогда
	
Процедура ПриОткрытииДополнительно(ЭтаФорма, ЭтотОбъект) Экспорт
	
	РежимНабораПерсонала = ПроцедурыУправленияПерсоналомДополнительный.ПолучитьРежимНабораПерсонала();
	
	Если ЭтотОбъект.ЭтоНовый() Тогда
		Если РежимНабораПерсонала = Перечисления.ВидыОрганизационнойСтруктурыПредприятия.ПоЦентрамОтветственности Тогда
			ЭтотОбъект.Организация = Справочники.Организации.ПустаяСсылка();
		КонецЕсли;
	КонецЕсли;	
	
	Если РежимНабораПерсонала = Перечисления.ВидыОрганизационнойСтруктурыПредприятия.ПоЦентрамОтветственности Тогда
		ЭтаФорма.ЭлементыФормы.ПанельОрганизация.Свертка = РежимСверткиЭлементаУправления.Верх;
	КонецЕсли;
	
КонецПроцедуры

#КонецЕсли

Процедура ПриНачалеРедактированияДополнительно(Элемент) Экспорт
	
	РежимНабораПерсонала = ПроцедурыУправленияПерсоналомДополнительный.ПолучитьРежимНабораПерсонала();
	
	Если РежимНабораПерсонала = Перечисления.ВидыОрганизационнойСтруктурыПредприятия.ПоСтруктуреЮридическихЛиц Тогда
		Элемент.ТекущаяСтрока.Подразделение = Справочники.ПодразделенияОрганизаций.ПустаяСсылка();
	Иначе
		Элемент.ТекущаяСтрока.Подразделение = Справочники.Подразделения.ПустаяСсылка();
	КонецЕсли;
		
КонецПроцедуры