#Если ТолстыйКлиентОбычноеПриложение Тогда
	
////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ФОРМЫ СПИСКА

Функция РежимВводаПоЦФО(мРежимВводаПоказателей) Экспорт
	
	Возврат мРежимВводаПоказателей <> Перечисления.ВидыОрганизационнойСтруктурыПредприятия.ПоСтруктуреЮридическихЛиц;
	
КонецФункции

// Устанавливает свертку панелей и видимость реквизитов формы в зависимости от выбранной организационной структуры 
//
Процедура ВидОрганизационнойСтруктурыПредприятияПриИзменении(мРежимВводаПоказателей, ЭлементыФормы)
	
	РежимВводаПоЦФО = РежимВводаПоЦФО(мРежимВводаПоказателей);
	
	ИспользоватьУправленческийУчетЗарплаты = глЗначениеПеременной("глИспользоватьУправленческийУчетЗарплаты");
	Если ИспользоватьУправленческийУчетЗарплаты Тогда
		ЭлементыФормы.ДействияФормы.Кнопки.ПорядокФормированияСМ.Кнопки.ПоЦФО.Пометка			= РежимВводаПоЦФО;
		ЭлементыФормы.ДействияФормы.Кнопки.ПорядокФормированияСМ.Кнопки.ПоОрганизациям.Пометка	= Не РежимВводаПоЦФО;
	КонецЕсли;
	
	ЭлементыФормы.ПанельОрганизации.Свертка = ?(РежимВводаПоЦФО, РежимСверткиЭлементаУправления.Верх, РежимСверткиЭлементаУправления.Нет);
		
КонецПроцедуры // ВидОрганизационнойСтруктурыПредприятияПриИзменении

Процедура ПереключитьРежимВводаПоказателейПоОрганизациям(мРежимВводаПоказателей, мОрганизация, Форма, Отбор) Экспорт
	
	мРежимВводаПоказателей = Перечисления.ВидыОрганизационнойСтруктурыПредприятия.ПоСтруктуреЮридическихЛиц;
	
	Если Не ЗначениеЗаполнено(Отбор.Организация.Значение) Тогда
		Если мОрганизация = Неопределено Тогда
			// заполним организацию
			РаботаСДиалогамиЗК.ЗаполнениеОтбораПоОрганизацииПоУмолчанию(Форма, Отбор.Организация, Форма.ЭлементыФормы.Организация, Неопределено, Ложь, глЗначениеПеременной("глТекущийПользователь"));
		Иначе
			Отбор.Организация.Значение = мОрганизация;
		КонецЕсли;
	КонецЕсли;
	
	Отбор.Организация.ВидСравнения = ?(ЗначениеЗаполнено(Отбор.Организация.Значение), ВидСравнения.Равно, ВидСравнения.НеРавно);
	
	ВидОрганизационнойСтруктурыПредприятияПриИзменении(мРежимВводаПоказателей, Форма.ЭлементыФормы);
	
КонецПроцедуры

Процедура ПереключитьРежимВводаПоказателейПоЦФО(мРежимВводаПоказателей, мОрганизация, Форма, Отбор) Экспорт
	
	мОрганизация = Отбор.Организация.Значение;
	Отбор.Организация.Установить(Справочники.Организации.ПустаяСсылка());
	
	мРежимВводаПоказателей = Перечисления.ВидыОрганизационнойСтруктурыПредприятия.ПоЦентрамОтветственности;
	
	ВидОрганизационнойСтруктурыПредприятияПриИзменении(мРежимВводаПоказателей, Форма.ЭлементыФормы);
	
КонецПроцедуры

#КонецЕсли

// Процедура получает набор исходных значений показателей, 
//  определяет курсы валют и выполняет пересчет значений в валюту управленческого учета
//
// Параметры:
//  ИсходныеЗначенияПоказателей - таблица значений, возвращаемая формой ввода значений показателей
//
Процедура ПересчитатьВалютныеЗначенияПоказателей(ИсходныеЗначенияПоказателей) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ИсходныеЗначенияПоказателей", ИсходныеЗначенияПоказателей);
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ИсходныеЗначенияПоказателей.Сотрудник КАК Сотрудник,
	|	ИсходныеЗначенияПоказателей.Показатель КАК Показатель,
	|	ИсходныеЗначенияПоказателей.ПериодДействия КАК ПериодДействия,
	|	ИсходныеЗначенияПоказателей.Валюта КАК ВалютаПоказателя,
	|	ИсходныеЗначенияПоказателей.Значение КАК Значение
	|ПОМЕСТИТЬ ИсходныеЗначенияПоказателей
	|ИЗ
	|	&ИсходныеЗначенияПоказателей КАК ИсходныеЗначенияПоказателей
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	КурсыВалютДляРасчетовСПерсоналом.Валюта КАК Валюта,
	|	КурсыВалютДляРасчетовСПерсоналом.Курс,
	|	КурсыВалютДляРасчетовСПерсоналом.Кратность
	|ПОМЕСТИТЬ КурсыВалют
	|ИЗ
	|	РегистрСведений.КурсыВалютДляРасчетовСПерсоналом КАК КурсыВалютДляРасчетовСПерсоналом
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ИсходныеЗначенияПоказателей КАК ИсходныеЗначенияПоказателей
	|		ПО КурсыВалютДляРасчетовСПерсоналом.Период = ИсходныеЗначенияПоказателей.ПериодДействия
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ
	|	Константы.ВалютаРегламентированногоУчета,
	|	1,
	|	1
	|ИЗ
	|	Константы КАК Константы
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Валюта
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ПоказателиВидовРасчета.Показатель,
	|	ПоказателиВидовРасчета.ВалютаПоказателя,
	|	ВЫБОР
	|		КОГДА ПоказателиВидовРасчета.ВалютаПоказателя = ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка)
	|				ИЛИ ПоказателиВидовРасчета.ВалютаПоказателя = Константы.ВалютаУправленческогоУчета
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК ВводВалютныхЗначений,
	|	ВЫБОР
	|		КОГДА ПоказателиВидовРасчета.ВалютаПоказателя = ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка)
	|				ИЛИ ПоказателиВидовРасчета.ВалютаПоказателя = Константы.ВалютаУправленческогоУчета
	|			ТОГДА 1
	|		ИНАЧЕ КурсыВалют.Курс / КурсВалютыУпрУчета.Курс
	|	КОНЕЦ КАК Курс,
	|	ВЫБОР
	|		КОГДА ПоказателиВидовРасчета.ВалютаПоказателя = ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка)
	|				ИЛИ ПоказателиВидовРасчета.ВалютаПоказателя = Константы.ВалютаУправленческогоУчета
	|			ТОГДА 1
	|		ИНАЧЕ КурсыВалют.Кратность / КурсВалютыУпрУчета.Кратность
	|	КОНЕЦ КАК Кратность
	|ПОМЕСТИТЬ КурсыВалютПоказателей
	|ИЗ
	|	ИсходныеЗначенияПоказателей КАК ПоказателиВидовРасчета
	|		ЛЕВОЕ СОЕДИНЕНИЕ КурсыВалют КАК КурсыВалют
	|		ПО ПоказателиВидовРасчета.ВалютаПоказателя = КурсыВалют.Валюта,
	|	Константы КАК Константы
	|		ЛЕВОЕ СОЕДИНЕНИЕ КурсыВалют КАК КурсВалютыУпрУчета
	|		ПО Константы.ВалютаУправленческогоУчета = КурсВалютыУпрУчета.Валюта
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ИсходныеЗначенияПоказателей.Сотрудник,
	|	ИсходныеЗначенияПоказателей.Показатель,
	|	ИсходныеЗначенияПоказателей.ПериодДействия,
	|	ИсходныеЗначенияПоказателей.ВалютаПоказателя,
	|	КурсыВалютПоказателей.ВводВалютныхЗначений,
	|	ВЫБОР
	|		КОГДА КурсыВалютПоказателей.ВводВалютныхЗначений
	|			ТОГДА ВЫБОР
	|					КОГДА КурсыВалютПоказателей.Курс ЕСТЬ NULL 
	|						ТОГДА 0
	|					ИНАЧЕ ИсходныеЗначенияПоказателей.Значение * КурсыВалютПоказателей.Курс / КурсыВалютПоказателей.Кратность
	|				КОНЕЦ
	|		ИНАЧЕ ИсходныеЗначенияПоказателей.Значение
	|	КОНЕЦ КАК Значение,
	|	ИсходныеЗначенияПоказателей.Значение КАК ЗначениеВалюта,
	|	ВЫБОР
	|		КОГДА КурсыВалютПоказателей.Курс ЕСТЬ NULL 
	|			ТОГДА КурсыВалютПоказателей.ВалютаПоказателя
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка)
	|	КОНЕЦ КАК НеЗаданКурсВалюты
	|ИЗ
	|	ИсходныеЗначенияПоказателей КАК ИсходныеЗначенияПоказателей
	|		ЛЕВОЕ СОЕДИНЕНИЕ КурсыВалютПоказателей КАК КурсыВалютПоказателей
	|		ПО ИсходныеЗначенияПоказателей.ВалютаПоказателя = КурсыВалютПоказателей.ВалютаПоказателя";
	
	ИсходныеЗначенияПоказателей = Запрос.Выполнить().Выгрузить();
	
КонецПроцедуры