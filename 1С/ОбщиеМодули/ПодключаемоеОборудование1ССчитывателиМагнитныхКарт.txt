///////////////////////////////////////////////////////////////////////////////
// ПРОГРАММНЫЙ ИНТЕРФЕЙС

// Функция осуществляет подключение устройства.
//
// Параметры:
//  ОбъектДрайвера   - <*>
//           - ОбъектДрайвера драйвера торгового оборудования.
//
// Возвращаемое значение:
//  <Булево> - Результат работы функции.
//
Функция ПодключитьУстройство(ОбъектДрайвера, Параметры, ПараметрыПодключения, ВыходныеПараметры) Экспорт

	Результат  = Истина;

	// Обязательные выходные
	Если ТипЗнч(ВыходныеПараметры) <> Тип("Массив") Тогда
		ВыходныеПараметры = Новый Массив();
	КонецЕсли;

	// Проверка настроенных параметров
	Порт             = Неопределено;
	Скорость         = Неопределено;
	СтопБит          = Неопределено;
	БитДанных        = Неопределено;
	ПараметрыДорожек = Неопределено;

	Параметры.Свойство("Порт",             Порт);
	Параметры.Свойство("Скорость",         Скорость);
	Параметры.Свойство("СтопБит",          СтопБит);
	Параметры.Свойство("БитДанных",        БитДанных);
	Параметры.Свойство("ПараметрыДорожек", ПараметрыДорожек);

	Если Порт             = Неопределено
	 Или Скорость         = Неопределено
	 Или СтопБит          = Неопределено
	 Или БитДанных        = Неопределено
	 Или ПараметрыДорожек = Неопределено Тогда
		ВыходныеПараметры.Добавить(999);
		ВыходныеПараметры.Добавить(НСтр("ru='Не настроены параметры устройства.
		|Для корректной работы устройства необходимо задать параметры его работы.
		|Сделать это можно при помощи формы ""Настройка параметров"" модели
		|подключаемого оборудования в форме ""Подключение и настройка оборудования"".'"));

		Результат = Ложь;
	КонецЕсли;

	Если Результат Тогда
		ВыходныеПараметры.Добавить("СчитывательМагнитныхКарт");
		ВыходныеПараметры.Добавить(Новый Массив());
		ВыходныеПараметры[1].Добавить("ПолученКодКарты");

		Результат = (ОбъектДрайвера.Подсоединить(ВыходныеПараметры[0]) = 0);
		Если НЕ Результат Тогда
			ВыходныеПараметры.Очистить();
			ВыходныеПараметры.Добавить(999);
			ВыходныеПараметры.Добавить(НСтр("ru='Ошибка при подключении устройства.
			|Проверьте настройки порта.'"));
		КонецЕсли;

		Если Результат = Истина Тогда
			ОбъектДрайвера.БитДанных  = Параметры.БитДанных;
			ОбъектДрайвера.Порт       = Параметры.Порт;
			ОбъектДрайвера.Скорость   = Параметры.Скорость;

			Для Индекс = 1 По 3 Цикл
				Если Параметры.ПараметрыДорожек[3 - Индекс].Использовать Тогда
					// начиная с версии 8.0.12.2 произошли изменения в интерфейсе драйвера; значение 8001202 на другое не менять!
					Если ВерсияСтрокаВЧисло(ОбъектДрайвера.ПолучитьНомерВерсии()) >= 8001202 И Порт=0 Тогда
						ОбъектДрайвера.СуффиксСтрока = СПВСимволы(Параметры.ПараметрыДорожек[3 - Индекс].Суффикс, 13);
					Иначе
						ОбъектДрайвера.СтопСимвол = КодСимвола(СПВСимволы(Параметры.ПараметрыДорожек[3 - Индекс].Суффикс, 13));
						ОбъектДрайвера.Порт       = Параметры.Порт;
					КонецЕсли;
					Прервать;
				КонецЕсли;
			КонецЦикла;

			ОбъектДрайвера.ИмяСобытия = ВыходныеПараметры[1][0];

			Результат = (ОбъектДрайвера.Занять(1) = 0);
			Если Результат Тогда
				ОбъектДрайвера.УстройствоВключено = 1;
				ОбъектДрайвера.ПосылкаДанных      = 1;
				ОбъектДрайвера.ОчиститьВход();
				ОбъектДрайвера.ОчиститьВыход();

				Результат = (ОбъектДрайвера.УстройствоВключено = 1);
				Если НЕ Результат Тогда
					ОтключитьУстройство(ОбъектДрайвера, Параметры, ПараметрыПодключения, ВыходныеПараметры);

					ВыходныеПараметры.Очистить();
					ВыходныеПараметры.Добавить(999);
					ВыходныеПараметры.Добавить(НСтр("ru='Ошибка при подключении устройства.
					|Проверьте настройки порта.'"));
				КонецЕсли;
			Иначе
				ОтключитьУстройство(ОбъектДрайвера, Параметры, ПараметрыПодключения, ВыходныеПараметры);

				ВыходныеПараметры.Очистить();
				ВыходныеПараметры.Добавить(999);
				ВыходныеПараметры.Добавить(НСтр("ru='Не удалось занять устройство.
				|Проверьте настройки порта.'"));
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;

	Возврат Результат;

КонецФункции

// Функция осуществляет отключение устройства.
//
// Параметры:
//  ОбъектДрайвера - <*>
//         - ОбъектДрайвера драйвера торгового оборудования.
//
// Возвращаемое значение:
//  <Булево> - Результат работы функции.
//
Функция ОтключитьУстройство(ОбъектДрайвера, Параметры, ПараметрыПодключения, ВыходныеПараметры) Экспорт

	Результат = Истина;

	// Обязательные выходные
	Если ТипЗнч(ВыходныеПараметры) <> Тип("Массив") Тогда
		ВыходныеПараметры = Новый Массив();
	КонецЕсли;

	ОбъектДрайвера.УстройствоВключено = 0;
	ОбъектДрайвера.Освободить();
	ОбъектДрайвера.Отсоединить();

	Возврат Результат;

КонецФункции

// Функция получает, обрабатывает и перенаправляет на исполнение команду к драйверу
//
Функция ВыполнитьКоманду(Команда, ВходныеПараметры = Неопределено, ВыходныеПараметры = Неопределено,
                         ОбъектДрайвера, Параметры, ПараметрыПодключения) Экспорт

	Результат = Истина;

	// Обязательные выходные
	Если ТипЗнч(ВыходныеПараметры) <> Тип("Массив") Тогда
		ВыходныеПараметры = Новый Массив();
	КонецЕсли;

	// Обработка события от устройства
	Если Команда = "ОбработатьСобытие" Тогда
		Событие = ВходныеПараметры[0];
		Данные  = ВходныеПараметры[1];

		Результат = ОбработатьСобытие(ОбъектДрайвера, Параметры, ПараметрыПодключения, Событие, Данные, ВыходныеПараметры);

	// Завершение обработки события от устройства
	ИначеЕсли Команда = "ЗавершитьОбработкуСобытия" Тогда
		Результат = ЗавершитьОбработкуСобытия(ОбъектДрайвера, Параметры, ПараметрыПодключения, ВыходныеПараметры);

	// Тестирование устройства
	ИначеЕсли Команда = "CheckHealth" Тогда
		Результат = ТестУстройства(ОбъектДрайвера, Параметры, ПараметрыПодключения, ВыходныеПараметры);

	// Настройка параметров журналирования драйвера
	ИначеЕсли Команда = "ПараметрыЖурналирования" Тогда
		Результат = ПараметрыЖурналирования(ОбъектДрайвера, Параметры, ПараметрыПодключения, ВыходныеПараметры);

	// Получение версии драйвера
	ИначеЕсли Команда = "ПолучитьВерсиюДрайвера" Тогда
		Результат = ПолучитьВерсиюДрайвера(ОбъектДрайвера, Параметры, ПараметрыПодключения, ВыходныеПараметры);

	// Указанная команда не поддерживается данным драйвером
	Иначе
		ВыходныеПараметры.Добавить(999);
		ВыходныеПараметры.Добавить(НСтр("ru='Команда ""%Команда%"" не поддерживается данным драйвером.'"));
		ВыходныеПараметры[1] = СтрЗаменить(ВыходныеПараметры[1], "%Команда%", Команда);
		Результат = Ложь;

	КонецЕсли;

	Возврат Результат;

КонецФункции

///////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

// Возвращает версию компоненты расположенной в макете.
//
Функция ПолучитьВерсиюВнешнейКомпоненты() 
	Возврат "8.0.14.1";
КонецФункции

// Функция осуществляет обработку внешних событий торгового оборудования.
//
Функция ОбработатьСобытие(ОбъектДрайвера, Параметры, ПараметрыПодключения, Событие, Данные, ВыходныеПараметры)

	Результат = Истина;
	КодКарты  = Данные;

	ОбъектДрайвера.ПосылкаДанных = 0;

	ПозицияПрефикса = 0;
	ПозицияСуффикса = 0;
	времКодКарты    = "";
	ДанныеКарты = "";
	ПозицияДляЧтения = 1;

	ДанныеДорожек = Новый Массив();
	Для НомерДорожки = 1 По 3 Цикл
		ДанныеДорожек.Добавить("");

		ТекущаяДорожка = Параметры.ПараметрыДорожек[НомерДорожки - 1];
		Если ТекущаяДорожка.Использовать Тогда
			ПрефиксДрайвера = Символ(ТекущаяДорожка.Префикс);
			СуффиксДрайвера = Символ(ТекущаяДорожка.Суффикс);

			Если ПозицияДляЧтения < СтрДлина(КодКарты) Тогда
				ДанныеКарты = Сред(КодКарты, ПозицияДляЧтения);

				ПозицияПрефикса = Найти(ДанныеКарты, ПрефиксДрайвера);
				ПозицияСуффикса = Найти(ДанныеКарты, СуффиксДрайвера);

				времПозицияПрефикса = ?(ПозицияПрефикса = 0, 1, ПозицияПрефикса + СтрДлина(ПрефиксДрайвера));
				времДлинаДоСуффикса = ?(ПозицияСуффикса = 0,
				    СтрДлина(ДанныеКарты) + 1 - времПозицияПрефикса, ПозицияСуффикса - времПозицияПрефикса);
				времКодКарты = времКодКарты + Сред(ДанныеКарты, времПозицияПрефикса, времДлинаДоСуффикса);

				ДанныеДорожек[НомерДорожки - 1] = Сред(ДанныеКарты,
				                                  времПозицияПрефикса,
				                                  времДлинаДоСуффикса);

				ПозицияДляЧтения = ПозицияДляЧтения + ?(ПозицияСуффикса = 0,
				                                        СтрДлина(ДанныеКарты),
				                                        ПозицияСуффикса + СтрДлина(СуффиксДрайвера) - 1);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;

	КодКарты = времКодКарты;

	ВыходныеПараметры.Добавить("TracksData");
	ВыходныеПараметры.Добавить(Новый Массив());
	ВыходныеПараметры[1].Добавить(КодКарты);
	ВыходныеПараметры[1].Добавить(Новый Массив);
	ВыходныеПараметры[1][1].Добавить(Сред(Данные,2));
	ВыходныеПараметры[1][1].Добавить(ДанныеДорожек);
	ВыходныеПараметры[1][1].Добавить(0);
	ВыходныеПараметры[1][1].Добавить(МенеджерОборудованияСервер.РасшифроватьКодМагнитнойКарты(ДанныеДорожек, Параметры.ПараметрыДорожек));

	Возврат Результат;

КонецФункции

// Процедура вызывается, когда система готова принять следующее событие от устройства.
Функция ЗавершитьОбработкуСобытия(ОбъектДрайвера, Параметры, ПараметрыПодключения, ВыходныеПараметры)

	Результат = Истина;

	ОбъектДрайвера.ПосылкаДанных = 1;

	Возврат Результат;

КонецФункции

// Осуществляется открытие формы проверки параметров драйвера
Функция ТестУстройства(ОбъектДрайвера, Параметры, ПараметрыПодключения, ВыходныеПараметры)

	Результат = Истина;

	ОбъектДрайвера.Порт      = Параметры.Порт;
	ОбъектДрайвера.Скорость  = Параметры.Скорость;
	ОбъектДрайвера.БитДанных = Параметры.БитДанных;
	//ОбъектДрайвера.СтопБит   = Параметры.СтопБит;

	ОбъектДрайвера.ТестУстройства();
	
	Возврат Результат;

КонецФункции

// Осуществляется открытие формы настройки параметров журналирования драйвера
Функция ПараметрыЖурналирования(ОбъектДрайвера, Параметры, ПараметрыПодключения, ВыходныеПараметры)

	Результат = Истина;

	ОбъектДрайвера.Скорость  = Параметры.Скорость;
	ОбъектДрайвера.БитДанных = Параметры.БитДанных;


	ОбъектДрайвера.ПараметрыЖурналирования();

	Возврат Результат;

КонецФункции

// Функция возвращает версию установленного драйвера
//
Функция ПолучитьВерсиюДрайвера(ОбъектДрайвера, Параметры, ПараметрыПодключения, ВыходныеПараметры)

	Результат = Истина;

	ВыходныеПараметры.Добавить(НСтр("ru='Установлен'"));
	ВыходныеПараметры.Добавить(НСтр("ru='Не определена'"));

	Попытка
		Версия = ОбъектДрайвера.ПолучитьНомерВерсии();
		ВыходныеПараметры[1] = Версия;
		// Дополнительные параметры
		ВыходныеПараметры.Добавить(ПолучитьВерсиюВнешнейКомпоненты());
		ВыходныеПараметры.Добавить(ВерсияСтрокаВЧисло(Версия));
		ВыходныеПараметры.Добавить(ВерсияСтрокаВЧисло(ПолучитьВерсиюВнешнейКомпоненты()));
	Исключение
	КонецПопытки;

	Возврат Результат;

КонецФункции

// функция преобразует строковое представление версии (с тремя точками) к числовому (пригодному для сравнения)
Функция ВерсияСтрокаВЧисло(ВерсияСтрока) 
Попытка
	Версия = ВерсияСтрока;	
	Результат = Число(Лев(Версия, Найти(Версия, ".")-1));
	Версия = Сред(Версия, Найти(Версия, ".") + 1);
	Результат = Результат*100 + Число(Лев(Версия, Найти(Версия, ".")-1));
	Версия = Сред(Версия, Найти(Версия, ".") + 1);
	Результат = Результат*100 + Число(Лев(Версия, Найти(Версия, ".")-1));
	Версия = Сред(Версия, Найти(Версия, ".") + 1);
	Результат = Результат*100 + Число(Версия);
Исключение
	Возврат 0;
КонецПопытки;
	Возврат Результат;	
КонецФункции

// функция преобразует суффикс/префикс в символьную строку для передачи в драйвер
// из формата #13#10 
// из формата 13(Строка) или  10(Число)
// из формата (13)CR
Функция СПВСимволы(СуффиксНомер, ПоУмолчанию) 
	Номер  = СуффиксНомер;
	СуффиксДрайвера = "";	
	Позиция = Найти(Номер, "#");
	
	Если Позиция = 0 Тогда
		Попытка
			ПозицияЛев = Найти(Номер, "(");
			ПозицияПрав = Найти(Номер, ")");
			Если ПозицияПрав >= ПозицияЛев Тогда
				Номер = Сред(Номер, ПозицияЛев+1, ПозицияПрав-ПозицияЛев-1);
				Номер = Число(Номер);
			Иначе
				Номер = ПоУмолчанию;	
			КонецЕсли;			
		Исключение
			Номер = ПоУмолчанию;
		КонецПопытки;
		
		Возврат Символ(Номер);
	КонецЕсли;
	
	Номер = Сред(Номер, Найти(Номер, "#") + 1);
	
	Попытка
	    Пока Истина Цикл
			ВремНомер = Номер;
			Позиция = Найти(Номер, "#");
			Если Позиция > 0 Тогда
				ВремНомер = Лев(Номер, Позиция-1);
			КонецЕсли;	
			СуффиксДрайвера = СуффиксДрайвера + Символ(Число(ВремНомер));
			Если Позиция = 0 Тогда 
				Прервать 
			КонецЕсли;
			Номер = Сред(Номер, Найти(Номер, "#") + 1);
		КонецЦикла;
	Исключение
	КонецПопытки;
	
	Возврат СуффиксДрайвера;
	
КонецФункции