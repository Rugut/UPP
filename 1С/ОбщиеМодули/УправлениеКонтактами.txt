// Функция возвращает номер строки в поле табличного документа
//  для вывода События по его дате (начала или конца)
//
// Параметры
//  Часы - Строка, часы даты
//  Минуты - Строка, минуты даты
//  Дата - Дата, текущее значение даты для определения
//  Начало - Булево, признак начала или окончания периода
//  ДатаСравнения - Дата, дата с которой сравнивается значение исходной даты
//
// Возвращаемое значение:
//   Число - номер строки в поле табличного документа
//
Функция ВозвратитьНомерСтроки(Часы, Минуты, Дата, Начало, ДатаСравнения) Экспорт
	
	Если ПустаяСтрока(Часы) Тогда
		Часы = 0;
	Иначе
		Часы = Число(Часы);
	КонецЕсли; 
	
	Если ПустаяСтрока(Минуты) Тогда
		Минуты = 0;
	Иначе
		Минуты = Число(Минуты);
	КонецЕсли; 
	
	Если Начало Тогда
		Если Дата < НачалоДня(ДатаСравнения) Тогда
			Возврат 1;
		Иначе
			Если Минуты < 30 Тогда
				Если Минуты = 0 Тогда
					Если Часы = 0 Тогда
						Возврат 1;
					Иначе
						Возврат (Часы * 2 + 1);
					КонецЕсли; 
				Иначе
					Возврат (Часы * 2 + 1);
				КонецЕсли; 
			Иначе
				Если Часы = 23 Тогда
					Возврат 48;
				Иначе
					Возврат (Часы * 2 + 2);
				КонецЕсли; 
			КонецЕсли;
		КонецЕсли; 
	Иначе
		Если Дата > КонецДня(ДатаСравнения) Тогда
			Возврат 48;
		Иначе
			Если Минуты = 0 Тогда
				Если Часы = 0 Тогда
					Возврат 1;
				Иначе
					Возврат (Часы * 2);
				КонецЕсли; 
			ИначеЕсли Минуты <= 30 Тогда
				Возврат (Часы * 2 + 1);
			Иначе
				Если Часы = 23 Тогда
					Возврат 48;
				Иначе
					Возврат (Часы * 2 + 2);
				КонецЕсли; 
			КонецЕсли;
		КонецЕсли; 
	КонецЕсли;
	
КонецФункции

// Процедура выполняется при проведении или отмене проведения документа Событие
//  и переписывает записи регитра сведений СобытияКалендаряПользователя.
Процедура РаспределитьСобытияДня(ДатаРаспределения,Пользователь = Неопределено) Экспорт
	
	Если Пользователь = Неопределено Тогда
		Пользователь = Справочники.Пользователи.ПустаяСсылка();
	КонецЕсли;
	
	Запрос = Новый Запрос;
	
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	СобытияКалендаряПользователяОбобщенные.Событие КАК Событие,
	|	СобытияКалендаряПользователяОбобщенные.НомерНачальнойСтроки КАК НомерНачальнойСтроки,
	|	СобытияКалендаряПользователяОбобщенные.НомерКонечнойСтроки КАК НомерКонечнойСтроки
	|ИЗ
	|	РегистрСведений.СобытияКалендаряПользователяОбобщенные КАК СобытияКалендаряПользователяОбобщенные
	|ГДЕ
	|	СобытияКалендаряПользователяОбобщенные.Дата = &ВыбДата
	|	И
	|	СобытияКалендаряПользователяОбобщенные.Пользователь = &ВыбПользователь
	|";
	
	Запрос.УстановитьПараметр("ВыбДата", ДатаРаспределения);
	Запрос.УстановитьПараметр("ВыбПользователь", Пользователь);
	
	ТаблицаЗапроса = Запрос.Выполнить().Выгрузить();
	
	НомерСтрокиНачало = 0;
	НомерСтрокиКонец  = 0;
	
	Если ТаблицаЗапроса.Количество() > 0 Тогда
		
		КопияТаблицыЗапроса = ТаблицаЗапроса.Скопировать();
		
		КопияТаблицыЗапроса.Сортировать("НомерНачальнойСтроки ВОЗР");
		НомерСтрокиНачало = КопияТаблицыЗапроса[0].НомерНачальнойСтроки;
		
		КопияТаблицыЗапроса.Сортировать("НомерКонечнойСтроки УБЫВ");
		НомерСтрокиКонец = КопияТаблицыЗапроса[0].НомерКонечнойСтроки;
		
	КонецЕсли; 
	
	СписокЗначенийВремен = Новый СписокЗначений;
	
	Если НомерСтрокиНачало <> 0 И НомерСтрокиКонец <> 0 Тогда
		
		Для а=1 По 48 Цикл
			НовыйСписок = Новый СписокЗначений;
			СписокЗначенийВремен.Добавить(НовыйСписок);
		КонецЦикла;
		
		Для а=НомерСтрокиНачало По НомерСтрокиКонец Цикл
			
			НовыйСписок = СписокЗначенийВремен[а-1].Значение;
			
			ИндексСтрокиТаблицы = 0;
			Пока Истина Цикл
				
				Если ИндексСтрокиТаблицы > ТаблицаЗапроса.Количество() - 1 Тогда
					Прервать;
				КонецЕсли; 
				
				СтрокаТаблицы = ТаблицаЗапроса[ИндексСтрокиТаблицы];
				
				Если СтрокаТаблицы.НомерНачальнойСтроки <= а И СтрокаТаблицы.НомерКонечнойСтроки >= а Тогда
					
					ЕстьСвободноеМесто = Ложь;
					
					Для каждого ЭлементНовогоСписка Из НовыйСписок Цикл
						
						Если ЭлементНовогоСписка.Значение = Неопределено Тогда
							
							ЭлементНовогоСписка.Значение = СтрокаТаблицы.Событие;
							
							ЕстьСвободноеМесто = Истина;
							ИндексСписка = НовыйСписок.Индекс(ЭлементНовогоСписка);
							Прервать;
							
						КонецЕсли;
						
					КонецЦикла;
					
					Если НЕ ЕстьСвободноеМесто Тогда
						
						Если НовыйСписок.Количество() >= 18 Тогда
							Прервать;
						КонецЕсли; 
						
						ВновьВведенныйЭлемент = НовыйСписок.Добавить(СтрокаТаблицы.Событие);
						ИндексСписка = НовыйСписок.Индекс(ВновьВведенныйЭлемент);
						
					КонецЕсли;
					
					Если а <= СписокЗначенийВремен.Количество() Тогда
						
						ИндексТекущегоСписка = а - 1 + 1;
						Для б = а По СтрокаТаблицы.НомерКонечнойСтроки - 1 Цикл
							
							ТекущийСписок = СписокЗначенийВремен[б].Значение;
							
							Если ТекущийСписок.Количество() < (ИндексСписка + 1) Тогда
								
								Для с = (ТекущийСписок.Количество() + 1) По (ИндексСписка + 1) Цикл
									
									ТекущийСписок.Добавить(Неопределено);
									
								КонецЦикла;
								
							КонецЕсли;
							
							ТекущийСписок[ИндексСписка].Значение = СтрокаТаблицы.Событие;
							
						КонецЦикла; 
						
					КонецЕсли; 
					
					ТаблицаЗапроса.Удалить(СтрокаТаблицы);
					Продолжить;
					
				КонецЕсли;
				
				ИндексСтрокиТаблицы = ИндексСтрокиТаблицы + 1;
				
			КонецЦикла; 
			
		КонецЦикла;
		
	КонецЕсли; 
	
	// Очистим значения предидущего распределния
	НаборЗаписейРегистра = РегистрыСведений.СобытияКалендаряПользователя.СоздатьНаборЗаписей();
	НаборЗаписейРегистра.Отбор.Пользователь.Значение      = Пользователь;
	НаборЗаписейРегистра.Отбор.Пользователь.Использование = Истина;
	НаборЗаписейРегистра.Отбор.Дата.Значение              = ДатаРаспределения;
	НаборЗаписейРегистра.Отбор.Дата.Использование         = Истина;
	НаборЗаписейРегистра.Очистить();
	
	// Проведем распределение заново
	Для каждого СтрокаВремени Из СписокЗначенийВремен Цикл
		
		СписокТекущегоВремени = СтрокаВремени.Значение;
		
		Если ТипЗнч(СписокТекущегоВремени) = Тип("СписокЗначений") И СписокТекущегоВремени.Количество() > 0 Тогда
			
			Для каждого ЭлементДокумента Из СписокТекущегоВремени Цикл
				
				НоваяЗаписьРегистра = НаборЗаписейРегистра.Добавить();
				НоваяЗаписьРегистра.Событие = ЭлементДокумента.Значение;
				НоваяЗаписьРегистра.НомерСтрокиТаблицы = СписокЗначенийВремен.Индекс(СтрокаВремени) + 1;
				НоваяЗаписьРегистра.Дата = ДатаРаспределения;
				НоваяЗаписьРегистра.Пользователь = Пользователь;
				НоваяЗаписьРегистра.ПорядковыйНомерВДне = СписокТекущегоВремени.Индекс(ЭлементДокумента) + 1;
				
			КонецЦикла; 
			
		КонецЕсли; 
		
	КонецЦикла;
	
	НаборЗаписейРегистра.Записать();
	
	Если ТипЗнч(КопияТаблицыЗапроса) = Тип("ТаблицаЗначений") Тогда
		
		СписокДокументов = Новый СписокЗначений;
		СписокДокументов.ЗагрузитьЗначения(КопияТаблицыЗапроса.ВыгрузитьКолонку("Событие"));
		
		Для каждого ДокументСписка Из СписокДокументов Цикл
			
			НаборЗаписейРегистра = РегистрыСведений.СобытияКалендаряПользователяОбобщенные.СоздатьНаборЗаписей();
			НаборЗаписейРегистра.Отбор.Пользователь.Значение      = Пользователь;
			НаборЗаписейРегистра.Отбор.Пользователь.Использование = Истина;
			НаборЗаписейРегистра.Отбор.Дата.Значение              = ДатаРаспределения;
			НаборЗаписейРегистра.Отбор.Дата.Использование         = Истина;
			НаборЗаписейРегистра.Отбор.Событие.Значение           = ДокументСписка.Значение;
			НаборЗаписейРегистра.Отбор.Событие.Использование      = Истина;
			НаборЗаписейРегистра.Прочитать();
			
			Для каждого ЗаписьРегистра Из НаборЗаписейРегистра Цикл
				КоличествоДокументов = 0;
				Для б = ЗаписьРегистра.НомерНачальнойСтроки - 1 По ЗаписьРегистра.НомерКонечнойСтроки - 1 Цикл
					ТекСписок = СписокЗначенийВремен[б].Значение;
					Если ТекСписок.Количество() > КоличествоДокументов Тогда
						КоличествоДокументов = ТекСписок.Количество();
					КонецЕсли; 
				КонецЦикла;
				ЗаписьРегистра.КоличествоОдновременныхДокументов = КоличествоДокументов;
				
			КонецЦикла;
			
			НаборЗаписейРегистра.Записать(Истина);
			
		КонецЦикла; 
		
	КонецЕсли; 
	
КонецПроцедуры

// Процедура выполняет запись в регистр сведений
//
Процедура ВвестиЗаписьВРегистр(Дата, НомерНачальнойСтроки, НомерКонечнойСтроки, НаборЗаписейРегистра, Ссылка, ДатаНачала, ДатаОкончания)
	
	НоваяЗапись = НаборЗаписейРегистра.Добавить();
	
	НоваяЗапись.Событие              = Ссылка;
	НоваяЗапись.Дата                 = Дата;
	НоваяЗапись.НомерНачальнойСтроки = НомерНачальнойСтроки;
	НоваяЗапись.НомерКонечнойСтроки  = НомерКонечнойСтроки;
	НоваяЗапись.Пользователь         = Справочники.Пользователи.ПустаяСсылка();
	
	НоваяЗапись.ДатаНачалаСобытия    = ДатаНачала;
	НоваяЗапись.ДатаКонцаСобытия     = ДатаОкончания;
	
КонецПроцедуры

// Процедура отражает занятость помещений в регистрах сведений для календаря пользователя
//
Процедура ОтразитьЗанятостьПомещений(Ссылка,ДатаНачала,ДатаОкончания,мСтараяДатаНачалаСобытия,мСтараяДатаОкончанияСобытия) Экспорт
	
	НаборЗаписей = РегистрыСведений.СобытияКалендаряПользователяОбобщенные.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Событие.Значение = Ссылка;
	НаборЗаписей.Отбор.Событие.Использование = Истина;
	НаборЗаписей.Отбор.Пользователь.Значение = Справочники.Пользователи.ПустаяСсылка();
	НаборЗаписей.Отбор.Пользователь.Использование = Истина;
	НаборЗаписей.Очистить();
	
	НачальнаяСтрока = ВозвратитьНомерСтроки(Формат(ДатаНачала,"ДФ=Ч"), Формат(ДатаНачала,"ДФ=м"), ДатаНачала, Истина, ДатаНачала);
	Если Формат(ДатаНачала,"ДФ=Ч") = Формат(ДатаОкончания,"ДФ=Ч") И Формат(ДатаНачала,"ДФ=м") = Формат(ДатаОкончания,"ДФ=м") Тогда
		КонечнаяСтрока  = НачальнаяСтрока;
	Иначе
		КонечнаяСтрока  = ВозвратитьНомерСтроки(Формат(ДатаОкончания,"ДФ=Ч"), Формат(ДатаОкончания,"ДФ=м"), ДатаОкончания, Ложь, ДатаОкончания);
	КонецЕсли; 
	
	Если НачалоДня(ДатаНачала) <> НачалоДня(ДатаОкончания) Тогда
		
		КоличествоИнтерваловМеждуДнями = Цел((НачалоДня(ДатаОкончания) - КонецДня(ДатаНачала) + 1)/(60*60*24));
		
		Для а = 1 По КоличествоИнтерваловМеждуДнями Цикл
			
			ДатаИнтервала = НачалоДня(ДатаНачала) + (60*60*24)*а;
			
			ВвестиЗаписьВРегистр(ДатаИнтервала, 1, 48, НаборЗаписей, Ссылка, ДатаНачала, ДатаОкончания);
			
		КонецЦикла;
		
		ВвестиЗаписьВРегистр(НачалоДня(ДатаНачала), НачальнаяСтрока, 48, НаборЗаписей, Ссылка, ДатаНачала, ДатаОкончания);
		ВвестиЗаписьВРегистр(НачалоДня(ДатаОкончания), 1, КонечнаяСтрока, НаборЗаписей, Ссылка, ДатаНачала, ДатаОкончания);
		
	Иначе
		
		ВвестиЗаписьВРегистр(НачалоДня(ДатаНачала), НачальнаяСтрока, КонечнаяСтрока, НаборЗаписей, Ссылка, ДатаНачала, ДатаОкончания);
		
	КонецЕсли;
	
	Если НаборЗаписей.Количество() > 0 Тогда
		НаборЗаписей.Записать();
	КонецЕсли;
	
	СписокПерераспределенныхДат = Новый СписокЗначений;
	
	Если НачалоДня(мСтараяДатаНачалаСобытия) <> НачалоДня(ДатаНачала) ИЛИ НачалоДня(мСтараяДатаОкончанияСобытия) <> НачалоДня(ДатаОкончания) Тогда
		КоличествоИнтерваловМеждуДнями = Цел((КонецДня(мСтараяДатаОкончанияСобытия) + 1 - НачалоДня(мСтараяДатаНачалаСобытия))/(60*60*24));
		Для а = 1 По КоличествоИнтерваловМеждуДнями Цикл
			
			ДатаИнтервала = НачалоДня(НачалоДня(мСтараяДатаНачалаСобытия) + (60*60*24)*а - 1);
			СписокПерераспределенныхДат.Добавить(ДатаИнтервала);
			
			РаспределитьСобытияДня(ДатаИнтервала);
			
		КонецЦикла; 
	КонецЕсли; 
	
	Если НачалоДня(ДатаНачала) <> НачалоДня(ДатаОкончания) Тогда
		
		КоличествоИнтерваловМеждуДнями = Цел((НачалоДня(ДатаОкончания) - КонецДня(ДатаНачала) + 1)/(60*60*24));
		
		Для а = 1 По КоличествоИнтерваловМеждуДнями Цикл
			
			ДатаИнтервала = НачалоДня(ДатаНачала) + (60*60*24)*а;
			Если СписокПерераспределенныхДат.НайтиПоЗначению(ДатаИнтервала) = Неопределено Тогда
				
				РаспределитьСобытияДня(ДатаИнтервала);
				
			КонецЕсли;
			
		КонецЦикла;
		
		Если СписокПерераспределенныхДат.НайтиПоЗначению(НачалоДня(ДатаНачала)) = Неопределено Тогда
			
			РаспределитьСобытияДня(НачалоДня(ДатаНачала));
			
			
		КонецЕсли; 
		
		Если СписокПерераспределенныхДат.НайтиПоЗначению(НачалоДня(ДатаОкончания)) = Неопределено Тогда
			
			РаспределитьСобытияДня(НачалоДня(ДатаОкончания));
			
			
		КонецЕсли; 
		
	Иначе
		
		Если СписокПерераспределенныхДат.НайтиПоЗначению(НачалоДня(ДатаНачала)) = Неопределено Тогда
			
			РаспределитьСобытияДня(НачалоДня(ДатаНачала));
			
		КонецЕсли; 
		
	КонецЕсли;

КонецПроцедуры

// Процедура отменяет занятость помещений в регистрах сведений для календаря пользователя
//
Процедура ОтменитьЗанятостьПомещений(Ссылка,ДатаНачала,ДатаОкончания,мСписокПользователей,мСписокСтарыхПользователей) Экспорт
	
	НаборЗаписей = РегистрыСведений.СобытияКалендаряПользователяОбобщенные.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Событие.Значение = Ссылка;
	НаборЗаписей.Отбор.Событие.Использование = Истина;
	НаборЗаписей.Очистить();
	НаборЗаписей.Записать();

	Если НачалоДня(ДатаНачала) <> НачалоДня(ДатаОкончания) Тогда
		
		КоличествоИнтерваловМеждуДнями = Цел((НачалоДня(ДатаОкончания) - КонецДня(ДатаНачала) + 1)/(60*60*24));
		
		Для а = 1 По КоличествоИнтерваловМеждуДнями Цикл
			ДатаИнтервала = НачалоДня(ДатаНачала) + (60*60*24)*а;
			
			Для каждого ЭлементСпискаПользователей Из мСписокСтарыхПользователей Цикл
				СтарыйПользователь = ЭлементСпискаПользователей.Значение;
				Если мСписокПользователей.НайтиПоЗначению(СтарыйПользователь) = Неопределено Тогда
					РаспределитьСобытияДня(ДатаИнтервала, СтарыйПользователь);
				КонецЕсли;
			КонецЦикла;
		
			Для каждого ЭлементСпискаПользователей Из мСписокПользователей Цикл
				РаспределитьСобытияДня(ДатаИнтервала, ЭлементСпискаПользователей.Значение);
			КонецЦикла;
			
		КонецЦикла;
			
		Для каждого ЭлементСпискаПользователей Из мСписокСтарыхПользователей Цикл
			СтарыйПользователь = ЭлементСпискаПользователей.Значение;
			Если мСписокПользователей.НайтиПоЗначению(СтарыйПользователь) = Неопределено Тогда
				РаспределитьСобытияДня(НачалоДня(ДатаНачала), СтарыйПользователь);
				РаспределитьСобытияДня(НачалоДня(ДатаОкончания), СтарыйПользователь);
			КонецЕсли;
		КонецЦикла;
	
		Для каждого ЭлементСпискаПользователей Из мСписокПользователей Цикл
			РаспределитьСобытияДня(НачалоДня(ДатаНачала), ЭлементСпискаПользователей.Значение);
			РаспределитьСобытияДня(НачалоДня(ДатаОкончания), ЭлементСпискаПользователей.Значение);
		КонецЦикла;
		
	Иначе
			
		Для каждого ЭлементСпискаПользователей Из мСписокСтарыхПользователей Цикл
			СтарыйПользователь = ЭлементСпискаПользователей.Значение;
			Если мСписокПользователей.НайтиПоЗначению(СтарыйПользователь) = Неопределено Тогда
				РаспределитьСобытияДня(НачалоДня(ДатаНачала), СтарыйПользователь);
			КонецЕсли;
		КонецЦикла;
	
		Для каждого ЭлементСпискаПользователей Из мСписокПользователей Цикл
			РаспределитьСобытияДня(НачалоДня(ДатаНачала), ЭлементСпискаПользователей.Значение);
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

// Функция определяет начало и окончание рабочего дня пользователя, по его графику работы
//
// Параметры
//  Пользователь - СправочникСсылка.Пользователи, пользователь по которому определяются
//                 <продолжение описания параметра>
//  <Параметр2>  – <Тип.Вид> – <описание параметра>
//                 <продолжение описания параметра>
//
// Возвращаемое значение:
//   <Тип.Вид>   – <описание возвращаемого значения>
//
Функция ОпределитьНачалоИОкончаниеРабочегоДняПользователя(Пользователь, РабочаяДата) Экспорт

	ДатаНачала		= '00010101000000';
	ДатаОкончания	= '00010101235959';
	НайденныйГрафик	= Неопределено;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Пользователь",	Пользователь);
	Запрос.УстановитьПараметр("ДатаСреза",		РабочаяДата);
	
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ВложенныйЗапрос.ГрафикРаботы
	|ИЗ
	|	(ВЫБРАТЬ
	|		РаботникиСрезПоследних.ГрафикРаботы КАК ГрафикРаботы
	|	ИЗ
	|		РегистрСведений.Работники.СрезПоследних(
	|				&ДатаСреза,
	|				Физлицо В
	|					(ВЫБРАТЬ
	|						Пользователи.ФизЛицо
	|					ИЗ
	|						Справочник.Пользователи КАК Пользователи
	|					ГДЕ
	|						Пользователи.Ссылка = &Пользователь)) КАК РаботникиСрезПоследних
	|	
	|	ОБЪЕДИНИТЬ
	|	
	|	ВЫБРАТЬ
	|		РаботникиОрганизацийСрезПоследних.ГрафикРаботы
	|	ИЗ
	|		РегистрСведений.РаботникиОрганизаций.СрезПоследних(
	|				&ДатаСреза,
	|				Сотрудник.Физлицо В
	|					(ВЫБРАТЬ
	|						Пользователи.ФизЛицо
	|					ИЗ
	|						Справочник.Пользователи КАК Пользователи
	|					ГДЕ
	|						Пользователи.Ссылка = &Пользователь)) КАК РаботникиОрганизацийСрезПоследних) КАК ВложенныйЗапрос";
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		НайденныйГрафик = Выборка.ГрафикРаботы;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(НайденныйГрафик) Тогда
		НайденныйГрафик = УправлениеПользователями.ПолучитьЗначениеПоУмолчанию(Пользователь, "ГрафикРаботы");
	КонецЕсли;
		
	Если ЗначениеЗаполнено(НайденныйГрафик) И ТипЗнч(НайденныйГрафик) = Тип("СправочникСсылка.ГрафикиРаботы") Тогда
		
		Запрос.УстановитьПараметр("ГрафикРаботы",	НайденныйГрафик);
		
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	МИНИМУМ(ВремяРаботы.ВремяНачала) КАК ВремяНачала,
		|	МАКСИМУМ(ВремяРаботы.ВремяОкончания) КАК ВремяОкончания
		|ИЗ
		|	(ВЫБРАТЬ
		|		ГрафикиРаботыПериодыСмены.ВремяНачала КАК ВремяНачала,
		|		ГрафикиРаботыПериодыСмены.ВремяОкончания КАК ВремяОкончания
		|	ИЗ
		|		Справочник.ГрафикиРаботы.ПериодыСмены КАК ГрафикиРаботыПериодыСмены
		|	
		|	ГДЕ
		|		ГрафикиРаботыПериодыСмены.Ссылка = &ГрафикРаботы
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		СменыПериодыСмены.ВремяНачала,
		|		СменыПериодыСмены.ВремяОкончания
		|	ИЗ
		|		Справочник.ГрафикиРаботы.Смены КАК ГрафикиРаботыСмены
		|			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Смены.ПериодыСмены КАК СменыПериодыСмены
		|			ПО ГрафикиРаботыСмены.Смена = СменыПериодыСмены.Ссылка
		|	
		|	ГДЕ
		|		ГрафикиРаботыСмены.Ссылка = &ГрафикРаботы) КАК ВремяРаботы";
		
			
		Выборка = Запрос.Выполнить().Выбрать();
		
		Если Выборка.Следующий() Тогда
			ДатаНачала = Выборка.ВремяНачала;
			Если ЗначениеЗаполнено(Выборка.ВремяОкончания) Тогда
				ДатаОкончания = Выборка.ВремяОкончания;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли; 
	
	СтруктураВозврата = Новый Структура("ДатаНачала, ДатаОкончания", ДатаНачала, ДатаОкончания);
	
	Возврат СтруктураВозврата;
	
КонецФункции // ОпределитьНачалоИОкончаниеРабочегоДняПользователя()

// Процедура показывает формы Напоминаний.
// 
//  Параметры
//   ТаблицаНапоминаний - ТаблицаЗначений, с Напоминаниями
//
Процедура ОбработкаСпискаНапоминаний(ТаблицаНапоминаний)

	ФормаНапоминания = ПолучитьОбщуюФорму("ФормаНапоминания",,"Уникум");
	ФормаНапоминания.ОбновитьТаблицуНапоминаний(ТаблицаНапоминаний);

	Если ФормаНапоминания.Открыта() Тогда
		ФормаНапоминания.Активизировать();
	Иначе
		ФормаНапоминания.Открыть();
	КонецЕсли;

КонецПроцедуры // ОбработкаСпискаНапоминаний()

// Процедура проверяет Напоминания из регистра.
// 
//  Параметры
//   ТаблицаНапоминаний - ТаблицаЗначений, с Напоминаниями
//
Процедура ПроверитьНапоминанияПользователя(ВыбПользователь, ПроверятьДеньРождения = Ложь) Экспорт
	
 	РезультирующаяТаблица = Новый ТаблицаЗначений;
	РезультирующаяТаблица.Колонки.Добавить("ДокументНапоминания");
	РезультирующаяТаблица.Колонки.Добавить("Пользователь");
	РезультирующаяТаблица.Колонки.Добавить("ДатаНапоминания");
	РезультирующаяТаблица.Колонки.Добавить("Тема");
	РезультирующаяТаблица.Колонки.Добавить("Контрагент");
	РезультирующаяТаблица.Колонки.Добавить("КонтактноеЛицо");
	РезультирующаяТаблица.Колонки.Добавить("ТекстНапоминания");
	РезультирующаяТаблица.Колонки.Добавить("ФлагДеньРождения");
	
	Если ПроверятьДеньРождения Тогда

		Запрос = Новый Запрос;

		Запрос.Текст = "
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	КонтактныеЛицаКонтрагентов.КонтактноеЛицо              КАК КонтактноеЛицо,
		|	КонтактныеЛицаКонтрагентов.Владелец                    КАК Контрагент,
		|	КонтактныеЛицаКонтрагентов.РольКонтактногоЛица         КАК РольКонтактногоЛица,
		|	КонтактныеЛицаКонтрагентов.Должность                   КАК ДолжностьКонтактногоЛица,
		|	КонтактныеЛицаКонтрагентов.КонтактноеЛицо.ДатаРождения КАК ДатаРождения
		|ИЗ
		|	Справочник.КонтактныеЛицаКонтрагентов КАК КонтактныеЛицаКонтрагентов
        |
		|ЛЕВОЕ СОЕДИНЕНИЕ
		|	Справочник.Контрагенты.МенеджерыПокупателя КАК Менеджеры
		|ПО
		|	Менеджеры.Ссылка = КонтактныеЛицаКонтрагентов.Владелец
		|
		|ГДЕ
		|	(КонтактныеЛицаКонтрагентов.Владелец.ОсновнойМенеджерПокупателя = &ТекущийПользователь
		|	ИЛИ Менеджеры.МенеджерПокупателя = &ТекущийПользователь)
		|	И КонтактныеЛицаКонтрагентов.Владелец ССЫЛКА Справочник.Контрагенты
		|	И КонтактныеЛицаКонтрагентов.Владелец <> &ПустойКонтрагент
		|	И КонтактныеЛицаКонтрагентов.КонтактноеЛицо.НапоминатьОДнеРождения = Истина
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ 
		|	ЛичныеКонтакты.Ссылка 			КАК КонтактноеЛицо,
		|	NULL 							КАК Контрагент,
		|	NULL 							КАК РольКонтактногоЛица,
		|	NULL        					КАК ДолжностьКонтактногоЛица,
		|	ЛичныеКонтакты.ДатаРождения  	КАК ДатаРождения
		|ИЗ
		|	Справочник.ЛичныеКонтакты КАК ЛичныеКонтакты
		|
		|ГДЕ
		|	ЛичныеКонтакты.ПользовательДляОграниченияПравДоступа = &ТекущийПользователь
		|	И ЛичныеКонтакты.НапоминатьОДнеРождения = Истина
		|
		//|ОБЪЕДИНИТЬ ВСЕ
		//|
		//|ВЫБРАТЬ
		//|	КонтактныеЛица.Ссылка                   КАК КонтактноеЛицо,
		//|	КонтактныеЛицаКонтрагентов.Владелец     КАК Контрагент,
		//|	КонтактныеЛицаКонтрагентов.РольКонтактногоЛица КАК РольКонтактногоЛица,
		//|	КонтактныеЛицаКонтрагентов.Должность    КАК ДолжностьКонтактногоЛица,
		//|	КонтактныеЛица.ДатаРождения             КАК ДатаРождения
		//|ИЗ
		//|	Справочник.КонтактныеЛица КАК КонтактныеЛица
		//|ЛЕВОЕ СОЕДИНЕНИЕ
		//|	Справочник.КонтактныеЛицаКонтрагентов КАК КонтактныеЛицаКонтрагентов
		//|	ПО 
		//|	КонтактныеЛицаКонтрагентов.КонтактноеЛицо = КонтактныеЛица.Ссылка
		//|
		//|ГДЕ
		//|	КонтактныеЛицаКонтрагентов.Владелец = &ТекущийПользователь
		//|	И (КонтактныеЛицаКонтрагентов.Владелец ЕСТЬ NULL
		//|	ИЛИ КонтактныеЛицаКонтрагентов.Владелец = &ПустойКонтрагент)
		//|	И КонтактныеЛица.НапоминатьОДнеРождения = Истина
		|";

		Запрос.УстановитьПараметр("ТекущийПользователь"      , ВыбПользователь);
        Запрос.УстановитьПараметр("ПустойКонтрагент"      	 , Справочники.Контрагенты.ПустаяСсылка());
		
		ТаблицаЗапроса = Запрос.Выполнить().Выгрузить();

		ЗапросСобытий = Новый Запрос;

		ЗапросСобытий.Текст = "
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	Событие.КонтактноеЛицо КАК КонтактноеЛицо
		|ИЗ
		|	Документ.Событие КАК Событие
		|	
		|ГДЕ
		|	Событие.ОписаниеСобытия ПОДОБНО &СтрокаДеньРождения
		|	И Событие.Дата >= &ДатаНачСобытия
		|	И Событие.Дата <= &ДатаКонСобытия
		|	И Событие.Проведен = ИСТИНА
		|	И Событие.СостояниеСобытия = &СостояниеСобытия
		|";

		ЗапросСобытий.УстановитьПараметр("ДатаНачСобытия"    , НачалоДня(ТекущаяДата() - 30*24*60*60));
		ЗапросСобытий.УстановитьПараметр("ДатаКонСобытия"    , КонецДня(ТекущаяДата() + 30*24*60*60));
		ЗапросСобытий.УстановитьПараметр("СтрокаДеньРождения", "%День рождения%");
		ЗапросСобытий.УстановитьПараметр("СостояниеСобытия"  , Перечисления.СостоянияСобытий.Запланировано);

		СобытияДняРождения = ЗапросСобытий.Выполнить().Выгрузить();

		Для каждого СтрокаТаблицы Из ТаблицаЗапроса Цикл

			Если СтрокаТаблицы.КонтактноеЛицо.Пустая() Тогда
				Продолжить;
			КонецЕсли;
			
			ДеньРожденияТекущегоГода  = ДобавитьМесяц((ДобавитьМесяц(НачалоДня(СтрокаТаблицы.КонтактноеЛицо.ДатаРождения), -(Год(СтрокаТаблицы.КонтактноеЛицо.ДатаРождения) - 1) * 12)) , ((Год(ТекущаяДата()) - 1) * 12));
			ДеньРожденияБудующегоГода = ДобавитьМесяц((ДобавитьМесяц(НачалоДня(СтрокаТаблицы.КонтактноеЛицо.ДатаРождения), -(Год(СтрокаТаблицы.КонтактноеЛицо.ДатаРождения) - 1) * 12)) , (Год(ТекущаяДата()) * 12));
			
			Если СтрокаТаблицы.КонтактноеЛицо.КоличествоДнейДоНапоминания = 0 Тогда
				Если ДеньРожденияТекущегоГода - НачалоДня(ТекущаяДата()) <> 0 Тогда
					Продолжить;
				КонецЕсли; 
			Иначе
				Если НЕ (((ДеньРожденияТекущегоГода - НачалоДня(ТекущаяДата())) <= ((СтрокаТаблицы.КонтактноеЛицо.КоличествоДнейДоНапоминания)*24*60*60) И (ДеньРожденияТекущегоГода - НачалоДня(ТекущаяДата())) > 0)
				 ИЛИ ((ДеньРожденияБудующегоГода - НачалоДня(ТекущаяДата())) <= ((СтрокаТаблицы.КонтактноеЛицо.КоличествоДнейДоНапоминания)*24*60*60) И (ДеньРожденияБудующегоГода - НачалоДня(ТекущаяДата()) > 0))) Тогда
					Продолжить;
				КонецЕсли; 
			КонецЕсли; 

			Если СобытияДняРождения.Найти(СтрокаТаблицы.КонтактноеЛицо, "КонтактноеЛицо") <> Неопределено Тогда
				Продолжить;
			КонецЕсли; 

			СтрокаРезТаблицы = РезультирующаяТаблица.Добавить();
			СтрокаРезТаблицы.ДокументНапоминания = ТекущаяДата();
			СтрокаРезТаблицы.Пользователь = ВыбПользователь;
			СтрокаРезТаблицы.ДатаНапоминания = СтрокаТаблицы.КонтактноеЛицо.ДатаРождения;
			СтрокаРезТаблицы.Тема = "День рождения";
			СтрокаРезТаблицы.Контрагент = СтрокаТаблицы.Контрагент;
			СтрокаРезТаблицы.КонтактноеЛицо = СтрокаТаблицы.КонтактноеЛицо;
			СтрокаРезТаблицы.ТекстНапоминания = "Поздравить с днем рождения" + Символы.ПС;
			Если ЗначениеЗаполнено(СтрокаТаблицы.Контрагент) Тогда
				СтрокаРезТаблицы.ТекстНапоминания = СтрокаРезТаблицы.ТекстНапоминания + "Роль: " + СтрокаТаблицы.РольКонтактногоЛица + Символы.ПС
				+ "Должность: " + СтрокаТаблицы.ДолжностьКонтактногоЛица + Символы.ПС
			КонецЕсли; 
			СтрокаРезТаблицы.ТекстНапоминания = СтрокаРезТаблицы.ТекстНапоминания + "Дата рождения: " + Формат(СтрокаТаблицы.ДатаРождения, "ДФ=dd.MM.yyyy") + Символы.ПС
																				  + "Исполняется лет: " + СокрЛП(Строка(Число(Формат(ТекущаяДата(),"ДФ=yyyy")) - Число(Формат(СтрокаТаблицы.КонтактноеЛицо.ДатаРождения,"ДФ=yyyy"))));
			СтрокаРезТаблицы.ФлагДеньРождения = Истина;
			
		КонецЦикла;
	
	КонецЕсли;
	
	Запрос = Новый Запрос;

	ДатаСреза = ТекущаяДата() + Константы.ИнтервалПроверкиНапоминанийВСекундах.Получить();
	
	Запрос.УстановитьПараметр("ДатаСреза"           , ДатаСреза);
	Запрос.УстановитьПараметр("ВыбПользователь"     , ВыбПользователь);
	Запрос.УстановитьПараметр("СостояниеСобытия"    , Перечисления.СостоянияСобытий.Запланировано);
	Запрос.УстановитьПараметр("ПустойКонтрагент"    , Справочники.Контрагенты.ПустаяСсылка());
	Запрос.УстановитьПараметр("ПустоеКонтактноеЛицо", Справочники.КонтактныеЛица.ПустаяСсылка());
	
	Запрос.Текст = "
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Событие.Ссылка           КАК ДокументНапоминания,
	|	Событие.Ответственный    КАК Пользователь,
	|	Событие.Контрагент       КАК Контрагент,
	|	Событие.КонтактноеЛицо   КАК КонтактноеЛицо,
	|	Событие.ВремяНапоминания КАК ДатаНапоминания
	|ИЗ
	|	Документ.Событие КАК Событие
	|	
	|ГДЕ 
	|	Событие.НапомнитьОСобытии = Истина
	|	И Событие.ВремяНапоминания <= &ДатаСреза
	|	И Событие.Ответственный = &ВыбПользователь
	|	И Событие.Проведен
	|	И Событие.СостояниеСобытия = &СостояниеСобытия
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Заказ.Ссылка           КАК ДокументНапоминания,
	|	Заказ.Ответственный    КАК Пользователь,
	|	Заказ.Контрагент       КАК Контрагент,
	|	&ПустоеКонтактноеЛицо  КАК КонтактноеЛицо,
	|	Заказ.ВремяНапоминания КАК ДатаНапоминания
	|ИЗ
	|	Документ.ЗаказПокупателя КАК Заказ
	|	
	|ГДЕ 
	|	Заказ.НапомнитьОСобытии = Истина
	|	И Заказ.ВремяНапоминания <= &ДатаСреза
	|	И Заказ.Ответственный = &ВыбПользователь
	|	И Заказ.Проведен
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Заказ.Ссылка           КАК ДокументНапоминания,
	|	Заказ.Ответственный    КАК Пользователь,
	|	&ПустойКонтрагент      КАК Контрагент,
	|	&ПустоеКонтактноеЛицо  КАК КонтактноеЛицо,
	|	Заказ.ВремяНапоминания КАК ДатаНапоминания
	|ИЗ
	|	Документ.ВнутреннийЗаказ КАК Заказ
	|	
	|ГДЕ 
	|	Заказ.НапомнитьОСобытии = Истина
	|	И Заказ.ВремяНапоминания <= &ДатаСреза
	|	И Заказ.Ответственный = &ВыбПользователь
	|	И Заказ.Проведен
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Заказ.Ссылка           КАК ДокументНапоминания,
	|	Заказ.Ответственный    КАК Пользователь,
	|	Заказ.Контрагент       КАК Контрагент,
	|	&ПустоеКонтактноеЛицо  КАК КонтактноеЛицо,
	|	Заказ.ВремяНапоминания КАК ДатаНапоминания
	|ИЗ
	|	Документ.ЗаказПоставщику КАК Заказ
	|	
	|ГДЕ 
	|	Заказ.НапомнитьОСобытии = Истина
	|	И Заказ.ВремяНапоминания <= &ДатаСреза
	|	И Заказ.Ответственный = &ВыбПользователь
	|	И Заказ.Проведен
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	СчетПокупателя.Ссылка           КАК ДокументНапоминания,
	|	СчетПокупателя.Ответственный    КАК Пользователь,
	|	СчетПокупателя.Контрагент       КАК Контрагент,
	|	&ПустоеКонтактноеЛицо           КАК КонтактноеЛицо,
	|	СчетПокупателя.ВремяНапоминания КАК ДатаНапоминания
	|ИЗ
	|	Документ.СчетНаОплатуПокупателю КАК СчетПокупателя
	|	
	|ГДЕ 
	|	СчетПокупателя.НапомнитьОСобытии = Истина
	|	И СчетПокупателя.ВремяНапоминания <= &ДатаСреза
	|	И СчетПокупателя.Ответственный = &ВыбПользователь
	|	И НЕ СчетПокупателя.ПометкаУдаления
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	СчетПоставщика.Ссылка           КАК ДокументНапоминания,
	|	СчетПоставщика.Ответственный    КАК Пользователь,
	|	СчетПоставщика.Контрагент       КАК Контрагент,
	|	&ПустоеКонтактноеЛицо           КАК КонтактноеЛицо,
	|	СчетПоставщика.ВремяНапоминания КАК ДатаНапоминания
	|ИЗ
	|	Документ.СчетНаОплатуПоставщика КАК СчетПоставщика
	|	
	|ГДЕ 
	|	СчетПоставщика.НапомнитьОСобытии = Истина
	|	И СчетПоставщика.ВремяНапоминания <= &ДатаСреза
	|	И СчетПоставщика.Ответственный = &ВыбПользователь
	|	И НЕ СчетПоставщика.ПометкаУдаления
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ИзменениеСостоянияОС.Ссылка           КАК ДокументНапоминания,
	|	ИзменениеСостоянияОС.Ответственный    КАК Пользователь,
	|	&ПустойКонтрагент                     КАК Контрагент,
	|	&ПустоеКонтактноеЛицо                 КАК КонтактноеЛицо,
	|	ИзменениеСостоянияОС.ВремяНапоминания КАК ДатаНапоминания
	|ИЗ
	|	Документ.ИзменениеСостоянияОС КАК ИзменениеСостоянияОС
	|ГДЕ 
	|	ИзменениеСостоянияОС.НапомнитьОСобытии = Истина
	|	И ИзменениеСостоянияОС.ВремяНапоминания <= &ДатаСреза
	|	И ИзменениеСостоянияОС.Ответственный = &ВыбПользователь
	|	И НЕ ИзменениеСостоянияОС.ПометкаУдаления
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДокЗаказНаПроизводство.Ссылка           КАК ДокументНапоминания,
	|	ДокЗаказНаПроизводство.Ответственный    КАК Пользователь,
	|	&ПустойКонтрагент                       КАК Контрагент,
	|	&ПустоеКонтактноеЛицо                   КАК КонтактноеЛицо,
	|	ДокЗаказНаПроизводство.ВремяНапоминания КАК ДатаНапоминания
	|ИЗ
	|	Документ.ЗаказНаПроизводство КАК ДокЗаказНаПроизводство
	|ГДЕ 
	|	ДокЗаказНаПроизводство.НапомнитьОСобытии = Истина
	|	И ДокЗаказНаПроизводство.ВремяНапоминания <= &ДатаСреза
	|	И ДокЗаказНаПроизводство.Ответственный = &ВыбПользователь
	|	И НЕ ДокЗаказНаПроизводство.ПометкаУдаления
	|	
	|";

	РезультатЗапроса = Запрос.Выполнить();

	Если НЕ РезультатЗапроса.Пустой() Тогда

		Выборка = РезультатЗапроса.Выбрать();

		Пока Выборка.Следующий() Цикл

			Если Выборка.ДокументНапоминания <> Неопределено И НЕ Выборка.ДокументНапоминания.Пустая() Тогда

				Док = Выборка.ДокументНапоминания;

				СтрокаРезТаблицы = РезультирующаяТаблица.Добавить();
				СтрокаРезТаблицы.ДокументНапоминания = Выборка.ДокументНапоминания;
				СтрокаРезТаблицы.Пользователь        = ВыбПользователь;
				СтрокаРезТаблицы.ДатаНапоминания     = Выборка.ДатаНапоминания;
				СтрокаРезТаблицы.Контрагент          = Выборка.Контрагент;
				СтрокаРезТаблицы.КонтактноеЛицо      = Выборка.КонтактноеЛицо;
				СтрокаРезТаблицы.ДатаНапоминания     = Выборка.ДатаНапоминания;
				
				СтрокаРезТаблицы.ТекстНапоминания = Строка(Док);
				Если ТипЗнч(Док) = Тип("ДокументСсылка.Событие") И СтрЧислоВхождений(Док.ОписаниеСобытия, "День рождения") > 0 Тогда
					СтрокаРезТаблицы.ФлагДеньРождения = Истина;
				Иначе
					СтрокаРезТаблицы.ФлагДеньРождения = Ложь;
				КонецЕсли; 
				
				Если ТипЗнч(Док) = Тип("ДокументСсылка.Событие") Тогда
					СтрокаРезТаблицы.Тема = Строка(Док.ВидСобытия);
					СтрокаРезТаблицы.ТекстНапоминания = Док.ОписаниеСобытия + Символы.ПС + "с " + ?(НЕ ЗначениеЗаполнено(Док.НачалоСобытия), "Не задано",Формат(Док.НачалоСобытия, "ДФ='dd.MM.yyyy (HH:mm)'")) + " по " + ?(НЕ ЗначениеЗаполнено(Док.ОкончаниеСобытия), "Не задано",Формат(Док.ОкончаниеСобытия, "ДФ='dd.MM.yyyy (HH:mm)'")) + Символы.ПС + СтрокаРезТаблицы.ТекстНапоминания;
				ИначеЕсли ТипЗнч(Док) = Тип("ДокументСсылка.ЗаказПокупателя") Тогда
					СтрокаРезТаблицы.Тема = Док.Метаданные().Синоним;
					СуммаДокумента = ОбщегоНазначения.ФорматСумм(УчетНДС.ПолучитьСуммуДокументаСНДС(Док, "Товары"));
					СтрокаРезТаблицы.ТекстНапоминания = "Склад: " + Строка(Док.СкладГруппа) + ", Банк/Касса: " + Строка(Док.СтруктурнаяЕдиница) + Символы.ПС + "Сумма заказа: " + СуммаДокумента + " " + ?(НЕ ЗначениеЗаполнено(Док.ВалютаДокумента),"",СокрЛП(Док.ВалютаДокумента)) + Символы.ПС + СтрокаРезТаблицы.ТекстНапоминания;
				ИначеЕсли ТипЗнч(Док) = Тип("ДокументСсылка.ВнутреннийЗаказ") Тогда
					СтрокаРезТаблицы.Тема = Док.Метаданные().Синоним;
					СуммаДокумента = 0;
					СтрокаРезТаблицы.ТекстНапоминания = "Вид заказа: "+Док.ВидЗаказа+", Заказчик: " + Строка(Док.Заказчик)+ Символы.ПС + СтрокаРезТаблицы.ТекстНапоминания;
		
				ИначеЕсли ТипЗнч(Док) = Тип("ДокументСсылка.СчетНаОплатуПокупателю") Тогда
					СтрокаРезТаблицы.Тема = Док.Метаданные().Синоним;
					СуммаДокумента = ОбщегоНазначения.ФорматСумм(УчетНДС.ПолучитьСуммуДокументаСНДС(Док, "Товары"));
					СтрокаРезТаблицы.ТекстНапоминания = "Склад: " + Строка(Док.Склад) + ", Банк/Касса: " + Строка(Док.СтруктурнаяЕдиница) + Символы.ПС + "Сумма заказа: " + СуммаДокумента + " " + ?(НЕ ЗначениеЗаполнено(Док.ВалютаДокумента),"",СокрЛП(Док.ВалютаДокумента)) + Символы.ПС + СтрокаРезТаблицы.ТекстНапоминания;
				ИначеЕсли ТипЗнч(Док) = Тип("ДокументСсылка.ЗаказПоставщику") Тогда
					СтрокаРезТаблицы.Тема = Док.Метаданные().Синоним;
					СуммаДокумента = ОбщегоНазначения.ФорматСумм(УчетНДС.ПолучитьСуммуДокументаСНДС(Док, "Товары"));
					СтрокаРезТаблицы.ТекстНапоминания = "Склад: " + Строка(Док.Склад) + ", Банк/Касса: " + Строка(Док.СтруктурнаяЕдиница) + Символы.ПС + "Сумма заказа: " + СуммаДокумента + " " + ?(НЕ ЗначениеЗаполнено(Док.ВалютаДокумента),"",СокрЛП(Док.ВалютаДокумента)) + Символы.ПС + СтрокаРезТаблицы.ТекстНапоминания;
				ИначеЕсли ТипЗнч(Док) = Тип("ДокументСсылка.СчетНаОплатуПоставщика") Тогда
					СтрокаРезТаблицы.Тема = Док.Метаданные().Синоним;
					СуммаДокумента = ОбщегоНазначения.ФорматСумм(УчетНДС.ПолучитьСуммуДокументаСНДС(Док, "Товары"));
					СтрокаРезТаблицы.ТекстНапоминания = "Склад: " + Строка(Док.Склад) + ", Банк/Касса: " + Строка(Док.СтруктурнаяЕдиница) + Символы.ПС + "Сумма заказа: " + СуммаДокумента + " " + ?(НЕ ЗначениеЗаполнено(Док.ВалютаДокумента),"",СокрЛП(Док.ВалютаДокумента)) + Символы.ПС + СтрокаРезТаблицы.ТекстНапоминания;
				ИначеЕсли ТипЗнч(Док) = Тип("ДокументСсылка.ЗаказНаПроизводство") Тогда
					СтрокаРезТаблицы.Тема = Док.Метаданные().Синоним;
					СтрокаРезТаблицы.ТекстНапоминания = "Подразделение: " + Строка(Док.Подразделение) + Символы.ПС + СтрокаРезТаблицы.ТекстНапоминания;
				ИначеЕсли ТипЗнч(Док) = Тип("ДокументСсылка.ИзменениеСостоянияОС") Тогда
					СтрокаРезТаблицы.Тема = Док.Метаданные().Синоним;
				КонецЕсли;

			КонецЕсли;
		КонецЦикла; 

	КонецЕсли;

	ОбработкаСпискаНапоминаний(РезультирующаяТаблица);

КонецПроцедуры // ПроверитьНапоминанияПользователя()

// Процедура проверяет наличие активных заметок по переданному объекту
// и открывает список заметок в случае наличия активных
Процедура ПроверитьЗаметкиПоОбъекту(Объект)
	
	Если ТипЗнч(Объект) = Тип("СписокЗначений") Тогда
		Если Объект.Количество() = 1 Тогда
			Объект = Объект[0].Значение;
		ИначеЕсли Объект.Количество() = 0 Тогда
			Возврат;
		КонецЕсли;
	ИначеЕсли НЕ ЗначениеЗаполнено(Объект) Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Объект",Объект);
	Запрос.УстановитьПараметр("ТекущаяДата",ТекущаяДата());
	
	Запрос.Текст = "
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
	|	Заметки.Объект
	|ИЗ
	|	РегистрСведений.Заметки КАК Заметки
	|ГДЕ
	|	Заметки.Объект В(&Объект)
	|	И Заметки.Активная = ИСТИНА
	|	И Заметки.Дата <= &ТекущаяДата";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		
		# Если Клиент Тогда
		Предупреждение("ВНИМАНИЕ! Есть активные заметки.",5);
		# КонецЕсли
		
		ФормаСписка = РегистрыСведений.Заметки.ПолучитьФормуСписка();
		Если ТипЗнч(Объект) = Тип("СписокЗначений") Тогда
			ФормаСписка.Отбор.Объект.ВидСравнения = ВидСравнения.ВСписке
		КонецЕсли;
		ФормаСписка.Отбор.Объект.Значение = Объект;
		ФормаСписка.Отбор.Объект.Использование = Истина;
		ФормаСписка.Отбор.Активная.Значение = Истина;
		ФормаСписка.Отбор.Активная.Использование = Истина;
		ФормаСписка.Отбор.Дата.Использование = Истина;
		ФормаСписка.Отбор.Дата.Значение = ТекущаяДата();
		ФормаСписка.Отбор.Дата.ВидСравнения = ВидСравнения.МеньшеИлиРавно;
		
		ФормаСписка.ЭлементыФормы.РегистрСведенийСписок.Колонки.Объект.Видимость = Ложь;
		
		ФормаСписка.Открыть();
		
	КонецЕсли;
	
КонецПроцедуры

// Процедура подготовливает список объектов из переданного документа для проверки заметок
// 
Процедура ПроверитьЗаметкиПоДокументу(ДокументОбъект) Экспорт
	
	МетаданныеДокумента = ДокументОбъект.Метаданные();
	
	СписокОбъектовДляПроверки = Новый СписокЗначений;
	
	Если МетаданныеДокумента.Реквизиты.Найти("Контрагент") <> Неопределено И ТипЗнч(ДокументОбъект.Контрагент) = Тип("СправочникСсылка.Контрагенты") Тогда
		СписокОбъектовДляПроверки.Добавить(ДокументОбъект.Контрагент);
	КонецЕсли;
	
	Если МетаданныеДокумента.Реквизиты.Найти("КонтактноеЛицоКонтрагента") <> Неопределено И ТипЗнч(ДокументОбъект.КонтактноеЛицоКонтрагента) = Тип("СправочникСсылка.ФизическиеЛица") Тогда
		СписокОбъектовДляПроверки.Добавить(ДокументОбъект.КонтактноеЛицоКонтрагента);
	КонецЕсли;
	
	ПроверитьЗаметкиПоОбъекту(СписокОбъектовДляПроверки);
	
КонецПроцедуры






////////////////////////////////////////////////////////////////////////////////
// РАБОЧЕЕ МЕСТО МЕНЕДЖЕРА ПО ПРОДАЖАМ

#Если Клиент Тогда
	
// Процедура обрабатывает событие проверки перетаскивания
// номенклатуры в заказ покупателя.
//
Процедура ПроверкаПеретаскиванияВЗаказ(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка, ИмяТЧ) Экспорт

	СтандартнаяОбработка = Ложь;
	
	Если ТипЗнч(ПараметрыПеретаскивания.Значение) <> Тип("Структура") Тогда
		ПараметрыПеретаскивания.Действие = ДействиеПеретаскивания.Отмена;
		ПараметрыПеретаскивания.ДопустимыеДействия = ДопустимыеДействияПеретаскивания.НеОбрабатывать;
		Возврат;
	КонецЕсли;
	
	Если ((ИмяТЧ = "Товары" ИЛИ ИмяТЧ = "ВозвратнаяТара") И ПараметрыПеретаскивания.Значение.Номенклатура.Услуга)
	 ИЛИ (ИмяТЧ = "Услуги" И НЕ ПараметрыПеретаскивания.Значение.Номенклатура.Услуга) Тогда
		ПараметрыПеретаскивания.Действие = ДействиеПеретаскивания.Отмена;
		ПараметрыПеретаскивания.ДопустимыеДействия = ДопустимыеДействияПеретаскивания.НеОбрабатывать;
		Возврат;
	КонецЕсли;

КонецПроцедуры

// Процедура добавляет новую строку в ТЧ переденного имя.
//
Процедура ДобавитьСтрокуВТабличнуюЧастьДокумента(СтруктураДанныхДобавления, ИмяТЧ, ЭтотОбъект, ЭтаФорма) Экспорт
	
	// Спросим про добавляемое количество
	КоличествоДобавления = 1;
	Если НЕ ВвестиЧисло(КоличествоДобавления, "Введите количество", 15, 3) Тогда
		Возврат;
	КонецЕсли;
	
	Если КоличествоДобавления = 0 Тогда
		Возврат;
	КонецЕсли; 
	
	СтруктураДанныхДобавления.Вставить("Количество",КоличествоДобавления);
	
	ЭтаФорма.ОбработкаПодбора(ЭтотОбъект[ИмяТЧ], СтруктураДанныхДобавления);
	
КонецПроцедуры

// Процедура обрабатывает событие перетаскивания
// номенклатуры в заказ покупателя.
//
Процедура ПеретаскиваниеВЗаказ(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка, ИмяТЧ, ЭтотОбъект, ЭтаФорма) Экспорт

	СтандартнаяОбработка = Ложь;
	
	Если ТипЗнч(ПараметрыПеретаскивания.Значение) <> Тип("Структура") Тогда
		ПараметрыПеретаскивания.Действие = ДействиеПеретаскивания.Отмена;
		ПараметрыПеретаскивания.ДопустимыеДействия = ДопустимыеДействияПеретаскивания.НеОбрабатывать;
		Возврат;
	КонецЕсли;
	
	Если ((ИмяТЧ = "Товары" ИЛИ ИмяТЧ = "ВозвратнаяТара") И ПараметрыПеретаскивания.Значение.Номенклатура.Услуга)
	 ИЛИ (ИмяТЧ = "Услуги" И НЕ ПараметрыПеретаскивания.Значение.Номенклатура.Услуга) Тогда
		ПараметрыПеретаскивания.Действие = ДействиеПеретаскивания.Отмена;
		ПараметрыПеретаскивания.ДопустимыеДействия = ДопустимыеДействияПеретаскивания.НеОбрабатывать;
		Возврат;
	КонецЕсли;
	
	ДобавитьСтрокуВТабличнуюЧастьДокумента(ПараметрыПеретаскивания.Значение, ИмяТЧ, ЭтотОбъект, ЭтаФорма);
	
КонецПроцедуры

#КонецЕсли


////////////////////////////////////////////////////////////////////////////////
// РЕГИСТРАЦИЯ ДАННЫХ НЕЗАРЕГИСТРИРОВАННЫХ КОНТРАГЕНТОВ, БЕЗ ДОБАВЛЕНИЯ В СПРАВОЧНИК

// Процедура записывает в регистр сведений данные
// незарегистрированного контрагента.
//
Процедура ЗаписатьДанныеНезарегистрированногоКонтрагента(Ссылка, ДанныеНезарегистрированногоКонтрагента, Отказ) Экспорт

	ДанныеНезарегистрированногоКонтрагента.ОбъектРегистратор = Ссылка;
	
	Попытка
		ДанныеНезарегистрированногоКонтрагента.Записать(Истина);
	Исключение
		ОбщегоНазначения.СообщитьОбОшибке(ОписаниеОшибки(),, "Не удалось записать данные контрагента.");
		Отказ = Истина;
	КонецПопытки;

КонецПроцедуры

// Процедура читает из регистра сведений данные
// незарегистрированного контрагента.
//
Процедура ПрочитатьДанныеНезарегистрированногоКонтрагента(Ссылка, ДанныеНезарегистрированногоКонтрагента) Экспорт

	Если ЗначениеЗаполнено(Ссылка) Тогда
		ДанныеНезарегистрированногоКонтрагента.ОбъектРегистратор = Ссылка;
		ДанныеНезарегистрированногоКонтрагента.Прочитать();
	КонецЕсли; 

КонецПроцедуры

#Если Клиент Тогда

// Процедура записывает нового контрагента, заполняя его даннными
// из регистра сведений.
//
Процедура РегистрироватьНовогоКонтрагента(Элемент, Текст, Значение, СтандартнаяОбработка, мПоискПоСтрокеКонтрагента, мТекстПоискаПоСтрокеКонтрагента, мПоследнееЗначениеЭлементаПоискаПоСтрокеКонтрагент, ЭтаФорма, Контрагент, КонтактноеЛицо, Модифицированность, ДанныеНезарегистрированногоКонтрагента) Экспорт

	СтандартнаяОбработка = Ложь;
	
	Если ТипЗнч(Элемент.Значение) <> Тип("Строка") Тогда
	
		ТекстВопроса = "Контрагент не найден. Зарегистрировать данные нового контрагента?";
		
		ОтветНаВопрос = Вопрос(ТекстВопроса, РежимДиалогаВопрос.ДаНетОтмена, , КодВозвратаДиалога.Нет);
		
		Если ОтветНаВопрос <> КодВозвратаДиалога.Да Тогда
			Если ОтветНаВопрос = КодВозвратаДиалога.Нет Тогда
				мПоискПоСтрокеКонтрагента = Истина;
				мТекстПоискаПоСтрокеКонтрагента = Текст;
			Иначе
				Значение = мПоследнееЗначениеЭлементаПоискаПоСтрокеКонтрагент;
			КонецЕсли; 
			Возврат;
		КонецЕсли;
		
		ОбработкаРедактирования = Обработки.РедактированиеДанныхНезарегистрированныхКонтрагентов.Создать();
		ОбработкаРедактирования.Запись = ДанныеНезарегистрированногоКонтрагента;
		ФормаРегистрацииНовогоКонтрагента = ОбработкаРедактирования.ПолучитьФорму(, ЭтаФорма);
		
		ФормаРегистрацииНовогоКонтрагента.ЗаполнитьНачальныеДанные();
		ДанныеНезарегистрированногоКонтрагента.НаименованиеКонтрагента = Текст;
		Если ТипЗнч(КонтактноеЛицо) = Тип("Строка") Тогда
			ДанныеНезарегистрированногоКонтрагента.ФИОКонтактногоЛица = КонтактноеЛицо;
		КонецЕсли; 
		
		РезультатЗакрытияФормы = ФормаРегистрацииНовогоКонтрагента.ОткрытьМодально();
		
		Если РезультатЗакрытияФормы = Неопределено Тогда
			Значение       = ДанныеНезарегистрированногоКонтрагента.НаименованиеКонтрагента;
			Контрагент     = ДанныеНезарегистрированногоКонтрагента.НаименованиеКонтрагента;
			КонтактноеЛицо = ДанныеНезарегистрированногоКонтрагента.ФИОКонтактногоЛица;
		Иначе
			Значение = Контрагент;
		КонецЕсли;

		Модифицированность = Истина;
		
	Иначе
		
		Значение = Текст;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецЕсли

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОБЩЕГО НАЗНАЧЕНИЯ

// Процедура перехватывает событие начала выбора контактного лица контрагента
//
// Параметры
//  Элемент - элемент управления, выбора контактного лица контрагента
//  СтандартнаяОбработка - булево
//  СтандартнаяОбработка - булево
//  Контрагент - СправочникСсылка.Контрагенты, ,контрагент контактного лица
//
Процедура НачалоВыбораКонтактногоЛицаКонтрагента(Элемент, СтандартнаяОбработка, Контрагент = Неопределено) Экспорт
	
	СтандартнаяОбработка = Ложь;
	
	ФормаВыбора = Справочники.КонтактныеЛицаКонтрагентов.ПолучитьФормуВыбора(,Элемент);
	
	Если ТипЗнч(Контрагент) = Тип("СправочникСсылка.Контрагенты") И Контрагент <> Справочники.Контрагенты.ПустаяСсылка() Тогда
		ФормаВыбора.СправочникСписок.Отбор.Владелец.Использование = Истина;
		ФормаВыбора.СправочникСписок.Отбор.Владелец.Значение = Контрагент;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Элемент.Значение) И ТипЗнч(Элемент.Значение) = Тип("СправочникСсылка.КонтактныеЛицаКонтрагентов") Тогда
		ФормаВыбора.ЭлементыФормы.СправочникСписок.ТекущаяСтрока = Элемент.Значение;
	КонецЕсли;
	
	ФормаВыбора.РежимВыбора = Истина;
	ФормаВыбора.Заголовок = "Выбор контактного лица контрагента";
	ФормаВыбора.Открыть();

КонецПроцедуры

// Процедура перехватывает событие начала выбора прочего контактного лица
//
// Параметры
//  Элемент - элемент управления, выбора контактного лица контрагента
//  СтандартнаяОбработка - булево
//  СтандартнаяОбработка - булево
//  Контрагент - СправочникСсылка.Контрагенты, ,контрагент контактного лица
//
Процедура НачалоВыбораПрочегоКонтактногоЛица(Элемент, СтандартнаяОбработка, Пользователь = Неопределено) Экспорт

	СтандартнаяОбработка = Истина;
	
КонецПроцедуры

// Процедура перехватывает событие начала выбора прочего контактного лица
//
// Параметры
//  Элемент - элемент управления, выбора контактного лица контрагента
//  СтандартнаяОбработка - булево
//  СтандартнаяОбработка - булево
//  Контрагент - СправочникСсылка.Контрагенты, ,контрагент контактного лица
//
Процедура НачалоВыбораЛичныхКонтактов(Элемент, СтандартнаяОбработка) Экспорт

КонецПроцедуры

// Процедура открывает форму списка справочника контактные лица,
//с признаком контактные лица контрагентов
///
Процедура ОткрытьСписокКонтактныхЛицКонтрагентов() Экспорт

	ФормаСписка =  Справочники.КонтактныеЛицаКонтрагентов.ПолучитьФормуСписка(,, "КонтактныеЛицаКонтрагента");

	Если ФормаСписка.Открыта() Тогда
		ФормаСписка.Активизировать();
		Возврат;
	КонецЕсли; 
	
	ФормаСписка.Открыть();
	
КонецПроцедуры

// Процедура открывает форму списка справочника контактные лица,
//с признаком контактные лица контрагентов
///
Процедура ОткрытьСписокПрочихКонтактныхЛиц() Экспорт

	ФормаСписка =  Справочники.КонтактныеЛица.ПолучитьФормуСписка(,, "КонтактныеЛица");

	Если ФормаСписка.Открыта() Тогда
		ФормаСписка.Активизировать();
		Возврат;
	КонецЕсли; 
	
	ФормаСписка.Открыть();

КонецПроцедуры

// Процедура открывает форму списка справочника контактные лица,
//с признаком личные контакты
///
Процедура ОткрытьСписокЛичныхКонтактов() Экспорт

	ФормаСписка = Справочники.ЛичныеКонтакты.ПолучитьФормуСписка(,, "ЛичныеКонтакты");
	Если ФормаСписка.Открыта() Тогда
		ФормаСписка.Активизировать();
		Возврат;
	КонецЕсли;
	
	ФормаСписка.Открыть();

КонецПроцедуры

// Функция возвращает таблицу значений, заполненную информацией о контрагентах контактного лица
// из регистра сведений "КонтактныеЛицаКонтрагентов"
Функция ВернутьТаблицуКонтрагентовКонтактногоЛица(Ссылка,ТолькоПустые = Ложь) Экспорт
	
	Запрос = Новый Запрос;
	
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	ИсторияКонтактныхЛицКонтрагентов.Контрагент КАК Контрагент,
	|	ИсторияКонтактныхЛицКонтрагентов.КонтактноеЛицоКонтрагента.Должность КАК Должность,
	|	ИсторияКонтактныхЛицКонтрагентов.КонтактноеЛицоКонтрагента.РольКонтактногоЛица КАК РольКонтактногоЛица,
	|	ИсторияКонтактныхЛицКонтрагентов.Период КАК Период,
	|   ИсторияКонтактныхЛицКонтрагентов.ДатаУвольнения КАК ДатаУвольнения
	|ИЗ
	|	РегистрСведений.ИсторияКонтактныхЛицКонтрагентов КАК ИсторияКонтактныхЛицКонтрагентов
	|
	|ГДЕ
	|	ИсторияКонтактныхЛицКонтрагентов.КонтактноеЛицо = &КонтактноеЛицо";
	
	Запрос.УстановитьПараметр("КонтактноеЛицо",Ссылка);
	
	Результаты = Запрос.Выполнить().Выгрузить();

	Возврат Результаты;
	
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ РАБОТЫ С МЕХАНИЗМОМ НАПОМИНАНИЙ

#Если Клиент Тогда

// Функция Определяет картинку для напоминания
//
// Параметры
//  Напоминание - Произвольный, объект напоминания
//  ТипСобытия  - тип события, если не указан - определяется по напоминанию
//
// Возвращаемое значение:
//  Картинка - Картинка
//
Функция ПолучитьКартинкуНапоминания(Напоминание,ТипСобытия = Неопределено) Экспорт
		
	ИндексВозврата = ОпределитьИндексКартинкиВидаСобытия(Напоминание,ТипСобытия);
		
	Если ИндексВозврата = 15 Тогда
		Возврат БиблиотекаКартинок.ЛичнаяВстречаИсходящяя;
		
	ИначеЕсли ИндексВозврата = 10 Тогда
		Возврат БиблиотекаКартинок.ЛичнаяВстречаВходящяя;
		
	ИначеЕсли ИндексВозврата = 17 Тогда
		Возврат БиблиотекаКартинок.ПочтовоеПисьмоИсходящее;
		
	ИначеЕсли ИндексВозврата = 12 Тогда
		Возврат БиблиотекаКартинок.ПочтовоеПисьмоВходящее;
		
	ИначеЕсли ИндексВозврата = 13 Тогда
		Возврат БиблиотекаКартинок.ПрочееСобытиеИсходящее;
		
	ИначеЕсли ИндексВозврата = 8 Тогда
		Возврат БиблиотекаКартинок.ПрочееСобытиеВходящее;
		
	ИначеЕсли ИндексВозврата = 14 Тогда
		Возврат БиблиотекаКартинок.ТелефонныйЗвонокИсходящий;
		
	ИначеЕсли ИндексВозврата = 9 Тогда
		Возврат БиблиотекаКартинок.ТелефонныйЗвонокВходящий;
		
	ИначеЕсли ИндексВозврата = 16 Тогда
		Возврат БиблиотекаКартинок.ЭлектронноеПисьмоИсходящее;
		
	ИначеЕсли ИндексВозврата = 11 Тогда
		Возврат БиблиотекаКартинок.ЭлектронноеПисьмоВходящее;
		
	ИначеЕсли ИндексВозврата = 21 Тогда
		Возврат Новый Картинка;
		
	Иначе
		Возврат БиблиотекаКартинок.Важно;
		
	КонецЕсли; 
	
КонецФункции //ПолучитьКартинкуНапоминания()

// Процедура вызывается по событию НачалоВыбораИзСписка
// у полей ввода, в которых редактируются даты со временем.
// 
//  Параметры
//   ЭлементУправления - ПолеВвода, в котором редактируется значение даты
//   ЭтаФорма - Форма
//   Пользователь - СправочникСсылка.Пользователи
//   СтандартнаяОбработка - булево
//   ПолныйГод - Булево, предсавление года даты в списке выбора
//
Процедура ВыбратьВремяИзСписка(ЭлементУправления, ЭтаФорма, Пользователь, СтандартнаяОбработка, ПолныйГод = Истина) Экспорт

	СтандартнаяОбработка = Ложь;
	
	ДатаВремени = ЭлементУправления.Значение;
	
	СтруктураРабочихВремен = ОпределитьНачалоИОкончаниеРабочегоДняПользователя(Пользователь, ДатаВремени);
	
	СписокВремен = Новый СписокЗначений;
	НачалоРабочегоДня = НачалоДня(ДатаВремени)+Час(СтруктураРабочихВремен.ДатаНачала)*60*60+Минута(СтруктураРабочихВремен.ДатаНачала)*60;
	ОкончаниеРабочегоДня = НачалоДня(ДатаВремени)+Час(СтруктураРабочихВремен.ДатаОкончания)*60*60+Минута(СтруктураРабочихВремен.ДатаОкончания)*60 - 60*60;

	НачалоРабочегоДняДляСпискаВыбора = НачалоЧаса(НачалоРабочегоДня);
	а = 0;
	Пока Истина Цикл
		ВремяСписка = НачалоРабочегоДняДляСпискаВыбора + а*30*60;
		Если НачалоЧаса(ВремяСписка) > НачалоЧаса(ОкончаниеРабочегоДня) Тогда
			Прервать;
		КонецЕсли;
		Если ПолныйГод Тогда
			СписокВремен.Добавить(ВремяСписка, Формат(ВремяСписка,"ДФ='дд.ММ.гггг ЧЧ:мм'"));
		Иначе
			СписокВремен.Добавить(ВремяСписка, Формат(ВремяСписка,"ДФ='дд.ММ.гг ЧЧ:мм'"));
		КонецЕсли; 
		а = а + 1;
	КонецЦикла; 

	НачальноеЗначение = СписокВремен.НайтиПоЗначению(ДатаВремени);
	Если НачальноеЗначение = Неопределено Тогда
		ВыбранноеВремя = ЭтаФорма.ВыбратьИзСписка(СписокВремен, ЭлементУправления);
	Иначе
		ВыбранноеВремя = ЭтаФорма.ВыбратьИзСписка(СписокВремен, ЭлементУправления, НачальноеЗначение);
	КонецЕсли; 

	Если ВыбранноеВремя <> Неопределено Тогда
		ЭлементУправления.Значение = ВыбранноеВремя.Значение;
	КонецЕсли; 
	
КонецПроцедуры

#КонецЕсли

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ РАБОТЫ С МЕХАНИЗМОМ СОБЫТИЙ И КАЛЕНДАРЕМ ПОЛЬЗОВАТЕЛЯ

// Функция Определяет индекс картинки
//
// Параметры
//  ВидСобытия - ПеречислениеСсылка.ВидыСобытий
//  ТипСобытия - ПеречислениеСсылка.ВходящееИсходящееСобытие
//
// Возвращаемое значение:
//  Индекс - число
//
Функция ОпределитьИндексКартинкиВидаСобытия(Объект,ТипСобытия = Неопределено) Экспорт

	Если ТипЗнч(Объект) = Тип("ДокументСсылка.Событие") Тогда
		ВидСобытия = Объект.ВидСобытия;
		ТипСобытия = Объект.ТипСобытия;
	Иначе
		ВидСобытия = Объект;
	КонецЕсли;
	
	Если ТипЗнч(ВидСобытия) = Тип("ПеречислениеСсылка.ВидыСобытий") Тогда
		
		Если ВидСобытия = Перечисления.ВидыСобытий.ТелефонныйЗвонок Тогда
			Если ТипСобытия = Перечисления.ВходящееИсходящееСобытие.Исходящее Тогда
				Возврат 14;
			Иначе
				Возврат 9;
			КонецЕсли;
		ИначеЕсли ВидСобытия = Перечисления.ВидыСобытий.ЛичнаяВстреча Тогда
			Если ТипСобытия = Перечисления.ВходящееИсходящееСобытие.Исходящее Тогда
				Возврат 15;
			Иначе
				Возврат 10;
			КонецЕсли;
		ИначеЕсли ВидСобытия = Перечисления.ВидыСобытий.ЭлектронноеПисьмо Тогда
			Если ТипСобытия = Перечисления.ВходящееИсходящееСобытие.Исходящее Тогда
				Возврат 16;
			Иначе
				Возврат 11;
			КонецЕсли;
		ИначеЕсли ВидСобытия = Перечисления.ВидыСобытий.ПочтовоеПисьмо Тогда
			Если ТипСобытия = Перечисления.ВходящееИсходящееСобытие.Исходящее Тогда
				Возврат 17;
			Иначе
				Возврат 12;
			КонецЕсли;
		Иначе
			Если ТипСобытия = Перечисления.ВходящееИсходящееСобытие.Исходящее Тогда
				Возврат 13;
			Иначе
				Возврат 8;
			КонецЕсли;
		КонецЕсли;
			
	Иначе
		Возврат 21;
	КонецЕсли;

КонецФункции // ОпределитьИндексКартинкиВидаСобытия()

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ РАБОТЫ С ДОПОЛНИТЕЛЬНЫХ УСЛОВИЙ ПО ДОГОВОРАМ ВЗАИМОРАСЧЕТОВ

// Процедура формирует отчет для анализа условий поставок по договорам взаиморасчетов
//
// Параметры:
//  Ссылка - ДокументСсылка.УсловияПоставокПоДоговорамКонтрагентов
//
Процедура СформироватьАнализУсловийДоговораВзаиморасчетов(ТекущийПользователь, Ссылка) Экспорт

	Если ТипЗнч(Ссылка) <> Тип("ДокументСсылка.УсловияПоставокПоДоговорамКонтрагентов") Тогда
		Возврат;
	КонецЕсли;
	
	Отчет = Отчеты.ВыполнениеУсловийПоДоговорамКонтрагентов.Создать();
	Отчет.ДатаНачала                           = УправлениеПользователями.ПолучитьЗначениеПоУмолчанию(ТекущийПользователь, "ОсновнаяДатаНачалаОтчетов");
	Отчет.ДатаОкончания                        = ТекущаяДата();
	Отчет.ГруппировкаКонтрагент                = Истина;
	Отчет.ГруппировкаДоговорКонтрагента        = Истина;
	Отчет.ГруппировкаДокументУсловий           = Истина;
	Отчет.ГруппировкаДокументВыполненияУсловий = Истина;
	Отчет.РаскрашиватьГруппировки              = Истина;
	
	ЭлементОтбора = Отчет.Отбор.Добавить("ДокументУсловий",, "Документ условий");
	ЭлементОтбора.ВидСравнения  = ВидСравнения.Равно;
	ЭлементОтбора.Значение      = Ссылка;
	ЭлементОтбора.Использование = Истина;
	
	ФормаОтчета = Отчет.ПолучитьФорму("Форма");
	
	ФормаОтчета.НеЗаполнятьНастройкиПриОткрытии = Истина;
	
	Отчет.СформироватьОтчет(ФормаОтчета.ЭлементыФормы.ТабличныйДокумент);
	
	ФормаОтчета.Открыть();

КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ РАБОТЫ С МЕХАНИЗМОМ ОБЪМНО-КАЛЕНДАРНОГО ПЛАНИРОВАНИЯ ЗАКУПОК

// Функция формирует структуру с данными из объекта в принципе любого, но
//  писалась конкретно для документа ЗаказПоставщику
//
// Параметры
//  Объект - ДокументОбъект.ЗаказПоставщику, но в принципе может быть любой, объект, данные которого необходимо сохранить
//
// Возвращаемое значение:
//   Структура, с данными объекта
//
Функция СкопироватьДанныеОбъектаВСтруктуру(Объект) Экспорт

	СтруктураДанныхОбъекта = Новый Структура("___ИмяОбъекта___", Объект.Метаданные().Имя);
	
	Для каждого РеквизитОбъекта Из Объект.Метаданные().Реквизиты Цикл
		СтруктураДанныхОбъекта.Вставить(РеквизитОбъекта.Имя, Объект[РеквизитОбъекта.Имя]);
	КонецЦикла;
	
	Для каждого ТЧОбъекта Из Объект.Метаданные().ТабличныеЧасти Цикл
		СтруктураДанныхОбъекта.Вставить(ТЧОбъекта.Имя, Объект[ТЧОбъекта.Имя].Выгрузить());
	КонецЦикла;

	Возврат СтруктураДанныхОбъекта;
	
КонецФункции

// Функция заполняет объект данными из структуры
//
// Параметры
//  Объект - объект, который необходимо заполнить данными
//  СтруктураДанныхОбъекта - структура, с данными, которыми надо заполнить объект
//
// Возвращаемое значение:
//   Булево, успешно ли прошла данная опреация
//
Функция ВосстановитьДанныеОбъектаИзСтруктуры(Объект, СтруктураДанныхОбъекта) Экспорт
	
	РезультатРаботы = Истина;
	
	ИмяОбъекта = "";
	Если НЕ СтруктураДанныхОбъекта.Свойство("___ИмяОбъекта___", ИмяОбъекта) Тогда
		РезультатРаботы = Ложь;
	КонецЕсли;
	
	Если ИмяОбъекта <> Объект.Метаданные().Имя Тогда
		РезультатРаботы = Ложь;
	КонецЕсли; 
	
	Если РезультатРаботы Тогда
	
		ЗначениеЭлементаСтруктуры = Неопределено;
		
		Для каждого РеквизитОбъекта Из Объект.Метаданные().Реквизиты Цикл
			
			Если НЕ СтруктураДанныхОбъекта.Свойство(РеквизитОбъекта.Имя, ЗначениеЭлементаСтруктуры) Тогда
				РезультатРаботы = Ложь;
				Продолжить;
			КонецЕсли;
			
			Попытка
				Объект[РеквизитОбъекта.Имя] = ЗначениеЭлементаСтруктуры;
			Исключение
				РезультатРаботы = Ложь;
			КонецПопытки;
			
		КонецЦикла;
		
		Для каждого ТЧОбъекта Из Объект.Метаданные().ТабличныеЧасти Цикл
			
			Если НЕ СтруктураДанныхОбъекта.Свойство(ТЧОбъекта.Имя, ЗначениеЭлементаСтруктуры) Тогда
				РезультатРаботы = Ложь;
				Продолжить;
			КонецЕсли;
			
			Если ТипЗнч(ЗначениеЭлементаСтруктуры) <> Тип("ТаблицаЗначений") Тогда
				РезультатРаботы = Ложь;
				Продолжить;
			КонецЕсли; 
			
			Попытка
				Объект[ТЧОбъекта.Имя].Загрузить(ЗначениеЭлементаСтруктуры);
			Исключение
				РезультатРаботы = Ложь;
			КонецПопытки;
			
		КонецЦикла;
	
	КонецЕсли; 
	
	Возврат РезультатРаботы;

КонецФункции

// Функция сравнивает данные из структуры с данными объекта, для определения их идентичности
//
// Параметры
//  Объект - Объект с данными для сравнения
//  СтруктураДанныхОбъекта - структура с данными для сравнения
//
// Возвращаемое значение:
//   Булево - идентичны ли данные объета и структуры
//
Функция СравнитьСтруктуруДанныхОбъектаИОбъект(Объект, СтруктураДанныхОбъекта) Экспорт

	ИмяОбъекта = "";
	Если НЕ СтруктураДанныхОбъекта.Свойство("___ИмяОбъекта___", ИмяОбъекта) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Если ИмяОбъекта <> Объект.Метаданные().Имя Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ЗначениеЭлементаСтруктуры = Неопределено;
	
	Для каждого РеквизитОбъекта Из Объект.Метаданные().Реквизиты Цикл
		
		Если НЕ СтруктураДанныхОбъекта.Свойство(РеквизитОбъекта.Имя, ЗначениеЭлементаСтруктуры) Тогда
			Возврат Ложь;
		КонецЕсли;
		
		Попытка
			Если Объект[РеквизитОбъекта.Имя] <> ЗначениеЭлементаСтруктуры Тогда
				Возврат Ложь;
			КонецЕсли;
		Исключение
			Возврат Ложь;
		КонецПопытки;
		
	КонецЦикла;
	
	Для каждого ТЧОбъекта Из Объект.Метаданные().ТабличныеЧасти Цикл
		
		Если НЕ СтруктураДанныхОбъекта.Свойство(ТЧОбъекта.Имя, ЗначениеЭлементаСтруктуры) Тогда
			Возврат Ложь;
		КонецЕсли;
		
		Если ТипЗнч(ЗначениеЭлементаСтруктуры) <> Тип("ТаблицаЗначений") Тогда
			Возврат Ложь;
		КонецЕсли; 
		
		Если НЕ СравнитьТаблицыЗначений(Объект[ТЧОбъекта.Имя].Выгрузить(), ЗначениеЭлементаСтруктуры) Тогда
			Возврат Ложь;
		КонецЕсли; 
		
	КонецЦикла;

	Возврат Истина;
	
КонецФункции // СравнитьСтруктуруОбъектаИОбъект()

// Функция сравнивает две таблицы значений на идентичность структуры и данных
//
// Параметры
//  ТаблицаЗначений1 - ТаблицаЗначений для сравнения
//  ТаблицаЗначений2 - ТаблицаЗначений для сравнения
//
// Возвращаемое значение:
//   Булево, идентичны или нет две таблицы
//
Функция СравнитьТаблицыЗначений(ТаблицаЗначений1, ТаблицаЗначений2)

	Если ТипЗнч(ТаблицаЗначений1) <> Тип("ТаблицаЗначений") ИЛИ ТипЗнч(ТаблицаЗначений2) <> Тип("ТаблицаЗначений") Тогда
		Возврат Ложь;
	КонецЕсли; 
	
	Если ТаблицаЗначений1.Количество() <> ТаблицаЗначений2.Количество() Тогда
		Возврат Ложь;
	КонецЕсли; 

	Если ТаблицаЗначений1.Колонки.Количество() <> ТаблицаЗначений2.Колонки.Количество() Тогда
		Возврат Ложь;
	КонецЕсли; 

	Для каждого Колонка Из ТаблицаЗначений1.Колонки Цикл
		
		Если ТаблицаЗначений2.Колонки.Найти(Колонка.Имя) = Неопределено Тогда
			Возврат Ложь;
		КонецЕсли;
		
		Для каждого СтрокаТаблицы Из ТаблицаЗначений1 Цикл
		
			Попытка
			
				Если СтрокаТаблицы[Колонка.Имя] <> ТаблицаЗначений2[ТаблицаЗначений1.Индекс(СтрокаТаблицы)][Колонка.Имя] Тогда
				
					Возврат Ложь;
				
				КонецЕсли;
			
			Исключение
				
				Возврат Ложь;
				
			КонецПопытки;
		
		КонецЦикла; 
	
	КонецЦикла; 
	
	Возврат Истина;
	
КонецФункции // СравнитьТаблицыЗначений()

// Функция производит распределние заказов поставщикам из таблицы ИсходнаяТаблицаНоменклатурыЗаказов
//  по строкам таблицы потребности в номенклатуре ИсходнаяТаблицаПотребности по дополнительному полю "Индекс"
//
// Параметры
//  ИсходнаяТаблицаПотребности - ТаблицаЗначений, таблица потребности в номенклатуре
//  ИсходнаяТаблицаНоменклатурыЗаказов - ТаблицаЗначений, таблица заказов поставщикам
//
// Возвращаемое значение:
//   Структура, ключ ТаблицаПотребности - исходная таблица потребности с дополнительным полем "Индекс" (число) , где
//                                        значения соответствуют строкам таблицы ТаблицаНоменклатурыЗаказов в поле "Индекс"
//            , ключ ТаблицаНоменклатурыЗаказов - исходная таблица закзов поставщикам с дополнительным полем "Индекс" (число) , где
//                                        значения соответствуют строкам таблицы ТаблицаПотребности в поле "Индекс"
//
Функция ПолучитьСтруктуруРаспределенныхТаблиц(ИсходнаяТаблицаПотребности, ИсходнаяТаблицаНоменклатурыЗаказов, МаксимальноеЗначениеПрогрессора = 0, ТекущееЗначениеПрогрессора = 0) Экспорт

	ТаблицаПотребности = ИсходнаяТаблицаПотребности.Скопировать();
	ТаблицаНоменклатурыЗаказов = ИсходнаяТаблицаНоменклатурыЗаказов.Скопировать();
	
	ТаблицаНоменклатурыЗаказов.Сортировать("Контрагент ВОЗР, ДатаПоступленияЗаказа УБЫВ");

	ТаблицаЗаказовПоставщикам = Новый ТаблицаЗначений;
	ТаблицаЗаказовПоставщикам.Колонки.Добавить("Индекс", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15,0)));
	ТаблицаЗаказовПоставщикам.Колонки.Добавить("ЗаказПоставщику");
	ТаблицаЗаказовПоставщикам.Колонки.Добавить("Количество", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15,3)));
	ТаблицаЗаказовПоставщикам.Колонки.Добавить("Поставщик", Новый ОписаниеТипов("СправочникСсылка.Контрагенты"));
	ТаблицаЗаказовПоставщикам.Колонки.Добавить("ИДОбъекта");

	Если ТаблицаПотребности.Колонки.Найти("Индекс") = Неопределено Тогда
		ТаблицаПотребности.Колонки.Добавить("Индекс", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15,0)));
	КонецЕсли; 
	Если ТаблицаПотребности.Колонки.Найти("КоличествоПланаОстаток") = Неопределено Тогда
		ТаблицаПотребности.Колонки.Добавить("КоличествоПланаОстаток", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15,3)));
	КонецЕсли; 

	ТаблицаПотребности.Сортировать("Номенклатура ВОЗР, ХарактеристикаНоменклатуры ВОЗР, ДатаПотребности ВОЗР");

	МаксимальноеЗначениеПрогрессора = ТаблицаПотребности.Количество();
	Для каждого СтрокаПотребности Из ТаблицаПотребности Цикл
		ТекущееЗначениеПрогрессора = ТаблицаПотребности.Индекс(СтрокаПотребности) + 1;
		СтрокаПотребности.КоличествоПланаОстаток = СтрокаПотребности.КоличествоПлана;
		СтрокаПотребности.Индекс = ТаблицаПотребности.Индекс(СтрокаПотребности);
	КонецЦикла; 

	// Сначала распределим все заказы поставщикам, которые предназначены под конкретные заказы покупателей
	МаксимальноеЗначениеПрогрессора = ТаблицаНоменклатурыЗаказов.Количество();
	Для каждого СтрокаТаблицыНоменклатурыЗаказов Из ТаблицаНоменклатурыЗаказов Цикл
		
		ТекущееЗначениеПрогрессора = ТаблицаНоменклатурыЗаказов.Индекс(СтрокаТаблицыНоменклатурыЗаказов) + 1;
		
		Если СтрокаТаблицыНоменклатурыЗаказов.Количество <= 0 Тогда
			Продолжить;
		КонецЕсли; 
		
		Если НЕ ЗначениеЗаполнено(СтрокаТаблицыНоменклатурыЗаказов.ЗаказПокупателя) Тогда
			Продолжить;
		КонецЕсли;
		
		СтрокиТаблицыПотребности = ТаблицаПотребности.НайтиСтроки(Новый Структура("Номенклатура, ХарактеристикаНоменклатуры, ЗаказПокупателя, Тара", СтрокаТаблицыНоменклатурыЗаказов.Номенклатура, СтрокаТаблицыНоменклатурыЗаказов.ХарактеристикаНоменклатуры, СтрокаТаблицыНоменклатурыЗаказов.ЗаказПокупателя, СтрокаТаблицыНоменклатурыЗаказов.Тара));
		Для каждого СтрокаТаблицыПотребности Из СтрокиТаблицыПотребности Цикл
			
			Если СтрокаТаблицыПотребности.КоличествоПланаОстаток <= 0 Тогда
				Продолжить;
			КонецЕсли;
			
			Если СтрокаТаблицыПотребности.КоличествоПланаОстаток <= СтрокаТаблицыНоменклатурыЗаказов.Количество Тогда
				
				НоваяСтрока = ТаблицаЗаказовПоставщикам.Добавить();
				НоваяСтрока.Индекс          = СтрокаТаблицыПотребности.Индекс;
				НоваяСтрока.ЗаказПоставщику = СтрокаТаблицыНоменклатурыЗаказов.ЗаказПоставщику;
				Если ТаблицаНоменклатурыЗаказов.Колонки.Найти("ИДОбъекта") <> Неопределено Тогда
					НоваяСтрока.ИДОбъекта = СтрокаТаблицыНоменклатурыЗаказов.ИДОбъекта;
				КонецЕсли; 
				НоваяСтрока.Количество      = СтрокаТаблицыПотребности.КоличествоПланаОстаток;
				Если СтрокаТаблицыНоменклатурыЗаказов.ЗаказПоставщику.Пустая() И ТаблицаНоменклатурыЗаказов.Колонки.Найти("Контрагент") <> Неопределено Тогда
					НоваяСтрока.Поставщик = СтрокаТаблицыНоменклатурыЗаказов.Контрагент;
				Иначе
					НоваяСтрока.Поставщик = СтрокаТаблицыНоменклатурыЗаказов.ЗаказПоставщику.Контрагент;
				КонецЕсли; 
				
				СтрокаТаблицыНоменклатурыЗаказов.Количество = СтрокаТаблицыНоменклатурыЗаказов.Количество - СтрокаТаблицыПотребности.КоличествоПланаОстаток;
				СтрокаТаблицыПотребности.КоличествоПланаОстаток = 0;
				
			Иначе
				
				НоваяСтрока = ТаблицаЗаказовПоставщикам.Добавить();
				НоваяСтрока.Индекс          = СтрокаТаблицыПотребности.Индекс;
				НоваяСтрока.ЗаказПоставщику = СтрокаТаблицыНоменклатурыЗаказов.ЗаказПоставщику;
				Если ТаблицаНоменклатурыЗаказов.Колонки.Найти("ИДОбъекта") <> Неопределено Тогда
					НоваяСтрока.ИДОбъекта = СтрокаТаблицыНоменклатурыЗаказов.ИДОбъекта;
				КонецЕсли; 
				НоваяСтрока.Количество      = СтрокаТаблицыНоменклатурыЗаказов.Количество;
				Если СтрокаТаблицыНоменклатурыЗаказов.ЗаказПоставщику.Пустая() И ТаблицаНоменклатурыЗаказов.Колонки.Найти("Контрагент") <> Неопределено Тогда
					НоваяСтрока.Поставщик = СтрокаТаблицыНоменклатурыЗаказов.Контрагент;
				Иначе
					НоваяСтрока.Поставщик = СтрокаТаблицыНоменклатурыЗаказов.ЗаказПоставщику.Контрагент;
				КонецЕсли; 
				
				СтрокаТаблицыПотребности.КоличествоПланаОстаток = СтрокаТаблицыПотребности.КоличествоПланаОстаток - СтрокаТаблицыНоменклатурыЗаказов.Количество;
				СтрокаТаблицыНоменклатурыЗаказов.Количество = 0;
				
			КонецЕсли;
			
		КонецЦикла;
		
		Если СтрокиТаблицыПотребности.Количество() > 0 И СтрокаТаблицыНоменклатурыЗаказов.Количество > 0 Тогда
			
			НоваяСтрока = ТаблицаЗаказовПоставщикам.Добавить();
			НоваяСтрока.Индекс          = СтрокиТаблицыПотребности[0].Индекс;
			НоваяСтрока.ЗаказПоставщику = СтрокаТаблицыНоменклатурыЗаказов.ЗаказПоставщику;
			Если ТаблицаНоменклатурыЗаказов.Колонки.Найти("ИДОбъекта") <> Неопределено Тогда
				НоваяСтрока.ИДОбъекта = СтрокаТаблицыНоменклатурыЗаказов.ИДОбъекта;
			КонецЕсли; 
			НоваяСтрока.Количество      = СтрокаТаблицыНоменклатурыЗаказов.Количество;
			Если СтрокаТаблицыНоменклатурыЗаказов.ЗаказПоставщику.Пустая() И ТаблицаНоменклатурыЗаказов.Колонки.Найти("Контрагент") <> Неопределено Тогда
				НоваяСтрока.Поставщик = СтрокаТаблицыНоменклатурыЗаказов.Контрагент;
			Иначе
				НоваяСтрока.Поставщик = СтрокаТаблицыНоменклатурыЗаказов.ЗаказПоставщику.Контрагент;
			КонецЕсли; 
			
			СтрокаТаблицыНоменклатурыЗаказов.Количество = 0;
			
		Иначе
			
			СтрокаТаблицыНоменклатурыЗаказов.Количество = 0;
			
		КонецЕсли; 
			
	КонецЦикла;

	// После этого в таблице ТаблицаНоменклатурыЗаказов не осталост строк с заказами поставщикам, которым в соответствие
	// проставлены определенные заказы покупателей

	// Теперь удовлетворим оставшиеся потребности, у которых указаны заказы покупателей, теми заказами поставщикам, у которых
	// заказы покупателей не указаны (собственно только они и остались)
	МаксимальноеЗначениеПрогрессора = ТаблицаПотребности.Количество();
	Для каждого СтрокаПотребности Из ТаблицаПотребности Цикл
		
		ТекущееЗначениеПрогрессора = ТаблицаПотребности .Индекс(СтрокаПотребности) + 1;
		
		Если СтрокаПотребности.КоличествоПланаОстаток <= 0 Тогда
			Продолжить;
		КонецЕсли; 
			
		СтрокиТаблицаНоменклатурыЗаказов = ТаблицаНоменклатурыЗаказов.НайтиСтроки(Новый Структура("Номенклатура, ХарактеристикаНоменклатуры, Тара", СтрокаПотребности.Номенклатура, СтрокаПотребности.ХарактеристикаНоменклатуры, СтрокаПотребности.Тара));
		
		Для каждого СтрокаТаблицаНоменклатурыЗаказов Из СтрокиТаблицаНоменклатурыЗаказов Цикл
			
			Если НЕ ЗначениеЗаполнено(СтрокаТаблицаНоменклатурыЗаказов.ЗаказПокупателя) Тогда
				Продолжить;
			КонецЕсли; 
			
			Если СтрокаТаблицаНоменклатурыЗаказов.Количество <= 0 Тогда
				Продолжить;
			КонецЕсли;
			
			Если СтрокаТаблицаНоменклатурыЗаказов.ДатаПоступленияЗаказа > СтрокаПотребности.ДатаПотребности Тогда
				Продолжить;
			КонецЕсли; 
			
			Если СтрокаТаблицаНоменклатурыЗаказов.Количество >= СтрокаПотребности.КоличествоПланаОстаток Тогда
				
				НоваяСтрока = ТаблицаЗаказовПоставщикам.Добавить();
				НоваяСтрока.Индекс          = СтрокаПотребности.Индекс;
				НоваяСтрока.ЗаказПоставщику = СтрокаТаблицаНоменклатурыЗаказов.ЗаказПоставщику;
				Если ТаблицаНоменклатурыЗаказов.Колонки.Найти("ИДОбъекта") <> Неопределено Тогда
					НоваяСтрока.ИДОбъекта = СтрокаТаблицаНоменклатурыЗаказов.ИДОбъекта;
				КонецЕсли; 
				НоваяСтрока.Количество      = СтрокаПотребности.КоличествоПланаОстаток;
				Если СтрокаТаблицаНоменклатурыЗаказов.ЗаказПоставщику.Пустая() И ТаблицаНоменклатурыЗаказов.Колонки.Найти("Контрагент") <> Неопределено Тогда
					НоваяСтрока.Поставщик = СтрокаТаблицаНоменклатурыЗаказов.Контрагент;
				Иначе
					НоваяСтрока.Поставщик = СтрокаТаблицаНоменклатурыЗаказов.ЗаказПоставщику.Контрагент;
				КонецЕсли; 
				
				СтрокаТаблицаНоменклатурыЗаказов.Количество = СтрокаТаблицаНоменклатурыЗаказов.Количество - СтрокаПотребности.КоличествоПланаОстаток;
				
				СтрокаПотребности.КоличествоПланаОстаток = 0;
				
			Иначе
				
				НоваяСтрока = ТаблицаЗаказовПоставщикам.Добавить();
				НоваяСтрока.Индекс          = СтрокаПотребности.Индекс;
				НоваяСтрока.ЗаказПоставщику = СтрокаТаблицаНоменклатурыЗаказов.ЗаказПоставщику;
				Если ТаблицаНоменклатурыЗаказов.Колонки.Найти("ИДОбъекта") <> Неопределено Тогда
					НоваяСтрока.ИДОбъекта = СтрокаТаблицаНоменклатурыЗаказов.ИДОбъекта;
				КонецЕсли; 
				НоваяСтрока.Количество      = СтрокаТаблицаНоменклатурыЗаказов.Количество;
				Если СтрокаТаблицаНоменклатурыЗаказов.ЗаказПоставщику.Пустая() И ТаблицаНоменклатурыЗаказов.Колонки.Найти("Контрагент") <> Неопределено Тогда
					НоваяСтрока.Поставщик = СтрокаТаблицаНоменклатурыЗаказов.Контрагент;
				Иначе
					НоваяСтрока.Поставщик = СтрокаТаблицаНоменклатурыЗаказов.ЗаказПоставщику.Контрагент;
				КонецЕсли; 
				
				СтрокаПотребности.КоличествоПланаОстаток = СтрокаПотребности.КоличествоПланаОстаток - СтрокаТаблицаНоменклатурыЗаказов.Количество;
				
				СтрокаТаблицаНоменклатурыЗаказов.Количество = 0;
				
			КонецЕсли; 
			
		КонецЦикла; 
		
	КонецЦикла; 

	// Теперь удовлетворим оставшиеся потребности у которых заказы покупателей не указаны
	МаксимальноеЗначениеПрогрессора = ТаблицаПотребности.Количество();
	Для каждого СтрокаПотребности Из ТаблицаПотребности Цикл
		
		ТекущееЗначениеПрогрессора = ТаблицаПотребности.Индекс(СтрокаПотребности) + 1;
		
		Если СтрокаПотребности.КоличествоПланаОстаток <= 0 Тогда
			Продолжить;
		КонецЕсли; 
			
		СтрокиТаблицаНоменклатурыЗаказов = ТаблицаНоменклатурыЗаказов.НайтиСтроки(Новый Структура("Номенклатура, ХарактеристикаНоменклатуры, Тара", СтрокаПотребности.Номенклатура, СтрокаПотребности.ХарактеристикаНоменклатуры, СтрокаПотребности.Тара));
		
		Для каждого СтрокаТаблицаНоменклатурыЗаказов Из СтрокиТаблицаНоменклатурыЗаказов Цикл
			
			Если СтрокаТаблицаНоменклатурыЗаказов.ЗаказПокупателя <> Неопределено Тогда
				Если НЕ СтрокаТаблицаНоменклатурыЗаказов.ЗаказПокупателя.Пустая() Тогда
					Продолжить;
				КонецЕсли; 
			КонецЕсли; 
			
			Если СтрокаТаблицаНоменклатурыЗаказов.Количество <= 0 Тогда
				Продолжить;
			КонецЕсли;
			
			Если СтрокаТаблицаНоменклатурыЗаказов.ДатаПоступленияЗаказа > СтрокаПотребности.ДатаПотребности Тогда
				Продолжить;
			КонецЕсли; 
			
			Если СтрокаТаблицаНоменклатурыЗаказов.Количество >= СтрокаПотребности.КоличествоПланаОстаток Тогда
				
				НоваяСтрока = ТаблицаЗаказовПоставщикам.Добавить();
				НоваяСтрока.Индекс          = СтрокаПотребности.Индекс;
				НоваяСтрока.ЗаказПоставщику = СтрокаТаблицаНоменклатурыЗаказов.ЗаказПоставщику;
				Если ТаблицаНоменклатурыЗаказов.Колонки.Найти("ИДОбъекта") <> Неопределено Тогда
					НоваяСтрока.ИДОбъекта = СтрокаТаблицаНоменклатурыЗаказов.ИДОбъекта;
				КонецЕсли; 
				НоваяСтрока.Количество      = СтрокаПотребности.КоличествоПланаОстаток;
				Если СтрокаТаблицаНоменклатурыЗаказов.ЗаказПоставщику.Пустая() И ТаблицаНоменклатурыЗаказов.Колонки.Найти("Контрагент") <> Неопределено Тогда
					НоваяСтрока.Поставщик = СтрокаТаблицаНоменклатурыЗаказов.Контрагент;
				Иначе
					НоваяСтрока.Поставщик = СтрокаТаблицаНоменклатурыЗаказов.ЗаказПоставщику.Контрагент;
				КонецЕсли; 
				СтрокаТаблицаНоменклатурыЗаказов.Количество = СтрокаТаблицаНоменклатурыЗаказов.Количество - СтрокаПотребности.КоличествоПланаОстаток;
				
				СтрокаПотребности.КоличествоПланаОстаток = 0;
				
			Иначе
				
				НоваяСтрока = ТаблицаЗаказовПоставщикам.Добавить();
				НоваяСтрока.Индекс          = СтрокаПотребности.Индекс;
				НоваяСтрока.ЗаказПоставщику = СтрокаТаблицаНоменклатурыЗаказов.ЗаказПоставщику;
				Если ТаблицаНоменклатурыЗаказов.Колонки.Найти("ИДОбъекта") <> Неопределено Тогда
					НоваяСтрока.ИДОбъекта = СтрокаТаблицаНоменклатурыЗаказов.ИДОбъекта;
				КонецЕсли; 
				НоваяСтрока.Количество      = СтрокаТаблицаНоменклатурыЗаказов.Количество;
				Если СтрокаТаблицаНоменклатурыЗаказов.ЗаказПоставщику.Пустая() И ТаблицаНоменклатурыЗаказов.Колонки.Найти("Контрагент") <> Неопределено Тогда
					НоваяСтрока.Поставщик = СтрокаТаблицаНоменклатурыЗаказов.Контрагент;
				Иначе
					НоваяСтрока.Поставщик = СтрокаТаблицаНоменклатурыЗаказов.ЗаказПоставщику.Контрагент;
				КонецЕсли; 
				
				СтрокаПотребности.КоличествоПланаОстаток = СтрокаПотребности.КоличествоПланаОстаток - СтрокаТаблицаНоменклатурыЗаказов.Количество;
				
				СтрокаТаблицаНоменклатурыЗаказов.Количество = 0;
				
			КонецЕсли; 
			
		КонецЦикла; 
		
	КонецЦикла; 

	// Если остались свободные остатки по заказам не вошедшие в планы потребности, запишем их новыми строками
	// в план потребности с пустыми ссылками кроме Номенклатуры, Характеристики номенклатуры, Заказа покупателя. Дата портебности - пустая.

	МаксимальноеЗначениеПрогрессора = ТаблицаНоменклатурыЗаказов.Количество();
	Для каждого СтрокаТаблицыНоменклатурыЗаказов Из ТаблицаНоменклатурыЗаказов Цикл

		ТекущееЗначениеПрогрессора = ТаблицаНоменклатурыЗаказов.Индекс(СтрокаТаблицыНоменклатурыЗаказов) + 1;
		
		Если СтрокаТаблицыНоменклатурыЗаказов.Количество <= 0 Тогда
			Продолжить;
		КонецЕсли;
		
		СтрокиТаблицыПотребности = ТаблицаПотребности.НайтиСтроки(Новый Структура("Номенклатура, ХарактеристикаНоменклатуры, ДатаПотребности, Тара", СтрокаТаблицыНоменклатурыЗаказов.Номенклатура, СтрокаТаблицыНоменклатурыЗаказов.ХарактеристикаНоменклатуры, '00010101000000', СтрокаТаблицыНоменклатурыЗаказов.Тара));
		Если СтрокиТаблицыПотребности.Количество() > 0 Тогда
			СтрокаТаблицыПотребности = СтрокиТаблицыПотребности[0];
		Иначе
			СтрокаТаблицыПотребности = ТаблицаПотребности.Добавить();
			СтрокаТаблицыПотребности.Номенклатура = СтрокаТаблицыНоменклатурыЗаказов.Номенклатура;
			СтрокаТаблицыПотребности.ХарактеристикаНоменклатуры = СтрокаТаблицыНоменклатурыЗаказов.ХарактеристикаНоменклатуры;
			СтрокаТаблицыПотребности.ЗаказПокупателя = СтрокаТаблицыНоменклатурыЗаказов.ЗаказПокупателя;
			СтрокаТаблицыПотребности.ДатаПотребности = '00010101000000';
			СтрокаТаблицыПотребности.Тара = СтрокаТаблицыНоменклатурыЗаказов.Тара;
			Если ТаблицаПотребности.Колонки.Найти("ОтветственныйЗаПокупки") <> Неопределено И ЗначениеЗаполнено(СтрокаТаблицыНоменклатурыЗаказов.Номенклатура) Тогда
				СтрокаТаблицыПотребности.ОтветственныйЗаПокупки = СтрокаТаблицыНоменклатурыЗаказов.Номенклатура.ОтветственныйМенеджерЗаПокупки;
			КонецЕсли; 
			Попытка
				СтрокаТаблицыПотребности.КонечнаяПродукция = Справочники.Номенклатура.ПустаяСсылка();
				СтрокаТаблицыПотребности.Проект = Справочники.Проекты.ПустаяСсылка();
			Исключение
			КонецПопытки;
			СтрокаТаблицыПотребности.Индекс = ТаблицаПотребности.Индекс(СтрокаТаблицыПотребности);
			СтрокаТаблицыПотребности.КоличествоПлана = 0;
		КонецЕсли;
		
		НоваяСтрока = ТаблицаЗаказовПоставщикам.Добавить();
		НоваяСтрока.Индекс          = СтрокаТаблицыПотребности.Индекс;
		НоваяСтрока.ЗаказПоставщику = СтрокаТаблицыНоменклатурыЗаказов.ЗаказПоставщику;
		Если ТаблицаНоменклатурыЗаказов.Колонки.Найти("ИДОбъекта") <> Неопределено Тогда
			НоваяСтрока.ИДОбъекта = СтрокаТаблицыНоменклатурыЗаказов.ИДОбъекта;
		КонецЕсли; 
		НоваяСтрока.Количество      = СтрокаТаблицыНоменклатурыЗаказов.Количество;
		Если СтрокаТаблицыНоменклатурыЗаказов.ЗаказПоставщику.Пустая() И ТаблицаНоменклатурыЗаказов.Колонки.Найти("Контрагент") <> Неопределено Тогда
			НоваяСтрока.Поставщик = СтрокаТаблицыНоменклатурыЗаказов.Контрагент;
		Иначе
			НоваяСтрока.Поставщик = СтрокаТаблицыНоменклатурыЗаказов.ЗаказПоставщику.Контрагент;
		КонецЕсли; 
		
		СтрокаТаблицыНоменклатурыЗаказов.Количество = 0;
		
	КонецЦикла; 

	Если ТаблицаЗаказовПоставщикам.Колонки.Найти("ИДОбъекта") <> Неопределено Тогда
		ТаблицаЗаказовПоставщикам.Свернуть("ЗаказПоставщику, ИДОбъекта, Индекс, Поставщик", "Количество");
	Иначе
		ТаблицаЗаказовПоставщикам.Свернуть("ЗаказПоставщику, Индекс, Поставщик", "Количество");
	КонецЕсли; 
	
	СтруктураВозврата = Новый Структура();
	СтруктураВозврата.Вставить("ТаблицаПотребности", ТаблицаПотребности);
	СтруктураВозврата.Вставить("ТаблицаЗаказовПоставщикам", ТаблицаЗаказовПоставщикам);

	Возврат СтруктураВозврата;
	
КонецФункции

// Функция формирует дерево значений, с распределенными заказами поставщикам под потребности в номенклатуре
//  для нужд производства и отдела продаж
//
// Параметры
//  ТаблицаСтрокГруппировки             - ТаблицаЗначений, таблица потребностей в номенклатуре
//  ТаблицаЗаказовПоставщикам           - ТаблицаЗначений, заказы поставщикам, которые необходимо распределить по потребностям
//  СтруктураЗначенийГруппировок        - Структура, содержащая значения группировок, которыми должен быть ограничен вывод строк,
//                                        точнее это своеобразный отбор по значениям группировок дерева
//  Группировки                         - ТаблицаЗначений, группировки дерева, точнее в каком порядке и какую информацию надо
//                                        отображать в основной колонке дерева
//  ПоказыватьПустыеСтрокиНижнихУровней - Булево, показывать ли в дереве строки, у которых все подчиненные строки содержат пустые значения
//
// Возвращаемое значение:
//   ДеревоЗначений
//
Функция ПолучитьДеревоРаспределенияПотребностей(ТаблицаСтрокГруппировки, ТаблицаЗаказовПоставщикам, СтруктураЗначенийГруппировок, Группировки, ПоказыватьПустыеСтрокиНижнихУровней, МаксимальноеЗначениеПрогрессора = 0, ТекущееЗначениеПрогрессора = 0) Экспорт

	Дерево = Новый ДеревоЗначений;
	Дерево.Колонки.Добавить("ЗначениеГруппировки");
	Дерево.Колонки.Добавить("КоличествоПлана", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15,3)));
	Дерево.Колонки.Добавить("КоличествоФакта", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15,3)));
	Дерево.Колонки.Добавить("ХарактеристикаНоменклатуры", Новый ОписаниеТипов("СправочникСсылка.ХарактеристикиНоменклатуры"));
	Дерево.Колонки.Добавить("ИДОбъекта", Новый ОписаниеТипов("Строка",, Новый КвалификаторыСтроки(50)));
	Дерево.Колонки.Добавить("Тара", Новый ОписаниеТипов("Булево"));
	
	МаксимальноеЗначениеПрогрессора = ТаблицаСтрокГруппировки.Количество();
	
	СформироватьСтрокиДерева(Дерево, ТаблицаСтрокГруппировки, ТаблицаЗаказовПоставщикам, 0, СтруктураЗначенийГруппировок, Группировки, ПоказыватьПустыеСтрокиНижнихУровней, МаксимальноеЗначениеПрогрессора, ТекущееЗначениеПрогрессора);
	
	Возврат Дерево;

КонецФункции

// Процедура рекурсивно формирует строки дерева для функции ПолучитьДеревоРаспределенияПотребностей
//
// Параметры
//  СтрокаДереваПрошлогоУровня          - СтрокаДереваЗначений, строки подчиненные которой необходимо формировать
//  ТаблицаСтрокГруппировки             - ТаблицаЗначений, таблица потребностей в номенклатуре
//  ТаблицаЗаказовПоставщикам           - ТаблицаЗначений, заказы поставщикам, которые необходимо распределить по потребностям
//  ИндексГруппировки                   - Число, индекс группировки, строки для которой мы формируем
//  СтруктураЗначенийГруппировок        - Структура, содержащая значения группировок, которыми должен быть ограничен вывод строк,
//                                        точнее это своеобразный отбор по значениям группировок дерева
//  Группировки                         - ТаблицаЗначений, группировки дерева, точнее в каком порядке и какую информацию надо
//                                        отображать в основной колонке дерева
//  ПоказыватьПустыеСтрокиНижнихУровней - Булево, показывать ли в дереве строки, у которых все подчиненные строки содержат пустые значения
//
//
// Возвращаемое значение:
//   НЕТ
//
Процедура СформироватьСтрокиДерева(СтрокаДереваПрошлогоУровня, ТаблицаСтрокГруппировки, ТаблицаЗаказовПоставщикам, ИндексГруппировки, СтруктураЗначенийГруппировок, Группировки, ПоказыватьПустыеСтрокиНижнихУровней, МаксимальноеЗначениеПрогрессора, ТекущееЗначениеПрогрессора)

	Если ИндексГруппировки > Группировки.Количество() - 1 Тогда
		Возврат;
	КонецЕсли; 
	
	Если ТаблицаСтрокГруппировки <> МаксимальноеЗначениеПрогрессора И ИндексГруппировки = 1 Тогда
		ТекущееЗначениеПрогрессора = ТекущееЗначениеПрогрессора + ТаблицаСтрокГруппировки.Количество();
	КонецЕсли; 
	
	ИмяГруппировки = Группировки[ИндексГруппировки].Имя;
	
	Если ИмяГруппировки = "Поставщик" ИЛИ ИмяГруппировки = "ЗаказПоставщику" ИЛИ ИмяГруппировки = "ИДОбъекта" Тогда
		// Поля правой таблицы
		
		ЗначенияГруппировки = Неопределено;
		СтруктураЗначенийГруппировок.Свойство(ИмяГруппировки, ЗначенияГруппировки);
		Если ЗначенияГруппировки = Неопределено Тогда
			Возврат;
		КонецЕсли;
		
		ТаблицаСтрокДаннойГруппировки = ТаблицаСтрокГруппировки.Скопировать();
		ТекущаяТаблицаЗаказовПоставщикам = ТаблицаЗаказовПоставщикам.Скопировать();
		ИндексСтроки = 0;
		Пока Истина Цикл
		
			Если ИндексСтроки > ТекущаяТаблицаЗаказовПоставщикам.Количество() - 1 Тогда
				Прервать;
			КонецЕсли;
			
			СтрокаТекущейТаблицыЗаказовПоставщикам = ТекущаяТаблицаЗаказовПоставщикам[ИндексСтроки];
			
			Если ТаблицаСтрокДаннойГруппировки.Найти(СтрокаТекущейТаблицыЗаказовПоставщикам.Индекс, "Индекс") = Неопределено Тогда
				ТекущаяТаблицаЗаказовПоставщикам.Удалить(СтрокаТекущейТаблицыЗаказовПоставщикам);
				Продолжить;
			КонецЕсли; 
			
			ИндексСтроки = ИндексСтроки + 1;
			
		КонецЦикла;
		
		Если ТекущаяТаблицаЗаказовПоставщикам.Количество() = 0 Тогда
			
			СтрокаДереваТекущегоУровня = СтрокаДереваПрошлогоУровня.Строки.Добавить();
			СтрокаДереваТекущегоУровня.ЗначениеГруппировки = "<...>";
			
			СформироватьСтрокиДерева(СтрокаДереваТекущегоУровня, ТаблицаСтрокДаннойГруппировки, ТекущаяТаблицаЗаказовПоставщикам, (ИндексГруппировки + 1), СтруктураЗначенийГруппировок, Группировки, ПоказыватьПустыеСтрокиНижнихУровней, МаксимальноеЗначениеПрогрессора, ТекущееЗначениеПрогрессора);
			
			Если ТипЗнч(СтрокаДереваПрошлогоУровня) = Тип("СтрокаДереваЗначений") Тогда
				СтрокаДереваПрошлогоУровня.КоличествоФакта = СтрокаДереваПрошлогоУровня.КоличествоФакта + СтрокаДереваТекущегоУровня.КоличествоФакта;
			КонецЕсли; 
			
			Если НЕ ПоказыватьПустыеСтрокиНижнихУровней И СтрокаДереваТекущегоУровня.Строки.Количество() = 0 Тогда
				СтрокаДереваПрошлогоУровня.Строки.Удалить(СтрокаДереваТекущегоУровня);
			КонецЕсли; 
			
		Иначе
			
			// Сначала обработаем те строки левой таблицы, которых нет в правой
			
			КопияТаблицаСтрокДаннойГруппировки = ТаблицаСтрокДаннойГруппировки.Скопировать();
			ИндексСтроки = 0;
			Пока Истина Цикл
			
				Если ИндексСтроки > КопияТаблицаСтрокДаннойГруппировки.Количество() - 1 Тогда
					Прервать;
				КонецЕсли; 
				
				СтрокаКопияТаблицаСтрокДаннойГруппировки = КопияТаблицаСтрокДаннойГруппировки[ИндексСтроки];
			
				Если ТаблицаЗаказовПоставщикам.Найти(СтрокаКопияТаблицаСтрокДаннойГруппировки.Индекс, "Индекс") <> Неопределено Тогда
					КопияТаблицаСтрокДаннойГруппировки.Удалить(СтрокаКопияТаблицаСтрокДаннойГруппировки);
					Продолжить;
				КонецЕсли; 
				
				ИндексСтроки = ИндексСтроки + 1;
			
			КонецЦикла;
			
			Если КопияТаблицаСтрокДаннойГруппировки.Количество() > 0 Тогда
			
				СтрокаДереваТекущегоУровня = СтрокаДереваПрошлогоУровня.Строки.Добавить();
				СтрокаДереваТекущегоУровня.ЗначениеГруппировки = "<...>";
				
				ТекущаяТаблицаЗаказовПоставщикам = ТаблицаЗаказовПоставщикам.Скопировать();
				
				СформироватьСтрокиДерева(СтрокаДереваТекущегоУровня, КопияТаблицаСтрокДаннойГруппировки, ТекущаяТаблицаЗаказовПоставщикам, (ИндексГруппировки + 1), СтруктураЗначенийГруппировок, Группировки, ПоказыватьПустыеСтрокиНижнихУровней, МаксимальноеЗначениеПрогрессора, ТекущееЗначениеПрогрессора);
				
				Если ТипЗнч(СтрокаДереваПрошлогоУровня) = Тип("СтрокаДереваЗначений") Тогда
					СтрокаДереваПрошлогоУровня.КоличествоФакта = СтрокаДереваПрошлогоУровня.КоличествоФакта + СтрокаДереваТекущегоУровня.КоличествоФакта;
				КонецЕсли; 
			
				Если НЕ ПоказыватьПустыеСтрокиНижнихУровней И СтрокаДереваТекущегоУровня.Строки.Количество() = 0 Тогда
					СтрокаДереваПрошлогоУровня.Строки.Удалить(СтрокаДереваТекущегоУровня);
				КонецЕсли; 
			
			КонецЕсли;
			
			Для каждого ЗначениеГруппировки Из ЗначенияГруппировки Цикл
				
				Если ТекущаяТаблицаЗаказовПоставщикам.Найти(ЗначениеГруппировки, ИмяГруппировки) = Неопределено Тогда
					Продолжить;
				КонецЕсли;
				
				СтрокиТекущейТаблицыЗаказовПоставщикам = ТекущаяТаблицаЗаказовПоставщикам.НайтиСтроки(Новый Структура(ИмяГруппировки, ЗначениеГруппировки));
				
				КопияТаблицаСтрокДаннойГруппировки = ТаблицаСтрокДаннойГруппировки.Скопировать();
			
				ИндексСтрокиКопии = 0;
				Пока Истина Цикл
					
					Если ИндексСтрокиКопии > КопияТаблицаСтрокДаннойГруппировки.Количество() - 1 Тогда
						Прервать;
					КонецЕсли; 
					
					СтрокаКопии = КопияТаблицаСтрокДаннойГруппировки[ИндексСтрокиКопии];
					
					НайденаСтрокаПоИндексу = Ложь;
					Для каждого Строка Из СтрокиТекущейТаблицыЗаказовПоставщикам Цикл
						Если Строка.Индекс = СтрокаКопии.Индекс Тогда
							НайденаСтрокаПоИндексу = Истина;
							Прервать;
						КонецЕсли; 
					КонецЦикла;
					
					Если НайденаСтрокаПоИндексу = Ложь Тогда
						КопияТаблицаСтрокДаннойГруппировки.Удалить(СтрокаКопии);
						Продолжить;
					КонецЕсли;
					
					ИндексСтрокиКопии = ИндексСтрокиКопии + 1;
				
				КонецЦикла;
				
				КоличествоФакта = 0;
				КопияТаблицаЗаказовПоставщикам = ТекущаяТаблицаЗаказовПоставщикам.Скопировать();
				ИндексСтроки = 0;
				Пока Истина Цикл
					
					Если ИндексСтроки > КопияТаблицаЗаказовПоставщикам.Количество() - 1 Тогда
						Прервать;
					КонецЕсли;
					
					СтрокаКопияТаблицаЗаказовПоставщикам = КопияТаблицаЗаказовПоставщикам[ИндексСтроки];
					
					Если СтрокаКопияТаблицаЗаказовПоставщикам[ИмяГруппировки] <> ЗначениеГруппировки Тогда
						КопияТаблицаЗаказовПоставщикам.Удалить(СтрокаКопияТаблицаЗаказовПоставщикам);
						Продолжить;
					КонецЕсли; 
					
					КоличествоФакта = КоличествоФакта + СтрокаКопияТаблицаЗаказовПоставщикам.Количество;
					
					ИндексСтроки = ИндексСтроки + 1;
					
				КонецЦикла; 
				
				Если (ИмяГруппировки = "ЗаказПоставщику" ИЛИ ИмяГруппировки = "ИДОбъекта") И КоличествоФакта <= 0 Тогда
					Продолжить;
				КонецЕсли; 
				СтрокаДереваТекущегоУровня = СтрокаДереваПрошлогоУровня.Строки.Добавить();
				СтрокаДереваТекущегоУровня.ЗначениеГруппировки = ЗначениеГруппировки;
				Если ИмяГруппировки = "ЗаказПоставщику" ИЛИ ИмяГруппировки = "ИДОбъекта" Тогда
					СтрокаДереваТекущегоУровня.КоличествоФакта = КоличествоФакта;
					СтрокаЗаказаПоставщику = ТекущаяТаблицаЗаказовПоставщикам.Найти(ЗначениеГруппировки, ИмяГруппировки);
					Если СтрокаЗаказаПоставщику <> Неопределено И ТекущаяТаблицаЗаказовПоставщикам.Колонки.Найти("ИДОбъекта") <> Неопределено Тогда
						СтрокаДереваТекущегоУровня.ИДОбъекта = СтрокаЗаказаПоставщику.ИДОбъекта;
					КонецЕсли; 
				КонецЕсли;
				
				СформироватьСтрокиДерева(СтрокаДереваТекущегоУровня, КопияТаблицаСтрокДаннойГруппировки, КопияТаблицаЗаказовПоставщикам, (ИндексГруппировки + 1), СтруктураЗначенийГруппировок, Группировки, ПоказыватьПустыеСтрокиНижнихУровней, МаксимальноеЗначениеПрогрессора, ТекущееЗначениеПрогрессора);
				
				Если ТипЗнч(СтрокаДереваПрошлогоУровня) = Тип("СтрокаДереваЗначений") Тогда
					СтрокаДереваПрошлогоУровня.КоличествоФакта = СтрокаДереваПрошлогоУровня.КоличествоФакта + СтрокаДереваТекущегоУровня.КоличествоФакта;
				КонецЕсли; 
				
				Если ИмяГруппировки = "Поставщик" И НЕ ПоказыватьПустыеСтрокиНижнихУровней И СтрокаДереваТекущегоУровня.Строки.Количество() = 0 Тогда
					СтрокаДереваПрошлогоУровня.Строки.Удалить(СтрокаДереваТекущегоУровня);
				КонецЕсли; 
			
			КонецЦикла;
			
		КонецЕсли; 
		
	Иначе
		// Поля левой таблицы
		
		ЗначенияГруппировки = Неопределено;
		СтруктураЗначенийГруппировок.Свойство(ИмяГруппировки, ЗначенияГруппировки);
		Если ЗначенияГруппировки = Неопределено Тогда
			Возврат;
		КонецЕсли;
		
		Для каждого ЗначениеГруппировки Из ЗначенияГруппировки Цикл
			
			ТекЗначениеГруппировки = ЗначениеГруппировки;
			
			Если ИмяГруппировки = "Номенклатура" Тогда
				Если ТаблицаСтрокГруппировки.НайтиСтроки(Новый Структура("Номенклатура, ХарактеристикаНоменклатуры, Тара", ТекЗначениеГруппировки.Номенклатура, ТекЗначениеГруппировки.ХарактеристикаНоменклатуры, ТекЗначениеГруппировки.Тара)).Количество() = 0 Тогда
					Продолжить;
				КонецЕсли; 
			Иначе
				Если ТаблицаСтрокГруппировки.Найти(ТекЗначениеГруппировки, ИмяГруппировки) = Неопределено Тогда
					Продолжить;
				КонецЕсли; 
			КонецЕсли;
			
			СтрокаГруппировки = "";
			Если ИмяГруппировки = "Номенклатура" Тогда
				ТекЗначениеГруппировки = ЗначениеГруппировки.Номенклатура;
				ХарактеристикаНоменклатуры = ЗначениеГруппировки.ХарактеристикаНоменклатуры;
				Тара = ЗначениеГруппировки.Тара;
			Иначе
				СтрокаГруппировки = Строка(ЗначениеГруппировки);
			КонецЕсли;
			
			КоличествоПлана = 0;
			
			ТаблицаСтрокДаннойГруппировки = ТаблицаСтрокГруппировки.Скопировать();
			ИндексСтроки = 0;
			Пока Истина Цикл
			
				Если ИндексСтроки > ТаблицаСтрокДаннойГруппировки.Количество() - 1 Тогда
					Прервать;
				КонецЕсли;
				
				СтрокаТаблицы = ТаблицаСтрокДаннойГруппировки[ИндексСтроки];
				
				Если ИмяГруппировки = "Номенклатура" Тогда
					Если СтрокаТаблицы.Номенклатура <> ЗначениеГруппировки.Номенклатура ИЛИ СтрокаТаблицы.ХарактеристикаНоменклатуры <> ЗначениеГруппировки.ХарактеристикаНоменклатуры ИЛИ СтрокаТаблицы.Тара <> ЗначениеГруппировки.Тара Тогда
						ТаблицаСтрокДаннойГруппировки.Удалить(СтрокаТаблицы);
						Продолжить;
					КонецЕсли; 
				Иначе
					Если СтрокаТаблицы[ИмяГруппировки] <> ЗначениеГруппировки Тогда
						ТаблицаСтрокДаннойГруппировки.Удалить(СтрокаТаблицы);
						Продолжить;
					КонецЕсли; 
				КонецЕсли;
			
				КоличествоПлана = КоличествоПлана + СтрокаТаблицы.КоличествоПлана;
			
				ИндексСтроки = ИндексСтроки + 1;
				
			КонецЦикла;
			
			СтрокаДереваТекущегоУровня = СтрокаДереваПрошлогоУровня.Строки.Добавить();
			Если ИмяГруппировки = "Номенклатура" Тогда
				СтрокаДереваТекущегоУровня.ЗначениеГруппировки = ЗначениеГруппировки.Номенклатура;
				СтрокаДереваТекущегоУровня.ХарактеристикаНоменклатуры = ЗначениеГруппировки.ХарактеристикаНоменклатуры;
				СтрокаДереваТекущегоУровня.Тара = ЗначениеГруппировки.Тара;
			Иначе
				СтрокаДереваТекущегоУровня.ЗначениеГруппировки = ?(ПустаяСтрока(СтрокаГруппировки), ЗначениеГруппировки, СтрокаГруппировки);
				СтрокаДереваТекущегоУровня.ЗначениеГруппировки = ЗначениеГруппировки;
			КонецЕсли; 
			СтрокаДереваТекущегоУровня.КоличествоПлана = КоличествоПлана;
			
			СформироватьСтрокиДерева(СтрокаДереваТекущегоУровня, ТаблицаСтрокДаннойГруппировки, ТаблицаЗаказовПоставщикам, (ИндексГруппировки + 1), СтруктураЗначенийГруппировок, Группировки, ПоказыватьПустыеСтрокиНижнихУровней, МаксимальноеЗначениеПрогрессора, ТекущееЗначениеПрогрессора);
		
			Если ТипЗнч(СтрокаДереваПрошлогоУровня) = Тип("СтрокаДереваЗначений") Тогда
				СтрокаДереваПрошлогоУровня.КоличествоФакта = СтрокаДереваПрошлогоУровня.КоличествоФакта + СтрокаДереваТекущегоУровня.КоличествоФакта;
			КонецЕсли; 
			
			Если ТипЗнч(СтрокаДереваТекущегоУровня.ЗначениеГруппировки) = Тип("Дата") И СтрокаДереваТекущегоУровня.ЗначениеГруппировки = '00010101' И СтрокаДереваТекущегоУровня.Строки.Количество() = 0 Тогда
				СтрокаДереваПрошлогоУровня.Строки.Удалить(СтрокаДереваТекущегоУровня);
			КонецЕсли; 
			
		КонецЦикла;
		
	КонецЕсли; 
	
КонецПроцедуры

// Функция возвращает интервалы напоминаний и представлениями.
//
// Параметры
//  Нет
//
// Возвращаемое значение:
//  СписокЗначений
//
Функция ПолучитьСписокИнтерваловНапоминания() Экспорт

	СписокИнтервалов = Новый СписокЗначений;
	СписокИнтервалов.Добавить(5*60,"5 мин.");
	СписокИнтервалов.Добавить(10*60,"10 мин.");
	СписокИнтервалов.Добавить(15*60,"15 мин.");
	СписокИнтервалов.Добавить(30*60,"30 мин.");
	СписокИнтервалов.Добавить(1*60*60,"1 час");
	СписокИнтервалов.Добавить(2*60*60,"2 часа");
	СписокИнтервалов.Добавить(4*60*60,"4 часа");
	СписокИнтервалов.Добавить(8*60*60,"8 часов");
	СписокИнтервалов.Добавить(1*24*60*60,"1 день");
	СписокИнтервалов.Добавить(2*24*60*60,"2 дня");
	СписокИнтервалов.Добавить(3*24*60*60,"3 дня");
	СписокИнтервалов.Добавить(4*24*60*60,"4 дня");
	СписокИнтервалов.Добавить(1*7*24*60*60,"1 неделя");
	Возврат СписокИнтервалов;

КонецФункции