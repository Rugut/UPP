////////////////////////////////////////////////////////////////////////////////
// Вспомогательные процедуры, функции

////////////////////////////////////////////////////////////////////////////////
// Процедуры, функции объекта

////////////////////////////////////////////////////////////////////////////////
// Процедуры, функции для работы формы

#Если ТолстыйКлиентОбычноеПриложение Тогда

Функция ПодобратьОрганизацию(Организация) Экспорт
	
	Если Не ОбщегоНазначения.ЭтоДоступнаяОрганизация(Организация) Тогда
		//для пользователя не задана организация по умолчанию, подберем первую разрешенную
		Организация = РаботаСДиалогами.ПодобратьОрганизацию();
	КонецЕсли;

	Возврат Организация;
	
КонецФункции

Процедура ОбновитьДополнительно(ЭтаФорма) Экспорт
	
	// В этой конфигурации дополнительных действий не предусмотрено
	
КонецПроцедуры

Процедура ПриОткрытииДополнительно(ЭтаФорма, НазначаемоеДействие) Экспорт
	
	ЭтаФорма.ЭлементыФормы.ДействияФормы.Кнопки.Действия.Кнопки.Вставить(10);
	ЭтаФорма.ЭлементыФормы.ДействияФормы.Кнопки.Действия.Кнопки.Вставить(11, "Перейти", ТипКнопкиКоманднойПанели.Подменю);
	ЭтаФорма.ЭлементыФормы.ДействияФормы.Кнопки.Действия.Кнопки.Перейти.Кнопки.Добавить("СоответствиеПодразделенийИПодразделенийОрганизаций", ТипКнопкиКоманднойПанели.Действие, "Соответствие подразделений и подразделений организаций", НазначаемоеДействие);
	
КонецПроцедуры

Процедура ВыполнитьДополнительныеДействия(ЭтаФорма, Кнопка) Экспорт
	
	Если Кнопка.Имя = "СоответствиеПодразделенийИПодразделенийОрганизаций" Тогда
		
		Если ЭтаФорма.ЭлементыФормы.СправочникСписок.ТекущиеДанные = Неопределено Тогда
			Предупреждение("Не выбрано подразделение!");
		Иначе
			ФормаРегистра = РегистрыСведений.СоответствиеПодразделенийИПодразделенийОрганизаций.ПолучитьФормуСписка(, Кнопка);
			ФормаРегистра.РегистрСведенийСписок.Отбор.ПодразделениеОрганизации.Установить(ЭтаФорма.ЭлементыФормы.СправочникСписок.ТекущиеДанные.Ссылка);
			ФормаРегистра.РегистрСведенийСписок.Отбор.Организация.Установить(ЭтаФорма.ЭлементыФормы.СправочникСписок.ТекущиеДанные.Ссылка.Владелец);
			ФормаРегистра.ЭлементыФормы.РегистрСведенийСписок.НачальноеОтображениеСписка = НачальноеОтображениеСписка.Конец;
			ФормаРегистра.Открыть();
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Функция ВыполняетсяДополнительноеУсловие(ЭтаФорма) Экспорт
		
	НастройкиБухучетаДоступны = НастройкаПравДоступаПереопределяемый.ДоступнаРольРасчетчикаРегл();
	
	ЭтаФорма.ЭлементыФормы.ПанельПрочихДанных.Страницы.БухучетЗарплаты.Видимость = НастройкиБухучетаДоступны;

	Возврат НастройкиБухучетаДоступны;

КонецФункции

// Процедура получает из регистра сведений текущее отражение в учете
Процедура ПрочитатьОтражениеВУчете(ЭтаФорма, ЭтотОбъект) Экспорт

	Отбор = Новый Структура;
	Отбор.Вставить("ПодразделениеОрганизации", ЭтотОбъект.Ссылка);
	Отбор.Вставить("Организация", ЭтотОбъект.Ссылка.Владелец);
	
	СрезПоследних   = РегистрыСведений.УчетОсновногоЗаработкаРаботниковПодразделенияОрганизации.СрезПоследних(ТекущаяДата(), Отбор);
	
	НадписьОтражениеВБухучете = "";
	
	ПодпадаетПодЕНВД = Ложь;
	ОтражениеНачисленийПоУмолчанию = Справочники.СпособыОтраженияЗарплатыВРеглУчете.ОтражениеНачисленийПоУмолчанию;
	БольничныйЗаСчетРаботодателяПоУмолчанию = Справочники.СпособыОтраженияЗарплатыВРеглУчете.БольничныйЗаСчетРаботодателя;
	ПодразделенияОрганизацииДополнительный.ПолучитьСпособыОтраженияПоУмолчанию(ЭтотОбъект.Ссылка.Владелец, ОтражениеНачисленийПоУмолчанию, БольничныйЗаСчетРаботодателяПоУмолчанию, ПодпадаетПодЕНВД);
	
	Если ЭтотОбъект.ЭтоНовый() ИЛИ СрезПоследних.Количество() = 0 Тогда
		
		НадписьОтражениеВБухучете = "Зарплата сотрудников подразделения отражается как задано для предприятия в целом:" + Символы.ПС;
		//добавим представление способа отражения по умолчанию
		НадписьОтражениеВБухучете = НадписьОтражениеВБухучете + ПодразделенияОрганизацииДополнительный.ПолучитьПредставлениеСпособаОтражения(ОтражениеНачисленийПоУмолчанию) + Символы.ПС;
		Если ПодпадаетПодЕНВД Тогда
			НадписьОтражениеВБухучете = НадписьОтражениеВБухучете + ". Расходы полностью относятся к ЕНВД" + Символы.ПС;
		КонецЕсли;
		
	Иначе // зарегистированы данные в регистре
		
		Если ЗначениеЗаполнено(СрезПоследних[0].СпособОтраженияВБухучете) Тогда
			
			НадписьОтражениеВБухучете = "С " + Формат(СрезПоследних[0].Период, "ДЛФ=DD") + " зарплата сотрудников подразделения отражается как:" + Символы.ПС;
			//добавим представление способа отражения по умолчанию
			НадписьОтражениеВБухучете = НадписьОтражениеВБухучете + ПодразделенияОрганизацииДополнительный.ПолучитьПредставлениеСпособаОтражения(СрезПоследних[0].СпособОтраженияВБухучете);
			
			Если СрезПоследних[0].ПодпадаетПодЕНВД Тогда
				НадписьОтражениеВБухучете = НадписьОтражениеВБухучете + ". Расходы полностью относятся к ЕНВД" + Символы.ПС;
			Иначе
				НадписьОтражениеВБухучете = НадписьОтражениеВБухучете + Символы.ПС;
			КонецЕсли;
			
		Иначе // зарегистрирован пустой способ отражения
			
			НадписьОтражениеВБухучете = "С " + Формат(СрезПоследних[0].Период, "ДЛФ=DD") + " зарплата сотрудников подразделения отражается как задано для предприятия в целом:" + Символы.ПС;
			//добавим представление способа отражения по умолчанию
			НадписьОтражениеВБухучете = НадписьОтражениеВБухучете + ПодразделенияОрганизацииДополнительный.ПолучитьПредставлениеСпособаОтражения(ОтражениеНачисленийПоУмолчанию) + Символы.ПС;
			Если ПодпадаетПодЕНВД Тогда
				НадписьОтражениеВБухучете = НадписьОтражениеВБухучете + ". Расходы полностью относятся к ЕНВД" + Символы.ПС;
			КонецЕсли;
			
		КонецЕсли;
		
		ДатаНачалаРеформыСоцСтрахования = ПроведениеРасчетов.ДатаНачалаРеформыСоцСтрахования();
		Если СрезПоследних[0].Период < ДатаНачалаРеформыСоцСтрахования Тогда
			
			Если ЗначениеЗаполнено(СрезПоследних[0].СпособОтраженияВБухучетеБольничногоЗаСчетРаботодателя) Тогда
				
				//добавим описание отражения в учете доли б/л за счет работодателя
				НадписьОтражениеВБухучете = НадписьОтражениеВБухучете + Символы.ПС + "До 2011 года доля больничных листов, оплачиваемая за счет работодателя, отражается как:" + Символы.ПС;
				//добавим представление способа отражения доли б/л за счет работодателя
				НадписьОтражениеВБухучете = НадписьОтражениеВБухучете + ПодразделенияОрганизацииДополнительный.ПолучитьПредставлениеСпособаОтражения(СрезПоследних[0].СпособОтраженияВБухучетеБольничногоЗаСчетРаботодателя) + Символы.ПС;
				
			Иначе //не зарегистрировано отражение в учетете доли б/л
				
				//добавим описание отражения в учете доли б/л за счет работодателя
				НадписьОтражениеВБухучете = НадписьОтражениеВБухучете + Символы.ПС + "До 2011 года доля больничных листов, оплачиваемая за счет работодателя, отражается как задано для предприятия в целом:";
				//добавим представление способа отражения доли б/л за счет работодателя
				НадписьОтражениеВБухучете = НадписьОтражениеВБухучете + ПодразделенияОрганизацииДополнительный.ПолучитьПредставлениеСпособаОтражения(БольничныйЗаСчетРаботодателяПоУмолчанию) + Символы.ПС;
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
	ЭтаФорма.ЭлементыФормы.НадписьОтражениеВБухучете.Заголовок = НадписьОтражениеВБухучете;

КонецПроцедуры // ПрочитатьОтражениеВУчете() 

#КонецЕсли