///////////////////////////////////////////////////////////////////////////////
// ПРОГРАММНЫЙ ИНТЕРФЕЙС

// Функция осуществляет подключение устройства.
//
// Параметры:
//  ОбъектДрайвера   - <*>
//           - ОбъектДрайвера драйвера торгового оборудования.
//
// Возвращаемое значение:
//  <Булево> - Результат работы функции.
//
Функция ПодключитьУстройство(ОбъектДрайвера, Параметры, ПараметрыПодключения, ВыходныеПараметры) Экспорт

	Результат = Истина;
	ПараметрыПодключения.Вставить("СерийныйНомерККМ", "0");
	ПараметрыПодключения.Вставить("ИДУстройства", "");
	ПараметрыПодключения.Вставить("ФРПодключен", Ложь);
  	ПараметрыПодключения.Вставить("СтрокаЛога", "");

	//Обязательные выходные
	ВыходныеПараметры = Новый Массив();

	// Проверка настроенных параметров
	КодСимволаЧастичногоОтреза = Неопределено;
	НомерСекции                = Неопределено;
	Модель                     = Неопределено;

	Параметры.Свойство("КодСимволаЧастичногоОтреза", КодСимволаЧастичногоОтреза);
	Параметры.Свойство("НомерСекции"               , НомерСекции);
	Параметры.Свойство("Модель"                    , Модель);

	Если КодСимволаЧастичногоОтреза = Неопределено
	 Или НомерСекции                = Неопределено
	 Или Модель                     = Неопределено Тогда
		Результат = Ложь;
		ВыходныеПараметры.Добавить(999);
		ВыходныеПараметры.Добавить(НСтр("ru='Не настроены параметры устройства.
		|Для корректной работы устройства необходимо задать параметры его работы.
		|Сделать это можно при помощи формы ""Настройка параметров"" модели
		|подключаемого оборудования в форме ""Подключение и настройка оборудования"".'"));
	КонецЕсли;
	// Конец: Проверка настроенных параметров

	Если Результат Тогда
		Если ПараметрыПодключения.ФРПодключен Тогда
			ВыходныеПараметры.Добавить(999);
			ВыходныеПараметры.Добавить(НСтр("ru='Фискальный регистратор уже подключен'"));
			Результат = Ложь;
		Иначе
			ПараметрыПодключения.ФРПодключен = Истина;

			ВыходныеПараметры.Добавить(""); // Источник событий
			ВыходныеПараметры.Добавить(Неопределено); // Список событий
		КонецЕсли;
	КонецЕсли;

	Возврат Результат;

КонецФункции

// Функция осуществляет отключение устройства.
//
// Параметры:
//  ОбъектДрайвера - <*>
//         - ОбъектДрайвера драйвера торгового оборудования.
//
// Возвращаемое значение:
//  <Булево> - Результат работы функции.
//
Функция ОтключитьУстройство(ОбъектДрайвера, Параметры, ПараметрыПодключения, ВыходныеПараметры) Экспорт

	Результат = Истина;

	//Обязательные выходные
	ВыходныеПараметры = Неопределено;

	ПараметрыПодключения.ФРПодключен = Ложь;

	Возврат Результат;

КонецФункции

// Функция получает, обрабатывает и перенаправляет на исполнение команду к драйверу
//
Функция ВыполнитьКоманду(Команда, ВходныеПараметры = Неопределено, ВыходныеПараметры = Неопределено,
                         ОбъектДрайвера, Параметры, ПараметрыПодключения) Экспорт

	Результат = Истина;

	ВыходныеПараметры = Новый Массив();

	// Открыть смену
	Если Команда = "OpenDay" Тогда
		Результат = ОткрытьСмену(ОбъектДрайвера, Параметры, ПараметрыПодключения, ВыходныеПараметры);

	// Печать чека
	ИначеЕсли Команда = "PrintReceipt" Тогда
		Результат = ПечатьЧека(ОбъектДрайвера, Параметры, ПараметрыПодключения, ВходныеПараметры,	ВыходныеПараметры);

	// Печать слип чека
	ИначеЕсли Команда = "PrintText" Тогда
		СтрокаТекста = ВходныеПараметры[0];
		Результат = ПечатьТекста(ОбъектДрайвера, Параметры, ПараметрыПодключения,
		                         СтрокаТекста, ВыходныеПараметры);

	// Печать чека внесения/выемки
	ИначеЕсли Команда = "Encash" Тогда
		ТипИнкассации = ВходныеПараметры[0];
		Сумма         = ВходныеПараметры[1];
		Результат = Инкассация(ОбъектДрайвера, Параметры, ПараметрыПодключения, ТипИнкассации, Сумма, ВыходныеПараметры);

	// Печать отчета без гашения
	ИначеЕсли Команда = "PrintXReport" Тогда
		Результат = НапечататьОтчетБезГашения(ОбъектДрайвера, Параметры, ПараметрыПодключения, ВыходныеПараметры);

	// Печать отчета с гашением
	ИначеЕсли Команда = "PrintZReport" Тогда
		Результат = НапечататьОтчетСГашением(ОбъектДрайвера, Параметры, ПараметрыПодключения, ВыходныеПараметры);
		
	// Открытие денежного ящика
	ИначеЕсли Команда = "OpenCashDrawer" Тогда
		Результат = ОткрытьДенежныйЯщик(ОбъектДрайвера, Параметры, ПараметрыПодключения, ВыходныеПараметры);

	// Указанная команда не поддерживается данным драйвером
	Иначе
		ВыходныеПараметры.Добавить(999);
		ВыходныеПараметры.Добавить(НСтр("ru='Команда ""%Команда%"" не поддерживается данным драйвером.'"));
		ВыходныеПараметры[1] = СтрЗаменить(ВыходныеПараметры[1], "%Команда%", Команда);
		Результат = Ложь;

	КонецЕсли;

	Возврат Результат;

КонецФункции

///////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

// Функция осуществляет открытие смены
Функция ОткрытьСмену(ОбъектДрайвера, Параметры, ПараметрыПодключения, ВыходныеПараметры)

	Результат = Истина;
	СтрокаЛога = "";

	СтрокаЛога = "----- " + НСтр("ru='Смена открыта'") + " -----";

	// Заполнение выходных параметров
	ВыходныеПараметры.Добавить(0);
	ВыходныеПараметры.Добавить(0);
	ВыходныеПараметры.Добавить(0);
	ВыходныеПараметры.Добавить(ТекущаяДата());

	СообщитьПользователю(СтрокаЛога);

	Возврат Результат;

КонецФункции

// Осуществляет печать фискального чека
Функция ПечатьЧека(ОбъектДрайвера, Параметры, ПараметрыПодключения, ВходныеПараметры, ВыходныеПараметры)

	ПараметрыПодключения.СтрокаЛога = "";

	Результат = МенеджерОборудованияКлиентСерверПереопределяемый.ПечатьЧека(ПодключаемоеОборудование1СФискальныйРегистраторЭмулятор,
		ОбъектДрайвера, Параметры, ПараметрыПодключения, ВходныеПараметры, ВыходныеПараметры);
		
	СообщитьПользователю(ПараметрыПодключения.СтрокаЛога);

	Возврат Результат;
	
КонецФункции

// Функция осуществляет печать слип-чека
Функция ПечатьТекста(ОбъектДрайвера, Параметры, ПараметрыПодключения,
                       СтрокаТекста, ВыходныеПараметры)

	Результат  = Истина;

	// Открываем чек
	Результат = ОткрытьЧек(ОбъектДрайвера, Параметры, ПараметрыПодключения, Ложь, Ложь, ВыходныеПараметры);

	// Печатаем строки чека
	Если Результат Тогда
		Для НомерСтроки = 1 По СтрЧислоСтрок(СтрокаТекста) Цикл
			ВыделеннаяСтрока = СтрПолучитьСтроку(СтрокаТекста, НомерСтроки);
			Если Найти(ВыделеннаяСтрока, Символ(Параметры.КодСимволаЧастичногоОтреза)) > 0 Тогда
				ТаблицаОплат = Новый Массив();
				Результат = ЗакрытьЧек(ОбъектДрайвера, Параметры, ПараметрыПодключения, ТаблицаОплат, ВыходныеПараметры);
				Результат = ОткрытьЧек(ОбъектДрайвера, Параметры, ПараметрыПодключения, Ложь, Ложь, ВыходныеПараметры);
			Иначе
				Если НЕ НапечататьНефискальнуюСтроку(ОбъектДрайвера, Параметры, ПараметрыПодключения,
				                                     ВыделеннаяСтрока, ВыходныеПараметры) Тогда
					Прервать;
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;

	// Закрываем чек
	Если Результат Тогда
		ТаблицаОплат = Новый Массив();
		Результат = ЗакрытьЧек(ОбъектДрайвера, Параметры, ПараметрыПодключения, ТаблицаОплат, ВыходныеПараметры);
	КонецЕсли;

	СообщитьПользователю(ПараметрыПодключения.СтрокаЛога);
	
	Возврат Результат;

КонецФункции

// Функция осуществляет открытие нового чека.
//
Функция ОткрытьЧек(ОбъектДрайвера, Параметры, ПараметрыПодключения, ЧекВозврата, ФискальныйЧек, ВыходныеПараметры) Экспорт

	Результат  = Истина;
	СтрокаЛога = "";

	мНомерСмены = 1;
	мНомерЧека  = 1;

	СтрокаЛога = СтрокаЛога + "===== " + ?(ЧекВозврата, НСтр("ru='Открытие чека возврата'"), НСтр("ru='Открытие чека продажи'")) + " =====" + Символы.ПС;

	СтрокаЛога = СтрокаЛога + "=====" + ?(ФискальныйЧек, "== " + НСтр("ru='Фискальный режим'"), " " + НСтр("ru='Нефискальный режим'")) + "=====" + Символы.ПС;
	
	СтрокаЛога = СтрокаЛога + НСтр("ru='№ чека'")   + ": " + Формат(мНомерЧека,  "ЧЦ=4; ЧН=0; ЧВН=")
	           + "    "
	           + НСтр("ru='№ смены'") + ": " + Формат(мНомерСмены, "ЧЦ=4; ЧН=0; ЧВН=") + Символы.ПС;

	НомерСмены = мНомерСмены;
	НомерЧека  = мНомерЧека;

	// Заполнение выходных параметров
	ВыходныеПараметры.Добавить(НомерСмены);
	ВыходныеПараметры.Добавить(НомерЧека);
	ВыходныеПараметры.Добавить(0);
	ВыходныеПараметры.Добавить(ТекущаяДата());

	ПараметрыПодключения.Вставить("ФискальныйЧек", ФискальныйЧек);
	ПараметрыПодключения.Вставить("ЧекОткрыт"    , Истина);
	ПараметрыПодключения.Вставить("ИтогЧека"     , 0);

	ПараметрыПодключения.СтрокаЛога = ПараметрыПодключения.СтрокаЛога + СтрокаЛога;
	Возврат Результат;

КонецФункции

// Функция осуществляет печать фискальной строки.
//
Функция НапечататьФискальнуюСтроку(ОбъектДрайвера, Параметры, ПараметрыПодключения,
                                   Наименование, Количество, Цена, ПроцентСкидки, Сумма,
                                   НомерСекции, СтавкаНДС, ВыходныеПараметры) Экспорт

	Результат = Истина;
	СтрокаЛога = "";

	Если Не ПараметрыПодключения.ЧекОткрыт Тогда
		ВыходныеПараметры.Очистить();
		ВыходныеПараметры.Добавить(999);
		ВыходныеПараметры.Добавить(НСтр("ru='Чек не был открыт'"));
		Результат = Ложь;
	Иначе
		// Печать строки чека
		СтрокаЛога = ".............................." + Символы.ПС;
		СтрокаЛога = СтрокаЛога + Наименование + Символы.ПС;
		СтрокаЛога = СтрокаЛога + Строка(Количество) + " * " + Строка(Цена) + " = " + Формат(Количество*Цена, "ЧЦ=15; ЧДЦ=2; ЧН=0; ЧГ=0") + Символы.ПС;

		СкидкаНаценка = Сумма - Цена*Количество;
		Если СкидкаНаценка < 0 Тогда
			СтрокаЛога = СтрокаЛога + НСтр("ru='Скидка'") + ": " + Формат(-СкидкаНаценка, "ЧЦ=15; ЧДЦ=2; ЧН=0; ЧГ=0")
			           + " (" + ПроцентСкидки + "%)" + Символы.ПС;
		Иначе
			СтрокаЛога = СтрокаЛога + НСтр("ru='Наценка'") + ": " + Формат(СкидкаНаценка, "ЧЦ=15; ЧДЦ=2; ЧН=0; ЧГ=0")
			           + " (" + ПроцентСкидки + "%)" + Символы.ПС;
		КонецЕсли;
		СтрокаЛога = СтрокаЛога + НСтр("ru='Отдел'") + ": №" + НомерСекции + Символы.ПС;
		Если СтавкаНДС > 0 Тогда
			СтрокаЛога = СтрокаЛога + НСтр("ru='НДС'") + ": " + СтавкаНДС + "%" + Символы.ПС;
		КонецЕсли;
		СтрокаЛога = СтрокаЛога + ".............................." + Символы.ПС;
		ПараметрыПодключения.ИтогЧека = ПараметрыПодключения.ИтогЧека + Сумма;
	КонецЕсли;

	ПараметрыПодключения.СтрокаЛога = ПараметрыПодключения.СтрокаЛога + СтрокаЛога;
	Возврат Результат;

КонецФункции

// Функция осуществляет печать нефискальной строки.
//
Функция НапечататьНефискальнуюСтроку(ОбъектДрайвера, Параметры, ПараметрыПодключения, СтрокаТекста, ВыходныеПараметры) Экспорт

	Результат = Истина;

	ПараметрыПодключения.СтрокаЛога = ПараметрыПодключения.СтрокаЛога + СтрокаТекста + Символы.ПС;

	Возврат Результат;

КонецФункции

// Функция осуществляет закрытие ранее открытого чека.
//
Функция ЗакрытьЧек(ОбъектДрайвера, Параметры, ПараметрыПодключения, ТаблицаОплат, ВыходныеПараметры) Экспорт

	Результат = Истина;
	СтрокаЛога = "";

	СуммаНаличнойОплаты     = 0;
	СуммаБезналичнойОплаты1 = 0;
	СуммаБезналичнойОплаты2 = 0;

	Для ИндексОплаты = 0 По ТаблицаОплат.Количество() - 1 Цикл
		Если ТаблицаОплат[ИндексОплаты][0].Значение = 0 Тогда
			СуммаНаличнойОплаты = СуммаНаличнойОплаты + ТаблицаОплат[ИндексОплаты][1].Значение;
		ИначеЕсли ТаблицаОплат[ИндексОплаты][0].Значение = 1 Тогда
			СуммаБезналичнойОплаты1 = СуммаБезналичнойОплаты1 + ТаблицаОплат[ИндексОплаты][1].Значение;
		Иначе
			СуммаБезналичнойОплаты2 = СуммаБезналичнойОплаты2 + ТаблицаОплат[ИндексОплаты][1].Значение;
		КонецЕсли;
	КонецЦикла;

	Если Не ПараметрыПодключения.ЧекОткрыт Тогда
		ВыходныеПараметры.Очистить();
		ВыходныеПараметры.Добавить(999);
		ВыходныеПараметры.Добавить(НСтр("ru='Чек не был открыт'"));
		Результат = Ложь;
	Иначе
		Если ПараметрыПодключения.ФискальныйЧек Тогда
			СтрокаЛога = СтрокаЛога + НСтр("ru='ИТОГО'") + ": " + Формат(ПараметрыПодключения.ИтогЧека, "ЧЦ=15; ЧДЦ=2; ЧН=0; ЧГ=0") + Символы.ПС;
			СтрокаЛога = СтрокаЛога + НСтр("ru='Оплачено наличными'") + ": "      + СуммаНаличнойОплаты + Символы.ПС;
			СтрокаЛога = СтрокаЛога + НСтр("ru='Оплачено безналичными 1'") + ": " + СуммаБезналичнойОплаты1 + Символы.ПС;
			СтрокаЛога = СтрокаЛога + НСтр("ru='Оплачено безналичными 2'") + ": " + СуммаБезналичнойОплаты2 + Символы.ПС;
			СтрокаЛога = СтрокаЛога + НСтр("ru='Сдача'") + ": "
			                        + Строка(СуммаНаличнойОплаты
			                               + СуммаБезналичнойОплаты1
			                               + СуммаБезналичнойОплаты2
			                               - ПараметрыПодключения.ИтогЧека) + Символы.ПС;
		КонецЕсли;
		СтрокаЛога = СтрокаЛога + "===== "
		                        + ?(ПараметрыПодключения.ФискальныйЧек,
		                            НСтр("ru='Закрытие фискального чека'"),
		                            НСтр("ru='Закрытие нефискального чека'"))
		                        + " =====" + Символы.ПС;
		ПараметрыПодключения.ЧекОткрыт = Ложь;
	КонецЕсли;

	ПараметрыПодключения.СтрокаЛога = ПараметрыПодключения.СтрокаЛога + СтрокаЛога;
	Возврат Результат;

КонецФункции

// Функция осуществляет внесение или выемку суммы на ФР.
//
Функция Инкассация(ОбъектДрайвера, Параметры, ПараметрыПодключения, ТипИнкассации, Сумма, ВыходныеПараметры)

	Результат = Истина;

	СообщитьПользователю(?(ТипИнкассации = 1, НСтр("ru='Внесение на сумму'"), НСтр("ru='Выемка на сумму'")) + " " + Сумма);

	// Заполнение выходных параметров
	ВыходныеПараметры.Добавить(1);
	ВыходныеПараметры.Добавить(1);
	ВыходныеПараметры.Добавить(0);
	ВыходныеПараметры.Добавить(ТекущаяДата());

	Возврат Результат;

КонецФункции

// Функция осуществляет снятие отчёта без гашения.
//
Функция НапечататьОтчетБезГашения(ОбъектДрайвера, Параметры, ПараметрыПодключения, ВыходныеПараметры)

	Результат = Истина;

	СообщитьПользователю(НСтр("ru='Печать отчета без гашения'"));

	// Заполнение выходных параметров
	ВыходныеПараметры.Добавить(1);
	ВыходныеПараметры.Добавить(1);
	ВыходныеПараметры.Добавить(0);
	ВыходныеПараметры.Добавить(ТекущаяДата());

	Возврат Результат;

КонецФункции

// Функция осуществляет снятие отчёта с гашением.
//
Функция НапечататьОтчетСГашением(ОбъектДрайвера, Параметры, ПараметрыПодключения, ВыходныеПараметры)

	Результат = Истина;

	СообщитьПользователю(НСтр("ru='Печать отчета с гашением'") + Символы.ПС
	                      + "----- " + НСтр("ru='Смена закрыта'") + " -----");

	// Заполнение выходных параметров
	ВыходныеПараметры.Добавить(1);
	ВыходныеПараметры.Добавить(1);
	ВыходныеПараметры.Добавить(0);
	ВыходныеПараметры.Добавить(ТекущаяДата());

	Возврат Результат;

КонецФункции

// Функция осуществляет открытие денежного ящика
//
Функция ОткрытьДенежныйЯщик(ОбъектДрайвера, Параметры, ПараметрыПодключения, ВыходныеПараметры)

	Результат = Истина;

	СообщитьПользователю(НСтр("ru='Денежный ящик открыт'"));

	Возврат Результат;

КонецФункции

// Формирует и выводит сообщение 
//
Процедура СообщитьПользователю(ТекстСообщения)
	
	ПоказатьОповещениеПользователя(НСтр("ru = '1С: Фискальный регистратор (эмулятор)'"), , ТекстСообщения);
	
КонецПроцедуры