// Функция формирует список интерфейсов для выбора из списка
//
// Параметры
//  Элемент - Элемент управления, в котором производится выбор из списка
//                 
// Возвращаемое значение:
//   СписокЗначений   - список значений Метаданные.Интерфейсы
//
Функция ПолучитьСписокВыбораИнтерфейсов(Элемент) Экспорт
	
	СписокИсключаемыхИнтерфейсов = Новый Массив;
	СписокИсключаемыхИнтерфейсов.Добавить(Метаданные.Интерфейсы.Общий);
	СписокИсключаемыхИнтерфейсов.Добавить(Метаданные.Интерфейсы.ОбщийАдминистратора);
	СписокИсключаемыхИнтерфейсов.Добавить(Метаданные.Интерфейсы.ОбщийВнешниеОбработки);
	СписокИсключаемыхИнтерфейсов.Добавить(Метаданные.Интерфейсы.ОбщийРегламентированный);
	СписокИсключаемыхИнтерфейсов.Добавить(Метаданные.Интерфейсы.ОбщийЭлектроннаяПочта);
	СписокИсключаемыхИнтерфейсов.Добавить(Метаданные.Интерфейсы.РегламентированныйУчет);
	
	СписокВыбора = Новый СписокЗначений;
	
	Для Каждого Интерфейс Из Метаданные.Интерфейсы Цикл
		Если СписокИсключаемыхИнтерфейсов.Найти(Интерфейс) = Неопределено Тогда
			СписокВыбора.Добавить(Интерфейс, Интерфейс.Синоним);
		КонецЕсли;
		
	КонецЦикла;
	
	Если (Элемент.Значение <> Неопределено) И (СписокВыбора.НайтиПоЗначению(Элемент.Значение) = Неопределено) Тогда
		СписокВыбора.Добавить(Элемент.Значение, Элемент.Значение.Синоним);
	КонецЕсли;		
	
	Возврат СписокВыбора;
КонецФункции

// Процедура устанавливает в списке СписокДоступныхРолейПользователяИБ пометку у роли
// Пользователь, если пользователь установил пометку у роли, требующей еще и роль Пользователь
//
// Параметры
//  СписокДоступныхРолейПользователяИБ  - СписокЗначений, содержит роли пользователей
//                 
//  ТекущаяСтрока  - Текущие данные строки списка значений, в которой пользователь изменил Пометку
//
Процедура НайтиИПометитьВСпискеРольПользователь(СписокДоступныхРолейПользователяИБ, ТекущаяСтрока) Экспорт

	МассивРолей = Новый Массив;
	МассивРолей.Добавить(Метаданные.Роли.КадровикРегламентированныхДанных);
	МассивРолей.Добавить(Метаданные.Роли.КадровикУправленческихДанных);
	МассивРолей.Добавить(Метаданные.Роли.МенеджерПоНаборуПерсонала);
	МассивРолей.Добавить(Метаданные.Роли.РасчетчикРегламентированнойЗарплаты);
	МассивРолей.Добавить(Метаданные.Роли.РасчетчикУправленческойЗарплаты);
	
	УправлениеПользователямиДополнительный.ДополнитьСписокРолейДляПометкиРолиПользователь(МассивРолей);
	
	Если ТекущаяСтрока.Пометка И МассивРолей.Найти(ТекущаяСтрока.Значение) <> Неопределено Тогда
		СписокДоступныхРолейПользователяИБ.Найти(Метаданные.Роли.Пользователь, "Значение").Пометка = Истина;
	КонецЕсли;

КонецПроцедуры // НайтиИПометитьВСпискеРольПользователь()

// Функция проверяет, что роли настроены правильно
//
// Параметры
//	СписокДоступныхРолейПользователяИБ  - СписокЗначений, содержит роли пользователей
//
Функция РолиНастроеныПравильно(СписокДоступныхРолейПользователяИБ) Экспорт
	
	МассивРолей = Новый Массив;
	МассивРолей.Добавить(Метаданные.Роли.ПолныеПрава);
	МассивРолей.Добавить(Метаданные.Роли.Пользователь);
	
	УправлениеПользователямиДополнительный.ДополнитьСписокРолейДляПроверкиПравильностиНастройки(МассивРолей);
	
	НастроеныПравильно = Ложь;
	ПредставлениеНеобходимыхРолей = "";
	Для Каждого Роль Из МассивРолей Цикл
		Если СписокДоступныхРолейПользователяИБ.Найти(Роль, "Значение").Пометка Тогда
			НастроеныПравильно = Истина;
			Прервать;
		КонецЕсли;
		
		Если Не ПустаяСтрока(ПредставлениеНеобходимыхРолей) Тогда
			ПредставлениеНеобходимыхРолей = ПредставлениеНеобходимыхРолей + """ или """;
		КонецЕсли;
		ПредставлениеНеобходимыхРолей = ПредставлениеНеобходимыхРолей + Роль.Синоним;
	КонецЦикла;
	
	Если Не НастроеныПравильно Тогда
		ОбщегоНазначения.СообщитьОбОшибке("Для пользователя необходимо установить одну из ролей """ + ПредставлениеНеобходимыхРолей +  """!");
		Возврат Ложь;
	КонецЕсли;
	
	Если Не УправлениеПользователямиДополнительный.РолиСочетаютсяПравильно(СписокДоступныхРолейПользователяИБ) Тогда
		Возврат Ложь;
	Конецесли;
	
	Возврат Истина;
	
КонецФункции