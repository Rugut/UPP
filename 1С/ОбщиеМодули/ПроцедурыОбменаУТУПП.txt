Функция ПолучитьМассивВсехУзлов() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ различные
	               |	ОбменУправлениеПредприятиемУправлениеТорговлей.Ссылка КАК Ссылка
	               |ИЗ
	               |	ПланОбмена.ОбменУправлениеПредприятиемУправлениеТорговлей КАК ОбменУправлениеПредприятиемУправлениеТорговлей
				   |
				   |ГДЕ
				   |
				   |	ОбменУправлениеПредприятиемУправлениеТорговлей.Ссылка <> &ЭтотУзел";
				   
	Запрос.УстановитьПараметр("ЭтотУзел", ПланыОбмена.ОбменУправлениеПредприятиемУправлениеТорговлей.ЭтотУзел());			   
				   
	МассивСсылок = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
	
	Возврат МассивСсылок;
	
КонецФункции

Функция ВернутьМассивВсехУзловИзПараметровСеанса()
	
	Возврат ПараметрыСеанса.ВсеУзлыОбменаУТУПП;	
	
КонецФункции

Функция ПолучитьСоответствиеУзловИОрганизаций() Экспорт
	
	Хранилище = ПараметрыСеанса.СоответствиеОрганизацийИУзловОбменаУТУПП;
		
	СоответствиеУзловИОрганизаций = Хранилище.Получить();
	
	Возврат СоответствиеУзловИОрганизаций;
	
КонецФункции

Функция ВернутьМассивУзловПоОднойОрганизации(Организация, СоответствиеУзловИОрганизаций = Неопределено)
	
	Если СоответствиеУзловИОрганизаций = Неопределено Тогда
		
		СоответствиеУзловИОрганизаций = ПолучитьСоответствиеУзловИОрганизаций();
	
	КонецЕсли;
	
	Возврат СоответствиеУзловИОрганизаций[Организация];	
	
КонецФункции

Функция ПолучитьЗапросомМассивУзловПоОрганизации(Организация) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ Различные
	               |	ОбменУправлениеПредприятиемУправлениеТорговлей.Ссылка КАК Ссылка
	               |ИЗ
	               |	ПланОбмена.ОбменУправлениеПредприятиемУправлениеТорговлей КАК ОбменУправлениеПредприятиемУправлениеТорговлей
				   |		Левое соединение ПланОбмена.ОбменУправлениеПредприятиемУправлениеТорговлей.Организации КАК СписокОрганизаций
				   |			ПО (ОбменУправлениеПредприятиемУправлениеТорговлей.Ссылка = СписокОрганизаций.Ссылка)
				   |
	               |ГДЕ
	               |	(СписокОрганизаций.Организация В (&СсылкаНаОрганизацию)
				   |		ИЛИ СписокОрганизаций.Организация Есть NULL)
				   |	И ОбменУправлениеПредприятиемУправлениеТорговлей.Ссылка <> &ЭтотУзел";
				   
	Запрос.УстановитьПараметр("СсылкаНаОрганизацию", Организация);
	
	Запрос.УстановитьПараметр("ЭтотУзел", ПланыОбмена.ОбменУправлениеПредприятиемУправлениеТорговлей.ЭтотУзел());
	
	МассивСсылок = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
	
	Возврат МассивСсылок;	
	
КонецФункции

Функция ПолучитьТаблицуУзловИДат()
	
	Хранилище = ПараметрыСеанса.СоответствиеУзловИДатДляОбменаУТУПП;
		
	ТаблицаУзловИДат = Хранилище.Получить();
	
	Возврат ТаблицаУзловИДат;
	
КонецФункции

Функция ПолучитьМассивУзловПоОрганизации(Организация, СоответствиеУзловИОрганизаций = Неопределено)
	
	Если ТипЗнч(Организация) = Тип("Массив") Тогда
		
		КоличествоЭлементовВМассиве = Организация.Количество();
		
		Если КоличествоЭлементовВМассиве = 0 Тогда
			
			ОрганизацияДляПоиска = Справочники.Организации.ПустаяСсылка();
			
		ИначеЕсли КоличествоЭлементовВМассиве = 1 Тогда
			
			ОрганизацияДляПоиска = Организация[0];
			
		Иначе
			
			ОрганизацияДляПоиска = Неопределено;
			
		КонецЕсли;
		
	Иначе
		
		ОрганизацияДляПоиска = Организация;
		
	КонецЕсли;
	
	Если ОрганизацияДляПоиска <> Неопределено Тогда
		
		МассивУзлов = ВернутьМассивУзловПоОднойОрганизации(ОрганизацияДляПоиска, СоответствиеУзловИОрганизаций);
		
	Иначе	
		
		МассивУзлов = ПолныеПрава.ПолучитьЗапросомМассивУзловПоОрганизацииДляОбменаУТУПП(Организация);
		
	КонецЕсли;	
	
	Возврат МассивУзлов;
	
КонецФункции

Процедура ЗаполнитьСтруктуруПараметровПоЭлементу(Элемент, ИмяБазовогоТипа, ТипЭлемента, СтруктураПараметров, 
	СтруктураПолученияКоличества = Неопределено)
	
	ИмяДляЗапроса = "";
	ЭтоСсылка = ОпределитьПоЭлементуЭтоСсылка(Элемент, ИмяБазовогоТипа, ТипЭлемента, ИмяДляЗапроса);
	
	Если НЕ ЭтоСсылка Тогда
		
		Для Каждого ЭлементСтруктуры Из СтруктураПараметров Цикл
			
			СтруктураПараметров[ЭлементСтруктуры.Ключ] = Элемент[ЭлементСтруктуры.Ключ];
			
		КонецЦикла;
		
		Если СтруктураПолученияКоличества <> Неопределено Тогда 
			
			Для Каждого ЭлементСтруктуры Из СтруктураПолученияКоличества Цикл
				
				СтруктураПолученияКоличества[ЭлементСтруктуры.Ключ] = Элемент[ЭлементСтруктуры.Ключ].Количество();
				
			КонецЦикла;
			
		КонецЕсли;
		
		Возврат;
				
	КонецЕсли;
	
	// надо все единым запросом получить
	//нужно построить запрос и вернуть параметр по ссылке
	
	СтрокаИменПараметров = "";
	Для Каждого ЭлементСтруктуры Из СтруктураПараметров Цикл
			
		СтрокаИменПараметров = СтрокаИменПараметров + "," + Символы.ПС + "ОбъектДанных." + ЭлементСтруктуры.Ключ;
		
	КонецЦикла;
	
	Если СтруктураПолученияКоличества <> Неопределено Тогда
		
		Для Каждого ЭлементСтруктуры Из СтруктураПолученияКоличества Цикл
				
			СтрокаИменПараметров = СтрокаИменПараметров + "," + Символы.ПС + "Количество(ОбъектДанных." + ЭлементСтруктуры.Ключ + ".НомерСтроки) КАК " + ЭлементСтруктуры.Ключ;
			
		КонецЦикла;
		
	КонецЕсли;
	
	СтрокаИменПараметров = Сред(СтрокаИменПараметров, 2);
	
	
	Запрос = Новый Запрос();
	Запрос.Текст = "ВЫБРАТЬ
	               |	" + СтрокаИменПараметров + "
	               |ИЗ
	               |	" + ИмяДляЗапроса + " КАК ОбъектДанных
	               |ГДЕ
	               |	ОбъектДанных.Ссылка = &Ссылка";
	Запрос.УстановитьПараметр("Ссылка", Элемент);
	
	РезультатЗапроса = Запрос.Выполнить(); 
	Если НЕ РезультатЗапроса.Пустой() Тогда
						
		Выборка = РезультатЗапроса.Выбрать();
		Выборка.Следующий();
		ЗаполнитьЗначенияСвойств(СтруктураПараметров, Выборка);
		
		Если СтруктураПолученияКоличества <> Неопределено Тогда
			
			Для Каждого ЭлементСтруктуры Из СтруктураПолученияКоличества Цикл
				
				Таблица = Выборка[ЭлементСтруктуры.Ключ];
				Количество = 0;
				
				Если Таблица <> Неопределено
					И Таблица <> NULL Тогда
					
					ТаблицаКоличества = Таблица.Выгрузить();
					
					Если ТаблицаКоличества.Количество() <> 0 Тогда
						Количество = ТаблицаКоличества[0][ТаблицаКоличества.Колонки[0].Имя];
					КонецЕсли;						
					
				КонецЕсли;
				
				СтруктураПолученияКоличества.Вставить(ЭлементСтруктуры.Ключ, Количество);				
				
			КонецЦикла;	
			
		КонецЕсли;
		
	Иначе
		
		Если СтруктураПолученияКоличества <> Неопределено Тогда
			
			Для Каждого ЭлементСтруктуры Из СтруктураПолученияКоличества Цикл
				
				СтруктураПолученияКоличества.Вставить(ЭлементСтруктуры.Ключ, 0);				
				
			КонецЦикла;	
			
		КонецЕсли;
		
	КонецЕсли;		
	
КонецПроцедуры

// функция по элементу определяет это группа и возвращает ссылку на него
Функция ОпределитьПоЭлементуЭтоГруппаИСсылку(Элемент, ИмяБазовогоТипа, ТипЭлемента, НужноОпределятьЭтоГруппа = Ложь, ЭтоГруппа = Ложь)
	
	ИмяДляЗапроса = "";
	ЭтоСсылка = ОпределитьПоЭлементуЭтоСсылка(Элемент, ИмяБазовогоТипа, ТипЭлемента, ИмяДляЗапроса);
	
	Если НЕ ЭтоСсылка Тогда
		
		Если НужноОпределятьЭтоГруппа Тогда
			
			ЭтоГруппа = Элемент.ЭтоГруппа;
			
			Если ЭтоГруппа = Неопределено Тогда
				ЭтоГруппа = Ложь;
			КонецЕсли;
			
		Конецесли;
		
		Возврат Элемент.Ссылка;
		
	КонецЕсли;
	
	Если НужноОпределятьЭтоГруппа Тогда
		
		ЭтоГруппа = ПолучитьЗначениеПараметраОбъектаИлиСсылки(Элемент, ИмяБазовогоТипа, ТипЭлемента, "ЭтоГруппа");	
		Если ЭтоГруппа = Неопределено Тогда
			ЭтоГруппа = Ложь;
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Элемент;
	
КонецФункции

Функция ОпределитьПоЭлементуЭтоСсылка(Элемент, ИмяБазовогоТипа, ТипЭлемента, ИмяДляЗапроса)
	
	МетаданныеЭлемента = Элемент.Метаданные();
	
	Если ИмяБазовогоТипа = "Справочники" Тогда
		
		ТипСсылки = Тип("СправочникСсылка." + МетаданныеЭлемента.Имя);
		ИмяДляЗапроса = "Справочник." + МетаданныеЭлемента.Имя;
		
	ИначеЕсли ИмяБазовогоТипа = "Документы" Тогда
		
		ТипСсылки = Тип("ДокументСсылка." + МетаданныеЭлемента.Имя);
		ИмяДляЗапроса = "Документ." + МетаданныеЭлемента.Имя; 
		
	Иначе
		
		ТипСсылки = Неопределено;
		
	КонецЕсли;
	
	Возврат ТипЭлемента = ТипСсылки;	
	
КонецФункции


// функция анализирует Элемент это объект или нет
// если объект возвращает его реквизит, иначе
// строит по ссылке запрос и возвращает значение этого реквизита из запроса
Функция ПолучитьЗначениеПараметраОбъектаИлиСсылки(Элемент, ИмяБазовогоТипа, ТипЭлемента, ИмяПараметра)
	
	ИмяДляЗапроса = "";
	ЭтоСсылка = ОпределитьПоЭлементуЭтоСсылка(Элемент, ИмяБазовогоТипа, ТипЭлемента, ИмяДляЗапроса);
		
	Если НЕ ЭтоСсылка Тогда
		Возврат Элемент[ИмяПараметра]
	КонецЕсли;
			
	Запрос = Новый Запрос();
	Запрос.Текст = "ВЫБРАТЬ
	               |	ОбъектДанных." + ИмяПараметра + "
				   |
	               |ИЗ
	               |	" + ИмяДляЗапроса + " КАК ОбъектДанных
	               |ГДЕ
	               |	ОбъектДанных.Ссылка = &Ссылка";
	Запрос.УстановитьПараметр("Ссылка", Элемент);
	
	РезультатЗапроса = Запрос.Выполнить(); 
	Если РезультатЗапроса.Пустой() Тогда
		Возврат Неопределено;
	КонецЕсли;
		
	Выборка = РезультатЗапроса.Выбрать();
	Выборка.Следующий();	
	
	Возврат Выборка[ИмяПараметра];		
		
КонецФункции

Процедура ДополнитьМассивУзловДляВыгрузкиДокумента(Элемент, МассивУзловДляПередачи, 
	ТипЭлемента = Неопределено, ВыгружатьДляВсехУзлов = Ложь, ВозвращатьМассивВсехУзлов = Истина,
	СоответствиеУзловИОрганизаций = Неопределено, НужноАнализироватьОграниченияПоДатам = Ложь)
	
	Если ТипЭлемента = Неопределено Тогда
		ТипЭлемента = ТипЗнч(Элемент);
	КонецЕсли;
	
	СтруктураПараметров = Новый Структура("Дата");
	СтруктураДопПараметров = Новый Структура();
	
	// если нужно делаем органичения для выгрузки для организации
	// не для всех документов нужно такое органичение
	// для некотороыех документов если в шапке пустая организация - значит документ едет везде
	
	Если ТипЭлемента = Тип("ДокументОбъект.ВнутреннееПеремещениеНаличныхДенежныхСредств") 
		ИЛИ ТипЭлемента = Тип("ДокументСсылка.ВнутреннееПеремещениеНаличныхДенежныхСредств") Тогда
		
		СтруктураПараметров.Вставить("ОрганизацияПолучатель");
			
		ЗаполнитьСтруктуруПараметровПоЭлементу(Элемент, "Документы", ТипЭлемента, СтруктураПараметров);
		
		ДополнитьМассивУзламиПоОрганизации(МассивУзловДляПередачи, СтруктураПараметров.ОрганизацияПолучатель, СоответствиеУзловИОрганизаций);
						
	ИначеЕсли ТипЭлемента = Тип("ДокументОбъект.ЗакрытиеВнутреннихЗаказов") 
		ИЛИ ТипЭлемента = Тип("ДокументСсылка.ЗакрытиеВнутреннихЗаказов")
		ИЛИ ТипЭлемента = Тип("ДокументОбъект.ЗакрытиеЗаказовПокупателей") 
		ИЛИ ТипЭлемента = Тип("ДокументСсылка.ЗакрытиеЗаказовПокупателей")
		ИЛИ ТипЭлемента = Тип("ДокументОбъект.ЗакрытиеЗаказовПоставщикам") 
		ИЛИ ТипЭлемента = Тип("ДокументСсылка.ЗакрытиеЗаказовПоставщикам") Тогда
		
		СтруктураПараметров.Вставить("Организация");
			
		ЗаполнитьСтруктуруПараметровПоЭлементу(Элемент, "Документы", ТипЭлемента, СтруктураПараметров);
		
		Если ЗначениеЗаполнено(СтруктураПараметров.Организация) Тогда
		
			ДополнитьМассивУзламиПоОрганизации(МассивУзловДляПередачи, СтруктураПараметров.Организация, СоответствиеУзловИОрганизаций);
			
		Иначе
			
			ОбеспечитьВыгрузкуОбъектаДляВсехУзлов(ВыгружатьДляВсехУзлов, ВозвращатьМассивВсехУзлов, МассивУзловДляПередачи);	
			
		КонецЕсли;
		
	Иначе	
		
		МетаданныеДокумента = Метаданные.НайтиПоТипу(ТипЭлемента);

		ЕстьОрганизация = МетаданныеДокумента.Реквизиты.найти("Организация") <> Неопределено;
		
		Если ЕстьОрганизация Тогда
		
			СтруктураПараметров.Вставить("Организация");
			
		КонецЕсли;			
		
		Если ТипЭлемента = Тип("ДокументОбъект.Событие") 
			ИЛИ ТипЭлемента = Тип("ДокументСсылка.Событие") Тогда
						
			СтруктураПараметров.Вставить("ВидОбъекта");
			ЗаполнитьСтруктуруПараметровПоЭлементу(Элемент, "Документы", ТипЭлемента, СтруктураПараметров);

			Если СтруктураПараметров.ВидОбъекта = Перечисления.ВидыОбъектовСобытия.ЗаявкаКандидата
				ИЛИ	СтруктураПараметров.ВидОбъекта = Перечисления.ВидыОбъектовСобытия.Кандидат Тогда
				
				ВыгружатьДляВсехУзлов = Ложь;
				Возврат;
				
			КонецЕсли;		
			
		ИначеЕсли ТипЭлемента = Тип("ДокументОбъект.ЗаявкаНаРасходованиеСредств") 
			ИЛИ ТипЭлемента = Тип("ДокументСсылка.ЗаявкаНаРасходованиеСредств") Тогда
			
			
			СтруктураПараметров.Вставить("ВидОперации");
			ЗаполнитьСтруктуруПараметровПоЭлементу(Элемент, "Документы", ТипЭлемента, СтруктураПараметров);

			Если СтруктураПараметров.ВидОперации = Перечисления.ВидыОперацийЗаявкиНаРасходование.ПеречислениеЗП
				ИЛИ	СтруктураПараметров.ВидОперации = Перечисления.ВидыОперацийЗаявкиНаРасходование.РасчетыПоКредитамИЗаймамСРаботниками Тогда
				
				ВыгружатьДляВсехУзлов = Ложь;
				Возврат;
				
			КонецЕсли;
			
		ИначеЕсли ТипЭлемента = Тип("ДокументОбъект.КомплектацияНоменклатуры") 
			ИЛИ ТипЭлемента = Тип("ДокументСсылка.КомплектацияНоменклатуры") Тогда
			
			
			СтруктураПараметров.Вставить("ВидОперации");
			ЗаполнитьСтруктуруПараметровПоЭлементу(Элемент, "Документы", ТипЭлемента, СтруктураПараметров);

			Если СтруктураПараметров.ВидОперации = Перечисления.ВидыОперацийКомплектацияНоменклатуры.ВыпускПродукции
				ИЛИ	СтруктураПараметров.ВидОперации = Перечисления.ВидыОперацийКомплектацияНоменклатуры.ПоступлениеОтПереработчика Тогда
				
				ВыгружатьДляВсехУзлов = Ложь;
				Возврат;
				
			КонецЕсли;
			
		ИначеЕсли ТипЭлемента = Тип("ДокументОбъект.ПеремещениеТоваров") 
			ИЛИ ТипЭлемента = Тип("ДокументСсылка.ПеремещениеТоваров") Тогда
			
			
			СтруктураПараметров.Вставить("ВидОперации");
			ЗаполнитьСтруктуруПараметровПоЭлементу(Элемент, "Документы", ТипЭлемента, СтруктураПараметров);

			Если СтруктураПараметров.ВидОперации = Перечисления.ВидыОперацийПеремещениеТоваров.Оборудование	Тогда
				
				ВыгружатьДляВсехУзлов = Ложь;
				Возврат;
				
			КонецЕсли;
			
		ИначеЕсли ТипЭлемента = Тип("ДокументОбъект.ПриходныйКассовыйОрдер") 
			ИЛИ ТипЭлемента = Тип("ДокументСсылка.ПриходныйКассовыйОрдер") Тогда
			
			
			СтруктураПараметров.Вставить("ВидОперации");
			ЗаполнитьСтруктуруПараметровПоЭлементу(Элемент, "Документы", ТипЭлемента, СтруктураПараметров);

			Если СтруктураПараметров.ВидОперации = Перечисления.ВидыОперацийПКО.ВозвратДенежныхСредствРаботником
				ИЛИ	СтруктураПараметров.ВидОперации = Перечисления.ВидыОперацийПКО.РасчетыПоКредитамИЗаймамСРаботниками Тогда
				
				ВыгружатьДляВсехУзлов = Ложь;
				Возврат;
				
			КонецЕсли;
			
		ИначеЕсли ТипЭлемента = Тип("ДокументОбъект.ПлатежноеПоручениеИсходящее") 
			ИЛИ ТипЭлемента = Тип("ДокументСсылка.ПлатежноеПоручениеИсходящее") Тогда
			
			
			СтруктураПараметров.Вставить("ВидОперации");
			ЗаполнитьСтруктуруПараметровПоЭлементу(Элемент, "Документы", ТипЭлемента, СтруктураПараметров);

			Если СтруктураПараметров.ВидОперации = Перечисления.ВидыОперацийППИсходящее.ПеречислениеЗП
				ИЛИ	СтруктураПараметров.ВидОперации = Перечисления.ВидыОперацийППИсходящее.РасчетыПоКредитамИЗаймамСРаботниками Тогда
				
				ВыгружатьДляВсехУзлов = Ложь;
				Возврат;
				
			КонецЕсли;
			
		ИначеЕсли ТипЭлемента = Тип("ДокументОбъект.ПриходныйОрдерНаТовары") 
			ИЛИ ТипЭлемента = Тип("ДокументСсылка.ПриходныйОрдерНаТовары") Тогда
			
			
			СтруктураПараметров.Вставить("ВидОперации");
			ЗаполнитьСтруктуруПараметровПоЭлементу(Элемент, "Документы", ТипЭлемента, СтруктураПараметров);

			Если СтруктураПараметров.ВидОперации = Перечисления.ВидыОперацийПриходныйОрдер.ИзПереработки
				ИЛИ СтруктураПараметров.ВидОперации = Перечисления.ВидыОперацийПриходныйОрдер.НеОпределен Тогда
				
				ВыгружатьДляВсехУзлов = Ложь;
				Возврат;
				
			КонецЕсли;
			
		ИначеЕсли ТипЭлемента = Тип("ДокументОбъект.РасходныйОрдерНаТовары") 
			ИЛИ ТипЭлемента = Тип("ДокументСсылка.РасходныйОрдерНаТовары") Тогда
			
			
			СтруктураПараметров.Вставить("ВидОперации");
			ЗаполнитьСтруктуруПараметровПоЭлементу(Элемент, "Документы", ТипЭлемента, СтруктураПараметров);

			Если СтруктураПараметров.ВидОперации = Перечисления.ВидыОперацийРасходныйОрдер.ВПереработку
				ИЛИ СтруктураПараметров.ВидОперации = Перечисления.ВидыОперацийРасходныйОрдер.ИзПереработки
				ИЛИ СтруктураПараметров.ВидОперации = Перечисления.ВидыОперацийРасходныйОрдер.НеОпределен Тогда
				
				ВыгружатьДляВсехУзлов = Ложь;
				Возврат;
				
			КонецЕсли;
			
		ИначеЕсли ТипЭлемента = Тип("ДокументОбъект.РеализацияТоваровУслуг") 
			ИЛИ ТипЭлемента = Тип("ДокументСсылка.РеализацияТоваровУслуг") Тогда
			
			
			СтруктураПараметров.Вставить("ВидОперации");
			ЗаполнитьСтруктуруПараметровПоЭлементу(Элемент, "Документы", ТипЭлемента, СтруктураПараметров);

			Если СтруктураПараметров.ВидОперации = Перечисления.ВидыОперацийРеализацияТоваров.Оборудование Тогда
				
				ВыгружатьДляВсехУзлов = Ложь;
				Возврат;
				
			КонецЕсли;
			
		ИначеЕсли ТипЭлемента = Тип("ДокументОбъект.РасходныйКассовыйОрдер") 
			ИЛИ ТипЭлемента = Тип("ДокументСсылка.РасходныйКассовыйОрдер") Тогда
			
			
			СтруктураПараметров.Вставить("ВидОперации");
			ЗаполнитьСтруктуруПараметровПоЭлементу(Элемент, "Документы", ТипЭлемента, СтруктураПараметров);

			Если СтруктураПараметров.ВидОперации = Перечисления.ВидыОперацийРКО.ВыплатаДепонентов
				ИЛИ СтруктураПараметров.ВидОперации = Перечисления.ВидыОперацийРКО.ВыплатаЗаработнойПлатыПоВедомостям
				ИЛИ СтруктураПараметров.ВидОперации = Перечисления.ВидыОперацийРКО.ВыплатаЗаработнойПлатыРаботнику
				ИЛИ СтруктураПараметров.ВидОперации = Перечисления.ВидыОперацийРКО.РасчетыПоКредитамИЗаймамСРаботниками Тогда
				
				ВыгружатьДляВсехУзлов = Ложь;
				Возврат;
				
			КонецЕсли;
			
		ИначеЕсли ТипЭлемента = Тип("ДокументОбъект.АккредитивПереданный") 
			ИЛИ ТипЭлемента = Тип("ДокументСсылка.АккредитивПереданный")
			ИЛИ ТипЭлемента = Тип("ДокументОбъект.ИнкассовоеПоручениеПолученное") 
			ИЛИ ТипЭлемента = Тип("ДокументСсылка.ИнкассовоеПоручениеПолученное")
			ИЛИ ТипЭлемента = Тип("ДокументОбъект.ПлатежноеТребованиеПолученное") 
			ИЛИ ТипЭлемента = Тип("ДокументСсылка.ПлатежноеТребованиеПолученное")
			ИЛИ ТипЭлемента = Тип("ДокументОбъект.ПлатежныйОрдерСписаниеДенежныхСредств") 
			ИЛИ ТипЭлемента = Тип("ДокументСсылка.ПлатежныйОрдерСписаниеДенежныхСредств") Тогда
			
			
			СтруктураПараметров.Вставить("ВидОперации");
			ЗаполнитьСтруктуруПараметровПоЭлементу(Элемент, "Документы", ТипЭлемента, СтруктураПараметров);

			Если СтруктураПараметров.ВидОперации = Перечисления.ВидыОперацийСписаниеБезналичныхДенежныхСредств.ПеречислениеЗП Тогда
				
				ВыгружатьДляВсехУзлов = Ложь;
				Возврат;
				
			КонецЕсли;
			
		ИначеЕсли ТипЭлемента = Тип("ДокументОбъект.ВозвратТоваровОтПокупателя") 
			ИЛИ ТипЭлемента = Тип("ДокументСсылка.ВозвратТоваровОтПокупателя") Тогда
			
			
			СтруктураПараметров.Вставить("ВидОперации");
			ЗаполнитьСтруктуруПараметровПоЭлементу(Элемент, "Документы", ТипЭлемента, СтруктураПараметров);

			Если СтруктураПараметров.ВидОперации = Перечисления.ВидыОперацийВозвратТоваровОтПокупателя.Оборудование Тогда
				
				ВыгружатьДляВсехУзлов = Ложь;
				Возврат;
				
			КонецЕсли;
			
		ИначеЕсли ТипЭлемента = Тип("ДокументОбъект.ВозвратТоваровПоставщику") 
			ИЛИ ТипЭлемента = Тип("ДокументСсылка.ВозвратТоваровПоставщику") Тогда
			
			
			СтруктураПараметров.Вставить("ВидОперации");
			ЗаполнитьСтруктуруПараметровПоЭлементу(Элемент, "Документы", ТипЭлемента, СтруктураПараметров);

			Если СтруктураПараметров.ВидОперации = Перечисления.ВидыОперацийВозвратТоваровПоставщику.ИзПереработки
				ИЛИ СтруктураПараметров.ВидОперации = Перечисления.ВидыОперацийВозвратТоваровПоставщику.Оборудование Тогда
				
				ВыгружатьДляВсехУзлов = Ложь;
				Возврат;
				
			КонецЕсли;
			
		ИначеЕсли ТипЭлемента = Тип("ДокументОбъект.ЗаказПокупателя") 
			ИЛИ ТипЭлемента = Тип("ДокументСсылка.ЗаказПокупателя") Тогда
			
			
			СтруктураПараметров.Вставить("ВидОперации");
			
			СтруктураДопПараметров.Вставить("ВозвратнаяТара");
			СтруктураДопПараметров.Вставить("Товары");
			СтруктураДопПараметров.Вставить("Услуги");
			
			ЗаполнитьСтруктуруПараметровПоЭлементу(Элемент, "Документы", ТипЭлемента, СтруктураПараметров, СтруктураДопПараметров);

			Если СтруктураПараметров.ВидОперации = Перечисления.ВидыОперацийЗаказПокупателя.Переработка
				ИЛИ (СтруктураДопПараметров.ВозвратнаяТара + СтруктураДопПараметров.Товары + СтруктураДопПараметров.Услуги = 0)  Тогда
				
				ВыгружатьДляВсехУзлов = Ложь;
				Возврат;
				
			КонецЕсли;
			
		ИначеЕсли ТипЭлемента = Тип("ДокументОбъект.ЗаказПоставщику") 
			ИЛИ ТипЭлемента = Тип("ДокументСсылка.ЗаказПоставщику") Тогда
			
			
			СтруктураПараметров.Вставить("ВидОперации");
			
			СтруктураДопПараметров.Вставить("ВозвратнаяТара");
			СтруктураДопПараметров.Вставить("Товары");
			СтруктураДопПараметров.Вставить("Услуги");
			
			ЗаполнитьСтруктуруПараметровПоЭлементу(Элемент, "Документы", ТипЭлемента, СтруктураПараметров, СтруктураДопПараметров);

			Если СтруктураПараметров.ВидОперации = Перечисления.ВидыОперацийЗаказПоставщику.Оборудование
				ИЛИ СтруктураПараметров.ВидОперации = Перечисления.ВидыОперацийЗаказПоставщику.Переработка
				ИЛИ (СтруктураДопПараметров.ВозвратнаяТара + СтруктураДопПараметров.Товары + СтруктураДопПараметров.Услуги = 0)  Тогда
				
				ВыгружатьДляВсехУзлов = Ложь;
				Возврат;
				
			КонецЕсли;
			
		ИначеЕсли ТипЭлемента = Тип("ДокументОбъект.КорректировкаЗаказаПокупателя") 
			ИЛИ ТипЭлемента = Тип("ДокументСсылка.КорректировкаЗаказаПокупателя") Тогда
			
			
			СтруктураПараметров.Вставить("ЗаказПокупателя");
			
			СтруктураДопПараметров.Вставить("ВозвратнаяТара");
			СтруктураДопПараметров.Вставить("Товары");
			СтруктураДопПараметров.Вставить("Услуги");
			
			ЗаполнитьСтруктуруПараметровПоЭлементу(Элемент, "Документы", ТипЭлемента, СтруктураПараметров, СтруктураДопПараметров);
			
			Если (СтруктураДопПараметров.ВозвратнаяТара + СтруктураДопПараметров.Товары + СтруктураДопПараметров.Услуги = 0)  Тогда
				
				ВыгружатьДляВсехУзлов = Ложь;
				Возврат;
				
			КонецЕсли;

			Если ЗначениеЗаполнено(СтруктураПараметров.ЗаказПокупателя) Тогда
				
				ЗаказПокупателя = СтруктураПараметров.ЗаказПокупателя;
				ТипЗаказа = Неопределено;
				СтруктураВременныхПараметров = Новый Структура();
				СтруктураВременныхПараметров.Вставить("ВидОперации");
				
				СтруктураВременныхДопПараметров = Новый Структура();
				СтруктураВременныхДопПараметров.Вставить("ВозвратнаяТара");
				СтруктураВременныхДопПараметров.Вставить("Товары");
				СтруктураВременныхДопПараметров.Вставить("Услуги");
				
				ЗаполнитьСтруктуруПараметровПоЭлементу(ЗаказПокупателя, "Документы", ТипЗаказа, СтруктураВременныхПараметров, СтруктураВременныхДопПараметров);
				
				Если СтруктураВременныхПараметров.ВидОперации = Перечисления.ВидыОперацийЗаказПокупателя.Переработка
					ИЛИ (СтруктураВременныхДопПараметров.ВозвратнаяТара + СтруктураВременныхДопПараметров.Товары + СтруктураВременныхДопПараметров.Услуги = 0)  Тогда
					
					ВыгружатьДляВсехУзлов = Ложь;
					Возврат;
					
				КонецЕсли;
				
			КонецЕсли;
			
		ИначеЕсли ТипЭлемента = Тип("ДокументОбъект.КорректировкаЗаказаПоставщику") 
			ИЛИ ТипЭлемента = Тип("ДокументСсылка.КорректировкаЗаказаПоставщику") Тогда
			
			
			СтруктураПараметров.Вставить("ЗаказПоставщику");
			
			СтруктураДопПараметров.Вставить("ВозвратнаяТара");
			СтруктураДопПараметров.Вставить("Товары");
			СтруктураДопПараметров.Вставить("Услуги");
			
			ЗаполнитьСтруктуруПараметровПоЭлементу(Элемент, "Документы", ТипЭлемента, СтруктураПараметров, СтруктураДопПараметров);
			
			Если (СтруктураДопПараметров.ВозвратнаяТара + СтруктураДопПараметров.Товары + СтруктураДопПараметров.Услуги = 0)  Тогда
				
				ВыгружатьДляВсехУзлов = Ложь;
				Возврат;
				
			КонецЕсли;

			Если ЗначениеЗаполнено(СтруктураПараметров.ЗаказПоставщику) Тогда
				
				ЗаказПоставщику = СтруктураПараметров.ЗаказПоставщику;
				ТипЗаказа = Неопределено;
				СтруктураВременныхПараметров = Новый Структура();
				СтруктураВременныхПараметров.Вставить("ВидОперации");
				
				СтруктураВременныхДопПараметров = Новый Структура();
				СтруктураВременныхДопПараметров.Вставить("ВозвратнаяТара");
				СтруктураВременныхДопПараметров.Вставить("Товары");
				СтруктураВременныхДопПараметров.Вставить("Услуги");
				
				ЗаполнитьСтруктуруПараметровПоЭлементу(ЗаказПоставщику, "Документы", ТипЗаказа, СтруктураВременныхПараметров, СтруктураВременныхДопПараметров);
				
				Если СтруктураВременныхПараметров.ВидОперации = Перечисления.ВидыОперацийЗаказПоставщику.Оборудование
					ИЛИ СтруктураВременныхПараметров.ВидОперации = Перечисления.ВидыОперацийЗаказПоставщику.Переработка
					ИЛИ (СтруктураВременныхДопПараметров.ВозвратнаяТара + СтруктураВременныхДопПараметров.Товары + СтруктураВременныхДопПараметров.Услуги = 0)  Тогда
					
					ВыгружатьДляВсехУзлов = Ложь;
					Возврат;
					
				КонецЕсли;
				
			КонецЕсли;
			
		ИначеЕсли ТипЭлемента = Тип("ДокументОбъект.КорректировкаСерийИХарактеристикТоваров") 
			ИЛИ ТипЭлемента = Тип("ДокументСсылка.КорректировкаСерийИХарактеристикТоваров") Тогда
			
			
			СтруктураПараметров.Вставить("ВидОперации");
			ЗаполнитьСтруктуруПараметровПоЭлементу(Элемент, "Документы", ТипЭлемента, СтруктураПараметров);

			Если СтруктураПараметров.ВидОперации = Перечисления.ВидыОперацийКорректировкаСерийИХарактеристикТоваров.Оборудование Тогда
				
				ВыгружатьДляВсехУзлов = Ложь;
				Возврат;
				
			КонецЕсли;
			
		ИначеЕсли ТипЭлемента = Тип("ДокументОбъект.ПоступлениеДопРасходов") 
			ИЛИ ТипЭлемента = Тип("ДокументСсылка.ПоступлениеДопРасходов") Тогда
			
			
			СтруктураДопПараметров.Вставить("Товары");
						
			ЗаполнитьСтруктуруПараметровПоЭлементу(Элемент, "Документы", ТипЭлемента, СтруктураПараметров, СтруктураДопПараметров);

			Если (СтруктураДопПараметров.Товары = 0)  Тогда
				
				ВыгружатьДляВсехУзлов = Ложь;
				Возврат;
				
			КонецЕсли;
			
		ИначеЕсли ТипЭлемента = Тип("ДокументОбъект.ПоступлениеТоваровУслуг") 
			ИЛИ ТипЭлемента = Тип("ДокументСсылка.ПоступлениеТоваровУслуг") Тогда
			
			
			СтруктураПараметров.Вставить("ВидОперации");
			
			СтруктураДопПараметров.Вставить("ВозвратнаяТара");
			СтруктураДопПараметров.Вставить("Товары");
			СтруктураДопПараметров.Вставить("Услуги");
			
			ЗаполнитьСтруктуруПараметровПоЭлементу(Элемент, "Документы", ТипЭлемента, СтруктураПараметров, СтруктураДопПараметров);

			Если СтруктураПараметров.ВидОперации = Перечисления.ВидыОперацийПоступлениеТоваровУслуг.ВПереработку
				ИЛИ СтруктураПараметров.ВидОперации = Перечисления.ВидыОперацийПоступлениеТоваровУслуг.Оборудование
				ИЛИ СтруктураПараметров.ВидОперации = Перечисления.ВидыОперацийПоступлениеТоваровУслуг.ОбъектыСтроительства
				ИЛИ (СтруктураДопПараметров.ВозвратнаяТара + СтруктураДопПараметров.Товары + СтруктураДопПараметров.Услуги = 0)  Тогда
				
				ВыгружатьДляВсехУзлов = Ложь;
				Возврат;
				
			КонецЕсли;
			
		ИначеЕсли ТипЭлемента = Тип("ДокументОбъект.СчетНаОплатуПоставщика") 
			ИЛИ ТипЭлемента = Тип("ДокументСсылка.СчетНаОплатуПоставщика") Тогда
			
			
			СтруктураДопПараметров.Вставить("ВозвратнаяТара");
			СтруктураДопПараметров.Вставить("Товары");
			СтруктураДопПараметров.Вставить("Услуги");
			
			ЗаполнитьСтруктуруПараметровПоЭлементу(Элемент, "Документы", ТипЭлемента, СтруктураПараметров, СтруктураДопПараметров);

			Если (СтруктураДопПараметров.ВозвратнаяТара + СтруктураДопПараметров.Товары + СтруктураДопПараметров.Услуги = 0)  Тогда
				
				ВыгружатьДляВсехУзлов = Ложь;
				Возврат;
				
			КонецЕсли;
			
		ИначеЕсли ТипЭлемента = Тип("ДокументОбъект.ОприходованиеТоваров") 
			ИЛИ ТипЭлемента = Тип("ДокументСсылка.ОприходованиеТоваров") Тогда
			
			СтруктураПараметров.Вставить("ВидОперации");
			
			ЗаполнитьСтруктуруПараметровПоЭлементу(Элемент, "Документы", ТипЭлемента, СтруктураПараметров);

			Если СтруктураПараметров.ВидОперации = Перечисления.ВидыОперацийОприходованиеТоваров.ВводНачальныхОстатков Тогда
				
				ВыгружатьДляВсехУзлов = Ложь;
				Возврат;
				
			КонецЕсли;
			
		Иначе
			
			ЗаполнитьСтруктуруПараметровПоЭлементу(Элемент, "Документы", ТипЭлемента, СтруктураПараметров);	
			
		КонецЕсли;
		
		Если ЕстьОрганизация Тогда
			
		 	ДополнитьМассивУзламиПоОрганизации(МассивУзловДляПередачи, СтруктураПараметров.Организация, СоответствиеУзловИОрганизаций);
			
		Иначе
			
			ОбеспечитьВыгрузкуОбъектаДляВсехУзлов(ВыгружатьДляВсехУзлов, ВозвращатьМассивВсехУзлов, МассивУзловДляПередачи);
			
		КонецЕсли;
		
	КонецЕсли;
	
	Если НужноАнализироватьОграниченияПоДатам Тогда
		
		// ограничение по датам смотрим
		ТаблицаДатИУзлов = ПолучитьТаблицуУзловИДат();
		
		Номер = 0;
		Пока Номер <= МассивУзловДляПередачи.Количество() - 1 Цикл
			
			СтрокаТаблицы = ТаблицаДатИУзлов.Найти(МассивУзловДляПередачи[Номер], "ссылка");
			Если СтрокаТаблицы <> Неопределено
				И ЗначениеЗаполнено(СтруктураПараметров.Дата)
				И СтруктураПараметров.Дата < СтрокаТаблицы.ДатаНачалаВыгрузкиДокументов Тогда
				
				МассивУзловДляПередачи.Удалить(Номер);	
				
			Иначе
				
				Номер = Номер + 1;
				
			КонецЕсли;			
			
		КонецЦикла;
		
	КонецЕсли;
			
КонецПроцедуры

Процедура ДополнитьМассивСсылками(ТекущийМассив, МассивДляДобавления)
	
	Если МассивДляДобавления = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого Элемент Из МассивДляДобавления Цикл
		
		Если ТекущийМассив.Найти(Элемент) = Неопределено Тогда
		
			ТекущийМассив.Добавить(Элемент);
			
		КонецЕсли;
		
	КонецЦикла;	
	
КонецПроцедуры

Процедура ДополнитьМассивУзламиПоСправочникуИПараметру(ИмяСправочника, ИмяРеквизита, 
	СсылкаНаСправочник, МассивУзловДляПередачи, ИмяРеквизитаОрганизации = "Организация", СоответствиеУзловИОрганизаций = Неопределено)
	
	Если СсылкаНаСправочник.Пустая() Тогда
		Возврат;
	КонецЕсли;
	
	РезультатЗапроса = ПолныеПрава.ПолучитьРезультатЗапросаПоВыборкеОрганизаций(ИмяСправочника, ИмяРеквизита, ИмяРеквизитаОрганизации, СсылкаНаСправочник);
	
	Если РезультатЗапроса.Пустой() Тогда
		
		ДополнитьМассивУзламиПоОрганизации(МассивУзловДляПередачи, Справочники.Организации.ПустаяСсылка(), СоответствиеУзловИОрганизаций);	
		
	Иначе
	
		МассивОрганизаций = РезультатЗапроса.Выгрузить().ВыгрузитьКолонку("Организация");
		
		ДополнитьМассивУзламиПоОрганизации(МассивУзловДляПередачи, МассивОрганизаций, СоответствиеУзловИОрганизаций);
	
	Конецесли;
	
КонецПроцедуры

Функция ПолучитьПоНаборуЗаписейРазличныеЗначенияРеквизита(НаборЗаписей, ИмяРеквизита)
	
	Если НаборЗаписей.Количество() = 0 Тогда
		Возврат Новый Массив();
	КонецЕсли;
	
	ТаблицаДанных = НаборЗаписей.Выгрузить(, ИмяРеквизита);
	ТаблицаДанных.Свернуть(ИмяРеквизита);
	
	МассивОрганизаций = ТаблицаДанных.ВыгрузитьКолонку(ИмяРеквизита); 
	
	Возврат МассивОрганизаций;	
	
КонецФункции

Процедура ДополнитьМассивВсемиУзлами(МассивУзловДляПередачи)
	
	МассивУзлов = ВернутьМассивВсехУзловИзПараметровСеанса();
	
	ДополнитьМассивСсылками(МассивУзловДляПередачи, МассивУзлов);	
	 
КонецПроцедуры

Процедура ДополнитьМассивУзламиПоОрганизации(МассивУзловДляПередачи, Организация, СоответствиеУзловИОрганизаций = Неопределено)
	
	МассивУзлов = ПолучитьМассивУзловПоОрганизации(Организация, СоответствиеУзловИОрганизаций);
	ДополнитьМассивСсылками(МассивУзловДляПередачи, МассивУзлов);
	
КонецПроцедуры

Процедура ДополнитьМассивУзловДляВыгрузкиСправочника(Элемент, МассивУзловДляПередачи, 
	ТипЭлемента = Неопределено, ВыгружатьДляВсехУзлов = Ложь, ВозвращатьМассивВсехУзлов = Истина,
	СоответствиеУзловИОрганизаций = Неопределено)
	
	Перем ЭтоГруппа, СсылкаНаЭлемент;
	
	// пердопределенные элементы выгружаем и загружаем послностью
	Если Элемент.Предопределенный Тогда
		
		ОбеспечитьВыгрузкуОбъектаДляВсехУзлов(ВыгружатьДляВсехУзлов, ВозвращатьМассивВсехУзлов, МассивУзловДляПередачи);
		Возврат;
		
	КонецЕсли;
	
	ВыгружатьДляВсехУзлов = Ложь;
	Если ТипЭлемента = Неопределено Тогда
	
		ТипЭлемента = ТипЗнч(Элемент);
	
	КонецЕсли;
	
	Если ТипЭлемента = Тип("СправочникОбъект.БанковскиеСчета")
		ИЛИ ТипЭлемента = Тип("СправочникСсылка.БанковскиеСчета") Тогда
		
		// нужно определить владельца
		// если владелец не организация, тогда регистрируем изменения для всех узлов, 
		// если организация, то только для тех узлов куда организация передается
		
		Владелец = ПолучитьЗначениеПараметраОбъектаИлиСсылки(Элемент, "Справочники", ТипЭлемента, "Владелец");
		
		Если ТипЗнч(Владелец) = Тип("СправочникСсылка.Организации") Тогда
			
			// только узлы с указанной организацией
			ДополнитьМассивУзламиПоОрганизации(МассивУзловДляПередачи, Владелец, СоответствиеУзловИОрганизаций);
			
		ИначеЕсли ТипЗнч(Владелец) = Тип("СправочникСсылка.Контрагенты") Тогда
			
			ДополнитьМассивУзламиПоСправочникуИПараметру("ДоговорыКонтрагентов", "Владелец", 
				Владелец, МассивУзловДляПередачи, "Организация", СоответствиеУзловИОрганизаций);	
			
		Иначе
			
			// все узлы
			ОбеспечитьВыгрузкуОбъектаДляВсехУзлов(ВыгружатьДляВсехУзлов, ВозвращатьМассивВсехУзлов, МассивУзловДляПередачи);
			
		КонецЕсли;
		
	ИначеЕсли ТипЭлемента = Тип("СправочникОбъект.Организации")
		ИЛИ ТипЭлемента = Тип("СправочникСсылка.Организации") Тогда		
		
		СсылкаНаЭлемент = ОпределитьПоЭлементуЭтоГруппаИСсылку(Элемент, "Справочники", ТипЭлемента, Ложь);
		
		ДополнитьМассивУзламиПоОрганизации(МассивУзловДляПередачи, СсылкаНаЭлемент, СоответствиеУзловИОрганизаций);
			
	ИначеЕсли ТипЭлемента = Тип("СправочникОбъект.Контрагенты")
		ИЛИ ТипЭлемента = Тип("СправочникСсылка.Контрагенты") Тогда		
		
		СсылкаНаЭлемент = ОпределитьПоЭлементуЭтоГруппаИСсылку(Элемент, "Справочники", ТипЭлемента, Истина, ЭтоГруппа);
		
		Если ЭтоГруппа Тогда
			
			// все узлы
			ОбеспечитьВыгрузкуОбъектаДляВсехУзлов(ВыгружатьДляВсехУзлов, ВозвращатьМассивВсехУзлов, МассивУзловДляПередачи);
			
		Иначе
		
			ДополнитьМассивУзламиПоСправочникуИПараметру("ДоговорыКонтрагентов", "Владелец", 
				СсылкаНаЭлемент, МассивУзловДляПередачи, "Организация", СоответствиеУзловИОрганизаций);
				
		КонецЕсли;		
		
	ИначеЕсли ТипЭлемента = Тип("СправочникОбъект.ДоговорыКонтрагентов")
		ИЛИ ТипЭлемента = Тип("СправочникСсылка.ДоговорыКонтрагентов") Тогда		
		
		СтруктураПараметров = Новый Структура("ЭтоГруппа, Организация", Ложь, Справочники.Организации.ПустаяСсылка());
		ЗаполнитьСтруктуруПараметровПоЭлементу(Элемент, "Справочники", ТипЭлемента, СтруктураПараметров);
		
		Если СтруктураПараметров.ЭтоГруппа Тогда
			
			// все узлы
			ОбеспечитьВыгрузкуОбъектаДляВсехУзлов(ВыгружатьДляВсехУзлов, ВозвращатьМассивВсехУзлов, МассивУзловДляПередачи);
			
		Иначе
		
			ДополнитьМассивУзламиПоОрганизации(МассивУзловДляПередачи, СтруктураПараметров.Организация, СоответствиеУзловИОрганизаций);	
			
		КонецЕсли;
		
	ИначеЕсли ТипЭлемента = Тип("СправочникОбъект.Кассы")
		ИЛИ ТипЭлемента = Тип("СправочникСсылка.Кассы") Тогда		
		
		СтруктураПараметров = Новый Структура("ЭтоГруппа, Владелец", Ложь, Справочники.Организации.ПустаяСсылка());
		ЗаполнитьСтруктуруПараметровПоЭлементу(Элемент, "Справочники", ТипЭлемента, СтруктураПараметров);
		
		Если СтруктураПараметров.ЭтоГруппа Тогда
			
			// все узлы
			ОбеспечитьВыгрузкуОбъектаДляВсехУзлов(ВыгружатьДляВсехУзлов, ВозвращатьМассивВсехУзлов, МассивУзловДляПередачи);
			
		Иначе
		
			ДополнитьМассивУзламиПоОрганизации(МассивУзловДляПередачи, СтруктураПараметров.Владелец, СоответствиеУзловИОрганизаций);	
			
		КонецЕсли;
		
	ИначеЕсли ТипЭлемента = Тип("СправочникОбъект.КассыККМ")
		ИЛИ ТипЭлемента = Тип("СправочникСсылка.КассыККМ") Тогда		
		
		СтруктураПараметров = Новый Структура("Владелец", Справочники.Организации.ПустаяСсылка());
		ЗаполнитьСтруктуруПараметровПоЭлементу(Элемент, "Справочники", ТипЭлемента, СтруктураПараметров);
		
		ДополнитьМассивУзламиПоОрганизации(МассивУзловДляПередачи, СтруктураПараметров.Владелец, СоответствиеУзловИОрганизаций);	
		
	ИначеЕсли ТипЭлемента = Тип("СправочникОбъект.ХранилищеДополнительнойИнформации")
		ИЛИ ТипЭлемента = Тип("СправочникСсылка.ХранилищеДополнительнойИнформации") Тогда		
		
		СтруктураПараметров = Новый Структура("Объект", Неопределено);
		ЗаполнитьСтруктуруПараметровПоЭлементу(Элемент, "Справочники", ТипЭлемента, СтруктураПараметров);
		
		ТипОбъекта = ТипЗнч(СтруктураПараметров.Объект);
		
		Если ТипОбъекта = Тип("СправочникСсылка.ДоговорыКонтрагентов") 
             ИЛИ ТипОбъекта = Тип("СправочникСсылка.Контрагенты") 
             ИЛИ ТипОбъекта = Тип("СправочникСсылка.Номенклатура") 
             ИЛИ ТипОбъекта = Тип("СправочникСсылка.Организации")
             ИЛИ ТипОбъекта = Тип("СправочникСсылка.Проекты")
             ИЛИ ТипОбъекта = Тип("СправочникСсылка.СерииНоменклатуры")
             ИЛИ ТипОбъекта = Тип("СправочникСсылка.Склады")
             ИЛИ ТипОбъекта = Тип("СправочникСсылка.ФизическиеЛица") Тогда
			 
			МассивУзловДляПередачи = ПроцедурыОбменаУТУПП.ОпределитьМассивУзловДляРегистрацииПроизвольногоТипа(СтруктураПараметров.Объект, 
				"Справочники", ТипОбъекта, , ВыгружатьДляВсехУзлов, ВозвращатьМассивВсехУзлов, СоответствиеУзловИОрганизаций);
			 
		ИначеЕсли ТипОбъекта = Тип("ДокументСсылка.АвансовыйОтчет") 
             ИЛИ ТипОбъекта = Тип("ДокументСсылка.ЗаказПокупателя")
             ИЛИ ТипОбъекта = Тип("ДокументСсылка.ЗаказПоставщику")
             ИЛИ ТипОбъекта = Тип("ДокументСсылка.ЗаявкаНаРасходованиеСредств") 
             ИЛИ ТипОбъекта = Тип("ДокументСсылка.ПланируемоеПоступлениеДенежныхСредств") 
             ИЛИ ТипОбъекта = Тип("ДокументСсылка.Событие") Тогда
			
			МассивУзловДляПередачи = ПроцедурыОбменаУТУПП.ОпределитьМассивУзловДляРегистрацииПроизвольногоТипа(СтруктураПараметров.Объект, 
				"Документы", ТипОбъекта, , ВыгружатьДляВсехУзлов, ВозвращатьМассивВсехУзлов, СоответствиеУзловИОрганизаций);
			
		КонецЕсли;
		
	ИначеЕсли ТипЭлемента = Тип("СправочникОбъект.ГруппыСобытий")
		ИЛИ ТипЭлемента = Тип("СправочникСсылка.ГруппыСобытий") Тогда		
		
		СтруктураПараметров = Новый Структура("ВидОбъекта", Неопределено);
		ЗаполнитьСтруктуруПараметровПоЭлементу(Элемент, "Справочники", ТипЭлемента, СтруктураПараметров);
		
		Если НЕ (СтруктураПараметров.ВидОбъекта = Перечисления.ВидыОбъектовСобытия.ЗаявкаКандидата
			ИЛИ СтруктураПараметров.ВидОбъекта = Перечисления.ВидыОбъектовСобытия.Кандидат) Тогда
			
			// все узлы
			ОбеспечитьВыгрузкуОбъектаДляВсехУзлов(ВыгружатьДляВсехУзлов, ВозвращатьМассивВсехУзлов, МассивУзловДляПередачи);	
			
		КонецЕсли;
		
	ИначеЕсли ТипЭлемента = Тип("СправочникОбъект.СтатьиЗатрат")
		ИЛИ ТипЭлемента = Тип("СправочникСсылка.СтатьиЗатрат") Тогда		
		
		СтруктураПараметров = Новый Структура("ВидЗатрат", Неопределено);
		ЗаполнитьСтруктуруПараметровПоЭлементу(Элемент, "Справочники", ТипЭлемента, СтруктураПараметров);
		
		Если НЕ (СтруктураПараметров.ВидЗатрат = Перечисления.ВидыЗатрат.ОплатаТруда
			ИЛИ СтруктураПараметров.ВидЗатрат = Перечисления.ВидыЗатрат.Амортизация) Тогда
			
			// все узлы
			ОбеспечитьВыгрузкуОбъектаДляВсехУзлов(ВыгружатьДляВсехУзлов, ВозвращатьМассивВсехУзлов, МассивУзловДляПередачи);	
			
		КонецЕсли;
		
	ИначеЕсли ТипЭлемента = Тип("СправочникОбъект.ТиповыеАнкеты")
		ИЛИ ТипЭлемента = Тип("СправочникСсылка.ТиповыеАнкеты") Тогда		
		
		СтруктураПараметров = Новый Структура("ВидСправочникаДляЗагрузки", Неопределено);
		ЗаполнитьСтруктуруПараметровПоЭлементу(Элемент, "Справочники", ТипЭлемента, СтруктураПараметров);
		
		Если НЕ (СтруктураПараметров.ВидСправочникаДляЗагрузки = Перечисления.ВидыОбъектовЗагружаемыхИзОпроса.ФизическиеЛица) Тогда
			
			// все узлы
			ОбеспечитьВыгрузкуОбъектаДляВсехУзлов(ВыгружатьДляВсехУзлов, ВозвращатьМассивВсехУзлов, МассивУзловДляПередачи);	
			
		КонецЕсли;
							
	Иначе
		
		ОбеспечитьВыгрузкуОбъектаДляВсехУзлов(ВыгружатьДляВсехУзлов, ВозвращатьМассивВсехУзлов, МассивУзловДляПередачи);
		
	КонецЕсли;	
	
КонецПроцедуры

Процедура ОбеспечитьВыгрузкуОбъектаДляВсехУзлов(ВыгружатьДляВсехУзлов, ВозвращатьМассивВсехУзлов, МассивУзловДляПередачи)
	
	ВыгружатьДляВсехУзлов = Истина;
			
	Если ВозвращатьМассивВсехУзлов Тогда
		ДополнитьМассивВсемиУзлами(МассивУзловДляПередачи);
	КонецЕсли;	
	
КонецПроцедуры

Функция ОпределениеУзловДляВыгрузкиСправочника(Элемент, ТипЭлемента = Неопределено, 
	ВыгружатьДляВсехУзлов = Ложь, ВозвращатьМассивВсехУзлов = Истина, 
	СоответствиеУзловИОрганизаций = Неопределено)
	
	МассивУзловДляПередачи = Новый Массив;
	
	ДополнитьМассивУзловДляВыгрузкиСправочника(Элемент, МассивУзловДляПередачи, 
		ТипЭлемента, ВыгружатьДляВсехУзлов, ВозвращатьМассивВсехУзлов, СоответствиеУзловИОрганизаций);
	
	Возврат МассивУзловДляПередачи;
	
КонецФункции

Функция ОпределениеУзловДляВыгрузкиДокумента(Элемент, ТипРеквизита = Неопределено, ВыгружатьДляВсехУзлов = Ложь, 
	ВозвращатьМассивВсехУзлов = Истина, 
	СоответствиеУзловИОрганизаций = Неопределено, НужноАнализироватьОграниченияПоДатам = Ложь)
	
	МассивУзловДляПередачи = Новый Массив;
	
	ДополнитьМассивУзловДляВыгрузкиДокумента(Элемент, МассивУзловДляПередачи, 
		ТипРеквизита, ВыгружатьДляВсехУзлов, ВозвращатьМассивВсехУзлов, СоответствиеУзловИОрганизаций, НужноАнализироватьОграниченияПоДатам);
	
	Возврат МассивУзловДляПередачи;
	
КонецФункции

Процедура ДополнитьМассивУзловДляВыгрузкиСсылочногоТипа(ИмяБазовогоТипа, Элемент, МассивУзловДляПередачи, 
	ТипЭлемента = Неопределено, ВыгружатьДляВсехУзлов = Ложь, ВозвращатьМассивВсехУзлов = Истина,
	СоответствиеУзловИОрганизаций = Неопределено)
	
	Если ИмяБазовогоТипа = "Справочники" Тогда
		
		ДополнитьМассивУзловДляВыгрузкиСправочника(Элемент, МассивУзловДляПередачи, 
			ТипЭлемента, ВыгружатьДляВсехУзлов, ВозвращатьМассивВсехУзлов,
			СоответствиеУзловИОрганизаций);
		
	ИначеЕсли ИмяБазовогоТипа = "Документы" Тогда
		
		ДополнитьМассивУзловДляВыгрузкиДокумента(Элемент, МассивУзловДляПередачи, 
			ТипЭлемента, ВыгружатьДляВсехУзлов, ВозвращатьМассивВсехУзлов,
			СоответствиеУзловИОрганизаций);
		
	КонецЕсли;
	
КонецПроцедуры


Функция ОпределитьМассивУзловДляРегистрацииПроизвольногоТипа(Данные, ИмяБазовогоТипа, ТипЭлемента = Неопределено, МетаданныеОбъекта = Неопределено,
	ВыгружатьДляВсехУзлов = Ложь, ВозвращатьМассивВсехУзлов = Истина,
	СоответствиеУзловИОрганизаций = Неопределено, НужноАнализироватьОграниченияПоДатам = Истина) Экспорт
	
	Если ИмяБазовогоТипа = "Справочники" Тогда
			
		МассивУзловДляРегистрацииСсылки = ОпределениеУзловДляВыгрузкиСправочника(Данные, ТипЭлемента, ВыгружатьДляВсехУзлов, 
			ВозвращатьМассивВсехУзлов, СоответствиеУзловИОрганизаций);
		
	ИначеЕсли ИмяБазовогоТипа = "Документы" Тогда
		
		МассивУзловДляРегистрацииСсылки = ОпределениеУзловДляВыгрузкиДокумента(Данные, ТипЭлемента, ВыгружатьДляВсехУзлов, 
			ВозвращатьМассивВсехУзлов, СоответствиеУзловИОрганизаций, НужноАнализироватьОграниченияПоДатам);
		
	ИначеЕсли ИмяБазовогоТипа = "РегистрыСведений" Тогда	
		
		МассивУзловДляРегистрацииСсылки = ОпределениеУзловДляВыгрузкиНабораЗаписейРегистраСведений(Данные, МетаданныеОбъекта, ВыгружатьДляВсехУзлов, 
			ВозвращатьМассивВсехУзлов, СоответствиеУзловИОрганизаций);
		
	КонецЕсли;		
			
	Возврат МассивУзловДляРегистрацииСсылки;
	
КонецФункции

Процедура ОпределитьПоНаборуиРеквизитуМассивУзловДляПередачи(НаборЗаписей, МассивУзловДляПередачи, 
	ИмяРеквизита, ТипЭлемента = Неопределено, СоответствиеУзловИОрганизаций = Неопределено, ИмяБазовогоТипа = "Справочники")
	
	МассивРазличныхЗначенийРеквизита = ПолучитьПоНаборуЗаписейРазличныеЗначенияРеквизита(НаборЗаписей, ИмяРеквизита);
	
	МассивУзловДляПередачи = Новый Массив();
	
	Для Каждого Элемент Из МассивРазличныхЗначенийРеквизита Цикл 
		
		// для каждого элемента массива нужно определить набор узлов куда он может передаваться
		ДополнитьМассивУзловДляВыгрузкиСсылочногоТипа(ИмяБазовогоТипа, Элемент, МассивУзловДляПередачи, ТипЭлемента, , , СоответствиеУзловИОрганизаций);
	
	КонецЦикла;
	
КонецПроцедуры

Функция ОпределениеУзловДляВыгрузкиНабораЗаписейРегистраСведений(Элемент, МетаданныеРегистра, 
	ВыгружатьДляВсехУзлов = Ложь, ВозвращатьМассивВсехУзлов = Истина, СоответствиеУзловИОрганизаций = Неопределено)
	
	ВыгружатьДляВсехУзлов = Ложь;
	МассивУзловДляПередачи = Новый Массив;
	
	ТипЭлемента = ТипЗнч(Элемент);
	
	МетаданныеОбъекта = Метаданные.НайтиПоТипу(ТипЭлемента);					
	НаличиеОрганизации = (МетаданныеОбъекта.Измерения.Найти("Организация") <> Неопределено);
	
	Если НаличиеОрганизации Тогда
		
		ОпределитьПоНаборуиРеквизитуМассивУзловДляПередачи(Элемент, МассивУзловДляПередачи, "Организация", 
			Тип("СправочникСсылка.Организации"), СоответствиеУзловИОрганизаций);	
		
	Иначе
		
		ОбеспечитьВыгрузкуОбъектаДляВсехУзлов(ВыгружатьДляВсехУзлов, ВозвращатьМассивВсехУзлов, МассивУзловДляПередачи);
				
	КонецЕсли;
		
	Возврат МассивУзловДляПередачи;
	
КонецФункции

Процедура ЗаполнитьПолучателейДляОбмена(Источник, МассивУзловДляРегистрацииПередЗаписью, МассивУзловДляРегистрацииПриЗаписи = Неопределено)
	
	ПроцедурыОбменаДанными.ДополнитьМассивПолучателейУзламиАвторегистрации(Источник);
	
	// для регистрации ссылки
	Для Каждого Элемент Из МассивУзловДляРегистрацииПередЗаписью Цикл
		
		Источник.ОбменДанными.Получатели.Добавить(Элемент);
		
	КонецЦикла;
	
	
	Если МассивУзловДляРегистрацииПриЗаписи <> Неопределено Тогда
		
		// для регистрации объекта
		Для Каждого Элемент Из МассивУзловДляРегистрацииПриЗаписи Цикл
			
			Источник.ОбменДанными.Получатели.Добавить(Элемент);
			
		КонецЦикла;	
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередЗаписьюСсылочногоТипаДляОбменаПоОрганизации(Источник, Отказ, ИмяБазовогоТипа, 
	МассивУзловДляРегистрацииСсылки = Неопределено, МассивУзловДляРегистрацииОбъекта = Неопределено)
	
	Если Отказ
		ИЛИ НЕ ПараметрыСеанса.НаличиеОбменаУТУПП Тогда
		
		Возврат;
		
	КонецЕсли;		
	
	Если Источник.ЭтоНовый() Тогда
		
		// элемент еще никуда не передавался
		МассивУзловДляРегистрацииСсылки = Новый Массив;
		
	Иначе
		
		МассивУзловДляРегистрацииСсылки = ОпределитьМассивУзловДляРегистрацииПроизвольногоТипа(Источник.Ссылка, ИмяБазовогоТипа);
						
	КонецЕсли;
		
	МассивУзловДляРегистрацииОбъекта = ОпределитьМассивУзловДляРегистрацииПроизвольногоТипа(Источник, ИмяБазовогоТипа);
			
	ЗаполнитьПолучателейДляОбмена(Источник, МассивУзловДляРегистрацииСсылки, МассивУзловДляРегистрацииОбъекта);	
			
КонецПроцедуры

Процедура ПередЗаписьюНабораЗаписейДляОбменаПоОрганизации(Источник, Отказ, Замещение, ИмяБазовогоТипа, ТипЗначенияИсточника = Неопределено)
	
	Если Отказ
		ИЛИ НЕ ПараметрыСеанса.НаличиеОбменаУТУПП Тогда
		
		Возврат;
		
	КонецЕсли;
	
	Если ТипЗначенияИсточника = Неопределено Тогда
		ТипЗначенияИсточника = ТипЗнч(Источник);
	КонецЕсли;
	
	МетаданныеРегистра = Метаданные.НайтиПоТипу(ТипЗначенияИсточника);
	
	Если Замещение Тогда
		
		Если ИмяБазовогоТипа = "РегистрыСведений" Тогда
			СтарыйНаборЗаписей = РегистрыСведений[МетаданныеРегистра.Имя].СоздатьНаборЗаписей();
		КонецЕсли;
	
		Для Каждого ЗначениеОтбора Из Источник.Отбор Цикл
			
			Если ЗначениеОтбора.Использование = Ложь Тогда
				Продолжить;
			КонецЕсли;
			
			СтрокаОтбора = СтарыйНаборЗаписей.Отбор.Найти(ЗначениеОтбора.Имя);
			СтрокаОтбора.Значение = ЗначениеОтбора.Значение;
			СтрокаОтбора.Использование = Истина;
			
		КонецЦикла;
		
		СтарыйНаборЗаписей.Прочитать();
		
		МассивУзловДляРегистрацииСсылки = ОпределитьМассивУзловДляРегистрацииПроизвольногоТипа(СтарыйНаборЗаписей, ИмяБазовогоТипа,  , МетаданныеРегистра);		
				
	Иначе
		
		МассивУзловДляРегистрацииСсылки = Новый Массив;
			
	КонецЕсли;
			
	МассивУзловДляРегистрацииОбъекта = ОпределитьМассивУзловДляРегистрацииПроизвольногоТипа(Источник, ИмяБазовогоТипа,  , МетаданныеРегистра);
	
	ЗаполнитьПолучателейДляОбмена(Источник, МассивУзловДляРегистрацииСсылки, МассивУзловДляРегистрацииОбъекта);
	
КонецПроцедуры

Процедура ПередЗаписьСправочникаДляОбменаУТУПП(Источник, Отказ) Экспорт
	
	Если Отказ
		ИЛИ НЕ ПараметрыСеанса.НаличиеОбменаУТУПП Тогда
		
		Возврат;
		
	КонецЕсли;
	
	ПередЗаписьюСсылочногоТипаДляОбменаПоОрганизации(Источник, Отказ, "Справочники");	
		
КонецПроцедуры

Процедура ПередЗаписьюДокументаДляОбменаУТУПП(Источник, Отказ, РежимЗаписи, РежимПроведения) Экспорт
	
	Если Отказ
		ИЛИ НЕ ПараметрыСеанса.НаличиеОбменаУТУПП Тогда
		
		Возврат;
		
	КонецЕсли;
	
	ПередЗаписьюСсылочногоТипаДляОбменаПоОрганизации(Источник, Отказ, "Документы");	
	
КонецПроцедуры

Процедура ПередЗаписьюРегистраСведенийОбменУПП(Источник, Отказ, Замещение) Экспорт
	
	Если Отказ
		ИЛИ НЕ ПараметрыСеанса.НаличиеОбменаУТУПП Тогда
		
		Возврат;
		
	КонецЕсли;
	
	ТипИсточника = ТипЗнч(Источник);
	
	Если ТипИсточника = Тип("РегистрСведенийНаборЗаписей.ОбъектыДоступаДокументов") Тогда
		
		ДокументСсылка = Источник.Отбор.ДокументСсылка.Значение;
		
		Если ЗначениеЗаполнено(ДокументСсылка) Тогда 
		
			МассивУзловДляРегистрацииСсылки = ОпределитьМассивУзловДляРегистрацииПроизвольногоТипа(ДокументСсылка, "Документы");		
			
			ЗаполнитьПолучателейДляОбмена(Источник, МассивУзловДляРегистрацииСсылки);
			
		КонецЕсли;
		
	ИначеЕсли ТипИсточника = Тип("РегистрСведенийНаборЗаписей.ОтветственныеЛицаОрганизаций") Тогда
		
		СтруктурнаяЕдиница = Источник.Отбор.СтруктурнаяЕдиница.Значение;
		ОтветственноеЛицо = Источник.Отбор.ОтветственноеЛицо.Значение; 
		
		ВыгружатьДляВсехУзлов = Ложь;
		МассивУзловДляПередачи = Новый Массив();
		
		Если ЗначениеЗаполнено(СтруктурнаяЕдиница)
			И (ТипЗнч(СтруктурнаяЕдиница) = Тип("СправочникСсылка.ПодразделенияОрганизаций")) Тогда
			
			Возврат;
			
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ОтветственноеЛицо)
			И (ОтветственноеЛицо = Перечисления.ОтветственныеЛицаОрганизаций.ИндивидуальныйПредприниматель
			ИЛИ ОтветственноеЛицо = Перечисления.ОтветственныеЛицаОрганизаций.Исполнитель
			ИЛИ ОтветственноеЛицо = Перечисления.ОтветственныеЛицаОрганизаций.ОтветственныйЗаНалоговыеРегистры
			ИЛИ ОтветственноеЛицо = Перечисления.ОтветственныеЛицаОрганизаций.РуководительКадровойСлужбы
			ИЛИ ОтветственноеЛицо = Перечисления.ОтветственныеЛицаОрганизаций.УполномоченныйПредставитель) Тогда
			
			Возврат;
			
		КонецЕсли;
			
		ОбеспечитьВыгрузкуОбъектаДляВсехУзлов(ВыгружатьДляВсехУзлов, Истина, МассивУзловДляПередачи);
		ЗаполнитьПолучателейДляОбмена(Источник, МассивУзловДляПередачи);
		
	Иначе
	
		ПередЗаписьюНабораЗаписейДляОбменаПоОрганизации(Источник, Отказ, Замещение, "РегистрыСведений", ТипИсточника);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередУдалениемСправочникаДляОбменаУТУПП(Источник, Отказ) Экспорт
	
	Если Отказ
		ИЛИ НЕ ПараметрыСеанса.НаличиеОбменаУТУПП Тогда
		
		Возврат;
		
	КонецЕсли;
	
	ПередЗаписьюСсылочногоТипаДляОбменаПоОрганизации(Источник, Отказ, "Справочники");	
	
КонецПроцедуры

Процедура ПередУдалениемДокументаДляОбменаУТУПП(Источник, Отказ) Экспорт
	
	Если Отказ
		ИЛИ НЕ ПараметрыСеанса.НаличиеОбменаУТУПП Тогда
		
		Возврат;
		
	КонецЕсли;
	
	ПередЗаписьюСсылочногоТипаДляОбменаПоОрганизации(Источник, Отказ, "Документы");	
	
КонецПроцедуры