// Запрашивает у пользователя диалог с подтверждением легальности полученного
// обновления и завершает работу системы если обновление получено не легально
// (см. параметр ЗавершатьРаботуСистемы).
//
// Параметры
//  ЗавершатьРаботуСистемы - Булево - завершать работу системы, если пользователь
//					указал что обновление получено не легально
//
// Возвращаемое значение:
//   Булево   - Истина, если проверка завершилась успешно (пользователь
//				подтвердил, что обновление получено легально)
//
Функция ПроверитьЛегальностьПолученияОбновления(знач ЗавершатьРаботуСистемы = Ложь) Экспорт
	
	Если СтандартныеПодсистемыКлиентПовтИсп.ПараметрыРаботыКлиента().ЭтоБазоваяВерсияКонфигурации Тогда
		Возврат Истина;
	КонецЕсли;
	
	Результат = ОткрытьФормуМодально("Обработка.ЛегальностьПолученияОбновлений.Форма",
							Новый Структура("ПоказыватьПредупреждениеОПерезапуске,ПринудительныйЗапуск", 
											?(ЗавершатьРаботуСистемы, Истина, Ложь),
											Истина) );
	
	Если Результат <> Истина Тогда
		Если ЗавершатьРаботуСистемы Тогда
			ПропуститьПредупреждениеПередЗавершениемРаботыСистемы = Истина;
			ЗавершитьРаботуСистемы(Ложь);
		КонецЕсли;
		Возврат Ложь;
	КонецЕсли;
		
	Возврат Истина;
	
КонецФункции

// Процедура для проверки легальности получения обновления.
// Должна вызываться перед обновлением информационной базы.
//
Функция ПодтвердитьЛегальностьПолученияОбновления() Экспорт
	
	ПараметрыРаботыКлиента = СтандартныеПодсистемыКлиентПовтИсп.ПараметрыРаботыКлиента();
	
	Если ПараметрыРаботыКлиента.ПервыйЗапуск
	 ИЛИ НЕ ПараметрыРаботыКлиента.НеобходимоОбновлениеИнформационнойБазы
	 ИЛИ НЕ ПараметрыРаботыКлиента.ЭтоГлавныйУзел Тогда
		Возврат Истина;
	КонецЕсли;
	
	Возврат ПроверитьЛегальностьПолученияОбновления(Истина);
	
КонецФункции