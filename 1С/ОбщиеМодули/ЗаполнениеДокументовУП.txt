// Функция определяет возможность отразить документ в бух. учете
Функция МожноОтражатьВБухгалтерскомУчете(Организация, Дата, СообщениеОбОшибке = "")
	
	Если НЕ ЗначениеЗаполнено(Организация) Тогда
		СообщениеОбОшибке = Нстр("ru = 'Не указана организация'");
		Возврат Ложь;
	КонецЕсли;
	
	Если НЕ Организация.ОтражатьВРегламентированномУчете Тогда
		СообщениеОбОшибке = Нстр("ru = 'Не ведется регламентированный учет по организации ""%Организация""'");
		СообщениеОбОшибке = СтрЗаменить(СообщениеОбОшибке, "%Организация", "" + Организация);
		Возврат Ложь;
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

// Контроль и заполнение признаков отражения в учете для управляемого режима
Процедура ПередЗаписьюУправлениеОтражениемВУчете(Источник, Отказ, РежимЗаписи, РежимПроведения) Экспорт
	Если ТекущийРежимЗапуска() = РежимЗапускаКлиентскогоПриложения.УправляемоеПриложение Тогда
		
		СообщениеОбОшибкеБУ = "";
		СообщениеОбОшибкеНУ = "";
		
		ОтражатьВБухгалтерскомУчете = МожноОтражатьВБухгалтерскомУчете(Источник.Организация, Источник.Дата, СообщениеОбОшибкеБУ);
		ОтражатьВНалоговомУчете     = ОтражатьВБухгалтерскомУчете;
		
		Если РольДоступна("ОтражениеВРегламентированномУчете")
		  ИЛИ РольДоступна("ПолныеПрава") Тогда
	    	//Для ответственных за отражение в учете сообщим об ошибках
			Если Источник.ОтражатьВБухгалтерскомУчете И НЕ ОтражатьВБухгалтерскомУчете Тогда
				ОбщегоНазначения.СообщитьОбОшибке(Нстр("ru = 'Не удалось отразить документ в бухгалтерском учете.'") + Символы.ПС + СообщениеОбОшибкеБУ,Отказ);
			Иначе
				Источник.ОтражатьВНалоговомУчете = Источник.ОтражатьВБухгалтерскомУчете И ОтражатьВНалоговомУчете;
			КонецЕсли;	
		Иначе	
			//Для обычных ползьователей, установим флаги в соответствии с выбранной организацией
			Источник.ОтражатьВУправленческомУчете = Истина;
			Источник.ОтражатьВБухгалтерскомУчете = ОтражатьВБухгалтерскомУчете;
			Источник.ОтражатьВНалоговомУчете = ОтражатьВНалоговомУчете;
		КонецЕсли;	
	КонецЕсли;	
КонецПроцедуры