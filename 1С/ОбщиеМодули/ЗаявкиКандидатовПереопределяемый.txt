////////////////////////////////////////////////////////////////////////////////
// Вспомогательные процедуры, функции

////////////////////////////////////////////////////////////////////////////////
// Процедуры, функции объекта

Процедура ДополнитьИнициализациюРеквизитов(ЭтотОбъект) Экспорт
	
	РежимНабораПерсонала = ПроцедурыУправленияПерсоналомДополнительный.ПолучитьРежимНабораПерсонала();
	
	Если РежимНабораПерсонала = Перечисления.ВидыОрганизационнойСтруктурыПредприятия.ПоЦентрамОтветственности Тогда
		Если ЭтотОбъект.Подразделение = Неопределено ИЛИ ЭтотОбъект.Подразделение.Пустая() Тогда
			ЭтотОбъект.Подразделение = УправлениеПользователями.ПолучитьЗначениеПоУмолчанию(глЗначениеПеременной("глТекущийПользователь"), "ОсновноеПодразделение");
		КонецЕсли;
	Иначе
		Если ЭтотОбъект.Подразделение = Неопределено ИЛИ ЭтотОбъект.Подразделение.Пустая() Тогда
			ЭтотОбъект.Подразделение	= УправлениеПользователями.ПолучитьЗначениеПоУмолчанию(глЗначениеПеременной("глТекущийПользователь"), "ОсновноеПодразделениеОрганизации");
			ЭтотОбъект.Организация		= ЭтотОбъект.Подразделение.Владелец;
		КонецЕсли;
		Если ЭтотОбъект.Организация.Пустая() Тогда
			Если ЭтотОбъект.Подразделение.Пустая() Тогда
				ЭтотОбъект.Организация		= УправлениеПользователями.ПолучитьЗначениеПоУмолчанию(глЗначениеПеременной("глТекущийПользователь"), "ОсновнаяОрганизация");
			Иначе
				ЭтотОбъект.Организация		= ЭтотОбъект.Подразделение.Владелец;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Процедуры, функции для работы формы справочника

#Если ТолстыйКлиентОбычноеПриложение Тогда

Процедура ПолучитьПустоеПодразделение(Подразделение) Экспорт
	
	РежимНабораПерсонала = ПроцедурыУправленияПерсоналомДополнительный.ПолучитьРежимНабораПерсонала();
	
	Если РежимНабораПерсонала = Перечисления.ВидыОрганизационнойСтруктурыПредприятия.ПоСтруктуреЮридическихЛиц Тогда
		Если ТипЗнч(Подразделение) <> Тип("СправочникСсылка.ПодразделенияОрганизаций") Тогда
			Подразделение = Справочники.ПодразделенияОрганизаций.ПустаяСсылка();
		КонецЕсли;
	Иначе
		Если ТипЗнч(Подразделение) <> Тип("СправочникСсылка.Подразделения") Тогда
			Подразделение = Справочники.Подразделения.ПустаяСсылка();
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

Процедура УстановитьВидимостьЭлементовУправленияФормыСписка(Форма) Экспорт
	
	РежимНабораПерсонала = ПроцедурыУправленияПерсоналомДополнительный.ПолучитьРежимНабораПерсонала();
	
	ПоказыватьОрганизацию = РежимНабораПерсонала = Перечисления.ВидыОрганизационнойСтруктурыПредприятия.ПоСтруктуреЮридическихЛиц;
	
	Форма.ЭлементыФормы.СправочникСписок.Колонки.Организация.Видимость			= ПоказыватьОрганизацию;
	Форма.ЭлементыФормы.СправочникСписок.Колонки.Организация.ИзменятьНастройку	= ПоказыватьОрганизацию;
	Форма.ЭлементыФормы.ОрганизацияФлаг.Видимость								= ПоказыватьОрганизацию;
	Форма.ЭлементыФормы.Организация.Видимость									= ПоказыватьОрганизацию;
		
КонецПроцедуры

Процедура УстановитьВидимостьЭлементовУправленияФормыЭлемента(Форма) Экспорт	
	
	РежимНабораПерсонала = ПроцедурыУправленияПерсоналомДополнительный.ПолучитьРежимНабораПерсонала();

	Если РежимНабораПерсонала = Перечисления.ВидыОрганизационнойСтруктурыПредприятия.ПоЦентрамОтветственности Тогда
		Форма.ЭлементыФормы.ПанельОрганизация.Свертка	= РежимСверткиЭлементаУправления.Верх;
	КонецЕсли;
	
КонецПроцедуры

Процедура ВыполнитьОформлениеСтроки(Элемент, ОформленияСтрок) Экспорт 
	
	Если Элемент.Имя = "ПерепискаВстречиОпросы" Тогда
		
		Для Каждого ОформлениеСтроки Из ОформленияСтрок Цикл
			
			ОформлениеСтроки.Ячейки.ВидДокумента.ОтображатьКартинку = Истина;
			
			Если ТипЗнч(ОформлениеСтроки.ДанныеСтроки.Ссылка) = Тип("ДокументСсылка.ЭлектронноеПисьмо") Тогда
				ОформлениеСтроки.Ячейки.ВидДокумента.Картинка = БиблиотекаКартинок.ПерепискаСКандидатом;
			ИначеЕсли ТипЗнч(ОформлениеСтроки.ДанныеСтроки.Ссылка) = Тип("ДокументСсылка.Встречи") Тогда
				ОформлениеСтроки.Ячейки.ВидДокумента.Картинка = БиблиотекаКартинок.Контакт;
				ОформлениеСтроки.Ячейки.Тема.Текст = ОформлениеСтроки.ДанныеСтроки.Комментарий;
			ИначеЕсли ТипЗнч(ОформлениеСтроки.ДанныеСтроки.Ссылка) = Тип("ДокументСсылка.Опрос") Тогда
				ОформлениеСтроки.Ячейки.ВидДокумента.Картинка = БиблиотекаКартинок.СвойстваОбъектов;
				ОформлениеСтроки.Ячейки.ДатаКонтакта.Текст = ОформлениеСтроки.ДанныеСтроки.Дата;
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;

КонецПроцедуры

Функция ДоступенОтборПоОрганизации() Экспорт
	
	РежимНабораПерсонала = ПроцедурыУправленияПерсоналомДополнительный.ПолучитьРежимНабораПерсонала();
	
	Возврат РежимНабораПерсонала = Перечисления.ВидыОрганизационнойСтруктурыПредприятия.ПоСтруктуреЮридическихЛиц;
	
КонецФункции

#КонецЕсли

////////////////////////////////////////////////////////////////////////////////
// Процедуры, функции работы с QR кодом

Функция ЭтоQRКодКонтакта(ОписаниеСобытия) Экспорт
	
	// В данной конфигурации распознавание QR кодов не поддерживается
	Возврат Ложь;
	
КонецФункции

Функция ОбработатьСобытиеОтСканераШтрихкода(ОписаниеСобытия, ОписаниеОшибки = "") Экспорт
	
	// В данной конфигурации распознавание QR кодов не поддерживается
	Возврат Истина;
	
КонецФункции