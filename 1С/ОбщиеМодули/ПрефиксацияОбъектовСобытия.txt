// Процедура предназначена для очистки введенного кода/номера объекта при изменении организации
// Номер очищается, если 
//	- организация не выбрана
//	- префикс новой организации пустой
//	- префикс новой организации отличается от текущего префикса
//
// Параметры
//	Номер	    - Номер объекта, который требуется очистить
//  Организация	- ОрганизацияСсылка
//
Процедура ОчиститьНомерОбъекта(Номер, Организация) Экспорт
	
	Если ПустаяСтрока(Номер) Тогда
		Возврат;
	КонецЕсли; 
	
	Если Организация.Пустая() Тогда
		Номер = "";
	Иначе
		
		НовыйПрефиксОрганизации = Организация.Префикс;
		
		Если ПустаяСтрока(НовыйПрефиксОрганизации) Тогда
			Номер = "";
		Иначе
			
			// Считаем, что длина префикса для всех организаций одинаковая
			ДлинаПрефикса = СтрДлина(НовыйПрефиксОрганизации);		
			
			Если ПараметрыСеанса.ИспользованиеРИБ Тогда
				// Если используется РИБ, то сначала следует префикс узла, затем префикс организации
				НачалоПрефикса = СтрДлина(ПараметрыСеанса.ПрефиксУзлаРаспределеннойИнформационнойБазы) + 1;
			Иначе
				НачалоПрефикса = 1;
			КонецЕсли;
			
			ТекущийПрефиксОрганизации = Сред(Номер, НачалоПрефикса, ДлинаПрефикса);
			
			Если ТекущийПрефиксОрганизации <> НовыйПрефиксОрганизации Тогда
				Номер = "";
			КонецЕсли;
			
		КонецЕсли;
			
	КонецЕсли;
	
КонецПроцедуры