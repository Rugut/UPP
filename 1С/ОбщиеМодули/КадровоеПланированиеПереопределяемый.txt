////////////////////////////////////////////////////////////////////////////////
// Вспомогательные процедуры, функции

////////////////////////////////////////////////////////////////////////////////
// Процедуры, функции объекта

Функция ОбновитьРабочиеМестаРегл(Организация, ПодразделениеОрганизации, Период = Неопределено) Экспорт
	
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("Организация",				Организация);
	Запрос.УстановитьПараметр("ПодразделениеОрганизации",	ПодразделениеОрганизации);
	
	Если Период <> Неопределено Тогда
		Запрос.УстановитьПараметр("ДатаАктуальности",	КонецДня(Период));
		
		Запрос.Текст =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	КадровоеПланирование.Подразделение КАК Подразделение,
		|	ВЫБОР
		|		КОГДА КадровоеПланирование.Подразделение = &ПодразделениеОрганизации
		|			ТОГДА ЛОЖЬ
		|		ИНАЧЕ ИСТИНА
		|	КОНЕЦ КАК ЕстьИерархия,
		|	КадровоеПланирование.Должность,
		|	КадровоеПланирование.Должность.Наименование КАК ДолжностьНаименование,
		|	КадровоеПланирование.КоличествоСтавок,
		|	КадровоеПланирование.ЗанятоСтавок,
		|	КадровоеПланирование.КоличествоСтавок - КадровоеПланирование.ЗанятоСтавок КАК Вакантно
		|ИЗ
		|	(ВЫБРАТЬ
		|		КадровоеПланирование.Подразделение КАК Подразделение,
		|		КадровоеПланирование.Должность КАК Должность,
		|		МАКСИМУМ(КадровоеПланирование.КоличествоСтавок) КАК КоличествоСтавок,
		|		МАКСИМУМ(КадровоеПланирование.ЗанятоСтавок) КАК ЗанятоСтавок
		|	ИЗ
		|		(ВЫБРАТЬ
		|			КадровыйПланСрезПоследних.ПодразделениеОрганизации КАК Подразделение,
		|			КадровыйПланСрезПоследних.Должность КАК Должность,
		|			СУММА(КадровыйПланСрезПоследних.Количество) КАК КоличествоСтавок,
		|			СУММА(0) КАК ЗанятоСтавок
		|		ИЗ
		|			РегистрСведений.КадровыйПлан.СрезПоследних(
		|					&ДатаАктуальности,
		|					Подразделение = ЗНАЧЕНИЕ(Справочник.Подразделения.ПустаяСсылка)
		|						И ПодразделениеОрганизации.Владелец = &Организация";
		Если ПодразделениеОрганизации <> Неопределено Тогда
		Запрос.Текст = Запрос.Текст + "
		|						И ПодразделениеОрганизации В ИЕРАРХИИ (&ПодразделениеОрганизации)";
		КонецЕсли;
		Запрос.Текст = Запрос.Текст + "
		|																						) КАК КадровыйПланСрезПоследних
		|		
		|		СГРУППИРОВАТЬ ПО
		|			КадровыйПланСрезПоследних.ПодразделениеОрганизации,
		|			КадровыйПланСрезПоследних.Должность
		|		
		|		ОБЪЕДИНИТЬ ВСЕ
		|		
		|		ВЫБРАТЬ
		|			ЗанятыеШтатныеЕдиницыОрганизацийОстатки.ПодразделениеОрганизации,
		|			ЗанятыеШтатныеЕдиницыОрганизацийОстатки.Должность.Должность,
		|			СУММА(0),
		|			СУММА(ЗанятыеШтатныеЕдиницыОрганизацийОстатки.КоличествоСтавокОстаток)
		|		ИЗ
		|			РегистрНакопления.ЗанятыеШтатныеЕдиницыОрганизаций.Остатки(
		|					&ДатаАктуальности,
		|					ПодразделениеОрганизации.Владелец = &Организация";
		Если ПодразделениеОрганизации <> Неопределено Тогда
		Запрос.Текст = Запрос.Текст + "
		|						И ПодразделениеОрганизации В ИЕРАРХИИ (&ПодразделениеОрганизации)";
		КонецЕсли;
		Запрос.Текст = Запрос.Текст + "
		|						И Должность.Должность <> ЗНАЧЕНИЕ(Справочник.Должности.ПустаяСсылка)) КАК ЗанятыеШтатныеЕдиницыОрганизацийОстатки
		|		
		|		СГРУППИРОВАТЬ ПО
		|			ЗанятыеШтатныеЕдиницыОрганизацийОстатки.ПодразделениеОрганизации,
		|			ЗанятыеШтатныеЕдиницыОрганизацийОстатки.Должность.Должность
		|		
		|		ОБЪЕДИНИТЬ ВСЕ
		|		
		|		ВЫБРАТЬ
		|			Вакансии.Подразделение,
		|			Вакансии.Должность,
		|			СУММА(1),
		|			СУММА(0)
		|		ИЗ
		|			Справочник.Вакансии КАК Вакансии
		|		ГДЕ
		|			(НЕ Вакансии.Закрыта)
		|			И Вакансии.Подразделение ССЫЛКА Справочник.ПодразделенияОрганизаций";
		Если ПодразделениеОрганизации <> Неопределено Тогда
		Запрос.Текст = Запрос.Текст + "
		|			И Вакансии.Подразделение В ИЕРАРХИИ(&ПодразделениеОрганизации)";
		КонецЕсли;
		Запрос.Текст = Запрос.Текст + "
		|			И Вакансии.Подразделение.Владелец = &Организация
		|		
		|		СГРУППИРОВАТЬ ПО
		|			Вакансии.Подразделение,
		|			Вакансии.Должность
		|		
		|		ОБЪЕДИНИТЬ ВСЕ
		|		
		|		ВЫБРАТЬ
		|			ШтатноеРасписаниеОрганизацийСрезПоследних.ПодразделениеОрганизации,
		|			ШтатноеРасписаниеОрганизацийСрезПоследних.Должность.Должность,
		|			СУММА(ШтатноеРасписаниеОрганизацийСрезПоследних.КоличествоСтавок),
		|			СУММА(0)
		|		ИЗ
		|			РегистрСведений.ШтатноеРасписаниеОрганизаций.СрезПоследних(
		|					&ДатаАктуальности,
		|					ПодразделениеОрганизации.Владелец = &Организация";
		Если ПодразделениеОрганизации <> Неопределено Тогда
		Запрос.Текст = Запрос.Текст + "
		|						И ПодразделениеОрганизации В ИЕРАРХИИ (&ПодразделениеОрганизации)";
		КонецЕсли;
		Запрос.Текст = Запрос.Текст + "
		|						И Должность.Должность <> ЗНАЧЕНИЕ(Справочник.Должности.ПустаяСсылка)
		|						И (НЕ (ПодразделениеОрганизации, Должность.Должность) В
		|							(ВЫБРАТЬ РАЗЛИЧНЫЕ
		|								КадровыйПлан.ПодразделениеОрганизации,
		|								КадровыйПлан.Должность
		|							ИЗ
		|								РегистрСведений.КадровыйПлан.СрезПоследних(&ДатаАктуальности, Подразделение = ЗНАЧЕНИЕ(Справочник.Подразделения.ПустаяСсылка)
		|									И ПодразделениеОрганизации.Владелец = &Организация";
		Если ПодразделениеОрганизации <> Неопределено Тогда
		Запрос.Текст = Запрос.Текст + "
		|									И ПодразделениеОрганизации В ИЕРАРХИИ (&ПодразделениеОрганизации)";
		КонецЕсли;
		Запрос.Текст = Запрос.Текст + "
		|									) КАК КадровыйПлан
		|							ГДЕ
		|								КадровыйПлан.Количество <> 0))) КАК ШтатноеРасписаниеОрганизацийСрезПоследних
		|		
		|		СГРУППИРОВАТЬ ПО
		|			ШтатноеРасписаниеОрганизацийСрезПоследних.ПодразделениеОрганизации,
		|			ШтатноеРасписаниеОрганизацийСрезПоследних.Должность.Должность) КАК КадровоеПланирование
		|	
		|	СГРУППИРОВАТЬ ПО
		|		КадровоеПланирование.Подразделение,
		|		КадровоеПланирование.Должность) КАК КадровоеПланирование
		|ГДЕ
		|	НЕ (КадровоеПланирование.КоличествоСтавок = 0
		|	И КадровоеПланирование.ЗанятоСтавок = 0)
		|
		|УПОРЯДОЧИТЬ ПО
		|	ДолжностьНаименование,
		|	Подразделение ИЕРАРХИЯ";
		
	Иначе
		Запрос.Текст =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	КадровыйПлан.Регистратор.Дата КАК Период,
		|	КадровыйПлан.ПодразделениеОрганизации КАК Подразделение,
		|	ВЫБОР
		|		КОГДА КадровыйПлан.ПодразделениеОрганизации = &ПодразделениеОрганизации
		|			ТОГДА ЛОЖЬ
		|		ИНАЧЕ ИСТИНА
		|	КОНЕЦ КАК ЕстьИерархия,
		|	КадровыйПлан.Должность КАК Должность,
		|	КадровыйПлан.Должность.Наименование КАК ДолжностьНаименование,
		|	КадровыйПлан.Количество КАК КоличествоСтавок
		|ИЗ
		|	РегистрСведений.КадровыйПлан КАК КадровыйПлан
		|ГДЕ
		|	КадровыйПлан.Подразделение = ЗНАЧЕНИЕ(Справочник.Подразделения.ПустаяСсылка)
		|	И КадровыйПлан.ПодразделениеОрганизации.Владелец = &Организация";
		Если ПодразделениеОрганизации <> Неопределено Тогда
		Запрос.Текст = Запрос.Текст + "
		|	И КадровыйПлан.ПодразделениеОрганизации В ИЕРАРХИИ (&ПодразделениеОрганизации)";
		КонецЕсли;
		Запрос.Текст = Запрос.Текст + "
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	Вакансии.ДатаОткрытия,
		|	Вакансии.Подразделение,
		|	ВЫБОР
		|		КОГДА Вакансии.Подразделение = &ПодразделениеОрганизации
		|			ТОГДА ЛОЖЬ
		|		ИНАЧЕ ИСТИНА
		|	КОНЕЦ,
		|	Вакансии.Должность,
		|	Вакансии.Должность.Наименование,
		|	1
		|ИЗ
		|	Справочник.Вакансии КАК Вакансии
		|ГДЕ
		|	Вакансии.Подразделение.Владелец = &Организация";
		Если ПодразделениеОрганизации <> Неопределено Тогда
		Запрос.Текст = Запрос.Текст + "
		|	И Вакансии.Подразделение В ИЕРАРХИИ (&ПодразделениеОрганизации)";
		КонецЕсли;
		Запрос.Текст = Запрос.Текст + "
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ШтатноеРасписаниеОрганизаций.Период,
		|	ШтатноеРасписаниеОрганизаций.ПодразделениеОрганизации,
		|	ВЫБОР
		|		КОГДА ШтатноеРасписаниеОрганизаций.ПодразделениеОрганизации = &ПодразделениеОрганизации
		|			ТОГДА ЛОЖЬ
		|		ИНАЧЕ ИСТИНА
		|	КОНЕЦ,
		|	ШтатноеРасписаниеОрганизаций.Должность.Должность,
		|	ШтатноеРасписаниеОрганизаций.Должность.Должность.Наименование,
		|	ШтатноеРасписаниеОрганизаций.КоличествоСтавок
		|ИЗ
		|	РегистрСведений.ШтатноеРасписаниеОрганизаций КАК ШтатноеРасписаниеОрганизаций
		|ГДЕ
		|	ШтатноеРасписаниеОрганизаций.ПодразделениеОрганизации.Владелец = &Организация
		|	И ШтатноеРасписаниеОрганизаций.Должность.Должность <> ЗНАЧЕНИЕ(Справочник.Должности.ПустаяСсылка)";
		Если ПодразделениеОрганизации <> Неопределено Тогда
		Запрос.Текст = Запрос.Текст + "
		|	И ШтатноеРасписаниеОрганизаций.ПодразделениеОрганизации В ИЕРАРХИИ (&ПодразделениеОрганизации)";
		КонецЕсли;
		Запрос.Текст = Запрос.Текст + "
		|
		|УПОРЯДОЧИТЬ ПО
		|	ДолжностьНаименование,
		|	Период";
		
	КонецЕсли;
	
	Возврат Запрос.Выполнить();
	
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// Процедуры, функции для работы формы

Функция ОтчетПоТекучести() Экспорт
	
	РежимНабораПерсонала = ПроцедурыУправленияПерсоналомДополнительный.ПолучитьРежимНабораПерсонала();
	
	Если РежимНабораПерсонала = Перечисления.ВидыОрганизационнойСтруктурыПредприятия.ПоСтруктуреЮридическихЛиц Тогда
		Возврат Отчеты.КоэффициентТекучестиКадровОрганизаций.Создать();
	Иначе
		Возврат Отчеты.КоэффициентТекучестиКадров.Создать();
	КонецЕсли;
	
КонецФункции

Процедура ЗаполнитьЗначенияДополнительно(Форма, ЭтаФорма) Экспорт
	
	РежимНабораПерсонала = ПроцедурыУправленияПерсоналомДополнительный.ПолучитьРежимНабораПерсонала();
	
	Если РежимНабораПерсонала = Перечисления.ВидыОрганизационнойСтруктурыПредприятия.ПоСтруктуреЮридическихЛиц Тогда
		Форма.Организация	= ЭтаФорма.Организация;
	КонецЕсли;
	
КонецПроцедуры

Процедура РабочиеМестаНоваяВакансияДополнительно(ДанныеСтроки, ЭтаФорма) Экспорт
	
	РежимНабораПерсонала = ПроцедурыУправленияПерсоналомДополнительный.ПолучитьРежимНабораПерсонала();
	
	ДанныеСтроки = ЭтаФорма.ЭлементыФормы.Подразделения.ТекущиеДанные;
	
КонецПроцедуры

#Если ТолстыйКлиентОбычноеПриложение Тогда

Функция ОбновитьДополнительно(ЭтотОбъект, ЭтаФорма) Экспорт
	
	РежимНабораПерсонала = ПроцедурыУправленияПерсоналомДополнительный.ПолучитьРежимНабораПерсонала();
	
	Если РежимНабораПерсонала = Перечисления.ВидыОрганизационнойСтруктурыПредприятия.ПоЦентрамОтветственности Тогда
		КадровоеПланированиеДополнительный.ОбновитьДанныеПодразделения(ЭтотОбъект, ЭтаФорма);
		Возврат Истина;
	Иначе
		Возврат Ложь;
	КонецЕсли;
	
КонецФункции

Процедура ОпределитьДоступ(ЭтаФорма) Экспорт

	ЭтаФорма.ЕстьДоступУпр	= ПравоДоступа("Чтение", Метаданные.РегистрыНакопления.ЗанятыеРабочиеМеста) И ПравоДоступа("Добавление", Метаданные.Документы.ИзменениеКадровогоПлана);
	ЭтаФорма.ЕстьДоступРегл	= ПравоДоступа("Чтение", Метаданные.РегистрыНакопления.ЗанятыеШтатныеЕдиницыОрганизаций) И ПравоДоступа("Добавление", Метаданные.Документы.ИзменениеКадровогоПлана);

КонецПроцедуры

Процедура ПередОткрытиемДополнительно(ЭтаФорма, ДополнительныеДействия) Экспорт
	
	КадровоеПланированиеДополнительный.ЗаполнитьДополнительноеМеню(ЭтаФорма, ДополнительныеДействия);
	
	КадровоеПланированиеДополнительный.ПриИзмененииРежимаНабораПерсонала(ЭтаФорма);	
	
	ЭтаФорма.Подразделения.Порядок.Установить("Порядок");
	
КонецПроцедуры

Процедура ДополнитьТекстМакетаВстроеннойСправки(ТекстМакетаВстроеннойСправки) Экспорт 
	
	РежимНабораПерсонала = ПроцедурыУправленияПерсоналомДополнительный.ПолучитьРежимНабораПерсонала();
    	
	РежимНабораПерсонала_ПоСтруктуреЮридическихЛиц	= ?(РежимНабораПерсонала = Перечисления.ВидыОрганизационнойСтруктурыПредприятия.ПоСтруктуреЮридическихЛиц, "inline", "none");

	ТекстМакетаВстроеннойСправки = ТекстМакетаВстроеннойСправки + "
	|		<DIV><P id=РежимНабораПерсонала_ПоСтруктуреЮридическихЛиц style=""DISPLAY:"+РежимНабораПерсонала_ПоСтруктуреЮридическихЛиц+""">Информация о количестве ставок берется из штатного расписания.</P></DIV>";
	
КонецПроцедуры

Функция ОтсутствиеДоступаКРежимуПланирования(ЭтаФорма) Экспорт
	
	РежимНабораПерсонала = ПроцедурыУправленияПерсоналомДополнительный.ПолучитьРежимНабораПерсонала();

	Если РежимНабораПерсонала = Перечисления.ВидыОрганизационнойСтруктурыПредприятия.ПоСтруктуреЮридическихЛиц Тогда
		Если Не ЭтаФорма.ЕстьДоступРегл Тогда
			Возврат Истина;
		КонецЕсли;
	Иначе
		Если Не ЭтаФорма.ЕстьДоступУпр Тогда
			Возврат Истина;
		КонецЕсли;
	КонецЕсли;

	Возврат Ложь;
	
КонецФункции

Процедура РабочиеМестаВыборДополнительно(ЭтаФорма, Элемент, ВыбраннаяСтрока, Колонка, СтандартнаяОбработка) Экспорт
	
	СтандартнаяОбработка = Ложь;
	
	ДанныеСтроки = ЭтаФорма.ЭлементыФормы.РабочиеМеста.ТекущиеДанные;
	Если ДанныеСтроки = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ФормаЗаписи = РегистрыСведений.КадровыйПлан.ПолучитьФорму("ФормаЗаписи");
	
	ФормаЗаписи.Должность		= ВыбраннаяСтрока.Должность;
	
	РежимНабораПерсонала = ПроцедурыУправленияПерсоналомДополнительный.ПолучитьРежимНабораПерсонала();

	Если РежимНабораПерсонала = Перечисления.ВидыОрганизационнойСтруктурыПредприятия.ПоЦентрамОтветственности Тогда
		ФормаЗаписи.Подразделение				= ДанныеСтроки.Подразделение;
	Иначе
		ФормаЗаписи.ПодразделениеОрганизации	= ДанныеСтроки.Подразделение;
	КонецЕсли;
	
	ФормаЗаписи.Открыть();
	
КонецПроцедуры

Функция ВыполняетсяДополнительноеУсловие(Команда, ЭтаФорма) Экспорт

	Если Команда = "SwitchOrganizationalStructure" Тогда
		
		РежимНабораПерсонала = ПроцедурыУправленияПерсоналомДополнительный.ПолучитьРежимНабораПерсонала();
		
		Если РежимНабораПерсонала = Перечисления.ВидыОрганизационнойСтруктурыПредприятия.ПоСтруктуреЮридическихЛиц Тогда
			РежимНабораПерсонала = Перечисления.ВидыОрганизационнойСтруктурыПредприятия.ПоЦентрамОтветственности;
		Иначе
			РежимНабораПерсонала = Перечисления.ВидыОрганизационнойСтруктурыПредприятия.ПоСтруктуреЮридическихЛиц;
		КонецЕсли;
		
		Константы.РежимНабораПерсонала.Установить(РежимНабораПерсонала);
		
		КадровоеПланированиеДополнительный.ПриИзмененииРежимаНабораПерсонала(ЭтаФорма);
		
		Возврат Истина;
		
	Иначе
		
		Возврат Ложь;

	КонецЕсли;
	
	
КонецФункции

Процедура ОбновитьСправкуФормы(ЭтаФорма, ПолеДанных = Неопределено) Экспорт
	
	РежимНабораПерсонала = ПроцедурыУправленияПерсоналомДополнительный.ПолучитьРежимНабораПерсонала();
	
	ДокHTML = ЭтаФорма.ЭлементыФормы.ВстроеннаяСправка.Документ;
	
	Если ПолеДанных = Неопределено Тогда
		РаботаСДиалогами.УстановитьВидимостьТекста(ДокHTML, "РежимНабораПерсонала_ПоСтруктуреЮридическихЛиц", РежимНабораПерсонала = Перечисления.ВидыОрганизационнойСтруктурыПредприятия.ПоСтруктуреЮридическихЛиц);
	КонецЕсли;
	
КонецПроцедуры // ПоказатьСправкуФормы()
 
Процедура ВыполнитьДополнительныеДействия(ЭтаФорма, Кнопка) Экспорт
	
	Если Не (ЭтаФорма.ЕстьДоступУпр И ЭтаФорма.ЕстьДоступРегл) Тогда
		Предупреждение("У вас нет прав для изменения режима кадрового планирования");
		Возврат;
	КонецЕсли;
	
	РежимНабораПерсонала = Перечисления.ВидыОрганизационнойСтруктурыПредприятия[Кнопка.Имя];
	
	Константы.РежимНабораПерсонала.Установить(РежимНабораПерсонала);
	
	КадровоеПланированиеДополнительный.ПриИзмененииРежимаНабораПерсонала(ЭтаФорма);

КонецПроцедуры

#КонецЕсли